./ ADD NAME=$$$DOC

  * NOTE FROM SAM GOLOB:  Members DITTPARM and DITTTSOM are fixes
  *  for 4-digit UCB support.  Substitute them into the appropriate
  *  places in the DITTOSRC member.

  MEMBER    DESCRIPTION
  $$$DOC    THIS MEMBER
  ASMJCL    SAMPLE JCL TO ASSEMBLE AND LINKEDIT DITTO
  DITTAUTH  DYNAMIC AUTH/DE-AUTH (TSO ENVIROMENT)
  DITTALOC  ACQ/REL COMMANDS
  DITTATTN  TSO ATTENTION EXIT
  DITTCARD  BATCH ENVIRONMENT COMMAND READER
  DITTCDI1  CARD READER
  DITTCDO1  CARD PUNCH
  DITTCMD   COMMANDS TABLE
  DITTCOMM  COMMON DATA MODULE
  DITTCONS  STC ENVIRONMENT COMMAND READER
  DITTDAIR  SYSTEM DAIR INTERFACE
  DITTDAI1  DASD READER
  DITTEXCP  EXCP I/O MODULE
  DITTINIT  INITIALIZATION/TERMINATION MODULE
  DITTINQ   BATCH/STC INQ FUNCTION
  DITTMAIN  BATCH/STC MAINLINE
  DITTPARM  PARAMETER CONVERTER
  DITTPRMD  DENSITY PARAMETER EDIT CHECKER
  DITTPRMO  'OPTION' PARAMETER EDIT CHECKER
  DITTPRMV  VOLSER PARAMETER EDIT CHECKER
  DITTPRO1  'PRINT' OUTPUT
  DITTPRT   PRINTER
  DITTSTAE  STAE EXIT
  DITTTINT  TAPE INITIALIZATION COMMAND
  DITTTMAP  TAPE MAP COMMAND
  DITTTPI1  TAPE READER
  DITTTPI2  FORWARD SPACE FILE COMMAND
  DITTTPI3  BACK-SPACE RECORDS COMMAND
  DITTTPI4  BACK-SPACE FILE COMMAND
  DITTTPO1  TAPE OUTPUT
  DITTTPO2  TAPE REWIND COMMAND
  DITTTPO3  TAPE REWIND-UNLOAD COMMAND
  DITTTPO4  WRITE TAPE MARK COMMAND
  DITTTSOM  TSO ENVIRONMENT MAINLINE
  DITTTSO1  TSO ENVIRONMENT DATA DISPLAY
  DITTTSO2  TSO ENVIRONMENT TAPE MAP
  DITTTSO3  TSO ENVIRONMENT INQ FUNCTION
  DITTXXXX  DUMMY END OF LOAD MODULE
  JCL       SAMPLE JCL TO EXECUTE DITTO IN BATCH
  PROC      SAMPLE JCL TO EXECUTE DITTO AS A STARTED TASK
  TSODITTO  SAMPLE CLIST TO EXECUTE DITTO FROM TSO
./ ADD NAME=$$$DOC2
                       Installing DITTO

1)  Allocate a SOURCE, MACLIB, ISPPLIB or ISPPENU, and if you want
    to keep DITTO in its own loadlib a LOADLIB that must be APF
    authorized.

    Space estimates:
                         3380 tracks   Directory blocks      DCB
         SOURCE              30               10          FB, 80, 3120
         MACLIB               5                5          FB, 80, 3120
         ISPPLIB or ISPPENU   2                2          FB, 80, 3120
         LOADLIB              1                1          U, 6144

   ** You do need separate SOURCE and MACLIBs.


2)  DITTO bypasses opens so it must run APF authorized.  In the TSO
    environment DITTO will dynamically authorize and de-authorize itself
    if you have an SVC for this purpose.  Add the code necessary to
    invoke your SVC in source member DITTAUTH.

3)  Edit the ASMJCL member of the SOURCE library:

         a)  Change the JOBCARD to your site's requirements
         b)  Change the MACLIB references to the name you gave to the
             DITTO maclib.
         c)  Change the SOURCE library references to the name you gave
             the DITTO source lib.
         d)  Change the LOADLIB references to the name you gave the
             DITTO loadlib or the name of an existing APF authorized
             library.

4)  Run the ASMJCL member to assemble and link DITTO (should produce
    about 75,000 lines of print).

5)  If you want to run DITTO as a started task, add a PROC to one of
    your JES proclibs (member "PROC" in the SOURCE library may be used
    as a sample).

6)  If you want to use DITTO from a TSO session:
       a) the panels must be available so either add the ISPPLIB or
          ISPPENU library to your logon proc or allocate it in a CLIST
          or REXX exec.
       b) The LOADLIB must be available so add it to your LINKLST or
          your logon proc or allocate it in a CLIST or REXX exec.
       c) If you want to do tape functions, the user must have MOUNT
          authority.




                        Using DITTO

BATCH job
When running as a batch job, DITTO reads its control statements from
SYSIN.  Control statements must contain the string '$$DITTO ' in columns
1 to 8.  Parameters are specified by keywords.  Control statements may
span more than one control statement (that is "continued").  To indicate
that a statement is continued, code a comma and a blank after the last
keyword/value on the current statement, for example:

     $$DITTO TT,IN=xxx,
     $$DITTO    OUT=yyy,
     $$DITTO    FILES=3

  in this example the command is a tape-to-tape copy, input device is
  unit 'xxx', the output device is unit 'yyy', and 3 files would be
  copied.

Keywords may be specified in any order and must be delimited from the
value by an equal (=) sign.  Keyword/values must be separated from
each other by commas.  A blank indicates end-of-statement.  Comments
may be coded on control statements after the first blank.

For a list of the commands available, execute DITTO and specifiy command
'XXX' ($$DITTO XXX).



STARTED TASK
When running as a started task DITTO will prompt the operator for
commands.  Parameters are positional.  If the operator omits a parameter
or the value specified is invalid, DITTO will prompt the operator with a
message explaining that a parameter was omitted or was invalid and will
allow the parameter to be entered without having to re-key the entire
command.  If you did omit a parameter or one was invalid and you would
rather abort the command, you may enter 'CANCEL' to the prompt for the
missing or invalid parameter and DITTO will prompt for a new command.
If you know the command you want to use, but you can't remember all of
the parameters or you can't remember their order, enter the command
with a question mark (?) and DITTO will respond with a sample of the
command.  For example if you want to do a tape-to-tape copy, the command
is 'TT'.  If you enter 'TT,?' to the command prompt, DITTO will respond
with 'TT,INCUU,OUTCUU,# OF FILES', and will prompt for a command again.
You could have just entered 'TT' and DITTO would have prompted for the
input device, then the output device, and then the number of files one
parameter at a time (since this is more time consuming, you may want to
use the question mark method).  Some parameters are optional, like the
number of records on the TP command.  If you omit the number of records,
DITTO assumes you want the entire file printed, it will not prompt for
omitted optional parameters... you may print a lot more than you had in
mind.  If you omit a required parameter and optional parameters, DITTO
will prompt for the missing required ones, not the optional ones.  If
you want to enter the optional parameter (to limit the number of records
for example), you may want to enter 'CANCEL' to the prompt for the
missing required parameter and re-enter the entire command with the
optional value specified.  If you enter an optional parameter with an
invalid value (like alpha characters in RECORDS), DITTO will prompt to
to allow a valid value to be entered.


TSO
In TSO mode DITTO prompts for commands and parameters with ISPF panels.
Parameters are positional, just like in started task mode.  Rather than
prompting for missing and invalid parameters, DITTO resends the menu
screen with error messages explaining the problem.  Tape and disk reads
always read one physical block at a time.  If you asked for de-blocking,
the logical records in the block will be separated.  Data is displayed
in ISPF tables.  The data may be browsed forward and backward one
physical block at a time.  To use tape commands, the user must have
MOUNT authority.  DITTO issues WAITs that may cause your TSO session
to 'hang' waiting I/O, especially tape... I would not use DITTO from
my TSO session without being physically close the to tape drive.



GENERAL
The commands and print are as similar to IBM's DOS/DITTO as I could make
them.  One significant difference is the 'ACQ' command.  The 'ACQ'
command allocates devices to DITTO.  This prevents MVS from allocating
the device to another user between commands (you wouldn't want to get
the tape positioned where you want it only to have another job cause
it to be re-wound).  Also MVS has a nasty habit of rewinding and
unloading tapes when they are allocated, if you allocated tapes for each
command, MVS would rewind and unload it each time.  The 'REL' command
is the complement command to 'ACQ' and releases devices (of course all
devices are released when DITTO terminates).

DITTO bypasses MVS open.  This means you can read data whether you shoul
be authorized or not.  It also means anyone else who has access to DITTO
can do the same.  There are no SMF records generated either, so not
only are security measures bypassed, there is no trace of the access
at all.  If you use DITTO to write on a tape, tape management systems
like TMS are not aware of the update either.
./ ADD NAME=$$$DOC3

I added this member for those types who would like to understand the
"nuts and bolts" of how DITTO is designed.



DITTO was written as a learning exercise.  I "cut my teeth" on DOS and
when I began working with MVS, I wanted to learn how things like
handling abends, communicating with the operator, etc worked under MVS.
One of the first utilities that I learned to use on DOS was DITTO.  I
don't know how anyone gets by without DITTO (I never found a bug in
DITTO either).  I found that MVS has many good utilities, but nothing
quite like DITTO.

DITTO
    1) determines its environment (batch, STC, TSO)
    2) intercepts abends (ESTAE)
    3) dynamically allocates/frees devices
    4) performs EXCP I/O to tape and dasd
    5) communicates with the operator (in STC mode)
    6) uses SPF panels
    7) I found out that not all MVS's are created equal... some use JES2
       and others use JES3.  If you use JES3, and you don't allow BLP
       processing, and the tape is standard labelled, you must know the
       volser before you use the tape (even if you are only going to
       read it).  One of the main uses of DITTO is to find out what is
       on a tape by "breaking the rules", like not having to know
       volsers or data set names.  At times you may have a tape with
       "botched" labels or some other non-standard situation.  DITTO
       can allow you to position the tape where you want it, copy
       portions, back space, forward space, write tape marks anywhere...
       To get around JES, tape management systems, RACF, etc DITTO
       bypasses OPEN and "opens" the data sets itself.  So DITTO uses
       some APF required services and builds its own DEB (another
       learning experience).


At the high level the flow of DITTO is:

   1) DITTINIT determines the environment (batch, STC, or TSO)
   2) if the environment is batch or STC
         a) call the batch/STC mainline DITTMAIN
         b) if batch, DITTMAIN will call DITTCARD to obtain a command
             . DITTCARD will call DITTCDI1 to read the control records
             . DITTCARD will call DITTPARM to convert the parameters
                  one at a time into internal format.
             . DITTCARD will determine if the command was valid and
                  if all required parameters were present.
             . If the command was not valid an error message will be
                  printed and DITTCARD will look for the next command.
             . If some required parameter was invalid or missing, a
                  message will be printed for each and DITTCARD will
                  look for the next valid command.
             . If the command and all parameters were valid and present,
                  the input module address, output module address,
                  input module processing options byte, output module
                  processing options byte will be set then DITTCARD will
                  return to DITTMAIN.
         c) if STC, DITTMAIN will call DITTCONS to obtain a command.
             . DITTCONS issues a WTOR asking for a command
             . DITTCONS will verify the command and call DITTPARM to
                  convert the parameters one at a time into internal
                  format.
             . if the command was not valid, DITTCONS will issue a
                  WTO explaining the command was not valid and issue
                  another WTOR for asking for a valid command.
             . if a required parameter is missing or invalid, DITTCONS
                  will issue a WTO explaining which parameter is
                  missing or invalid and a WTOR asking for the parameter
             . if the command is valid and all required parameters are
                  entered the input module address, output module
                  address, input module processing options byte, output
                  module processing options byte will be set then
                  DITTCONS will return to DITTMAIN.
         d) After DITTCARD or DITTCONS returns with a valid command,
            DITTMAIN calls DITTDAIR to locate the "DYNBLOK" for the
            input and output devices (except for card, punch, or print).
            The exception to this is command "ACQ" which allocates the
            devices and builds the "DYNBLOK"s.  The DYNBLOK address for
            the input device and output device are in the common area.
         e) After the input and output device are located, DITTMAIN
            enters an input-output loop.  Some commands do not have
            both an input and output module, if the command is input-
            only, the output module address will be zero.  Likewise if
            the command is output-only, the input module address will
            be zero.  The loop continues until either the input or
            output module signals "end-of-file".  Note that "end-of-file
            can be set by output modules... for example the WTM command
            is output only, so the output module signals eof when it has
            written the requested number of tape marks.
         f) When eof is signalled, DITTMAIN performs "reset"... the
            input module address and processing options are cleared,
            output module address and processing options are cleard,
            input and output DYNBLOK addresses are cleared, DITTPARM
            is called to reset all parameters from the previous command.
         g) go back to step "b"

      if the environment is TSO, call the TSO mainline DITTTSOM.
         a) DITTTSOM defines the SPF variables.
         b) DITTTSOM builds the menu data and displays the menu panel.
         c) DITTTSOM verifies the command is valid and calls DITTPARM
               to convert the parameters to internal format.
         d) if the command is not valid, a message is added to the menu
               panel and the menu is displayed again.
         e) if a parameter is invalid or a required parameter is missing
               a message is added to the menu explaining the problem and
               the menu is displayed again.
         f, g, h would be the same as steps d, e, and f for batch and ST
         i) go to step b.

   3) The batch/STC or TSO mainlines return control to DITTINIT.
      DITTINIT frees the trace table and work areas and terminates.




All parameter conversion is performed by DITTPARM whether in batch, STC,
or TSO environment.  This reduced the amount of code over-all, and if
any special conversion technique was needed for a parameter, the same
code could be used for any other parameter with the same internal format
The resultant value for all parameters is a field in DITTCOMM.  The
parameter definition table specifies the internal field name, internal
field id, format, displacement of the field into DITTCOMM, length of
the field in DITTCOMM, and messages to issue if the parameter is missing
or invalid.  Each parameter causes an entry to be added to an assembler
GLOBAL variable.  On commands the required and optional parameters
on the DITTFUNC macro reference the field names in this GLOBAL variable.
Part of the output of the DITTFUNC macro is two tables that contain up
10 parameter id fields.  This is how DITTO knows which fields are valid,
and required or optional for each command.  For STC and TSO modes the
order of the field names in the REQPARM parameter defines the order the
command parameters must be entered.  For batch mode, DITTCARD keeps a
table of the parameters entered on the command, then makes sure all the
parameters in the REQPARM list are in the table.
./ ADD NAME=ASMJCL
//........ JOB ........                                <------- 1
//* ----------------------------------------------------------------- *
//*                                                                   *
//*    1) CHANGE JOB CARD                                             *
//*    2) CHANGE MACLIB TO YOUR DITTO MACRO LIBRARY NAME              *
//*    3) CHANGE SOURCE TO YOUR DITTO SOURCE LIBRARY NAME             *
//*    4) CHANGE LOADLIB TO AN APF AUTHORIZED LIBRARY NAME            *
//*                                                                   *
//* ----------------------------------------------------------------- *
//ASM      PROC NAME=,
//             LIST='SYSOUT=*'
//ASM      EXEC PGM=IEV90,COND=(4,LT),
//             REGION=4096K,
//             PARM='LINECOUNT(55),DECK,NOOBJECT,XREF(SHORT)'
//SYSPRINT DD  &LIST
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=...MACLIB...,DISP=SHR               <------- 2
//SYSPUNCH DD  DSN=&&WK,DISP=MOD
//SYSIN    DD  DSN=...SOURCE...(&NAME),DISP=SHR        <------- 3
// PEND
//* ----------------------------------------------------------------- *
//*                                                                   *
//*           ALLOCATE WORK FILE                                      *
//*                                                                   *
//* ----------------------------------------------------------------- *
//ALLOC    EXEC PGM=IEFBR14
//WK       DD DSN=&&WK,DISP=(NEW,PASS),
//            UNIT=SYSDA,
//            SPACE=(TRK,(15,15)),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120)
//* ----------------------------------------------------------------- *
//*                                                                   *
//*           ASSEMBLE ALL MODULES                                    *
//*                                                                   *
//* ----------------------------------------------------------------- *
//ALOC EXEC ASM,NAME=DITTALOC        'ACQ/REL' COMMAND
//ATTN EXEC ASM,NAME=DITTATTN        TSO ATTENTION EXIT
//AUTH EXEC ASM,NAME=DITTAUTH        DYNAMIC AUTHORIZATION
//CARD EXEC ASM,NAME=DITTCARD        CARD READER
//CDI1 EXEC ASM,NAME=DITTCDI1        CARD-INPUT COMMAND INTERPRETER
//CMD  EXEC ASM,NAME=DITTCMD         COMMAND TABLE
//CDO1 EXEC ASM,NAME=DITTCDO1        CARD OUTPUT
//COMM EXEC ASM,NAME=DITTCOMM        GLOBAL OR 'COMMON' DATA
//CONS EXEC ASM,NAME=DITTCONS        CONSOLE-INPUT COMMAND INTERPRETER
//DAIR EXEC ASM,NAME=DITTDAIR        DAIR INTERFACE
//DAI1 EXEC ASM,NAME=DITTDAI1        DASD READER
//EXCP EXEC ASM,NAME=DITTEXCP        EXCP INTERFACE
//INIT EXEC ASM,NAME=DITTINIT        INITIALIZATION
//INQ  EXEC ASM,NAME=DITTINQ         STC INQUIRE FUNCTION
//MAIN EXEC ASM,NAME=DITTMAIN        BATCH/STC MAINLINE
//PARM EXEC ASM,NAME=DITTPARM        PARAMETER CONVERTER
//PRMD EXEC ASM,NAME=DITTPRMD        TAPE DENSITY CONVERTER/CHECKER
//PRMO EXEC ASM,NAME=DITTPRMO        'OPT' PARAMETER CHECKER
//PRMV EXEC ASM,NAME=DITTPRMV        VOLSER RANGE CHECKER
//PRO1 EXEC ASM,NAME=DITTPRO1        DATA PRINT
//PRT  EXEC ASM,NAME=DITTPRT         PRINTER
//STAE EXEC ASM,NAME=DITTSTAE        STAE EXIT
//TINT EXEC ASM,NAME=DITTTINT        'INT' COMMAND
//TMAP EXEC ASM,NAME=DITTTMAP        'TMAP' COMMAND
//TPI1 EXEC ASM,NAME=DITTTPI1        TAPE READER
//TPI2 EXEC ASM,NAME=DITTTPI2        FORWARD SPACE FILE (FSF)
//TPI3 EXEC ASM,NAME=DITTTPI3        BACK-SPACE RECORD (BSR)
//TPI4 EXEC ASM,NAME=DITTTPI4        BACK-SPACE FILE (BSF)
//TPO1 EXEC ASM,NAME=DITTTPO1        TAPE OUTPUT
//TPO2 EXEC ASM,NAME=DITTTPO2        REWIND (REW)
//TPO3 EXEC ASM,NAME=DITTTPO3        REWIND/UNLOAD (RUN)
//TPO4 EXEC ASM,NAME=DITTTPO4        WRITE TAPE MARK (WTM)
//TSOM EXEC ASM,NAME=DITTTSOM        TSO ENVIRONMENT MAINLINE
//TSO1 EXEC ASM,NAME=DITTTSO1        TSO DATA 'PRINT'
//TSO2 EXEC ASM,NAME=DITTTSO2        TSO ENVIRONMENT 'TMAP'
//TSO3 EXEC ASM,NAME=DITTTSO3        TSO ENVIRONMENT INQUIRE FUNCTION
//XXXX EXEC ASM,NAME=DITTXXXX        DUMMY MODULE FOR STAE EXIT
//* ----------------------------------------------------------------- *
//*                                                                   *
//*           LINKEDIT DITTO                                          *
//*                                                                   *
//* ----------------------------------------------------------------- *
//LINK     EXEC PGM=HEWLH096,REGION=512K,
//             PARM='LIST,LET,XREF,AC=1,NCAL'
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSDA,SPACE=(TRK,(15,15))
//SYSLMOD  DD  DSN=...LOADLIB...,DISP=SHR                <---------- 4
//SYSLIN   DD  DSN=&&WK,DISP=OLD
//         DD  *
  ENTRY DITTINIT
  NAME DITTO(R)
/*
//
./ ADD NAME=DITTALOC
*--------------------------------------------------------------------*
*                                                                    *
*              UNIT ALLOCATION                                       *
*                                                                    *
*    THIS MODULE INTERFACES WITH 'DITTDAIR' TO ALLOCATE OR RELEASE   *
*    DEVICES.  ALLOCATING A DEVICE AS A SEPARATE COMMAND PREVENTS    *
*    ANOTHER JOB OR TASK FROM ALLOCATING THE DEVICE BETWEEN DITTO    *
*    COMMANDS.                                                       *
*                                                                    *
*    WHEN INVOKED AS AN INPUT MODULE, THE REQUEST IS 'ALLOCATE'      *
*    (ACQ), WHEN INVOKED AS AN OUTPUT MODULE, THE REQUEST IS RELASE  *
*    (REL).  THIS MODULE DOES NO I/O AND SIGNALS EOF AT ITS EXIT.    *
*                                                                    *
*--------------------------------------------------------------------*
DITTALOC DITTPRFX ALOCSAVE,'ALLOCATE DEVICE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMINU       .. TRACE CUU RECEIVED
         TM    COMMFLAG,$COMMCLS   'CLOSE' IN PROGRESS??
         BO    ALOC9900            YES.. NOTHING FOR US TO DO
ALOC0010 DS    0H
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOCK BY CUU
         MVC   DAIRCUU,COMMINU     MOVE UNIT ADDRESS
         C     R12,COMMINM         RUNNING AS INPUT MODULE?
         BE    ALOC0020            YES.. THEN FUNCTION IS 'ACQ'
         C     R12,COMMOUTM        RUNNING AS OUTPUT MODULE?
         BE    ALOC0030            YES.. THEN FUNCTION IS 'REL'
         ABEND ABEND013,DUMP,,USER ABEND WITH REASON
ALOC0020 DS    0H
         DITTRACE ID=ALOCACQ       TRACE CALLS TO DAIR MODULE
         MVI   DAIRCMMD,$DAIRACQ   ALLOCATION REQUEST
         B     ALOC0040
ALOC0030 DS    0H
         DITTRACE ID=ALOCREL       TRACE CALLS TO DAIR MODULE
         MVI   DAIRCMMD,$DAIRREL   RELEASE REQUEST
ALOC0040 DS    0H
         LA    R1,DAIRBLOK         DAIR REQUEST BLOCK
         L     R15,ADAIR           DAIR MODULE ADDRESS
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    ALOC0050            YES.. TEST FOR RELEASE FUNCTION
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL??
         BNE   ALOC0400            NO, EXAMINE FURTHER
         OI    COMMFLAG,$COMMEOF   SIGNAL EOF (STOP INPUT-OUTPUT)
         B     ALOC9900            EXIT
ALOC0050 DS    0H
         CLI   DAIRCMMD,$DAIRREL   WAS FUNCTION 'RELEASE'?
         BNE   ALOC0400            NO
         LA    R2,NOTALOCM         SET MESSAGE BLOCK ADDRESS
         B     ALOC0420            ISSUE MESSAGE(S)
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR ROUTINES                                        *
*                                                                    *
*--------------------------------------------------------------------*
ALOC0400 DS    0H
         LA    R2,MSGTABLE         MESSAGE TABLE ADDRESS
         USING MSGDSECT,R2         DEFINE BASE
ALOC0410 DS    0H
         CLI   MSGSTAT,X'FF'       END OF TABLE??
         BE    ALOC0450            UNKNOWN STATUS FROM DITTDAIR
         CLC   MSGSTAT,DAIRSTAT    STATUS MESSAGE LOCATED??
         BE    ALOC0420            YES
         AH    R2,MSGLEN           NEXT MESSAGE
         B     ALOC0410            CONTINUE SEARCH
ALOC0420 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,MSGWTO         LENGTH FROM WTO
         SH    R1,H5               MINUS WTO PREFIX LENGTH + 1
         CLI   COMMENV,$ENVTSO     RUNNING AS A TSO USER?
         BE    ALOC0440            YES
         EX    R1,PRTMSGM          MOVE MESSAGE TEXT
         MVI   PRTCC,C' '          CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTCMD     MOVE COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ADDRESS
         BALR  R14,R15             LINK TO PRINT MODULE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   ALOC0480            NO... CANCELLED ON ANY STATUS
         LA    R3,MSGWTO           WTO ADDRESS
         TM    MSGFLAG,$ABORT      ABORT ON THIS ERROR??
         BO    ALOC0470            YES
         ST    R3,PARMWTO          PASS ADDRESS TO PARAMETER MODULE
         MVI   PARMCMMD,$PARMEWTO  ISSUE PROVIDED WTO
ALOC0430 DS    0H
         MVC   PARMPARM,UNITID     'ID' FOR UNIT
         LA    R1,PARMBLOK         PARAMETER BLOCK
         L     R15,APARM           PARAMETER CONVERTER BLOCK
         BALR  R14,R15             LINK TO 'DITTPARM'
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    ALOC0600            YES
         TM    PARMFLAG,$PARMOK    WAS A GOOD UNIT ENTERED??
         BO    ALOC0010            YES .. ATTEMPT TO REQUEST AGAIN
         B     ALOC0430            ASK FOR CUU AGAIN
ALOC0440 DS    0H
         EX    R1,TSOMSGM          MOVE MESSAGE TEXT TO 'MSG02'
         B     ALOC0480            AND EXIT
ALOC0450 DS    0H
         OI    COMMFLAG,$ABORT+$ABEND
         CLI   COMMENV,$ENVTSO     RUNNING IN TSO ENVIRONMENT?
         BE    ALOC0460            YES
         MVC   PRTDATA(ALOCM01L),ALOCM01 ERROR MESSAGE
         MVI   PRTCC,C' '          CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTCMD     MOVE COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ADDRESS
         BALR  R14,R15             LINK TO PRINT MODULE
         B     ALOC9900            AND EXIT
ALOC0460 DS    0H
         MVC   MSG01(ALOCM01L),ALOCM01
         B     ALOC9900            AND EXIT
ALOC0470 DS    0H
         WTO   MF=(E,(3)),         ISSUE WTO                           +
               CONSID=COMMCID      .. CONSOLE ID
ALOC0480 DS    0H
         OI    COMMFLAG,$ABORT     ABORT THIS COMMAND
         B     ALOC9900            AND EXIT
ALOC0600 DS    0H
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         DITTRACE ID=CANCEL
         WTO   'ALLOCATE REQUEST CANCELLED',                           +
               CONSID=COMMCID      .. CONSOLE ID
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
ALOC9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
PRTMSGM  MVC   PRTDATA(0),MSGWTO+4 MOVE MESSAGE TEXT
TSOMSGM  MVC   MSG02(0),MSGWTO+4   MOVE MESSAGE TEXT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
ALOCSAVE DC    18F'0'              REGISTER SAVE AREA
H5       DC    H'5'                CONSTANT
         PARMGEN GEN=NO            INVOKE PARMGEN TO DEFINE FIELD ID'S
UNITID   PARMID UNIT               GENERATE FIELD ID FOR 'UNIT'
ALOCM01  DC    C'UNKNOWN DITTDAIR STATUS RETURNED (INTERNAL ERROR)'
ALOCM01L EQU   *-ALOCM01
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC ALLOCATION/DE-ALLOCATION REQUEST BLOCK        *
*                                                                    *
*--------------------------------------------------------------------*
DAIRBLOK DAIRBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERSION REQUEST BLOCK                    *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR MESSAGE/FLAGS                                   *
*                                                                    *
*--------------------------------------------------------------------*
NOTALOCM STATMSG STAT=$DAIRSLE,                                        +
               FLAGS=,                                                 +
               MESSAGE='UNIT IS NOT ALLOCATED'
MSGTABLE DS    0F
         STATMSG STAT=$DAIRSDE,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DAIR ERROR (INTERAL ERROR)'
         STATMSG STAT=$DAIRSLE,                                        +
               FLAGS=,                                                 +
               MESSAGE='DYNAMIC BLOCK COULD NOT BE LOCATED'
         STATMSG STAT=$DAIRSIR,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='INVALID DAIR REQUEST (INTERNAL ERROR)'
         STATMSG STAT=$DAIRSNU,                                        +
               FLAGS=,                                                 +
               MESSAGE='UNIT DOES NOT EXIST'
         STATMSG STAT=$DAIRSTE,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS NOT A SUPPORTED DEVICE TYPE'
         STATMSG STAT=$DAIRSRE,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='INVALID RESOURCE TYPE (INTERNAL ERROR)'
         STATMSG STAT=$DAIRSDP,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS ALREADY ALLOCATED'
         STATMSG STAT=$DAIRSOF,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS OFFLINE'
         STATMSG STAT=$DAIRSAL,                                        +
               FLAGS=,                                                 +
               MESSAGE='DEVICE IS ALLOCATED TO ANOTHER USER'
         STATMSG STAT=$DAIRSUD,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 1600 OR 6250 BPI'
         STATMSG STAT=$DAIRS16,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 1600 BPI'
         STATMSG STAT=$DAIRS62,                                        +
               FLAGS=,                                                 +
               MESSAGE='3420 UNIT DOES NOT ALLOW 6250 BPI'
         STATMSG STAT=$DAIRSD1,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DENSITY MAY NOT BE SPECIFIED FOR 3480'
         STATMSG STAT=$DAIRSD2,                                        +
               FLAGS=$ABORT,                                           +
               MESSAGE='DENSITY MAY NOT BE SPECIFIED FOR DASD UNITS'
         DC    X'FFFF'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MESSAGE DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
MSGDSECT DSECT
MSGLEN   DS   AL2              LENGTH OF THIS ENTRY
MSGSTAT  DS   X                CORRESPONDS TO 'DAIRSTAT' VALUE
MSGFLAG  DS   X                FLAGS
MSGWTO   DS   0F               WTO MESSAGE (IF RUNNING AS A STC)
         END  DITTALOC
./ ADD NAME=DITTATTN
*--------------------------------------------------------------------*
*                                                                    *
*              TSO ATTENTION EXIT                                    *
*                                                                    *
*--------------------------------------------------------------------*
DITTATTN DITTPRFX ATTNSAVE,'TSO ATTENTION EXIT'
         USING DITTCOMM,R11        SPECIFY BASE
         L     R11,8(R1)           SET COMM AREA ADDRESS
         DITTRACE ID=ATTNHUT
         OI    COMMATTN,$ATTN      SET ATTENTION EXIT FLAG
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 GET OUT OF DODGE
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
*--------------------------------------------------------------------*
ATTNSAVE DC    18F'0'              REGISTER SAVE AREA
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         SPACE 2
         END
./ ADD NAME=DITTAUTH
*--------------------------------------------------------------------*
*                                                                    *
*             DYNAMIC AUTHORIZATION                                  *
*                                                                    *
*       R1 CONTAINS THE REQUEST TYPE:                                *
*           R1 = ZERO, TURN ON AUTHORIZATION                         *
*           R1 NON ZERO, TURN OFF AUTHORIZATION                      *
*                                                                    *
*--------------------------------------------------------------------*
DITTAUTH DITTPRFX AUTHSAVE,'DYNAMIC AUTHORIZATION'
         USING DITTCOMM,R11        DEFINE COMM AREA BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         LTR   R1,R1               TURN OFF?
         BNZ   AUTH0010            YES..
         DITTRACE ID=AUTHON
***
***   ADD YOUR DYNAMIC AUTHORIZATION CODE HERE
***
         B     AUTH9900            AND EXIT
AUTH0010 DS    0H
         DITTRACE ID=AUTHOFF
***
***   ADD YOUR DYNAMIC DE-AUTHORIZATION CODE HERE
***
*--------------------------------------------------------------------*
*                                                                    *
*              EXIT POINT                                            *
*                                                                    *
*--------------------------------------------------------------------*
AUTH9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
AUTHSAVE DC   18F'0'               REGISTER SAVE AREA
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTAUTH
./ ADD NAME=DITTCARD
*--------------------------------------------------------------------*
*                                                                    *
*              CARD COMMAND INPUT ROUTINE                            *
*                                                                    *
*      THIS MODULE PROCESSES CARD-INPUT COMMANDS.  THIS MODULE       *
*      IS CALLED BY 'DITTMAIN' WHEN DITTO IS RUNNING AS A JOB        *
*      (NOT A STARTED TASK OR TSO SESSION).                          *
*                                                                    *
*      SINCE CARDS MAY BE USED AS COMMANDS OR AS DATA, AND TO        *
*      KEEP THIS MODULE FROM BEING TOO COMPLICATED, ALL CARD         *
*      READING IS DONE BY A SEPARATE MODULE 'DITTCDI1'.              *
*                                                                    *
*      SINCE THE SAME PARAMETERS WILL BE REQUIRED BY CARD-INPUT,     *
*      CONSOLE-INPUT, AND TSO-INPUT, PARAMETER CONVERSION IS         *
*      PERFORMED BY A SEPARATE MODULE.  THIS WAS TO ELIMINATE AS     *
*      MUCH DUPLICATION OF CODE AS POSSIBLE.  ALSO IF A NEW          *
*      PARAMETER IS REQUIRED, ONLY ONE MODULE WOULD HAVE TO BE       *
*      CHANGED TO CONVERT IT.  THE PARAMETER CONVERTER IS 'DITTPARM'.*
*                                                                    *
*      IF THE COMMAND IS UNDEFINED OR ALL PARAMETERS ARE NOT         *
*      ENTERED OR ARE IN ERROR, MESSAGES WILL BE PRINTED IDENTIFYING *
*      THE INVALID COMMAND OR PARAMETER AND ERROR INDICATORS WILL    *
*      BE SET IN 'DITTCOMM'.                                         *
*                                                                    *
*--------------------------------------------------------------------*
DITTCARD DITTPRFX CARDSAVE,'CARD-INPUT COMMAND INTERPRETER'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
CARD0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE NEW COMMAND READS
         L     R15,ACDI1           CARD INPUT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO CARD INPUT
         TM    COMMFLAG,$COMMEOF   END OF FILE?
         BO    CARD9900            YES, EXIT
         L     R3,COMMCRCD         CARD RECORD
         LH    R4,COMMCRCL         LENGTH OF I/O AREA
         LA    R1,0(R3,R4)         END OF I/O AREA +1
         BCTR  R1,0                END OF I/O AREA
CARD0020 DS    0H
         CLI   0(R3),C' '          AT LEAST A BLANK?
         BNE   CARD0030            YES
         LA    R3,1(R3)            TRY NEXT ONE
         BCT   R4,CARD0020         LOOP
         B     CARD0010            NOTHING FOUND, READ NEXT RECORD
CARD0030 DS    0H
         CLI   0(R1),C' '          AT LEAST A BLANK?
         BH    CARD0040            YES
         BCTR  R1,0                BACK UP
         BCT   R4,CARD0030         LOOP BACK
         B     CARD0010            NOTHING FOUND, READ NEXT RECORD
CARD0040 DS    0H
         BAL   R14,CARD8000        FIND COMMAND ON INPUT RECORD
         CLC   CARDXTR,CARDWORK    EXTERNAL TRACE REQUEST?
         BE    CARD3000            YES
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=CARDWORK      .. COMMAND BEING SEARCHED FOR
CARD0070 DS    0H
         DITTRACE ID=SCANCMD,      TRACE COMMAND TABLE SCAN            +
               DATA1=FUNCCMD       .. COMMAND IN CURRENT TABLE ENTRY
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CARDERR1            YES, INVALID COMMAND
         TM    FUNCENV,$FUNCJOB    APPLICABLE TO BATCH JOB MODE?
         BNO   CARD0080            NO.. SKIP THIS COMMAND
         LH    R1,CARDWLEN         ENTERED LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH?
         BNE   CARD0080            NO
         BCTR  R1,0                ADJUST FOR EXECUTE
         EX    R1,CARDFCLC         CHECK FUNCTION CODE
         BE    CARD0090            COMMAND LOCATED
CARD0080 DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     CARD0070            LOOP
CARD0090 DS    0H
         DITTRACE ID=CMDFND        TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED?
         BE    CARD0200            YES
         CLC   FUNCCMD,LISTFNC     XXX FUNCTION?
         BE    CARD1000            YES
         XC    CARDRQCT,CARDRQCT   CLEAR # REQ PARMS FOUND
         XC    CARDOPCT,CARDOPCT   CLEAR # OPT PARMS FOUND
         XC    REQTABLE,REQTABLE   CLEAR FOUND REQ PARM ID'S
CARD0100 DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARD0180            NO, TEST PARAMETERS
         MVC   CARDWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CARD8000        GET NEXT KEYWORD
         BAL   R14,CARD2000        SEARCH KEYWORDS
         CLI   CARDPID,X'FF'       VALID PARAMETER?
         BE    CARDERR2            NO.. INVALID KEYWORD
         LA    R6,FUNCREQ          REQUIRED PARAMETER ID'S
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    CARD0150            NO REQUIRED PARAMETERS
         DITTRACE ID=REQSCAN       TRACE REQUIRED PARM TABLE SCAN
CARD0110 DS    0H
         CLC   CARDPID,0(R6)       VALID REQUIRED PARM?
         BE    CARD0120            YES
         LA    R6,1(R6)            NEXT ENTRY
         BCT   R2,CARD0110         LOOP
         B     CARD0150            TRY FOR OPTIONAL KEYWORD
CARD0120 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARDERR5            NO, TEST PARAMETERS
         BAL   R14,CARD8000        GET OPERAND
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   MOVE COMMAND
         MVC   PARMPARM,CARDPID    PASS FIELD ID
         MVC   PARMWORK,CARDWORK   PASS DATA
         MVC   PARMWLEN,CARDWLEN   PASS DATA LENGTH
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALUES FOR OPTIONAL PARMS
         LA    R1,FUNCOPV          VALUES FOR OPTIONAL PARMS
         ST    R1,PARMOPV          PASS ADDRESS TO PARM CONVERTER
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARM CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BNO   CARDERR3            NO...
         LA    R1,REQTABLE         FOUND PARM TABLE
CARD0130 DS    0H
         CLI   0(R1),X'00'         AVAILABLE SLOT?
         BE    CARD0140            YES
         CLC   CARDPID,0(R1)       SAME PARAMETER?
         BE    CARD0100            YES.. DON'T COUNT IT TWICE
         LA    R1,1(R1)            TRY NEXT BYTE
         B     CARD0130            LOOP
CARD0140 DS    0H
         MVC   0(1,R1),CARDPID     SAVE PARAMETER ID
         LH    R1,CARDRQCT         REQUIRED PARAMETERS FOUND
         LA    R1,1(R1)            ADD 1
         STH   R1,CARDRQCT         SAVE TOTAL
         B     CARD0100            PROCESS NEXT PARAMETER
CARD0150 DS    0H
         DITTRACE ID=OPTSCAN       TRACE OPTIONAL PARM TABLE SCAN
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    CARDERR2            UNKNOWN KEYWORD
         LA    R6,FUNCOPT          FIRST OPTIONAL PARAMETER ID
CARD0160 DS    0H
         CLC   CARDPID,0(R6)       VALID PARM ID?
         BE    CARD0170            YES
         LA    R6,1(R6)            NEXT ENTRY
         BCT   R2,CARD0160         LOOP
         B     CARDERR2            KEYWORD IS UNKNOWN
CARD0170 DS    0H
         LTR   R4,R4               ANY DATA LEFT?
         BZ    CARDERR5            NO
         BAL   R14,CARD8000        GET OPERAND
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   MOVE COMMAND
         MVC   PARMPARM,CARDPID    PASS FIELD ID
         MVC   PARMWORK,CARDWORK   PASS DATA
         MVC   PARMWLEN,CARDWLEN   PASS DATA LENGTH
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALUES FOR OPTIONAL PARMS
         LA    R1,FUNCOPV          VALUES FOR OPTIONAL PARMS
         ST    R1,PARMOPV          PASS ADDRESS TO PARM CONVERTER
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BNO   CARDERR3            NO...
         LH    R1,CARDOPCT         OPTIONAL PARAMETERS FOUND
         LA    R1,1(R1)            ADD 1
         STH   R1,CARDOPCT         SAVE TOTAL
         B     CARD0100            PROCESS NEXT PARAMETER
CARD0180 DS    0H
         DITTRACE ID=PARMCHK       TRACE PARAMETER CHECK
         CLC   FUNCNREQ,CARDRQCT   ALL REQUIRED PARAMETERS FOUND?
         BNE   CARDERR4            NO
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN1        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    CARD0190            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
CARD0190 DS    0H
         ICM   R1,3,FUNCOUT1       OUTPUT MODULE ADDRESS DISPLACEMENT
         BZ    CARD9900            NO OUTPUT MODULE
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         B     CARD9900            EXIT
CARD0200 DS    0H
         MVC   CARDM06A,CARDWORK   MOVE COMMAND
         OC    CARDM06A,COMMBLKS   MAKE IT ALL PRINTABLE
         MVC   CARDM06B,FUNCEX1    MOVE EXAMPLE OF COMMAND
         LA    R1,CARDM06          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD0010            LOOK FOR NEXT COMMAND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              LIST AVAILABLE COMMANDS                               *
*                                                                    *
*--------------------------------------------------------------------*
CARD1000 DS    0H
         DITTRACE ID=LISTCMDS      TRACE START OF LIST
         L     R5,ACMD             FUNCTIONS TABLE ADDRESS
         MVI   PRTCMMD,$PRTHEAD    REQUEST HEADING
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1010 DS    0H
         DITTRACE ID=LISTFUNC      TRACE EACH FUNCTION
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CARD1030            YES
         TM    FUNCENV,$FUNCJOB    COMMAND APPLICABLE TO BATCH JOB?
         BNO   CARD1020            NO
         MVC   CARDM07A,FUNCCMD    MOVE COMMAND
         MVC   CARDM07B,FUNCDESC   MOVE DESCRIPTION
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         MVC   PRTDATA(CARDM07L),CARDM07
         MVI   PRTCC,C'0'          DOUBLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         MVC   CARDM08A,FUNCEX1    MOVE EXAMPLE OF COMMAND
         MVC   PRTDATA(CARDM08L),CARDM08
         MVI   PRTCC,C' '          SINGLE SPACE CARRIAGE CONTROL
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1020 DS    0H
         AH    R5,FUNCELEN         NEXT FUNCTION
         B     CARD1010            LIST NEXT FUNCTION
CARD1030 DS    0H
         MVC   PRTDATA(CARDM10L),CARDM10
         MVI   PRTCC,C'-'          TRIPLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         LA    R2,KEYWDTBL         FIRST KEYWORD/PARM
         USING KEYWDSCT,R2         DEFINE BASE
CARD1040 DS    0H
         CLI   0(R2),X'FF'         END OF TABLE?
         BE    CARD0010            YES
         CLC   KEYWD,KEYWDPRM      KEYWORD AND ALIAS THE SAME?
         BE    CARD1050            DON'T PRINT IT
         MVC   CARDM11K,KEYWD      SET KEYWORD
         MVC   CARDM11A,KEYWDPRM   SET KEYWORD ALIAS
         MVC   PRTDATA(CARDM11L),CARDM11
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
CARD1050 DS    0H
         LA    R2,KEYWDDLN(R2)     NEXT KEYWORD
         B     CARD1040            PRINT ALL PARMS/ALIASES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              LOCATE KEYWORDS                                       *
*                                                                    *
*--------------------------------------------------------------------*
CARD2000 DS    0H
         LA    R2,KEYWDTBL         KEYWORD TABLE
         MVI   CARDPID,X'FF'       ASSUME INVALID KEYWORD
CARD2010 DS    0H
         CLI   KEYWDLEN,X'FF'      END OF TABLE?
         BER   R14                 YES
         LH    R1,KEYWDLEN         KEYWORD LENGTH
         CH    R1,CARDWLEN         MATCHING LENGTHS?
         BNE   CARD2020            NO
         BCTR  R1,0                ADJUST
         EX    R1,KEYWDCLC         KEYWORD LOCATED?
         BE    CARD2030            YES
CARD2020 DS    0H
         LA    R2,KEYWDDLN(R2)     NEXT KEYWORD
         B     CARD2010            CHECK IT
CARD2030 DS    0H
         MVC   CARDPID,KEYWDPID    MOVE PARAMETER ID
         BR    R14                 EXIT
KEYWDCLC CLC   KEYWD(0),CARDWORK   CHECK FOR MATCHING KEYWORDS
*--------------------------------------------------------------------*
*                                                                    *
*              EXTERNAL TRACE                                        *
*                                                                    *
*--------------------------------------------------------------------*
CARD3000 DS    0H
         DITTRACE ID=XTRACE
         BAL   R14,CARD8000        FIND TRACE OPTION (ON OR OFF)
         CLC   CARDON,CARDWORK     TURN TRACE ON?
         BE    CARD3010            YES
         CLC   CARDOFF,CARDWORK    TURN TRACE OFF?
         BE    CARD3030            YES
         LA    R1,CARDM15          INVALID EXTERNAL TRACE COMMAND
         B     CARD3050            PRINT MESSAGE
CARD3010 DS    0H
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    CARD3020            YES
         DITTRACE ID=TRACEON
         OI    XTRFLAG,$XTRACE     SIGNAL EXTERNAL TRACE ON
         LA    R1,CARDM09          TRACE ACTIVATED MESSAGE
         B     CARD3050            PRINT MESSAGE
CARD3020 DS    0H
         DITTRACE ID=DUPON
         LA    R1,CARDM12          TRACE ALREADY ACTIVE
         B     CARD3050            PRINT MESSAGE
CARD3030 DS    0H
         TM    XTRFLAG,$XTRACE     TRACE ON?
         BNO   CARD3040            NO
         DITTRACE ID=TRACEOFF
         LA    R1,CARDM13          TRACE DE-ACTIVATED MESSAGE
         NI    XTRFLAG,255-$XTRACE RESET FLAG
         B     CARD3050            PRINT MESSAGE
CARD3040 DS    0H
         DITTRACE ID=DUPOFF
         LA    R1,CARDM14          TRACE NOT ACTIVE MESSAE
CARD3050 DS    0H
         BAL   R9,CARD8200         PRINT MESSAGE
         B     CARD0010            READ NEXT COMMAND
*--------------------------------------------------------------------*
*                                                                    *
*              INPUT PARSER                                          *
*         R1, R2, R15 ARE WORK REGISTERS                             *
*         R3 HAS CURRENT INPUT ADDRESS                               *
*         R4 LENGTH REMAINING ON INPUT                               *
*         R14 IS RETURN ADDRESS                                      *
*                                                                    *
*         ON EXIT 'CARDWORK' CONTAINS DATA FOUND TO NEXT DELIMITER   *
*         UP TO 50 BYTES MAX, 'CARDWLEN' WILL BE SET TO THE LENGTH.  *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL REFLECT  *
*         LENGTH LEFT FOLLOWING THE DELIMITER.                       *
*                                                                    *
*--------------------------------------------------------------------*
CARD8000 DS    0H
         ST    R14,PARSESAV        SAVE RETURN ADDRESS
         DITTRACE ID=PARSE
         XC    CARDWORK,CARDWORK   CLEAR WORK AREA
         XC    CARDWLEN,CARDWLEN   CLEAR LENGTH
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZ    CARD8040            NO..
         LA    R1,CARDWORK         WORK AREA
         SR    R2,R2               CLEAR LENGTH COUNTER
         LA    R15,L'CARDWORK      MAXIMUM LENGTH
CARD8010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BE    CARD8030            YES
         CLI   0(R3),C','          COMMA?
         BE    CARD8030            YES
         CLI   0(R3),C'='          EQUAL SIGN?
         BE    CARD8030            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,CARD8020         DECREMENT COUNT
         STH   R2,CARDWLEN         SAVE CURRENT LENGTH
         B     CARD8040            LENGTH EXHAUSTED.. STOP SCAN
CARD8020 DS    0H
         BCT   R15,CARD8010        LOOP
CARD8030 DS    0H
         STH   R2,CARDWLEN         SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
CARD8040 DS    0H
         DITTRACE ID=PARSEEND,     PARSE COMPLETE                      +
               DATA1=CARDWORK,     .. OUTPUT VALUE                     +
               DATA2=CARDWLEN      .. OUTPUT LENGTH
         L     R14,PARSESAV        RESTORE RETURN ADDRESS
         BR    R14                 LENGTH EXHAUSTED, EXIT
*--------------------------------------------------------------------*
*                                                                    *
*          MESSAGE PRINTING                                          *
*                                                                    *
*        R1 SHOULD POINT TO A 2-BYTE LENGTH PREFIXING THE MESSAGE    *
*        TEXT.                                                       *
*                                                                    *
*--------------------------------------------------------------------*
CARD8200 DS    0H
         MVI   PRTCMMD,$PRTCMD     PRINT COMMAND
         LH    R15,0(R1)           MESSAGE LENGTH
         BCTR  R15,0               ADJUST FOR EXECUTE
         EX    R15,MSGMVC          MOVE THE MESSAGE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT ROUTINE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT ROUTINE
         BR    R9                  RETURN
MSGMVC   MVC   PRTDATA(0),2(R1)    MOVE MESSAGE
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR ROUTINES                                        *
*                                                                    *
*--------------------------------------------------------------------*
CARDERR1 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM01M,CARDWORK   MOVE FUNCTION
         LA    R1,CARDM01          POINT TO MESAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR2 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM02M,CARDWORK   MOVE KEYWORD
         LA    R1,CARDM02          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR3 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         L     R1,PARMMSGA         RETURNED MESSAGE'S ADDRESS
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM03M,CARDWORK   MOVE OPERAND
         LA    R1,CARDM03          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
CARDERR4 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         LA    R1,CARDM04          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         LA    R6,FUNCREQ          FIRST REQUIRED PARAMETER
         DITTRACE ID=LISTREQ       TRACE LISTING MISSING PARMS
CARDE401 DS    0H
         LA    R1,REQTABLE         FOUND PARM TABLE
CARDE402 DS    0H
         CLI   0(R1),X'00'         EMPTY SLOT?
         BE    CARDE403            YES, NO NEED TO LOOK FURTHER
         CLC   0(1,R6),0(R1)       WAS THIS PARAMETER FOUND?
         BE    CARDE404            YES
         LA    R1,1(R1)            NEXT FOUND PARAMETER ID
         B     CARDE402            TRY THIS ONE
CARDE403 DS    0H
         MVI   PARMCMMD,$PARMMIS   MOVE COMMAND
         MVC   PARMPARM,0(R6)      MOVE PARAMETER ID
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         BAL   R9,CARD8200         PRINT RETURNED MESSAGE
CARDE404 DS    0H
         LA    R6,1(R6)            NEXT PARAMETER
         BCT   R2,CARDE401         LOOK FOR THIS PARAMETER
         B     CARD9900            EXIT
CARDERR5 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL COMMAND ERROR
         OC    CARDWORK,COMMBLKS   MAKE ALL POSITIONS PRINTABLE
         MVC   CARDM05M,CARDWORK   MOVE KEYWORD
         LA    R1,CARDM05          POINT TO MESSAGE
         BAL   R9,CARD8200         LINK TO PRINT ROUTINE
         B     CARD9900            EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
CARD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXITS
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
*--------------------------------------------------------------------*
*                                                                    *
*              CARD-INPUT FUNCTION EXECUTED INSTRUCTIONS             *
*                                                                    *
*--------------------------------------------------------------------*
CARDFCLC CLC   FUNCCMD(0),CARDWORK  CORRECT COMMAND?
CARDCMVC MVC   PRTDATA+8(0),0(R15)  MOVE COMMAND(S) TO PRINT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CARD-INPUT FUNCTION WORK AREAS                        *
*                                                                    *
*--------------------------------------------------------------------*
CARDSAVE DC    18F'0'              REGISTER SAVE AREA
PARSESAV DS    A                   PARSE ROUTINE RETURN ADDRESS
CARDWLEN DC    H'0'                CURRENT PARSER OUTPUT LENGTH
CARDRQCT DC    H'0'                REQUIRED PARAMETERS COUNT FOUND
CARDOPCT DC    H'0'                OPTIONAL PARAMETERS COUNT FOUND
CARDWORK DC    CL50' '             PARSER OUTPUT WORK AREA
REQTABLE DC    XL10'00'            FOUND REQUIRED PARAMETER TABLE
CARDPID  DC    X'FF'               PARAMETER ID FROM KEYWD TABLE
CARDXTR  DC    C'TRACE'            EXTERNAL TRACE COMMAND
CARDON   DC    C'ON'               CONSTANT
CARDOFF  DC    C'OFF'              CONSTANT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CARD-INPUT FUNCTION PRINT MODULE INTERFACE            *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CARD-INPUT FUNCTION PARAMETER SEARCH INTERFACE        *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
LISTFNC  DC    CL4'XXX '
CARDM01  DC    AL2(CARDM01L-2)
         DC    C'INVALID CARD INPUT FUNCTION: '
CARDM01M DC    CL50' '
CARDM01L EQU   *-CARDM01
CARDM02  DC    AL2(CARDM02L-2)
         DC    C'INVALID KEYWORD IN CARD INPUT FUNCTION: '
CARDM02M DC    CL44' '
CARDM02L EQU   *-CARDM02
CARDM03  DC    AL2(CARDM03L-2)
         DC    C'INVALID OPERAND IN CARD INPUT FUNCTION: '
CARDM03M DC    CL44' '
CARDM03L EQU   *-CARDM03
CARDM04  DC    AL2(CARDM04L-2)
         DC    C'REQUIRED PARAMETERS OMITTED'
CARDM04L EQU   *-CARDM04
CARDM05  DC    AL2(CARDM05L-2)
         DC    C'PARAMETER SPECIFIED WITH NO OPERAND: '
CARDM05M DC    CL44' '
CARDM05L EQU   *-CARDM05
CARDM06  DC    AL2(CARDM06L-2)
         DC    C'EXAMPLE OF COMMAND- '
CARDM06A DC    CL8' '
         DC    C' - '
CARDM06B DC    CL80' '
CARDM06L EQU   *-CARDM06
CARDM07  DC    C'COMMAND: '
CARDM07A DC    CL4' '
         DC    CL18' '
CARDM07B DC    CL80' '
CARDM07L EQU   *-CARDM07
CARDM08  DC    C'EXAMPLE OF COMMAND:'
         DC    CL12' '
CARDM08A DC    CL80' '
CARDM08L EQU   *-CARDM08
CARDM09  DC    AL2(CARDM09L)
CARDM09M DC    C'EXTERNAL TRACE ACTIVATED'
CARDM09L EQU   *-CARDM09M
CARDM10  DC    CL20'KEYWORD ALIASES --  '
         DC    CL08' PARM   '
         DC    CL02' '
         DC    CL08' ALIAS  '
CARDM10L EQU   *-CARDM10
CARDM11  DC    CL20' '
CARDM11K DC    CL08' '
         DC    CL02' '
CARDM11A DC    CL08' '
CARDM11L EQU   *-CARDM11
CARDM12  DC    AL2(CARDM12L)
CARDM12M DC    C'EXTERNAL TRACE ALREADY ACTIVE'
CARDM12L EQU   *-CARDM12M
CARDM13  DC    AL2(CARDM13L)
CARDM13M DC    C'EXTERNAL TRACE DE-ACTIVATED'
CARDM13L EQU   *-CARDM13M
CARDM14  DC    AL2(CARDM14L)
CARDM14M DC    C'EXTERNAL TRACE NOT ACTIVE'
CARDM14L EQU   *-CARDM14M
CARDM15  DC    AL2(CARDM15L)
CARDM15M DC    C'INVALID TRACE COMMAND (USE ''TRACE ON'' OR ''TRACE OFF+
               '')'
CARDM15L EQU   *-CARDM15M
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DEFINE PARAMETERS                                     *
*                                                                    *
*--------------------------------------------------------------------*
         PARMGEN GEN=NO
*--------------------------------------------------------------------*
*                                                                    *
*              DEFINE KEYWORDS                                       *
*                                                                    *
*--------------------------------------------------------------------*
KEYWDTBL DS    0H
         KEYWD BLKSIZE,PARM=BLKSIZE
         KEYWD COUNT,PARM=RECORDS
         KEYWD CUUIN,PARM=CUUIN
         KEYWD CUUOUT,PARM=CUUOUT
         KEYWD DENSITY,PARM=DENSITY
         KEYWD DISKADDR,PARM=DISKADDR
         KEYWD FILES,PARM=FILES
         KEYWD FROMVOL,PARM=LOWVOL
         KEYWD HIGHVOL,PARM=HIGHVOL
         KEYWD IN,PARM=CUUIN
         KEYWD INCUU,PARM=CUUIN
         KEYWD LRECL,PARM=RECSIZE
         KEYWD LOWVOL,PARM=LOWVOL
         KEYWD OPT,PARM=OPT
         KEYWD OPTION,PARM=OPT
         KEYWD OUT,PARM=CUUOUT
         KEYWD OUTCUU,PARM=CUUOUT
         KEYWD RECSIZE,PARM=RECSIZE
         KEYWD RECORDS,PARM=RECORDS
         KEYWD RECS,PARM=RECORDS
         KEYWD TOVOL,PARM=HIGHVOL
         KEYWD UNIT,PARM=UNIT
         KEYWD VOLSER,PARM=LOWVOL
         DC    X'FFFF'
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              FUNCTION TABLE DSECT                                  *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  FUNCBLOK
         SPACE
*--------------------------------------------------------------------*
*                                                                    *
*              KEYWORD TABLE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
KEYWDSCT DSECT
KEYWDLEN DS    AL2                KEYWORD LENGTH
KEYWD    DS    CL8                KEYWORD
KEYWDPRM DS    CL8                INTERNAL PARM NAME
KEYWDPID DS    X                  KEYWORD PARAMETER ID
KEYWDDLN EQU   *-KEYWDSCT         TABLE ENTRY LENGTH
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCARD
./ ADD NAME=DITTCDI1
*--------------------------------------------------------------------*
*                                                                    *
*              CARD INPUT MODULE                                     *
*                                                                    *
*      THIS IS THE CARD-READER MODULE.  IT IS INVOKED BE EITHER      *
*      THE MAINLINE MODULE OR THE CARD-INPUT COMMAND MODULE.  BY     *
*      USING ONE MODULE TO READ THE SYSIN DATA WHETHER IT IS FOR     *
*      DATA OR COMMANDS, IT SIMPLIFIES THE LOGIC OF THE OTHER        *
*      MODULES.  THE CARD-INPUT COMMAND MODULE IS NOT COMPLICATED    *
*      WITH LOGIC TO DETERMINE IF IT IS TO FIND A COMMAND OR DATA    *
*      AND MAINLINE IS NOT COMPLICATED BY LOGIC TO SIGNAL A CARD     *
*      READER MODULE TO LOOK FOR COMMANDS OR FOR DATA.  THE CARD-    *
*      INPUT COMMAND MODULE SIGNALS THIS MODULE TO SEARCH FOR A      *
*      COMMAND BY LOADING A NON-ZERO VALUE IN REGISTER 1 BEFORE      *
*      LINKING TO THIS MODULE.  END-OF-FILE IS SIGNALED ON           *
*      REACHING END-OF-DATA,  WHEN THE NUMBER OF RECORDS TO PROCESS  *
*      IS REACHED, OR WHEN A NEW COMMAND IS ENCOUNTERD IN THE        *
*      INPUT STREAM.  THIS MODULE ASSUMES A RECORD LENGTH OF 80      *
*      FOR SYSIN.  COMMANDS ARE IDENTIFIED BY THE CHARACTERS         *
*      '$$DITTO' IN COLUMNS 1-7.   COLUMNS 73-80 ARE IGNORED ON      *
*      COMMANDS.                                                     *
*                                                                    *
*--------------------------------------------------------------------*
DITTCDI1 DITTPRFX CARDSAVE,'CARD READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   'CLOSE FILES' CALL?
         BO    CARD9900            YES... EXIT
         TM    COMMFLAG,$COMMEOJ   END OF JOB IN PROGRESS?
         BNO   CARD0010            NO
         TM    CARDFLAG,$CARDOPN   IS THE FILE OPEN?
         BNO   CARD9900            NO, EXIT
         DITTRACE ID=CLOSE         TRACE SYSIN CLOSE
         CLOSE SYSIN               CLOSE INPUT FILE
         NI    CARDFLAG,255-$CARDOPN FILE NOT OPEN
         B     CARD9900            EXIT
CARD0010 DS    0H
         TM    CARDFLAG,$CARDOPN   IS FILE OPEN
         BO    CARD0020            YES, BYPASS OPEN
         DITTRACE ID=OPEN          TRACE SYSIN OPEN
         OPEN  (SYSIN,INPUT)       OPEN SYSIN
         OI    CARDFLAG,$CARDOPN   FLAG OPEN
CARD0020 DS    0H
         TM    CARDFLAG,$CARDEOF   REAL EOF FOUND?
         BO    CARD8010            YES
         MVC   COMMCRCD,ACOMMIOI   INITIALIZE CURRENT ADDRESS
         XC    COMMCRCL,COMMCRCL   ZERO DATA LENGTH
         TM    COMMFLAG,$COMM1ST   FIRST TIME THROUGH?
         BNO   CARD0030            NO
         ZAP   CARDRCDS,COMMP0     INITIALIZE RECORD COUNTER
CARD0030 DS    0H
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         TM    CARDFLAG,$CARDNP    LAST CARD NOT PROCESSED?
         BO    CARD0040            NO
         DITTRACE ID=READ          TRACE SYSIN READ
         GET   SYSIN,CARDRECD      READ A CONTROL RECORD
         CLC   DITTABND,CARDRECD   ABEND REQUEST?
         BNE   CARD0040            NO
         DITTRACE ID=ABEND         TRACE JUST BEFORE ABENDING
         ABEND ABEND999,DUMP,,USER BLOW THIS THING
CARD0040 DS    0H
         NI    CARDFLAG,255-$CARDNP TURN OFF NOT PROCESSED FLAG
         C     R12,COMMINM         RUNNING AS A COMMAND?
         BE    CARD0100            YES.. READ DATA
         MVC   CARDM03D,CARDRECD   COPY CONTROL RECORD
         MVC   PRTDATA(CARDM03L),CARDM03
         BAL   R9,PRT0000          PRINT CONTROL STATEMENT
         CLC   CTLCARD,CARDRECD    IS THIS A CONTROL CARD?
         BNE   CARD0080            NO.. TEST FOR INCOMPLETE STATEMENT
         LA    R1,CARDRECD+8       POINT PAST CONTROL CARD ID
         L     R2,COMMCRCD         CURRENT DATA ADDRESS
         LH    R3,COMMCRCL         CURRENT LENGTH
         LA    R4,62               MAX DATA ON A STATEMENT
CARD0050 DS    0H
         CLI   0(R1),C' '          END OF STATEMENT?
         BE    CARD0060            YES
         CLC   CARDCONT,0(R1)      CONTINUATION ', ' ?
         BE    CARD0070            YES
         MVC   0(1,R2),0(R1)       MOVE DATA TO INPUT I/O AREA
         LA    R1,1(R1)            NEXT
         LA    R2,1(R2)            NEXT
         LA    R3,1(R3)            1 MORE IN LENGTH
         BCT   R4,CARD0050         LOOP
CARD0060 DS    0H
         DITTRACE ID=CMDEND,       TRACE END OF COMMAND STATEMENT      +
               RDATA1=R3,          .. CAPTURE COMMAND LENGTH           +
               DATA2=ACOMMIOI      .. CAPTURE SOME OF COMMAND
         NI    CARDFLAG,255-$CARDCTT   TURN OFF CONTINUATION FLAG
         STH   R3,COMMCRCL         SET DATA LENGTH
         MVC   COMMCRCD,ACOMMIOI   RESET DATA ADDRESS
         B     CARD9900            EXIT THIS MODULE
CARD0070 DS    0H
         DITTRACE ID=CONTINUE      TRACE CONTINUATION
         MVC   0(1,R2),0(R1)       MOVE DATA TO INPUT I/O AREA
         LA    R2,1(R2)            NEXT
         LA    R3,1(R3)            1 MORE IN LENGTH
         ST    R2,COMMCRCD         SAVE CURRENT ADDRESS
         STH   R3,COMMCRCL         SAVE CURRENT LENGTH
         OI    CARDFLAG,$CARDCTT   SET CONTINUATION FLAG
         B     CARD0030            READ NEXT RECORD
CARD0080 DS    0H
         TM    CARDFLAG,$CARDCTT   IMCOMPLETE COMMAND?
         BNO   CARD0090            NO
         MVC   PRTDATA(CARDM01L),CARDM01
         BAL   R9,PRT0000          PRINT MESSAGE
         NI    CARDFLAG,255-$CARDCTT   TURN OFF CONTINUATION FLAG
         B     CARD0020            READ UNTIL COMPLETE COMMAND
CARD0090 DS    0H
         MVC   PRTDATA(CARDM02L),CARDM02
         BAL   R9,PRT0000          PRINT MESSAGE
         B     CARD0020            READ UNTIL COMPLETE COMMAND
CARD0100 DS    0H
         CLC   CTLCARD,CARDRECD    IS THIS A CONTROL CARD?
         BE    CARD0130            YES
CARD0110 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD LIMIT GIVEN?
         BNO   CARD0120            NO
         CP    CARDRCDS,COMMRCDS   ENOUGH RECORDS PROCESSED?
         BE    CARD0140            YES
CARD0120 DS    0H
         LA    R1,CARDRECD         POINT PAST CONTROL CARD ID
         ST    R1,COMMCRCD         SET CURRENT RECORD ADDRESS
         MVC   COMMCRCL,H80        SET CURRENT RECORD LENGTH
         AP    CARDRCDS,COMMP1     ADD 1 TO RECORDS
         B     CARD9900            EXIT THIS MODULE
CARD0130 DS    0H
         OI    CARDFLAG,$CARDNP    LAST CARD NOT PROCESSED
CARD0140 DS    0H
         OI    COMMFLAG,$COMMEOF   SIGNAL PSEUDO EOF
         B     CARD9900            EXIT
CARD8000 DS    0H
         OI    CARDFLAG,$CARDEOF   SIGNAL REAL EOF
         TM    CARDFLAG,$CARDCTT   INCOMPLETE COMMAND AT EOF?
         BNO   CARD8010            NO
         MVC   PRTDATA(CARDM04L),CARDM04
         BAL   R9,PRT0000          PRINT MESSAGE
CARD8010 DS    0H
         DITTRACE ID=EOF           TRACE SYSIN END OF FILE
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
         B     CARD9900            AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              MESSAGE PRINTING                                      *
*                                                                    *
*--------------------------------------------------------------------*
PRT0000  DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         BR    R9                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
CARD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
CARDSAVE DC    18F'0'              REGISTER SAVE AREA
H80      DC    H'80'               CONSTANT
CARDFLAG DC    X'00'               SWITCHES/FLAGS
$CARDOPN EQU   X'80'               .. FILE OPEN INDICATOR
$CARDNP  EQU   X'40'               .. LAST CARD NOT PROCESSED
$CARDEOF EQU   X'20'               .. END OF FILE INDICATOR
$CARDCTT EQU   X'10'               .. COMMAND CONTINUATION
CTLCARD  DC    C'$$DITTO'          CONTROL CARD IDENTIFIER
DITTABND DC    C'$$ABEND'          ABEND REQUEST
CARDCONT DC    C', '               CONTINUATION
CARDM01  DC    C'** INCOMPLETE COMMAND, SKIPPING UNTIL COMPLETE COMMAND+
                STATEMENT'
CARDM01L EQU   *-CARDM01
CARDM02  DC    C'** INVALID DITTO CONTROL STATEMENT OR MISSPLACED SYSIN+
                DATA'
CARDM02L EQU   *-CARDM02
CARDM03  DC    C'CONTROL STATEMENT: '
CARDM03D DC    CL72' '
CARDM03L EQU   *-CARDM03
CARDM04  DC    C'** LAST CONTROL STATEMENT IS AN INCOMPLETE COMMAND'
CARDM04L EQU   *-CARDM04
CARDRCDS DC    PL5'0'              RECORD COUNTER
CARDRECD DC    CL80' '             SYSIN I/O AREA
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              READER DCB                                            *
*                                                                    *
*--------------------------------------------------------------------*
SYSIN    DCB   DDNAME=SYSIN,                                           +
               EODAD=CARD8000,                                         +
               DSORG=PS,                                               +
               MACRF=GM,                                               +
               RECFM=FB,                                               +
               LRECL=80
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         ABCODES
         SPACE 2
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCDI1
./ ADD NAME=DITTCDO1
*--------------------------------------------------------------------*
*                                                                    *
*              CARD OUTPUT MODULE                                    *
*                                                                    *
*      THIS IS THE CARD PUNCH MODULE.  A RECORD LENGTH OF 80         *
*      IS ASSUMED FOR THE CARD-PUNCH.  IF A RECORD IS LESS THAN      *
*      80 CHARACTERS, THE CARD WILL BE PADDED TO THE RIGHT WITH      *
*      BLANKS.  IF A RECORD IS MORE THAN 80 BYTES, IT WILL BE        *
*      TRUNCATED AT 80.  VARIABLE LENGTH RECORDS WILL HAVE THE       *
*      LENGTH DATA (THE FIRST FOUR BYTES) REMOVED.                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCDO1 DITTPRFX CARDSAVE,'CARD PUNCH MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   'CLOSE FILES' CALL?
         BO    CARD9900            YES.. EXIT
         TM    COMMFLAG,$COMMEOJ   END OF JOB IN PROGRESS??
         BNO   CARD0010            NO
         TM    CARDFLAG,$CARDOPN   IS THE FILE OPEN??
         BNO   CARD9900            NO, EXIT
         DITTRACE ID=CLOSE         TRACE SYSPUNCH CLOSE
         CLOSE SYSPUNCH            CLOSE PUNCH FILE
         NI    CARDFLAG,255-$CARDOPN FILE NOT OPEN
         B     CARD9900            EXIT
CARD0010 DS    0H
         TM    CARDFLAG,$CARDOPN   IS FILE OPEN
         BO    CARD0020            YES, BYPASS OPEN
         DITTRACE ID=OPEN          TRACE SYSPUNCH OPEN
         OPEN  (SYSPUNCH,OUTPUT)   OPEN SYSPUNCH
         OI    CARDFLAG,$CARDOPN   FLAG OPEN
CARD0020 DS    0H
         L     R3,ACOMMIOO         OUTPUT BUFFER ADDRESS
         MVC   0(80,R3),COMMBLKS   INITIALIZE I/O AREA
         LH    R1,COMMCRCL         LOAD CURRENT RECORD LENGTH
         CH    R1,H80              TOO LONG??
         BNH   CARD0030            NO
         LH    R1,H80              LIMIT TO 80 BYTES
CARD0030 DS    0H
         L     R2,COMMCRCD         CURRENT RECORD ADDRESS
         EX    R1,CARDOMVC         MOVE DATA TO OUTPUT
         PUT   SYSPUNCH,0(R3)      PUNCH THE CARD
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
CARD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              EXECUTED INSTRUCTIONS                                 *
*                                                                    *
*--------------------------------------------------------------------*
CARDOMVC MVC   0(0,R3),0(R2)       MOVE DATA TO OUTPUT AREA
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
CARDSAVE DC    18F'0'              REGISTER SAVE AREA
H80      DC    H'80'               CONSTANT
CARDFLAG DC    X'00'               SWITCHES/FLAGS
$CARDOPN EQU   X'80'               .. FILE OPEN INDICATOR
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              READER DCB                                            *
*                                                                    *
*--------------------------------------------------------------------*
SYSPUNCH    DCB   DDNAME=SYSPUNCH,                                     +
               DSORG=PS,                                               +
               MACRF=PM,                                               +
               RECFM=FB,                                               +
               LRECL=80
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         SPACE 2
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCDO1
./ ADD NAME=DITTCMD
* ------------------------------------------------------------------- *
*                                                                     *
*            FUNCTION DEFINITION TABLE                                *
*                                                                     *
* ------------------------------------------------------------------- *
         PARMGEN GEN=NO
DITTCMD  CSECT
         DITTFUNC ACQ,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='ACQUIRE A TAPE OR DASD DEVICE',                   +
               EXAMPLE1='ACQ,UNIT=CUU <,DENSITY=6250>',                +
               EXAMPLE2='ACQ,CUU <,6250>',                             +
               REQPARM=(UNIT),                                         +
               OPTPARM=(DENSITY),                                      +
               IN1=ALOC,                                               +
               IN2=ALOC
         DITTFUNC BSF,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='BACK SPACE FILE (TAPE ONLY)',                     +
               EXAMPLE1='BSF,IN=488,FILES=1',                          +
               EXAMPLE2='BSF,CUU,# OF FILES',                          +
               REQPARM=(CUUIN,FILES),                                  +
               IN1=TPI4,                                               +
               IN2=TPI4
         DITTFUNC BSR,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='BACK SPACE RECORDS (TAPE ONLY)',                  +
               EXAMPLE1='BSR,IN=488,RECORDS=1',                        +
               EXAMPLE2='BSR,CUU,# OF RECORDS',                        +
               REQPARM=(CUUIN,RECORDS),                                +
               IN1=TPI3,                                               +
               IN2=TPI3
         DITTFUNC CC,                                                  +
               ENV=(JOB),                                              +
               DESC='CARD TO CARD (CARD IN, CARD PUNCH OUT)',          +
               EXAMPLE1='CC <,RECORDS=100>',                           +
               OPTPARM=(RECORDS),                                      +
               IN1=CDI1,                                               +
               OUT1=CDO1
         DITTFUNC CD,                                                  +
               ENV=(JOB),                                              +
               DESC='CARD TO PRINT IN VERTICAL HEX',                   +
               EXAMPLE1='CD <,RECORDS=100>',                           +
               OPTPARM=(RECORDS),                                      +
               IN1=CDI1,                                               +
               OUT1=PRO1,                                              +
               OFLAGS=(HEX)
         DITTFUNC CP,                                                  +
               ENV=(JOB),                                              +
               DESC='CARD TO PRINT (CHARACTER ONLY)',                  +
               EXAMPLE1='CP <,RECORDS=100>',                           +
               OPTPARM=(RECORDS),                                      +
               IN1=CDI1,                                               +
               OUT1=PRO1
         DITTFUNC DD,                                                  +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT (VERTICAL HEX)',                    +
               REQPARM=(CUUIN,DISKADDR,RECORDS),                       +
               EXAMPLE1='DD,IN=1C8,DISKADDR=000100001,RECORDS=10',     +
               EXAMPLE2='DD,CUU,CCCCHHRRR,# OF RECORDS',               +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDD,                                           +
               OFLAGS=(HEX)
         DITTFUNC DP,                                                  +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT (CHARACTER ONLY)',                  +
               REQPARM=(CUUIN,DISKADDR,RECORDS),                       +
               EXAMPLE1='DP,IN=1C8,DISKADDR=000100001,RECORDS=10',     +
               EXAMPLE2='DP,CUU,CCCCHHRRR,# OF RECORDS',               +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDP
         DITTFUNC DDD,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT DEBLOCKED (VERTICAL HEX)',          +
               REQPARM=(CUUIN,DISKADDR,RECSIZE,RECORDS),               +
               EXAMPLE1='DDD,IN=1C8,DISKADDR=000100001,RECSIZE=80,RECOR+
               DS=50',                                                 +
               EXAMPLE2='DDD,1C8,CCCCHHRRR,RECSIZE,# OF RECORDS',      +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               IFLAGS=(DEBLOCK),                                       +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDDD,                                          +
               OFLAGS=(HEX)
         DITTFUNC DDDV,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT DEBLOCKED (VERTICAL HEX,RECFM=V)',  +
               REQPARM=(CUUIN,DISKADDR,RECORDS),                       +
               EXAMPLE1='DDDV,IN=1C8,DISKADDR=000100001,RECORDS=50',   +
               EXAMPLE2='DDDV,1C8,CCCCHHRRR,# OF RECORDS',             +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               IFLAGS=(DEBLOCK,VREC),                                  +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDDDV,                                         +
               OFLAGS=(HEX)
         DITTFUNC DPD,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT DEBLOCKED (CHARACTER ONLY)',        +
               REQPARM=(CUUIN,DISKADDR,RECSIZE,RECORDS),               +
               EXAMPLE1='DPD,IN=1C8,DISKADDR=000100001,RECSIZE=80,RECOR+
               DS=50',                                                 +
               EXAMPLE2='DPD,CUU,CCCCHHRRR,RECSIZE,# OF RECORDS',      +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               IFLAGS=(DEBLOCK),                                       +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDPD
         DITTFUNC DPDV,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='DISK TO PRINT DEBLOCKED (CHARACTER ONLY,RECFM=V)',+
               REQPARM=(CUUIN,DISKADDR,RECORDS),                       +
               EXAMPLE1='DPDV,IN=1C8,DISKADDR=000100001,RECORDS=50',   +
               EXAMPLE2='DPDV,CUU,CCCCHHRRR,# OF RECORDS',             +
               IN1=DAI1,                                               +
               IN2=DAI1,                                               +
               IFLAGS=(DEBLOCK,VREC),                                  +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTDPDV
         DITTFUNC FSF,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='FORWARD SPACE FILE (TAPE ONLY)',                  +
               EXAMPLE1='FSF,IN=488,FILES=1',                          +
               EXAMPLE2='FSF,CUU,# OF FILES',                          +
               REQPARM=(CUUIN,FILES),                                  +
               IN1=TPI2,                                               +
               IN2=TPI2
         DITTFUNC FSR,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='FORWARD SPACE RECORDS (TAPE ONLY)',               +
               EXAMPLE1='FSR,IN=488,RECORDS=1',                        +
               EXAMPLE2='FSR,CUU,# OF RECORDS',                        +
               REQPARM=(CUUIN,RECORDS),                                +
               IN1=TPI1,                                               +
               IN2=TPI1
         DITTFUNC INT,                                                 +
               ENV=(JOB,STC),                                          +
               DESC='INITIALIZE TAPE WITH STANDARD VOLSER',            +
               EXAMPLE1='INT,IN=488,VOLSER=000001 <,HIGHVOL=000999>',  +
               EXAMPLE2='INT,CUU,LOWVOL <,HIGHVOL>',                   +
               REQPARM=(CUUIN,LOWVOL),                                 +
               OPTPARM=(HIGHVOL),                                      +
               IN1=TINT
         DITTFUNC INQ,                                                 +
               ENV=(STC,TSO),                                          +
               DESC='INQUIRE ON DITTO STATUS (ALLOCATED UNITS, TRACE)',+
               EXAMPLE2='INQ,TAPE OR INQ,DASD OR INQ,TRACE',           +
               REQPARM=(INQQUAL),                                      +
               IN1=INQ,                                                +
               IN2=TSO3,                                               +
               PANEL=DITTINQ
         DITTFUNC REL,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='RELEASE A DEVICE',                                +
               EXAMPLE1='REL,UNIT=488',                                +
               EXAMPLE2='REL,CUU',                                     +
               REQPARM=(UNIT),                                         +
               OUT1=ALOC,                                              +
               OUT2=ALOC
         DITTFUNC REW,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='REWIND TAPE',                                     +
               EXAMPLE1='REW,OUT=488',                                 +
               EXAMPLE2='REW,CUU',                                     +
               REQPARM=(CUUOUT),                                       +
               OUT1=TPO2,                                              +
               OUT2=TPO2
         DITTFUNC RUN,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='REWIND AND UNLOAD TAPE',                          +
               EXAMPLE1='RUN,OUT=488',                                 +
               EXAMPLE2='RUN,CUU',                                     +
               REQPARM=(CUUOUT),                                       +
               OUT1=TPO3,                                              +
               OUT2=TPO3
         DITTFUNC TC,                                                  +
               ENV=(JOB),                                              +
               DESC='TAPE TO CARDS',                                   +
               REQPARM=(CUUIN),                                        +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TC,IN=488 <,RECORDS=10>',                     +
               IN1=TPI1,                                               +
               OUT1=CDO1
         DITTFUNC TD,                                                  +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT IN VERTICAL HEX',                   +
               REQPARM=(CUUIN),                                        +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TD,IN=488 <,RECORDS=10>',                     +
               EXAMPLE2='TD,CUU <,# OF RECORDS>',                      +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               OFLAGS=(HEX),                                           +
               PANEL=DITTTD
         DITTFUNC TDD,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT DEBLOCKED IN VERTICAL HEX',         +
               REQPARM=(CUUIN,RECSIZE),                                +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TDD,IN=488,RECSIZE=80 <,RECORDS=10>',         +
               EXAMPLE2='TDD,CUU,RECSIZE <,# OF RECORDS>',             +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK),                                       +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               OFLAGS=(HEX),                                           +
               PANEL=DITTTDD
         DITTFUNC TDDV,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT DEBLOCKED VARIABLE LENGTH RECORDS IN+
                VERTICAL HEX',                                         +
               REQPARM=(CUUIN),                                        +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TDDV,IN=488 <,RECORDS=10>',                   +
               EXAMPLE2='TDDV,CUU <,# OF RECORDS>',                    +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK,VREC),                                  +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               OFLAGS=(HEX),                                           +
               PANEL=DITTTDDV
         DITTFUNC TP,                                                  +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT (CHARACTER ONLY)',                  +
               REQPARM=(CUUIN),                                        +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TP,IN=488 <,RECORDS=10>',                     +
               EXAMPLE2='TP,CUU <,# OF RECORDS>',                      +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTTP
         DITTFUNC TPD,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT DEBLOCKED (CHARACTER ONLY)',        +
               REQPARM=(CUUIN,RECSIZE),                                +
               OPTPARM=(RECORDS),                                      +
               EXAMPLE1='TPD,IN=488,RECSIZE=80 <,RECORDS=10>',         +
               EXAMPLE2='TPD,CUU,RECSIZE <,# OF RECORDS>',             +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK),                                       +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTTPD
         DITTFUNC TPDV,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO PRINT DEBLOCKED VARIABLE LENGTH RECORDS (C+
               HARACTER ONLY)',                                        +
               REQPARM=(CUUIN),                                        +
               EXAMPLE1='TPDV,IN=488, <,RECORDS=10>',                  +
               EXAMPLE2='TPDV,CUU, <,# OF RECORDS>',                   +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK,VREC),                                  +
               OUT1=PRO1,                                              +
               OUT2=TSO1,                                              +
               PANEL=DITTTPDV
         DITTFUNC TMAP,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='PRINT TAPE VTOC',                                 +
               EXAMPLE1='TMAP,IN=488 <,OPTION=FULLTAPE>',              +
               EXAMPLE2='TMAP,CUU <,FULLTAPE>',                        +
               REQPARM=(CUUIN),                                        +
               OPTPARM=(OPT),                                          +
               IN1=TMAP,                                               +
               IN2=TSO2,                                               +
               PANEL=DITTTMAP,                                         +
               OPTVAL=(FULL,FULLTAPE,ALL)
         DITTFUNC TT,                                                  +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO TAPE COPY',                               +
               REQPARM=(CUUIN,CUUOUT,FILES),                           +
               EXAMPLE1='TT,IN=488,OUT=48A,FILES=2',                   +
               EXAMPLE2='TT,INCUU,OUTCUU,# OF FILES',                  +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               OUT1=TPO1,                                              +
               OUT2=TPO1
         DITTFUNC TTR,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO TAPE COPY REBLOCKED',                     +
               REQPARM=(CUUIN,CUUOUT,RECSIZE,BLKSIZE,FILES),           +
               EXAMPLE1='TTR,IN=488,OUT=48A,RECSIZE=80,BLKSIZE=3120,FIL+
               ES=3',                                                  +
               EXAMPLE2='TTR,CUUIN,CUUOUT,RECSIZE,BLKSIZE,# OF FILES', +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK),                                       +
               OUT1=TPO1,                                              +
               OUT2=TPO1,                                              +
               OFLAGS=(REBLOCK)
         DITTFUNC TTRV,                                                +
               ENV=(JOB,STC,TSO),                                      +
               DESC='TAPE TO TAPE COPY VARIABLE LENGTH RECORDS REBLOCKE+
               D',                                                     +
               REQPARM=(CUUIN,CUUOUT,BLKSIZE,FILES),                   +
               EXAMPLE1='TTRV,IN=488,OUT=48A,BLKSIZE=12000,FILES=3',   +
               EXAMPLE2='TTRV,CUUIN,CUUOUT,BLKSIZE,# OF FILES',        +
               IN1=TPI1,                                               +
               IN2=TPI1,                                               +
               IFLAGS=(DEBLOCK,VREC),                                  +
               OUT1=TPO1,                                              +
               OUT2=TPO1,                                              +
               OFLAGS=(REBLOCK)
         DITTFUNC WTM,                                                 +
               ENV=(JOB,STC,TSO),                                      +
               DESC='WRITE TAPE MARKS',                                +
               EXAMPLE1='WTM,OUT=488 <,COUNT=5>',                      +
               EXAMPLE2='WTM,CUU <,# OF TAPE MARKS>',                  +
               REQPARM=(CUUOUT),                                       +
               OPTPARM=(RECORDS),                                      +
               OUT1=TPO4,                                              +
               OUT2=TPO4
         DITTFUNC XXX,                                                 +
               ENV=(JOB,STC),                                          +
               DESC='LIST AVAILABLE FUNCTIONS',                        +
               REQPARM=,                                               +
               OPTPARM=,                                               +
               EXAMPLE1='XXX',                                         +
               EXAMPLE2='XXX'
         DC    X'FFFF'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CARD-INPUT TABLE DSECT                                *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  FUNCBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*               COMMON MODULE MAP                                    *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         COPY REGEQU
         END  DITTCMD
./ ADD NAME=DITTCOMM
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE                                         *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM CSECT
         DITTCOMM
         END
./ ADD NAME=DITTCONS
*--------------------------------------------------------------------*
*                                                                    *
*           CONSOLE COMMAND INPUT MODULE                             *
*                                                                    *
*      THIS MODULE PROCESSES CONSOLE-INPUT COMMANDS.  THIS MODULE    *
*      IS CALLED BY 'DITTMAIN' WHEN DITTO IS RUNNING AS A STARTED    *
*      TASK (NOT A JOB).                                             *
*                                                                    *
*      SINCE THE SAME PARAMETERS WILL BE REQUIRED BY CARD-INPUT      *
*      OR CONSOLE-INPUT, THE PARAMETER CONVERSION IS PERFORMED       *
*      BY A SEPARATE MODULE THAT IS CALLED BY BOTH INPUT COMMAND     *
*      MODULES.  THIS WAS TO ELIMINATE AS MUCH DUPLICATION OF        *
*      CODE AS POSSIBLE.  ALSO IF A NEW PARAMETER IS REQUIRED,       *
*      ONLY ONE MODULE USUALLY NEEDS TO BE CHANGED. THE PARAMETER    *
*      CONVERSION MODULE IS 'DITTPARM'.                              *
*                                                                    *
*      IF THE COMMAND IS UNDEFINED OR ALL PARAMETERS ARE NOT         *
*      ENTERED OR ARE IN ERROR, WTO'S WILL BE ISSUED IDENTIFYING     *
*      THE INVALID COMMAND OR PARAMETER, AND THE OPERATOR WILL       *
*      BE PROMPTED FOR MISSING OR INCORRECT INFORMATION.             *
*                                                                    *
*--------------------------------------------------------------------*
DITTCONS DITTPRFX CONSSAVE,'CONSOLE COMMAND MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
CONS0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE NEW COMMAND READS
         XC    COMMCECB,COMMCECB   CLEAR CONSOLE ECB
         MVC   CONSAREA,COMMBLKS   INITIALIZE REPLY AREA
         LA    R3,COMMCECB         ECB ADDRESS
         WTOR  'ENTER A DITTO COMMAND',  MESSAGE TEXT FOR CONSOLE      +
               CONSAREA,           .. REPLY AREA                       +
               80,                 .. LENGTH OF REPLY AREA             +
               (R3),               .. CONSOLE ECB                      +
               CONSID=COMMCID      .. CONSOLE ID
         WAIT  1,ECB=(R3)          WAIT FOR OPERATOR
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,7,CONSMID        MESSAGE ID FROM LAST WTO
         BZ    CONS0020            NO OUTSTANDING MESSAGE
         DOM   MSG=(R1)            DELETE THE MESSAGE
         XC    CONSMID,CONSMID     CLEAR THE ID
CONS0020 DS    0H
         CLC   CONSEOJ,CONSAREA    'EOJ' COMMAMND?
         BNE   CONS0030            NO
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
         B     CONS9900            EXIT
CONS0030 DS    0H
         LA    R3,CONSAREA         WTOR REPLY AREA
         LA    R4,80               MAX LENGTH OF REPLY
         LA    R1,0(R3,R4)         END OF I/O AREA +1
         BCTR  R1,0                END OF I/O AREA
CONS0050 DS    0H
         CLI   0(R3),C' '          AT LEAST A BLANK?
         BNE   CONS0060            YES
         LA    R3,1(R3)            TRY NEXT ONE
         BCT   R4,CONS0050         LOOP
         B     CONS0010            NOTHING FOUND ISSUE MSG AGAIN
CONS0060 DS    0H
         CLI   0(R1),C' '          AT LEAST A BLANK?
         BH    CONS0070            YES
         BCTR  R1,0                BACK UP
         BCT   R4,CONS0060         LOOP BACK
         B     CONS0010            NOTHING FOUND ISSUE MSG AGAIN
CONS0070 DS    0H
         MVC   PRTDATA(CONSM01L),CONSM01
         MVC   PRTDATA+8(80),CONSAREA MOVE CONSOLE REPLY
         MVI   PRTCMMD,$PRTCMD     MOVE PRINT COMMAND
         BAL   R9,PRT0000          PRINT
         CLC   CONSXTR,CONSAREA    'TRACE' COMMAND?
         BE    CONS2000            YES
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        FIND COMMAND ON INPUT RECORD
         L     R5,ACMD             COMMAND TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=CONSWORK      .. CAPTURE THE COMMAND
CONS0090 DS    0H
         DITTRACE ID=SCANCMD       TRACE COMMAND TABLE SCAN
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CONSERR1            YES, INVALID COMMAND
         TM    FUNCENV,$FUNCSTC    DOES THIS COMMAND APPLY?
         BNO   CONS0100            NO
         LH    R1,CONSWLEN         GET LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH?
         BNE   CONS0100            NO
         BCTR  R1,0                ADJUST FOR EXECUTE
         EX    R1,CONSFCLC         CHECK FUNCTION CODE
         BE    CONS0110            COMMAND LOCATED
CONS0100 DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     CONS0090            LOOP
CONS0110 DS    0H
         DITTRACE ID=CMMDFND       TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED?
         BE    CONS0500            YES
         CLC   FUNCCMD,LISTFNC     XXX FUNCTION?
         BE    CONS1000            YES
         SR    R6,R6               CLEAR FOR NUMBER OF REQ PARMS
         ICM   R6,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    CONS0150            NONE ... TRY FOR OPTIONAL PARMS
         LA    R7,FUNCREQ          FIRST REQUIRED PARAMETER
CONS0120 DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN REPLY?
         BZ    CONS0300            START PROMPTING OPERATOR FOR THEM
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        GET PARAMETER
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
CONS0130 DS    0H
         MVC   PARMPARM,0(R7)      PASS FIELD ID
         MVC   PARMWORK,CONSWORK   PASS DATA
         MVC   PARMWLEN,CONSWLEN   PASS DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY PONT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    CONS0600            YES... ABORT THIS COMMAND
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BO    CONS0140            YES
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,0(R1)         TEXT LENGTH
         BCTR  R15,0               FOR EXECUTE
         EX    R15,PRTMMVC         MOVE TEXT TO PRINT I/O AREA
         MVC   CONSWTO+4(80),COMMBLKS
         EX    R15,WTOMMVC         MOVE MESSAGE
         MVI   PRTCMMD,$PRTCMD
         BAL   R9,PRT0000          PRINT MESSAGE
         CLC   PARMPARM,HIVOLID    WAS THIS 'HIGH VOLSER' OF A RANGE?
         BE    CONS0610            YES.. FORCE OPERATOR TO RE-ENTER
         LA    R1,CONSWTO          POINT TO OUR WTO
         ST    R1,PARMWTO          PASS ADDRESS BACK TO DITTPARM
         MVI   PARMCMMD,$PARMEWTO  INDICATE ERROR MSG WTO AND PROMPT
         B     CONS0130            RE-DRIVE PARAMETER MODULE
CONS0140 DS    0H
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,CONS0120         PROCESS NEXT PARAMETER
*---------------------------------------------------------------------*
*        ALL REQUIRED PARAMETERS HAVE BEEN PROCESSED.                 *
*        NOW PROCESS ANY OPTIONAL PARAMETERS.                         *
*---------------------------------------------------------------------*
CONS0150 DS    0H
         SR    R6,R6               CLEAR FOR NUMBER OF OPTIONAL PARMS
         ICM   R6,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    CONS0400            NONE ... ALL DONE
         LA    R7,FUNCOPT          FIRST OPTIONAL PARAMETER
CONS0160 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN REPLY?
         BZ    CONS0400            NO
         MVC   CONSWORK,COMMBLKS   INITIALIZE WORK AREA
         BAL   R14,CONS7000        GET PARAMETER
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
CONS0170 DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,CONSWORK   DATA
         MVC   PARMWLEN,CONSWLEN   DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUEST 'CANCEL'?
         BO    CONS0190            YES... OPTIONAL PARAMETER NOT USED
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE?
         BO    CONS0180            YES
         L     R1,PARMMSGA         RETURNED MESSAGE ADDRESS
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,0(R1)         TEXT LENGTH
         BCTR  R15,0               FOR EXECUTE
         EX    R15,PRTMMVC         MOVE MESSAGE
         MVC   CONSWTO+4(80),COMMBLKS
         EX    R15,WTOMMVC         MOVE MESSAGE
         MVI   PRTCMMD,$PRTCMD
         BAL   R9,PRT0000          PRINT MESSAGE
         CLC   PARMPARM,HIVOLID    WAS THIS 'HIGH VOLSER' OF A RANGE?
         BE    CONS0610            YES.. FORCE OPERATOR TO RE-ENTER
         LA    R1,CONSWTO          POINT TO OUR WTO
         ST    R1,PARMWTO          PASS ADDRESS BACK TO DITTPARM
         MVI   PARMCMMD,$PARMEWTO  INDICATE ERROR MSG WTO AND PROMPT
         B     CONS0170            RE-DRIVE PARAMETER MODULE
CONS0180 DS    0H
         LA    R7,1(R7)            NEXT OPTIONAL PARAMETER
         BCT   R6,CONS0160         PROCESS NEXT PARAMETER
         B     CONS0400            ALL OPTIONAL PARAMETERS DONE
CONS0190 DS    0H
         WTO   'OPTIONAL PARAMETER REMOVED FROM CURRENT COMMAND',      +
               CONSID=COMMCID      CONSOLE ID
         B     CONS0180            CONTINUE WITH NEXT OPTIONAL PARM
         SPACE 2
*---------------------------------------------------------------------*
*        OPERATOR COMMAND EXHAUSTED.  WE STILL NEED SOME REQUIRED     *
*        PARAMETERS.  START PROMPTING OPERATOR FOR THEM.              *
*---------------------------------------------------------------------*
CONS0300 DS    0H
         DITTRACE ID=MISSING       ENTRY INTO MISSING PARM PROMPTS
CONS0310 DS    0H
         DITTRACE ID=PROMPT3       TRACE EACH ITERATION
         MVI   PARMCMMD,$PARMWTO   INDICATE PROMPT FOR PARAMETER
CONS0320 DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,CONSWORK   DATA
         MVC   PARMWLEN,CONSWLEN   DATA LENGTH
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMCAN   OPERATOR REQUET 'CANCEL'?
         BO    CONS0600            YES... ABORT THIS COMMAND
         TM    PARMFLAG,$PARMOK    PARAMETER OK?
         BO    CONS0330            YES
         DITTRACE ID=BADPARM3      TRACE INVALID PARAMETER
         MVI   PARMCMMD,$PARMEWTO  REQUEST ERROR MSG WTO AND PROMPT
         B     CONS0320            TRY AGAIN
CONS0330 DS    0H
         DITTRACE ID=GOODPRM3      TRACE GOOD PARAMETER
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,CONS0310         PROCESS NEXT PARAMETER
*---------------------------------------------------------------------*
*        ALL PARAMETERS PROCESSED.  PASS INPUT AND OUTPUT MODULE      *
*        ADDRESSES AND FLAGS.                                         *
*---------------------------------------------------------------------*
CONS0400 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN1        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    CONS0410            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
CONS0410 DS    0H
         ICM   R1,3,FUNCOUT1       OUTPUT MODULE ADDRESS DISPLACEMENT
         BZ    CONS9900            NO OUTPUT MODULE
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         B     CONS9900            EXIT
*---------------------------------------------------------------------*
*        ISSUE EXAMPLE WTO FOR OPERATOR.                              *
*---------------------------------------------------------------------*
CONS0500 DS    0H
         DITTRACE ID=EXAMPLE       TRACE POINT
         MVC   CONSWTO+4(80),FUNCEX2
         WTO   MF=(E,CONSWTO),     ISSUE WTO OF COMMAND EXAMPLE        +
               CONSID=COMMCID      CONSOLE ID
         STCM  R1,7,CONSMID        SAVE WTO MESSAGE ID FOR DOM
         B     CONS0010            REQUEST ANOTHER COMMAND
*---------------------------------------------------------------------*
*        COMMAND CANCELLED BY OPERATOR                                *
*---------------------------------------------------------------------*
CONS0600 DS    0H
         DITTRACE ID=CANCEL        TRACE POINT
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(CONSM04L),CONSM04
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         B     CONS0620            INFORM OPERATOR
CONS0610 DS    0H
         DITTRACE ID=RANGERR       TRACE POINT
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(CONSM05L),CONSM05
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         WTO   CONSID=COMMCID,     ISSUE WTO FOR REASON                +
               MF=(E,CONSWTO)
CONS0620 DS    0H
         WTO   'CURRENT COMMAND CANCELLED',                            +
               CONSID=COMMCID      CONSOLE ID
         B     CONS0010            REQUEST ANOTHER COMMAND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              LIST AVAILABLE COMMANDS                               *
*                                                                    *
*--------------------------------------------------------------------*
CONS1000 DS    0H
         DITTRACE ID=LISTCMDS      TRACE START OF LIST
         L     R5,ACMD             FUNCTIONS TABLE ADDRESS
         BAL   R9,PRT0000          LINK TO PRINT MODULE
CONS1010 DS    0H
         DITTRACE ID=LISTFUNC      TRACE EACH FUNCTION
         CLI   FUNCELEN,X'FF'      END OF TABLE?
         BE    CONS0010            YES
         TM    FUNCENV,$FUNCSTC    DOES THIS COMMAND APPLY?
         BNO   CONS1020            NO
         MVC   CONSM01C,FUNCCMD    MOVE COMMAND
         MVC   CONSM01D,FUNCDESC   MOVE DESCRIPTION
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         MVC   PRTDATA(CONSM01L),CONSM01
         MVI   PRTCC,C'0'          DOUBLE SPACE CARRIAGE CONTROL
         MVI   PRTCMMD,$PRTDATA    SET COMMAND
         BAL   R9,PRT0000          LINK TO PRINT MODULE
         MVC   CONSM02C,FUNCEX2    MOVE EXAMPLE OF COMMAND
         MVC   PRTDATA(CONSM02L),CONSM02
         MVI   PRTCC,C' '          SINGLE SPACE CARRIAGE CONTROL
         BAL   R9,PRT0000          LINK TO PRINT MODULE
CONS1020 DS    0H
         AH    R5,FUNCELEN         NEXT FUNCTION
         B     CONS1010            LIST NEXT FUNCTION
*--------------------------------------------------------------------*
*                                                                    *
*              EXTERNAL TRACE CONTROL                                *
*                                                                    *
*--------------------------------------------------------------------*
CONS2000 DS    0H
         DITTRACE ID=XTRACE
         CLC   CONSON,CONSAREA+6   TRACE ON?
         BE    CONS2010            YES
         CLC   CONSOFF,CONSAREA+6  TRACE OFF?
         BE    CONS2030            YES
         WTO   'INVALID EXTERNAL TRACE COMMAND (USE ''TRACE ON'' OR ''T+
               RACE OFF'')',                                           +
               CONSID=COMMCID
         MVC   PRTDATA(CONSM06L),CONSM06
         BAL   R9,PRT0000          PRINT MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2010 DS    0H
         DITTRACE ID=TRACEON
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    CONS2020            YES
         OI    XTRFLAG,$XTRACE     SET EXTERNAL TRACE FLAG
         WTO   'EXTERNAL TRACE ACTIVATED',                             +
               CONSID=COMMCID
         MVC   PRTDATA(CONSM03L),CONSM03
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2020 DS    0H
         DITTRACE ID=DUPON
         WTO   'EXTERNAL TRACE ALREADY ACTIVE',                        +
               CONSID=COMMCID
         MVC   PRTDATA(CONSM07L),CONSM07
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2030 DS    0H
         TM    XTRFLAG,$XTRACE     EXTERNAL TRACE FLAG ON?
         BNO   CONS2040            NO
         DITTRACE ID=TRACEOFF
         NI    XTRFLAG,255-$XTRACE TURN OFF FLAG
         WTO   'EXTERNAL TRACE DE-ACTIVATED',                          +
               CONSID=COMMCID
         MVC   PRTDATA(CONSM08L),CONSM08
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
CONS2040 DS    0H
         DITTRACE ID=DUPOFF
         WTO   'EXTERNAL TRACE NOT ACTIVE',                            +
               CONSID=COMMCID
         MVC   PRTDATA(CONSM09L),CONSM09
         BAL   R9,PRT0000          PRINT THE MESSAGE
         B     CONS0010            REQUEST ANOTHER COMMAND
*--------------------------------------------------------------------*
*                                                                    *
*              INPUT PARSER                                          *
*         'CONSWORK' SHOULD BE INITIALIZED BEFORE INVOKING           *
*         R1, R2, R15 ARE WORK REGISTERS                             *
*         R3 HAS CURRENT INPUT ADDRESS                               *
*         R4 LENGTH REMAINING ON INPUT                               *
*         R14 IS RETURN ADDRESS                                      *
*                                                                    *
*         ON EXIT 'CONSWORK' CONTAINS DATA FOUND TO NEXT DELIMITER,  *
*         UPTO 8 BYTES.  'CONSWLEN' CONTAINS ITS LENGTH.             *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL          *
*         REFLECT LENGTH LEFT FOLLOWING THE DELIMITER.               *
*                                                                    *
*--------------------------------------------------------------------*
CONS7000 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD?
         BZR   R14                 NO.. EXIT
         XC    CONSWORK,CONSWORK   CLEAR WORK AREA
         LA    R1,CONSWORK         POINT TO WORK AREA
         SR    R2,R2               CLEAR LENGTH COUNTER
         LA    R15,44              MAXIMUM LENGTH
CONS7010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BE    CONS7030            YES
         CLI   0(R3),C','          COMMA?
         BE    CONS7030            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,CONS7020         DECREMENT COUNT
         STH   R2,CONSWLEN         SAVE CURRENT LENGTH
         BR    R14                 LENGTH EXHAUSTED, EXIT HERE
CONS7020 DS    0H
         BCT   R15,CONS7010        LOOP
CONS7030 DS    0H
         STH   R2,CONSWLEN         SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
         BR    R14                 EXIT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT ROUTINE                                         *
*                                                                    *
*--------------------------------------------------------------------*
PRT0000  DS    0H
         LA    R1,PRTBLOK          PRINT INTERFACE PARAMETERS
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT
         BR    R9                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR ROUTINES                                        *
*                                                                    *
*--------------------------------------------------------------------*
CONSERR1 DS    0H
         OI    COMMFLAG,$COMMCER   SIGNAL INVALID COMMAND
         WTO   'INVALID CONSOLE COMMAND',                              +
               CONSID=COMMCID      .. CONSOLE ID
         STCM  R1,7,CONSMID        SAVE MESSAGE ID FOR DOM
         B     CONS9900            EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
CONS9900 DS    0H
         DITTRACE ID=EXIT,         TRACE EXITS                         +
               DATA1=COMMINM,      .. INPUT MODULE ADDRESS             +
               DATA2=COMMOUTM      .. OUTPUT MODULE ADDRESS
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
*--------------------------------------------------------------------*
*                                                                    *
*              CONS-INPUT FUNCTION EXECUTED INSTRUCTIONS             *
*                                                                    *
*--------------------------------------------------------------------*
CONSFCLC CLC   FUNCCMD(0),CONSWORK  CORRECT COMMAND?
CONSCMVC MVC   PRTDATA(0),0(R15)    MOVE COMMAND(S) TO PRINT
WTOMMVC  MVC   CONSWTO+4(0),2(R1)   MOVE MESSAGE TEXT TO WTO
PRTMMVC  MVC   PRTDATA(0),2(R1)     MOVE MESSAGE TEXT TO PRINT I/O
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CONS-INPUT FUNCTION WORK AREAS                        *
*                                                                    *
*--------------------------------------------------------------------*
CONSSAVE DC    18F'0'              REGISTER SAVE AREA
CONSWORK DC    CL44' '             PARSER OUTPUT WORK AREA
CONSWLEN DC    H'0'                CURRENT PARSER OUTPUT LENGTH
CONSMID  DC    XL3'000000'         MESSAGE ID FROM LAST WTO
CONSEOJ  DC    C'EOJ '             'END OF JOB' COMMAND
CONSCAN  DC    C'CANCEL '          'CANCEL' COMMAND
CONSXTR  DC    C'TRACE '           'TRACE' COMMAND
CONSON   DC    C'ON '              CONSTANT
CONSOFF  DC    C'OFF '             CONSTANT
CONSAREA DC    CL80' '             CONSOLE REPLY AREA
CONSWTO  WTO   '                                                       +
                                        ',                             +
               CONSID=,                                                +
               MF=L
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERTER INTERFACE BLOCK                   *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CONSTANTS                                             *
*                                                                    *
*--------------------------------------------------------------------*
LISTFNC  DC    C'XXX '
CONSM01  DC    C'COMMAND: '
CONSM01C DC    CL4' '
         DC    C'   DESCRIPTION:'
CONSM01D DC    CL80' '
CONSM01L EQU   *-CONSM01
CONSM02  DC    C'EXAMPLE OF COMMAND:'
         DC    CL12' '
CONSM02C DC    CL80' '
CONSM02L EQU   *-CONSM02
CONSM03  DC    C'EXTERNAL TRACE ACTIVATED'
CONSM03L EQU   *-CONSM03
CONSM04  DC    C'COMMAND CANCELLED BY OPERATOR'
CONSM04L EQU   *-CONSM04
CONSM05  DC    C'INCONSISTENT PARAMETERS, COMMAND ABORTED'
CONSM05L EQU   *-CONSM05
CONSM06  DC    C'INVALID TRACE COMMAND (USE ''TRACE ON'' OR ''TRACE OFF+
               '')'
CONSM06L EQU   *-CONSM06
CONSM07  DC    C'EXTERNAL TRACE ALREADY ACTIVE'
CONSM07L EQU   *-CONSM07
CONSM08  DC    C'EXTERNAL TRACE DE-ACTIVATED'
CONSM08L EQU   *-CONSM08
CONSM09  DC    C'EXTERNAL TRACE NOT ACTIVE'
CONSM09L EQU   *-CONSM09
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DEFINE PARAMETERS                                     *
*                                                                    *
*--------------------------------------------------------------------*
         PARMGEN GEN=NO
HIVOLID  PARMID HIGHVOL
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              FUNCTION TABLE DSECT                                  *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  FUNCBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTCONS
./ ADD NAME=DITTDAIR
         MACRO
&NAME    MSG   &TEXT
         LCLA  &LEN
&LEN     SETA  (K'&TEXT)-2
&NAME    DS    0C
         DC    AL2(&LEN)           LENGTH OF MESSAGE
         DC    C&TEXT              MESSAGE
         MEND
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC ALLOCATION ROUTINE  (DAIR) INTERFACE          *
*                                                                    *
*      THIS MODULE INTERFACES WITH THE SYSTEM DYNAMIC ALLOCATION     *
*      ROUTINES (DAIR).  A REQUEST MUST SPECIFY THE TYPE OF ALLOCATE *
*      (UNIT IS ONLY TYPE SO FAR) AND ASSOCIATED PARAMETERS.         *
*      WHEN A DEVICE IS ALLOCATED, IT IS ADDED TO A CHAIN ANCHORED   *
*      IN 'DITTCOMM'.  THIS MODULE ACQUIRES STORAGE FOR THE DCB,     *
*      IOB, ECB, AND CCW'S.  THIS STORAGE IS MAPPED BY THE 'DYNBLOK' *
*      DSECT.                                                        *
*                                                                    *
*      ON DE-ALLOCATION REQUESTS THE DYNAMIC BLOCK IS LOCATED ON THE *
*      CHAIN, THE DCB IS "CLOSED", THE UNIT DE-ALLOCATED, AND THE    *
*      CONTROL BLOCK STORAGE IS FREED.                               *
*                                                                    *
*--------------------------------------------------------------------*
DITTDAIR DITTPRFX DAIRSAVE,'DYNAMIC ALLOCATION INTERFACE'
         LR    R10,R1              SAVE PARAMETER BLOCK ADDRESS
         USING DAIRBLOK,R10        ADDRESS IT
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=DAIRCMMD,     .. CAPTURE COMMAND                  +
               DATA2=DAIRCUU       .. CAPTURE CUU
         MVI   DAIRSTAT,$DAIRSOK   CLEAR STATUS BYTE
         TM    DAIRTYPE,$DAIRCUU   UNIT ALLOCATION/DEALLOCATION??
         BNO   DAIR2040            NO... INVALID TYPE
         ICM   R6,15,COMMDYHD      POINT TO HEAD OF CHAIN
         USING DYNBLOK,R6          SPECIFY BASE
DAIR0010 DS    0H
         BZ    DAIR0030            NO DYNAMIC BLOCK FOR THIS CUU
         CLC   DYNCUU,DAIRCUU      CORRECT UNIT??
         BE    DAIR0020            YES
         ICM   R6,15,DYNNEXT       LOAD ADDRESS OF NEXT BLOCK
         B     DAIR0010            LOOP
DAIR0020 DS    0H
         DITTRACE ID=DYNLOC,       DYN BLOCK LOCATED                   +
               RDATA1=R6           CAPTURE DYNBLOK ADDRESS
         CLI   DAIRCMMD,$DAIRACQ   ALLOCATE REQUEST??
         BE    DAIR2050            YES .. ALREADY ALLOCATED
         CLI   DAIRCMMD,$DAIRREL   RELEASE REQUEST
         BE    DAIR0600            YES
         CLI   DAIRCMMD,$DAIRLOC   LOCATE REQUEST??
         BNE   DAIR2010            INVALID REQUEST
         STCM  R6,15,DAIRAREA      RETURN ADDRESS OF DYNAMIC BLOCK
         MVI   DAIRSTAT,$DAIRSOK   GOOD STATUS
         B     DAIR9900            EXIT TO CALLER
DAIR0030 DS    0H
         DITTRACE ID=NODYN         DYN BLOCK NOT LOCATED
         CLI   DAIRCMMD,$DAIRREL   RELEASE REQUEST??
         BE    DAIR2000            YES .. NOT LOCATED
         CLI   DAIRCMMD,$DAIRLOC   LOCATE REQUEST??
         BE    DAIR2000            YES .. NOT LOCATED
         CLI   DAIRCMMD,$DAIRACQ   ALLOCTE REQUEST??
         BNE   DAIR2010            NO .. INVALID REQUEST
         DITTRACE ID=NEWALLOC      TRACE NEW ALLOCATION REQUESTS
         XC    UCBTOKEN,UCBTOKEN   CLEAR THE TOKEN
         XC    UCBWORK,UCBWORK     CLEAR THE WORK AREA TOO
* ------------------------------------------------------------------- *
*                                                                     *
*        LOCATE THE UCB                                               *
*                                                                     *
* ------------------------------------------------------------------- *
DAIR0040 DS    0H
         DITTRACE ID=UCBSCAN,DATA1=UCBTOKEN
         MODESET MODE=SUP,KEY=ZERO ENTER SUPERVISOR STATE, KEY ZERO
         UCBSCAN ADDRESS,          LOCATE UCB, RETURN IT'S ADDRESS     +
               UCBPTR=UCBADDR,     .. RETURNED ADDRESS                 +
               WORKAREA=UCBWORK,      .. WORK AREA                     +
               DYNAMIC=YES,           .. INCLUDE DYNAMICALLY ADDED     +
               PIN,                   .. DON'T LET ANYONE DELETE IT    +
               TEXT=UCBTEXT,          .. REASON FOR PIN                +
               PTOKEN=UCBTOKEN        .. TOKEN
         MODESET MODE=PROB,KEY=NZERO  PROBLEM STATE, PROBLEM KEY
         ICM   R5,15,UCBADDR       UCB ADDRESS
         USING UCBOB,R5            ADDRESS UCB
         DITTRACE ID=UCB,          TRACE EACH UCB EXAMINED             +
               RDATA1=R5,          .. UCB BLOCK ADDRESS                +
               DATA2=UCBCHAN       .. DEVICE ADDRESS (NAME)
         CLC   DAIRCUU,UCBCHAN     CORRECT DEVICE??
         BNE   DAIR0040            NO
DAIR0050 DS    0H
         DITTRACE ID=UCBLOC,       TRACE UCB HAS BEEN LOCATED          +
               RDATA1=R5,          .. CAPTURE UCB ADDRESS              +
               DATA2=UCBTBYT3      .. CAPTURE UNIT TYPE
DAIR0060 DS    0H
         TM    UCBSTAT,UCBONLI     IS DEVICE ONLINE??
         BNO   DAIR2060            NO
         TM    UCBTBYT3,UCB3DACC   IS THIS A DASD UNIT??
         BO    DAIR0170            YES
         TM    UCBSTAT,UCBALOC     IS DEVICE ALREADY ALLOCATED??
         BO    DAIR2070            YES
         TM    UCBTBYT3,UCB3TAPE   IS THIS A TAPE UNIT??
         BO    DAIR0070            YES
         B     DAIR2030            DEVICE TYPE IS NOT ALLOWED
DAIR0070 DS    0H
         DITTRACE ID=ALOCTAPE      ALLOCATING A TAPE UNIT
         CLI   UCBTBYT4,UCB3480    IS THIS A 3480?
         BE    DAIR0140            YES
         DITTRACE ID=ALOC3420      ALLOCATE REQUESTED FOR A 3420
         TM    COMMPFLG,$COMMDEN   WAS A DENSITY REQUESTED?
         BO    DAIR0100            YES
         TM    UCBTBYT1,UCBD6250   6250 BPI DRIVE?
         BO    DAIR0080            YES
         TM    UCBTBYT2,UCBDUDN2   1600 AND 6250??
         BO    DAIR0080            YES.. GO WITH 6250
         TM    UCBTBYT1,UCBD1600   1600 BPI DRIVE??
         BO    DAIR0090            YES
         TM    UCBTBYT2,UCBDUDN1   800 AND 1600?
         BO    DAIR0090            YES
         DITTRACE ID=UNSP3420      UNSUPPORTED 3420
         MVI   DAIRSTAT,$DAIRSUD   UNSUPPORTED 3420 (NOT 1600 OR 6250)
         B     DAIR9900            AND EXIT
DAIR0080 DS    0H
         DITTRACE ID=DFLT6250      SETTING DENSITY TO 6250 BY DEFAULT
         MVI   COMMDEN,$DEN6250    SET 6250 DENSITY
         B     DAIR0160            ALLOCATE THE DEVICE
DAIR0090 DS    0H
         DITTRACE ID=DFLT1600      SETTING DENSITY TO 1600 BY DEFAULT
         MVI   COMMDEN,$DEN1600    SET 1600 DENSITY
         B     DAIR0160            ALLOCATE THE DEVICE
DAIR0100 DS    0H
         DITTRACE ID=CHECKDEN      CHECK DENSITY
         CLI   COMMDEN,$DEN6250    6250 BPI REQUESTED?
         BE    DAIR0120            YES
         DITTRACE ID=CHK1600       CHECKING 1600
         TM    UCBTBYT1,UCBD1600   1600 BPI DRIVE??
         BO    DAIR0110            YES... VALID REQUEST
         TM    UCBTBYT2,UCBDUDN1   800/1600 BPI DRIVE??
         BO    DAIR0110            YES... VALID REQUEST
         TM    UCBTBYT2,UCBDUDN2   1600/6250 BPI DRIVE??
         BO    DAIR0110            YES... VALID REQUEST
         DITTRACE ID=1600INV       DRIVE WILL NOT ALLOW 1600
         MVI   DAIRSTAT,$DAIRS16   DRIVE WILL NOT ALLOW 1600
         B     DAIR9900            EXIT
DAIR0110 DS    0H
         DITTRACE ID=1600OK        1600 IS A-OK
         MVI   COMMDEN,$DEN1600    SET 1600 BPI
         B     DAIR0160            ALLOCATE THE DRIVE
DAIR0120 DS    0H
         DITTRACE ID=CHK6250       CHECKING 6250
         TM    UCBTBYT1,UCBD6250   6250 BPI DRIVE??
         BO    DAIR0130            YES... VALID REQUEST
         TM    UCBTBYT2,UCBDUDN2   1600/6250 BPI DRIVE??
         BO    DAIR0130            YES.. VALID REQUEST
         DITTRACE ID=6250INV       DRIVE WILL NOT ALLOW 6250
         MVI   DAIRSTAT,$DAIRS62   DRIVE WILL NOT ALLOW 6250
         B     DAIR9900            EXIT
DAIR0130 DS    0H
         DITTRACE ID=6250OK        6250 IS A-OK
         MVI   COMMDEN,$DEN6250    SET DENSITY TO 6250
         B     DAIR0160            ALLOCATE THE DRIVE
DAIR0140 DS    0H
         DITTRACE ID=ALOC3480
         TM    COMMPFLG,$COMMDEN   WAS DENSITY SPECIFIED??
         BNO   DAIR0150            NO.. GOOD (NOT ALLOWED ON 3480'S)
         MVI   DAIRSTAT,$DAIRSD1   DENSITY MAY NOT BE SPECIFIED
         B     DAIR9900            EXIT
DAIR0150 DS    0H
         DITTRACE ID=3480OK        3480 DENSITY OK
         MVI   COMMDEN,$DEN3480    SET 3480 DENSITY
*--------------------------------------------------------------------*
*                                                                    *
*       CONSTRUCT SVC99 PARAMETER LIST FOR TAPE ALLOCATION           *
*                                                                    *
*--------------------------------------------------------------------*
DAIR0160 DS    0H
         DITTRACE ID=TAPE99
         MVC   SVC99DDN(4),DAIRTAPE MOVE 'TAPE'
         UNPK  COMMDWRD(5),DAIRCUU(3) UNPACK CUU
         MVZ   COMMDWRD(4),DAIR0F0F   TURN OFF ZONES
         TR    COMMDWRD(4),DAIRHXCH   TRANSLATE TO CHARACTER
         MVC   SVC99DDN+4(4),COMMDWRD MOVE UNPACKED CUU
         MVC   SVC99DSN,COMMBLKS      INITIALIZE DATASET NAME
         MVC   SVC99DSN(L'SVC99DDN),SVC99DDN
         MVC   SVC99UTX,COMMBLKS   INITIALIZE UNIT NAME
         MVC   SVC99UTX(3),COMMDWRD+1 MOVE CUU TO 'UNIT'
         MVI   SVC99STT,4          INITIAL STATUS 'NEW'
         XC    SVC99P1A(SVC99PL),SVC99P1A
         LA    R1,SVC99RB          DYNALLOC REQUEST BLOCK
         USING S99RB,R1            DEFINE DSECT BASE
         MVI   S99VERB,S99VRBAL    MOVE 'ALLOCATE' VERB
         LA    R1,SVC99DD          DD NAME ADDRESS
         ST    R1,SVC99P1A         STORE THE ADDRESS
         LA    R1,SVC99DS          DSN NAME ADDRESS
         ST    R1,SVC99P2A         STORE THE ADDRESS
         LA    R1,SVC99UT          UNIT NAME ADDRESS
         ST    R1,SVC99P3A         STORE THE ADDRESS
         LA    R1,SVC99ST          INITIAL STATUS (NEW)
         ST    R1,SVC99P4A         STORE THE ADDRESS
         LA    R1,SVC99DP          NORMAL TERMINATION DISP (KEEP)
         ST    R1,SVC99P5A         STORE THE ADDRESS
         LA    R1,SVC99DFP         DEFERRED MOUNT
         ST    R1,SVC99P6A         STORE THE ADDRESS
         OI    SVC99P6A,X'80'      SET END OF LIST
         BAL   R9,DAIR1000         LINK TO SVC99 ROUTINE
         CLI   DAIRSTAT,$DAIRSOK   SVC99 A-OK??
         BE    DAIR0180            YES, CONTINUE
         DITTRACE ID=TAPEFAIL      TRACE TAPE ALLOCATION FAILURE
         B     DAIR9900            GO TO ERROR ROUTINE
*--------------------------------------------------------------------*
*                                                                    *
*       CONSTRUCT SVC99 PARAMETER LIST FOR DASD ALLOCATION           *
*                                                                    *
*--------------------------------------------------------------------*
DAIR0170 DS    0H
         DITTRACE ID=DASD99
         TM    COMMPFLG,$COMMDEN   WAS A DENSITY REQUESTED?
         BO    DAIR2080            YES.. NOT ALLOWED ON DASD
         UNPK  COMMDWRD(5),DAIRCUU(3) UNPACK CUU
         MVZ   COMMDWRD(4),DAIR0F0F   TURN OFF ZONES
         TR    COMMDWRD(4),DAIRHXCH   TRANSLATE TO CHARACTER
         MVC   SVC99DDN(4),DAIRDASD MOVE 'DASD'
         MVC   SVC99DDN+4(4),COMMDWRD MOVE UNPACKED CUU
         MVC   SVC99UTX,SYSALLDA   SET UNIT NAME
         MVC   SVC99VLX,UCBVOLI    SET VOLSER
         MVI   SVC99STT,1          INITIAL STATUS 'OLD'
         XC    SVC99P1A(SVC99PL),SVC99P1A
         LA    R1,SVC99RB          DYNALLOC REQUEST BLOCK
         USING S99RB,R1            DEFINE DSECT BASE
         MVI   S99VERB,S99VRBAL    MOVE 'ALLOCATE' VERB
         LA    R1,SVC99DD          DD NAME ADDRESS
         ST    R1,SVC99P1A         STORE THE ADDRESS
         LA    R1,SVC99UT          UNIT NAME ADDRESS
         ST    R1,SVC99P2A         STORE THE ADDRESS
         LA    R1,SVC99VOL         VOLSER ADDRESS
         ST    R1,SVC99P3A         STORE THE ADDRESS
         LA    R1,SVC99ST          INITIAL STATUS (OLD)
         ST    R1,SVC99P4A         STORE THE ADDRESS
         OI    SVC99P4A,X'80'      SET END OF LIST
         BAL   R9,DAIR1000         LINK TO SVC99 ROUTINE
         CLI   DAIRSTAT,$DAIRSOK   SVC99 A-OK??
         BE    DAIR0180            YES, CONTINUE
         DITTRACE ID=DASDFAIL      TRACE DASD ALLOCATION FAILURE
         B     DAIR9900            GO TO ERROR ROUTINE
*--------------------------------------------------------------------*
*                                                                    *
*        INITIALIZE DYNAMIC BLOCK                                    *
*                                                                    *
*--------------------------------------------------------------------*
DAIR0180 DS    0H
         DITTRACE ID=UNITOK        TRACE SUCESSFUL DAIR CALLS
         LA    R1,COMMDYHD         SAVE LAST BLOCK ADDRESS
         ICM   R6,15,COMMDYHD      HEAD OF CHAIN
DAIR0190 DS    0H
         BZ    DAIR0200            END OF CHAIN FOUND
         LR    R1,R6               SAVE LAST BLOCK ADDRESS
         ICM   R6,15,DYNNEXT       NEXT BLOCK
         B     DAIR0190            LOOP
DAIR0200 DS    0H
         LR    R6,R1               RESTORE BASE REG
         LA    R0,DYNBLOKL         LENGTH OF DYNAMIC BLOCK
         GETMAIN R,LV=(0)          ACQUIRE STORAGE FOR DYNAMIC BLOCK
         DITTRACE ID=DYNGETM,      TRACE DYNAMIC BLOCK ACQUIRED        +
               RDATA1=R2           .. SAVE DYNBLOCK ADDRESS
         LR    R2,R1               SAVE STORAGE ADDRESS
         LR    R0,R1               COPY STORAGE ADDRESS
         LA    R1,DYNBLOKL         STORAGE SIZE
         SR    R15,R15             SECOND LENGTH = 0
         MVCL  R0,R14              CLEAR THE STORAGE
         ST    R2,DYNNEXT          CHAIN TO NEXT BLOCK
         DROP  R6
         USING DYNBLOK,R2          SWAP BASE
         ST    R6,DYNPREV          CHAIN BACKWARD
         LR    R6,R2               LOAD PERM BASE REG
         STCM  R6,15,DAIRAREA      PASS BLOCK ADDRESS BACK
         DROP  R2
         USING DYNBLOK,R6          RESTORE BASE
         GETMAIN RU,LV=128         ACQUIRE STORAGE FOR DEB
         ST    R1,DYNDEBA          SAVE DEB STORAGE ADDRESS
         LR    R7,R1               SAVE DEB STORAGE ADDRESS
         DITTRACE ID=DEBGETM,      TRACE DEB STORAGE ACQUIRED          +
               RDATA1=R7           .. SAVE STORAGE ADDRESS
         LA    R3,DYNIOB           IOB IN DYNAMIC BLOCK
         LA    R4,DYNDCB           DCB IN DYNAMIC BLOCK
* ------------------------------------------------------------------- *
*                                                                     *
*          REGISTERS AT THIS TIME:                                    *
*                                                                     *
*             R3  IOB STORAGE WITHIN DYNAMIC BLOCK                    *
*             R4  DCB STORAGE WITHIN DYNAMIC BLOCK                    *
*             R5  UCB ADDRESS OF DEVICE ALLOCATED                     *
*             R6  DYNAMIC BLOCK ADDRESS                               *
*             R7  DEB STORAGE ADDRESS                                 *
*                                                                     *
* ------------------------------------------------------------------- *
         MVC   DYNCUU,DAIRCUU      MOVE CUU
         MVC   DYNVOL,COMMBLKS     INITIALIZE VOLSER
* ------------------------------------------------------------------- *
*           INITIALIZE IOB                                            *
* ------------------------------------------------------------------- *
         USING IOB,R3
         USING IHADCB,R4                DEFINE BASE
         USING DEB,R7
         MVI   IOBFLAG1,X'02'      SET FLAG BYTE 1
         ST    R4,IOBDCBPT         SET DCB ADDRESS
* ------------------------------------------------------------------- *
*           INITIALIZE DCB                                            *
* ------------------------------------------------------------------- *
         TM    UCBTBYT3,UCB3DACC   DASD UNIT?
         BO    DAIR0210            YES
         MVC   DYNDCB(DYNDCBL),TAPEDCB  INITIALIZE THE DCB
         B     DAIR0220
DAIR0210 DS    0H
         MVC   DYNDCB(DYNDCBL),DASDDCB  INITIALIZE THE DCB
DAIR0220 DS    0H
         MVC   DCBDDNAM,SVC99DDN   SET DDNAME USED
         MVC   DYNDDNAM,SVC99DDN   SET DDNAME USED
         LA    R1,DEBBASIC         DEB BASIC SECTION ADDRESS
         ST    R1,DCBDEBAD         INSERT DEB ADDRESS
         ST    R3,DCBIOBA          INSERT IOB ADDRESS
         MVC   DCBMACRF,DCBMACR    COPY DCB MACRO FORMAT PARAMETERS
         MVC   DCBIFLGS,DCBIFLG    COPY I/O ERROR FLAGS
         OI    DCBOFLGS,DCBOFOPN   INDICATE DCB IS OPEN
* ------------------------------------------------------------------- *
*           INITIALIZE DEB                                            *
* ------------------------------------------------------------------- *
         XC    DEBPREFX(DEBPREFE-DEBPREFX),DEBPREFX  CLEAR PREFIX
         XC    DEBBASIC(DEBBASND-DEBBASIC),DEBBASIC  CLEAR BASIC
         USING PSA,R0              DEFINE PSA BASE
         MVC   DEBTCBAD,PSATNEW    SET TCB ADDRESS
         MVI   DEBAMLNG,16         SET ACCESS METHOD SECTION LENGTH
         MVI   DEBOFLGS,DEBDSOLD   DISPOSITION 'OLD'
         MVI   DEBOPATB,DEBLEAVE+DEBOUTPT
         MVI   DEBNMEXT,1          SET NUMBER OF EXTENTS
         MVI   DEBPRIOR,X'EB'      SET PRIORITY
         ST    R4,DEBDCBAD         INSERT ASSOCIATED DCB ADDRESS
         MVI   DEBDEBID,X'0F'      IDENTIFY THIS AS A DEB
         ST    R5,DEBSUCBA         UCB ASSOCIATED
         L     R1,CVTPTR           CVT ADDRESS
         USING CVT,R1              DEFINE CVT BASE
         MVC   DEBAPPAD,CVTXAPG    SET I/O APPENDAGE VECTOR TABLE ADDR
         TM    UCBTBYT3,UCB3DACC   DASD UNIT?
         BO    DAIR0230            YES
         MVC   DEBSDVM,COMMDEN     SET DENSITY
         B     DAIR0240
DAIR0230 DS    0H
         MVI   DEBEXSCL,4          SET EXTENT SCALE
         LA    R1,DEBBASND         DASD EXTENTION TO DEB
         USING DEBDASD,R1          DEFINE BASE
         XC    DEBDASD(DEBDASDE-DEBDASD),DEBDASD
         MVI   DEBDVMOD,X'D8'      SET FILE MASK
         STCM  R5,7,DEBUCBA        SET UCB ADDRESS
         MVC   DEBENDCC,XFFFF      SET TO HIGH VALUE
         MVC   DEBNMTRK,XFFFF      SET TO HIGH VALUE
* ------------------------------------------------------------------- *
*           ADD DEB TO DEBCHAIN FROM TCB AND TO DEBTABLE              *
* ------------------------------------------------------------------- *
DAIR0240 DS    0H
         DITTRACE ID=ADDDEB        ADDING DEB TO DEBTABLE
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0250            NO
         SR    R1,R1               'TURN ON' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0250 DS    0H
         MODESET MODE=SUP,KEY=ZERO SWITCH TO SUPERVISOR STATE, KEY ZERO
         USING PSA,R0              DEFINE PSA BASE
         L     R1,PSATNEW          OUR TCB ADDRESS FROM THE PSA
         USING TCB,R1              DEFINE BASE
         L     R15,TCBDEB          FIRST DEB ON DEB CHAIN
         LA    R2,DEBBASIC         'BASIC' PORTION OF DEB
DAIR0260 DS    0H
         STCM  R15,7,DEBDEBB       INSERT PREVIOUS ADDRESS INTO OUR DEB
         CS    R15,R2,TCBDEB       ADD OUR DEB TO CHAIN
         BNE   DAIR0260            UNSUCCESSFUL... TRY AGAIN
         DEBCHK (R4),              USE DEBCHK (REFERENCE DCB NOT DEB)  +
               AM=EXCP,            .. EXCP ACCESS METHOD               +
               TYPE=ADD            .. ADD A DEB TO DEBTABLE
         LR    R2,R15              SAVE RETURN CODE
         MODESET MODE=PROB,KEY=NZERO    PROBLEM STATE, PROBLEM KEY
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0270            NO
         LA    R1,1                'TURN OFF' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0270 DS    0H
         DITTRACE ID=DEBCHK,       TRACE DEBCHK STATUS                 +
               RDATA1=R2           .. SAVE RETURN CODE
         LTR   R2,R2               DEBCHK SUCCESFUL??
         BNZ   DAIR0330            NO
         DITTRACE ID=DEBDONE       TRACE SUCCESSFUL COMPLETION
         CLC   DAIRDASD,DYNDDNAM   DASD?
         BE    DAIR0320
         CLI   COMMDEN,$DEN1600    1600 BPI??
         BE    DAIR0280            YES
         CLI   COMMDEN,$DEN6250    6250 BPI??
         BE    DAIR0300            YES
         B     DAIR9900            EXIT
DAIR0280 DS    0H
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DAIR0290            YES
         MVC   PRTDATA(DAIRM01L),DAIRM01
         BAL   R14,PRT0000         PRINT DENSITY MESSAGE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB??
         BE    DAIR9900            YES
         WTO   'DRIVE ALLOCATED WITH DENSITY 1600 BPI',                +
               CONSID=COMMCID
         B     DAIR9900            EXIT
DAIR0290 DS    0H
         MVC   MSG01(DAIRM01L),DAIRM01 MOVE MESSAGE
         B     DAIR9900            AND EXIT
DAIR0300 DS    0H
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DAIR0310            YES
         MVC   PRTDATA(DAIRM02L),DAIRM02
         BAL   R14,PRT0000         PRINT DENSITY MESSAGE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB??
         BE    DAIR9900            YES
         WTO   'DRIVE ALLOCATED WITH DENSITY 6250 BPI',                +
               CONSID=COMMCID
         B     DAIR9900            EXIT
DAIR0310 DS    0H
         MVC   MSG01(DAIRM02L),DAIRM02 MOVE MESSAGE
         B     DAIR9900            AND EXIT
* ------------------------------------------------------------------ *
*    CALL DASD MODULE TO INTIALIZE VOLSER AND MAX CYLINDER AND HEAD. *
*                                                                    *
*    NOTE ALSO THAT AT THIS TIME, DAIR WILL HAVE BEEN CALLED BY      *
*    'DITTALOC' WHILE 'DITTALOC' IS RUNNING AS THE INPUT MODULE AND  *
*    ONLY WHEN THE 'FIRST PASS' FLAG IS ON.  WHEN 'FIRST PASS' FLAG  *
*    IS ON AND THE VOLSER IN THE DYNBLOCK IS BLANK, 'DITTDAI1' ONLY  *
*    DOES THE INITIALIZATION PORTION OF ITS PROCESSING.              *
*                                                                    *
* ------------------------------------------------------------------ *
DAIR0320 DS    0H
         ST    R6,COMMIND          SET DYNAMIC BLOCK ADDRESS FOR 'DAI1'
         L     R15,ADAI1           DASD READER ENTRY POINT
         BALR  R14,R15             LINK TO DASD MODULE FOR INITIALIZE
         B     DAIR9900            AND EXIT
DAIR0330 DITTRACE ID=DEBAFAIL,     TRACE DEB ADD FAILURES              +
               RDATA1=R2           .. CAPTURE DEBCHK RETURN CODE
         ABEND ABEND015,DUMP,,USER
*--------------------------------------------------------------------*
*                                                                    *
*              RELEASE DYNAMICALLY ALLOCATED RESOURCES               *
*                                                                    *
*--------------------------------------------------------------------*
DAIR0600 DS    0H
         DITTRACE ID=UNITFREE      TRACE DE-ALLOCATIONS
DAIR0610 DS    0H
         LA    R4,DYNDCB           DCB FOR THIS UNIT
         USING IHADCB,R4           DEFINE DSECT BASE
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0620            NO
         SR    R1,R1               'TURN ON' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0620 DS    0H
         MODESET MODE=SUP,KEY=ZERO SWITCH TO SUPERVISOR STATE, KEY ZERO
         DEBCHK (R4),              USE DEBCHK (REFERENCE DCB NOT DEB)  +
               TYPE=DELETE         .. TO DELETE THE DEB FROM DEBTABLE
         LR    R2,R15              SAVE RETURN CODE
         DITTRACE ID=DEBDEL,       TRACE DEB DELETIONS                 +
               RDATA1=R2           .. CAPTURE RETURN CODE
         LTR   R2,R2               DEBCHK SUCCESSFUL?
         BNZ   DAIR0710            NO
DAIR0630 DS    0H
         L     R7,DYNDEBA          DEB ADDRESS IN THIS DYN BLOCK
         LA    R2,DEBBASIC         BASIC PORTION OF DEB
         L     R1,PSATNEW          CURRENT TCB ADDRESS FROM PSA
         USING TCB,R1              DEFINE DCB BASE
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,7,TCBDEB+1       1ST DEB ON DEB CHAIN
         LA    R4,TCBDEB           ADDRESS'S ADDRESS
DAIR0640 DS    0H
         DITTRACE ID=DAIR0640,     TRACE POINT                         +
               RDATA1=R3,          .. SAVE DEB'S ADDRESS               +
               RDATA2=R4           .. SAVE ADDRESS'S ADDRESS
         CR    R3,R2               IS THIS THE CORRECT DEB?
         BE    DAIR0660            YES
         LA    R4,DEBDEBAD-DEBBASIC(R3)   NEXT DEB ADDRESS ADDRESS
         ICM   R3,7,DEBDEBB-DEBBASIC(R3)  NEXT DEB
         BNZ   DAIR0640            CHECK AGAIN
         MODESET MODE=PROB,KEY=NZERO
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0650            NO
         LA    R1,1                'TURN OFF' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0650 DS    0H
         DITTRACE ID=LOSTDEB       COULDN'T FIND THE DEB
         ABEND ABEND016,DUMP,,USER
DAIR0660 DS    0H
         ICM   R2,7,DEBDEBB        NEXT DEB ON CHAIN
         ICM   R2,8,0(R4)          SAVE ACCESS METHOD SECTION LENGTH
         ICM   R3,15,0(R4)         CS WILL USE ALL 4 BYTES
         DITTRACE ID=DAIR0660,     TRACE POINT                         +
               RDATA1=R2,          .. 'NEXT' DEB'S ADDRESS             +
               RDATA2=R3           .. 'DEBDEBAD' IN PREVIOUS DEB
         CS    R3,R2,0(R4)         REMOVE DEB FROM DEB CHAIN
         BNE   DAIR0630            SOMETHING CHANGED, TRY AGAIN
         MODESET MODE=PROB,KEY=NZERO   RETURN TO OUR NORMAL SELF
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0670            NO
         LA    R1,1                'TURN OFF' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0670 DS    0H
         L     R1,DYNDEBA          DEB STORAGE ADDRESS
         FREEMAIN R,A=(1),LV=128   FREE DEB STORAGE
         LA    R1,SVC99RB          DYNALLOC REQUEST BLOCK
         USING S99RB,R1            DEFINE DSECT BASE
         MVI   S99VERB,S99VRBUN    MOVE 'DE-ALLOCATE' VERB
         MVC   SVC99DDN,DYNDDNAM   MOVE DD NAME
         XC    SVC99P1A(SVC99PL),SVC99P1A
         LA    R1,SVC99DD          DD NAME ADDRESS
         ST    R1,SVC99P1A         STORE THE ADDRESS
         OI    SVC99P1A,X'80'      SET END OF LIST
         BAL   R9,DAIR1000         INVOKE DYNAMIC ALLOCATION ROUTINES
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL??
         BE    DAIR0680            YES, CONTINUE
         DITTRACE ID=RELFAIL       TRACE DE-ALLOCATION FAILURE
         OI    COMMFLAG,$ABEND     SIGNAL ABEND REQUEST
         B     DAIR9900            EXIT
DAIR0680 DS    0H
         ICM   R1,15,DYNNEXT       'FWD' POINTER
         BZ    DAIR0690            NO 'FWD' POINTER
         MVC   4(4,R1),DYNPREV     RECHAIN-BWD
DAIR0690 DS    0H
         ICM   R1,15,DYNPREV       'BWD' POINTER
         BZ    DAIR0700            NO 'BWD' POINTER
         MVC   0(4,R1),DYNNEXT     RE-CHAIN FWD
DAIR0700 DS    0H
         LA    R0,DYNBLOKL         LENGTH OF DYNAMIC BLOCK
         FREEMAIN R,LV=(0),A=(R6)  FREE THE STORAGE
         B     DAIR9900            EXIT TO CALLER
DAIR0710 DS    0H
         MODESET MODE=PROB,KEY=NZERO   RETURN TO OUR NORMAL SELF
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BNE   DAIR0720            NO
         LA    R1,1                'TURN OFF' REQUEST
         L     R15,AAUTH           DITTAUTH ENTRY POINT
         BALR  R14,R15             LINK TO DITTAUTH
DAIR0720 DS    0H
         DITTRACE ID=DEBDFAIL,     TRACE DEB DELETE FAILURES           +
               RDATA1=R2           .. CAPTURE DEBCHK RETURN CODE
         ABEND ABEND017,DUMP,,USER
*--------------------------------------------------------------------*
*                                                                    *
*              INVOKE DYNAMIC ALLOCATION ROUTINES                    *
*                                                                    *
*--------------------------------------------------------------------*
DAIR1000 DS    0H
         OI    SVC99PTR,X'80'      ENSURE BIT IS SET
         LA    R1,SVC99RB          REQUEST BLOCK
         MVI   S99RBLN,S99RBEND-S99RB ZAP IN LENGTH
         LA    R15,SVC99P1A        DD NAME ADDRESS ADDRESS
         ST    R15,S99TXTPP        STORE IN REQUEST BLOCK
         DITTRACE ID=CALLDAIR,     TRACE CALL TO DAIR                  +
               DATA1=SVC99PTR      .. CAPTURE DAIR RB POINTER ADDRESS
         LA    R1,SVC99PTR         POINTER ADDRESS
         DYNALLOC                  INVOKE DAIR ROUTINES
         STCM  R15,15,DAIRR15      SAVE RETURN CODE
         LA    R1,SVC99RB          REQUEST BLOCK
         USING S99RB,R1            DEFINE DSECT BASE
         MVC   DAIRERR,S99ERROR    MOVE ERROR CODE
         MVC   DAIRINFO,S99INFO    MOVE REASON CODE
         DITTRACE ID=DAIRRC,       TRACE DAIR RETURN                   +
               RDATA1=R15,         .. SAVE R15                         +
               DATA2=DAIRERR       .. REASON FOR VALUE IN R15
         LTR   R15,R15             DAIR SUCCESSFUL??
         BZR   R9                  YES
         MVI   DAIRSTAT,$DAIRSDE   INDICATE ERROR FROM DAIR CALL
         L     R15,ACLASS2         CLASS 2 MESSAGE TABLE
         CLI   DAIRERR,X'02'       CLASS 2 ERROR??
         BE    DAIR1010            YES
         L     R15,ACLASS3         CLASS 3 MESSAGE TABLE
         CLI   DAIRERR,X'03'       CLASS 3 ERROR??
         BE    DAIR1010            YES
         L     R15,ACLASS4         CLASS 4 MESSAGE TABLE
         CLI   DAIRERR,X'04'       CLASS 4 ERROR??
         BNER  R9                  NO
DAIR1010 DS    0H
         OI    COMMFLAG,$ABORT     TURN ON ABORT FLAG
         SR    R14,R14             CLEAR REGISTER
         IC    R14,DAIRERR+1       MESSAGE DISP
         AR    R15,R14             MESSAGE ADDRESS POINTER
         L     R14,0(R15)          MESSAGE BLOCK ADDRESS
         USING MSGDSECT,R14        DEFINE DSECT BASE
         LH    R15,MSGMSGL         MESSAGE LENGTH
         BCTR  R15,0               ADJUST LENGTH
         EX    R15,PRTMSGM         MOVE MESSAGE TO PRINT
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DAIR1020            YES
         MVI   PRTCC,C' '          CARRIAGE CONTROL
         BAL   R14,PRT0000         LINK TO PRINT MODULE
         BR    R9                  RETURN
DAIR1020 DS    0H
         EX    R15,TSOMSGM         MOVE MESSAGE
         BR    R9                  RETURN
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR CONDITIONS                                      *
*                                                                    *
*--------------------------------------------------------------------*
DAIR2000 DS    0H
         MVI   DAIRSTAT,$DAIRSLE   UNSUCCESSFUL LOCATE ON REL/LOC
         B     DAIR9900            EXIT
DAIR2010 DS    0H
         MVI   DAIRSTAT,$DAIRSIR   INVALID REQUEST
         B     DAIR9900            EXIT
DAIR2020 DS    0H
         MVI   DAIRSTAT,$DAIRSNU   UNIT DOES NOT EXIST (ALLOCATE)
         B     DAIR9900            EXIT
DAIR2030 DS    0H
         MVI   DAIRSTAT,$DAIRSTE   INVALID DEVICE TYPE
         B     DAIR9900            EXIT
DAIR2040 DS    0H
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         MVI   DAIRSTAT,$DAIRSRE   INVALID RESOURCE TYPE
         B     DAIR9900            EXIT
DAIR2050 DS    0H
         MVI   DAIRSTAT,$DAIRSDP   SECOND ALLOCATE FOR SAME DEVICE
         B     DAIR9900            EXIT
DAIR2060 DS    0H
         MVI   DAIRSTAT,$DAIRSOF   DEVICE OFFLINE
         B     DAIR9900            EXIT
DAIR2070 DS    0H
         MVI   DAIRSTAT,$DAIRSAL   DEVICE ALREADY ALLOCATED
         B     DAIR9900            EXIT
DAIR2080 DS    0H
         MVI   DAIRSTAT,$DAIRSD2   DENSITY NOT ALLOWED ON DASD
         B     DAIR9900            EXIT
PRT0000  DS    0H
         ST    R14,PRTSAVE         SAVE R14
         MVI   PRTCMMD,$PRTCMD     REQUEST PRINT
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         L     R14,PRTSAVE         RESTORE R14
         BR    R14                 RETURN
DAIR9900 DS    0H
         DITTRACE ID=EXIT          TRACE
         L     R13,4(R13)          RESTORE REGISTER 13
ASE01670
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
ASE01680
         SR    R15,R15             GIVE GOOD RETURN CODE
ASE01690
         BR    R14                 RETURN TO CALLER
ASE01700
*--------------------------------------------------------------------*
*                                                                    *
*              EXECUTED INSTRUCTIONS                                 *
*                                                                    *
*--------------------------------------------------------------------*
PRTMSGM  MVC   PRTDATA(0),MSGMSG   MOVE MESSAGE TO PRINT AREA
TSOMSGM  MVC   MSG01(0),MSGMSG     MOVE MESSAGE TO PRINT AREA
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CONSTANTS                                             *
*                                                                    *
*--------------------------------------------------------------------*
DAIRTAPE DC    C'TAPE'
DAIRDASD DC    C'DASD'
SYSALLDA DC    C'SYSALLDA'
DAIR0F0F DC    16X'0F'
DAIRHXCH DC    C'0123456789ABCDEF'
XFFFF    DC    X'FFFF'
UCBTEXT  DC    CL58'PINNED BY DITTO'
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
*--------------------------------------------------------------------*
DAIRSAVE DC    18F'0'                  REGISTER SAVE AREA
PRTSAVE  DC    A(0)                    R14 SAVED DURING 'PRT0000'
ACLASS2  DC    A(CLASS2)               CLASS '2' ERROR MESSAGES
ACLASS3  DC    A(CLASS3)               CLASS '3' ERROR MESSAGES
ACLASS4  DC    A(CLASS4)               CLASS '4' ERROR MESSAGES
UCBADDR  DC    A(0)                    UCB ADDRESS
UCBTOKEN DC    8X'00'
         DC    CL16'DAIR PARM BLOCK'
SVC99PTR DC    A(X'80000000'+SVC99RB)  DYNALLOC POINTER
SVC99RB  DC    (S99RBEND-S99RB)X'00'   DYNALLOC REQUEST BLOCK
SVC99P1A DC    A(0)                    DAIR TEXT UNIT ADDRESSES
SVC99P2A DC    A(0)
SVC99P3A DC    A(0)
SVC99P4A DC    A(0)
SVC99P5A DC    A(0)
SVC99P6A DC    A(0)
SVC99P7A DC    A(0)
SVC99P8A DC    A(0)
SVC99P9A DC    A(0)
SVC99PL  EQU   *-SVC99P1A              PARAMETER LIST LENGTH
SVC99DD  DC    AL2(DALDDNAM)           KEY FOR DATASET NAME
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(8)                  LENGTH OF ENTRY
SVC99DDN DC    CL8'TAPEXXX '           DATASET NAME
SVC99DS  DC    AL2(DALDSNAM)           KEY FOR DATASET NAME
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(44)                 LENGTH OF ENTRY
SVC99DSN DC    CL44'TAPEXXX'           DATASET NAME
SVC99UT  DC    AL2(DALUNIT)            KEY FOR UNIT
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(8)                  LENGTH OF ENTRY
SVC99UTX DC    CL8' '                  UNIT
SVC99ST  DC    AL2(DALSTATS)           KEY FOR STATUS
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(1)                  LENGTH OF ENTRY
SVC99STT DC    X'04'                   STATUS -NEW-
SVC99DP  DC    AL2(DALNDISP)           KEY FOR STATUS
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(1)                  LENGTH OF ENTRY
SVC99DSP DC    X'08'                   NORMAL DISPOSITION -KEEP-
SVC99DFP DC    AL2(DALDEFER)           KEY FOR DEFERRED MOUNT
         DC    AL2(0000)               NUMBER OF LENGTH/ENTRIES
SVC99VOL DC    AL2(DALVLSER)           KEY FOR VOLSER
         DC    AL2(0001)               NUMBER OF LENGTH/ENTRIES
         DC    AL2(6)                  LENGTH OF ENTRY
SVC99VLX DC    CL6' '                  VOLSER
         SPACE 2
TAPEDCB  DCB   MACRF=EXCP,             MODEL TAPE DCB                  +
               DDNAME=TAPEXXXX,                                        +
               DEVD=TA,                                                +
               DSORG=PS
DASDDCB  DCB   MACRF=EXCP,             MODEL DASD DCB                  +
               DDNAME=DASDXXXX,                                        +
               DEVD=DA,                                                +
               DSORG=PS
DAIRM01  DC    C'DRIVE ALLOCATED WITH DENSITY 1600 BPI'
DAIRM01L EQU   *-DAIRM01
DAIRM02  DC    C'DRIVE ALLOCATED WITH DENSITY 6250 BPI'
DAIRM02L EQU   *-DAIRM02
UCBWORK  DC    XL100'00'
*--------------------------------------------------------------------*
*                                                                    *
*              PRINTER INTERFACE BLOCK                               *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 2 DYNAMIC ALLOCATION MESSAGES ADCONS            *
*                                                                    *
*--------------------------------------------------------------------*
CLASS2   DS   0A
         DC   A(0)                UNUSED
         DC   A(E0204)            ADDRESS OF MESSAGE FOR CODE '0204'
         DC   A(E0208)            ADDRESS OF MESSAGE FOR CODE '0208'
         DC   A(E020C)            ADDRESS OF MESSAGE FOR CODE '020C'
         DC   A(E0210)            ADDRESS OF MESSAGE FOR CODE '0210'
         DC   A(E0214)            ADDRESS OF MESSAGE FOR CODE '0214'
         DC   A(E0218)            ADDRESS OF MESSAGE FOR CODE '0218'
         DC   A(E021C)            ADDRESS OF MESSAGE FOR CODE '021C'
         DC   A(E0220)            ADDRESS OF MESSAGE FOR CODE '0220'
         DC   A(E0224)            ADDRESS OF MESSAGE FOR CODE '0224'
         DC   A(E0228)            ADDRESS OF MESSAGE FOR CODE '0228'
         DC   A(E022C)            ADDRESS OF MESSAGE FOR CODE '022C'
         DC   A(E0230)            ADDRESS OF MESSAGE FOR CODE '0230'
         DC   A(E0234)            ADDRESS OF MESSAGE FOR CODE '0234'
         DC   A(E0238)            ADDRESS OF MESSAGE FOR CODE '0238'
         DC   A(E023C)            ADDRESS OF MESSAGE FOR CODE '023C'
         DC   A(E0240)            ADDRESS OF MESSAGE FOR CODE '0240'
         DC   A(E0244)            ADDRESS OF MESSAGE FOR CODE '0244'
         DC   A(E0248)            ADDRESS OF MESSAGE FOR CODE '0248'
         DC   A(E024C)            ADDRESS OF MESSAGE FOR CODE '024C'
         DC   A(E0250)            ADDRESS OF MESSAGE FOR CODE '0250'
         DC   A(E0254)            ADDRESS OF MESSAGE FOR CODE '0254'
         DC   A(E0258)            ADDRESS OF MESSAGE FOR CODE '0258'
         DC   A(E025C)            ADDRESS OF MESSAGE FOR CODE '025C'
         DC   A(E0260)            ADDRESS OF MESSAGE FOR CODE '0260'
         DC   A(E0264)            ADDRESS OF MESSAGE FOR CODE '0264'
         DC   A(E0268)            ADDRESS OF MESSAGE FOR CODE '0268'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 3 DYNAMIC ALLOCATION MESSAGES ADCONS            *
*                                                                    *
*--------------------------------------------------------------------*
CLASS3   DS   0A
         DC   A(0)                UNUSED
         DC   A(E0304)            ADDRESS OF MESSAGE FOR CODE '0304'
         DC   A(E0308)            ADDRESS OF MESSAGE FOR CODE '0308'
         DC   A(E030C)            ADDRESS OF MESSAGE FOR CODE '030C'
         DC   A(E0310)            ADDRESS OF MESSAGE FOR CODE '0310'
         DC   A(E0314)            ADDRESS OF MESSAGE FOR CODE '0314'
         DC   A(E0318)            ADDRESS OF MESSAGE FOR CODE '0318'
         DC   A(E031C)            ADDRESS OF MESSAGE FOR CODE '031C'
         DC   A(E0320)            ADDRESS OF MESSAGE FOR CODE '0320'
         DC   A(E0324)            ADDRESS OF MESSAGE FOR CODE '0324'
         DC   A(E0328)            ADDRESS OF MESSAGE FOR CODE '0328'
         DC   A(E032C)            ADDRESS OF MESSAGE FOR CODE '032C'
         DC   A(E0330)            ADDRESS OF MESSAGE FOR CODE '0330'
         DC   A(E0334)            ADDRESS OF MESSAGE FOR CODE '0334'
         DC   A(E0338)            ADDRESS OF MESSAGE FOR CODE '0338'
         DC   A(E033C)            ADDRESS OF MESSAGE FOR CODE '033C'
         DC   A(E0340)            ADDRESS OF MESSAGE FOR CODE '0340'
         DC   A(E0344)            ADDRESS OF MESSAGE FOR CODE '0344'
         DC   A(E0348)            ADDRESS OF MESSAGE FOR CODE '0348'
         DC   A(E034C)            ADDRESS OF MESSAGE FOR CODE '034C'
         DC   A(E0350)            ADDRESS OF MESSAGE FOR CODE '0350'
         DC   A(E0354)            ADDRESS OF MESSAGE FOR CODE '0354'
         DC   A(E0358)            ADDRESS OF MESSAGE FOR CODE '0358'
         DC   A(E035C)            ADDRESS OF MESSAGE FOR CODE '035C'
         DC   A(E0360)            ADDRESS OF MESSAGE FOR CODE '0360'
         DC   A(E0364)            ADDRESS OF MESSAGE FOR CODE '0364'
         DC   A(E0368)            ADDRESS OF MESSAGE FOR CODE '0368'
         DC   A(E036C)            ADDRESS OF MESSAGE FOR CODE '037C'
         DC   A(E0370)            ADDRESS OF MESSAGE FOR CODE '0370'
         DC   A(E0374)            ADDRESS OF MESSAGE FOR CODE '0374'
         DC   A(E0378)            ADDRESS OF MESSAGE FOR CODE '0378'
         DC   A(E037C)            ADDRESS OF MESSAGE FOR CODE '038C'
         DC   A(E0380)            ADDRESS OF MESSAGE FOR CODE '0380'
         DC   A(E0384)            ADDRESS OF MESSAGE FOR CODE '0384'
         DC   A(E0388)            ADDRESS OF MESSAGE FOR CODE '0388'
         DC   A(E038C)            ADDRESS OF MESSAGE FOR CODE '038C'
         DC   A(E0390)            ADDRESS OF MESSAGE FOR CODE '0390'
         DC   A(E0394)            ADDRESS OF MESSAGE FOR CODE '0394'
         DC   A(E0398)            ADDRESS OF MESSAGE FOR CODE '0398'
         DC   A(E039C)            ADDRESS OF MESSAGE FOR CODE '039C'
         DC   A(E03A0)            ADDRESS OF MESSAGE FOR CODE '03A0'
         DC   A(E03A4)            ADDRESS OF MESSAGE FOR CODE '03A4'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 4 DYNAMIC ALLOCATION MESSAGES ADCONS            *
*                                                                    *
*--------------------------------------------------------------------*
CLASS4   DS   0A
         DC   A(0)                UNUSED
         DC   A(E0404)            ADDRESS OF MESSAGE FOR CODE '0404'
         DC   A(E0408)            ADDRESS OF MESSAGE FOR CODE '0408'
         DC   A(E040C)            ADDRESS OF MESSAGE FOR CODE '040C'
         DC   A(E0410)            ADDRESS OF MESSAGE FOR CODE '0410'
         DC   A(E0414)            ADDRESS OF MESSAGE FOR CODE '0414'
         DC   A(E0418)            ADDRESS OF MESSAGE FOR CODE '0418'
         DC   A(E041C)            ADDRESS OF MESSAGE FOR CODE '041C'
         DC   A(E0420)            ADDRESS OF MESSAGE FOR CODE '0420'
         DC   A(E0424)            ADDRESS OF MESSAGE FOR CODE '0424'
         DC   A(E0428)            ADDRESS OF MESSAGE FOR CODE '0428'
         DC   A(E042C)            ADDRESS OF MESSAGE FOR CODE '042C'
         DC   A(E0430)            ADDRESS OF MESSAGE FOR CODE '0430'
         DC   A(E0434)            ADDRESS OF MESSAGE FOR CODE '0434'
         DC   A(E0438)            ADDRESS OF MESSAGE FOR CODE '0438'
         DC   A(E043C)            ADDRESS OF MESSAGE FOR CODE '043C'
         DC   A(E0440)            ADDRESS OF MESSAGE FOR CODE '0440'
         DC   A(E0444)            ADDRESS OF MESSAGE FOR CODE '0444'
         DC   A(E0448)            ADDRESS OF MESSAGE FOR CODE '0448'
         DC   A(E044C)            ADDRESS OF MESSAGE FOR CODE '044C'
         DC   A(E0450)            ADDRESS OF MESSAGE FOR CODE '0450'
         DC   A(E0454)            ADDRESS OF MESSAGE FOR CODE '0454'
         DC   A(E0458)            ADDRESS OF MESSAGE FOR CODE '0458'
         DC   A(E045C)            ADDRESS OF MESSAGE FOR CODE '045C'
         DC   A(E0460)            ADDRESS OF MESSAGE FOR CODE '0460'
         DC   A(E0464)            ADDRESS OF MESSAGE FOR CODE '0464'
         DC   A(E0468)            ADDRESS OF MESSAGE FOR CODE '0468'
         DC   A(E046C)            ADDRESS OF MESSAGE FOR CODE '047C'
         DC   A(E0470)            ADDRESS OF MESSAGE FOR CODE '0470'
         DC   A(E0474)            ADDRESS OF MESSAGE FOR CODE '0474'
         DC   A(E0478)            ADDRESS OF MESSAGE FOR CODE '0478'
         DC   A(E047C)            ADDRESS OF MESSAGE FOR CODE '048C'
         DC   A(E0480)            ADDRESS OF MESSAGE FOR CODE '0480'
         DC   A(E0484)            ADDRESS OF MESSAGE FOR CODE '0484'
         DC   A(E0488)            ADDRESS OF MESSAGE FOR CODE '0488'
         DC   A(E048C)            ADDRESS OF MESSAGE FOR CODE '048C'
         DC   A(E0490)            ADDRESS OF MESSAGE FOR CODE '0490'
         DC   A(E0494)            ADDRESS OF MESSAGE FOR CODE '0494'
         DC   A(E0498)            ADDRESS OF MESSAGE FOR CODE '0498'
         DC   A(E049C)            ADDRESS OF MESSAGE FOR CODE '049C'
         DC   A(E04A0)            ADDRESS OF MESSAGE FOR CODE '04A0'
         DC   A(E04A4)            ADDRESS OF MESSAGE FOR CODE '04A4'
         DC   A(E04A8)            ADDRESS OF MESSAGE FOR CODE '04A8'
         DC   A(E04AC)            ADDRESS OF MESSAGE FOR CODE '04AC'
         DC   A(E04B0)            ADDRESS OF MESSAGE FOR CODE '04B0'
         DC   A(E04B4)            ADDRESS OF MESSAGE FOR CODE '04B4'
         DC   A(E04B8)            ADDRESS OF MESSAGE FOR CODE '04B8'
         DC   A(E04BC)            ADDRESS OF MESSAGE FOR CODE '04BC'
         DC   A(E04C0)            ADDRESS OF MESSAGE FOR CODE '04C0'
         DC   A(E04C4)            ADDRESS OF MESSAGE FOR CODE '04C4'
         DC   A(E04C8)            ADDRESS OF MESSAGE FOR CODE '04C8'
         DC   A(E04CC)            ADDRESS OF MESSAGE FOR CODE '04CC'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 2 DYNAMIC ALLOCATION MESSAGES                   *
*                                                                    *
*--------------------------------------------------------------------*
E0204   MSG    'INSUFFICIENT STORAGE'
E0208   MSG    'MANUAL SAYS THIS IS A RESERVED CODE'
E020C   MSG    'REQUEST FOR SHARED DEVICE CANNOT BE HONORED'
E0210   MSG    'DATASET IN USE'
E0214   MSG    'UNIT NOT AVAILABLE'
E0218   MSG    'VOLUME NOT MOUNTED'
E021C   MSG    'UNIT NAME UNDEFINED'
E0220   MSG    'REQUESTED VOLUME NOT AVAILABLE'
E0224   MSG    'ELIGIBLE DEVICE TYPE(S) DO NOT CONTAIN ENOUGH UNITS'
E0228   MSG    'SPECIFIED VOLUME OR UNIT IN USE BY SYSTEM OR RESERVED U+
               NIT'
E022C   MSG    'VOLUME MOUNTED ON INELIGIBLE PERMANENTLY RESIDENT OR RE+
               SERVED UNIT'
E0230   MSG    'PERMANENTLY RESIDENT OR RESERVED VOLUME ON REQUIRED UNI+
               T'
E0234   MSG    'MORE THAN ONE DEVICE REQUIRED FOR REQUEST SPECIFIYING A+
                SPECIFIC UNIT'
E0238   MSG    'NO SPACE AVAILABLE IN TIOT'
E023C   MSG    'REQUIRED CATALOG NOT MOUNTED'
E0240   MSG    'REQUESTED DEVICE IS A CONSOLE'
E0244   MSG    'TELECOMMUNICATION DEVICE NOT ACCESSABLE'
E0248   MSG    'MSS VIRTUAL VOLUME CANNOT BE MOUNTED'
E024C   MSG    'OPERATING-SYSTEM-MANAGED RESOURCE UNAVAILABE TO SUBSYST+
               EM'
E0250   MSG    'SUBSYSTEM RESOURCE NOT AVAILABLE'
E0254   MSG    'TIOT CURRENTLY UNVAILABLE'
E0258   MSG    'INSUFFICIENT NUMBER OF NON-RESTRICTED UNITS'
E025C   MSG    'REQUESTED DEVICE IS BOXED (VARIED OFFLINE)'
E0260   MSG    'UNIT DOES NOT MEET STATUS REQUIREMENTS'
E0264   MSG    'INVALID REQUEST DUE TO CURRENT UNIT STATUS'
E0268   MSG    'TAPE DEVICE IS BROKEN'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 3 DYNAMIC ALLOCATION MESSAGES                   *
*                                                                    *
*--------------------------------------------------------------------*
E0304   MSG    'ASSIGNED BY DAIR'
E0308   MSG    'ASSIGNED BY DAIR'
E030C   MSG    'ASSIGNED BY DAIR'
E0310   MSG    'ASSIGNED BY DAIR'
E0314   MSG    'ASSIGNED BY DAIR'
E0318   MSG    'ASSIGNED BY DAIR'
E031C   MSG    'ASSIGNED BY DAIR'
E0320   MSG    'ASSIGNED BY DAIR'
E0324   MSG    'ASSIGNED BY DAIR'
E0328   MSG    'ASSIGNED BY DAIR'
E032C   MSG    'ASSIGNED BY DAIR'
E0330   MSG    'ASSIGNED BY DAIR'
E0334   MSG    'ASSIGNED BY DAIR'
E0338   MSG    'ASSIGNED BY DAIR'
E033C   MSG    'RESERVED'
E0340   MSG    'RESERVED'
E0344   MSG    'RESERVED'
E0348   MSG    'RESERVED'
E034C   MSG    'RESERVED'
E0350   MSG    'RESERVED'
E0354   MSG    'RESERVED'
E0358   MSG    'OVERRIDING DISPOSITION OF DELETE INVALID FOR DATASET AL+
               LOCATED SHR'
E035C   MSG    'INVALID PARM IN TEXT UNIT (INTERNAL ERROR)'
E0360   MSG    'INVALID KEY IN TEXT UNIT (INTERNAL ERROR)'
E0364   MSG    'JOBLIB/STEPLIB/JOBCAT/STEPCAT SPECIFIED AS DDNAME'
E0368   MSG    'AUTHORIZED FUNCTION REQUESTED'
E036C   MSG    'INVALID PARAMETER LIST FORMAT'
E0370   MSG    'RESERVED'
E0374   MSG    'INVALID # SPECIFIED IN TEXT UNIT (INTERNAL ERROR)'
E0378   MSG    'DUPLICATE KEY IN TEXT UNITS (INTERNAL ERROR)'
E037C   MSG    'INVALID LENGTH IN TEXT UNIT (INTERNAL ERROR)'
E0380   MSG    'MUTUALLY EXCLUSIVE KEYS IN TEXT UNITS (INTERNAL ERROR)'
E0384   MSG    'REQUIRED KEY NOT SPECIFIED IN TEXT UNITS (INTERNAL ERRO+
               R)'
E0388   MSG    'REQUIRED KEY NOT SPECIFIED IN TEXT UNITS (INTERNAL ERRO+
               R)'
E038C   MSG    'DUPLICATE DDNAMES (CONCATENATION)'
E0390   MSG    'GDG GROUP NAME SPECIFIED WITH RELATIVE GENERATION EXCEE+
               DS 35 CHARACTERS'
E0394   MSG    'STATUS AND RELATIVE GENERATION ARE INCOMPATIBLE'
E0398   MSG    'VOLUME SEQUENCE NUMBER EXCEEDS NUMBER OF VOLUMES'
E039C   MSG    'DEVICE TYPE AND VOLUME ARE INCOMPATIBLE'
E03A0   MSG    'SUBSYSTEM DETECTED AN INVALID PARAMETER'
E03A4   MSG    'UNABLE TO PROTECT DATASET/VOLUME BECAUSE OF CONFLICTING+
                KEYWORD SPECIFICATION'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              CLASS 4 DYNAMIC ALLOCATION MESSAGES                   *
*                                                                    *
*--------------------------------------------------------------------*
E0404   MSG    'RESERVED'
E0408   MSG    'RESERVED'
E040C   MSG    'RESERVED'
E0410   MSG    'SPECIFIED DDNAME UNAVAILABLE'
E0414   MSG    'RESERVED'
E0418   MSG    'RESERVED'
E041C   MSG    'RESERVED'
E0420   MSG    'SPECIFIED DDNAME ASSOCIATED WITH AN OPEN DATASET'
E0424   MSG    'DECONCATENATION WOULD RESULT IN DUPLICATE DDNAME'
E0428   MSG    'RESERVED'
E042C   MSG    'RESERVED'
E0430   MSG    'RESERVED'
E0434   MSG    'DDNAME IN DDNAME ALLOCATION IS ASSOCIATED WITH A CONVER+
               TABLE OR NON-PERMANENTLY ALLOCATED RESOURCE'
E0438   MSG    'SPECIFIED DDNAME NOT FOUND'
E043C   MSG    'SYSTEM COULD NOT DEALLOCATE ENOUGH RESOURCES BEING HELD+
                FOR REUSE TO MEET CONTROL LIMIT'
E0440   MSG    'SPECIFIED DDNAME NOT FOUND'
E0444   MSG    'RELATIVE ENTRY NUMBER SPECIFIED IN INFORMATION RETRIEVA+
               L REQUEST NOT FOUND'
E0448   MSG    'REQUEST FOR DATASET FAILED-DATASET ALREADY EXISTS'
E044C   MSG    'REQUEST WITH DISPOSITION DELETE FAILED- DATASET MAY BE +
               DELETED AT ANY TIME'
E0450   MSG    'REQUEST EXCEEDS LIMIT OF 1635 CONCURRENT ALLOCATIONS'
E0454   MSG    'GDG GROUP NAME'
E0458   MSG    'DSNAME IN DCB REFERENCE OR VOLUME REFERENCE IS A GDG GR+
               OUP NAME'
E045C   MSG    'SPECIFIED DSNAME TO BE DEALLOCATED IS MEMBER OF A PERMA+
               NENTLY-CONCATENATED GROUP'
E0460   MSG    'SPECIFIED DSNAME OR MEMBER NOT ASSOCIATED WITH SPECIFIE+
               D DDNAME'
E0464   MSG    'SPECIFIED DDNAME FOR DEALLOCATION IS A PRIVATE CATALOG'
E0468   MSG    'ERROR WHILE ALLOCATING OR OPENING A PRIVATE CATALOG'
E046C   MSG    'REMOTE WORK STATION NOT DEFINED TO JES'
E0470   MSG    'USER NOT AUTHORIZED FOR JES REQUESTS'
E0474   MSG    'ERROR WHILE ATTEMPTING TO SELECT OPTIMUM DEVICE'
E0478   MSG    'UNABLE TO PROCESS JES REQUEST'
E047C   MSG    'UNABLE TO ESTABLISH ESTAE ENVIRONMENT'
E0480   MSG    'NUMBER OF UNITS NEEDED EXCEEDS LIMIT'
E0484   MSG    'REQUEST DENIED BY OPERATOR'
E0488   MSG    'GDG PATTERN DSCB NOT MOUNTED'
E048C   MSG    'GDG PATTERM DSCB NOT FOUND'
E0490   MSG    'ERROR CHANGING ALLOCATION ASSIGNMENTS'
E0494   MSG    'ERROR PROCESSING OS CVOL'
E0498   MSG    'MSS VIRTUAL VOLUME NOT ACCESSIBLE'
E049C   MSG    'MSS VIRTUAL VOLUME NOT DEFINED'
E04A0   MSG    'SPECIFIED MSVGP NAME NOT DEFINED'
E04A4   MSG    'SUBSYSTEM REQUEST IN ERROR'
E04A8   MSG    'SUBSYSTEM DOES NOT SUPPORT ALLOCATION VIA KEY DALSSNM'
E04AC   MSG    'SUBSYSTEM NOT OPERATIONAL'
E04B0   MSG    'SUBSYSTEM DOES NOT EXIST'
E04B4   MSG    'PROTECT REQUEST NOT PROCESSED, RACF NOT ACTIVE'
E04B8   MSG    'MSS NOT INITIALIZED FOR ALLOCATION'
E04BC   MSG    'MSS VOLUME SELECT ERROR'
E04C0   MSG    'PROTECT REQUEST FAILED, USER NOT DEFINED TO RACF'
E04C4   MSG    'MAXIMUM REFERBACK LIMIT EXCEEDED'
E04C8   MSG    'NON-ZERO RETURN CODE FROM COMMON ALLOCATION OR JFCB HOU+
               SEKEEPING'
E04CC   MSG    'INVALID REFERENCE TO OUTPUT JCL STATEMENT'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              ERROR MESSAGE TABLE DSECT                             *
*                                                                    *
*--------------------------------------------------------------------*
MSGDSECT DSECT
MSGMSGL  DS    AL2                MESSAGE LENGTH
MSGMSG   DS    0C                 MESSAGE TEXT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC DEVICE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK  TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC ALLOCATE/DE-ALLOCATE REQUEST BLOCK            *
*                                                                    *
*--------------------------------------------------------------------*
DAIRBLOK DAIRBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              ABEND CODE EQUATES                                    *
*                                                                    *
*--------------------------------------------------------------------*
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS COMMUNICATIONS VECTOR TABLE                       *
*                                                                    *
*--------------------------------------------------------------------*
         CVT   DSECT=YES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS UNIT CONTROL BLOCK                                *
*                                                                    *
*--------------------------------------------------------------------*
         IEFUCBOB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU    *-IHADCB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS PSA DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IHAPSA DSECT=YES,LIST=NO
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS TCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IKJTCB LIST=NO
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DEB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZDEB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DAIR INTERFACE DSECTS/EQUATES                     *
*                                                                    *
*--------------------------------------------------------------------*
         IEFZB4D0
         IEFZB4D2
         END
./ ADD NAME=DITTDAI1
*--------------------------------------------------------------------*
*                                                                    *
*              DASD INPUT MODULE                                     *
*                                                                    *
*--------------------------------------------------------------------*
DITTDAI1 DITTPRFX DASDSAVE,'DASD READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    DASD9900            YES
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         LA    R9,DYNIOB           IOB WITHIN DYNBLOCK
         USING IOB,R9              DEFINE BASE
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   DASD0010            NO
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADRESS TO EXCP
         ZAP   DASDRCDS,COMMP0     ZERO RECORD COUNTER
         XC    DASDCRCD,DASDCRCD   CLEAR CURRENT RECORD ADDRESS
         XC    DASDREM,DASDREM     CLEAR REMAINING BLOCK AMOUNT
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         MVC   DYNCCW(CCWL),SRCHCCW INITIALIZE CCW STRING
         LA    R1,IOBCC            SEARCH ADDRESS
         STCM  R1,7,DYNCCW1+1      SET SEARCH DATA ADDRESS
         LA    R1,DYNCCW1          SEARCH CCW ADDRESS
         STCM  R1,7,DYNCCW2+1      SET TRANSFER ADDRESS IN TIC CCW
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,DYNCCW3+1      SET DATA BUFFER ADDRESS
         CLC   DYNVOL,COMMBLKS     VOLSER/HIGH ADDRESS DETERMINED?
         BNE   DASD0010            YES
*--------------------------------------------------------------------*
*                                                                    *
*        DYNBLOK INITIALIZATION                                      *
*                                                                    *
*     THIS MODULE IS CALLED BY 'DITTDAIR' DURING ALLOCATION OF A     *
*     DASD DEVICE.  THIS PORTION OF PROCESSING DETERMINES THE DASD'S *
*     VOLSER AND THE MAX CYLINDER AND HEAD NUMBERS.                  *
*                                                                    *
*--------------------------------------------------------------------*
         MVC   IOBCC(5),VTOCPTRA   SET DISK ADDRESS FOR VTOC POINTER
         BAL   R8,DASD0250         READ VTOC POINTER RECORD
         CLI   EXCPSTAT,$EXCPOK    READ SUCCESSFULLY?
         BNE   DASD0400            NO
         L     R1,DASDCRCD         INPUT BUFFER ADDRESS
         USING VTOCPTR,R1          DEFINE VTOC POINTER BASE
         MVC   DYNVOL,VTOCSER      SAVE VOLSER
         MVC   IOBCC(5),VTOCADDR   SET DISK ADDRESS TO VTOC'S ADDRESS
         DITTRACE ID=READFMT4,     READING FORMAT 4 RECORD             +
               DATA1=IOBCC         .. INCLUDE DISK ADDRESS USED
         BAL   R8,DASD0250         READ FORMAT 4 RECORD
         CLI   EXCPSTAT,$EXCPOK    READ SUCCESSFULLY?
         BNE   DASD0400            NO
         L     R1,DASDCRCD         INPUT BUFFER ADDRESS
         USING FMT4,R1             DEFINE FORMAT 4 RECORD BASE
         MVC   DYNHDADR,DS4DEVSZ   SAVE MAX CYLINDER AND HEAD NUMBERS
         B     DASD9900            'INITIALIZATION' COMPLETE
DASD0010 DS    0H
         LA    R2,DYNDCB           DCB WITHIN DYNBLOK
         USING IHADCB,R2           DEFINE DSECT BASE
         NI    COMMIFLG,255-$NEWBLOK TURN OFF NEW BLOCK INDICATOR
DASD0020 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD COUNT GIVEN??
         BNO   DASD0030            NO, BYPASS RECORD COUNT CHECK
         CP    DASDRCDS,COMMRCDS   READ ENOUGH RECORDS??
         BE    DASD0530            YES, SIGNAL END OF FILE
DASD0030 DS    0H
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   DASD0100            NO
         DITTRACE ID=NEXTRECD,     CALLED FOR NEXT RECORD,             +
               DATA1=DASDCRCD,     .. CURRENT RECORD'S ADDRESS         +
               DATA2=DASDREM       .. LENGTH REMAINING
         OC    DASDREM,DASDREM     ANY LENGTH REMAINING??
         BZ    DASD0100            NO.. NEED ANOTHER RECORD
         DITTRACE ID=DEBLOCK       LENGTH REMAINING, DEBLOCK RECORD
DASD0040 DS    0H
         ICM   R3,15,DASDCRCD      CURRENT RECORD ADDRESS
         BZ    DASD0460            ERROR
         SR    R2,R2               CLEAR REGISTER
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BO    DASD0050            YES
         ICM   R2,3,COMMLRCL       LOGICAL RECORD SIZE
         B     DASD0060            CONTINUE
DASD0050 DS    0H
         ICM   R2,3,0(R3)          RECORD SIZE
DASD0060 DS    0H
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ??
         BO    DASD0070            YES, LEAVE CURRENT ADDRESS
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
DASD0070 DS    0H
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BNO   DASD0080            NO, RECORD LENGTH WILL BE THE SAME
         ICM   R2,3,0(R3)          LENGTH OF NEW RECORD
         BZ    DASD0420         ...RECORD LENGTH ZERO.. INVALID
DASD0080 DS    0H
         CLM   R2,3,DASDREM        LONGER THAN REMAINING LENGTH??
         BNH   DASD0090            NO
         ICM   R2,3,DASDREM        OTHERWISE LIMIT LENGTH
DASD0090 DS    0H
         ST    R3,DASDCRCD         SAVE CURRENT RECORD ADDRESS
         ST    R3,COMMCRCD         PASS CURRENT RECORD ADDRESS
         STH   R2,COMMCRCL         PASS CURRENT RECORD LENGTH
         DITTRACE ID=LREC,         RECORD ADDRESS AND LENGTH           +
               RDATA1=R3,          .. LOGICAL RECORD'S ADDRESS         +
               RDATA2=R2           .. LOGICAL RECORD'S LENGTH
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,DASDREM        REMAINING SIZE
         SR    R3,R2               COMPUTE REMAINING SIZE
         STCM  R3,3,DASDREM        SAVE REMAINING SIZE
         AP    DASDRCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     DASD9900            EXIT
DASD0100 DS    0H
         DITTRACE ID=NEXTBLOK      READ NEXT PHYSICAL RECORD
         BAL   R8,DASD0200         LINK TO I/O
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   DASD0130            NO
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   DASD0040            NO
         SR    R0,R0               CLEAR REGISTER
         L     R3,DASDCRCD         BLOCK ADDRESS
         ICM   R0,3,0(R3)          LENGTH FROM BDW (MAYBE)
         SH    R0,H4               MINUS BDW LENGTH
         BNH   DASD0040            INSUFFICIENT LENGTH
         LA    R3,4(R3)            FIRST RDW IN THE BLOCK
         SR    R2,R2               CLEAR REGISTER
DASD0110 DS    0H
         ICM   R2,3,0(R3)          RECORD LENGTH FROM RDW
         BZ    DASD0420            RECORD WITH LENGTH = ZERO!
         SR    R0,R2               MINUS LENGTH OF THIS RECORD
         BM    DASD0040            NEGATIVE... LENGTHS DON'T ADD UP
         BZ    DASD0120            ZERO.. LENGTHS MATCH
         AR    R3,R2               NEXT RDW ADDRESS
         B     DASD0110            LOOP
DASD0120 DS    0H
         L     R3,DASDCRCD         BLOCK ADDRESS
         ICM   R2,3,0(R3)          BLOCK LENGTH
         SH    R2,H4               MINUS BDW LENGTH
         STCM  R2,3,DASDREM        SET LENGTH REMAINING
         LA    R3,4(R3)            FIRST LOGICAL RECORD'S ADDRESS
         ST    R3,DASDCRCD         SET RECORD ADDRESS
         B     DASD0040            DEBLOCK FIRST RECORD
DASD0130 DS    0H
         DITTRACE ID=PHYRECD,      PHYSICAL BLOCK                      +
               DATA1=DASDCRCD,     .. PHYSICAL BLOCK ADDRESS           +
               DATA2=DASDREM       .. PHYSICAL BLOCK SIZE
         MVC   COMMCRCD,DASDCRCD   PASS RECORD ADDRESS
         MVC   COMMCRCL,DASDREM    PASS RECORD LENGTH
         AP    DASDRCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     DASD9900            EXIT
*---------------------------------------------------------------------*
*                                                                     *
*                    LINK TO EXCP FOR ACTUAL I/O                      *
*                                                                     *
*---------------------------------------------------------------------*
DASD0200 DS    0H
         TM    COMMFLAG,$COMM1ST   FIRST PASS?
         BNO   DASD0210            NO
         MVC   IOBCC(5),COMMDADR   SET DISK ADDRESS
         B     DASD0240            SKIP DISK ADDRESS UPDATE
DASD0210 DS    0H
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             RECORD NUMBER
         LA    R1,1(R1)            NEXT RECORD
         STC   R1,IOBR             SAVE RECORD NUMBER
         DITTRACE ID=NEWREC,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         B     DASD0240            READ WITH NEW RECORD NUMBER
DASD0220 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,IOBHH          CURRENT HEAD NUMBER
         LA    R1,1(R1)            NEXT HEAD
         STCM  R1,3,IOBHH          SAVE HEAD NUMBER
         MVI   IOBR,1              START OVER AT RECORD 1
         DITTRACE ID=NEWHEAD,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         CLM   R1,3,DYNHHEAD       TIME FOR NEW CYLINDER?
         BL    DASD0240            NO
DASD0230 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,IOBCC          CURRENT CYLINDER NUMBER
         LA    R1,1(R1)            NEXT CYLINDER
         STCM  R1,3,IOBCC          SAVE CYLINDER NUMBER
         XC    IOBHH,IOBHH         SET HEAD TO ZERO
         MVI   IOBR,1              START OVER AT RECORD 1
         DITTRACE ID=NEWCYL,DATA1=IOBCC
         MVC   COMMDADR,IOBCC      SAVE DISK ADDRESS
         CLM   R1,3,DYNHCYL        BEYOND MAX CYLINDER?
         BNL   DASD0500            YES.. WHOA!
DASD0240 DS    0H
         CLC   DYNHCYL,IOBCC       INVALID CYLINDER
         BL    DASD0430            YES
         CLC   DYNHHEAD,IOBHH      INVALID DISK ADDRESS?
         BL    DASD0430            YES
DASD0250 DS    0H
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             .. SUBTRACT 1 FROM RECORD NUMBER
         BCTR  R1,0                   BECAUSE WE ALWAYS DO A READ
         STC   R1,IOBR                COUNT, KEY, AND DATA
         DITTRACE ID=DASDIO,       TRACE I/O'S                         +
               DATA1=IOBCC         .. DISK ADDRESS USED FOR I/O
         XC    COMMKEY,COMMKEY     CLEAR KEY'S ADDRESS
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETER LIST
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         SR    R1,R1               CLEAR REGISTER
         IC    R1,IOBR             .. ADD 1 BACK TO RECORD NUMBER
         LA    R1,1(R1)               BECAUSE WE ALWAYS DO A READ
         STC   R1,IOBR                COUNT, KEY, AND DATA
         CLI   EXCPSTAT,$EXCPRNF   RECORD NOT FOUND?
         BE    DASD0220            YES, TRY NEXT HEAD
         L     R1,ACOMMIOI         I/O AREA ADDRESS
         MVC   COMMCNT,0(R1)       MOVE CKD COUNT INFO TO COMM
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL?
         BNE   DASD0400            NO..
         CLC   CNTADDR,IOBCC       DID I READ THE RECORD I WANTED?
         BL    DASD0220            NO.. TRACK WRAPPED
         OI    COMMIFLG,$NEWBLOK   TURN ON NEW BLOCK INDICATOR
         MVC   DASDREM,CNTDATAL    SET DATA LENGTH
         LA    R1,COMMCNTL(R1)     COUNT IS NOT PART OF RECORD'S DATA
         SR    R0,R0               CLEAR REG 0
         ICM   R0,1,CNTKEYL        KEY LENGTH
         BZ    DASD0260            NO KEY
         DITTRACE ID=KEYADDR,      KEYED RECORD                        +
               RDATA1=R1
         ST    R1,COMMKEY          SET KEY'S ADDRESS
         AR    R1,R0               PLUS KEY'S LENGTH
DASD0260 DS    0H
         DITTRACE ID=BLOKADDR,     PHYSICAL DATA'S ADDRESS             +
               RDATA1=R1,          .. DATA ADDRESS                     +
               DATA2=DASDREM       .. REMAINING LENGTH
         ST    R1,DASDCRCD         SET DATA'S ADDRESS
         BR    R8                  RETURN
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
DASD0400 DS    0H
         CLC   CNTADDR,IOBCC       RECORD ADDRESS PRESENT IN I/O AREA?
         BNE   DASD0410            NO
         OC    CNTL,CNTL           KEY LENGTH AND DATA LENGTH = ZERO?
         BZ    DASD0520            YES.. EOF RECORD
DASD0410 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=READERR       TRACE READ ERRORS
         ABEND ABEND018,DUMP,,USER
DASD0420 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND019,DUMP,,USER
DASD0430 DS    0H
         DITTRACE ID=BADADDR       BAD DISK ADDRESS
         TM    COMMFLAG,$COMM1ST   FIRST PASS?
         BO    DASD0440            YES... USER ENTERED A BAD ADDRESS
         ABEND ABEND020,DUMP,,USER
DASD0440 DS    0H
         DITTRACE ID=USERBAD       BAD DISK ADDRESS
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DASD0450            YES
         MVC   PRTDATA(DASDM02L),DASDM02
         MVI   PRTCMMD,$PRTCMD     PRINT COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT ENTRY POINT
         BALR  R14,R15             PRINT BAD ADDRESS MESSAGE
         CLI   COMMENV,$ENVJOB     BATCH ENVIRONMENT?
         BE    DASD9900            YES EXIT
         WTO   'INVALID DISK ADDRESS (CYLINDER OR HEAD NUMBER TOO HIGH)+
               ',CONSID=COMMCID
         B     DASD9900            AND EXIT
DASD0450 DS    0H
         MVC   MSG01(DASDM02L),DASDM02
         B     DASD9900            AND EXIT
DASD0460 DS    0H
         DITTRACE ID=ZEROCRCD      CURRENT RECORD ADDRESS WAS ZERO
         ABEND ABEND024,DUMP,,USER
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
DASD0500 DS    0H
         DITTRACE ID=EOV           END OF VOLUME
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    DASD0510            YES
         MVC   PRTDATA(DASDM01L),DASDM01
         MVI   PRTCMMD,$PRTDATA    PRINT COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT ENTRY POINT
         BALR  R14,R15             PRINT EOV MESSAGE
         CLI   COMMENV,$ENVSTC     STC ENVIRONMENT?
         BNE   DASD0530            NO
         WTO   'END OF VOLUME REACHED, COMMAND HALTED',                +
               CONSID=COMMCID      .. CONSOLE ID
         B     DASD0530            SET EOF AND EXIT
DASD0510 DS    0H
         MVC   MSG01(DASDM01L),DASDM01
         B     DASD0530
DASD0520 DS    0H
         DITTRACE ID=EOFRECD
         MVI   DYNSTAT,$DYNEOF     SET EOF RECORD READ
         TM    COMMPFLG,$COMMRCS   'RECORDS' IN EFFECT?
         BNO   DASD0530            NO
         DITTRACE ID=FORCEOF       EOF FORCED BY EOF RECORD
         OI    COMMFLAG,$COMMFE    EOF FORCED BY EOF RECORD
DASD0530 DS    0H
         DITTRACE ID=EOF
         OI    COMMFLAG,$COMMEOF   SIGNAL EOF
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
DASD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
SRCHCCW  CCW   X'31',*,CC,5        SEARCH FOR RECORD BY RECORD ID
TICCCW   CCW   X'08',SRCHCCW,0,8   TRANSFER IN CHANNEL
READCCW  CCW   X'1E',*,SILI,65535  READ COUNT, KEY, AND DATA
CCWL     EQU   *-SRCHCCW           CCW STRING LENGTH
DASDSAVE DC    18F'0'              REGISTER SAVE AREA
DASDCRCD DC    A(0)                CURRENT RECORD ADDRESS
H4       DC    H'4'                CONSTANT
DASDREM  DC    H'0'                LENGTH REMAINING IN RECORD
DASDRCDS DC    PL5'0'              RECORD COUNTER
VTOCPTRA DC    X'0000000003'       VTOC POINTER DISK ADDRESS
DASDM01  DC    C'END OF VOLUME REACHED, COMMAND HALTED'
DASDM01L EQU   *-DASDM01
DASDM02  DC    C'DISK ADDRESS INVALID (CYLINDER OR HEAD NUMBER TOO HIGH+
               )'
DASDM02L EQU   *-DASDM02
         LTORG
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE                                *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
CC       EQU   X'40'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
*--------------------------------------------------------------------*
*                                                                    *
*              VTOC POINTER RECORD                                   *
*                                                                    *
*--------------------------------------------------------------------*
VTOCPTR  DSECT
VTOCVOL1 DS   CL4                VOL1 RECORD IDENTIFIER 'VOL1'
VTOCSER  DS   CL6                VOLSER
         DS   X
VTOCADDR DS   XL5                VTOC'S DISK ADDRESS
         DS   CL64
*--------------------------------------------------------------------*
*                                                                    *
*              FORMAT 4 LABEL                                        *
*                                                                    *
*--------------------------------------------------------------------*
FMT4     DSECT
         IECSDSL1  4             GENERATE FORMAT 4 RECORD MAP
         END  DITTDAI1
./ ADD NAME=DITTEXCP
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE                                        *
*                                                                    *
*       THIS MODULE INITIATES ALL EXCP I/O.  THE CCW STRING WILL     *
*       ALREADY HAVE BEEN PLACED IN THE DYNAMIC BLOCK (DYNCCW).      *
*       THIS MODULE REMOVES THE DUPLICATION OF THE EXCP AND THE      *
*       ERROR CHECKING.                                              *
*                                                                    *
*--------------------------------------------------------------------*
DITTEXCP DITTPRFX EXCPSAVE,'EXCP INTERFACE'
         USING DITTCOMM,R11        SPECIFY BASE
         LR    R10,R1              SAVE PARAMETER BLOCK ADDRESS
         USING EXCPBLOK,R10        DEFINE ADDRESSABILITY
         DITTRACE ID=ENTRY         TRACE ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*                    BUILD IOB AND INITIATE I/O                       *
*                                                                     *
*---------------------------------------------------------------------*
EXCP0010 DS    0H
         L     R2,EXCPDYN          DYNAMIC BLOCK IN USE
         USING DYNBLOK,R2          DEFINE DYNBLOK ADDRESSABILITY
         LA    R3,DYNIOB           IOB WITHIN DYNBLOK
         USING IOB,R3              DEFINE DSECT BASE
         CLC   EXCPTAPE,DYNDDNAM   IS THIS A TAPE I/O?
         BNE   EXCP0020            NO
         TM    DYNSTAT,$DYNPEOT    TAPE AT PHYSICAL EOT?
         BNO   EXCP0020            NO.. ANYTHING IS ACCEPTABLE
         CLI   DYNCCW,X'07'        REWIND CCW?
         BE    EXCP0020            YES.. ALLOWABLE
         CLI   DYNCCW,X'0F'        REWIND/UNLOAD CCW?
         BNE   EXCP0200            NO.. DON'T ALLOW
EXCP0020 DS    0H
         DITTRACE ID=STARTIO       TRACE I/O'S
         MVI   DYNSTAT,0           CLEAR ALL INDICATORS
         LA    R1,DYNCCW           CCW STRING ADDRESS
         ST    R1,IOBSTART         INSERT CCW STRING ADDRESS
         LA    R1,DYNECB           INPUT FILE ECB
         STCM  R1,7,IOBECBPB       INSERT ECB ADDRESS
         LA    R1,DYNDCB           DCB FOR THIS IOB
         ST    R1,IOBDCBPT         INSERT DCB ADDRESS
         XC    DYNECB,DYNECB       CLEAR ECB
         EXCP  IOBSTDRD            START THE I/O
         WAIT  ECB=DYNECB          WAIT FOR I/O COMPLETION
         CLI   DYNECB,X'44'        SHOULD I RETRY
         BE    EXCP0020            YES
         CLI   DYNECB,X'7F'        I/O SUCCESSFUL??
         BNE   EXCP0080            NO
         SR    R0,R0               CLEAR REGISTER
         SR    R1,R1               CLEAR REGISTER
         CLC   EXCPTAPE,DYNDDNAM   IS THIS A TAPE I/O?
         BE    EXCP0050            YES
         LA    R14,DYNCCW1         FIRST CCW
         LA    R15,8               NUMBER OF CCW'S POSSIBLE
EXCP0030 DS    0H
         CLI   0(R14),X'06'        READ DATA?
         BE    EXCP0040            YES
         CLI   0(R14),X'86'        READ DATA?
         BE    EXCP0040            YES
         CLI   0(R14),X'0E'        READ KEY AND DATA?
         BE    EXCP0040            YES
         CLI   0(R14),X'8E'        READ KEY AND DATA?
         BE    EXCP0040            YES
         CLI   0(R14),X'1E'        READ COUNT, KEY, AND DATA?
         BE    EXCP0040            YES
         CLI   0(R14),X'9E'        READ COUNT, KEY, AND DATA?
         BE    EXCP0040            YES
         LA    R14,L'DYNCCW1(R14)  NEXT CCW
         BCT   R15,EXCP0030        CHECK NEXT CCW
         B     EXCP0070            NO LENGTH FROM THIS I/O
EXCP0040 DS    0H
         ICM   R0,3,6(R14)         LENGTH FROM CCW
         B     EXCP0060
EXCP0050 DS    0H
         CLI   DYNCCW,X'02'        IS THIS A 'READ FORWARD' CCW?
         BNE   EXCP0070            NO
         ICM   R0,3,DYNCCW1+6      LENGTH FROM CCW
EXCP0060 DS    0H
         ICM   R1,3,IOBSTDRD+14    RESIDUAL LENGTH
         SR    R0,R1               MINUS RESIDUAL LENGTH
         STH   R0,EXCPLEN          SET LENGTH ACTUALLY READ
         DITTRACE ID=DATALEN,      TRACE POINT                         +
               RDATA1=R0           .. CAPTURE DATA LENGTH
EXCP0070 DS    0H
         DITTRACE ID=GOODIO,       I/O SUCCESSFUL                      +
               DATA1=IOBFLAG1,     .. SAVE IOBFLAG1                    +
               DATA2=IOBFLAG3      .. SAVE IOBFLAG3
         MVI   EXCPSTAT,$EXCPOK    SET STATUS
         B     EXCP9900            AND EXIT
EXCP0080 DS    0H
         DITTRACE ID=IOERROR,      TRACE I/O ERRORS                    +
               DATA1=IOBFLAG1,     .. SAVE IOBFLAG1                    +
               DATA2=IOBFLAG3      .. SAVE IOBFLAG3
         TM    IOBFLAG1,IOBIOERR   UNIT EXCEPTION?
         BO    EXCP0090            YES ...
         LA    R1,SENSECCW         SENSE CCW ADDRESS
         ST    R1,IOBSTART         INSERT SENSE CCW ADDRESS
         XC    DYNECB,DYNECB       CLEAR ECB
         EXCP  IOBSTDRD            START THE SENSE I/O
         WAIT  ECB=DYNECB          WAIT FOR I/O COMPLETION
         CLI   DYNECB,X'7F'        SENSE SUCCESSFUL?
         BNE   EXCP0310            NO..
         DITTRACE ID=ERRSENSE,     TRACE SENSE DATA                    +
               DATA1=SENSE,        .. BYTES 0-7                        +
               DATA2=SENSE+8       .. BYTES 8-15
         ABEND ABEND010,DUMP,,USER ABEND
EXCP0090 DS    0H
         CLC   EXCPTAPE,DYNDDNAM   WAS THIS A TAPE I/O?
         BE    EXCP0110            YES
         TM    IOBSENS1,X'08'      RECORD NOT FOUND?
         BO    EXCP0100            YES
         OI    EXCPSTAT,$EXCPIOE   I/O ERROR
         B     EXCP9900            AND EXIT
EXCP0100 DS    0H
         OI    EXCPSTAT,$EXCPRNF   RECORD NOT FOUND
         B     EXCP9900            AND EXIT
EXCP0110 DS    0H
         DITTRACE ID=UNITEX        UNIT EXCEPTION
         LA    R1,SENSECCW         SENSE CCW ADDRESS
         ST    R1,IOBSTART         INSERT SENSE CCW ADDRESS
         XC    DYNECB,DYNECB       CLEAR ECB
         EXCP  IOBSTDRD            START THE SENSE I/O
         WAIT  ECB=DYNECB          WAIT FOR I/O COMPLETION
         DITTRACE ID=UXSENSE,      TRACE SENSE DATA FOR UNIT EXCEPTION +
               DATA1=SENSE,        .. BYTES 0-7                        +
               DATA2=SENSE+8       .. BYTES 8-15
         CLI   DYNECB,X'7F'        SENSE SUCCESSFUL?
         BNE   EXCP0320            NO..
         CLI   SENSE04,$S4TPIND    TAPE INDICATE (PHYSICAL EOT)?
         BE    EXCP0140            YES
         LA    R1,DYNCCW           FIRST CCW
EXCP0120 DS    0H
         CLI   0(R1),X'01'         WRITE CCW?
         BE    EXCP0150            YES
         CLI   0(R1),X'1F'         WRITE TAPE-MARK CCW?
         BE    EXCP0150            YES
         TM    4(R1),CHAINCMD      CHAINED COMMANDS?
         BNO   EXCP0130            NO
         LA    R1,8(R1)            NEXT CCW
         B     EXCP0120            CHECK ALL CCW'S
EXCP0130 DS    0H
*
*
*      IF ANYONE CAN FIGURE OUT HOW TO DETECT THE RESET KEY, I WOULD
*      CERTAINLY LIKE TO HEAR FROM YOU.
*
*      DITTO SHOULD PROBABLY BE SMARTER AND TEST FOR OTHER CONDITIONS
*      BEFORE ASSUMING THE EXCEPTION WAS FOR A TAPE MARK.
*
*
         DITTRACE ID=TAPEMARK      TRACE TAPE MARK FOUND
         MVI   DYNSTAT,$DYNTM      INDICATE TAPE MARK
         B     EXCP9900            EXIT.. STATUS TM
EXCP0140 DS    0H
         DITTRACE ID=TAPEIND       TRACE PHYSICAL EOT
         MVI   DYNSTAT,$DYNPEOT+$DYNTM  SET TAPE MARK+PEOT FLAGS
         B     EXCP9900            EXIT.. STATUS PHYSICAL EOT
EXCP0150 DS    0H
         TM    SENSE01,$S1FILEP    FILE PROTECT ON?
         BNO   EXCP0160            NO
         DITTRACE ID=FILEPROT      FILE PROTECT IS ON
         MVI   DYNSTAT,$DYNPROT    TAPE IS FILE PROTECTED
         B     EXCP9900            EXIT
EXCP0160 DS    0H
         DITTRACE ID=WRITERR       I/O ERROR DURING WRITE OR WTM
         OI    EXCPSTAT,$EXCPIOE   I/O ERROR
         B     EXCP9900            AND EXIT
EXCP0200 DS    0H
         DITTRACE ID=NOTALLOW      I/O NOT ALLOWED WHILE AT PEOT
         MVI   EXCPSTAT,$EXCPNA    SET NOT ALLOWED STATUS
         B     EXCP9900
EXCP0300 DS    0H
         ABEND ABEND031,DUMP,,USER ABEND
EXCP0310 DS    0H
         ABEND ABEND032,DUMP,,USER ABEND
EXCP0320 DS    0H
         ABEND ABEND033,DUMP,,USER ABEND
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
EXCP9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
SENSECCW CCW   4,SENSE,SILI,SENSEL SENSE CCW
EXCPSAVE DC    18F'0'              REGISTER SAVE AREA
EXCPTAPE DC    CL4'TAPE'           CONSTANT
SENSE    DS    0C
SENSE00  DC    X'00'               SENSE BYTE 0
$S0CREJ  EQU   X'80'               .. COMMAND REJECT
$S0IVREQ EQU   X'40'               .. INTERVENTION REQUIRED
$S0BUSO  EQU   X'20'               .. BUS OUT
$S0EQCHK EQU   X'10'               .. EQUIPMENT CHECK
$S0DACHK EQU   X'08'               .. DATA CHECK
$S0DOVRN EQU   X'04'               .. DATA OVER-RUN
$S0WDCT0 EQU   X'02'               .. WORD COUNT ZERO
$S0DCNVC EQU   X'01'               .. DATA CONVERTER CHECK
*---------------------------------------------------------------------*
*           3480 CODES ARE THE SAME WITH THE FOLLOW EXCEPTIONS        *
*---------------------------------------------------------------------*
$S0UCTIM EQU   X'02'               .. UNIT CHECK TIMING
$S0ASSGN EQU   X'01'               .. UNIT IS ASSIGNED ELSEWHERE
         SPACE 3
SENSE01  DC    X'00'               SENSE BYTE 1
$S1NOISE EQU   X'80'               .. NOISE
$S1TUSTA EQU   X'40'               .. TAPE UNIT STATUS A
$S1TUSTB EQU   X'20'               .. TAPE UNIT STATUS B
$S17TRK  EQU   X'10'               .. 7 TRACK TAPE UNIT
$S1LDPT  EQU   X'08'               .. LOAD POINT
$S1WRTST EQU   X'04'               .. WRITE STATUS
$S1FILEP EQU   X'02'               .. FILE PROTECT
$S1NCPBL EQU   X'01'               .. NOT CAPABLE
*---------------------------------------------------------------------*
*           3480 CODES ARE THE SAME WITH THE FOLLOW EXCEPTIONS        *
*---------------------------------------------------------------------*
$S1LBLKF EQU   X'80'               .. LOCATE BLOCK FAILED
$S1ON    EQU   X'40'               .. DRIVE ONLINE TO CONTROL UNIT
$S1SEQ   EQU   X'10'               .. RECORD OUT OF SEQUENCE
         SPACE 3
SENSE02  DC    X'00'               SENSE BYTE 2
*---------------------------------------------------------------------*
*           3480 CODES                                                *
*---------------------------------------------------------------------*
$S2PATH  EQU   X'20'               PATHING
$S2ADAPT EQU   X'10'               0=CU0, 1=CU1
$S2MICRO EQU   X'08'               MICRO PROCESSOR: 0=CU0, 1=CU1
$S2BLOCK EQU   X'01'               BLOCK ID POSITIONAL INDICATOR
         SPACE 3
SENSE03  DC    X'00'               SENSE BYTE 3
$S3RWRC  EQU   X'80'               .. READ/WRITE VERT. REDUNDANCY CHECK
$S3MTES  EQU   X'40'               .. MULTI-TRACK ERROR/SKEW
$S3END   EQU   X'20'               .. END
$S3VCC   EQU   X'10'               .. VERTICAL/CYCLIC CHECK
$S31600C EQU   X'08'               .. 1600 ENVIRONMENT CHECK
$S3BBSCU EQU   X'04'               .. BACKWARD BPI SET IN CONTROL UNIT
$S3CP    EQU   X'02'               .. CENTRAL PROCESSOR
$S3CMP   EQU   X'01'               .. COMPARE
         SPACE 3
SENSE04  DC    X'00'               SENSE BYTE 4
$S4ALUMP EQU   X'80'               .. ALU/MP HARDWARE ERROR
$S4REJTU EQU   X'40'               .. REJECT TAPE UNIT
$S4TPIND EQU   X'20'               .. TAPE INDICATE
$S4WTVCR EQU   X'10'               .. WRITE TRIGGER VERT. REDUNDANCY CK
$S4MICRO EQU   X'08'               .. MICRO PROGRAM DETECTED ERROR
$S4LWRD  EQU   X'04'               .. LOOP WRITE TO READ ERROR
$S4TUCHK EQU   X'02'               .. TAPE UNIT CHECK
$S4RSV1  EQU   X'01'               .. RESERVED
         SPACE 3
SENSE05  DC    X'00'               SENSE BYTE 5
$S5NSUB1 EQU   X'80'               .. NEW SUBSYSTEM
$S5NSUB2 EQU   X'40'               .. NEW SUBSYSTEM
$S5WTMCK EQU   X'20'               .. WRITE TAPE MARK CHECK
$S5IDBST EQU   X'10'               .. ID BURST
$S5SRDCK EQU   X'08'               .. START READ CHECK
$S5PART  EQU   X'04'               .. PARTIAL RECORD
$S5EPATM EQU   X'02'               .. EXCESSIVE POST-AMBLE OR TAPE MARK
$S5RPQ   EQU   X'01'               .. RPQ
         SPACE 3
SENSE06  DC    X'00'               SENSE BYTE 6
$S67TRK  EQU   X'80'               .. 7 TRACK
$S6WRTCF EQU   X'40'               .. WRITE CURRENT FAILURE
$S6DDEN  EQU   X'20'               .. DUAL DENSITY
$S6N1600 EQU   X'10'               .. DENSITY NOT 1600
$S6RSV5  EQU   X'08'               .. RESERVED
$S6RSV6  EQU   X'04'               .. RESERVED
$S6RSV7  EQU   X'02'               .. RESERVED
$S6RSV8  EQU   X'01'               .. RESERVED
         SPACE 3
SENSE07  DC    X'00'               SENSE BYTE 7
$S7LAMP  EQU   X'80'               .. LAMP FAILURE
$S7BOTL  EQU   X'40'               .. TAPE BOTTOM LEFT
$S7BOTR  EQU   X'20'               .. TAPE BOTTOM RIGHT
$S7RESET EQU   X'10'               .. RESET KEY
$S7SECUR EQU   X'08'               .. DATA SECURITY ERASE
$S7ERASE EQU   X'04'               .. ERASE HEAD
$S7AIR   EQU   X'02'               .. AIR BEARING FAILURE
$S7LOAD  EQU   X'01'               .. LOAD FAILURE
         SPACE 3
SENSE08  DC    X'00'               SENSE BYTE 8
$S8IBG   EQU   X'80'               .. INTERBLOCK GAP DETECTED IN WRITE
$S8FEED  EQU   X'40'               .. FEAD THROUGH
$S8RSV1  EQU   X'20'               .. RESERVED
$S8EBRDC EQU   X'10'               .. EARLY BEGIN READ-BACK CHECK
$S8EERBC EQU   X'08'               .. EARLY END READ-BACK CHECK
$S8SBRBC EQU   X'04'               .. SLOW BEGIN READ-BACK CHECK
$S8SERBC EQU   X'02'               .. SLOW END READ-BACK CHECK
$S8VELRR EQU   X'01'               .. VELOCITY RETRY/RESTART
         SPACE 3
SENSE09  DC    X'00'               SENSE BYTE 9
$S96250  EQU   X'80'               .. 6250 ERROR CORRECTION
$S9VELCH EQU   X'40'               .. VELOCITY CHANGE WHILE WRITING
$S9CHBUF EQU   X'20'               .. CHANNEL BUFFER CHECK
$S9CRC   EQU   X'10'               .. CYCLIC REDUNDANCY CHECK III
$S938032 EQU   X'08'               .. 3803-2
$S9RSV1  EQU   X'04'               .. RESERVED
$S9RSV2  EQU   X'02'               .. RESERVED
$S9RSV3  EQU   X'01'               .. RESERVED
         SPACE 3
SENSE10  DC    X'00'               SENSE BYTE 10
$S10CMDR EQU   X'80'               .. COMMAND STATUS REJECT
$S10RSV1 EQU   X'40'               .. RESERVED
$S10CTLR EQU   X'20'               .. CONTROL STATUS REJECT
$S10NBLK EQU   X'10'               .. NO BLOCK ON RECORD READ-BACK
$S10NWTM EQU   X'08'               .. WTM NOT DETECTED
$S10TACH EQU   X'04'               .. TACHOMETER START FAILURE
$S10RSV2 EQU   X'02'               .. RESERVED
$S10VEL  EQU   X'01'               .. VELOCITY
         SPACE 3
SENSE11  DC    X'00'               SENSE BYTE 11  (PROCESSOR 1 ERRORS)
$S11BPAR EQU   X'80'               .. BUS PARITY LCL STRG REG ADDR ERR
$S11RSV1 EQU   X'40'               .. RESERVED
$S11ROSP EQU   X'20'               .. LOW READ-ONLY STORAGE PARITY MP1
$S11DCOD EQU   X'10'               .. INSTRUCTION DECODE ERROR MP1
$S11MP   EQU   X'08'               .. MICRO PROGRAM DETECTED ERROR MP1
$S11DBP  EQU   X'04'               .. DATA BUS PARITY CHECK MP1
$S11RSV2 EQU   X'02'               .. RESERVED
$S11BOC  EQU   X'01'               .. BRANCH ON CONDITION MP1
         SPACE 3
SENSE12  DC    X'00'               SENSE BYTE 12 (PROCESSOR 2 ERRORS)
$S12BPAR EQU   X'80'               .. BUS PARITY LCL STRG REG ADDR ERR
$S12RSV1 EQU   X'40'               .. RESERVED
$S12ROSP EQU   X'20'               .. LOW READ-ONLY STORAGE PARITY MP2
$S12DCOD EQU   X'10'               .. INSTRUCTION DECODE ERROR MP2
$S12MP   EQU   X'08'               .. MICRO PROGRAM DETECTED ERROR MP2
$S12DBP  EQU   X'04'               .. DATA BUS PARITY CHECK MP2
$S12RSV2 EQU   X'02'               .. RESERVED
$S12BOC  EQU   X'01'               .. BRANCH ON CONDITION MP2
         SPACE 3
SENSE13  DC    X'00'               SENSE BYTE 13
$S13RSV1 EQU   X'80'               .. RESERVED
$S13RSV2 EQU   X'40'               .. RESERVED
$S13RSV3 EQU   X'20'               .. RESERVED
$S13RSV4 EQU   X'10'               .. RESERVED
$S13RSV5 EQU   X'08'               .. RESERVED
$S13RSV6 EQU   X'04'               .. RESERVED
$S13RSV7 EQU   X'02'               .. RESERVED
$S13RSV8 EQU   X'01'               .. RESERVED
         SPACE 3
SENSE14  DC    X'00'               SENSE BYTE 14
$S14RSV1 EQU   X'80'               .. RESERVED
$S14RSV2 EQU   X'40'               .. RESERVED
$S14RSV3 EQU   X'20'               .. RESERVED
$S14RSV4 EQU   X'10'               .. RESERVED
$S14RSV5 EQU   X'08'               .. RESERVED
$S14RSV6 EQU   X'04'               .. RESERVED
$S14RSV7 EQU   X'02'               .. RESERVED
$S14RSV8 EQU   X'01'               .. RESERVED
         SPACE 3
SENSE15  DC    X'00'               SENSE BYTE 15
$S15RSV1 EQU   X'80'               .. RESERVED
$S15RSV2 EQU   X'40'               .. RESERVED
$S15RSV3 EQU   X'20'               .. RESERVED
$S15RSV4 EQU   X'10'               .. RESERVED
$S15RSV5 EQU   X'08'               .. RESERVED
$S15RSV6 EQU   X'04'               .. RESERVED
$S15RSV7 EQU   X'02'               .. RESERVED
$S15RSV8 EQU   X'01'               .. RESERVED
SENSEL   EQU   *-SENSE
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP STATUS BLOCK                                     *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
CHAINCMD EQU   X'40'
SILI     EQU   X'20'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              ABEND CODES                                           *
*                                                                    *
*--------------------------------------------------------------------*
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTEXCP
./ ADD NAME=DITTINIT
*--------------------------------------------------------------------*
*                                                                    *
*                       INITIALIZATION                               *
*                                                                    *
*      THE FUNCTION OF THE INITIALIZATION MODULE IS TO ACQUIRE       *
*      STORAGE FOR THE TRACE TABLE, INPUT I/O AREA, OUTPUT I/O AREA, *
*      AND DETERMINE THE ENVIRONMENT.                                *
*                                                                    *
*      IF THE ENVIRONMENT IS 'JOB' OR 'STC', CONTROL WILL BE GIVEN   *
*      TO 'DITTMAIN', THE BATCH JOB AND STC MAINLINE.  IF THE        *
*      ENVIRONMENT IS 'TSO', CONTROL WILL BE GIVEN TO 'DITTTSOM',    *
*      THE TSO ENVIRONMENT MAINLINE.                                 *
*                                                                    *
*--------------------------------------------------------------------*
DITTINIT DITTPRFX INITSAVE,'INITIALIZATION MODULE'
         L     R11,VCOMM           COMMON MODULE ADDRESS
         USING DITTCOMM,R11        SPECIFY BASE
         GETMAIN RC,LV=TRSIZE      GETMAIN TRACE TABLE AREA
         LTR   R15,R15             GETMAIN SUCCESSFUL??
         BNZ   ERR0000             NO
         ST    R1,INITATRC         SAVE TRACE AREA ADDRESS
         LA    R1,32(R1)           ADD 32
         SRL   R1,5                SHIFT OUT LOW BIT(S)
         SLL   R1,5                32 BYTE BOUNDARY
         ST    R1,COMM1ST          INITIALIZE TOP OF TABLE ADDRESS
         ST    R1,COMMCURR         INITIALIZE CURRENT ENTRY ADDRESS
         LA    R1,3168(R1)         LAST ENTRY'S ADDRESS
         ST    R1,COMMLAST         INITIALIZE LAST ENTRY'S ADDRESS
         L     R2,ASTAE            STAE EXIT ENTRY POINT
*        ESTAE (R2),               SET ESTAE
*              CT,                 .. CREATING NEW ESTAE
*              PARAM=(R11),        .. ESTAE WILL NEED COMM AREA ADDRESS
*              XCTL=YES,           .. KEEP ESTAE SET ACROSS XCTL
*              PURGE=NONE,         .. TRY NOT TO PURGE I/O REQUESTS
*              ASYNCH=YES,         .. ALLOW ASYNCHRONOUS EVENTS
*              TERM=YES            .. ESTAE ACTIVE FOR X22 ABENDS
         LA    R2,COMMCCOM         EXTRACT RESULT AREA
         EXTRACT (R2),'S',FIELDS=(COMM,ASID)
         LOCASCB ASID=COMMASID+2   LOCATE ASCB FOR OUR ADDRESS SPACE
         LTR   R1,R1               WAS LOCATE SUCCESSFUL??
         BM    INIT0030            NO, ASSUME JOB
         DITTRACE ID=ASID,         ASID DETERMINED                     +
               RDATA1=R1           .. CAPTURE ASCB ADDRESS
         USING ASCB,R1             DEFINE BASE
         L     R15,ASCBJBNI        JOB NAME ADDRESS
         LA    R15,0(R15)          CLEAR HIGH BIT(S)
         LTR   R15,R15             JOB NAME PRESENT??
         BNZ   INIT0030            YES
         L     R2,ASCBOUCB         OUCB ADDRESS FROM ASCB
         L     R15,AINIT10         31-BIT CODE STARTING POINT
         BSM   R0,R15              SET AMODE TO 31
INIT0010 DS    0H
         USING OUCB,R2             DEFINE OUCB ADDRESSABILITY
         TM    OUCBYFL,OUCBSTT     STARTED TASK?
         BO    INIT0040            YES
         TM    OUCBYFL,OUCBLOG     TSO USER?
         BO    INIT0050            YES
         LA    R15,INIT0020        24-BIT CODE STARTING POINT
         BSM   R0,R15              SET AMODE TO 24
INIT0020 DS    0H
         WTO   'UNSUPPORTED ENVIRONMENT'
         ABEND 1,DUMP,,USER
INIT0030 DS    0H
         DITTRACE ID=ENVJOB
         MVI   COMMENV,$ENVJOB     ENVIRONMENT IS BATCH JOB
         B     INIT0060
INIT0040 DS    0H
         DITTRACE ID=ENVSTC
         MVI   COMMENV,$ENVSTC     ENVIRONMENT IS 'STARTED TASK'
         B     INIT0060
INIT0050 DS    0H
         DITTRACE ID=ENVTSO
         MVI   COMMENV,$ENVTSO     ENVIRONMENT IT 'TSO USER'
INIT0060 DS    0H
         LA    R15,INIT0070        24-BIT CODE STARTING POINT
         BSM   R0,R15              SET AMODE TO 24
INIT0070 DS    0H
         GETMAIN RC,LV=65535       ACQUIRE INPUT I/O AREA
         LTR   R2,R15              GETMAIN SUCCESSFUL?
         BNZ   ERR0010             NO
         ST    R1,ACOMMIOI         SET INPUT I/O AREA ADDRESS
         GETMAIN RC,LV=65535       ACQUIRE OUTPUT I/O AREA
         LTR   R2,R15              GETMAIN SUCCESSFUL?
         BNZ   ERR0020             NO
         ST    R1,ACOMMIOO         SET OUTPUT I/O AREA ADDRESS
         DITTRACE ID=ENV,          TRACE ENVIRONMENT DETERMINED        +
               DATA1=COMMENV       .. ENVIRONMENT FLAG
         CLI   COMMENV,$ENVTSO     RUNNING AS A TSO USER?
         BE    TSO0000             YES
         L     R15,AMAIN           BATCH/STC MAINLINE ENTRY POINT
         BALR  R14,R15             LINK TO BATCH/STC MAINLINE
         B     INIT9900            AND EXIT
TSO0000  DS    0H
         L     R15,ATSOM           TSO MAINLINE ENTRY POINT
         BALR  R14,R15             LINK TO TSO MAINLINE
INIT9900 DS    0H
         DITTRACE ID=EXIT          TRACE
         ESTAE 0                   TERMINATE ESTAE EXIT
         ICM   R1,15,INITATRC      TRACE AREA ADDRESS
         BZ    INIT9910            NOT ACQUIRED
         FREEMAIN R,A=(1),LV=TRSIZE FREEMAIN TRACE AREA
         ICM   R1,15,ACOMMIOI      INPUT AREA ADDRESS
         BZ    INIT9910            NOT ACQUIRED
         FREEMAIN R,A=(1),LV=65535 FREEMAIN INPUT AREA
         ICM   R1,15,ACOMMIOO      OUTPUT AREA ADDRESS
         BZ    INIT9910            NOT ACQUIRED
         FREEMAIN R,A=(1),LV=65535 FREEMAIN OUTPUT AREA
INIT9910 DS    0H
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
ERR0000  DS    0H
         ABEND ABEND004,DUMP,,USER GETMAIN FOR TRACE TABLE FAILED
ERR0010  DS    0H
         ABEND ABEND005,DUMP,,USER GETMAIN FOR INPUT I/O AREA FAILED
ERR0020  DS    0H
         ABEND ABEND006,DUMP,,USER GETMAIN FOR OUTPUT I/O AREA FAILED
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXTERNAL CSECT ADDRESSES                              *
*                                                                    *
*--------------------------------------------------------------------*
VCOMM    DC    V(DITTCOMM)         COMMON MODULE ADDRESS
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CONSTANTS                                             *
*                                                                    *
*--------------------------------------------------------------------*
AINIT10  DC    A(INIT0010+X'80000000')
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
*--------------------------------------------------------------------*
INITSAVE DC    9D'0'               REGISTER SAVE AREA
INITATRC DC    A(0)                TRACE TABLE AREA ADDRESS
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
TRSIZE   EQU  (500*32)
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         ABCODES
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              ADDRESS SPACE CONTROL BLOCK                           *
*                                                                    *
*--------------------------------------------------------------------*
         IHAASCB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              RESOURCE MANAGER USER CONTROL BLOCK                   *
*                                                                    *
*--------------------------------------------------------------------*
         IRAOUCB
         END
./ ADD NAME=DITTINQ
*--------------------------------------------------------------------*
*                                                                    *
*              STC MODE INQUIRY                                      *
*                                                                    *
*    WHEN RUNNING IN STC MODE, THIS MODULE PROVIDES THAT OPERATOR    *
*    WITH A WAY TO DISPLAY ALLOCATED TAPES, DASD'S, AND THE EXTERNAL *
*    TRACE STATUS.                                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTINQ  DITTPRFX INQSAVE,'STC MODE INQUIRE FUNCTION'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMINQ       .. INQUIRE PARAMETER
         TM    COMMFLAG,$COMMCLS   CLOSE-UP CALL?
         BO    INQ9910             YES.. JUST EXIT
         CLC   INQTAPE,COMMINQ     DISPLAY TAPES?
         BE    INQ0010             YES
         CLC   INQDASD,COMMINQ     DISPLAY DASD'S?
         BE    INQ0100             YES
         CLC   INQTRACE,COMMINQ    DISPLAY TRACE STATUS?
         BE    INQ0200             YES
         LA    R8,INQM01           INVALID INQUIRE REQUEST
         BAL   R9,WTO0000          DISPLAY MESSAGE AND PRINT IT
         B     INQ9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              LIST ALLOCATED TAPE DRIVES                            *
*                                                                    *
*--------------------------------------------------------------------*
INQ0010  DS    0H
         DITTRACE ID=TAPEINQ
         NI    INQFLAG,255-$INQFND RESET 'FOUND' FLAG
         ICM   R2,15,COMMDYHD      FIRST DYNAMIC BLOCK
         USING DYNBLOK,R2          DEFINE BASE
         BZ    INQ0300             NOTHING ON THE CHAIN
INQ0020  DS    0H
         CLC   INQTAPE,DYNDDNAM    IS THIS A TAPE?
         BNE   INQ0070             NO
         OI    INQFLAG,$INQFND     SET FOUND FLAG
         MVC   INQM02UNT,DYNDDNAM+4 COPY DEVICE NUMBER
         L     R1,DYNDEBA          DEB ADDRESS FROM DYNAMIC BLOCK
         USING DEB,R1              DEFINE BASE
         CLI   DEBSDVM,$DEN1600    1600 BPI?
         BE    INQ0030             YES
         CLI   DEBSDVM,$DEN6250    6250 BPI?
         BE    INQ0040             YES
         CLI   DEBSDVM,$DEN3480    3480 DRIVE?
         BE    INQ0050             YES
         MVC   INQM02TYP,INQUNKN   UNKNOWN DENSITY OR TYPE
         MVC   INQM02BPI,COMMBLKS  SET 'BPI' TO BLANKS
         B     INQ0060
INQ0030  DS    0H
         MVC   INQM02TYP,INQ1600   SET TYPE TO 1600
         MVC   INQM02BPI,INQBPI    SET 'BPI' IN MESSAGE
         B     INQ0060
INQ0040  DS    0H
         MVC   INQM02TYP,INQ6250   SET TYPE TO 6250
         MVC   INQM02BPI,INQBPI    SET 'BPI' IN MESSAGE
         B     INQ0060
INQ0050  DS    0H
         MVC   INQM02TYP,INQ3480   SET TYPE TO 3480
         MVC   INQM02BPI,COMMBLKS  SET 'BPI' TO BLANKS
INQ0060  DS    0H
         LA    R8,INQM02           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
INQ0070  DS    0H
         ICM   R2,15,DYNNEXT       NEXT BLOCK ON CHAIN
         BNZ   INQ0020             LOOP
         TM    INQFLAG,$INQFND     ANY TAPES FOUND?
         BNO   INQ0300             NO
         B     INQ9900             EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              LIST ALLOCATED DASD                                   *
*                                                                    *
*--------------------------------------------------------------------*
INQ0100  DS    0H
         DITTRACE ID=DASDINQ
         NI    INQFLAG,255-$INQFND RESET 'FOUND' FLAG
         ICM   R2,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    INQ0300             NOTHING ON THE CHAIN
INQ0110  DS    0H
         CLC   INQDASD,DYNDDNAM    IS THIS A DASD?
         BNE   INQ0120             NO
         OI    INQFLAG,$INQFND     SET FOUND FLAG
         MVC   INQM03UNT,DYNDDNAM+4 COPY DEVICE NUMBER
         MVC   INQM03VOL,DYNVOL    COPY VOLSER
         LA    R8,INQM03           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
INQ0120  DS    0H
         ICM   R2,15,DYNNEXT       NEXT BLOCK ON CHAIN
         BNZ   INQ0110             LOOP
         TM    INQFLAG,$INQFND     ANY TAPES FOUND?
         BNO   INQ0300             NO
         B     INQ9900             EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              DISPLAY EXTERNAL TRACE STATUS                         *
*                                                                    *
*--------------------------------------------------------------------*
INQ0200  DS    0H
         DITTRACE ID=INQTRACE
         TM    XTRFLAG,$XTRACE     EXTERNAL TRACE ACTIVE?
         BNO   INQ0210             ON
         LA    R8,INQM04           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT
         B     INQ9900             AND EXIT
INQ0210  DS    0H
         LA    R8,INQM05           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT
         B     INQ9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              NO UNITS OF REQUESTED TYPE ALLOCATED                  *
*                                                                    *
*--------------------------------------------------------------------*
INQ0300  DS    0H
         LA    R8,INQM06           MESSAGE'S ADDRESS
         BAL   R9,WTO0000          ISSUE WTO AND PRINT MESSAGE
         B     INQ9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              ISSUE WTO'S AND PRINT MESSAGE TEXT                    *
*                                                                    *
*--------------------------------------------------------------------*
WTO0000  DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,0(R8)          MESSAGE TEXT'S LENGTH
         BCTR  R1,0                ADJUST FOR EXECUTE'S
         EX    R1,PRTMVC           MOVE DATA TO PRINT AREA
         LA    R14,WTOL            LIST FORM WTO'S ADDRESS
         USING WPL,R14             DEFINE WPL BASE
         MVC   WPLTXT(50),COMMBLKS CLEAR MESSAGE IN WTO LIST FORM
         EX    R1,WTOMVC           MOVE TEXT TO WTO MESSAGE
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         WTO   CONSID=COMMCID,     ISSUE WTO                           +
               MF=(E,WTOL)
         BR    R9                  RETURN
PRTMVC   MVC   PRTDATA(0),2(R8)    MOVE MESSAGE TEXT
WTOMVC   MVC   WPLTXT(0),2(R8)     MOVE MESSAGE TEXT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
INQ9900  DS    0H
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
INQ9910  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
INQSAVE  DC    18F'0'              REGISTER SAVE AREA
WTOL     WTO   '....5...10....5...20....5...30....5...40....5...50',   +
               CONSID=,                                                +
               MF=L
INQFLAG  DC    X'00'               FLAGS/SWITCHES
$INQFND  EQU   X'80'               .. UNIT OF REQUEST TYPE FOUND
INQTAPE  DC    C'TAPE'
INQDASD  DC    C'DASD'
INQTRACE DC    C'TRACE'
INQ1600  DC    C'1600'
INQ6250  DC    C'6250'
INQ3480  DC    C'3480'
INQUNKN  DC    C'UNKN'
INQBPI   DC    C'BPI'
INQM01   DC    AL2(INQM01L)
INQM01M  DC    C'INVALID INQUIRE PARAMETER'
INQM01L  EQU   *-INQM01M
INQM02   DC    AL2(INQM02L)
INQM02M  DS    0C
         DC    C'TAPE UNIT '
INQM02UNT DC   CL04' '
         DC    C' IS ALLOCATED AS '
INQM02TYP DC   CL04' '
INQM02BPI DC   CL03' '
INQM02L  EQU   *-INQM02M
INQM03   DC    AL2(INQM03L)
INQM03M  DS    0C
         DC    C'DASD VOLUME '
INQM03VOL DC   CL06' '
         DC    C' IS ALLOCATED ON UNIT '
INQM03UNT DS   CL04' '
INQM03L  EQU   *-INQM03M
INQM04   DC    AL2(INQM04L)
INQM04M  DC    C'EXTERNAL TRACE IS ACTIVE'
INQM04L  EQU   *-INQM04M
INQM05   DC    AL2(INQM05L)
INQM05M  DC    C'EXTERNAL TRACE IS NOT ACTIVE'
INQM05L  EQU   *-INQM05M
INQM06   DC    AL2(INQM06L)
INQM06M  DC    C'NONE ALLOCATED'
INQM06L  EQU   *-INQM06M
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC UNITS BLOCK DSECT                             *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK  TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD  DSORG=PS
DYNDCBL  EQU   *-IHADCB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DEB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZDEB LIST=NO
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              WTO (WPL) DSECT                                       *
*                                                                    *
*--------------------------------------------------------------------*
         IEZWPL DSECT=YES
         END  DITTINQ
./ ADD NAME=DITTMAIN
*--------------------------------------------------------------------*
*                                                                    *
*                   BATCH JOB AND STC MAINLINE                       *
*                                                                    *
*      IF DITTO IS RUNNING AS A BATCH JOB, MAIN WILL LINK TO THE     *
*      CARD INPUT COMMAND MODULE 'DITTCARD', AND COMMANDS WILL BE    *
*      READ FROM 'SYSIN'.  IF DITTO IS A STARTED TASK, MAIN WILL     *
*      LINK TO THE CONSOLE COMMAND INPUT MODULE 'DITTCONS', AND      *
*      COMMANDS WILL BE ENTERED INTERACTIVELY.                       *
*                                                                    *
*      THE COMMAND MODULE WILL THEN SIGNAL WHETHER OR NOT THE        *
*      COMMAND AND ALL REQUIRED PARAMETERS WERE ENTERED AND VALID,   *
*      OR NOT.  IF NOT AN ERROR MESSAGE IS PRINTED AND THE NEXT      *
*      COMMAND IS REQUESTED.  IF A GOOD COMMAND WAS ENTERED, THE     *
*      COMMAND MODULE WILL HAVE PLACED THE INPUT AND OUTPUT MODULE   *
*      ADDRESSES IN THE 'COMMON' MODULE 'DITTCOMM'.                  *
*                                                                    *
*      MAIN THEN ENTERS A INPUT-OUTPUT LOOP.  THE INPUT MODULE IS    *
*      INVOKED, THEN THE OUTPUT MODULE, BACK TO THE INPUT MODULE     *
*      AND SO ON UNTIL END-OF-FILE IS SIGNALLED.  THEN A 'CLOSE'     *
*      INDICATOR IS SET AND INPUT AND OUTPUT ARE INVOKED AGAIN TO    *
*      ALLOW THEM ANY 'CLEAN-UP' AND TO CLOSE THEIR FILES IF         *
*      NECESSARY.                                                    *
*                                                                    *
*--------------------------------------------------------------------*
DITTMAIN DITTPRFX MAINSAVE,'MAINLINE PROGRAM'
         USING DITTCOMM,R11        SPECIFY BASE
         USING DYNBLOK,R2          DEFINE DYNAMIC BLOCK BASE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    INIT0010            YES... NO CIB TO WORRY ABOUT
         ICM   R1,15,COMMCCOM      COMMUNICATION PARM LIST PRESENT?
         BZ    ERR0010             NO...
         DITTRACE ID=CONSCOMM,     TRACE COMMUNICATION PARM LIST       +
               RDATA1=R1           .. CAPTURE COMM ADDRESS IN TRACE
         USING CCOM,R1             DEFINE BASE
         L     R1,COMCIBPT         CIB ADDRESS FROM COMM PARM LIST
         DITTRACE ID=CONSCIB,      TRACE CIB LOCATED                   +
               RDATA1=R1           .. CAPTURE CIB ADDRESS
         USING CIB,R1              DEFINE CIB BASE
         MVC   COMMCID+3(1),CIBCONID  SAVE CONSOLE ID
         DITTRACE ID=CONSID,       TRACE CONSOLE ID                    +
               DATA1=COMMCID       .. CAPTURE CONSOLE ID
INIT0010 DS    0H
         MVI   PRTCMMD,$PRTHEAD    SET COMMAND
         LA    R1,PRTBLOK          POINT TO PRINTER PARAMETER BLOCK
         L     R15,APRT            ENTRY POINT OF PRINT MODULE
         BALR  R14,R15             LINK TO PRINT MODULE
MAIN0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE
         MVI   COMMPFLG,X'00'      CLEAR ANY OPTIONAL INDICATORS
         NI    COMMFLAG,255-$ABORT CLEAR ABORT FLAG IF SET
         NI    COMMFLAG,255-$COMMCER AND COMMAND ERROR FLAG IF SET
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    MAIN0020            YES
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BE    MAIN0030            YES
         WTO   'INTERNAL ERROR, ENVIRONMENT NOT DETERMINED'
         ABEND 2,DUMP,,USER
MAIN0020 DS    0H
         L     R15,ACARD           CARD-INPUT COMMAND MODULE
         BALR  R14,R15             ENTER CARD-INPUT MODULE
         B     MAIN0040            CHECK FOR 'EOJ'
MAIN0030 DS    0H
         L     R15,ACONS           CONSOLE COMMAND MODULE ENTRY POINT
         BALR  R14,R15             ENTER CONSOLE-INPUT
         B     MAIN0040            CHECK FOR 'EOJ'
MAIN0040 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ENCOUNTERED?
         BO    MAIN9900            YES, EXIT
         TM    COMMFLAG,$COMMCER   INPUT COMMAND ERROR?
         BNO   MAIN0050            NO
         MVC   PRTDATA(MSG001L),MSG001
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0010            GO TO NEXT RECORD
*--------------------------------------------------------------------*
*                                                                    *
*        LOCATE THE DYNAMIC BLOCKS FOR THE INPUT AND OUTPUT MODULES. *
*        HAVING THIS CODE HERE RATHER THAN IN THE INPUT AND OUTPUT   *
*        MODULES ELEMINATES THE DUPLICATE CODE THAT WOULD HAVE TO BE *
*        IN EACH MODULE.                                             *
*                                                                    *
*--------------------------------------------------------------------*
MAIN0050 DS    0H
         CLC   COMMINM,AALOC       IS THE INPUT MODULE 'DITTALOC'?
         BE    MAIN0110            YES.. BYPASS
         OC    COMMINM,COMMINM     INPUT MODULE FOR THIS COMMAND?
         BZ    MAIN0080            NO
         OC    COMMINU,COMMINU     INPUT CUU PRESENT?
         BZ    MAIN0080            NO
         DITTRACE ID=ALOCIN
         MVI   DAIRCMMD,$DAIRLOC   LOCATE COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOK BY CUU
         MVC   DAIRCUU,COMMINU     CUU TO LOCATE
         LA    R1,DAIRBLOK         DAIR MODULE PARAMETER BLOCK
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    MAIN0070            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNBLOK NOT LOCATED?
         BE    MAIN0060            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
MAIN0060 DS    0H
         DITTRACE ID=INNOTACQ
         MVC   PRTDATA(MSG007L),MSG007   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0170            NO
         WTO   'INPUT UNIT NOT ALLOCATED, COMMAND ABORTED',            +
               CONSID=COMMCID      .. CONSOLE ID
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0070 DS    0H
         L     R1,COMMINM          INPUT MODULE ENTRY POINT
         USING MODPRFX,R1
         ICM   R2,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM     UNIT CONSISTENT?
         BNE   MAIN0280            NO..
         ST    R2,COMMIND          SAVE DYNAMIC BLOCK ADDRESS
MAIN0080 DS    0H
         OC    COMMOUTM,COMMOUTM   OUTPUT MODULE FOR THIS COMMAND?
         BZ    MAIN0110            NO
         OC    COMMOUTU,COMMOUTU   OUTPUT CUU PRESENT?
         BZ    MAIN0110            NO
         DITTRACE ID=ALOCOUT
         MVI   DAIRCMMD,$DAIRLOC   LOCATE COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNBLOK BY CUU
         MVC   DAIRCUU,COMMOUTU    CUU TO LOCATE
         LA    R1,DAIRBLOK         DAIR MODULE PARAMETER BLOCK
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR MODULE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    MAIN0100            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNBLOK NOT LOCATED?
         BE    MAIN0090            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
MAIN0090 DS    0H
         DITTRACE ID=OUTNTACQ
         MVC   PRTDATA(MSG008L),MSG008   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0170            NO
         WTO   'OUTPUT UNIT NOT ALLOCATED, COMMAND ABORTED',           +
               CONSID=COMMCID      .. CONSOLE ID
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0100 DS    0H
         L     R1,COMMOUTM         OUTPUT MODULE ENTRY POINT
         ICM   R2,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM     UNIT CONSISTENT?
         BNE   MAIN0290            NO..
         ST    R2,COMMOUTD         SAVE OUTPUT DYNAMIC BLOCK ADDRESS
MAIN0110 DS    0H
         DITTRACE ID=FIRST,        TRACE ENTRY INTO I/O LOOP           +
               DATA1=COMMIND,      .. INPUT DYNAMIC BLOCK ADDRESS      +
               DATA2=COMMOUTD      .. OUTPUT DYNAMIC BLOCK ADDRESS
         OI    COMMFLAG,$COMM1ST   SIGNAL FIRST TIME
MAIN0120 DS    0H
         DITTRACE ID=CALLIN        TRACE
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    MAIN0130            ZERO .. DON'T ATTEMPT TO INVOKE IT
         BALR  R14,R15             INVOKE INPUT ROUTINE
         TM    COMMFLAG,$ABEND     ABEND SET BY INPUT?
         BO    MAIN0260            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY INPUT?
         BO    MAIN0240            YES
*--------------------------------------------------------------------*
*                                                                    *
*        NOTE THAT THE EOF FLAG MAY BE SET BY AN OUTPUT ROUTINE.     *
*        THIS IS THE CASE WHEN THE FUNCTION REQUIRES AN OUTPUT       *
*        ROUTINE ONLY SUCH AS 'REW' OR 'RUN'.                        *
*                                                                    *
*--------------------------------------------------------------------*
MAIN0130 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ON INPUT?
         BO    MAIN0150            YES ...
         DITTRACE ID=CALLOUT       TRACE
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    MAIN0140            ZERO .. DON'T ATTEMPT TO INVOKE IT
         BALR  R14,R15             INVOKE OUTPUT ROUTINE
         TM    COMMFLAG,$ABEND     ABEND SET BY OUTPUT?
         BO    MAIN0270            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY OUTPUT?
         BO    MAIN0250            YES
         TM    COMMFLAG,$COMMEOF   EOF SET BY OUTPUT?
         BO    MAIN0150            YES
MAIN0140 DS    0H
         DITTRACE ID=FIRSTOFF      NO LONGER FIRST PASS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         B     MAIN0120            LOOP 'TILL END OF FILE
MAIN0150 DS    0H
         DITTRACE ID=CLOSE         'CLOSE' IN PROGRESS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         OI    COMMFLAG,$COMMCLS   INDICATE CLOSE FILES
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    MAIN0160            NO INPUT MODULE
         BALR  R14,R15             ALLOW INPUT TO DO ANY CLEAN-UP
MAIN0160 DS    0H
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    MAIN0170            NO OUTPUT MODULE
         BALR  R14,R15             ALLOW OUTPUT TO DO ANY CLEAN-UP
* ------------------------------------------------------------------- *
*                                                                     *
*           CURRENT COMMAND IS TERMINATING                            *
*                                                                     *
*    IF THE COMMAND IS BEING ABORTED, NO FURTHER MESSAGES ARE ISSUED. *
*    IF THE COMMAND HAS INPUT FROM TAPE OR DASD, AND AN 'END-OF-FILE' *
*    WAS REACHED (MAY HAVE BEEN END-OF-TAPE), ISSUE A MESSAGE TO      *
*    INDICATE THE REASON THE COMMAND STOPPED... THIS IS MAY HELP      *
*    ELIMINATE CONFUSION WHEN A NUMBER OF RECORDS WAS SPECIFIED ON    *
*    THE COMMAND, BUT EOF WAS REACHED BEFORE THE REQUESTED NUMBER OF  *
*    RECORDS WAS READ.                                                *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0170 DS    0H
         DITTRACE ID=TERMCMD
         TM    COMMFLAG,$ABORT     WAS ABORT FLAG SET?
         BO    MAIN0230            YES
         ICM   R2,15,COMMIND       INPUT DYNAMIC BLOCK PRESENT?
         BZ    MAIN0230            NO.. RE-INITIALIZE
         CLC   MAINTAPE,DYNDDNAM   WAS THIS A TAPE INPUT COMMAND?
         BE    MAIN0190            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED TO TO EOF RECORD?
         BNO   MAIN0230            NO.. RE-INITIALIZE
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    MAIN0180            YES
         WTO   'EOF FOUND ON INPUT, COMMAND TERMINATED',               +
               CONSID=COMMCID
MAIN0180 DS    0H
         MVC   PRTDATA(MSG011L),MSG011
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0230            RE-INITIALIZE
MAIN0190 DS    0H
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    MAIN0210            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY TAPEMARK?
         BNO   MAIN0230            NO.. RE-INITIALIZE
         CLI   COMMENV,$ENVJOB     RUNNING AS A JOB?
         BE    MAIN0200            YES
         WTO   'TAPE MARK READ ON INPUT, COMMAND TERMINATED',          +
               CONSID=COMMCID
MAIN0200 DS    0H
         MVC   PRTDATA(MSG013L),MSG013
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0230            RE-INITIALIZE
MAIN0210 DS    0H
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    MAIN0220            YES
         WTO   'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINAT+
               ED',                                                    +
               CONSID=COMMCID
MAIN0220 DS    0H
         MVC   PRTDATA(MSG012L),MSG012
         BAL   R9,MAINPRT          PRINT MESSAGE
MAIN0230 DS    0H
         DITTRACE ID=REINIT
         NI    COMMFLAG,255-$COMMCLS TURN OFF CLOSE FLAG
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         NI    COMMFLAG,255-$COMMFE  TURN OFF FORCED EOF FLAG
         XC    COMMINM,COMMINM     CLEAR MODULE ADDRESS
         XC    COMMOUTM,COMMOUTM   CLEAR MODULE ADDRESS
         XC    COMMIND,COMMIND     CLEAR DYNAMIC BLOCK ADDRESS
         XC    COMMOUTD,COMMOUTD   CLEAR DYNAMIC BLOCK ADDRESS
         MVI   PARMCMMD,$PARMINT   INITIALIZE COMMAND
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARAMETER BLOCK
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO 'DITTPARM' TO RE-INITIALIZE
         B     MAIN0010            GET NEXT COMMAND
MAIN0240 DS    0H
         DITTRACE ID=ABORTIN       INPUT ABORTED CURRENT FUNCTION
         MVC   PRTDATA(MSG002L),MSG002   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0250 DS    0H
         DITTRACE ID=ABORTOUT      OUTPUT ABORTED CURRENT FUNCTION
         MVC   PRTDATA(MSG003L),MSG003   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'COMMAND ABORTED, CHECK SYSPRINT FOR EXPLANTION',       +
               CONSID=COMMCID      .. CONSOLE ID
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0260 DS    0H
         DITTRACE ID=ABENDIN       INPUT REQUESTED ABEND
         MVC   PRTDATA(MSG004L),MSG004   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND002,DUMP,,USER ABEND US
MAIN0270 DS    0H
         DITTRACE ID=ABENDOUT      OUTPUT REQUESTED ABEND
         MVC   PRTDATA(MSG005L),MSG005   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND003,DUMP,,USER ABEND US
MAIN0280 DS    0H
         DITTRACE ID=XINUNIT       INCONSISTENT UNIT TYPE AND FUNCTION
         MVC   PRTDATA(MSG009L),MSG009   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'INPUT UNIT TYPE INCONSISTENT WITH COMMAND, COMMAND ABOR+
               TED',                                                   +
               CONSID=COMMCID      .. CONSOLE ID
         B     MAIN0170            RE-INITIALIZE PARAMETERS
MAIN0290 DS    0H
         DITTRACE ID=XOUTUNIT      INCONSISTENT UNIT TYPE AND FUNCTION
         MVC   PRTDATA(MSG010L),MSG010   MOVE MESSAGE
         BAL   R9,MAINPRT          PRINT MESSAGE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK?
         BNE   MAIN0010            NO
         WTO   'OUTPUT UNIT TYPE INCONSISTENT WITH COMMAND, COMMAND ABO+
               RTED',                                                  +
               CONSID=COMMCID      .. CONSOLE ID
         B     MAIN0170            RE-INITIALIZE PARAMETERS
ERR0010  DS    0H
         WTO   'COMM ADDRESS FROM EXTRACT IS ZERO, UNABLE TO DETERMINE +
               CONSOLE ID'
         ABEND ABEND012,DUMP,,USER
MAINPRT  DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         BR    R9                  RETURN
MAIN9900 DS    0H
         DITTRACE ID=EXIT          TRACE
         L     R3,COMMDYHD         FIRST BLOCK ON CHAIN
MAIN9910 DS    0H
         LTR   R2,R3               ADDRESS PRESENT?
         BZ    MAIN9920            ALL HAVE BEEN RELEASED
         DITTRACE ID=RELDYN,       TRACE RELEASE                       +
               RDATA1=R2           .. SAVE DYNAMIC BLOCK ADDRESS
         L     R3,DYNNEXT          NEXT DYNAMIC BLOCK ADDRESS
         MVI   DAIRCMMD,$DAIRREL   SET COMMAND (RELEASE)
         MVI   DAIRTYPE,$DAIRCUU   SET TYPE OF LOCATE (BY CUU)
         MVC   DAIRCUU,DYNCUU      SET CUU
         LA    R1,DAIRBLOK         PARAMETER BLOCK ADDRESS
         L     R15,ADAIR           'DITTDAIR' ENTRY POINT
         BALR  R14,R15             RELEASE THIS RESOURCE
         CLI   DAIRSTAT,$DAIRSOK   SUCCESSFULLY RELEASED?
         BE    MAIN9910            YES
         MVC   MSG6DD,DYNDDNAM     COPY DD NAME
         UNPK  MSG615(3),DAIRR15+3(2)
         MVZ   MSG615,MAINHX0F     TURN OFF ZONES
         TR    MSG615,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG615+2,C' '       RESTORE BLANK
         UNPK  MSG6ER(5),DAIRERR(3)
         MVZ   MSG6ER,MAINHX0F     TURN OFF ZONES
         TR    MSG6ER,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG6ER+4,C' '       RESTORE BLANK
         UNPK  MSG6IN(5),DAIRINFO(3)
         MVZ   MSG6IN,MAINHX0F     TURN OFF ZONES
         TR    MSG6IN,MAINHXCH     TRANSLATE TO PRINTABLE
         MVI   MSG6IN+4,C' '       RESTORE BLANK
         MVC   PRTDATA(MSG006L),MSG006   MOVE MESSAGE
         MVI   PRTCC,C'0'          DOUBLE SPACE
         BAL   R9,MAINPRT          PRINT MESSAGE
         ABEND ABEND008,DUMP,,USER ABEND
MAIN9920 DS    0H
         DITTRACE ID=RELDONE       DYNAMIC RELEASES ARE COMPLETE
         NI    COMMFLAG,255-$COMMEOF      TURN EOF FLAG
         OI    COMMFLAG,$COMMEOJ   SIGNAL END OF JOB IN PROGRESS
         L     R15,ACDI1           CARD READ MODULE ENTRY POINT
         BALR  R14,R15             CLOSE SYSIN
         L     R15,ACDO1           CARD PUNCH MODULE ENTRY POINT
         BALR  R14,R15             CLSOE SYSPUNCH
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             CLOSE SYSPRINT
         TM    XTRFLAG,$XTROPEN    EXTERNAL TRACE FILE OPEN?
         BNO   MAIN9930            NO
         LA    R2,XTRDCB           EXTERNAL TRACE DCB
         CLOSE ((R2))              CLOSE EXTERNAL TRACE
MAIN9930 DS    0H
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE                                *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERTER INTERFACE                         *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC ALLOCATION INTERFACE                          *
*                                                                    *
*--------------------------------------------------------------------*
DAIRBLOK DAIRBLOK TYPE=CSECT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CONSTANTS                                             *
*                                                                    *
*--------------------------------------------------------------------*
MAINHX0F DC    8X'0F'
MAINHXCH DC    C'0123456789ABCDEF'
MAINTAPE DC    C'TAPE'
MSG001   DC    C'COMMAND/PARM ERROR, CONTINUING WITH NEXT COMMAND'
MSG001L  EQU   *-MSG001
MSG002   DC    C'COMMAND ABORTED BY INPUT PROCESSING, CONTINUING WITH N+
               EXT COMMAND'
MSG002L  EQU   *-MSG002
MSG003   DC    C'COMMAND ABORTED BY OUTPUT PROCESSING, CONTINUING WITH +
               NEXT COMMAND'
MSG003L  EQU   *-MSG003
MSG004   DC    C'EXECUTION ABORTED BY INPUT PROCESSING'
MSG004L  EQU   *-MSG004
MSG005   DC    C'EXECUTION ABORTED BY OUTPUT PROCESSING'
MSG005L  EQU   *-MSG005
MSG006   DC    CL21'DYNAMIC RELEASE FAILED, DDNAME='
MSG6DD   DC    CL08' '
         DC    CL06', R15='
MSG615   DC    CL02' '
         DC    CL09' DAIRERR='
MSG6ER   DC    CL04' '
         DC    CL10' DAIRINFO='
MSG6IN   DC    CL04' '
MSG006L  EQU   *-MSG006
MSG007   DC    C'INPUT UNIT NOT ALLOCATED, COMMAND WILL BE ABORTED'
MSG007L  EQU   *-MSG007
MSG008   DC    C'OUTPUT UNIT NOT ALLOCATED, COMMAND WILL BE ABORTED'
MSG008L  EQU   *-MSG008
MSG009   DC    C'INPUT UNIT INCONSISTENT WITH COMMAND, COMMAND ABORTED'
MSG009L  EQU   *-MSG009
MSG010   DC    C'OUTPUT UNIT INCONSISTENT WITH COMMAND, COMMAND ABORTED+
               '
MSG010L  EQU   *-MSG010
MSG011   DC    C'EOF FOUND ON INPUT, COMMAND TERMINATED'
MSG011L  EQU   *-MSG011
MSG012   DC    C'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINA+
               TED'
MSG012L  EQU   *-MSG012
MSG013   DC    C'TAPE MARK READ ON INPUT, COMMAND TERMINATED'
MSG013L  EQU   *-MSG013
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
*--------------------------------------------------------------------*
MAINSAVE DC    18F'0'              REGISTER SAVE AREA
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         ABCODES
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC RESOURCE CONTROL BLOCK                        *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=SHORT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMUNICATION PARAMETER LIST                          *
*                                                                    *
*--------------------------------------------------------------------*
CCOM     DSECT
         IEZCOM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CONSOLE INPUT BUFFER                                  *
*                                                                    *
*--------------------------------------------------------------------*
CIB      IEZCIB
         END
./ ADD NAME=DITTPARM
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER SEARCH ROUTINE/TABLE                        *
*                                                                    *
*     THIS MODULE IS LINKED TO BY THE CARD-INPUT COMMAND MODULE      *
*     'DITTCARD', THE CONSOLE-INPUT COMMAND MODULE 'DITTCONS', AND   *
*     THE TSO-INPUT COMMAND MODULE 'DITTTSO'.                        *
*                                                                    *
*     SINCE THE SAME PARAMETERS CAN BE ENTERED FROM EACH SOURCE,     *
*     A COMMON PARAMETER CONVERTER ROUTINE ELIMINATED DUPLICATION    *
*     OF CODE.  ALSO NEW PARAMETERS SHOULD ONLY REQUIRE MODIFICATION *
*     OF ONE MODULE.                                                 *
*                                                                    *
*     THIS MODULE ALSO WILL RETURN A MESSAGE FOR REQUIRED PARAMETERS *
*     THAT WERE NOT ENTERED.  THE CONSOLE-INPUT MODE ALLOWS THE      *
*     OPERATOR TO BE PROMPTED FOR MISSING PARAMETERS, THE PROMPT     *
*     IS ISSUEED FROM THIS MODULE.                                   *
*                                                                    *
*     TO ALLOW SPECIALIZED CHECKING OF INDIVIDUAL PARAMETERS WITHOUT *
*     CODING SECTIONS IN THIS MODULE FOR SPECIFIC FIELDS, AN EDITOR/ *
*     CHECKER MODULE NAME MY BE CODED IN THE 'PARM' MACRO WHICH      *
*     DEFINES THE PARAMETER.  THE 'PARMBLOK' ADDRESS WILL BE PASSED  *
*     TO ANY EDITOR/CHECKER MODULE IN R10, ALLOWING THE EDITOR/      *
*     CHECKER MODULE TO DETERMINE WHICH FIELD IS BEING CHECKED       *
*     (ONE EDITOR/CHECKER COULD BE USED FOR MULTIPLE PARAMETERS).    *
*                                                                    *
*--------------------------------------------------------------------*
DITTPARM DITTPRFX PARMSAVE,'PARAMETER CONVERSION MODULE'
         LR    R10,R1              COPY PARAMETER BLOCK ADDRESS
         USING PARMBLOK,R10        SPECIFY BASE
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=PARMWTO,      .. TRACE 1ST PART OF BLOCK          +
               DATA2=PARMWORK      .. TRACE MOST ALL OF REMAINDER
         XC    PARMMSGA,PARMMSGA   CLEAR MESSAGE ADDRESS
         NI    PARMFLAG,255-$PARMOK-$PARMCAN   RESET FLAGS
         LA    R6,PARMTBLE         PARAMETER TABLE
         USING PARMD,R6            ADDRESSIBILITY
         CLI   PARMCMMD,$PARMINT   INITIALIZE PARAMETERS??
         BE    PARM0500            YES
         DITTRACE ID=PARMSTRT      TRACE TABLE SEARCH START
PARM0010 DS    0H
         CLI   PARMELEN,X'FF'      END OF TABLE??
         BE    PARM8000            YES .. INVALID PARAMETER ID
         CLC   PARMID,PARMPARM     CORRECT PARAMETER??
         BE    PARM0020            YES
         AH    R6,PARMELEN         ADD ENTRY LENGTH
         B     PARM0010            LOOP
PARM0020 DS    0H
         DITTRACE ID=PARMFND       TRACE PARAMETER FOUND
         CLI   PARMCMMD,$PARMWTO   PROMPT OPERATOR FOR PARM??
         BE    PARM0210            YES
         CLI   PARMCMMD,$PARMEWTO  ERROR MSG/PROMPT FOR PARM??
         BE    PARM0220            YES
         CLI   PARMCMMD,$PARMMIS   RETURN MESSAGE FOR MISSING PARM?
         BE    PARM0200            YES
PARM0030 DS    0H
         DITTRACE ID=PARMCNV       TRACE PARAMETER FOUND
         TM    PARMDSCR,$PARMBIN   HEX FIELD??
         BO    PARM0050            YES
         TM    PARMDSCR,$PARMCUU   CUU FIELD??
         BO    PARM0080            YES
         TM    PARMDSCR,$PARMDEC   DECIMAL FIELD??
         BO    PARM0110            YES
         TM    PARMDSCR,$PARMCHR   CHARACTER FIELD??
         BO    PARM0140            YES
         TM    PARMDSCR,$PARMADR   ADDRESS FIELD?
         BO    PARM0160            YES
PARM0040 DS    0H
         DITTRACE ID=UNKNPARM      TRACE PARAMETER TYPE UNKNOWN
         B     PARM9900            EXIT
PARM0050 DS    0H
         DITTRACE ID=PARMBIN       TRACE BINARY PARAMETER CONVERSION
         LH    R2,PARMWLEN         LOAD WORK AREA LENGTH
         BCTR  R2,0                ADJUST IT
         EX    R2,PARMTRT2         CHECK FOR VALID DECIMAL VALUE
         BNZ   PARM0060            NOT VALID
         EX    R2,PARMPACK         PACK THE NUMBER
         CVB   R1,COMMDWRD         CONVERT TO BINARY
         CH    R1,PARMHBIG         VALUE TOO BIG??
         BH    PARM0070            YES, EXIT
         ST    R1,COMMDWRD         STORE RESULT IN BINARY
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,PARMADDR       PARAMETER RESULT OFFSET
         LA    R1,DITTCOMM(R1)     OUTPUT FIELD
         LA    R2,COMMDWRD+3       END OF BINARY RESULT
         LH    R3,PARMLEN          PARAMETER LENGTH
         BCTR  R3,0                ADJUST IT
         SR    R2,R3               FIRST BYTE TO MOVE FROM
         EX    R3,HEXMVC           MOVE RESULT
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,PARMCDSP      DISPLACEMENT TO CHECKER ADDRESS
         BZ    PARM0300            NO CHECKER.. ALL IS WELL
         DITTRACE ID=BINCHECK      TRACE CALL TO FIELD CHECKER
         L     R15,DITTCOMM(R15)   CHECKER ENTRY POINT FROM DITTCOMM
         BALR  R14,R15             LINK TO CHECKER
         TM    PARMFLAG,$PARMCKE   ERROR DETECTED BY CHECKER??
         BNO   PARM0300            NO.. ALL IS A-OK
         NI    PARMFLAG,255-$PARMCKE    RESET THE FLAG
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0060 DS    0H
         DITTRACE ID=ODDBIN        BINARY FIELD WITH ODD NBR OF DIGITS
         MVC   PARMM01P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM01          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0070 DS    0H
         DITTRACE ID=BIGBIN        BINARY FIELD WITH ODD NBR OF DIGITS
         MVC   PARMM02P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM02          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0080 DS    0H
         DITTRACE ID=PARMCUU       TRACE CUU PARAMETER CONVERSION
         CLC   PARMWLEN,H3         CORRECT LENGTH FOR A 'CUU'??
         BNE   PARM0090            NO, EXIT
         TRT   PARMWORK(3),COMMVHEX CHECK FOR VALID HEX
         BNZ   PARM0100            NOT VALID
         NC    PARMWORK,COMM1F     CLEAR HIGH 3 BITS
         TR    PARMWORK,COMMHXPK   TRANSLATE FOR HEX PACKING
         ICM   R1,7,PARMWORK       PICK UP CUU IN CHARACTER
         STCM  R1,7,PARMWORK+1     MOVE IT OVER 1 BYTE
         MVI   PARMWORK,X'00'      PREFIX IT WITH ZEROS
         PACK  COMMDWRD(3),PARMWORK(5) PACK THE VALUE
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,PARMADDR       PARAMETER RESULT OFFSET
         LA    R1,DITTCOMM(R1)     OUTPUT FIELD
         MVC   0(2,R1),COMMDWRD    MOVE THE PARAMETER VALUE
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,PARMCDSP      DISPLACEMENT TO CHECKER ADDRESS
         BZ    PARM0300            NO CHECKER.. ALL IS WELL
         DITTRACE ID=CUUCHECK      TRACE CALL TO FIELD CHECKER
         L     R15,DITTCOMM(R15)   CHECKER ENTRY POINT FROM DITTCOMM
         BALR  R14,R15             LINK TO CHECKER
         TM    PARMFLAG,$PARMCKE   ERROR DETECTED BY CHECKER??
         BNO   PARM0300            NO.. ALL IS A-OK
         NI    PARMFLAG,255-$PARMCKE    RESET THE FLAG
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0090 DS    0H
         DITTRACE ID=BADCUUL
         MVC   PARMM03P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM03          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT
PARM0100 DS    0H
         DITTRACE ID=BADCUUD
         MVC   PARMM04P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM04          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT
PARM0110 DS    0H
         DITTRACE ID=PARMDEC       TRACE DECIMAL PARAMETER CONVERSION
         LH    R2,PARMWLEN         WORK AREA LENGTH
         SRL   R2,1                DIVIDE LENGTH BY 2
         LA    R2,1(R2)            ADD 1
         CH    R2,PARMLEN          VALUE TOO LONG??
         BH    PARM0120            YES, EXIT
         LH    R2,PARMWLEN         WORK AREA LENGTH
         BCTR  R2,0                ADJUST IT
         EX    R2,PARMTRT2         CHECK FOR VALID DECIMAL VALUE
         BNZ   PARM0130            NOT VALID
         EX    R2,PARMPACK         PACK THE NUMBER
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,PARMADDR       PARAMETER RESULT OFFSET
         LA    R1,DITTCOMM(R1)     OUTPUT FIELD
         LH    R2,PARMLEN          MAX RESULT LENGTH
         BCTR  R2,0                ADJUST LENGTH
         SLL   R2,4                SHIFT LENGTH FOUR BITS
         EX    R2,PARMZAP          MOVE RESULT
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,PARMCDSP      DISPLACEMENT TO CHECKER ADDRESS
         BZ    PARM0300            NO CHECKER.. ALL IS WELL
         DITTRACE ID=DECCHECK      TRACE CALL TO FIELD CHECKER
         L     R15,DITTCOMM(R15)   CHECKER ENTRY POINT FROM DITTCOMM
         BALR  R14,R15             LINK TO CHECKER
         TM    PARMFLAG,$PARMCKE   ERROR DETECTED BY CHECKER??
         BNO   PARM0300            NO.. ALL IS A-OK
         NI    PARMFLAG,255-$PARMCKE    RESET THE FLAG
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0120 DS    0H
         DITTRACE ID=BADDECL
         MVC   PARMM05P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM05          MESSAGE ADDRESS
         ST    R1,PARMMSGA         SET MESSAGE
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0130 DS    0H
         DITTRACE ID=BADDECD
         MVC   PARMM06P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM06          MESSAGE ADDRESS
         ST    R1,PARMMSGA         SET MESSAGE
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0140 DS    0H
         DITTRACE ID=PARMCHAR      TRACE CHARACTER FIELDS
         LH    R2,PARMWLEN         WORK AREA LENGTH
         CH    R2,PARMLEN          TOO LONG??
         BH    PARM0150            YES.. EXIT
         LH    R3,PARMADDR         RESULT OFFSET INTO DITTCOMM
         LA    R3,DITTCOMM(R3)     RESULT FIELD
         LH    R1,PARMLEN          FULL LENGTH
         BCTR  R1,0                ADJUST IT
         EX    R1,CHARINIT         INITIALIZE CHARACTER FIELD
         BCTR  R2,0                ADJUST ACTUAL DATA LENGTH
         EX    R2,CHARMVC          MOVE CHARACTER FIELD TO RESULT
         SR    R15,R15             CLEAR REGISTER
         ICM   R15,3,PARMCDSP      DISPLACEMENT TO CHECKER ADDRESS
         BZ    PARM0300            NO CHECKER.. ALL IS WELL
         DITTRACE ID=CHCHECK       TRACE CALL TO FIELD CHECKER
         L     R15,DITTCOMM(R15)   CHECKER ENTRY POINT FROM DITTCOMM
         BALR  R14,R15             LINK TO CHECKER
         TM    PARMFLAG,$PARMCKE   ERROR DETECTED BY CHECKER??
         BNO   PARM0300            NO.. ALL IS A-OK
         NI    PARMFLAG,255-$PARMCKE    RESET THE FLAG
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0150 DS    0H
         MVC   PARMM07P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM07          MESSAGE ADDRESS
         ST    R1,PARMMSGA         SET MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0160 DS    0H
         DITTRACE ID=PARMADDR,     TRACE DISK ADDRESS CONVERSION       +
               DATA1=PARMWLEN,     .. DATA LENGTH                      +
               DATA2=PARMWORK      .. DATA
         LH    R2,PARMWLEN         LOAD WORK AREA LENGTH
         CH    R2,H9               PROPER LENGTH?
         BNE   PARM0170            NO..
         BCTR  R2,0                ADJUST IT
         EX    R2,PARMTRT2         CHECK FOR VALID DECIMAL VALUE
         BNZ   PARM0180            NOT VALID
         SR    R2,R2               CLEAR REGISTER
         ICM   R2,3,PARMADDR       PARAMETER RESULT OFFSET
         LA    R2,DITTCOMM(R2)     OUTPUT FIELD ADDRESS
         PACK  COMMDWRD,PARMWORK(4)    PACK CYLINDER NUMBER
         CVB   R1,COMMDWRD         CONVERT TO BINARY
         STCM  R1,3,0(R2)          SET CYLINDER NUMBER
         PACK  COMMDWRD,PARMWORK+4(2)  PACK HEAD NUMBER
         CVB   R1,COMMDWRD         CONVERT TO BINARY
         STCM  R1,3,2(R2)          SET HEAD NUMBER
         PACK  COMMDWRD,PARMWORK+6(3)  PACK RECORD NUMBER
         CVB   R1,COMMDWRD         CONVERT TO BINARY
         STC   R1,4(R2)            SET RECORD NUMBER
         CLI   4(R2),0             RECORD NUMBER = ZERO?
         BE    PARM0190            YES.. INVALID RECORD NUMBER
         ICM   R15,3,PARMCDSP      DISPLACEMENT TO CHECKER ADDRESS
         BZ    PARM0300            NO CHECKER.. ALL IS WELL
         DITTRACE ID=ADRCHECK      TRACE CALL TO FIELD CHECKER
         L     R15,DITTCOMM(R15)   CHECKER ENTRY POINT FROM DITTCOMM
         BALR  R14,R15             LINK TO CHECKER
         TM    PARMFLAG,$PARMCKE   ERROR DETECTED BY CHECKER??
         BNO   PARM0300            NO.. ALL IS A-OK
         NI    PARMFLAG,255-$PARMCKE    RESET THE FLAG
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0170 DS    0H
         DITTRACE ID=ADRLEN        INCORRECT DISK ADDRESS LENGTH
         MVC   PARMM08P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM08          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0180 DS    0H
         DITTRACE ID=BADADR        INVALID VALUE IN DISK ADDRESS
         MVC   PARMM06P,PARMNAME   SET PARM NAME IN MESSAGE
         LA    R1,PARMM06          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
PARM0190 DS    0H
         DITTRACE ID=BADREC        INVALID RECORD NUMBER IN DISK ADDR
         LA    R1,PARMM09          MESSAGE LENGTH/DATA ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PARM9900            EXIT WITHOUT SETTING 'OK' FLAG
*--------------------------------------------------------------------*
*                                                                    *
*              RETURN MESSAGE FOR MISSING PARMS                      *
*                                                                    *
*--------------------------------------------------------------------*
PARM0200 DS    0H
         MVC   PARMMSGA,PARMMMSG   RETURN 'MISSING' PARM MESSAGE
         B     PARM9900
*--------------------------------------------------------------------*
*                                                                    *
*              PROMPT OPERATOR FOR PARAMETERS                        *
*                                                                    *
*--------------------------------------------------------------------*
PARM0210 DS    0H
         ICM   R3,15,PARMMWTO      WTO ADDRESS
         B     PARM0230            ISSUE WTO
PARM0220 DS    0H
         DITTRACE ID=PARMEWTO
         ICM   R3,15,PARMWTO       MESSAGE PROVIDED BY CALLER?
         BNZ   PARM0230            YES
         ICM   R3,15,PARMIWTO      WTO ADDRESS (FOR ERROR MSG)
PARM0230 DS    0H
         XC    PARMWTO,PARMWTO     CLEAR ADDRESS
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(L'PARMMSG1),PARMMSG1
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,0(R3)          LENGTH FROM WTO
         SH    R1,H5               MINUS WTO PREFIX LENGTH + 1
         EX    R1,WTOMSG           MOVE TEXT FROM WTO
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         WTO   MF=(E,(R3)),        ISSUE WTO                           +
               CONSID=COMMCID      .. CONSOLE ID
         LR    R4,R1               SAVE WTO MESSAGE ID
         MVC   WTOAREA(WTOAREAL),COMMBLKS    CLEAR REPLY AREA
         LA    R5,COMMCECB         ECB ADDRESS
         XC    COMMCECB,COMMCECB   CLEAR ECB
         LA    R2,WTOAREA          REPLY AREA ADDRESS
         ICM   R3,15,PARMWTOR      WTOR ADDRESS
         WTOR  ,(R2),,(R5),        ISSUE WTOR FOR PROMPT               +
               CONSID=COMMCID,     .. CONSOLE ID                       +
               MF=(E,(R3))         .. MACRO FORM
         WAIT  1,ECB=(R5)          WAIT FOR OPERATOR'S RESPONSE
         DOM   MSG=(R4)            DELETE WTO MESSAGE
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(L'PARMMSG2),PARMMSG2
         MVC   PRTDATA+L'PARMMSG2(WTOAREAL),WTOAREA
         LA    R1,PRTBLOK          PRINT PARAMETER BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         CLC   PARMCAN,WTOAREA     DID OPERATOR REQUEST COMMAND CANCEL?
         BE    PARM0600            YES
         XC    PARMWLEN,PARMWLEN   ZERO DATA LENGTH
         XC    PARMWORK,PARMWORK   CLEAR WORK AREA
         LA    R1,WTOAREA          DATA ADDRESS
         LA    R2,80               MAXIMUM DATA LENGTH
PARM0240 DS    0H
         CLI   0(R1),C' '          AT LEAST A BLANK??
         BH    PARM0250            YES
         LA    R1,1(R1)            NEXT ONE
         BCT   R2,PARM0240         LOOP
         B     PARM0210            NOTHING??? RE-ISSUE WTO/WTOR
PARM0250 DS    0H
         SR    R3,R3               CLEAR FOR LENGTH
         LA    R4,PARMWORK         OUTPUT WORK AREA
PARM0260 DS    0H
         CLI   0(R1),C' '          MORE THAN A BLANK??
         BNH   PARM0270            NO
         LA    R3,1(R3)            ADD 1 TO LENGTH
         MVC   0(1,R4),0(R1)       MOVE IT
         LA    R1,1(R1)            NEXT ONE
         LA    R4,1(R4)            NEXT ONE
         BCT   R2,PARM0260         CONTINUE
PARM0270 DS    0H
         STH   R3,PARMWLEN         SAVE DATA LENGTH
         B     PARM0030            GO PROCESS DATA
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXIT POINT IF PARM WAS PROCESSED SUCCESSFULLY         *
*                                                                    *
*--------------------------------------------------------------------*
PARM0300 DS    0H
         DITTRACE ID=PARMGOOD      TRACE GOOD PARAMETER INDICATION
         OI    PARMFLAG,$PARMOK    PARMETER AND OPERAND ARE OK
         OC    COMMPFLG,PARMPFLG   TURN ON INDICATORS IF ANY
         B     PARM9900            EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              RE-INITIALIZE ALL PARAMETERS AS 'CLEAN-UP'            *
*                                                                    *
*--------------------------------------------------------------------*
PARM0500 DS    0H
         DITTRACE ID=INITSTRT      TRACE TABLE SCAN START
PARM0510 DS    0H
         CLI   PARMELEN,X'FF'      END OF TABLE??
         BE    PARM0560            YES .. INVALID PARAMETER ID
         LH    R2,PARMADDR         RESULT'S OFFSET INTO DITTCOMM
         LA    R2,DITTCOMM(R2)     RESULT'S ADDRESS
         LH    R3,PARMLEN          RESULT LENGTH
         LTR   R3,R3               LENGTH ZERO??
         BZ    PARM0550            YES
         BCTR  R3,0                ADJUST FOR XC
         TM    PARMDSCR,$PARMBIN   BINARY??
         BO    PARM0520            YES
         TM    PARMDSCR,$PARMCUU   UNIT ADDRESS??
         BO    PARM0520            YES
         TM    PARMDSCR,$PARMADR   DISK ADDRESS??
         BO    PARM0520            YES
         TM    PARMDSCR,$PARMDEC   DECIMAL??
         BO    PARM0530            YES
         TM    PARMDSCR,$PARMCHR   CHARACTER??
         BO    PARM0540            YES
         DITTRACE ID=INITUNKN,     TRACE UNKNOWN PARM TYPE             +
               DATA1=PARMNAME,     .. CAPTURE PARM NAME                +
               RDATA2=R6           .. CAPTURE ITS ADDRESS
         B     PARM0550
PARM0520 DS    0H
         EX    R3,INITXC           CLEAR TO BINARY ZEROS
         B     PARM0550
PARM0530 DS    0H
         SLL   R3,4                SHIFT TO 'LENGTH 1' BITS
         EX    R3,INITZAP          SET TO ZERO
         B     PARM0550
PARM0540 DS    0H
         EX    R3,INITMVC          INITIALIZE TO BLANKS
PARM0550 DS    0H
         AH    R6,PARMELEN         ADD ENTRY LENGTH
         B     PARM0510            LOOP
PARM0560 DS    0H
         DITTRACE ID=INITGOOD      INITIALIZATION COMPLETE
         OI    PARMFLAG,$PARMOK    INITIALIZATION OK
         B     PARM9900            EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              OPERATOR REQUESTED CURRENT COMMAND TO BE ABORTED      *
*                                                                    *
*--------------------------------------------------------------------*
PARM0600 DS    0H
         DITTRACE ID=CANCEL        TRACE COMMAND CANCELLED
         OI    PARMFLAG,$PARMCAN   SET 'CANCEL' FLAG
         B     PARM9900            AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER 'ID' PASSED WAS INVALID (INTERNAL ERROR)    *
*                                                                    *
*--------------------------------------------------------------------*
PARM8000 DS    0H
         DITTRACE ID=PARMINV       TRACE INVALID PARAMETER ID
         DC    H'0'                BLOW OURSELVES AWAY
         DC    C'INVALID PARAMETER ID ENCOUNTERED'
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PARM9900 DS    0H
         DITTRACE ID=EXIT          TRACE GOOD PARAMETER INDICATION
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXECUTED INSTRUCTIONS                                 *
*                                                                    *
*--------------------------------------------------------------------*
PARMTRT1 TRT   PARMWORK(0),COMMVHEX   CHECK FOR VALID HEX/CUU
PARMMVC  MVC   0(0,R1),COMMDWRD       MOVE HEX/CHAR VALUES
PARMTRT2 TRT   PARMWORK(0),COMMVDEC   CHECK FOR VALID DECIMAL
PARMPACK PACK  COMMDWRD,PARMWORK(0)   MOVE DECIMAL VALUES
PARMZAP  ZAP   0(0,R1),COMMDWRD       MOVE THE RESULT
HEXMVC   MVC   0(0,R1),0(R2)          MOVE THE RESULT
CHARINIT MVC   0(0,R3),COMMBLKS       INITIALIZE TO BLANKS
CHARMVC  MVC   0(0,R3),PARMWORK       MOVE PARAMETER TO RESULT
INITXC   XC    0(0,R2),0(R2)          CLEAR TO BINARY ZEROS
INITZAP  ZAP   0(0,R2),COMMP0         SET TO ZERO
INITMVC  MVC   0(0,R2),COMMBLKS       INTIALIZE TO BLANKS
WTOMSG   MVC   PRTDATA+L'PARMMSG1(0),4(R3)
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER-SEARCH MODULE WORK AREAS                    *
*                                                                    *
*--------------------------------------------------------------------*
PARMSAVE DC    18F'0'              REGISTER SAVE AREA
WTOAREA  DC    0F'0',CL80' '       WTOR REPLY AREA
WTOAREAL EQU   *-WTOAREA
PARMHBIG DC    H'32767'            LARGEST ALLOWABLE HEX VALUE
H3       DC    H'3'                CONSTANT
H5       DC    H'5'                CONSTANT
H9       DC    H'9'                CONSTANT
PARMCAN  DC    C'CANCEL '          CONSTANT
PARMMSG1 DC    C'OPERATOR PROMPT:' CONSTANT
PARMMSG2 DC    C'OPERATOR REPLY:'  CONSTANT
PARMM01  DC    AL2(PARMM01L)
         DC    C' HEX VALUE SPECIFIED WITH ODD NUMBER OF DIGITS FOR PAR+
               AMETER:'
PARMM01P DC    CL8' '
PARMM01L EQU   (*-PARMM01)-2
PARMM02  DC    AL2(PARMM02L)
         DC    C' HEX VALUE SPECIFIED WITH VALUE TOO LARGE FOR PARAMETE+
               R:'
PARMM02P DC    CL8' '
PARMM02L EQU   (*-PARMM02)-2
PARMM03  DC    AL2(PARMM03L)
         DC    C' CUU VALUE FOR PARAMETER '
PARMM03P DC    CL8' '
         DC    C' MUST CONTAIN 3 CHARACTERS'
PARMM03L EQU   (*-PARMM03)-2
PARMM04  DC    AL2(PARMM04L)
         DC    C' CUU VALUE FOR PARAMETER '
PARMM04P DC    CL8' '
         DC    C' CONTAINS INVALID DATA'
PARMM04L EQU   (*-PARMM04)-2
PARMM05  DC    AL2(PARMM05L)
         DC    C' DECIMAL VALUE TOO LONG FOR PARAMETER '
PARMM05P DC    CL8' '
PARMM05L EQU   (*-PARMM05)-2
PARMM06  DC    AL2(PARMM06L)
         DC    C' DECIMAL VALUE FOR PARAMETER '
PARMM06P DC    CL8' '
         DC    C' CONTAINS NON-NUMERIC DATA'
PARMM06L EQU   (*-PARMM06)-2
PARMM07  DC    AL2(PARMM07L)
         DC    C' VALUE FOR PARAMETER '
PARMM07P DC    CL8' '
         DC    C' IS TOO LONG'
PARMM07L EQU   (*-PARMM07)-2
PARMM08  DC    AL2(PARMM08L)
         DC    C' VALUE FOR PARAMETER '
PARMM08P DC    CL8' '
         DC    C' MUST BE 9 DIGITS (CCCCHHRRR)'
PARMM08L EQU   (*-PARMM08)-2
PARMM09  DC    AL2(PARMM09L)
         DC    C' RECORD NUMBER ZERO IS NOT ALLOWED'
PARMM09L EQU   (*-PARMM09)-2
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              EXTERNAL ROUTINE ADDRESSES                            *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINTER PARAMETER BLOCK                               *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER TABLE                                       *
*                                                                    *
*--------------------------------------------------------------------*
PARMTBLE DS    0H
         PARMGEN GEN=YES
         DC    X'FFFF'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER TABLE DSECT                                 *
*                                                                    *
*--------------------------------------------------------------------*
PARMD    DSECT
PARMELEN DS    XL2                 ENTRY LENGTH
PARMNAME DS    CL8                 PARAMETER NAME
PARMADDR DS    AL2                 RESULT FIELD OFFSET INTO DITTCOMM
PARMLEN  DS    AL2                 LENGTH OF RESULT FIELD
PARMID   DS    X                   PARAMETER ID
PARMDSCR DS    X                   PARAMETER DESCRIPTOR
$PARMBIN EQU   X'01'               .. FIELD IS BINARY
$PARMCUU EQU   X'02'               .. FIELD IS A 'CUU'
$PARMDEC EQU   X'04'               .. FIELD IS DECIMAL
$PARMCHR EQU   X'08'               .. FIELD IS CHARACTER
$PARMADR EQU   X'10'               .. FIELD IS DISK ADDRESS
PARMPFLG DS    X                   PROCESSING FLAGS
PARMCDSP DS    AL2                 FIELD CHECKER E.P. DISPLACEMENT
PARMMMSG DS    AL4                 ADDRESS OF MESSAGE FOR MISSING PARM
PARMMWTO DS    AL4                 ADDRESS OF WTO FOR MISSING PARM
PARMIWTO DS    AL4                 ADDRESS OF WTO FOR INVALID DATA
PARMWTOR DS    AL4                 ADDRESS OF WTOR FOR PARM PROMPT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER REQUEST/STATUS                              *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPARM
./ ADD NAME=DITTPRMD
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE DENSITY PARAMETER CONVERTER/CHECKER              *
*                                                                    *
*    THE TAPE DENSITY PARAMETER IS DEFINED IN THE 'PARMGEN' MACRO    *
*    AS HAVING FIELD 'COMMDWRD' AS ITS OUTPUT FIELD.  BY THE TIME    *
*    'DITTPARM' CALLS THIS MODULE, THE VALUE SPECIFIED WILL HAVE     *
*    BEEN MOVED INTO 'COMMDWRD', LEFT JUSTIFIED PADDED WITH BLANKS.  *
*    THIS MODULE CHECKS THE VALUE (MUST BE EITHER 1600 OR 6250) AND  *
*    CONVERTS THIS INTO THE TAPE COMMAND TO SET THE DESITY TO THE    *
*    REQUESTED MODE.  THE TAPE DENSITY MODE COMMANDS ARE DOCUMENTED  *
*    IN THE OLD "ASSEMBLER" REFERENCE CARD (NOW A PINK BOOK).        *
*                                                                    *
*    THE PARAMETER REQUEST BLOCK 'PARMBLOK' ADDRESS WILL BE PASSED   *
*    TO THIS MODULE IN R10.                                          *
*                                                                    *
*--------------------------------------------------------------------*
DITTPRMD DITTPRFX PRMDSAVE,'CONVERT/CHECK TAPE DENSITY'
         USING DITTCOMM,R11        SPECIFY BASE
         USING PARMBLOK,R10        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMDWRD      .. SAVE REQUESTED VALUE IN TRACE
PRMD0010 DS    0H
         CLC   DEN1600,COMMDWRD    1600 BPI?
         BE    PRMD0020            YES
         CLC   DEN6250,COMMDWRD    6250 BPI?
         BE    PRMD0030            YES
         OI    PARMFLAG,$PARMCKE   SET 'ERROR' FLAG
         LA    R1,PRMDM01          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN THE ADDRESS
         DITTRACE ID=BADDEN        TRACE BAD DENSITY REQUEST
         B     PRMD9900            AND EXIT
PRMD0020 DS    0H
         DITTRACE ID=DEN1600       1600 BPI SELECTED
         MVI   COMMDEN,$DEN1600    SET MODE TO 1600 BPI
         B     PRMD9900            AND EXIT
PRMD0030 DS    0H
         DITTRACE ID=DEN6250       6250 BPI SELECTED
         MVI   COMMDEN,$DEN6250    SET MODE TO 6250 BPI
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PRMD9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
PRMDSAVE DC    18F'0'              REGISTER SAVE AREA
DEN1600  DC    C'1600 '
DEN6250  DC    C'6250 '
PRMDM01  DC    AL2(PRMDM01L)
         DC    C' INVALID DENSITY REQUEST (ONLY 1600 AND 6250 ARE VALID+
               )'
PRMDM01L EQU   (*-PRMDM01)-2
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERSION REQUEST BLOCK                    *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPRMD
./ ADD NAME=DITTPRMO
*--------------------------------------------------------------------*
*                                                                    *
*              OPTION PARAMETER CHECKER                              *
*                                                                    *
*    THE 'OPT' PARAMETER WAS INVENTED TO SERVE MULTIPLE PURPOSES.    *
*    IT IS ALWAYS AN 'OPTIONAL' PARAMETER.  THE ONLY EDIT CHECKING   *
*    FOR 'OPT' IS TO VERIFY THAT IT MATCHES ONE OF THE VALUES        *
*    SPECIFIED IN THE 'OPTVAL=' PARAMETER OF THE 'DITTFUNC' MACRO    *
*    DEFINING THE CURRENT FUNCTION.                                  *
*                                                                    *
*    THE PARAMETER REQUEST BLOCK 'PARMBLOK' ADDRESS WILL BE PASSED   *
*    TO THIS MODULE IN R10.                                          *
*                                                                    *
*--------------------------------------------------------------------*
DITTPRMO DITTPRFX PRMOSAVE,'OPT PARAMETER CHECKER'
         USING DITTCOMM,R11        SPECIFY BASE
         USING PARMBLOK,R10        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
PRMO0010 DS    0H
         L     R1,PARMOPV          FIRST VALID OPTION VALUE
         LH    R2,PARMOPN          NUMBER OF VALID VALUES
PRMO0020 DS    0H
         CLC   COMMOPT,0(R1)       VALID OPTION VALUE?
         BE    PRMO0030            YES, EXIT
         LA    R1,8(R1)            NEXT OPTION
         BCT   R2,PRMO0020         LOOP
         OI    PARMFLAG,$PARMCKE   SET 'ERROR' FLAG
         LA    R1,PRMOM01          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN THE ADDRESS
         DITTRACE ID=BADOPT        TRACE BAD DENSITY REQUEST
         B     PRMO9900            AND EXIT
PRMO0030 DS    0H
         DITTRACE ID=GOODOPT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PRMO9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
PRMOSAVE DC    18F'0'              REGISTER SAVE AREA
PRMOM01  DC    AL2(PRMOM01L)
         DC    C'INVALID VALUE FOR OPTIONAL PARAMETER'
PRMOM01L EQU   (*-PRMOM01)-2
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERSION REQUEST BLOCK                    *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPRMO
./ ADD NAME=DITTPRMV
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE VOLSER PARAMETER CONVERTER/CHECKER               *
*                                                                    *
*    THE TAPE INITIALIZATION FUNCTION WILL INITIALIZE 1 TAPE WITH A  *
*    GIVEN VOLSER, OR A NUMBER OF TAPES WITH A SPECIFIED RANGE.      *
*    VOLSERS NEED NOT BE ALL NUMERIC.  IF ONLY 1 TAPE IS TO BE       *
*    INITIALIZED, ANY 1 TO 6 CHARACTER VOLSER WILL BE ACCEPTED.      *
*    IF A RANGE IS TO BE INITIALIZED, AT LEAST THE LAST CHARACTER    *
*    MUST BE NUMERIC.  THE "PREFIX" WILL BE PROPAGATED TO ALL TAPES  *
*    INITIALIZED AND MUST BE IDENTICAL IN THE LOW AND HIGH VOLSERS   *
*    IN THE INT COMMAND.  THE "SUFFIX" WILL BE INCREMENTED 1 DIGIT   *
*    ON EACH SUCCESSIVE INIT UNTIL THE HIGH VOLSER IS REACHED.       *
*    THE PREFIX WILL BE DETERMINED BY SCANNING THE VOLSER GIVEN      *
*    BACKWARDS TO THE FIRST NON-NUMERIC VALUE.                       *
*                                                                    *
*--------------------------------------------------------------------*
DITTPRMV DITTPRFX PRMVSAVE,'CHECK VOLSER RANGE'
         USING DITTCOMM,R11        SPECIFY BASE
         USING PARMBLOK,R10        SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=COMMLOV,      .. SAVE LOW VOLSER                  +
               DATA2=COMMHIV       .. SAVE HIGH VOLSER
PRMV0010 DS    0H
         LA    R1,COMMLOV+(L'COMMLOV-1)   LAST CHARACTER OF LOW VOLSER
         LA    R2,COMMHIV+(L'COMMHIV-1)   LAST CHARACTER OF HIGH VOLSER
         LA    R3,L'COMMLOV        MAXIMUM DIGITS
PRMV0020 DS    0H
         CLI   0(R1),C'0'          VALID DIGIT??
         BL    PRMV0030            NO
         CLI   0(R1),C'9'          VALID DIGIT??
         BH    PRMV0030            NO
         CLI   0(R2),C'0'          VALID DIGIT??
         BL    PRMV0050            NO..
         CLI   0(R2),C'9'          VALID DIGIT??
         BH    PRMV0050            NO..
         BCTR  R1,0                MINUS 1
         BCTR  R2,0                MINUS 1
         BCT   R3,PRMV0020         LOOP
         B     PRMV0040            ALL NUMERIC, CHECK FOR LOW > HIGH
PRMV0030 DS    0H
         CH    R3,VOLSERL          VOLSER ALL NON-NUMERIC??
         BE    PRMV0060            YES... CAN'T BE A RANGE
         BCTR  R3,0                MINUS 1
         EX    R3,PFXCLC           CHECK LOW TO HIGH VOLSER PREFIX
         BNE   PRMV0070            PREFIXES DON'T MATCH
PRMV0040 DS    0H
         CLC   COMMHIV,COMMLOV     LOW > HIGH??
         BH    PRMV9900            NO.. ALL'S WELL
         LA    R1,PRMVM01          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PRMV0080            SET ERROR FLAG
PRMV0050 DS    0H
         LA    R1,PRMVM02          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PRMV0080
PRMV0060 DS    0H
         LA    R1,PRMVM03          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
         B     PRMV0080
PRMV0070 DS    0H
         LA    R1,PRMVM04          MESSAGE ADDRESS
         ST    R1,PARMMSGA         RETURN MESSAGE ADDRESS
PRMV0080 DS    0H
         OI    PARMFLAG,$PARMCKE   SET 'ERROR' INDICATOR
         B     PRMV9900            AND EXIT
PFXCLC   CLC   COMMLOV(0),COMMHIV  CHECK PREFIXES
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PRMV9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
PRMVSAVE DC    18F'0'              REGISTER SAVE AREA
VOLSERL  DC    AL2(L'COMMLOV)
PRMVM01  DC    AL2(PRMVM01L)
         DC    C' RANGE LOW LIMIT IS LARGER THAN RANGE HIGH LIMIT'
PRMVM01L EQU   (*-PRMVM01)-2
PRMVM02  DC    AL2(PRMVM02L)
         DC    C' INVALID CHARACTER IN VOLSER'
PRMVM02L EQU   (*-PRMVM02)-2
PRMVM03  DC    AL2(PRMVM03L)
         DC    C' RANGES ARE NOT VALID WITH ALL NON-NUMERIC VOLSER, COM+
               MAND WILL BE ABORTED'
PRMVM03L EQU   (*-PRMVM03)-2
PRMVM04  DC    AL2(PRMVM04L)
         DC    C' PREFIXES MUST MATCH ON LOW AND HIGH VALUE OF A RANGE'
PRMVM04L EQU   (*-PRMVM04)-2
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERSION REQUEST BLOCK                    *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPRMV
./ ADD NAME=DITTPRO1
*--------------------------------------------------------------------*
*                                                                    *
*            DATA PRINT                                              *
*                                                                    *
*       THIS IS MODULE PRODUCES THE STANDARD OR VERTICAL HEX PRINT   *
*       OUTPUT.  IT IS ALWAYS LINKED TO FROM 'DITTMAIN' AS AN OUTPUT *
*       MODULE.  THE PRINT LINES ARE BUILT AND PASSED TO 'DITTPRT'   *
*       TO DO THE ACTUAL PRINTING.                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTPRO1 DITTPRFX PRTSAVE,'DATA PRINT'
         USING DITTCOMM,R11        ADDRESS IT
         DITTRACE ID=ENTRY         TRACE ENTRY
PRT0010  DS    0H
         TM    COMMFLAG,$COMMEOF   EOF FLAG ON?
         BO    PRT0070             YES
         DITTRACE ID=RECORD        TRACE STARTING RECORD PRINT
         ICM   R10,15,COMMIND      INPUT DYNAMIC BLOCK ADDRESS
         BZ    PRT0030             NO DYNAMIC BLOCK...
         USING DYNBLOK,R10         DEFINE BASE
         CLC   PRTDASD,DYNDDNAM    INPUT FROM DASD?
         BNE   PRT0030             NO
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ?
         BNO   PRT0030             NO
         SR    R1,R1               CLEAR REGISTER
         IC    R1,CNTREC           RECORD NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT RECORD NUMBER
         MVC   DASDREC,EDWORK+3    COPY RECORD NUMBER
         IC    R1,CNTKEYL          KEY LENGTH FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD3      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT KEY LENGTH
         MVC   DASDKL,EDWORK+3     COPY KEY LENGTH
         ICM   R1,3,CNTHEAD        HEAD NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT HEAD NUMBER
         MVC   DASDHEAD,EDWORK+4   COPY RECORD NUMBER
         ICM   R1,3,CNTCYL         CYLINDER NUMBER FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD2      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT CYLINDER NUMBER
         MVC   DASDCYL,EDWORK+2    COPY CYLINDER NUMBER
         ICM   R1,3,CNTDATAL       DATA LENGTH FROM 'COUNT'
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD3      INTIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT DATA LENGTH
         MVC   DASDDL,EDWORK+1     COPY DATA LENGTH
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTAREA(DASDMSGL),DASDMSG
         BAL   R8,PRT0100          PRINT DASD ADDRESS MESSAGE
         BAL   R8,PRT0100          PRINT BLANK LINE
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,1,CNTKEYL        PICK UP KEY LENGTH
         BZ    PRT0020             NO KEY
         L     R3,COMMKEY          KEY'S ADDRESS
         MVC   PRTAREA1,KEYHEAD    MOVE 'KEY' TO LINE HEADING1 AREA
         MVC   PRTAREA2,COMMBLKS   CLEAR LINE HEADING 2
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT KEY
         BAL   R8,PRT0100          PRINT BLANK LINE
PRT0020  DS    0H
         TM    COMMIFLG,$DEBLOCK   IS INPUT DEBLOCKING RECORDS?
         BO    PRT0030             YES.. PRINT LOGICAL RECORD
         L     R3,COMMCRCD         DATA'S ADDRESS
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,3,CNTDATAL       PICK UP DATA LENGTH
         BZ    PRT9900             ALL DONE
         MVC   PRTAREA1,DATAHEAD   MOVE 'DATA' TO LINE HEADING 1 AREA
         MVC   PRTAREA2,COMMBLKS   CLEAR LINE HEADING 2
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT DATA
         B     PRT9900             AND EXIT
PRT0030  DS    0H
         L     R3,COMMCRCD         DATA'S ADDRESS
         SR    R4,R4               CLEAR REGISTER
         ICM   R4,3,COMMCRCL       CURRENT RECORD LENGTH
         MVC   PRTAREA,PRTAREA-1   INITIALIZE PRINT LINE
         MVC   PRTAREA1,=C'RECORD SIZE '
         CVD   R4,COMMDWRD         CONVERT RECORD LENGTH TO DECIMAL
         MVC   PRTAREA2,EDWORD1    INITIALIZE WITH EDIT WORD
         ED    PRTAREA2,COMMDWRD+5 EDIT RECORD LENGTH
         ZAP   PRT100,P0           RESET SCALE OFFSET COUNTER
         BAL   R9,PRT0040          PRINT DATA
         B     PRT9900             AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        DATA PRINTING.  AT ENTRY THIS CODE EXPECTS R3 TO BE THE      *
*        DATA ADDRESS, R4 TO BE THE DATA LENGTH, AND R9 TO BE THE     *
*        RETURN ADDRESS.                                              *
*                                                                     *
*        R1, R2, R3, R4, R14, AND R15 WILL BE MODIFIED                *
*                                                                     *
* ------------------------------------------------------------------- *
PRT0040  DS    0H
         DITTRACE ID=PRT0040,      TRACE POINT                         +
               RDATA1=R3,          .. DATA ADDRESS                     +
               RDATA2=R4           .. DATA LENGTH
         LR    R2,R4               COPY RECORD LENGTH
         CH    R2,H100             TOO MUCH FOR 1 LINE?
         BNH   PRT0050             NO, CONTINUE
         LH    R2,H100             LIMIT LENGTH TO 100
PRT0050  DS    0H
         DITTRACE ID=PRT0050,      TRACE POINT                         +
               RDATA1=R11,         .. COMM AREA ADDRESS                +
               RDATA2=R12          .. BASE ADDRESS
         BCTR  R2,0                ADJUST FOR EXECUTES
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,PRTTR            TRANSLATE OUT UNPRINTABLES
         BAL   R8,PRT0100          PRINT
         TM    COMMOFLG,$HEX       PRINTING IN HEX?
         BNO   PRT0060             NO
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,ZONEMVC          PREPARE FOR ZONES
         EX    R2,HEXCHAR          TRANSLATE HEX TO CHARACTER
         BAL   R8,PRT0100          PRINT
         EX    R2,CHARMVC          MOVE DATA TO PRINT AREA 3
         EX    R2,NUMRMVC          PREPARE FOR NUMERICS
         EX    R2,HEXCHAR          TRANSLATE HEX TO CHARACTER
         BAL   R8,PRT0100          PRINT
         EX    R2,SCALEMVC         MOVE SCALE
         MVC   PRTPR100,EDWORD4    INITIALIZE WITH EDIT WORD
         ED    PRTPR100,PRT100     EDIT SCALE OFFSET
         BAL   R8,PRT0100          PRINT
         BAL   R8,PRT0100          PRINT BLANK LINE
         AP    PRT100,P10          ADD TO SCALE OFFSET
PRT0060  DS    0H
         LA    R2,1(R2)            RESET LENGTH
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
         SR    R4,R2               COMPUTE LENGTH REMAINING
         BNZ   PRT0040             REPEAT 'TILL ALL LENGTH DONE
         BR    R9                  RETURN
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
PRT0070  DS    0H
         DITTRACE ID=EOF
         B     PRT9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
PRT0100  DS    0H
         MVI   PRTCMMD,$PRTDATA    INSERT PRINT COMMAND
         MVC   PRTDATA,PRTAREA     MOVE DATA TO PRINT
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         MVC   PRTAREA,PRTAREA-1   INITIALIZE PRINT LINE
         BR    R8                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PRT9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXECUTED INSTRUCTIONS                                 *
*                                                                    *
*--------------------------------------------------------------------*
CHARMVC  MVC   PRTAREA3(0),0(R3)   MOVE DATA TO PRINT AREA
PRTTR    TR    PRTAREA3(0),CHARTBL TRANSLATE OUT UNPRINTABLES
ZONEMVC  MVN   PRTAREA3(0),HEX0000 PREPARE FOR ZONE TRANSLATE
NUMRMVC  MVZ   PRTAREA3(0),HEX0000 PREPARE FOR NUMERIC TRANSLATE
HEXCHAR  TR    PRTAREA3(0),HEXTBL  TRANSLATE HEX TO CHARACTER
SCALEMVC MVC   PRTAREA3(0),SCALE   MOVE SCALE
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINTABLE CHARACTERS TABLE                            *
*                                                                    *
*--------------------------------------------------------------------*
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F
CHARTBL  DC    XL16'40404040404040404040404040404040'     X'00' - X'0F'
         DC    XL16'40404040404040404040404040404040'     X'10' - X'1F'
         DC    XL16'40404040404040404040404040404040'     X'20' - X'2F'
         DC    XL16'40404040404040404040404040404040'     X'30' - X'3F'
         DC    XL16'404040404040404040404A4B4C4D4E4F'     X'40' - X'4F'
         DC    XL16'504040404040404040405A5B5C5D5E5F'     X'50' - X'5F'
         DC    XL16'60614040404040404040406B6C6D6E6F'     X'60' - X'6F'
         DC    XL16'404040404040404040407A7B7C7D7E7F'     X'70' - X'7F'
         DC    XL16'40404040404040404040408B8C8D8E8F'     X'80' - X'8F'
         DC    XL16'40404040404040404040409B9C9D9E9F'     X'90' - X'9F'
         DC    XL16'4040404040404040404040ABACADAEAF'     X'A0' - X'AF'
         DC    XL16'4040404040404040404040BBBCBDBEBF'     X'B0' - X'BF'
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'     X'C0' - X'CF'
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'     X'D0' - X'DF'
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'     X'E0' - X'EF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'     X'F0' - X'FF'
*
HEXTBL   DC    CL16'0123456789ABCDEF'                    X'00' - X'0F'
         DC    CL16'1               '                    X'10' - X'1F'
         DC    CL16'2               '                    X'20' - X'2F'
         DC    CL16'3               '                    X'30' - X'3F'
         DC    CL16'4               '                    X'40' - X'4F'
         DC    CL16'5               '                    X'50' - X'5F'
         DC    CL16'6               '                    X'60' - X'6F'
         DC    CL16'7               '                    X'70' - X'7F'
         DC    CL16'8               '                    X'80' - X'8F'
         DC    CL16'9               '                    X'90' - X'9F'
         DC    CL16'A               '                    X'A0' - X'AF'
         DC    CL16'B               '                    X'B0' - X'BF'
         DC    CL16'C               '                    X'C0' - X'CF'
         DC    CL16'D               '                    X'D0' - X'DF'
         DC    CL16'E               '                    X'E0' - X'EF'
         DC    CL16'F               '                    X'F0' - X'FF'
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERCOMMUNICATION                       *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE WORK AREAS                               *
*                                                                    *
*--------------------------------------------------------------------*
PRTSAVE  DC    18F'0'              REGISTER SAVE AREA
H100     DC    H'100'              CONSTANT
PRTFLAG  DC    X'00'               FLAGS/SWITCHES
HEX0000  DC    100X'00'
EDWORD1  DC    X'4020206B202120'   CONSTANT
EDWORD2  DC    X'F02020202020'     CONSTANT
EDWORD3  DC    X'402020202120'     CONSTANT
EDWORD4  DC    X'402020202020'     CONSTANT
PRT100   DC    PL3'0'              SCALE OFFSET COUNTER
P0       DC    P'0'                CONSTANT
P10      DC    P'10'               CONSTANT
PRTDASD  DC    C'DASD'             CONSTANT
KEYHEAD  DC    CL12'KEY'
DATAHEAD DC    CL12'DATA'
EDWORK   DC    CL6' '
         DC    C' '
PRTAREA  DS    0CL121              PRINT LINE
PRTAREA1 DC    CL12' '             COMMENT AREA
PRTAREA2 DC    CL7' '              RECORD SIZE AREA
         DC    C' '                SPACE
         ORG   *-6
PRTPR100 DC    CL6' '
PRTAREA3 DC    100C' '
DASDMSG  DS    0C
         DC    C' DASD ADDRESS:'
DASDCYL  DC    CL4' '
         DC    C'-'
DASDHEAD DC    CL2' '
         DC    C'-'
DASDREC  DC    CL3' '
         DC    C'   KEY LENGTH='
DASDKL   DC    CL4' '
         DC    C'   DATA LENGTH='
DASDDL   DC    CL5' '
DASDMSGL EQU   *-DASDMSG
SCALE    DC    C'1...+...10....+...20....+...30....+...40....+...50....+
               +...60....+...70....+...80....+...90....+..100'
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=SHORT
         END  DITTPRO1
./ ADD NAME=DITTPRT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE                                          *
*                                                                    *
*      THIS MODULE DOES ALL PRINTING FOR DITTO... ALL COMMANDS,      *
*      MESSAGES, AND SYSOUT OUTPUT IS DONE BY THIS MODULE.  ANY      *
*      MODULE EXCEPT THE TRACE ROUTINE IN 'DITTCOMM' MAY LINK TO     *
*      THIS MODULE.                                                  *
*                                                                    *
*--------------------------------------------------------------------*
DITTPRT  DITTPRFX PRTSAVE,'PRINTER MODULE'
         USING DITTCOMM,R11        ADDRESS IT
         LR    R10,R1              SAVE INPUT PARAMETER ADDRESS
         USING PRTBLOK,R10         SPECIFY BASE
         DITTRACE ID=ENTRY,        TRACE ENTRY                         +
               DATA1=PRTFLAG       .. CAPTURE FLAG STATUS
         TM    COMMFLAG,$COMMEOJ   END OF JOB IN PROGRESS??
         BNO   PRT0010             NO
         TM    PRTFLAG,$PRTOPEN    IS PRINT FILE OPEN??
         BNO   PRT9900             NO, EXIT
         DITTRACE ID=CLOSE         TRACE PRINTER CLOSES
         CLOSE PRINTER             OTHERWISE CLOSE PRINT FILE
         NI    PRTFLAG,255-$PRTOPEN TURN OFF OPEN FLAG
         B     PRT9900             EXIT THIS MODULE
PRT0010  DS    0H
         TM    PRTFLAG,$PRTOPEN    IS PRINT FILE OPEN??
         BO    PRT0020             YES, BYPASS OPEN
         DITTRACE ID=OPEN          TRACE PRINTER OPENS
         OPEN  (PRINTER,OUTPUT)    OPEN PRINTER FILE
         OI    PRTFLAG,$PRTOPEN    INDICATE PRINT FILE IS OPEN
PRT0020  DS    0H
         CLI   PRTCMMD,$PRTHEAD    FORCED HEADING??
         BE    PRT0080             YES
         CLI   PRTCMMD,$PRTDATA    PRINTING DATA?
         BE    PRT0040             YES..
*--------------------------------------------------------------------*
*                                                                    *
*         PRINTING A COMMAND                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DITTRACE ID=PRINTCMD      PRINTING A COMMAND OR COMMAND MSG
         TM    PRTFLAG,$PRTDATA    WAS DATA PRINTED LAST TIME?
         BNO   PRT0030             NO
         BAL   R9,PRT0090          PRINT A NEW HEADING
PRT0030  DS    0H
         NI    PRTFLAG,255-$PRTDATA    TURN DATA FLAG OFF
         OI    PRTFLAG,$PRTCMD     TURN COMMAND FLAG ON
         BAL   R9,PRT0060          PRINT COMMAND
         B     PRT9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*         PRINTING DATA                                              *
*                                                                    *
*--------------------------------------------------------------------*
PRT0040  DS    0H
         DITTRACE ID=PRTDATA
         TM    PRTFLAG,$PRTCMD     WAS A COMMAND PRINTED LAST TIME?
         BNO   PRT0050             NO
         BAL   R9,PRT0090          PRINT NEW HEADING
PRT0050  DS    0H
         NI    PRTFLAG,255-$PRTCMD TURN OFF COMMAND PRINTED
         OI    PRTFLAG,$PRTDATA    TURN ON DATA PRINTED
         BAL   R9,PRT0060          PRINT DATA
         B     PRT9900             AND EXIT
PRT0060  DS    0H
         PUT   PRINTER,PRTCC       PRINT THE REQUESTED LINE
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
         CLI   PRTCC,C' '          SINGLE SPACE??
         BE    PRT0070             YES
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
         CLI   PRTCC,C'0'          DOUBLE SPACE??
         BE    PRT0070             YES
         AP    COMMLINE,COMMP1     ADD 1 TO LINE COUNT
PRT0070  DS    0H
         MVI   PRTCC,C' '          CLEAR CARRIAGE CONTROL
         MVC   PRTDATA,PRTCC       CLEAR PRINT DATA
         CP    COMMLINE,COMMP60    IS PAGE FULL??
         BLR   R9                  NO, EXIT
         DITTRACE ID=PAGEFULL      PAGE FULL
         B     PRT0090
PRT0080  DS    0H
         DITTRACE ID=FORCEDHD      FORCED HEADING
         BAL   R9,PRT0090          PRINT HEADING
         B     PRT9900             AND EXIT
PRT0090  DS    0H
         DITTRACE ID=HEADING
         AP    PRTPAGE,COMMP1      ADD 1 TO PAGE COUNT
         MVC   PRPAGE,=X'4020206B202021'
         ED    PRPAGE,PRTPAGE      EDIT PAGE NUMBER
         PUT   PRINTER,DITTHEAD    PRINT HEADING
         ZAP   COMMLINE,COMMP1     RESET LINE COUNT
         AP    COMMLINE,COMMP1     RESET LINE COUNT
         BR    R9                  PRINTING DONE
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
PRT9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE WORK AREAS                               *
*                                                                    *
*--------------------------------------------------------------------*
PRTSAVE  DC    18F'0'              REGISTER SAVE AREA
*--------------------------------------------------------------------*
*                                                                    *
*   THE BITS IN PRTFLAG ALSO HAVE DEFINITIONS IN THE 'PRTCMMD'       *
*   FIELD OF THE 'PRTBLOK' DSECT ($PRTCMD AND $PRTDATA).  IF NEW     *
*   FLAGS ARE DEFINED IN PRTFLAG, YOU MUST USE BITS OTHER THAN THE   *
*   ONES USED FOR $PRTCMD AND $PRTDATA.                              *
*                                                                    *
*--------------------------------------------------------------------*
PRTFLAG  DC    X'00'               FLAGS/SWITCHES
$PRTOPEN EQU   X'80'               .. FILE OPEN
PRTPAGE  DC    PL3'0'              PAGE COUNTER
DITTHEAD DC    C'1'                CARRIAGE CONTROL
         DC    CL108'            OS/DITTO'
         DC    C'PAGE '
PRPAGE   DC    CL7' '              PAGE NUMBER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE PRINTER DCB                              *
*                                                                    *
*--------------------------------------------------------------------*
PRINTER  DCB   DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               RECFM=FBA,                                              +
               LRECL=121,                                              +
               MACRF=PM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERCOMMUNICATION                       *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK TYPE=DSECT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         END  DITTPRT
./ ADD NAME=DITTSTAE
*--------------------------------------------------------------------*
*                                                                    *
*              STAE EXIT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         MACRO
&NAME    MSG   &A,&B,&C,&D
         LCLC  &TEXT
         AIF   (T'&A EQ 'O').NOTEXT
&TEXT    SETC  '&A'
         AIF   (T'&B EQ 'O').M0010
&TEXT    SETC  '&TEXT.,&B'
         AIF   (T'&C EQ 'O').M0010
&TEXT    SETC  '&TEXT.,&C'
         AIF   (T'&D EQ 'O').M0010
&TEXT    SETC  '&TEXT.,&D'
.M0010   ANOP
&NAME    DC    AL2(&NAME.E-&NAME)
         DC    AL2(0)
         DC    &TEXT
&NAME.E  EQU   *
         MEXIT
.NOTEXT  MNOTE 12,'NO TEXT'
         MEND
DITTSTAE DITTPRFX STAESAVE,'STAE EXIT'
         L     R11,VCOMM                SET COMM AREA BASE
         USING DITTCOMM,R11             DEFINE COMM AREA BASE
         MVI   STAEFLAG,0               RESET ALL STAE FLAGS
*--------------------------------------------------------------------*
*              DETERMINE ABEND CODE                                  *
*--------------------------------------------------------------------*
         CH    R0,H12                   SDWA PROVIDED?
         BNE   STAE0010                 YES
         LR    R15,R1                   COPY ABEND CODE
         SRL   R1,12                    SHIFT OUT USER CODE IF PRESENT
         STCM  R1,3,STAESCDE            SAVE SYSTEM ABEND CODE
         STCM  R15,3,STAEUCDE           SAVE USER ABEND CODE
         NI    STAEUCDE,X'0F'           TURN OFF ANY SYSTEM CODE BITS
         B     STAE0020                 CHECK FOR DITTDUMP DD
STAE0010 DS    0H
         OI    STAEFLAG,$SDWA           SDWA IS PRESENT
         LR    R3,R1                    COPY SDWA ADDRESS
         USING SDWA,R3                  DEFINE SDWA ADDRESSABILITY
         SR    R15,R15                  CLEAR REGISTER
         ICM   R15,3,SDWACMPC           SYSTEM CODE
         SRL   R15,4                    SHIFT OUT USER BITS
         STCM  R15,3,STAESCDE           SAVE SYSTEM CODE
         MVC   STAEUCDE,SDWACMPC+1      COPY USER CODE
         NI    STAEUCDE,X'0F'           TURN OFF ANY SYSTEM CODE BITS
*--------------------------------------------------------------------*
*              DETERMINE IF DITTDUMP DD STATEMENT IS PRESENT         *
*--------------------------------------------------------------------*
STAE0020 DS    0H
         EXTRACT TIOTADDR,FIELDS=TIOT   REQUEST TIOT ADDRESS
         L     R2,TIOTADDR              OUR TIOT'S ADDRESS
         USING TIOT1,R2                 DEFINE TIOT ADDRESSABILITY
         SR    R1,R1                    CLEAR FOR TIOT ENTRY LENGTH
STAE0030 DS    0H
         ICM   R1,1,TIOELNGH            INSERT LENGTH OF THIS ENTRY
         BZ    STAE0050                 DITTDUMP DOES NOT EXIST
         CLC   TIOEDDNM,=CL8'DITTDUMP'  IS THIS THE DITTDUMP DD?
         BE    STAE0040                 YES
         AR    R2,R1                    ADDRESS OF NEXT TIOT ENTRY
         B     STAE0030                 CHECK AGAIN
STAE0040 DS    0H
         OI    STAEFLAG,$DUMPDD         DITTDUMP DD IS PRESENT
         OPEN  (DITTDUMP,OUTPUT)        OPEN STAE OUTPUT
         OI    STAEFLAG,$PRTOPEN        INDICATE PRINT IS OPEN
         BAL   R10,HEAD0000             PRINT STAE HEADING
STAE0050 DS    0H
         OC    STAEUCDE,STAEUCDE        USER CODE?
         BNZ   STAE0060                 YES
         MVC   PRABTYPE,TYPESYS         SYSTEM ABEND
         UNPK  PRABCODE(5),STAESCDE(3)  UNPACK ABEND CODE
         MVZ   PRABCODE,HEX0000         PREPARE FOR TRANSLATE
         TR    PRABCODE,HEXCHAR         TRANSLATE TO PRINTABLE
         MVI   PRABCODE,C' '            BLANK OUT FIRST DIGIT
         MVI   PRABCODE+4,C' '          RESTORE THE BLANK
         B     STAE0070                 PRINT ABEND CODE MESSAGE
STAE0060 DS    0H
         MVC   PRABTYPE,TYPEUSER        SYSTEM ABEND
         LH    R1,STAEUCDE              USER CODE
         CVD   R1,STAEDWRD              CONVERT TO DECIMAL
         UNPK  PRABCODE(5),STAEDWRD+6(3)
         MVI   PRABCODE+3,C' '          RESTORE THE BLANK
         MVI   PRABCODE+4,C' '          RESTORE THE BLANK
STAE0070 DS    0H
         LA    R15,STAEM01
         BAL   R10,PRT0000        PRINT ABEND MESSAGE
*--------------------------------------------------------------------*
*              DETERMINE MODULE IN CONTROL AT TIME OF ABEND          *
*--------------------------------------------------------------------*
         TM    STAEFLAG,$SDWA          SDWA PRESENT?
         BNO   STAE0120                NO
         CLC   SDWANXT1,ACOMM          ADDRESS TOO LOW?
         BL    STAE0130                YES.. NOT IN DITTO
         CLC   SDWANXT1,AXXXX          ADDRESS TOO HIGH?
         BH    STAE0130                YES.. NOT IN DITTO
         LA    R1,AXXXX-4              LAST MODULE ID/ADDRESS
         B     STAE0090                ENTER LOOP
STAE0080 DS    0H
         CLC   ISP,0(R1)               IS THIS THE ISP MODULE ENTRY?
         BE    STAE0090                YES.. IGNORE IT
         CLC   SDWANXT1,4(R1)          ADDRESS WITHIN THIS CSECT?
         BH    STAE0100                YES
         CLC   0(R1),ACOMM-4           COMMON AREA JUST CHECKED?
         BE    STAE0110                YES.. SHOULD BE A LOGIC ERROR
STAE0090 DS    0H
         SH    R1,H8                   PREVIOUS MODULE ID/ADDRESS
         B     STAE0080                CONTINUE THE SEARCH
STAE0100 DS    0H
         MVC   PRCSECT,0(R1)           MOVE CSECT NAME
         L     R1,4(R1)                CSECT NAME
         L     R2,SDWANXT1             ABEND POINT
         SR    R2,R1                   DISPLACEMENT INTO ABENDING CSECT
         ST    R2,STAEDWRD             SAVE DISPLACEMENT
         UNPK  PRDISP(9),STAEDWRD(5)
         MVZ   PRDISP,HEX0000          PREPARE FOR TRANSLATE
         TR    PRDISP,HEXCHAR          TRANSLATE TO PRINTABLE
         MVI   PRDISP+8,C' '           RESTORE THE BLANK
         LA    R15,STAEM02
         BAL   R10,PRT0000             PRINT CSECT/DISP MESSAGE
         B     STAE0200                PROCESS COMMON AREA
STAE0110 DS    0H
         LA    R15,STAEM03
         BAL   R10,PRT0000             PRINT
         B     STAE0200                PROCESS COMMON AREA
STAE0120 DS    0H
         LA    R15,STAEM04
         BAL   R10,PRT0000             PRINT
         B     STAE0200                PROCESS COMMON AREA
STAE0130 DS    0H
         LA    R15,STAEM05
         BAL   R10,PRT0000        PRINT
*--------------------------------------------------------------------*
*              DUMP COMMON AREA                                      *
*--------------------------------------------------------------------*
STAE0200 DS    0H
         LA    R15,STAEM07             *-------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *             *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM19             * COMMON AREA *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *             *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM07             *-------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM20
         BAL   R10,PRT0000             PRINT
         LR    R2,R11                  SET COMMON AREA ADDRESS
         LA    R3,COMMTR00-DITTCOMM    COMMON AREA LENGTH
         BAL   R9,STAE0900             DUMP COMMON AREA
*--------------------------------------------------------------------*
*              DUMP DYNAMIC BLOCKS                                   *
*--------------------------------------------------------------------*
         ICM   R8,15,COMMDYHD          FIRST DYNAMIC BLOCK
         BNZ   STAE0210                PROCESS DYNAMIC BLOCKS
         LA    R15,STAEM06
         BAL   R10,PRT0000             NO DYNAMIC BLOCKS
         B     STAE0400                PROCESS TRACE TABLE
STAE0210 DS    0H
         BAL   R10,HEAD0000            FORCE HEADING
STAE0220 DS    0H
         ST    R8,STAEDWRD             SAVE DYNAMIC BLOCK'S ADDRESS
         USING DYNBLOK,R8
         UNPK  PRDYNADR(9),STAEDWRD(5) UNPACK DYNAMIC BLOCK'S ADDRESS
         MVZ   PRDYNADR,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDYNADR,HEXCHAR        TRANSLATE ADDRESS TO PRINTABLE
         MVI   PRDYNADR+8,C' '         RESTORE THE BLANK
         LA    R15,STAEM07             *---------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *               *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM09             * DYNAMIC BLOCK *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *               *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM07             *---------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM20
         BAL   R10,PRT0000             PRINT
         UNPK  PRDYNNXT(9),DYNNEXT(5)
         MVZ   PRDYNNXT,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDYNNXT,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDYNNXT+8,C' '         RESTORE BLANK
         UNPK  PRDYNPRV(9),DYNPREV(5)
         MVZ   PRDYNPRV,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDYNPRV,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDYNPRV+8,C' '         RESTORE BLANK
         UNPK  PRDYNCUU(5),DYNCUU(3)
         MVZ   PRDYNCUU,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDYNCUU,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDYNCUU,C' '           RESTORE BLANK
         MVI   PRDYNCUU+4,C' '         RESTORE BLANK
         MVC   PRDYNDDN,DYNDDNAM       COPY DDNAME
         MVC   PRDYNDVL,DYNVOL         COPY DISK VOLSER
         UNPK  PRDYNECB(9),DYNECB(5)
         MVZ   PRDYNECB,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDYNECB,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDYNECB+8,C' '         RESTORE BLANK
         LA    R15,STAEM10
         BAL   R10,PRT0000             PRINT
         LR    R2,R8                   COPY DYNBLOCK ADDRESS
         LA    R3,DYNBLOKL             DYNBLOCK LENGTH
         BAL   R9,STAE0900             DUMP DYNBLOCK
         ICM   R8,15,DYNNEXT           NEXT ON CHAIN
         BNZ   STAE0220                PROCESS ALL ON CHAIN
*--------------------------------------------------------------------*
*              'CLOSE' DCB'S IN DYNAMIC BLOCKS                       *
*--------------------------------------------------------------------*
STAE0300 DS    0H
         ICM   R8,15,COMMDYHD          FIRST DYNAMIC BLOCK
         CLI   COMMENV,$ENVTSO         TSO ENVIRONMENT?
         BNE   STAE0310                NO
         SR    R1,R1                   'TURN ON' REQUEST
         L     R15,AAUTH               DITTAUTH ENTRY POINT
         BALR  R14,R15                 LINK TO DITTAUTH
STAE0310 DS    0H
         MODESET MODE=SUP,KEY=ZERO     ENTER SUPERVISOR STATE, KEY ZERO
STAE0320 DS    0H
         LA    R4,DYNDCB               DCB FOR THIS UNIT
         USING IHADCB,R4               DEFINE DSECT BASE
         DEBCHK (R4),                  DEBCHK (REFERENCE DCB NOT DEB)  +
               TYPE=DELETE             .. DELETE THE DEB FROM DEBTABLE
         LTR   R15,R15                 DEBCHK SUCCESSFUL?
         BNZ   STAE0370                NO.. TRY CLEAN-UP FOR NEXT BLOCK
STAE0330 DS    0H
         L     R7,DYNDEBA              DEB ADDRESS IN THIS DYN BLOCK
         USING DEB,R7                  DEFINE DEB BASE
         LA    R2,DEBBASIC             BASIC PORTION OF DEB
         USING PSA,R0                  DEFINE PSA BASE
         L     R1,PSATNEW              CURRENT TCB ADDRESS FROM PSA
         USING TCB,R1                  DEFINE DCB BASE
         SR    R3,R3                   CLEAR REGISTER
         ICM   R3,7,TCBDEB+1           1ST DEB ON DEB CHAIN
         LA    R4,TCBDEB               ADDRESS'S ADDRESS
STAE0340 DS    0H
         CR    R3,R2                   IS THIS THE CORRECT DEB?
         BE    STAE0350                YES
         LA    R4,DEBDEBAD-DEBBASIC(R3)   NEXT DEB ADDRESS ADDRESS
         ICM   R3,7,DEBDEBB-DEBBASIC(R3)  NEXT DEB
         BNZ   STAE0340                CHECK AGAIN
         MVC   STAEM12D,DYNDDNAM       COPY DDNAME
         LA    R15,STAEM12
         BAL   R10,PRT0000             COULD NOT LOCATE DEB...
         B     STAE0370                TRY CLEAN-UP FOR NEXT BLOCK
STAE0350 DS    0H
         ICM   R2,7,DEBDEBB            NEXT DEB ON CHAIN
         ICM   R2,8,0(R4)              SAVE ACCESS METHOD SECT LENGTH
         ICM   R3,15,0(R4)             CS WILL USE ALL 4 BYTES
         CS    R3,R2,0(R4)             REMOVE DEB FROM DEB CHAIN
         BNE   STAE0330                SOMETHING CHANGED, TRY AGAIN
         MVC   STAEM13D,DYNDDNAM       COPY DDNAME
         LA    R15,STAEM13
         BAL   R10,PRT0000             CLEAN-UP COMPLETE
         B     STAE0370                TRY CLEAN-UP FOR NEXT BLOCK
STAE0360 DS    0H
         MVC   STAEM14D,DYNDDNAM       COPY DDNAME
         STC   R2,STAEDWRD             SAVE RETURN CODE
         UNPK  STAEM14R(3),STAEDWRD(2)
         MVZ   STAEM14R,HEX0000        PREPARE FOR TRANSLATE
         TR    STAEM14R,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   STAEM14R+2,C' '         RESTORE THE BLANK
         LA    R15,STAEM14
         BAL   R10,PRT0000             DEBCHK TYPE=DELETE FAILED
STAE0370 DS    0H
         ICM   R8,15,DYNNEXT           NEXT DYNAMIC BLOCK
         BNZ   STAE0320                TRY CLEAN-UP FOR THIS BLOCK
         MODESET MODE=PROB,KEY=NZERO
         CLI   COMMENV,$ENVTSO         TSO ENVIRONMENT?
         BNE   STAE0380                NO
         LA    R1,1                    'TURN OFF' REQUEST
         L     R15,AAUTH               DITTAUTH ENTRY POINT
         BALR  R14,R15                 LINK TO DITTAUTH
STAE0380 DS    0H
*--------------------------------------------------------------------*
*              PROCESS TRACE TABLE                                   *
*--------------------------------------------------------------------*
STAE0400 DS    0H
         L     R8,COMM1ST              FIRST TRACE ENTRY
         USING TRENTRY,R8              DEFINE BASE
         BAL   R10,HEAD0000            FORCE HEADING
STAE0410 DS    0H
         LA    R15,STAEM07             *-------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *             *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM15             * TRACE TABLE *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM08             *             *
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM07             *-------------*
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM20
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM16             TRACE DATA HEADING 1
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM17             TRACE DATA HEADING 2
         BAL   R10,PRT0000             PRINT
         LA    R15,STAEM20
         BAL   R10,PRT0000             PRINT
STAE0420 DS    0H
         MVC   PRDATA1C,COMMBLKS       CLEAR DATA 1 (CHARACTER)
         MVC   PRDATA1H,COMMBLKS       CLEAR DATA 1 (HEX)
         MVC   PRDATA2C,COMMBLKS       CLEAR DATA 2 (CHARACTER)
         MVC   PRDATA2H,COMMBLKS       CLEAR DATA 1 (HEX)
         MVC   PRCURR,COMMBLKS         CLEAR CURRENT ENTRY EYECATCHER
         MVC   PRMODNM,TRMODNM         COPY MODULE NAME
         MVC   PRTRID,TRID             COPY TRACE ID
         MVC   PRDATA1C,TRDATA1        COPY DATA1
         L     R15,ATABLE2             TRANSLATE TABLE ADDRESS
         TR    PRDATA1C,0(R15)         TRANSLATE OUT UNPRINTABLES
         UNPK  PRDATA1H+0(9),TRDATA1+0(5)
         UNPK  PRDATA1H+8(9),TRDATA1+4(5)
         MVZ   PRDATA1H,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDATA1H,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDATA1H+16,C' '        RESTORE THE BLANK
         MVC   PRDATA2C,TRDATA2        COPY DATA1
         TR    PRDATA2C,0(R15)         TRANSLATE OUT UNPRINTABLES
         UNPK  PRDATA2H+0(9),TRDATA2+0(5)
         UNPK  PRDATA2H+8(9),TRDATA2+4(5)
         MVZ   PRDATA2H,HEX0000        PREPARE FOR TRANSLATE
         TR    PRDATA2H,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   PRDATA2H+16,C' '        RESTORE THE BLANK
         C     R8,COMMCURR             IS THIS THE CURRENT ENTRY?
         BNE   STAE0430                NO
         MVC   PRCURR,CURRENT          SET CURRENT
STAE0430 DS    0H
         LA    R15,STAEM18
         BAL   R10,PRT0000             PRINT
         C     R8,COMMLAST             LAST ENTRY JUST PRINTED?
         BE    STAE9900                YES.. EXIT
         LA    R8,TRENTRYL(R8)         NEXT TRACE ENTRY
         CP    LINECT,P0               NEW PAGE JUST STARTED?
         BE    STAE0410                YES.. PRINT SECONDARY HEADINGS
         B     STAE0420                PROCESS THIS ENTRY
*--------------------------------------------------------------------*
*              DUMP REQUESTED STORAGE                                *
*                                                                    *
*              R2 IS STORAGE ADDRESS                                 *
*              R3 IS STORAGE LENGTH                                  *
*              R9 IS RETURN ADDRESS                                  *
*                                                                    *
*              R1, R10, R14, AND R15 WILL BE ALTERED                 *
*--------------------------------------------------------------------*
STAE0900 DS    0H
         LA    R15,STAEM11             CLEAR MESSAGE AREA
         ST    R2,STAEDWRD             SAVE ADDRESS
         UNPK  DUMPADDR(9),STAEDWRD(5)
         MVZ   DUMPADDR,HEX0000        PREPARE FOR TRANSLATE
         TR    DUMPADDR,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   DUMPADDR+8,C' '         RESTORE THE BLANK
         UNPK  WORKHEX1(9),00(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX2(9),04(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX3(9),08(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX4(9),12(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX5(9),16(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX6(9),20(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX7(9),24(5,R2)    UNPACK FOR HEX
         UNPK  WORKHEX8(9),28(5,R2)    UNPACK FOR HEX
         MVZ   WORKHEX1,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX2,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX3,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX4,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX5,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX6,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX7,HEX0000        PREPARE FOR TRANSLATE
         MVZ   WORKHEX8,HEX0000        PREPARE FOR TRANSLATE
         TR    WORKHEX1,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX2,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX3,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX4,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX5,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX6,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX7,HEXCHAR        TRANSLATE TO PRINTABLE
         TR    WORKHEX8,HEXCHAR        TRANSLATE TO PRINTABLE
         MVI   WORKHEX1+8,C' '         RESTORE BLANK
         MVI   WORKHEX2+8,C' '         RESTORE BLANK
         MVI   WORKHEX3+8,C' '         RESTORE BLANK
         MVI   WORKHEX4+8,C' '         RESTORE BLANK
         MVI   WORKHEX5+8,C' '         RESTORE BLANK
         MVI   WORKHEX6+8,C' '         RESTORE BLANK
         MVI   WORKHEX7+8,C' '         RESTORE BLANK
         MVI   WORKHEX8+8,C' '         RESTORE BLANK
         LR    R1,R3                   COPY DATA LENGTH
         CH    R1,H32                  TOO MUCH?
         BNH   STAE0910                NO
         LH    R1,H32                  LIMIT TO 32
STAE0910 DS    0H
         BCTR  R1,0                    RELATIVE TO ZERO
         EX    R1,CHARMVC              MOVE CHARACTER PORTION
         L     R15,ATABLE1             TRANSLATE TABLE ADDRESS
         EX    R1,CHARTR               TRANSLATE OUT UNPRINTABLES
         IC    R1,WORKLEN(R1)          CONVERT LENGTH TO OUTPUT LENGTH
         EX    R1,WORKMVC              MOVE WORK DATA TO OUTPUT
         LA    R15,STAEM11
         BAL   R10,PRT0000             PRINT
         LA    R2,32(R2)               NEXT 32 BYTES
         SH    R3,H32                  MINUS 1 LINE'S WORTH
         BH    STAE0900                KEEP UP THE GOOD WORK
         BR    R9                      WE'VE DONE OUR DAMAGE
STAE9900 DS    0H
         TM    STAEFLAG,$PRTOPEN       IS PRINT OPEN?
         BNO   STAE9910                NO
         CLOSE DITTDUMP                CLOSE PRINT DCB
STAE9910 DS    0H
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         L     R13,4(R13)              RESTORE CALLERS SAVE AREA
         SR    R15,R15                 REQUEST CONTINUATION OF ABEND
         BR    R14                     EXIT TO RTM
PRT0000  DS    0H
         TM    STAEFLAG,$DUMPDD        WAS DUMP DD PRESENT?
         BNOR  R10                     NO.. NO NEED TO PROCEED FURTHER
         MVC   STAECC,4(R15)           SAVE CARRIAGE CONTROL
         PUT   DITTDUMP,0(R15)         PRINT
         AP    LINECT,P1               ADD TO LINE COUNT
         CLI   STAECC,C' '             SINGLE SPACE?
         BE    PRT0010                 YES
         AP    LINECT,P1               ADD TO LINE COUNT
         CLI   STAECC,C'0'             DOUBLE SPACE?
         BE    PRT0010                 YES
         AP    LINECT,P1               ADD TO LINE COUNT
PRT0010  DS    0H
         CP    LINECT,P55              TIME FOR HEADING?
         BLR   R10                     NO
HEAD0000 DS    0H
         TM    STAEFLAG,$DUMPDD        WAS DUMP DD PRESENT?
         BNOR  R10                     NO.. NO NEED TO PROCEED FURTHER
         AP    PAGECT,P1               ADD TO PAGE COUNT
         MVC   HEADPAGE,EDWORD1        INITIALIZE WITH EDIT WORD
         ED    HEADPAGE,PAGECT         EDIT PAGE COUNT
         PUT   DITTDUMP,HEADING        PRINT HEADING
         ZAP   LINECT,P0               RESET LINE COUNT
         BR    R10                     EXIT
*--------------------------------------------------------------------*
*              EXECUTED INSTRUCTIONS                                 *
*--------------------------------------------------------------------*
CHARMVC  MVC   DUMPCHAR(0),0(R2)       MOVE CHARACTER DATA TO DUMP
CHARTR   TR    DUMPCHAR(0),TABLE1
WORKMVC  MVC   DUMPHEX(0),DUMPWORK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
STAESAVE DC    18F'0'              REGISTER SAVE AREA
STAEDWRD DC    D'0'
VCOMM    DC    V(DITTCOMM)         COMM AREA ADDRESS
ATABLE1  DC    A(TABLE1)
ATABLE2  DC    A(TABLE2)
TIOTADDR DC    A(0)                TIOT ADDRESS
H8       DC    H'8'                CONSTANT
H12      DC    H'12'               CONSTANT
H32      DC    H'32'               CONSTANT
STAESCDE DC    XL2'0000'           SYSTEM ABEND CODE
STAEUCDE DC    XL2'0000'           USER ABEND CODE
STAEFLAG DC    X'00'               FLAGS
$DUMPDD  EQU   X'80'               .. DITTDUMP DD IS PRESENT
$SDWA    EQU   X'40'               .. SDWA IS PRESENT
$PRTOPEN EQU   X'20'               .. PRINT DCB IS OPEN
STAECC   DC    C' '                SAVE CARRIAGE CONTROL
EDWORD1  DC    X'402020202120'     CONSTANT
TYPESYS  DC    CL06'SYSTEM'        CONSTANT
TYPEUSER DC    CL06'USER'          CONSTANT
ISP      DC    CL4'ISP '
CURRENT  DC    CL16'<--CURRENT ENTRY'
HEX0000  DC    16X'00'             CONSTANT
HEXCHAR  DC    C'0123456789ABCDEF' CONSTANT
LINECT   DC    PL3'0'              LINE COUNT
PAGECT   DC    PL3'0'              PAGE COUNT
P0       DC    P'0'                CONSTANT
P1       DC    P'1'                CONSTANT
P55      DC    P'55'               CONSTANT
WORKLEN  DS    0C                  DATA LENGTH       PRINT LENGTH
         DC    AL1(01)                 01                 02
         DC    AL1(03)                 02                 04
         DC    AL1(05)                 03                 06
         DC    AL1(07)                 04                 08
*
         DC    AL1(10)                 05                 11
         DC    AL1(12)                 06                 13
         DC    AL1(14)                 07                 15
         DC    AL1(16)                 08                 17
*
         DC    AL1(19)                 09                 20
         DC    AL1(21)                 10                 22
         DC    AL1(23)                 11                 24
         DC    AL1(25)                 12                 26
*
         DC    AL1(28)                 13                 29
         DC    AL1(30)                 14                 31
         DC    AL1(32)                 15                 33
         DC    AL1(34)                 16                 35
*
         DC    AL1(39)                 17                 40
         DC    AL1(41)                 18                 42
         DC    AL1(43)                 19                 44
         DC    AL1(45)                 20                 46
*
         DC    AL1(48)                 21                 49
         DC    AL1(50)                 22                 51
         DC    AL1(52)                 23                 53
         DC    AL1(54)                 24                 55
*
         DC    AL1(57)                 25                 58
         DC    AL1(59)                 26                 60
         DC    AL1(61)                 27                 62
         DC    AL1(63)                 28                 64
*
         DC    AL1(66)                 29                 67
         DC    AL1(68)                 30                 69
         DC    AL1(70)                 31                 71
         DC    AL1(72)                 32                 73
DUMPWORK DS    0C
WORKHEX1 DC    CL08' '
         DC    CL01' '
WORKHEX2 DC    CL08' '
         DC    CL01' '
WORKHEX3 DC    CL08' '
         DC    CL01' '
WORKHEX4 DC    CL08' '
         DC    CL03' '
WORKHEX5 DC    CL08' '
         DC    CL01' '
WORKHEX6 DC    CL08' '
         DC    CL01' '
WORKHEX7 DC    CL08' '
         DC    CL01' '
WORKHEX8 DC    CL08' '
         DC    CL01' '
HEADING  DC    AL2(HEADINGL)
         DC    AL2(0)
         DC    CL111'1            DITTO STAE EXIT'
         DC    C'PAGE'
HEADPAGE DC    CL06' '
HEADINGL EQU   *-HEADING
STAEM01  DC    AL2(STAEM01L)
         DC    AL2(0)
         DC    C'0 DITTO HAS ABENDED WITH '
PRABTYPE DC    CL06' '
         DC    C' CODE '
PRABCODE DC    CL03' '
         DC    CL02' '
STAEM01L EQU   *-STAEM01
STAEM02  DC    AL2(STAEM02L)
         DC    AL2(0)
         DC    C'0 ABENDING CSECT NAME: DITT'
PRCSECT  DC    CL4' '
         DC    C'  ABEND AT DISPLACEMENT +'
PRDISP   DC    CL8' '
         DC    C' '
STAEM02L EQU   *-STAEM02
STAEM03  MSG   C'0 LOGIC ERROR IN CSECT LOCATING CODE, STAE EXIT TERMIN+
               ATING'
STAEM04  MSG   C'0 SDWA NOT PRESENT, UNABLE TO DETERMINE CSECT AND DISP+
               LACEMENT'
STAEM05  MSG   C'0 ABEND DID NOT OCCUR WITHIN DITTO CODE'
STAEM06  MSG   C'0 NO DYNAMIC BLOCKS FOUND'
STAEM07  MSG   C' ',C'*',118C'-',C'*'
STAEM08  MSG   C' ',C'*',118C' ',C'*'
STAEM09  DC    AL2(STAEM09L)
         DC    AL2(0)
         DC    C' ',C'*'
         DC    CL42' '
         DC    CL25'DYNAMIC BLOCK AT ADDRESS '
PRDYNADR DC    CL08' '
         DC    CL43' '
         DC    C'*'
STAEM09L EQU   *-STAEM09
STAEM10  DC    AL2(STAEM10L)
         DC    AL2(0)
         DC    C'NEXT:'
PRDYNNXT DC    CL8' '
         DC    C'   PREVIOUS:'
PRDYNPRV DC    CL8' '
         DC    C'   DEVICE CUU:'
PRDYNCUU DC    CL3' '
         DC    C'   DDNAME:'
PRDYNDDN DC    CL8' '
         DC    C'   VOLSER (IF DASD):'
PRDYNDVL DC    CL06' '
         DC    C'   ECB:'
PRDYNECB DC    CL8' '
         DC    C' '
STAEM10L EQU   *-STAEM10
STAEM11  DC    AL2(STAEM11L)
         DC    AL2(0)
DUMPADDR DC    CL08' '
         DC    CL03' '
DUMPHEX  DC    CL73' '
         DC    CL03' '
DUMPCHAR DC    CL32' '
STAEM11L EQU   *-STAEM11
STAEM12  DC    AL2(STAEM12L)
         DC    AL2(0)
         DC    C'0 CLEAN-UP UNSUCCESSFUL FOR DDNAME:'
STAEM12D DC    CL8' '
         DC    C', COULD NOT LOCATE DEB'
STAEM12L EQU   *-STAEM12
STAEM13  DC    AL2(STAEM13L)
         DC    AL2(0)
         DC    C'0 CLEAN-UP SUCCESSFUL FOR DDNAME:'
STAEM13D DC    CL8' '
STAEM13L EQU   *-STAEM13
STAEM14  DC    AL2(STAEM14L)
         DC    AL2(0)
         DC    C'0 CLEAN-UP UNSUCCESSFUL FOR DDNAME:'
STAEM14D DC    CL8' '
         DC    C', DEBCHK TYPE=DELETE FAILED, RC='
STAEM14R DC    CL02' '
         DC    C' '
STAEM14L EQU   *-STAEM14
STAEM15  DC    AL2(STAEM15L)
         DC    AL2(0)
         DC    C' '
         DC    CL01'*'
         DC    CL54' '
         DC    CL11'TRACE TABLE'
         DC    CL53' '
         DC    CL01'*'
STAEM15L EQU   *-STAEM15
STAEM16  DC    AL2(STAEM16L)
         DC    AL2(0)
         DC    C' '
         DC    CL08' MODULE '
         DC    CL04' '
         DC    CL08'TRACE ID'
         DC    CL04' '
         DC    CL25'*---- TRACE DATA 1 -----*'
         DC    CL04' '
         DC    CL25'*---- TRACE DATA 2 -----*'
STAEM16L EQU   *-STAEM16
STAEM17  DC    AL2(STAEM17L)
         DC    AL2(0)
         DC    C' '
         DC    CL24' '
         DC    CL25'  CHAR         HEX       '
         DC    CL04' '
         DC    CL25'  CHAR         HEX       '
STAEM17L EQU   *-STAEM17
STAEM18  DC    AL2(STAEM18L)
         DC    AL2(0)
         DC    C' '
PRMODNM  DC    CL08' '            MODULE NAME
         DC    CL04' '
PRTRID   DC    CL08' '            TRACE ID
         DC    CL04' '
PRDATA1C DC    CL08' '            TRACE DATA1 IN CHARACTER
         DC    CL01' '
PRDATA1H DC    CL16' '            TRACE DATA1 IN HEX
         DC    CL04' '
PRDATA2C DC    CL08' '            TRACE DATA2 IN CHARACTER
         DC    CL01' '
PRDATA2H DC    CL16' '            TRACE DATA2 IN HEX
         DC    CL01' '
PRCURR   DC    CL16' '            CURRENT ENTRY EYECATCHER
STAEM18L EQU   *-STAEM18
STAEM19  DC    AL2(STAEM19L)
         DC    AL2(0)
         DC    C' '
         DC    CL01'*'
         DC    CL54' '
         DC    CL11'COMMON AREA'
         DC    CL53' '
         DC    CL01'*'
STAEM19L EQU   *-STAEM19
STAEM20  MSG   C' '
DITTDUMP DCB   DSORG=PS,          STAE EXIT PRINT DCB                  +
               DDNAME=DITTDUMP,   .. DDNAME                            +
               LRECL=125,         .. RECORD SIZE                       +
               BLKSIZE=1632,      .. BLOCK SIZE                        +
               RECFM=VBA,         .. RECORD FORMAT                     +
               MACRF=PM           .. MACRO FORMAT
         LTORG
TABLE1   DS    0C   0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'00'-X'0F'
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'10'-X'1F'
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'20'-X'2F'
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'30'-X'3F'
         DC    XL16'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F'  X'40'-X'4F'
         DC    XL16'504B4B4B4B4B4B4B4B4B5A5B5C5D5E5F'  X'50'-X'5F'
         DC    XL16'60614B4B4B4B4B4B4B4B4B6B6C6D6E6F'  X'60'-X'6F'
         DC    XL16'4B4B4B4B4B4B4B4B4B797A7B7C7D7E7F'  X'70'-X'7F'
         DC    XL16'4B8182838485868788894B4B4B4B4B4B'  X'80'-X'8F'
         DC    XL16'4B9192939495969798994B4B4B4B4B4B'  X'90'-X'9F'
         DC    XL16'4B4BA2A3A4A5A6A7A8A94B4B4B4B4B4B'  X'A0'-X'AF'
         DC    XL16'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  X'B0'-X'BF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C94B4B4B4B4B4B'  X'C0'-X'CF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D94B4B4B4B4B4B'  X'D0'-X'DF'
         DC    XL16'E04BE2E3E4E5E6E7E8E94B4B4B4B4B4B'  X'E0'-X'EF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'  X'F0'-X'FF'
TABLE2   DS    0C   0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    XL16'40404040404040404040404040404040'  X'00'-X'0F'
         DC    XL16'40404040404040404040404040404040'  X'10'-X'1F'
         DC    XL16'40404040404040404040404040404040'  X'20'-X'2F'
         DC    XL16'40404040404040404040404040404040'  X'30'-X'3F'
         DC    XL16'404040404040404040404A4B4C4D4E4F'  X'40'-X'4F'
         DC    XL16'504040404040404040405A5B5C5D5E5F'  X'50'-X'5F'
         DC    XL16'60614040404040404040406B6C6D6E6F'  X'60'-X'6F'
         DC    XL16'404040404040404040797A7B7C7D7E7F'  X'70'-X'7F'
         DC    XL16'40818283848586878889404040404040'  X'80'-X'8F'
         DC    XL16'40919293949596979899404040404040'  X'90'-X'9F'
         DC    XL16'4040A2A3A4A5A6A7A8A9404040404040'  X'A0'-X'AF'
         DC    XL16'40404040404040404040404040404040'  X'B0'-X'BF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C9404040404040'  X'C0'-X'CF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9404040404040'  X'D0'-X'DF'
         DC    XL16'E040E2E3E4E5E6E7E8E9404040404040'  X'E0'-X'EF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'  X'F0'-X'FF'
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMICALLY ACQUIRED RESOURCE BLOCK                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK  TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              DCB DSECT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD     DSORG=PS
DYNDCBL  EQU      *-IHADCB
*--------------------------------------------------------------------*
*                                                                    *
*              PSA DSECT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         IHAPSA   DSECT=YES,LIST=NO
*--------------------------------------------------------------------*
*                                                                    *
*              TCB DSECT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         IKJTCB   LIST=NO
*--------------------------------------------------------------------*
*                                                                    *
*              DEB DSECT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         IEZDEB
*--------------------------------------------------------------------*
*                                                                    *
*              IOB DSECT                                             *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU   *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              DIAGNOSTIC WORK AREA DSECT                            *
*                                                                    *
*--------------------------------------------------------------------*
         IHASDWA
*--------------------------------------------------------------------*
*                                                                    *
*              TASK INPUT/OUTPUT TABLE (TIOT)                        *
*                                                                    *
*--------------------------------------------------------------------*
         DSECT
         IEFTIOT1
         END  DITTSTAE
./ ADD NAME=DITTTINT
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE INITIALIZATION                                   *
*                                                                    *
*       THIS MODULE PERFORMS THE 'INT' FUNCTION                      *
*                                                                    *
*--------------------------------------------------------------------*
DITTTINT DITTPRFX TINTSAVE,'TAPE INITIALIZE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TINT9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TINT0010            NO
         MVC   VOLSER,COMMLOV      INITIALIZE VOLSER
TINT0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
TINT0020 DS    0H
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,MOUNTWTO       LENGTH IN WTO
         SH    R1,VOLSERL          MINUS LENGTH OF VOLSER
         LA    R1,MOUNTWTO(R1)     FIRST CHARACTER OF VOLSER
         MVC   0(L'VOLSER,R1),VOLSER   INSERT VOLSER INTO WTO
         WTO   MF=(E,MOUNTWTO),    ISSUE MOUNT WTO                     +
               CONSID=COMMCID      .. CONSOLE ID
         DITTRACE ID=IO            TRACE I/O INITIATION
         MVC   DYNCCW(INITCCWL),REWCCW  INITIALIZE CCW'S
         LA    R1,EXCPBLOK         EXCP PARAMETER BLOCK
         L     R15,AEXCP           EXCP INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         TM    DYNSTAT,$DYNPROT    FILE PROTECTED?
         BO    TINT0080            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCESSFUL?
         BNE   TINT0400            NO
         CLC   COMMHIV,COMMBLKS    HIGH VOLSER ALL BLANK?
         BE    TINT0070            YES.. EXIT
         CLC   VOLSER,COMMHIV      REACHED UPPER LIMIT?
         BNL   TINT0070            YES.. EXIT
         MVC   PACK1,PACK1-1       RESET TO ALL ZEROS
         LA    R1,PACK1+6          LAST DIGIT IN WORK AREA
         LA    R2,VOLSER+5         LAST DIGIT IN VOLSER
         LA    R3,6                MAX DIGITS
TINT0030 DS    0H
         CLI   0(R2),C'0'          VALID DIGIT?
         BL    TINT0040            NO
         MVC   0(1,R1),0(R2)       COPY THE DIGIT
         BCTR  R1,0                PREVIOUS DIGIT
         BCTR  R2,0                PREVIOUS DIGIT
         BCT   R3,TINT0030         LOOP
TINT0040 DS    0H
         PACK  PACK2,PACK1         PACK NUMERIC PORTION
         AP    PACK2,COMMP1        ADD 1 TO VOLSER
         UNPK  PACK1,PACK2         UNPACK NEXT VOLSER OR VOLSER SUFFIX
         OI    PACK1+6,X'F0'       CHANGE SIGN
         LA    R1,PACK1+6          LAST DIGIT IN WORK AREA
         LA    R2,VOLSER+5         LAST DIGIT IN VOLSER
         LA    R3,6                MAX DIGITS
TINT0050 DS    0H
         CLI   0(R2),C'0'          VALID DIGIT?
         BL    TINT0060            NO.. INITIALIZE THE TAPE
         MVC   0(1,R2),0(R1)       COPY THE DIGIT
         BCTR  R1,0                PREVIOUS DIGIT
         BCTR  R2,0                PREVIOUS DIGIT
         BCT   R3,TINT0050         LOOP
*---------------------------------------------------------------------*
*                                                                     *
*                    UNLOAD CURRENT TAPE                              *
*                                                                     *
*---------------------------------------------------------------------*
TINT0060 DS    0H
         DITTRACE ID=UNLOAD
         MVC   DYNCCW(RUNCCWL),RUNCCW   INITIALIZE CCW
         LA    R1,EXCPBLOK         EXCP PARAMETER BLOCK
         L     R15,AEXCP           EXCP INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    I/O SUCESSFUL?
         BNE   TINT0400            NO
         B     TINT0020            INITIALIZE THE NEXT TAPE
TINT0070 DS    0H
         DITTRACE ID=INITDONE      TAPE OR RANGE INITIALIZED
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TINT9900            EXIT
TINT0080 DS    0H
         DITTRACE ID=FILEPROT      TAPE IS FILE PROTECTED
         WTO   'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING)',        +
               CONSID=COMMCID
         B     TINT0060            UNLOAD THE TAPE
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TINT0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TINT9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
REWCCW   CCW   X'07',*,CHAINCMD+SILI,1      REWIND CCW
WRITECCW CCW   X'01',VOL1,CHAINCMD+SILI,80  WRITE CCW
WTMCCW1  CCW   X'1F',*,CHAINCMD+SILI,1      WRITE TAPE MARK CCW
WTMCCW2  CCW   X'1F',*,SILI,1               WRITE TAPE MARK CCW
INITCCWL EQU   *-REWCCW
RUNCCW   CCW   X'0F',*,SILI,1               REWIND/UNLOAD CCW
RUNCCWL  EQU   *-RUNCCW
TINTSAVE DC    18F'0'              REGISTER SAVE AREA
CHAINCMD EQU   X'40'              CHAIN CCW COMMANDS
SILI     EQU   X'20'              SUPPRESS INCORRECT LENGTH INDICATION
VOLSERL  DC    AL2(L'VOLSER)      LENGTH OF VOLSER
         DC    C'0'
PACK1    DC    CL7'0'             VOLSER WORK AREA
PACK2    DC    PL4'0'             VOLSER WORK AREA
VOL1     DS    0C                 <--+
         DC    CL04'VOL1'            VOLUME SERIAL RECORD
VOLSER   DC    CL06' '               
         DC    CL70' '            <--+
MOUNTWTO WTO   'MOUNT TAPE TO BE INITIALIZED WITH VOLSER XXXXXX',      +
               CONSID=,                                                +
               MF=L
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE PARAMETERS                             *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTINT
./ ADD NAME=DITTTMAP
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE MAP 'TMAP' MODULE                                *
*                                                                    *
*       THIS MODULE PERFORMS THE 'TMAP' FUNCTION.  'HDR1', 'HDR2',   *
*       'EOF1', AND 'EOF2' RECORDS ARE "WATCHED" FOR, AND WHEN       *
*       ENCOUNTERED, THE RELATIVE FILE NUMBER AND FILE NAME ARE      *
*       PRINTED.  BY DEFAULT TMAP WILL STOP WHEN 2 TAPE MARKS IN     *
*       A ROW WITH NO INTERVIENING DATA ARE FOUND (LOGICAL-END-OF-   *
*       TAPE).  THE OPTIONS 'FULL', 'FULLTAPE', OR 'ALL' WILL CAUSE  *
*       THE TAPE TO BE READ TO THE REFLECTIVE MARKER OR OFF THE END  *
*       OF THE REEL.                                                 *
*                                                                    *
*--------------------------------------------------------------------*
DITTTMAP DITTPRFX TAPESAVE,'TAPE ''VTOC'' MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILES?
         BO    EXIT0000            YES.. EXIT
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         MVI   TAPEFLAG,00         INITIALIZE FLAGS
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           DCB ADDRESS
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,REWCCW+1       RELOCATE I/O ADDRESS
         STCM  R1,7,READCCW+1      RELOCATE I/O ADDRESS
         DITTRACE ID=REWIND        TRACE INITIAL REWIND
         MVC   DYNCCW,REWCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         DITTRACE ID=GOODREW       REWIND SUCCESSFUL
TAPE0020 DS    0H
         DITTRACE ID=READFWD       TRACE READ FORWARD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O NOT ALLOWED?
         BE    TAPE0050            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT REACHED?
         BO    TAPE0220            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE0200            YES
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         TM    TAPEFLAG,$TAPEFHD   FILE HEADING ALREADY PRINTED??
         BO    TAPE0030            YES
         BAL   R9,HEAD0000         PRINT FILE HEADING
         OI    TAPEFLAG,$TAPEFHD   SET HEADING FLAG
TAPE0030 DS    0H
         NI    TAPEFLAG,255-$TAPETM    TURN OFF TAPE MARK FLAG
         CLC   EXCPLEN,H80         WAS LENGTH 80?
         BNE   TAPE0040            NO
         TM    TAPEFLAG,$TAPEDTA   DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         ICM   R1,7,READCCW+1      I/O AREA USED
         CLC   TAPEVOL1,0(R1)      'VOL1' RECORD??
         BE    VOL10000            YES
         CLC   TAPEHDR1,0(R1)      'HDR1' RECORD??
         BE    HDR10000            YES
         CLC   TAPEEOV1,0(R1)      'EOV1' RECORD??
         BE    EOV10000            YES
         CLC   TAPEEOF1,0(R1)      'EOF1' RECORD??
         BE    EOF10000            YES
         CLC   TAPEHDR2,0(R1)      'HDR2' RECORD??
         BE    HDR20000            YES
         CLC   TAPEEOF2,0(R1)      'EOF2' RECORD??
         BE    EOF20000            YES
         CLC   TAPEEOV2,0(R1)      'EOV2' RECORD??
         BE    EOV20000            YES
TAPE0040 DS    0H
         DITTRACE ID=DATA          TRACE DATA ENCOUNTERED
         TM    TAPEFLAG,$TAPEDTA   IS TAPE DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         OI    TAPEFLAG,$TAPEDTA   SET DATA FLAG
         MVI   PRTCMMD,$PRTDATA    PRINT COMMAND
         MVI   PRTCC,C' '          SINGLE SPACE
         MVC   PRTDATA(L'DATAMSG),DATAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0020            READ ANOTHER BLOCK FROM THE TAPE
TAPE0050 DS    0H
         DITTRACE ID=NOTALLOW      I/O NOT ALLOWED BY EXCP
         ABEND ABEND034,DUMP,,USER ABEND
*---------------------------------------------------------------------*
*                                                                     *
*                    TAPE MARK HAS JUST BEEN READ                     *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0200 DS    0H
         DITTRACE ID=TAPEMARK      TRACE TAPE MARK ENCOUNTERED         +
               DATA1=TAPEFLAG      .. CAPTURE FLAG SETTING
         NI    TAPEFLAG,255-$TAPEDTA   RESET DATA FLAG
         NI    TAPEFLAG,255-$TAPEFHD   RESET FILE HEADING FLAG
         TM    TAPEFLAG,$TAPETM    TAPE MARK FLAG ALREADY ON??
         BO    TAPE0210            YES ... TWO IN A ROW IS LOGICAL EOT
         OI    TAPEFLAG,$TAPETM    SET TAPE MARK FLAG
         B     TAPE0020            READ ANOTHER BLOCK
TAPE0210 DS    0H
         TM    TAPEFLAG,$TAPEEOT   ALREADY REACHED LEOT?
         BO    TAPE0020            DON'T SEND LEOT MESSAGE AGAIN
         DITTRACE ID=LEOT          LOGICAL END OF TAPE
         OI    TAPEFLAG,$TAPEEOT   INDICATE LOGICAL EOT REACHED
         MVI   PRTCMMD,$PRTDATA
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTDATA(L'LEOTMSG),LEOTMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         CLC   FULLTAPE,COMMOPT    FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         CLC   FULL,COMMOPT        FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         CLC   ALL,COMMOPT         FULL TAPE?
         BE    TAPE0020            YES, KEEP ON TRUCKIN'
         OI    COMMFLAG,$COMMEOF   OTHERWISE EOF ON 2 TAPE MARKS
         B     EXIT0000            AND EXIT
TAPE0220 DS    0H
         DITTRACE ID=PEOT          LOGICAL END OF TAPE
         MVI   PRTCMMD,$PRTDATA
         MVI   PRTCC,C'-'          TRIPLE SPACE
         MVC   PRTDATA(L'PEOTMSG),PEOTMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         OI    COMMFLAG,$COMMEOF   SIGNAL 'END OF FILE'
         B     EXIT0000            EXIT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=IOERR         TRACE I/O ERRORS
         ABEND ABEND017,DUMP,,USER
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND018,DUMP,,USER
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT HEADING FOR NEW PHYSICAL FILE                   *
*                                                                    *
*--------------------------------------------------------------------*
HEAD0000 DS    0H
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         MVC   PRFILE,COMMED2      INITIALIZE WITH EDIT WORD
         ED    PRFILE,FILECT       EDIT FILE NUMBER
         MVC   PRTDATA(HEADMSGL),HEADMSG
         MVI   PRTCMMD,$PRTDATA    REQUEST PRINT
         MVI   PRTCC,C'0'          DOUBLE SPACE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         BR    R9                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*              VOL1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
VOL10000 DS    0H
         USING VOL1,R1             DEFINE DSECT BASE
         DITTRACE ID=VOL1          TRACE VOL1
         MVC   PRVOL,VOL1VOL       MOVE VOLUME SERIAL NUMBER
         MVC   PRTDATA(VOL1MSGL),VOL1MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              HDR1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
HDR10000 DS    0H
         USING HDR1,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR1          TRACE HDR1
         MVC   PRDSN,HDR1DSN       DATASET NAME
         MVC   PRVSEQ,HDR1VSEQ     VOLUME SEQUENCE
         MVC   PRDSEQ,HDR1DSEQ     DATASET SEQUENCE
         MVC   PRCDTE,HDR1CDTE     CREATE DATE
         MVC   PREXPD,HDR1EXPD     EXPIRATION DATE
         MVC   PRTDATA(HDR1MSGL),HDR1MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              EOF1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOF10000 DS    0H
         DITTRACE ID=EOF1          TRACE EOF1
         MVC   PRBLKC,HDR1BLKC     BLOCK COUNT
         MVC   PRTDATA(EOF1MSGL),EOF1MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              EOV1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOV10000 DS    0H
         DITTRACE ID=EOV1          TRACE EOV1
         MVC   PRTDATA(L'EOV1MSG),EOV1MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              HDR2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
HDR20000 DS    0H
         USING HDR2,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR2          TRACE HDR2
         MVC   PRRF,HDR2RF         RECORD FORMAT
         MVC   PRBLSZ,HDR2BLSZ     BLOCK SIZE
         MVC   PRRSZ,HDR2RSZ       RECORD SIZE
         MVC   PRDEN,HDR2DEN       TAPE DENSITY
         MVC   PRJOB,HDR2JOB       JOB/JOB STEP
         MVC   PRTDATA(HDR2MSGL),HDR2MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              EOF2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOF20000 DS    0H
         DITTRACE ID=EOF2          TRACE EOF2
         MVC   PRTDATA(L'EOF2MSG),EOF2MSG
         B     LBL0000             PRINT LABEL DATA
*--------------------------------------------------------------------*
*                                                                    *
*              EOV2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOV20000 DS    0H
         DITTRACE ID=EOV2          TRACE EOV2
         MVC   PRTDATA(L'EOV2MSG),EOV2MSG
         B     LBL0000             PRINT LABEL DATA
LBL0000  DS    0H
         MVI   PRTCMMD,$PRTDATA    REQUEST PRINT
         MVI   PRTCC,C' '          SINGLE SPACE
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
EXIT0000 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
REWCCW   CCW   X'07',*,SILI,1      REWIND CCW
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
H80      DC    H'80'               CONSTANT
TAPEFLAG DC    X'00'               SWITCHES/FLAGS
$TAPETM  EQU   X'80'               .. TAPE MARK JUST READ
$TAPEEOT EQU   X'40'               .. LOGICAL EOT REACHED
$TAPEDTA EQU   X'20'               .. DATA FILE FOUND
$TAPEFHD EQU   X'10'               .. FILE HEADING PRINTED
FILECT   DC    PL3'0'              FILES COUNTER
FULLTAPE DC    CL8'FULLTAPE'
FULL     DC    CL8'FULL'
ALL      DC    CL8'ALL'
TAPEVOL1 DC    C'VOL1'             CONSTANT
TAPEHDR1 DC    C'HDR1'             CONSTANT
TAPEHDR2 DC    C'HDR2'             CONSTANT
TAPEEOF1 DC    C'EOF1'             CONSTANT
TAPEEOF2 DC    C'EOF2'             CONSTANT
TAPEEOV1 DC    C'EOV1'             CONSTANT
TAPEEOV2 DC    C'EOV2'             CONSTANT
HEADMSG  DS    0C
         DC    C'* * * * * * * * * * * * * * * * * PHYSICAL FILE #'
PRFILE   DC    CL6' '
         DC    C' * * * * * * * * * * * * * * * * *'
HEADMSGL EQU   *-HEADMSG
LEOTMSG  DC    C'=============================== LOGICAL END OF TAPE ==+
               ====================='
PEOTMSG  DC    C'=============================== PHYSICAL END OF TAPE =+
               ====================='
DATAMSG  DC    C'DATA'
VOL1MSG  DS    0C
         DC    C'VOL1 VOLSER '
PRVOL    DC    CL6' '
VOL1MSGL EQU   *-VOL1MSG
HDR1MSG  DS    0C
         DC    C'HDR1 DSN='
PRDSN    DC    CL17' '
         DC    C' VOLSEQ='
PRVSEQ   DC    CL4' '
         DC    C' DATASET SEQ='
PRDSEQ   DC    CL4' '
         DC    C' CREATE DATE='
PRCDTE   DC    CL6' '
         DC    C' EXPIRE DATE='
PREXPD   DC    CL6' '
HDR1MSGL EQU   *-HDR1MSG
HDR2MSG  DS    0C
         DC    C'HDR2 RECORD FORMAT='
PRRF     DC    C' '
         DC    C' BLOCK SIZE='
PRBLSZ   DC    CL5' '
         DC    C' RECORD SIZE='
PRRSZ    DC    CL5' '
         DC    C' DENSITY='
PRDEN    DC    C' '
         DC    C' JOB/JOBSTEP='
PRJOB    DC    CL17' '
HDR2MSGL EQU   *-HDR2MSG
EOF1MSG  DS    0C
         DC    C'EOF1 BLOCK COUNT='
PRBLKC   DC    CL6' '
EOF1MSGL EQU   *-EOF1MSG
EOF2MSG  DC    C'EOF2'
EOV1MSG  DC    C'EOV1'
EOV2MSG  DC    C'EOV2'
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT REQUEST BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTEFACE BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE LABEL DSECTS                                     *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DITTTLBL
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU  *-IHADCB
         END  DITTTMAP
./ ADD NAME=DITTTPI1
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE INPUT MODULE                                     *
*                                                                    *
*       THIS IS THE TAPE INPUT MODULE.  IT IS INVOKED BY THE         *
*       MAINLINE MODULE 'DITTMAIN'.  FOR BATCH AND STC MODES         *
*       RECORD DEBLOCKING WILL BE PERFORMED BY THIS MODULE IF        *
*       REQUESTED BY THE COMMAND (TPD, TDD,..).                      *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPI1 DITTPRFX TAPESAVE,'TAPE READER MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         ZAP   TAPERCDS,COMMP0     ZERO RECORD COUNTER
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         XC    TAPECRCD,TAPECRCD   CLEAR CURRENT RECORD ADDRESS
         XC    TAPEREM,TAPEREM     CLEAR REMAINING BLOCK AMOUNT
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADRESS TO EXCP
         USING DYNBLOK,R10         SPECIFY BASE
         LA    R2,DYNDCB           DCB WITHIN DYNBLOK
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,READCCW+1      RELOCATE BUFFER ADDRESS
         NI    COMMIFLG,255-$NEWBLOK TURN OFF NEW BLOCK INDICATOR
TAPE0020 DS    0H
         TM    COMMPFLG,$COMMRCS   RECORD COUNT GIVEN??
         BNO   TAPE0030            NO, BYPASS RECORD COUNT CHECK
         CP    TAPERCDS,COMMRCDS   READ ENOUGH RECORDS??
         BE    TAPE7030            YES, SIGNAL END OF FILE
TAPE0030 DS    0H
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   TAPE0100            NO
         DITTRACE ID=NEXTRECD,     CALLED FOR NEXT RECORD              +
               DATA1=TAPECRCD,     .. CURRENT RECORD ADDRESS           +
               DATA2=TAPEREM       .. LENGTH REMAINING
         OC    TAPEREM,TAPEREM     ANY LENGTH REMAINING??
         BZ    TAPE0100            NO.. NEED ANOTHER RECORD
         DITTRACE ID=DEBLOCK       DEBLOCK NEXT RECORD
TAPE0040 DS    0H
         ICM   R3,15,TAPECRCD      CURRENT RECORD ADDRESS
         BZ    TAPE0410            ERROR
         SR    R2,R2               CLEAR REGISTER
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BO    TAPE0050            YES
         ICM   R2,3,COMMLRCL       LOGICAL RECORD SIZE
         B     TAPE0060            CONTINUE
TAPE0050 DS    0H
         ICM   R2,3,0(R3)          RECORD SIZE
TAPE0060 DS    0H
         TM    COMMIFLG,$NEWBLOK   NEW BLOCK JUST READ??
         BO    TAPE0070            YES, LEAVE CURRENT ADDRESS
         AR    R3,R2               BRING INPUT ADDRESS FORWARD
TAPE0070 DS    0H
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS??
         BNO   TAPE0080            NO, RECORD LENGTH WILL BE THE SAME
         ICM   R2,3,0(R3)          LENGTH OF NEW RECORD
         BZ    TAPE0410         ...RECORD LENGTH ZERO.. INVALID
TAPE0080 DS    0H
         CLM   R2,3,TAPEREM        LONGER THAN REMAINING LENGTH??
         BNH   TAPE0090            NO
         ICM   R2,3,TAPEREM        OTHERWISE LIMIT LENGTH
TAPE0090 DS    0H
         ST    R3,TAPECRCD         SAVE CURRENT RECORD ADDRESS
         ST    R3,COMMCRCD         PASS CURRENT RECORD ADDRESS
         STH   R2,COMMCRCL         PASS CURRENT RECORD LENGTH
         DITTRACE ID=LREC,         RECORD ADDRESS AND LENGTH           +
               RDATA1=R3,          .. RECORD'S ADDRESS                 +
               RDATA2=R2           .. RECORD'S LENGTH
         LH    R3,TAPEREM          REMAINING SIZE
         SR    R3,R2               COMPUTE REMAINING SIZE
         STH   R3,TAPEREM          SAVE REMAINING SIZE
         AP    TAPERCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     TAPE9900            EXIT
TAPE0100 DS    0H
         DITTRACE ID=NEXTBLOK      READ NEXT PHYSICAL RECORD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETER LIST
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         CLI   EXCPSTAT,$EXCPNA    I/O NOT ALLOWED?
         BE    TAPE0140            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE?
         BO    TAPE7020            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE7000            YES
         OI    COMMIFLG,$NEWBLOK   TURN ON NEW BLOCK INDICATOR
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,7,READCCW+1      I/O AREA ADDRESS
         ST    R3,TAPECRCD         SAVE CURRENT RECORD ADDRESS
         MVC   TAPEREM,EXCPLEN     SAVE DATA LENGTH
         TM    COMMIFLG,$DEBLOCK   DEBLOCKING RECORDS??
         BNO   TAPE0130            NO
         TM    COMMIFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TAPE0040            NO
         SR    R0,R0               CLEAR REGISTER
         ICM   R0,3,0(R3)          LENGTH FROM BDW (MAYBE)
         SH    R0,H4               MINUS BDW LENGTH
         BNH   TAPE0040            INSUFFICIENT LENGTH
         LA    R3,4(R3)            FIRST RDW IN THE BLOCK
         SR    R2,R2               CLEAR REGISTER
TAPE0110 DS    0H
         ICM   R2,3,0(R3)          RECORD LENGTH FROM RDW
         BZ    TAPE0410            RECORD WITH LENGTH = ZERO!
         SR    R0,R2               MINUS LENGTH OF THIS RECORD
         BM    TAPE0040            NEGATIVE... LENGTHS DON'T ADD UP
         BZ    TAPE0120            ZERO.. LENGTHS MATCH
         AR    R3,R2               NEXT RDW ADDRESS
         B     TAPE0110            LOOP
TAPE0120 DS    0H
         DITTRACE ID=ITADDSUP      COULDN'T THINK OF A BETTER COMMENT
         L     R3,TAPECRCD         BLOCK ADDRESS
         ICM   R2,3,0(R3)          BLOCK LENGTH
         SH    R2,H4               MINUS BDW LENGTH
         STCM  R2,3,TAPEREM        SET LENGTH REMAINING
         LA    R3,4(R3)            FIRST LOGICAL RECORD'S ADDRESS
         ST    R3,TAPECRCD         SET RECORD ADDRESS
         B     TAPE0040            DEBLOCK FIRST RECORD
TAPE0130 DS    0H
         DITTRACE ID=PHYRECD,      PHYSICAL RECORD                     +
               DATA1=TAPECRCD,     .. PHYSICAL BLOCK'S ADDRESS         +
               DATA2=TAPEREM       .. PHYSICAL BLOCK'S LENGTH
         MVC   COMMCRCD,TAPECRCD   PASS RECORD ADDRESS
         MVC   COMMCRCL,TAPEREM    PASS RECORD LENGTH
         AP    TAPERCDS,COMMP1     ADD 1 TO RECORD COUNT
         B     TAPE9900            EXIT
TAPE0140 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     RUNNING AS A TSO USER?
         BE    TAPE0160            YES
         CLI   COMMENV,$ENVJOB     RUNNING AS A BATCH JOB?
         BE    TAPE0150            YES
         WTO   'READ NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',       +
               CONSID=COMMCID
TAPE0150 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINT COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0160 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=READERR       TRACE READ ERRORS
         ABEND ABEND017,DUMP,,USER
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         ABEND ABEND018,DUMP,,USER
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    TAPE MARK PROCESSING                             *
*                                                                     *
* NOTE: THE TAPE MARK FLAG IN 'DITTCOMM' WILL BE SET BY 'DITTEXCP'.   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE7000 DS    0H
         DITTRACE ID=TAPEMARK      TRACE TAPE MARKS
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         TM    COMMPFLG,$COMMFLS   MULTIPLE FILES??
         BNO   TAPE7010            NO
         CP    FILECT,COMMFILS     ENOUGH FILES PROCESSED??
         BL    TAPE9900            NO
TAPE7010 DS    0H
         TM    COMMPFLG,$COMMRCS   'RECORDS' IN EFFECT?
         BNO   TAPE7030            NO, NORMAL EOF
         DITTRACE ID=FORCEOF       EOF FORCED BY TAPE MARK
         OI    COMMFLAG,$COMMFE    EOF FORCED BY TAPE MARK
         B     TAPE7030            OTHERWISE SIGNAL 'EOF'
TAPE7020 DS    0H
         DITTRACE ID=PEOT
TAPE7030 DS    0H
         DITTRACE ID=EOF           TRACE SYSIN END OF FILE
         OI    COMMFLAG,$COMMEOF   SIGNAL END OF FILE
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPECRCD DC    A(0)                CURRENT RECORD ADDRESS
H4       DC    H'4'                CONSTANT
TAPEREM  DC    H'0'                LENGTH REMAINING IN RECORD
TAPERCDS DC    PL5'0'              RECORD COUNTER
FILECT   DC    PL5'0'              FILES COUNTER
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
NAMSG    DC    C'READ NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT REQUEST BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPI1
./ ADD NAME=DITTTPI2
*--------------------------------------------------------------------*
*                                                                    *
*              FORWARD SPACE FILE                                    *
*                                                                    *
*       THIS ROUTINE PERFORMS THE FSF FUNCTION.                      *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPI2 DITTPRFX TAPESAVE,'TAPE FORWARD SPACE FILE MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         ZAP   TAPEFILS,COMMP0     ZERO FILES COUNTER
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           DCB WITHIN DYNBLOK
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,FSFCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         MVC   DYNCCW,FSFCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         CLI   EXCPSTAT,$EXCPNA    ATTEMPT WHILE AT PEOT?
         BE    TAPE0030            YES
         TM    DYNSTAT,$DYNPEOT    EOT REACHED?
         BO    TAPE0060            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL??
         BNE   TAPE0400            NO
         AP    TAPEFILS,COMMP1     ADD 1 TO FILES FORWARDED
         CP    TAPEFILS,COMMFILS   PROCESSED ENOUGH FILES??
         BL    TAPE0020            NO
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TAPE9900            EXIT
TAPE0030 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0050            YES
         CLI   COMMENV,$ENVJOB     RUNNING AS A BATCH JOB?
         BE    TAPE0040            YES
         WTO   'FSF NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',        +
               CONSID=COMMCID
TAPE0040 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0050 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0060 DS    0H
         DITTRACE ID=PEOT
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R2,DYNBLOK          DYNAMIC BLOCK
         LA    R3,DYNDCB           DCB
         LA    R4,DYNIOB           IOB
         LA    R5,DYNECB           ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPEFILS DC    PL5'0'              FILES COUNTER
FSFCCW   CCW   X'3F',*,SILI,1      FORWARD SPACE FILE CCW
NAMSG    DC    C'FSF NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
EOTMSG   DC    C'FSF STOPPED DUE TO REACHING PHYSICAL EOT'
EOTMSGL  EQU   *-EOTMSG
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE REQUEST BLOCK                            *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPI2
./ ADD NAME=DITTTPI3
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE BACK-SPACE RECORDS                               *
*                                                                    *
*       THIS ROUTINE PERFORMS THE BSR FUNCTION.                      *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPI3 DITTPRFX TAPESAVE,'TAPE BACK SPACE RECORD MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         POINT TO INPUT BUFFER
         STCM  R1,7,BSRCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         LA    R4,1                ASSUME 1 BLOCK
         TM    COMMPFLG,$COMMRCS   NUMBER TO READ GIVEN??
         BNO   TAPE0030            NO
         ZAP   COMMDWRD,COMMRCDS   MOVE TO DOUBLE WORD
         CVB   R4,COMMDWRD         CONVERT TO BINARY
         LTR   R4,R4               WAS IT > ZERO??
         BZ    TAPE0040            NO
         BM    TAPE0040            NO
TAPE0030 DS    0H
         MVC   DYNCCW,BSRCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP INTERFACE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0050            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT REACHED?
         BO    TAPE0080            YES..
         TM    DYNSTAT,$DYNTM      TAPE MARK READ?
         BO    TAPE0090            YES..
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL?
         BNE   TAPE0400            NO
         BCT   R4,TAPE0030         READ ALL REQUESTED
TAPE0040 DS    0H
         DITTRACE ID=BSRDONE       TRACE ALL DONE
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TAPE9900            EXIT
TAPE0050 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0070            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0060            YES
         WTO   'BSR NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',        +
               CONSID=COMMCID
TAPE0060 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINT INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            EXIT
TAPE0070 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            EXIT
TAPE0080 DS    0H
         DITTRACE ID=PEOT
         ABEND ABEND025,DUMP,,USER ABEND
TAPE0090 DS    0H
         DITTRACE ID=TAPEMARK
         OI    COMMFLAG,$COMMFE    SET EOF FLAG
         TM    COMMPFLG,$COMMRCS   'RECORDS' IN EFFECT?
         BNO   TAPE9900            NO
         OI    COMMFLAG,$COMMFE    EOF FORCED BY TAPE MARK
         B     TAPE9900            EXIT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R3,DYNIOB           POINT TO IOB
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R4,DYNDCB           POINT TO DCB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
BSRCCW   CCW   X'27',*,SILI,24     BACK-SPACE-BLOCK CCW
NAMSG    DC    C'BSR NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP MODULE INTERFACE BLOCK                           *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPI3
./ ADD NAME=DITTTPI4
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE BACK SPACE FILE                                  *
*                                                                    *
*       THIS ROUTINE PERFORMS THE BSF FUNCTION                       *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPI4 DITTPRFX TAPESAVE,'TAPE BACK SPACE FILE MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         ZAP   TAPEFILS,COMMP0     ZERO FILES COUNTER
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         POINT TO INPUT BUFFER
         STCM  R1,7,BSFCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         MVC   DYNCCW,BSFCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP MODULE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0030            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    END OF TAPE REACHED?
         BO    TAPE0060            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL??
         BNE   TAPE0400            NO
         AP    TAPEFILS,COMMP1     ADD 1 TO FILES BACK-SPACED
         CP    TAPEFILS,COMMFILS   PROCESSED ENOUGH FILES??
         BL    TAPE0020            NO
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TAPE9900            EXIT
TAPE0030 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0050            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0040            YES
         WTO   'BSF NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',        +
               CONSID=COMMCID
TAPE0040 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINT COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE ADDRESS
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0050 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0060 DS    0H
         DITTRACE ID=PEOT
         ABEND ABEND026,DUMP,,USER ABEND
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
BSFCCW   CCW   X'2F',*,SILI,1      BACK SPACE FILE CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPEFILS DC    PL5'0'              FILES COUNTER
NAMSG    DC    C'BSF NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE REQUEST BLOCK                            *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP MODULE PARAMETER BLOCK                           *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPI4
./ ADD NAME=DITTTPO1
*---------------------------------------------------------------------*
*                                                                     *
*        TAPE OUTPUT MODULE.                                          *
*                                                                     *
*        THIS MODULE DOES 'NORMAL' TAPE OUTPUT.  OUTPUT CAN BE        *
*        COPIED BLOCK-FOR-BLOCK FROM INPUT TO OUTPUT OR CAN BE        *
*        RE-BLOCKED TO THE SPECIFIED BLOCK SIZE.  ON END OF FILE      *
*        A TAPE-MARK WILL BE WRITTEN TO THE OUTPUT.                   *
*                                                                     *
*---------------------------------------------------------------------*
DITTTPO1 DITTPRFX TAPESAVE,'TAPE OUTPUT MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         L     R10,COMMOUTD        DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE0090            YES
         TM    COMMFLAG,$COMM1ST   FIRST PASS??
         BNO   TAPE0010            NO
         MVC   TAPECRCD,ACOMMIOO   STARTING ADDRESS IN OUTPUT BLOCK
         MVC   TAPEREM,COMMBLSZ    CLEAR REMAINING BLOCK AMOUNT
         ZAP   TAPEBLKS,COMMP0     RESET BLOCK COUNT
TAPE0010 DS    0H
         ICM   R1,15,COMMIND       INPUT DYNAMIC BLOCK ADDRESS
         BZ    TAPE0020            NO DYNAMIC BLOCK FOR INPUT
         CLC   TAPETAPE,DYNDDNAM-DYNBLOK(R1)
         BNE   TAPE0020            NOT TAPE
         TM    DYNSTAT-DYNBLOK(R1),$DYNTM
         BO    TAPE0090            END OF THIS LOGICAL FILE
TAPE0020 DS    0H
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOO         POINT TO INPUT BUFFER
         STCM  R1,7,WRITECCW+1     RELOCATE BUFFER ADDRESS
         STCM  R1,7,WTMCCW+1       RELOCATE BUFFER ADDRESS
TAPE0030 DS    0H
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0050            NO, OUTPUT INDIVIDUAL BLOCK
         CLC   COMMCRCL,COMMBLSZ   IS RECORD LONGER THAN MAX BLOCK??
         BH    TAPE0410            YES, NO WAY JOSE
         CLC   COMMCRCL,TAPEREM    WILL RECORD FIT IN THIS BLOCK??
         BH    TAPE0060            NO, OUTPUT THIS BLOCK
TAPE0040 DS    0H
         DITTRACE ID=ADDBLOCK      TRACE ADDITIONS TO BLOCK
         SR    R0,R0               CLEAR REGISTER
         SR    R1,R1               CLEAR REGISTER
         ICM   R0,3,TAPEREM        LOAD BLOCK SIZE LEFT
         ICM   R1,3,COMMCRCL       LENGTH OF NEW RECORD
         SR    R0,R1               SUBTRACT LENGTH OF NEW RECORD
         STH   R0,TAPEREM          SAVE LENGTH LEFT
TAPE0050 DS    0H
         DITTRACE ID=COPYRECD      TRACE COPY OF INPUT TO OUTPUT BLOCK
         L     R0,COMMCRCD         GET RECORD ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,COMMCRCL       GET RECORD LENGTH
         L     R2,TAPECRCD         GET CURRENT OUTPUT ADDRESS
         LR    R3,R1               COPY RECORD ADDRESS
         MVCL  R2,R0               MOVE RECORD TO OUTPUT
         L     R2,TAPECRCD         LOAD CURRENT ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,COMMCRCL       GET RECORD LENGTH
         AR    R2,R1               ADD CURRENT RECORD LENGTH
         ST    R2,TAPECRCD         SAVE TAPE OUTPUT ADDRESS
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BO    TAPE9900            YES, DON'T WRITE 'TILL BLOCK FULL
TAPE0060 DS    0H
         SR    R0,R0               CLEAR REGISTER
         ICM   R0,3,COMMCRCL       GET RECORD LENGTH
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0070            NO, USE CURRENT RECORD SIZE
         ICM   R0,3,COMMBLSZ       LOAD BLOCK SIZE
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,TAPEREM        UNUSED PORTION
         SR    R0,R1               SUBTRACT UNUSED PORTION
TAPE0070 DS    0H
         STH   R0,WRITECCW+6       STORE THE RECORD LENGTH
TAPE0080 DS    0H
         DITTRACE ID=WRITE         TRACE WRITES TO TAPE
         MVC   DYNCCW,WRITECCW     INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0130            NOT ALLOWED
         TM    DYNSTAT,$DYNPROT    FILE PROTECT ON?
         BO    TAPE0190            YES
         TM    DYNSTAT,$DYNPEOT    END OF TAPE REACHED?
         BO    TAPE0160            YES
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
         AP    TAPEBLKS,COMMP1     ADD 1 TO BLOCKS WRITTEN TO TAPE
         SR    R1,R1               CLEAR REGISTER
         MVC   TAPECRCD,ACOMMIOO   SAVE CURRENT RECORD ADDRESS
         MVC   TAPEREM,COMMBLSZ    RESET BLOCK SIZE
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BO    TAPE0040            YES, INSERT NEW RECORD
         B     TAPE9900            EXIT
TAPE0090 DS    0H
         TM    COMMOFLG,$REBLOCK   RE-BLOCKING RECORDS??
         BNO   TAPE0100            NO, WRITE END OF FILE TAPE MARK
         CLC   TAPEREM,COMMBLSZ    IS FULL BLOCK STILL AVAILABLE??
         BE    TAPE0100            YES, LAST BLOCK IS EMPTY
         SR    R1,R1               CLEAR FOR LENGTH
         ICM   R1,3,COMMBLSZ       LOAD BLOCK SIZE
         SH    R1,TAPEREM          SUBTRACT UNUSED PORTION
         STH   R1,WRITECCW+6       STORE THE RECORD LENGTH
         MVC   DYNCCW,WRITECCW     COPY CCW
         DITTRACE ID=LASTWRT       TRACE LAST BLOCK WRITES
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
TAPE0100 DS    0H
         MVC   DYNCCW,WTMCCW       MOVE WTM CCW
         DITTRACE ID=WRITETM       TRACE TAPE MARK WRITES
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    WRITE SUCCESSFUL??
         BNE   TAPE0400            NO
         MVC   PRBLKS,BLKSEDWD     MOVE EDIT WORD
         ED    PRBLKS,TAPEBLKS     EDIT BLOCKS WRITTEN
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0110            YES
         MVC   PRTDATA(BLKSMSGL),BLKSMSG
         MVI   PRTCC,C'0'          MOVE CARRIAGE CONTROL
         LA    R1,PRTBLOK          POINT TO PRINT BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         B     TAPE0120
TAPE0110 DS    0H
         LA    R1,BLKSMSG          MESSAGE ADDRESS
         LA    R0,BLKSMSGL         MESSAGE LENGTH
         TPUT  (R1),(R0)           SEND BLOCKS WRITTEN MESSAGE
TAPE0120 DS    0H
         MVC   TAPECRCD,ACOMMIOO   STARTING ADDRESS IN OUTPUT BLOCK
         MVC   TAPEREM,COMMBLSZ    CLEAR REMAINING BLOCK AMOUNT
         ZAP   TAPEBLKS,COMMP0     RESET BLOCK COUNT
         B     TAPE9900            EXIT
TAPE0130 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0150            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0140            YES
         WTO   'TAPE OUTPUT NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',+
               CONSID=COMMCID
TAPE0140 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINTER COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0150 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0160 DS    0H
         DITTRACE ID=PEOT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0180            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0170            YES
         WTO   'PHYSICAL EOT REACHED ON OUTPUT COMMAND TERMINATED',    +
               CONSID=COMMCID
TAPE0170 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINTER COMMAND
         MVC   PRTDATA(PEOTMSGL),PEOTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0180 DS    0H
         MVC   MSG01(PEOTMSGL),PEOTMSG
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0190 DS    0H
         DITTRACE ID=FILEPROT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0210            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0200            YES
         WTO   'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING)',        +
               CONSID=COMMCID
TAPE0200 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINTER COMMAND
         MVC   PRTDATA(PROTMSGL),PROTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0210 DS    0H
         MVC   MSG01(PROTMSGL),PROTMSG
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R3,DYNIOB           POINT TO IOB
         DITTRACE ID=WRITERR       TRACE I/O ERRORS
         DC    H'0'                ABEND
TAPE0410 DS    0H
         OI    COMMFLAG,$ABORT     ABORT CURRENT FUNCTION
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0420            YES
         MVC   PRTDATA(LONGMSGL),LONGMSG
         MVI   PRTCC,C'0'          MOVE CARRIAGE CONTROL
         LA    R1,PRTBLOK          POINT TO PRINT BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             LINK TO PRINT MODULE
         CLI   COMMENV,$ENVSTC     RUNNING AS A STARTED TASK??
         BNE   TAPE9900            NO
         WTO   'RECORD ENCOUNTERED THAT IS LONGER THAN MAXIMUM OUTPUT B+
               LOCK SIZE',                                             +
               CONSID=COMMCID
         B     TAPE9900            EXIT
TAPE0420 DS    0H
         MVC   MSG02(LONGMSGL),LONGMSG
         B     TAPE9900            EXIT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
WRITECCW CCW   X'01',*,SILI,0      WRITE CCW
WTMCCW   CCW   X'1F',*,SILI,24     WRITE TAPE MARK CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
TAPECRCD DC    A(0)                CURRENT RECORD ADDRESS
TAPEREM  DC    H'0'                LENGTH REMAINING IN RECORD
TAPEBLKS DC    PL5'0'              BLOCKS WRITTEN TO OUTPUT
BLKSEDWD DC    X'402020206B2020206B202120'
TAPETAPE DC    CL4'TAPE'           CONSTANT
LONGMSG  DC    C'* * * * * * TAPE RECORD ENCOUNTERED ON INPUT LONGER TH+
               AN MAXIMUM OUTPUT BLOCK SIZE * * * * * *'
LONGMSGL EQU   *-LONGMSG
BLKSMSG  DS    0C
PRBLKS   DC    CL12' '
         DC    C' BLOCKS AND TAPE MARK WRITTEN TO OUTPUT'
BLKSMSGL EQU   *-BLKSMSG
NAMSG    DC    C'TAPE OUTPUT NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
PEOTMSG  DC    C'PHYSICAL EOT REACHED ON OUTPUT, COMMAND TERMINATED'
PEOTMSGL EQU   *-PEOTMSG
PROTMSG  DC    C'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING), COMMAND+
               TERMINATED'
PROTMSGL EQU   *-PROTMSG
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPO1
./ ADD NAME=DITTTPO2
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE REWIND                                           *
*                                                                    *
*       THIS MODULE PERFORMS THE REW FUNCTION                        *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPO2 DITTPRFX TAPESAVE,'TAPE REWIND MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
TAPE0010 DS    0H
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         L     R10,COMMOUTD        DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         POINT TO INPUT BUFFER
         STCM  R1,7,REWCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         MVC   DYNCCW,REWCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O NOT ALLOWED?
         BE    TAPE0030            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT REACHED?
         BO    TAPE0040            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL??
         BNE   TAPE0400            NO
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TAPE9900            EXIT
TAPE0030 DS    0H
         DITTRACE ID=NOTALLOW
         ABEND ABEND027,DUMP,,USER ABEND
TAPE0040 DS    0H
         DITTRACE ID=PEOT
         ABEND ABEND028,DUMP,,USER ABEND
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         B     TAPE9900            EXIT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
REWCCW   CCW   X'07',*,SILI,1      REWIND CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT REQUEST BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPO2
./ ADD NAME=DITTTPO3
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE REWIND AND UNLOAD                                *
*                                                                    *
*       THIS ROUTINE PERFORMS THE RUN FUNCTION                       *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPO3 DITTPRFX TAPESAVE,'TAPE REWIND/UNLOAD MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
TAPE0010 DS    0H
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         L     R10,COMMOUTD        DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOO         POINT TO INPUT BUFFER
         STCM  R1,7,RUNCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         MVC   DYNCCW,RUNCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0030            NOT ALLOWED
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT?
         BO    TAPE0040            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL??
         BNE   TAPE0400            NO
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         B     TAPE9900            EXIT
TAPE0030 DS    0H
         DITTRACE ID=NOTALLOW
         ABEND ABEND029,DUMP,,USER ABEND
TAPE0040 DS    0H
         DITTRACE ID=PEOT
         ABEND ABEND030,DUMP,,USER ABEND
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
RUNCCW   CCW   X'0F',*,SILI,1      READ FORWARD CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT REQUEST BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPO3
./ ADD NAME=DITTTPO4
*--------------------------------------------------------------------*
*                                                                    *
*              WRITE TAPE MARK                                       *
*                                                                    *
*       THIS MODULE PERFORMS THE WTM FUNCTION                        *
*                                                                    *
*--------------------------------------------------------------------*
DITTTPO4 DITTPRFX TAPESAVE,'TAPE MARK MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
TAPE0010 DS    0H
         TM    COMMFLAG,$COMMCLS   CLOSE FILE INDICATOR??
         BO    TAPE9900            YES
         L     R10,COMMOUTD        DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           POINT TO DCB
         USING IHADCB,R2           ADDRESS DCB
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         POINT TO INPUT BUFFER
         STCM  R1,7,WTMCCW+1       RELOCATE BUFFER ADDRESS
TAPE0020 DS    0H
         ZAP   TAPEMRKS,COMMP0     ZERO MARKS WRITTEN
         LA    R4,1                ASSUME 1 TAPE MARK
         TM    COMMPFLG,$COMMRCS   NUMBER TO WRITE GIVEN??
         BNO   TAPE0030            NO
         ZAP   COMMDWRD,COMMRCDS   MOVE TO DOUBLE WORD
         CVB   R4,COMMDWRD         CONVERT TO BINARY
         LTR   R4,R4               WAS IT > ZERO??
         BZ    TAPE0040            NO
         BM    TAPE0040            NO
TAPE0030 DS    0H
         MVC   DYNCCW,WTMCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE PARAMETERS
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPNA    I/O ALLOWED?
         BE    TAPE0060            NOT ALLOWED
         TM    DYNSTAT,$DYNPROT    TAPE FILE PROTECTED?
         BO    TAPE0120            YES
         TM    DYNSTAT,$DYNPEOT    PHYSICAL EOT REACHED?
         BO    TAPE0090            YES
         CLI   EXCPSTAT,$EXCPOK    I/O SUCCESSFUL??
         BNE   TAPE0400            NO
         AP    TAPEMRKS,COMMP1     ADD 1 TO NUMBER WRITTEN
         BCT   R4,TAPE0030         WRITE ALL REQUESTED
TAPE0040 DS    0H
         OI    COMMFLAG,$COMMEOF   SET END-OF-FILE FLAG
         MVC   PRMRKS,MRKSEDWD     MOVE EDIT WORD
         ED    PRMRKS,TAPEMRKS     EDIT NUMBER WRITTEN
         CLI   COMMENV,$ENVTSO     TSO ENVIRONMENT?
         BE    TAPE0050            YES
         MVI   PRTCC,C'0'          DOUBLE SPACE
         MVI   PRTCMMD,$PRTDATA    PRINT
         MVC   PRTDATA(MRKSMSGL),MRKSMSG
         LA    R1,PRTBLOK          PASS PARAMETER BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT TAPE MARKS MESSAGE
         B     TAPE9900            EXIT
TAPE0050 DS    0H
         MVC   MSG01(MRKSMSGL),MRKSMSG
         B     TAPE9900            EXIT
TAPE0060 DS    0H
         DITTRACE ID=NOTALLOW
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0080            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0070            YES
         WTO   'WTM NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT',        +
               CONSID=COMMCID
TAPE0070 DS    0H
         MVI   PRTCMMD,$PRTCMD     SET PRINT COMMAND
         MVC   PRTDATA(NAMSGL),NAMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0080 DS    0H
         MVC   MSG01(NAMSGL),NAMSG SET MESSAGE
         OI    COMMFLAG,$ABORT     SET ABORT FLAG
         B     TAPE9900            AND EXIT
TAPE0090 DS    0H
         DITTRACE ID=PEOT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0110            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0100            YES
         WTO   'PHYSICAL EOT REACHED, WTM TERMINATED',                 +
               CONSID=COMMCID
TAPE0100 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINT COMMAND
         MVC   PRTDATA(PEOTMSGL),PEOTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0110 DS    0H
         MVC   MSG01(PEOTMSGL),PEOTMSG  SET MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0120 DS    0H
         DITTRACE ID=FILEPROT
         CLI   COMMENV,$ENVTSO     TSO USER?
         BE    TAPE0140            YES
         CLI   COMMENV,$ENVJOB     BATCH JOB?
         BE    TAPE0130            YES
         WTO   'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING)',        +
               CONSID=COMMCID
TAPE0130 DS    0H
         MVI   PRTCMMD,$PRTDATA    SET PRINT COMMAND
         MVC   PRTDATA(PROTMSGL),PROTMSG
         LA    R1,PRTBLOK          PRINTER INTERFACE BLOCK
         L     R15,APRT            PRINT MODULE ENTRY POINT
         BALR  R14,R15             PRINT MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
TAPE0140 DS    0H
         MVC   MSG01(PROTMSGL),PROTMSG SET MESSAGE
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         B     TAPE9900            AND EXIT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R2,DYNBLOK          POINT TO DYNAMIC BLOCK
         LA    R3,DYNDCB           POINT TO DCB
         LA    R4,DYNIOB           POINT TO IOB
         LA    R5,DYNECB           POINT TO ECB
         DITTRACE ID=BADIO         TRACE I/O FAILURES
         DC    H'0'                ABEND
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
TAPE9900 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO OPERATING SYSTEM
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
WTMCCW   CCW   X'1F',*,SILI,24     WRITE-TAPE-MARK CCW
TAPESAVE DC    18F'0'              REGISTER SAVE AREA
MRKSEDWD DC    X'402020206B2020206B202120'
TAPEMRKS DC    PL5'0'              TAPE MARKS WRITTEN
MRKSMSG  DS    0C
PRMRKS   DC    CL12' '
         DC    C' TAPE MARKS WRITTEN TO OUTPUT'
MRKSMSGL EQU   *-MRKSMSG
NAMSG    DC    C'WTM NOT ALLOWED WHILE TAPE IS AT PHYSICAL EOT'
NAMSGL   EQU   *-NAMSG
PEOTMSG  DC    C'PHYSICAL EOT REACHED, WTM TERMINATED'
PEOTMSGL EQU   *-PEOTMSG
PROTMSG  DC    C'OUTPUT TAPE IS FILE PROTECTED (NO WRITE RING), COMMAND+
                TERMINATED'
PROTMSGL EQU   *-PROTMSG
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PRINT MODULE INTERFACE BLOCK                          *
*                                                                    *
*--------------------------------------------------------------------*
PRTBLOK  PRTBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTERFACE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU   *-IHADCB
         END  DITTTPO4
./ ADD NAME=DITTTSOM
*--------------------------------------------------------------------*
*                                                                    *
*              TSO MAINLINE                                          *
*                                                                    *
*--------------------------------------------------------------------*
DITTTSOM DITTPRFX TSOMSAVE,'TSO MAINLINE PROGRAM'
         USING DITTCOMM,R11        SPECIFY BASE
         USING DYNBLOK,R3          DEFINE BASE
         LOAD  EP=ISPLINK          LOAD SPF INTERFACE
         ST    R0,AISP             SAVE SPF INTERFACE ADDRESS
         LA    R2,CMDPARM          'CMD' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,KLPARM           'KEYLEN' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DLPARM           'DATALEN' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,ADDRPARM         'DISKADDR' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DATAPARM         'DATA' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,SAMPPARM         'SAMPLE' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DCMDPARM         'CMD01-CMD05' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,TAPEPARM         'TAPE01' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,DASDPARM         'DASD01' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         LA    R2,MSGPARM          'MSG01-MSG05' VDEF PARAMETERS
         BAL   R10,VDEF0000        LINK TO 'VDEF'
         L     R2,AATTN            ATTENTION EXIT ADDRESS
         STAX  (R2),               SET ATTENTION EXIT                  +
               USADDR=(R11)        .. COMM AREA ADDRESS WILL BE HELPFUL
MENU0000 DS    0H
         DITTRACE ID=MENU
         LA    R1,CMD01            FIRST COMMAND DISPLAY AREA
         LA    R2,50               NUMBER OF DISPLAYABLE COMMANDS
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         USING FUNCBLOK,R5         DEFINE ADDRESSIBLITY
MENU0010 DS    0H
         CLI   FUNCELEN,X'FF'      END OF TABLE??
         BE    MENU0030            YES.. EXIT
         TM    FUNCENV,$FUNCTSO    DOES THIS COMMAND APPLY?
         BNO   MENU0020            NO
         MVC   0(L'FUNCCMD,R1),FUNCCMD
         LA    R1,6(R1)            NEXT COMMAND AREA
         BCT   R2,MENU0020         1 LESS LEFT
         B     MENU0030            ALL USED UP
MENU0020 DS    0H
         AH    R5,FUNCELEN         NEXT COMMAND
         B     MENU0010            LOOP
MENU0030 DS    0H
         MVC   CMD,MAINCMD         RESTORE SAVED COMMAND
         MVC   TAPE01,COMMBLKS     INITIALIZE ALLOCATED UNITS AREA
         MVC   TAPE01(MAINM01L),MAINM01 MOVE IN 'NONE' MESSAGE
         ICM   R3,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    MENU0090            NO DEVICES ALLOCATED
         LA    R4,TAPE01           FIRST UNIT IN DISPLAY AREA
         LA    R5,6                NUMBER OF UNITS DISPLAYABLE
         USING TAPEDSCT,R4         DEFINE BASE
MENU0040 DS    0H
         CLC   TSOTAPE,DYNDDNAM    IS THIS A TAPE UNIT?
         BNE   MENU0080            NOPE
         MVC   TAPECUU(TAPEL),COMMBLKS   INTIALIZE THIS ENTRY
         MVC   TAPECUU,DYNDDNAM+5  MOVE CUU FROM DDNAME
         L     R1,DYNDEBA          DEB ADDRESS
         USING DEB,R1              DEFINE DEB BASE
         CLI   DEBSDVM,$DEN1600    1600 BPI DENSITY?
         BE    MENU0050            YES
         CLI   DEBSDVM,$DEN6250    6250 BPI DENSITY?
         BE    MENU0060            YES
         CLI   DEBSDVM,$DEN3480    3480 DRIVE?
         BNE   MENU0070            NO
         MVC   TAPEDEN(MAINM02L),MAINM02
         B     MENU0070
MENU0050 DS    0H
         MVC   TAPEDEN(MAINM03L),MAINM03
         B     MENU0070
MENU0060 DS    0H
         MVC   TAPEDEN(MAINM04L),MAINM04
MENU0070 DS    0H
         LA    R4,TAPEL(R4)        NEXT DISPLAY AREA
         BCT   R5,MENU0080         MINUS 1 DISPLAY AREA
         B     MENU0090            ALL DISPLAY AREA USED
MENU0080 DS    0H
         ICM   R3,15,DYNNEXT       NEXT DYNAMIC BLOCK
         BNZ   MENU0040            CONTINUE
MENU0090 DS    0H
         MVC   DASD01,COMMBLKS     INITIALIZE ALLOCATED UNITS AREA
         MVC   DASD01(MAINM01L),MAINM01 MOVE IN 'NONE' MESSAGE
         ICM   R3,15,COMMDYHD      FIRST DYNAMIC BLOCK
         BZ    MENU0120            NO DEVICES ALLOCATED
         LA    R4,DASD01           FIRST UNIT IN DISPLAY AREA
         LA    R5,4                NUMBER OF UNITS DISPLAYABLE
         USING DASDDSCT,R4         DEFINE BASE
MENU0100 DS    0H
         CLC   TSOTAPE,DYNDDNAM    IS THIS A TAPE UNIT?
         BE    MENU0110            YES
         MVC   DASDCUU(DASDL),COMMBLKS   INTIALIZE THIS ENTRY
         MVC   DASDCUU,DYNDDNAM+5  COPY DASD CUU
         MVC   DASDVOL,DYNVOL      MOVE VOLSER
         LA    R4,DASDL(R4)        NEXT DISPLAY AREA
         BCT   R5,MENU0110         MINUS 1 DISPLAY AREA
         B     MENU0120            ALL DISPLAY AREA USED
MENU0110 DS    0H
         ICM   R3,15,DYNNEXT       NEXT DYNAMIC BLOCK
         BNZ   MENU0100            CONTINUE
MENU0120 DS    0H
         LA    R1,DISPPARM         DISPLAY PARAMETERS
         L     R15,AISP            SPF ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         TM    COMMATTN,$ATTN      ATTENTION DRIVEN DURING MENU?
         BO    ATTN0000            YES
         CH    R15,H8              END/RETURN REQUESTED?
         BE    MAIN9900            YES.. GO TO THE HOUSE
         CLC   ABENDCMD,CMD        ABEND?
         BE    ABEND000            YES
         MVC   SAMPLE,COMMBLKS     CLEAR MESSAGE
         MVC   MSG01,COMMBLKS      CLEAR MESSAGE
         MVC   MSG02,COMMBLKS      CLEAR MESSAGE
         MVC   MSG03,COMMBLKS      CLEAR MESSAGE
         MVC   MSG04,COMMBLKS      CLEAR MESSAGE
         MVC   MSG05,COMMBLKS      CLEAR MESSAGE
MAIN0010 DS    0H
         DITTRACE ID=NEWCMMD       TRACE
         MVC   MAINCMD,CMD         SAVE USER'S COMMAND
         CLC   TRACECMD,CMD        EXTERNAL TRACE?
         BE    XTRACE00            YES
         MVI   COMMPFLG,X'00'      CLEAR ANY OPTIONAL INDICATORS
         NI    COMMFLAG,255-$ABORT CLEAR ABORT FLAG IF SET
         NI    COMMFLAG,255-$COMMCER AND COMMAND ERROR FLAG IF SET
         LA    R3,CMD              COMMAND INPUT AREA
         LA    R4,L'CMD            MAX LENGTH OF REPLY
         LA    R1,CMD+(L'CMD-1)    LAST BYTE OF COMMAND DATA
TSO0000  DS    0H
         CLI   0(R1),C' '          BLANK?
         BH    TSO0010             NO
         BCTR  R1,0                MINUS 1
         BCT   R4,TSO0000          LOOP
         B     TSO0020             NO DATA AT ALL
TSO0010  DS    0H
         BAL   R14,PARSE000        FIND COMMAND ON INPUT RECORD
         OC    TSOWLEN,TSOWLEN     COMMAND PRESENT?
         BNZ   TSO0030             YES
TSO0020  DS    0H
         MVC   MSG01(TSOM001L),TSOM001
         B     MENU0030            REBUILD MENU
TSO0030  DS    0H
         L     R5,ACMD             FUNCTION TABLE ADDRESS
         DITTRACE ID=SCANSTRT,     TRACE COMMAND TABLE SCAN START      +
               DATA1=TSOWORK       .. CAPTURE THE COMMAND
TSO0040  DS    0H
         DITTRACE ID=SCANCMD       TRACE COMMAND TABLE SCAN
         CLI   FUNCELEN,X'FF'      END OF TABLE??
         BE    TSO0060             YES, INVALID COMMAND
         TM    FUNCENV,$FUNCTSO    DOES THIS COMMAND APPLY?
         BNO   TSO0050             NO
         LH    R1,TSOWLEN          GET LENGTH
         CH    R1,FUNCFLEN         FUNCTION LENGTH MATCH??
         BNE   TSO0050             NO
         BCTR  R1,0                AJUST FOR EXECUTE
         EX    R1,TSOFCLC          CHECK FUNCTION CODE
         BE    TSO0070             FOUND IT
TSO0050  DS    0H
         AH    R5,FUNCELEN         ADD ENTRY LENGTH
         B     TSO0040             LOOP
TSO0060  DS    0H
         MVC   MSG01(TSOM002L),TSOM002
         B     MENU0030            REBUILD MENU
TSO0070  DS    0H
         DITTRACE ID=CMMDFND       TRACE COMMAND FOUND
         CLI   0(R3),C'?'          COMMAND EXAMPLE WANTED??
         BNE   TSO0080             NO
         DITTRACE ID=EXAMPLE       TRACE POINT
         MVC   SAMPLE,FUNCEX2      MOVE COMMAND EXAMPLE
         B     MENU0030            RE-BUILD MENU
TSO0080  DS    0H
         SR    R6,R6               CLEAR FOR NUMBER OF REQ PARMS
         ICM   R6,3,FUNCNREQ       NUMBER OF REQUIRED PARAMETERS
         BZ    TSO0120             NONE... TRY FOR OPTIONAL PARMS
         LA    R7,FUNCREQ          FIRST REQUIRED PARAMETER
TSO0090  DS    0H
         DITTRACE ID=PARMSCAN      TRACE PARAMETER SEARCH START
         LTR   R4,R4               ANY DATA LEFT IN REPLY??
         BZ    TSO0300             NO... REQUIRED PARMS ARE MISSING
         MVC   TSOWORK,COMMBLKS    INITIALIZE WORK AREA
         BAL   R14,PARSE000        GET PARAMETER
         OC    TSOWLEN,TSOWLEN     PARAMETER FOUND?
         BZ    TSO0300             NO
         DITTRACE ID=REQPARM       TRACE REQUIRED PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
TSO0100  DS    0H
         MVC   PARMPARM,0(R7)      PASS FIELD ID
         MVC   PARMWORK,TSOWORK    PASS DATA
         MVC   PARMWLEN,TSOWLEN    PASS DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY PONT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE??
         BO    TSO0110             YES
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R2,0(R1)            MESSAGE LENGTH
         BCTR  R2,0                FOR EXECUTE
         EX    R2,PARMMSGE         MOVE MESSAGE
         B     MENU0030            RE-BUILD MENU
TSO0110  DS    0H
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,TSO0090          PROCESS NEXT PARAMETER
*---------------------------------------------------------------------*
*        ALL REQUIRED PARAMETERS HAVE BEEN PROCESSED.                 *
*        NOW PROCESS ANY OPTIONAL PARAMETERS.                         *
*---------------------------------------------------------------------*
TSO0120  DS     0H
         SR    R6,R6               CLEAR FOR NUMBER OF OPTIONAL PARMS
         ICM   R6,3,FUNCNOPT       NUMBER OF OPTIONAL PARAMETERS
         BZ    TSO0160             NONE..
         LA    R7,FUNCOPT          FIRST OPTIONAL PARAMETER
TSO0130  DS    0H
         LTR   R4,R4               ANY DATA LEFT IN REPLY??
         BZ    PROC0000            NO
         MVC   TSOWORK,COMMBLKS    INITIALIZE WORK AREA
         BAL   R14,PARSE000        GET PARAMETER
         OC    TSOWLEN,TSOWLEN     PARAMETER FOUND?
         BZ    TSO0150             NO, PARAMETER OMITTED
         DITTRACE ID=OPTPARM       TRACE OPTIONAL PARM CONVERSION
         MVI   PARMCMMD,$PARMLOC   'LOCATE' COMMAND
TSO0140  DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         MVC   PARMWORK,TSOWORK    DATA
         MVC   PARMWLEN,TSOWLEN    DATA LENGTH
         LA    R1,FUNCOPV          FIRST VALID VALUE FOR 'OPT'
         ST    R1,PARMOPV          PASS ADDRESS TO DITTPARM
         MVC   PARMOPN,FUNCOPN     NUMBER OF VALID VALUES
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         TM    PARMFLAG,$PARMOK    PARAMETER OKEE AND DOKEE??
         BO    TSO0150             YES
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R2,0(R1)            MESSAGE LENGTH
         BCTR  R2,0                FOR EXECUTE
         EX    R2,PARMMSGE         MOVE MESSAGE
         B     MENU0030            REBUILD MENU
TSO0150  DS    0H
         LA    R7,1(R7)            NEXT OPTIONAL PARAMETER
         BCT   R6,TSO0130          PROCESS NEXT PARAMETER
TSO0160  DS    0H
         LTR   R4,R4               ANY EXTRANEOUS DATA?
         BZ    PROC0000            NO
         MVC   MSG01(TSOM003L),TSOM003
         B     MENU0030            REBUILD MENU
         SPACE 2
*---------------------------------------------------------------------*
*        OPERATOR COMMAND EXHAUSTED.  WE STILL NEED SOME REQUIRED     *
*        PARAMETERS.  BUILD MESSAGES FOR UP TO 5 MISSING PARMS.       *
*---------------------------------------------------------------------*
TSO0300  DS    0H
         DITTRACE ID=MISSING       ENTRY INTO MISSING PARM PROMPTS
         LA    R2,5                NUMBER OF MESSAGE AREAS
         LA    R3,MSG01            FIRST MESSAGE AREA
         MVI   PARMCMMD,$PARMMIS   RETURN MESSAGE FOR MISSING PARMS
TSO0310  DS    0H
         MVC   PARMPARM,0(R7)      FIELD ID
         LA    R1,PARMBLOK         PARAMETER CONVERTER PARMS
         L     R15,APARM           PARAMETER CONVERTER ENTRY POINT
         BALR  R14,R15             LINK TO PARAMETER CONVERTER
         L     R1,PARMMSGA         MESSAGE ADDRESS
         LH    R14,0(R1)           MESSAGE LENGTH
         BCTR  R14,0               FOR EXECUTE
         EX    R14,PARMMSGM        MOVE MESSAGE
         LA    R3,L'MSG01(R3)      NEXT MESSAGE AREA
         BCT   R2,TSO0320          ACCOUNT FOR MESSAGE AREA USED
         B     MENU0030            ALL AREAS USED
TSO0320  DS    0H
         LTR   R4,R4               WAS ANY COMMAND DATA LEFT?
         BNZ   MENU0030            YES.. ONLY ISSUE MESSAGE FOR 1 PARM
         LA    R7,1(R7)            NEXT REQUIRED PARAMETER
         BCT   R6,TSO0310          PROCESS NEXT PARAMETER
         B     MENU0030            ALL AREAS USED
*---------------------------------------------------------------------*
*        ALL PARAMETERS PROCESSED.  PASS INPUT AND OUTPUT MODULE      *
*        ADDRESSES AND FLAGS.                                         *
*---------------------------------------------------------------------*
PROC0000 DS    0H
         MVC   COMMPANL,FUNCPANL   PANEL NAME FOR DISPLAY
         MVC   COMMIFLG,FUNCIFLG   INPUT MODULE FLAGS
         MVC   COMMOFLG,FUNCOFLG   OUTPUT MODULE FLAGS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,FUNCIN2        INPUT MODULE ADDRESS DISPLACEMENT
         BZ    PROC0010            NO INPUT MODULE
         LA    R2,DITTCOMM(R1)     INPUT MODULE ADDRESS ADDRESS
         MVC   COMMINM,0(R2)       INPUT MODULE ADDRESS
PROC0010 DS    0H
         ICM   R1,3,FUNCOUT2       TSO OUTPUT MODULE ADDRESS DISP
         BZ    PROC0030            NO OUTPUT MODULE
PROC0020 DS    0H
         LA    R2,DITTCOMM(R1)     OUTPUT MODULE ADDRESS ADDRESS
         MVC   COMMOUTM,0(R2)      OUTPUT MODULE ADDRESS
PROC0030 DS    0H
         MVC   CMD,COMMBLKS        CLEAR COMMAND AREA
         CLC   COMMINM,AALOC       IS INPUT MODULE 'ALOC'?
         BE    PROC0090            YES
         OC    COMMINM,COMMINM     IS THERE AN INPUT MODULE?
         BZ    PROC0060            NO
         OC    COMMINU,COMMINU     IS THERE AN INPUT UNIT?
         BZ    PROC0060            NO
         DITTRACE ID=ALOCIN        ALLOCATE INPUT
         MVI   DAIRCMMD,$DAIRLOC   DAIR COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNAMIC BLOCK BY CUU
         MVC   DAIRCUU,COMMINU     SET CUU
         LA    R1,DAIRBLOK         DAIR PARAMETERS
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR INTERFACE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    PROC0050            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    PROC0040            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
PROC0040 DS    0H
         MVC   MSG01(MAINM06L),MAINM06
         B     MENU0030            REBUILD MENU
PROC0050 DS    0H
         L     R1,COMMINM          INPUT MODULE ADDRESS
         USING MODPRFX,R1          DEFINE BASE
         ICM   R3,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM   UNIT CONSISTENT?
         BNE   PROC0200            NO
         ST    R3,COMMIND          SAVE DYNAMIC BLOCK ADDRESS
PROC0060 DS    0H
         OC    COMMOUTM,COMMOUTM   IS THERE AN OUTPUT MODULE?
         BZ    PROC0090            NO
         OC    COMMOUTU,COMMOUTU   IS THERE AN OUTPUT DEVICE?
         BZ    PROC0090            NO
         DITTRACE ID=ALOCOUT       ALLOCATE INPUT
         MVI   DAIRCMMD,$DAIRLOC   DAIR COMMAND
         MVI   DAIRTYPE,$DAIRCUU   LOCATE DYNAMIC BLOCK BY CUU
         MVC   DAIRCUU,COMMOUTU    SET CUU
         LA    R1,DAIRBLOK         DAIR PARAMETERS
         L     R15,ADAIR           DAIR MODULE ENTRY POINT
         BALR  R14,R15             LINK TO DAIR INTERFACE
         CLI   DAIRSTAT,$DAIRSOK   DAIR SUCCESSFUL?
         BE    PROC0080            YES
         CLI   DAIRSTAT,$DAIRSLE   DYNAMIC BLOCK NOT LOCATED?
         BE    PROC0070            YES
         ABEND ABEND011,DUMP,,USER UNEXPECTED STATUS FROM DITTDAIR
PROC0070 DS    0H
         DITTRACE ID=OUTNTACQ
         MVC   MSG01(MAINM07L),MAINM07
         B     MENU0030            REBUILD MENU
PROC0080 DS    0H
         L     R1,COMMOUTM         OUTPUT MODULE ADDRESS
         ICM   R3,15,DAIRAREA      DYNAMIC BLOCK ADDRESS
         CLC   PFXMODDS(4),DYNDDNAM   UNIT CONSISTENT?
         BNE   PROC0210            NO
         ST    R3,COMMOUTD         SAVE DYNAMIC BLOCK ADDRESS
PROC0090 DS    0H
         DITTRACE ID=FIRST,        TRACE ENTRY INTO I/O LOOP           +
               DATA1=COMMIND,                                          +
               DATA2=COMMOUTD
         OI    COMMFLAG,$COMM1ST   SIGNAL FIRST TIME
PROC0100 DS    0H
         TM    COMMATTN,$ATTN      ATTENTION EXIT DRIVEN?
         BO    ATTN0010            YES
         DITTRACE ID=CALLIN        TRACE
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    PROC0110            ZERO .. DON'T ATTEMPT TO INVOKE IT
         SR    R1,R1               NO INPUT PARAMETER FROM MAIN
         BALR  R14,R15             CALL INPUT
         TM    COMMFLAG,$ABEND     ABEND SET BY INPUT??
         BO    PROC0180            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY INPUT??
         BO    PROC0130            YES
         CLC   RTNCMD,CMD          'RETURN' COMMAND?
         BE    PROC0130            YES
         CLC   EXITCMD,CMD         'EXIT' COMMAND?
         BE    PROC0130            YES
*--------------------------------------------------------------------*
*                                                                    *
*        NOTE THAT THE EOF FLAG MAY BE SET BY AN OUTPUT ROUTINE.     *
*        THIS IS THE CASE WHEN THE FUNCTION REQUIRES AN OUTPUT       *
*        ROUTINE ONLY SUCH AS 'REW' OR 'RUN'.                        *
*                                                                    *
*--------------------------------------------------------------------*
PROC0110 DS    0H
         TM    COMMFLAG,$COMMEOF   END OF FILE ON INPUT??
         BO    PROC0130            YES...
         TM    COMMATTN,$ATTN      ATTENTION EXIT DRIVEN?
         BO    ATTN0010            YES
         DITTRACE ID=CALLOUT       TRACE
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    PROC0120            ZERO .. DON'T ATTEMPT TO INVOKE IT
         SR    R1,R1               NO INPUT PARAMETER FROM MAIN
         BALR  R14,R15             CALL OUTPUT
         TM    COMMFLAG,$ABEND     ABEND SET BY OUTPUT??
         BO    PROC0190            YES
         TM    COMMFLAG,$ABORT     ABORT SET BY OUTPUT??
         BO    PROC0130            YES
         TM    COMMFLAG,$COMMEOF   END OF FILE ON OUTPUT??
         BO    PROC0130            YES...
         CLC   RTNCMD,CMD          'RETURN' COMMAND?
         BE    PROC0130            YES
         CLC   EXITCMD,CMD         'EXIT' COMMAND?
         BE    PROC0130            YES
PROC0120 DS    0H
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         B     PROC0100            LOOP 'TILL END OF FILE
PROC0130 DS    0H
         DITTRACE ID=CLOSE         'CLOSE' IN PROGRESS
         NI    COMMFLAG,255-$COMM1ST TURN OFF FIRST TIME FLAG
         OI    COMMFLAG,$COMMCLS   INDICATE CLOSE FILES
         ICM   R15,15,COMMINM      INPUT MODULE ADDRESS
         BZ    PROC0140            NO INPUT MODULE
         BALR  R14,R15             ALLOW INPUT TO DO ANY CLEAN-UP
PROC0140 DS    0H
         ICM   R15,15,COMMOUTM     OUTPUT MODULE ADDRESS
         BZ    PROC0150            NO OUTPUT MODULE
         BALR  R14,R15             ALLOW OUTPUT TO DO ANY CLEAN-UP
PROC0150 DS    0H
         TM    COMMFLAG,$ABORT     COMMAND ABORTED?
         BNO   PROC0160            NO
         MVC   MSG05(MAINM05L),MAINM05
         B     PROC0170            RE-INITIALIZE
PROC0160 DS    0H
         MVC   MSG05(MAINM09L),MAINM09
         ICM   R3,15,COMMIND       INPUT DYNAMIC BLOCK ADDRESS
         BZ    PROC0170            NO DYNAMIC BLOCK
         CLC   TSOTAPE,DYNDDNAM    INPUT FROM TAPE?
         BE    PROCX160            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY DASD EOF RECORD?
         BNO   PROC0170            NO
         MVC   MSG01(MAINM14L),MAINM14
         B     PROC0170            RE-INITIALIZE
PROCX160 DS    0H
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    PROCX165            YES
         TM    COMMFLAG,$COMMFE    EOF FORCED BY TAPE MARK?
         BNO   PROC0170            NO, RE-INITIALIZE
         MVC   MSG01(MAINM15L),MAINM15
         B     PROC0170            RE-INITIALIZE
PROCX165 DS    0H
         MVC   MSG01(MAINM16L),MAINM16
         B     PROC0170            RE-INITIALIZE
PROC0170 DS    0H
         DITTRACE ID=REINIT
         NI    COMMFLAG,255-$COMMCLS TURN OFF CLOSE FLAG
         NI    COMMFLAG,255-$COMMEOF TURN OFF EOF FLAG
         NI    COMMFLAG,255-$COMMFE  TURN OFF FORCED EOF FLAG
         XC    COMMINM,COMMINM     CLEAR MODULE ADDRESS
         XC    COMMINU,COMMINU     CLEAR INPUT UNIT ADDRESS
         XC    COMMOUTM,COMMOUTM   CLEAR MODULE ADDRESS
         XC    COMMOUTU,COMMOUTU   CLEAR OUTPUT UNIT ADDRESS
         B     MENU0030            RE-BUILD MENU
PROC0180 DS    0H
         DITTRACE ID=ABENDIN       INPUT REQUESTED ABEND
         ABEND ABEND002,DUMP,,USER ABEND WITH REASON
PROC0190 DS    0H
         DITTRACE ID=ABENDOUT      OUTPUT REQUESTED ABEND
         ABEND ABEND003,DUMP,,USER ABEND WITH REASON
PROC0200 DS    0H
         MVC   MSG01(MAINM12L),MAINM12
         B     PROC0170            EXIT THIS COMMAND
PROC0210 DS    0H
         MVC   MSG01(MAINM13L),MAINM13
         B     PROC0170            EXIT THIS COMMAND
*--------------------------------------------------------------------*
*                                                                    *
*           EXTERNAL TRACE CONTROL                                   *
*                                                                    *
*--------------------------------------------------------------------*
XTRACE00 DS    0H
         DITTRACE ID=XTRACE
         CLC   ONCMD,CMD+6         TURN TRACE ON?
         BE    XTRACE10            YES
         CLC   OFFCMD,CMD+6        TURN TRACE OFF?
         BE    XTRACE30            YES
         MVC   MSG01(MAINM17L),MAINM17
         B     MENU0030            BUILD MENU
XTRACE10 DS    0H
         DITTRACE ID=TRACEON
         TM    XTRFLAG,$XTRACE     TRACE ALREADY ON?
         BO    XTRACE20            YES
         OI    XTRFLAG,$XTRACE     SET EXTERNAL TRACE FLAG
         MVC   MSG01(MAINM08L),MAINM08
         B     MENU0030            AND BUILD MENU
XTRACE20 DS    0H
         DITTRACE ID=DUPON
         MVC   MSG01(MAINM18L),MAINM18
         B     MENU0030            BUILD MENU
XTRACE30 DS    0H
         DITTRACE ID=TRACEOFF
         TM    XTRFLAG,$XTRACE     TRACE ON?
         BNO   XTRACE40            NO
         MVC   MSG01(MAINM19L),MAINM19
         NI    XTRFLAG,255-$XTRACE RESET FLAG
         B     MENU0030            AND BUILD MENU
XTRACE40 DS    0H
         DITTRACE ID=DUPOFF
         MVC   MSG01(MAINM20L),MAINM20
         B     MENU0030            BUILD MENU
         SPACE 2
ABEND000 DS    0H
         DITTRACE ID=ABEND
         ABEND ABEND999,DUMP,,USER ABEND AT OPERATOR'S REQUEST
VDEF0000 DS    0H
         USING VDEFDSCT,R2         DEFINE BASE
         DITTRACE ID=VDEF,         TRACE CALLS FOR VARIABLE DEFINITIONS+
               DATA1=VDEFNAME      .. CAPTURE VARIABLE BEING DEFINED
         ST    R2,DEFLEN           SET VARIABLE LENGTH ADDRESS
         OI    DEFLEN,X'80'        SET END OF LIST
         LA    R3,VDEFNAME         1ST NAME/DISPLACEMENT
         USING VDEFNAME,R3         DEFINE BASE
VDEF0010 DS    0H
         CLI   VDEFNAME,X'FF'      END OF LIST?
         BER   R10                 YES.. ALL DONE
         ST    R3,DEFNAME          SET VARIABLE NAME ADDRESS
         SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,VDEFDISP       DISPLACEMENT TO VARIABLE IN 'COMM'
         AR    R1,R11              VARIABLE'S ADDRESS
         ST    R1,DEFADDR          SET ADDRESS
         LA    R1,DEFPARM          VDEF PARM LIST
         L     R15,AISP            SPF ENTRY POINT
         BALR  R14,R15             LINK TO SPF TO DEFINE VARIABLE
         LTR   R4,R15              SPF RETURN CODE ZERO?
         BNZ   VDEF0020            NO
         LA    R3,VDEFNDL(R3)      NEXT NAME/DISPLACEMENT
         B     VDEF0010            LOOP
VDEF0020 DS    0H
         DITTRACE ID=VDEFFAIL,     TRACE DEFINITION FAILURES           +
               RDATA1=R4
         ABEND ABEND007,DUMP,,USER
*--------------------------------------------------------------------*
*                                                                    *
*              INPUT PARSER                                          *
*         R1, R2, R15 ARE WORK REGISTERS                             *
*         R3 HAS CURRENT INPUT ADDRESS                               *
*         R4 LENGTH REMAINING ON INPUT                               *
*         R14 IS RETURN ADDRESS                                      *
*                                                                    *
*         ON EXIT 'TSOWORK' CONTAINS DATA FOUND TO NEXT DELIMITER,   *
*         UP TO 44 BYTES.  'TSOWLEN' CONTAINS ITS LENGTH.             *
*         R3 WILL BE UPDATED PAST THE DELIMITER AND R4 WILL          *
*         REFLECT LENGTH LEFT FOLLOWING THE DELIMITER.               *
*                                                                    *
*--------------------------------------------------------------------*
PARSE000 DS    0H
         LTR   R4,R4               ANY DATA LEFT IN RECORD??
         BZR   R14                 NO.. EXIT
         SR    R2,R2               CLEAR LENGTH COUNTER
         XC    TSOWORK,TSOWORK     CLEAR WORK AREA
PARSE010 DS    0H
         CLI   0(R3),C' '          BLANK?
         BH    PARSE020            NO
         LA    R3,1(R3)            NEXT
         BCT   R4,PARSE010         LOOP
         B     PARSE050            NO DATA
PARSE020 DS    0H
         LA    R1,TSOWORK          POINT TO WORK AREA
         LA    R15,44              MAXIMUM LENGTH
PARSE030 DS    0H
         CLI   0(R3),C' '          BLANK??
         BE    PARSE050            YES
         CLI   0(R3),C','          COMMA??
         BE    PARSE050            YES
         MVC   0(1,R1),0(R3)       MOVE IT
         LA    R2,1(R2)            COUNT IT
         LA    R3,1(R3)            NEXT
         LA    R1,1(R1)            NEXT
         BCT   R4,PARSE040         DECREMENT COUNT
         STH   R2,TSOWLEN          SAVE CURRENT LENGTH
         BR    R14                 LENGTH EXHAUSTED, EXIT HERE
PARSE040 DS    0H
         BCT   R15,PARSE030        LOOP
PARSE050 DS    0H
         STH   R2,TSOWLEN          SAVE WORK AREA ACTIVE LENGTH
         LA    R3,1(R3)            SKIP DELIMITER
         BCTR  R4,0                REMOVE DELIMITER LENGTH
         BR    R14                 EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              ATTENTION EXIT                                        *
*                                                                    *
*--------------------------------------------------------------------*
ATTN0000 DS    0H
         DITTRACE ID=MENUSTAX      ATTENTION ENTERED WHILE IN MENU
         LA    R1,MAINM10          MAINLINE INTERRUPTED
         LA    R0,MAINM10L         TEXT LENGTH
         TPUT  (R1),(R0)           ISSUE MESSAGE
         B     MAIN9910            AND EXIT
ATTN0010 DS    0H
         DITTRACE ID=FUNCSTAX
         LA    R1,MAINM11          FUNCTION INTERRUPTED
         LA    R0,MAINM11L         TEXT LENGTH
         TPUT  (R1),(R0)           ISSUE MESSAGE
         NI    COMMATTN,255-$ATTN  RESET ATTENTION FLAG
         L     R2,AATTN            ATTENTION EXIT ADDRESS
         STAX  (R2),               RE-ESTABLISH ATTENTION EXIT         +
               USADDR=(R11)        .. COMM AREA ADDRESS WILL BE HELPFUL
         B     PROC0130            INTERRUPT CURRENT I/O LOOP
MAIN9900 DS    0H
         STAX  ,                   CANCEL ANY STAX EXIT
MAIN9910 DS    0H
         DITTRACE ID=EXIT          TRACE
         L     R2,COMMDYHD         FIRST BLOCK ON CHAIN
MAIN9920 DS    0H
         LTR   R3,R2               ADDRESS PRESENT??
         USING DYNBLOK,R3          DEFINE BASE
         BZ    MAIN9930            ALL HAVE BEEN RELEASED
         DITTRACE ID=RELDYN,       TRACE RELEASE                       +
               RDATA1=R2           .. SAVE DYNAMIC BLOCK ADDRESS
         L     R2,DYNNEXT          NEXT DYNAMIC BLOCK ADDRESS
         MVI   DAIRCMMD,$DAIRREL   SET COMMAND (RELEASE)
         MVI   DAIRTYPE,$DAIRCUU   SET TYPE OF LOCATE (BY CUU)
         MVC   DAIRCUU,DYNCUU      SET CUU
         LA    R1,DAIRBLOK         PARAMETER BLOCK ADDRESS
         L     R15,ADAIR           'DITTDAIR' ENTRY POINT
         BALR  R14,R15             RELEASE THIS RESOURCE
         CLI   DAIRSTAT,$DAIRSOK   SUCCESSFULLY RELEASED??
         BE    MAIN9920            YES
         ABEND ABEND008,DUMP,,USER ABEND
MAIN9930 DS    0H
         DITTRACE ID=RELDONE       DYNAMIC RELEASES ARE COMPLETE
         TM    XTRFLAG,$XTROPEN    EXTERNAL TRACE FILE OPEN??
         BNO   MAIN9940            NO
         LA    R2,XTRDCB           EXTERNAL TRACE DCB
         CLOSE ((R2))              CLOSE EXTERNAL TRACE
MAIN9940 DS    0H
         DELETE EP=ISPLINK         RELEASE SPF INTERFACE PROGRAM
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 GET OUT OF DODGE
*--------------------------------------------------------------------*
*                                                                    *
*               EXECUTED INSTRUCTIONS                                *
*                                                                    *
*--------------------------------------------------------------------*
TSOFCLC  CLC   FUNCCMD(0),TSOWORK   CORRECT COMMAND??
PARMMSGE MVC   MSG01(0),2(R1)       MOVE ERROR MESSAGE
PARMMSGM MVC   0(0,R3),2(R1)        MOVE 'MISSING' MESSAGE
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              PARAMETER CONVERTER INTERFACE                         *
*                                                                    *
*--------------------------------------------------------------------*
PARMBLOK PARMBLOK
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC ALLOCATION INTERFACE                          *
*                                                                    *
*--------------------------------------------------------------------*
DAIRBLOK DAIRBLOK TYPE=CSECT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              CONSTANTS/WORK AREAS                                  *
*                                                                    *
*--------------------------------------------------------------------*
TSOWORK  DC    CL44' '             PARSER OUTPUT WORK AREA
TSOWLEN  DC    H'0'                CURRENT PARSER OUTPUT LENGTH
DEFPARM  DC    A(VARDEF)         'VDEFINE' COMMAND
DEFNAME  DC    A(0)              VARIABLE NAME
DEFADDR  DC    A(0)              VARIABLE'S DATA ADDRESS
DEFFORM  DC    A(CHARFORM)       VARIABLE FORMAT
DEFLEN   DC    A(0)              VARIABLE LENGTH
DISPPARM DC    A(DISPLAY)        'DISPLAY' COMMAND
         DC    A(PANLNAME+X'80000000')
VARDEF   DC    CL8'VDEFINE'      SPF 'VDEFINE' COMMAND
DISPLAY  DC    CL8'DISPLAY'      SPF 'DISPLAY' COMMAND
CHARFORM DC    CL4'CHAR'
PANLNAME DC    CL8'DITTMENU'     PANEL NAME
TSOTAPE  DC    CL4'TAPE'
TRACECMD DC    C'TRACE '
ONCMD    DC    C'ON '
OFFCMD   DC    C'OFF '
ABENDCMD DC    C'ABEND '
RTNCMD   DC    C'RETURN '
EXITCMD  DC    C'EXIT '
MAINCMD  DC    CL67' '
         SPACE 1
CMDPARM  VDEF  LENGTH=67,                                              +
               FIELDS=(CMD,CMD)
KLPARM   VDEF  LENGTH=06,                                              +
               FIELDS=(KEYLEN,KEYLEN)
DLPARM   VDEF  LENGTH=06,                                              +
               FIELDS=(DATALEN,DATALEN)
ADDRPARM VDEF  LENGTH=11,                                              +
               FIELDS=(DISKADDR,DISKADDR)
DATAPARM VDEF  LENGTH=79,                                              +
               FIELDS=(DATA,DATA)
SAMPPARM VDEF  LENGTH=67,                                              +
               FIELDS=(SAMPLE,SAMPLE)
DCMDPARM VDEF  LENGTH=60,                                              +
               FIELDS=(CMD01,CMD01,                                    +
               CMD02,CMD02,                                            +
               CMD03,CMD03,                                            +
               CMD04,CMD04,                                            +
               CMD05,CMD05)
TAPEPARM VDEF  LENGTH=60,                                              +
               FIELDS=(TAPE01,TAPE01)
DASDPARM VDEF  LENGTH=60,                                              +
               FIELDS=(DASD01,DASD01)
MSGPARM  VDEF  LENGTH=79,                                              +
               FIELDS=(MSG01,MSG01,                                    +
               MSG02,MSG02,                                            +
               MSG03,MSG03,                                            +
               MSG04,MSG04,                                            +
               MSG05,MSG05)
MAINHX0F DC    8X'0F'
MAINHXCH DC    C'0123456789ABCDEF'
TSOM001  DC    C'ENTER A DITTO COMMAND'
TSOM001L EQU   *-TSOM001
TSOM002  DC    C'INVALID DITTO COMMAND'
TSOM002L EQU   *-TSOM002
TSOM003  DC    C'EXTRANEOUS DATA IN COMMAND, RE-ENTER'
TSOM003L EQU   *-TSOM003
MAINM01  DC    C'NONE'
MAINM01L EQU   *-MAINM01
MAINM02  DC    C'3480'
MAINM02L EQU   *-MAINM02
MAINM03  DC    C'1600'
MAINM03L EQU   *-MAINM03
MAINM04  DC    C'6250'
MAINM04L EQU   *-MAINM04
MAINM05  DC    C'COMMAND ABORTED'
MAINM05L EQU   *-MAINM05
MAINM06  DC    C'INPUT UNIT NOT ALLOCATED, COMMAND ABORTED'
MAINM06L EQU   *-MAINM06
MAINM07  DC    C'OUTPUT UNIT NOT ALLOCATED, COMMAND ABORTED'
MAINM07L EQU   *-MAINM07
MAINM08  DC    C'EXTERNAL TRACE ACTIVATED'
MAINM08L EQU   *-MAINM08
MAINM09  DC    C'LAST FUNCTION TERMINATED NORMALLY'
MAINM09L EQU   *-MAINM09
MAINM10  DC    C'DITTO TERMINATED DUE TO ATTENTION INTERRUPT'
MAINM10L EQU   *-MAINM10
MAINM11  DC    C'CURRENT FUNCTION TERMINATED DUE TO ATTENTION INTERRUPT+
               '
MAINM11L EQU   *-MAINM11
MAINM12  DC    C'INPUT DEVICE INCONSISTENT WITH COMMAND, COMMAND ABORTE+
               D'
MAINM12L EQU   *-MAINM12
MAINM13  DC    C'OUTPUT DEVICE INCONSISTENT WITH COMMAND, COMMAND ABORT+
               ED'
MAINM13L EQU   *-MAINM13
MAINM14  DC    C'EOF REACHED ON INPUT, COMMAND TERMINATED'
MAINM14L EQU   *-MAINM14
MAINM15  DC    C'TAPE MARK READ ON INPUT, COMMAND TERMINATED'
MAINM15L EQU   *-MAINM15
MAINM16  DC    C'PHYSICAL END OF TAPE REACHED ON INPUT, COMMAND TERMINA+
               TED'
MAINM16L EQU   *-MAINM16
MAINM17  DC    C'INVALID TRACE COMMAND (''TRACE ON'' OR ''TRACE OFF'')'
MAINM17L EQU   *-MAINM17
MAINM18  DC    C'TRACE ALREADY ACTIVE'
MAINM18L EQU   *-MAINM18
MAINM19  DC    C'TRACE DE-ACTIVATED'
MAINM19L EQU   *-MAINM19
MAINM20  DC    C'TRACE NOT ACTIVE'
MAINM20L EQU   *-MAINM20
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS                                            *
*                                                                    *
*--------------------------------------------------------------------*
TSOMSAVE DC    18F'0'              REGISTER SAVE AREA
H8       DC    H'8'                CONSTANT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
         ABCODES
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC RESOURCE CONTROL BLOCK                        *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=SHORT
*--------------------------------------------------------------------*
*                                                                    *
*              FUNCTION TABLE BLOCK                                  *
*                                                                    *
*--------------------------------------------------------------------*
         COPY FUNCBLOK
*--------------------------------------------------------------------*
*                                                                    *
*              VARIABLE PARAMETERS                                   *
*                                                                    *
*--------------------------------------------------------------------*
VDEFDSCT DSECT
VDEFLEN  DS    AL4      VARIABLE LENGTH
VDEFNAME DS    CL8      VARIABLE NAME
VDEFDISP DS    AL2      DISPLACEMENT INTO 'DITTCOMM'
VDEFNDL  EQU   *-VDEFNAME   NAME+DISPLACEMENT DATA LENGTH
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              ALLOCATED TAPE DEVICE DISPLAY                         *
*                                                                    *
*--------------------------------------------------------------------*
TAPEDSCT DSECT
TAPECUU  DS   CL3
         DS   C
TAPEDEN  DS   CL4
         DS   CL2
TAPEL    EQU  *-TAPEDSCT
*--------------------------------------------------------------------*
*                                                                    *
*              ALLOCATED DASD DEVICE DISPLAY                         *
*                                                                    *
*--------------------------------------------------------------------*
DASDDSCT DSECT
DASDCUU  DS   CL3
         DS   C
DASDVOL  DS   CL6
         DS   CL5
DASDL    EQU  *-DASDDSCT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DATA EXTENT BLOCK                                     *
*                                                                    *
*--------------------------------------------------------------------*
         IEZDEB LIST=NO
         END
./ ADD NAME=DITTTSO1
*--------------------------------------------------------------------*
*                                                                    *
*                  CHARACTER ONLY DATA DISPLAY                       *
*                                                                    *
*--------------------------------------------------------------------*
DITTTSO1 DITTPRFX TSO1SAVE,'CHARACTER ONLY DATA DISPLAY'
         USING DITTCOMM,R11        DEFINE COMM AREA BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSING TIME?
         BO    TSO0500             YES...
         DITTRACE ID=DEFTABLE      TRACE TABLE DEFINITION
         LA    R1,TBLEPARM         'DITTDATA' TABLE PARMS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF TO DEFINE TABLE
         CH    R15,H4              SUCCESSFUL?
         BH    ERR0010             NO...
         L     R10,COMMIND         INPUT DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         DEFINE BASE
         CLC   TSOTAPE,DYNDDNAM    WAS INPUT FROM TAPE?
         BE    TSO0020             YES...
         SR    R1,R1               CLEAR REGISTER
         IC    R1,COMMDREC         RECORD NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT RECORD NUMBER
         MVC   DISKREC,EDWORK+3    COPY RECORD NUMBER
         IC    R1,CNTKEYL          KEY LENGTH
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   KEYLEN,EDWORD2      INITIALIZE WITH EDIT WORD
         ED    KEYLEN,COMMDWRD+5   EDIT KEY LENGTH
         ICM   R1,3,COMMDCYL       CYLINDER NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT CYLINDER NUMBER
         MVC   DISKCYL,EDWORK+2    COPY CYLINDER NUMBER
         ICM   R1,3,COMMDHD        HEAD NUMBER
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   EDWORK,EDWORD1      INITIALIZE WITH EDIT WORD
         ED    EDWORK,COMMDWRD+5   EDIT HEAD NUMBER
         MVC   DISKHEAD,EDWORK+4   COPY HEAD NUMBER
         ICM   R1,3,CNTDATAL       DATA LENGTH FROM COUNT
         CVD   R1,COMMDWRD         CONVERT TO DECIMAL
         MVC   DATALEN,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATALEN,COMMDWRD+5  EDIT DATA LENGTH
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY 'KEY' IF PRESENT                               *
*                                                                     *
* ------------------------------------------------------------------- *
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,1,CNTKEYL        KEY LENGTH
         BZ    TSO0010             NO 'KEY' TO DISPLAY
         ICM   R4,15,COMMKEY       KEY'S ADDRESS
         BZ    ERR0030             KEY LENGTH WITH NO KEY ADDRESS?
         DITTRACE ID=DISPKEY,      TRACE                               +
               RDATA1=R3,          .. KEY'S LENGTH                     +
               RDATA2=R4           .. KEY'S ADDRESS
         MVC   DATAAREA,COMMBLKS   INITIALIZE WORK AREA
         MVC   KEYHEAD,=C'KEY'     SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR KEY
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY DATA                                           *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0010  DS    0H
         DITTRACE ID=DISPDATA
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,CNTDATAL       DATA LENGTH
         BZ    TSO0300             NO DATA... DISPLAY THE TABLE
         ICM   R4,15,COMMCRCD      DATA'S ADDRESS
         BZ    ERR0040             DATA LENGTH WITH NO DATA ADDRESS?
         TM    COMMOFLG,$DEBLOCK   DEBLOCKING RECORDS?
         BO    TSO0030             YES..
         MVC   DATAHEAD,=C'DATA'   SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR DATA
         B     TSO0300             DISPLAY TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              TAPE DATA                                              *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0020  DS    0H
         DITTRACE ID=TAPEDATA
         SR    R3,R3               CLEAR REGISTER
         ICM   R3,3,COMMCRCL       DATA LENGTH
         BZ    ERR0040             NO DATA...
         ICM   R4,15,COMMCRCD      DATA'S ADDRESS
         BZ    ERR0040             DATA LENGTH WITH NO DATA ADDRESS?
         CVD   R3,COMMDWRD         CONVERT TO DECIMAL
         MVC   DATALEN,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATALEN,COMMDWRD+5  EDIT BLOCK SIZE
         TM    COMMOFLG,$DEBLOCK   DEBLOCKING RECORDS?
         BO    TSO0030             YES..
         MVC   DATAHEAD,=C'DATA'   SET HEADING
         BAL   R9,TSO0100          BUILD DETAIL FOR DATA
         B     TSO0300             DISPLAY TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              DISPLAY DATA 1 LOGICAL RECORD AT A TIME                *
*                                                                     *
*         R3 IS FULL LENGTH OF DATA                                   *
*         R4 IS ADDRESS OF FIRST LOGICAL RECORD                       *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0030  DS    0H
         DITTRACE ID=DEBLOCK
         TM    COMMOFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TSO0060             NOPE
         LR    R5,R3               COPY DATA LENGTH
         LR    R6,R4               COPY DATA ADDRESS
TSO0040  DS    0H
         SH    R5,H4               MINUS LENGTH OF BDW
         BNH   TSO0060             TOO SHORT... WELL TRY ANYWAY
         LA    R6,4(R6)            FIRST RDW (MAYBE)
         SR    R1,R1               CLEAR REGISTER
TSO0050  DS    0H
         ICM   R1,3,0(R6)          RECORD LENGTH FROM RDW
         CR    R5,R1               RDW WITHIN REASON?
         BL    TSO0060             NO.. ASSUME RECFM=V (NOT VB)
         AR    R6,R1               NEXT RECORD'S ADDRESS
         SR    R5,R1               CALCULATE LENGTH REMAINING IN BLOCK
         BH    TSO0050             CHECK OUT THIS LOGICAL RECORD
         BNZ   TSO0060             RDW'S DON'T ADD UP TO BDW'S
         SH    R3,H4               MINUS BDW LENGTH
         LA    R4,4(R4)            SKIP BDW
TSO0060  DS    0H
         LR    R5,R3               SAVE DATA LENGTH
         LR    R6,R4               SAVE DATA ADDRESS
TSO0070  DS    0H
         ICM   R3,3,COMMLRCL       LOGICAL RECORD LENGTH
         TM    COMMOFLG,$VREC      VARIABLE LENGTH RECORDS?
         BNO   TSO0080             NO
         ICM   R3,3,0(R6)          LENGTH FROM RECORD
TSO0080  DS    0H
         CR    R3,R5               LONGER THAN LENGTH LEFT?
         BNH   TSO0090             NO
         LR    R3,R5               LIMIT LENGTH TO LENGTH LEFT
TSO0090  DS    0H
         LR    R4,R6               COPY RECORD'S ADDRESS
         LR    R7,R3               SAVE LENGTH USED FOR THIS RECORD
         DITTRACE ID=LREC,         TRACE EACH LOGICAL RECORD           +
               RDATA1=R3,          .. LOGICAL RECORD'S LENGTH          +
               RDATA2=R4           .. LOGICAL RECORD'S ADDRESS
         MVC   RECHEAD,=C'RECORD SIZE'
         CVD   R3,COMMDWRD         CONVERT TO DECIMAL
         MVC   RECSIZE,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    RECSIZE,COMMDWRD+5  EDIT LOGICAL RECORD SIZE
         BAL   R9,TSO0100          BUILD DETAIL LINES FOR THIS RECORD
         AR    R6,R7               NEXT LOGICAL RECORD'S ADDRESS
         SR    R5,R7               MINUS LENGTH OF LAST RECORD
         BH    TSO0070             BUILD DATA FOR EACH LOGICAL RECORD
         B     TSO0300             DISPLAY THE TABLE
* ------------------------------------------------------------------- *
*                                                                     *
*              BUILD DETAIL LINES AND ADD THEM TO THE TABLE           *
*                                                                     *
*         AT ENTRY:  R3 LENGTH OF DATA                                *
*                    R4 ADDRESS OF DATA                               *
*                    R9 RETURN ADDRESS                                *
*                                                                     *
*         REGISTERS 1, 2, 3, 4, 14, AND 15 WILL BE ALTERED            *
*                                                                     *
* ------------------------------------------------------------------- *
TSO0100  DS    0H
         DITTRACE ID=STRTBLD,      TRACE POINT                         +
               RDATA1=R3,          .. DATA ADDRESS                     +
               RDATA2=R4           .. DATA LENGTH
         ZAP   TSOOFF,P0           RESET OFFSET COUNTER
TSO0110  DS    0H
         LR    R2,R3               COPY RECORD LENGTH
         CH    R2,H50              TOO MUCH FOR 1 LINE??
         BNH   TSO0120             NO, CONTINUE
         LH    R2,H50              LIMIT LENGTH TO 50
TSO0120  DS    0H
         BCTR  R2,0                ADJUST FOR EXECUTES
         DITTRACE ID=DETAIL,       TRACE POINT                         +
               RDATA1=R2           .. LENGTH USED FOR DETAIL
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,DATATR           TRANSLATE OUT UNPRINTABLES
         TM    COMMOFLG,$HEX       DISPLAY IN HEX?
         BO    TSO0130             YES
         CP    TSOOFF,P0           FIRST LINE OF RECORD?
         BE    TSO0130             YES... SKIP OFFSET
         MVC   DATAOFF,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATAOFF,TSOOFF      EDIT OFFSET
         MVI   DATAPLUS,C'+'       MOVE PLUS SIGN
TSO0130  DS    0H
         BAL   R8,TSO0400          ADD ROW TO TABLE
         TM    COMMOFLG,$HEX       DISPLAY IN HEX?
         BNO   TSO0150             NO
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,ZONEMVC          PREPARE FOR ZONES TRANSLATE
         EX    R2,HEXTR            TRANSLATE ZONES TO DISPLAYABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
         EX    R2,DATAMVC          MOVE TO 'DATAAREA'
         EX    R2,NUMRMVC          PREPARE FOR NUMERICS TRANSLATE
         EX    R2,HEXTR            TRANSLATE NUMERICS TO DISPLAYABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
         EX    R2,SCALEMVC         MOVE TO SCALE
         CP    TSOOFF,P0           FIRST LINE OF RECORD?
         BE    TSO0140             YES... SKIP OFFSET
         MVC   DATAOFF,EDWORD2     INITIALIZE WITH EDIT WORD
         ED    DATAOFF,TSOOFF      EDIT OFFSET
         MVI   DATAPLUS,C'+'       MOVE PLUS SIGN
TSO0140  DS    0H
         BAL   R8,TSO0400          ADD ROW TO TABLE
         BAL   R8,TSO0400          ADD ROW TO TABLE
TSO0150  DS    0H
         LA    R2,1(R2)            RESET LENGTH
         AR    R4,R2               BRING INPUT ADDRESS FORWARD
         AP    TSOOFF,P50          ADD TO DATA OFFSET
         SR    R3,R2               COMPUTE LENGTH REMAINING
         BNZ   TSO0110             REPEAT 'TILL ALL LENGTH DONE
         TM    COMMOFLG,$HEX       DISPLAYING IN HEX?
         BO    TSO0160             YES
         BAL   R8,TSO0400          ADD BLANK LINE TO TABLE
TSO0160  DS    0H
         DITTRACE ID=BLDDONE
         BR    R9                  BUILD DONE
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              DISPLAY TABLE                                         *
*                                                                    *
*--------------------------------------------------------------------*
TSO0300  DS    0H
         DITTRACE ID=DISPLAY
         LA    R1,TOPPARM          'TBTOP' PARAMETERS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF FOR 'TBTOP'
         LA    R1,COMMPANL         PANEL NAME ADDRESS
         ST    R1,DISPPANL         SET ADDRESS IN PARMLIST
         OI    DISPPANL,X'80'      SET END OF LIST
         LA    R1,DISPPARM         DISPLAY PARAMETERS
         L     R15,AISP            SPF INTERFACE ADDRESS
         BALR  R14,R15             LINK TO SPF TO DISPLAY
         B     TSO9900             AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              ADD ROWS TO SPF TABLE                                 *
*                                                                    *
*--------------------------------------------------------------------*
TSO0400  DS    0H
         DITTRACE ID=ADDROW
         MVC   DATA,DATAAREA       COPY DATA
         LA    R1,ADDPARM          SPF TBADD PARMS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBADD SUCCESSFUL?
         BNZ   ERR0020             NO
         MVC   DATAAREA,COMMBLKS   RE-INITIALIZE WORK AREA
         BR    R8                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
TSO0500  DS    0H
         DITTRACE ID=EOF
         B     TSO9900             EXIT
*--------------------------------------------------------------------*
*                                                                    *
*                                                                    *
*                                                                    *
*--------------------------------------------------------------------*
ERR0010  DS    0H
         DITTRACE ID=CREFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG001RC,EDWORD3    INITIALIZE WITH EDIT WORD
         ED    MSG001RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG001           MESSAGE TEXT ADDRESS
         LA    R0,MSG001L          MESSAGE TEXT LENGTH
         B     TPUT0000            DISPLAY MESSAGE
ERR0020  DS    0H
         DITTRACE ID=ADDFAIL,      TBADD FAILED                        +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG002RC,EDWORD3    INITIALIZE WITH EDIT WORD
         ED    MSG002RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG002           MESSAGE TEXT ADDRESS
         LA    R0,MSG002L          MESSAGE TEXT LENGTH
         B     TPUT0000            DISPLAY MESSAGE
ERR0030  DS    0H
         ABEND ABEND022,DUMP,,USER ABEND
ERR0040  DS    0H
         ABEND ABEND023,DUMP,,USER ABEND
*--------------------------------------------------------------------*
*                                                                    *
*              DISPLAY TPUT MESSAGES                                 *
*                                                                    *
*--------------------------------------------------------------------*
TPUT0000 DS    0H
         TPUT  (R1),(R0)           DISPLAY TPUT MESSAGE
*--------------------------------------------------------------------*
*                                                                    *
*              EXIT POINT                                            *
*                                                                    *
*--------------------------------------------------------------------*
TSO9900  DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              EXECUTED INSTRUCTIONS                                 *
*                                                                    *
*--------------------------------------------------------------------*
DATAMVC  MVC   DATADATA(0),0(R4)   MOVE DATA TO DETAIL AREA
DATATR   TR    DATADATA(0),CHARTBL TRANSLATE OUT UNDISPLAYABLES
ZONEMVC  MVN   DATADATA(0),HEX0000 NULLIFY NUMERICS
NUMRMVC  MVZ   DATADATA(0),HEX0000 NULLIFY ZONES
HEXTR    TR    DATADATA(0),HEXCHAR TRANSLATE TO DISPLAYABLE
SCALEMVC MVC   DATADATA(0),SCALE   MOVE SCALE
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORK AREAS/CONSTANTS                                  *
*                                                                    *
*--------------------------------------------------------------------*
TSO1SAVE DC    18F'0'              REGISTER SAVE AREA
TBLEPARM DC    A(TBCREATE)         VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
         DC    A(0)                NO 'KEY' NAMES
         DC    A(NDATA)            VARIABLES LIST
         DC    A(NOWRITE)          IN CORE ONLY
         DC    A(REPLACE+X'80000000')
ADDPARM  DC    A(TBADD)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
TOPPARM  DC    A(TBTOP)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
DISPPARM DC    A(TBDISPL)          VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME
DISPPANL DC    A(0)                PANEL NAME ADDRESS
H4       DC    H'4'                CONSTANT
H50      DC    H'50'               CONSTANT
HEX0000  DC    50X'00'             CONSTANT
TSOOFF   DC    PL3'0'              DATA OFFSET
P0       DC    P'0'                CONSTANT
P50      DC    P'50'               CONSTANT
TBCREATE DC    CL8'TBCREATE'       TABLE CREATE VERB
TBADD    DC    CL8'TBADD'          TABLE ADD VERB
TBTOP    DC    CL8'TBTOP'          TABLE TOP VERB
TBDISPL  DC    CL8'TBDISPL'        TABLE DISPLAY VERB
NTABLE   DC    CL8'TSO1DATA'       TABLE NAME
NDATA    DC    C'(DATA)'           VARIABLE NAMES LIST
NOWRITE  DC    CL8'NOWRITE'        CONSTANT
REPLACE  DC    CL8'REPLACE'        CONSTANT
TSOTAPE  DC    C'TAPE'             CONSTANT
         SPACE
DATAAREA DS    0CL79
         DC    CL18' '
         ORG   DATAAREA
KEYHEAD  DS    CL3                 'KEY'
         ORG   DATAAREA
DATAHEAD DS    CL4                 'DATA'
         ORG   DATAAREA
RECHEAD  DS    CL11                'RECORD SIZE'
         DS    C
RECSIZE  DS    CL6                 LOGICAL RECORD SIZE
         ORG   DATAAREA+18
         DC    CL02' '
DATAPLUS DC    C' '
DATAOFF  DC    CL6' '              LINE'S OFFSET FROM FRONT OF RECORD
         DC    CL02' '
DATADATA DC    CL50' '             DATA AREA
         SPACE
SCALE    DC    C'....+...10....+...20....+...30....+...40....+...50'
EDWORK   DC    CL6' '
EDWORD1  DC    X'F02120202020'
EDWORD2  DC    X'402020202120'
EDWORD3  DC    X'40202120'         RETURN CODE EDIT WORD
MSG001   DS    0C
         DC    C'TBCREATE FAILED, RETURN CODE='
MSG001RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG001L  EQU   *-MSG001
MSG002   DS    0C
         DC    C'TBADD FAILED, RETURN CODE='
MSG002RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG002L  EQU   *-MSG002
*--------------------------------------------------------------------*
*                                                                    *
*              DISPLAYABLE CHARACTERS TABLE                          *
*                                                                    *
*--------------------------------------------------------------------*
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F
CHARTBL  DC    XL16'40404040404040404040404040404040'     X'00' - X'0F'
         DC    XL16'40404040404040404040404040404040'     X'10' - X'1F'
         DC    XL16'40404040404040404040404040404040'     X'20' - X'2F'
         DC    XL16'40404040404040404040404040404040'     X'30' - X'3F'
         DC    XL16'404040404040404040404A4B4C4D4E4F'     X'40' - X'4F'
         DC    XL16'504040404040404040405A5B5C5D5E5F'     X'50' - X'5F'
         DC    XL16'60614040404040404040406B6C6D6E6F'     X'60' - X'6F'
         DC    XL16'404040404040404040407A7B7C7D7E7F'     X'70' - X'7F'
         DC    XL16'40404040404040404040408B8C8D8E8F'     X'80' - X'8F'
         DC    XL16'40404040404040404040409B9C9D9E9F'     X'90' - X'9F'
         DC    XL16'4040404040404040404040ABACADAEAF'     X'A0' - X'AF'
         DC    XL16'4040404040404040404040BBBCBDBEBF'     X'B0' - X'BF'
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'     X'C0' - X'CF'
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'     X'D0' - X'DF'
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'     X'E0' - X'EF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'     X'F0' - X'FF'
*                   0 1 2 3 4 5 6 7 8 9 A B C D E F
HEXCHAR  DC    CL16'0123456789ABCDEF'                     X'00' - X'0F'
         DC    CL16'1               '                     X'10' - X'1F'
         DC    CL16'2               '                     X'20' - X'2F'
         DC    CL16'3               '                     X'30' - X'3F'
         DC    CL16'4               '                     X'40' - X'4F'
         DC    CL16'5               '                     X'50' - X'5F'
         DC    CL16'6               '                     X'60' - X'6F'
         DC    CL16'7               '                     X'70' - X'7F'
         DC    CL16'8               '                     X'80' - X'8F'
         DC    CL16'9               '                     X'90' - X'9F'
         DC    CL16'A               '                     X'A0' - X'AF'
         DC    CL16'B               '                     X'B0' - X'BF'
         DC    CL16'C               '                     X'C0' - X'CF'
         DC    CL16'D               '                     X'D0' - X'DF'
         DC    CL16'E               '                     X'E0' - X'EF'
         DC    CL16'F               '                     X'F0' - X'FF'
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  REGEQU
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK  TYPE=SHORT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         ABCODES
         END  DITTTSO1
./ ADD NAME=DITTTSO2
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE MAP 'TMAP' MODULE                                *
*                                                                    *
*       THIS MODULE PERFORMS THE 'TMAP' FUNCTION FOR THE TSO         *
*       ENVIRONMENT.  'HDR1', 'HDR2', 'EOF1', AND 'EOF2' ARE WATCHED *
*       FOR, AND WHEN ENCOUNTERED, THE RELATIVE FILE NUMBER AND FILE *
*       NAME ARE PRINTED.  BY DEFAULT TMAP WILL STOP WHEN 2 TAPE     *
*       MARKS IN A ROW WITH NO INTERVIENING DATA ARE FOUND.  THE     *
*       OPTIONS 'FULL', 'FULLTAPE', OR 'ALL' WILL CAUSE THE TAPE TO  *
*       BE READ TO THE REFLECTIVE MARKER OR OFF THE END OF THE REEL. *
*                                                                    *
*--------------------------------------------------------------------*
DITTTSO2 DITTPRFX TMAPSAVE,'TAPE ''VTOC'' MODULE'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
         TM    COMMFLAG,$COMMCLS   CLOSE FILES?
         BO    EXIT0000            YES.. EXIT
         ZAP   FILECT,COMMP0       ZERO FILES COUNTER
         MVI   TAPEFLAG,00         INITIALIZE FLAGS
         LA    R1,TBLEPARM         DEFINE TABLE PARMS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF TO DEFINE TABLE
         CH    R15,H4              DEFINE SUCCESSFUL?
         BH    ERR0010             NO
TAPE0010 DS    0H
         L     R10,COMMIND         DYNAMIC BLOCK ADDRESS
         USING DYNBLOK,R10         SPECIFY BASE
         ST    R10,EXCPDYN         PASS DYNAMIC BLOCK ADDRESS TO EXCP
         LA    R2,DYNDCB           DCB ADDRESS
         USING IHADCB,R2           DEFINE DSECT BASE
         DITTRACE ID=CCWINIT       TRACE INTIALIZE ROUTINE
         L     R1,ACOMMIOI         INPUT BUFFER
         STCM  R1,7,REWCCW+1       RELOCATE I/O ADDRESS
         STCM  R1,7,READCCW+1      RELOCATE I/O ADDRESS
         DITTRACE ID=REWIND        TRACE INITIAL REWIND
         MVC   DYNCCW,REWCCW       INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         DITTRACE ID=GOODREW       REWIND SUCCESSFUL
TAPE0020 DS    0H
         DITTRACE ID=READFWD       TRACE READ FORWARD
         MVC   DYNCCW,READCCW      INITIALIZE THE CCW
         LA    R1,EXCPBLOK         EXCP INTERFACE BLOCK
         L     R15,AEXCP           EXCP MODULE ENTRY POINT
         BALR  R14,R15             LINK TO EXCP MODULE
         TM    DYNSTAT,$DYNPEOT    PHYSICAL END OF TAPE REACHED?
         BO    TAPE0230            YES
         CLI   DYNSTAT,$DYNTM      TAPE MARK READ?
         BE    TAPE0200            YES
         CLI   EXCPSTAT,$EXCPOK    EXCP SUCCESSFUL?
         BNE   TAPE0400            NO
         TM    TAPEFLAG,$TAPEFHD   FILE HEADING ALREADY PERFORMED?
         BO    TAPE0030            YES
         AP    FILECT,COMMP1       ADD 1 TO FILE COUNT
         MVC   PRFILE,COMMED2      INITIALIZE WITH EDIT WORD
         ED    PRFILE,FILECT       EDIT FILE NUMBER
         MVC   DATA(HEADMSGL),HEADMSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         OI    TAPEFLAG,$TAPEFHD   SET HEADING FLAG
TAPE0030 DS    0H
         NI    TAPEFLAG,255-$TAPETM    TURN OFF TAPE MARK FLAG
         CLC   EXCPLEN,H80         WAS LENGTH 80?
         BNE   TAPE0040            NO
         TM    TAPEFLAG,$TAPEDTA   DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         ICM   R1,7,READCCW+1      I/O AREA USED
         CLC   TAPEVOL1,0(R1)      'VOL1' RECORD??
         BE    VOL10000            YES
         CLC   TAPEHDR1,0(R1)      'HDR1' RECORD??
         BE    HDR10000            YES
         CLC   TAPEEOV1,0(R1)      'EOV1' RECORD??
         BE    EOV10000            YES
         CLC   TAPEEOF1,0(R1)      'EOF1' RECORD??
         BE    EOF10000            YES
         CLC   TAPEHDR2,0(R1)      'HDR2' RECORD??
         BE    HDR20000            YES
         CLC   TAPEEOF2,0(R1)      'EOF2' RECORD??
         BE    EOF20000            YES
         CLC   TAPEEOV2,0(R1)      'EOV2' RECORD??
         BE    EOV20000            YES
TAPE0040 DS    0H
         DITTRACE ID=DATA          TRACE DATA ENCOUNTERED
         TM    TAPEFLAG,$TAPEDTA   IS TAPE DATA FLAG ON??
         BO    TAPE0020            YES... READ ANOTHER BLOCK
         OI    TAPEFLAG,$TAPEDTA   SET DATA FLAG
         MVC   DATA(L'DATAMSG),DATAMSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK FROM THE TAPE
*---------------------------------------------------------------------*
*                                                                     *
*                    TAPE MARK HAS JUST BEEN READ                     *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0200 DS    0H
         DITTRACE ID=TAPEMARK,     TRACE TAPE MARK ENCOUNTERED         +
               DATA1=TAPEFLAG      .. CAPTURE FLAG SETTING
         TM    TAPEFLAG,$TAPETM    TAPE MARK FLAG ALREADY ON??
         BO    TAPE0210            YES ... TWO IN A ROW IS EOT
         OI    TAPEFLAG,$TAPETM    SET TAPE MARK FLAG
         NI    TAPEFLAG,255-$TAPEDTA   RESET DATA FLAG
         NI    TAPEFLAG,255-$TAPEFHD   RESET FILE HEADING FLAG
         B     TAPE0020            READ ANOTHER BLOCK
TAPE0210 DS    0H
         TM    TAPEFLAG,$TAPEEOT   LOGCIAL END OF TAPE ALREADY REACHED?
         BO    TAPE0220            DON'T RESEND LOGICAL EOT MESSAGE
         OI    TAPEFLAG,$TAPEEOT   SET LOGICAL EOT FLAG
         DITTRACE ID=TAPEPEOT      END OF TAPE
         MVC   DATA(L'LEOTMSG),LEOTMSG
         BAL   R9,ADD0000          LINK TO PRINT MODULE
TAPE0220 DS    0H
         CLC   FULL,COMMOPT        FULL OPTION?
         BE    TAPE0020            YES
         CLC   FULLTAPE,COMMOPT    FULLTAPE OPTION?
         BE    TAPE0020            YES
         CLC   ALL,COMMOPT         ALL OPTION?
         BE    TAPE0020            YES
         B     TAPE0240            STOP ON LOGICAL-END-OF-TAPE
TAPE0230 DS    0H
         DITTRACE ID=TAPEPEOT      PHYSICAL EOT REACHED
         MVC   DATA(L'PEOTMSG),PEOTMSG
         BAL   R9,ADD0000          LINK TO PRINT MODULE
TAPE0240 DS    0H
         OI    COMMFLAG,$COMMEOF   SET EOF FLAG
         DITTRACE ID=TBTOP         TRACE
         LA    R1,TOPPARM          PERFORM 'TBTOP'
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBTOP SUCCESSFUL?
         BNZ   ERR0030             NO
         DITTRACE ID=TBDISPL       TRACE
         LA    R1,COMMPANL         PANEL NAME ADDRESS
         ST    R1,DISPPANL         SET PANEL NAME ADDRESS
         OI    DISPPANL,X'80'      SET END OF LIST
         LA    R1,DISPPARM         DISPLAY TABLE PARAMETERS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             RC=ZERO?
         BZ    EXIT0000            YES
         CH    R15,H8              END?
         BE    EXIT0000            YES
         CVD   R15,COMMDWRD        CONVERT TO DECIMAL
         MVC   MSG004RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG004RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG004           MESSAGE TEXT
         LA    R0,MSG004L          MESSAGE LENGTH
         B     ERR0090             ISSUE MESSAGE
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                    ERROR ROUTINES                                   *
*                                                                     *
*---------------------------------------------------------------------*
TAPE0400 DS    0H
         LA    R3,DYNIOB           IOB USED FOR I/O
         DITTRACE ID=IOERR         TRACE I/O ERRORS
         DC    H'0'                ABEND
TAPE0410 DS    0H
         DITTRACE ID=ZERORECL      TRACE LRECL = ZERO
         DC    H'0'                ABEND
*--------------------------------------------------------------------*
*                                                                    *
*              VOL1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
VOL10000 DS    0H
         USING VOL1,R1             DEFINE DSECT BASE
         DITTRACE ID=VOL1          TRACE VOL1
         MVC   PRVOL,VOL1VOL       MOVE VOLUME SERIAL NUMBER
         MVC   DATA(VOL1MSGL),VOL1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              HDR1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
HDR10000 DS    0H
         USING HDR1,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR1          TRACE HDR1
         MVC   PRDSN,HDR1DSN       DATASET NAME
         MVC   PRVSEQ,HDR1VSEQ     VOLUME SEQUENCE
         MVC   PRDSEQ,HDR1DSEQ     DATASET SEQUENCE
         MVC   PRCDTE,HDR1CDTE     CREATE DATE
         MVC   PREXPD,HDR1EXPD     EXPIRATION DATE
         MVC   DATA(HDR1MSGL),HDR1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              EOF1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOF10000 DS    0H
         DITTRACE ID=EOF1          TRACE EOF1
         MVC   PRBLKC,HDR1BLKC     BLOCK COUNT
         MVC   DATA(EOF1MSGL),EOF1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              EOV1 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOV10000 DS    0H
         DITTRACE ID=EOV1          TRACE EOV1
         MVC   DATA(L'EOV1MSG),EOV1MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              HDR2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
HDR20000 DS    0H
         USING HDR2,R1             DEFINE DSECT BASE
         DITTRACE ID=HDR2          TRACE HDR2
         MVC   PRRF,HDR2RF         RECORD FORMAT
         MVC   PRBLSZ,HDR2BLSZ     BLOCK SIZE
         MVC   PRRSZ,HDR2RSZ       RECORD SIZE
         MVC   PRDEN,HDR2DEN       TAPE DENSITY
         MVC   PRJOB,HDR2JOB       JOB/JOB STEP
         MVC   DATA(HDR2MSGL),HDR2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              EOF2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOF20000 DS    0H
         DITTRACE ID=EOF2          TRACE EOF2
         MVC   DATA(L'EOF2MSG),EOF2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              EOV2 RECORD PROCESSING                                *
*                                                                    *
*--------------------------------------------------------------------*
EOV20000 DS    0H
         DITTRACE ID=EOV2          TRACE EOV2
         MVC   DATA(L'EOV2MSG),EOV2MSG
         BAL   R9,ADD0000          ADD A LINE TO TABLE
         B     TAPE0020            READ ANOTHER BLOCK
*--------------------------------------------------------------------*
*                                                                    *
*              ADD ROWS TO SPF TABLE                                 *
*                                                                    *
*--------------------------------------------------------------------*
ADD0000  DS    0H
         DITTRACE ID=TBADD,        TRACE TABLE ADD                     +
               DATA1=DATA,         .. FIRST 8 BYTES                    +
               DATA2=DATA+8        .. NEXT 8 BYTES
         LA    R1,ADDPARM          'TBADD' PARAMETERS
         L     R15,AISP            SPF INTERFACE ENTRY POINT
         BALR  R14,R15             LINK TO SPF
         LTR   R15,R15             TBADD SUCCESSFUL?
         BNZ   ERR0020             NO
         MVC   DATA,COMMBLKS       CLEAR VARIABLE 'DATA'
         BR    R9                  RETURN
*--------------------------------------------------------------------*
*                                                                    *
*              SPF ERRORS                                            *
*                                                                    *
*--------------------------------------------------------------------*
ERR0010  DS    0H
         DITTRACE ID=CREFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG001RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG001RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG001           MESSAGE TEXT ADDRESS
         LA    R0,MSG001L          MESSAGE TEXT LENGTH
         TPUT  (R1),(R0)           SEND MESSAGE
         B     EXIT0000            AND EXIT
ERR0020  DS    0H
         DITTRACE ID=ADDFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG002RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG002RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG002           MESSAGE TEXT ADDRESS
         LA    R0,MSG002L          MESSAGE TEXT LENGTH
         B     ERR0090
ERR0030  DS    0H
         DITTRACE ID=TOPFAIL,      TBCREATE FAILED                     +
               RDATA1=R15          .. CAPTURE RETURN CODE
         CVD   R15,COMMDWRD        CONVERT RETURN CODE TO DECIMAL
         MVC   MSG003RC,RCEDWD     INITIALIZE WITH EDIT WORD
         ED    MSG003RC,COMMDWRD+6 EDIT RETURN CODE
         LA    R1,MSG003           MESSAGE TEXT ADDRESS
         LA    R0,MSG003L          MESSAGE TEXT LENGTH
         B     ERR0090
ERR0090  DS    0H
         TPUT  (R1),(R0)           SEND MESSAGE
         B     EXIT0000            AND EXIT
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
EXIT0000 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
REWCCW   CCW   X'07',*,SILI,1      REWIND CCW
READCCW  CCW   X'02',*,SILI,65535  READ FORWARD CCW
TMAPSAVE DC    18F'0'              REGISTER SAVE AREA
TBLEPARM DC    A(TBCREATE)         VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
         DC    A(0)                NO 'KEY' NAMES
         DC    A(NDATA)            VARIABLES LIST
         DC    A(NOWRITE)          IN CORE ONLY
         DC    A(REPLACE+X'80000000')
ADDPARM  DC    A(TBADD)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
TOPPARM  DC    A(TBTOP)            VERB ADDRESS
         DC    A(NTABLE+X'80000000')
DISPPARM DC    A(TBDISPL)          VERB ADDRESS
         DC    A(NTABLE)           TABLE NAME ADDRESS
DISPPANL DC    A(0)                PANEL NAME ADDRESS
H4       DC    H'4'                CONSTANT
H8       DC    H'8'                CONSTANT
H80      DC    H'80'               CONSTANT
TAPEFLAG DC    X'00'               SWITCHES/FLAGS
$TAPETM  EQU   X'80'               .. TAPE MARK JUST READ
$TAPEDTA EQU   X'40'               .. DATA FILE FOUND
$TAPEFHD EQU   X'20'               .. FILE HEADING PRINTED
$TAPEEOT EQU   X'10'               .. LOGICAL EOT REACHED
FILECT   DC    PL3'0'              FILES COUNTER
FULL     DC    CL8'FULL'           CONSTANT
FULLTAPE DC    CL8'FULLTAPE'       CONSTANT
ALL      DC    CL8'ALL'            CONSTANT
TBCREATE DC    CL8'TBCREATE'
TBADD    DC    CL8'TBADD'
TBTOP    DC    CL8'TBTOP'
TBDISPL  DC    CL8'TBDISPL'
NTABLE   DC    CL8'TMAPDATA'
NDATA    DC    C'(DATA)'
NOWRITE  DC    CL8'NOWRITE'
REPLACE  DC    CL8'REPLACE'
PANLNAME DC    CL8'DITTTSO2'
TAPEVOL1 DC    C'VOL1'             CONSTANT
TAPEHDR1 DC    C'HDR1'             CONSTANT
TAPEHDR2 DC    C'HDR2'             CONSTANT
TAPEEOF1 DC    C'EOF1'             CONSTANT
TAPEEOF2 DC    C'EOF2'             CONSTANT
TAPEEOV1 DC    C'EOV1'             CONSTANT
TAPEEOV2 DC    C'EOV2'             CONSTANT
MSG001   DC    C'TBCREATE FAILED, RETURN CODE='
MSG001RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG001L  EQU   *-MSG001
MSG002   DC    C'TBADD FAILED, RETURN CODE='
MSG002RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG002L  EQU   *-MSG002
MSG003   DC    C'TBTOP FAILED, RETURN CODE='
MSG003RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG003L  EQU   *-MSG003
MSG004   DC    C'TBDISPL FAILED, RETURN CODE='
MSG004RC DC    CL4' '
         DC    C' (DECIMAL)'
MSG004L  EQU   *-MSG004
RCEDWD   DC    X'40202120'
HEADMSG  DS    0C
         DC    C'* * * * * * * * * * * * * * PHYSICAL FILE #'
PRFILE   DC    CL6' '
         DC    C' * * * * * * * * * * * * * * *'
HEADMSGL EQU   *-HEADMSG
LEOTMSG  DC    C'=============================== LOGICAL END OF TAPE ==+
               ====================='
PEOTMSG  DC    C'=============================== PHYSICAL END OF TAPE =+
               ====================='
DATAMSG  DC    C'DATA'
VOL1MSG  DS    0C
         DC    C'VOL1 VOLSER '
PRVOL    DC    CL6' '
VOL1MSGL EQU   *-VOL1MSG
HDR1MSG  DS    0C
         DC    C'HDR1 DSN='
PRDSN    DC    CL17' '
         DC    C' VOLSEQ='
PRVSEQ   DC    CL4' '
         DC    C' D/S SEQ='
PRDSEQ   DC    CL4' '
         DC    C' CREATE='
PRCDTE   DC    CL6' '
         DC    C' EXPIRE='
PREXPD   DC    CL6' '
HDR1MSGL EQU   *-HDR1MSG
HDR2MSG  DS    0C
         DC    C'HDR2 RECFM='
PRRF     DC    C' '
         DC    C' BLKSZ='
PRBLSZ   DC    CL5' '
         DC    C' LRECL='
PRRSZ    DC    CL5' '
         DC    C' DENSITY='
PRDEN    DC    C' '
         DC    C' JOB/JOBSTEP='
PRJOB    DC    CL17' '
HDR2MSGL EQU   *-HDR2MSG
EOF1MSG  DS    0C
         DC    C'EOF1 BLOCK COUNT='
PRBLKC   DC    CL6' '
EOF1MSGL EQU   *-EOF1MSG
EOF2MSG  DC    C'EOF2'
EOV1MSG  DC    C'EOV1'
EOV2MSG  DC    C'EOV2'
*--------------------------------------------------------------------*
*                                                                    *
*              EXCP INTEFACE BLOCK                                   *
*                                                                    *
*--------------------------------------------------------------------*
EXCPBLOK EXCPBLOK
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              TAPE LABEL DSECTS                                     *
*                                                                    *
*--------------------------------------------------------------------*
         COPY  DITTTLBL
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
SILI     EQU   X'20'
         ABCODES
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU  *-IHADCB
         END  DITTTSO2
./ ADD NAME=DITTTSO3
*--------------------------------------------------------------------*
*                                                                    *
*              TSO INQUIRE FUNCTION                                  *
*                                                                    *
*--------------------------------------------------------------------*
DITTTSO3 DITTPRFX INQSAVE,'TSO MODE INQUIRE FUNCTION'
         USING DITTCOMM,R11        SPECIFY BASE
         DITTRACE ID=ENTRY         TRACE ENTRY
*--------------------------------------------------------------------*
*                                                                    *
*              MODULE EXIT POINT                                     *
*                                                                    *
*--------------------------------------------------------------------*
EXIT0000 DS    0H
         DITTRACE ID=EXIT          TRACE EXIT
         L     R13,4(R13)          RESTORE REGISTER 13
         LM    R14,R12,12(R13)     RESTORE ALL OTHER REGISTERS
         SR    R15,R15             GIVE GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              WORKAREAS/CONSTANTS                                   *
*                                                                    *
*--------------------------------------------------------------------*
INQSAVE  DC    18F'0'              REGISTER SAVE AREA
*--------------------------------------------------------------------*
*                                                                    *
*              REGISTER EQUATES                                      *
*                                                                    *
*--------------------------------------------------------------------*
         COPY REGEQU
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              COMMON MODULE DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DITTCOMM DITTCOMM TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              DYNAMIC BLOCK DSECT                                   *
*                                                                    *
*--------------------------------------------------------------------*
DYNBLOK  DYNBLOK TYPE=DSECT
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*              MVS IOB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         IEZIOB
IOBLEN   EQU  *-IOB
*--------------------------------------------------------------------*
*                                                                    *
*              MVS DCB DSECT                                         *
*                                                                    *
*--------------------------------------------------------------------*
         DCBD DSORG=PS
DYNDCBL  EQU  *-IHADCB
         END  DITTTSO3
./ ADD NAME=DITTXXXX
DITTXXXX CSECT
         DC    CL8'DITTXXXX'
         DC    CL8'&SYSDATE'
         DC    CL6'&SYSTIME'
         DC    C'END OF DITTO LOAD MODULE'
         END   DITTXXXX
./ ADD NAME=JCL
//........ JOB .........
//DITTO    EXEC PGM=DITTO
//STEPLIB  DD DSN=...LOADLIB...,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD SYSOUT=*
//DITTDUMP DD SYSOUT=*
//DITTRACE DD SYSOUT=*
//SYSIN    DD *
$$DITTO XXX
/*
//
./ ADD NAME=PROC
//DITTO    EXEC PGM=DITTO
//STEPLIB  DD DSN=...LOADLIB...,DISP=SHR
//ABNLIGNR DD DUMMY
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD SYSOUT=*
//DITTRACE DD SYSOUT=*
//DITTDUMP DD SYSOUT=*
./ ADD NAME=TSODITTO
PROC 0 DEBUG
  ISPEXEC LIBDEF ISPPLIB DATASET +
    ID('...ISPPLIB...')
  ISPEXEC  CONTROL ERRORS RETURN
  ISPEXEC  SELECT PGM(DITTO) NEWAPPL(DITT) PASSLIB
EXIT CODE(4)
