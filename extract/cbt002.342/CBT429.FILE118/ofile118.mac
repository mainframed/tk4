*          DATA SET CBT1277    AT LEVEL 006 AS OF 08/28/83
PUTXREF  TITLE 'PROGRAM TO CROSS REFERENCE A PUT TAPE BY FMID'
***********************************************************************
*                                                                     *
*              PUTXREF - VERSION 3.0                                  *
*                                                                     *
*        JERRY LAWSON  860-547-5000 EXT 2960                          *
*        HARTFORD INSURANCE GROUP,                                    *
*        690 ASYLUM AVE.                                              *
*        HARTFORD, CT.  06115                                         *
*                                                                     *
*        FUNCTION:                                                    *
*        THIS PROGRAM TAKES THE PUT PTF FILE (FILE 1 FROM A           *
*        STANDARD IBM PUT TAPE), AND PRODUCES A CROSS                 *
*        REFERENCE OF THE PTF'S INCLUDED, SORTED BY FMID.             *
*        IT CAN TAKE CONCATENATED INPUT, BUT THE TAPE IDENITY         *
*        ON WHICH THE PTF IS FOUND WILL BE LOST. THE PROGRAM          *
*        SHOULD BE USEFUL TO QUICKLY IDENTIFY WHAT PTF'S              *
*        ARE ON A GIVEN TAPE, WHAT FMID'S HAVE MAINTENANCE ON         *
*        A TAPE, AND TO DOUBLE CHECK THE STERLING FORREST             *
*        CROSS REFERENCE (IT LIES ONCE IN A WHILE.)                   *
*                                                                     *
*        DEPENDANCIES:                                                *
*        1.    THIS PROGRAM HAS AN INVOKED INCORE SORT.               *
*              IT LINKS TO THE ENTRY POINT OF "SORT".                 *
*              CHANGE THE LINK MACRO IF YOU DO NOT SUPPORT            *
*              THAT NAME OR ALIAS. YOU WILL NEED ABOUT                *
*              300K FOR THE WHOLE PROGRAM AND SORT.                   *
*        2.    TITLES.  THIS FILE (FOR FMID TITLES) IS                *
*              OPTIONAL. IF NOT INCLUDED, YOU WILL GET A              *
*              WARNING MESSAGE ON YOUR SYSOUT, AND NO TITLES.         *
*              I HAVE INCLUDED THE FILE IN THE JCL, I                 *
*              CREATED THE FILE BY DOING THE FOLLOWING:               *
*              A. ALLOCATE A DATA SET ON TSO  - NORMAL STUFF -        *
*                  RECFM=FB,LRECL=80                                  *
*              B.  ASSIGN THAT DATA SET TO DD NAME OF OZPRINT         *
*              C.  EXECUTE YOUR INFO - SYS CLIST OR COMMAND           *
*              D.  ISSUE A 'DEF A' COMMAND TO INFO (SEARCH            *
*                  ALL FILES)                                         *
*              E.  DO A KEY WORD SEARCH AS FOLLOWS -                  *
*                  KWS FMID XREF      I GOT ABOUT 22 HITS,            *
*                  BUT THE FIRST ONE HAD A TITLE OF                   *
*                  'THIS IS A CROSS REFERENCE OF MVS FMIDS',          *
*                  OR SOMETHING SIMILAR. BE SURE YOU                  *
*                  HAVE THE LATEST ONE                                *
*              F.  SELECT THE CORRECT ONE, THE ISSUE A                *
*                  'PRINT UP' COMMAND. THE OUTPUT WILL GO             *
*                  TO THE DATA SET ALLOCATED TO THE OZPRINT           *
*                  DDNAME                                             *
*              G.  END YOUR INFO SESSION                              *
*              H.  EDIT YOUR PRINT FROM THE INFO SESSION              *
*                  YOU WANT TO DELETE ALL LINES THAT DO               *
*                  NOT HAVE AN FMID IN COLUMN 3. NOTE THAT DELETED
*                  FMIDS HAVE A * IN COLUMN 3, FOR BETTER             *
*                  DOCUMENTATION I DELETE THE STAR ANDMOVE THE        *
*                  FMID BACK TO 3, BUT THIS IS                        *
*                  OPTIONAL.                                          *
*              I.  AFTER YOU HAVE SAVED THE DATA SET,                 *
*                  SORT IT ON COLUMN 3 FOR A LENGTH OF 7.             *
*              J.  THE TITLE LIST YOU HAVE JUST CREATED               *
*                  CAN BE EITHER INCLUDED IN THE JCL STREAM           *
*                  OR KEPT IN A SEPERATE DATA SET, AND POINTED        *
*                  TO BY THE JCL.                                     *
*        3.    REGS MACRO. THE STANDARD REGISTER EQUATES              *
*              SUCH AS IN R1      EQU    1      ETC.                  *
*              NOTE - REMOVED ON 3/10/82                              *
*                                                                     *
*        CHANGES FOR RELEASE 3.0                                      *
*        1.    CHANGED INPUT LOCATION OF TITLES FROM COLUMN 2 TO      *
*              COLUMN 3. THIS ALLOWS USE OF THE OUTPUT OF THE         *
*              OZPRINT DD STATEMENT WITHOUT HAVING TO DELETE          *
*              THE CARRIAGE CONTROL INFO COMPLETELY. SEE THE INFO IN  *
*              2. ABOVE FOR DETAILS ON HOW TO CREATE THE TITLE        *
*              FILE.                                                  *
*        2.    EXTENDED SIZE OF THE TITLE FIELD TO INCLUDE            *
*              THE COMPONENT ID NOW THAT IBM HAS                      *
*              INCLUDED IT IN THE FMID XREF.                          *
*                                                                     *
*        CHANGES FOR RELEASE 2.0:                                     *
*        1. ADDED BLOCKSIZES TO DCB MACROS TO CORRECT DEPENDENCY      *
*           ON LOCAL MODS                                             *
*        2. CORRECTED SORT FIELDS STATEMENT IN PROGRAM. ONLY USED IF  *
*           IBM SORT IS INVOKED. NOTE THAT PARM ERROR SEEMS TO BE     *
*           A STANDARD FEATURE WITH THIS PROGRAM - I CAN FIND NO ERROR*
*        3. SYSOUT JCL DD STATEMENT ADDED FOR IBM SORTS               *
*                                                                     *
*        MACROS.                                                      *
*        SAVE      OPEN      CLOSE      GET                           *
*        PUT       LINK      WTO        ABEND (U128 - BAD SORT)       *
*        DCB
*                                                                     *
*        REGISTER ASSIGNMENTS - NORMAL LINKAGE CONVENTIONS            *
*              REG 12              BASE REGISTER                      *
*              REG 2               BAL REGISTER                       *
*              REG 3               BCT REGISTER - PHASE 1             *
*              REG 4               POINTER TO PTF INPUT RECORD        *
*              REG 5               OUTPUT POSITION POINTER - PHASE 2  *
*              REG 6               BCT REGISTER - PHASE 2             *
*              REG 7               RETURN ADDRESS FOR EODAD (TITLES)  *
***********************************************************************
         EJECT
PUTXREF  CSECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING PUTXREF,R12         REG 12 IS BASE
         LA    R15,SAVE
         ST    R15,8(R13)
         ST    R13,SAVE+4
         LR    R13,R15
         OPEN  (SYSIN,INPUT)
         OPEN  (SORT,OUTPUT)
         SPACE 2
*---------------------------------------------------------------------*
*        THIS SECTION SEARCHES THE INPUT FOR ++PTF AND ++VER          *
*        STATEMENTS ON THE PUT TAPE. ALL OTHERS ARE REJECTED,         *
*        BUT A BAD PTF MIGHT CAUSE EXCESS SCANNING TO BE DONE.        *
*---------------------------------------------------------------------*
PX010    EQU   *
         BAL   R2,PXGET            LINK TO COMMON READ ROUTINE
         CLC   0(2,R4),=C'++'      IS THIS AN SMP CONTROL CARD?
         BNE   PX010               NO - BACK FOR ANOTHER
         LA    R4,2(R4)            YES - POINT PAST THE ++
         SH    R3,=H'2'            DECREMENT LENGTH FOR LOOP
PX015    EQU   *
         CLI   0(R4),C' '          ANYTHING HERE YET?
         BNE   PX020               YES - GO CHECK IT OUT
         LA    R4,1(R4)            NO - THEN POINT TO NEXT POSITION
         BCT   R3,PX015            LOOP DE LOOP
         BAL   R2,PXGET            WOOPS - OUT OF RECORD
         B     PX015               BACK AGAIN WITH ANOTHER RECORD
PX020    EQU   *
         CLC   0(3,R4),=C'PTF'     IS THIS A ++PTF CARD?
         BE    PX100               YES - GO GETPTF NUMBER
         CLC   0(3,R4),=C'VER'     NO - THEN IS IT A ++VER?
         BE    PX200               YES - THEN GO FIND THE FMID
         B     PX010               NO - THEN THIS IS NG - START OVER.
         EJECT
*---------------------------------------------------------------------*
*        THIS SECTION GETS THE PTF NUMBER FROM THE SMP CONTROL        *
*        CARDS. IT ASSUMES THIS TO BE THE FIRST NON-BLANK             *
*        CHARACTER AFTER THE FIRST LEFT PAREN AFTER                   *
*        THE KEYWORD 'PTF'                                            *
*---------------------------------------------------------------------*
PX100    EQU   *
         LA    R4,3(R4)            BUMP PAST THE PTF KEYWORD
         SH    R3,=H'3'            DECREMENT LOOP CONTROL
         C     R3,=F'0'            OUT OF RECORD?
         BH    PX110               NO - ON WITH THE SHOW
         BAL   R2,PXGET            YES - LINK TO COMMON GET ROUTINE
PX110    EQU   *
         CLI   0(R4),C'('          THIS A LEFT PAREN?
         BE    PX150               YES - THEN GO SAVE PTF NUMBER
         LA    R4,1(R4)            NO - POINT TO NEXT POSITION
         BCT   R3,PX110            LOOP AGAIN
         BAL   R2,PXGET            OUT OF RECORD AGAIN
         B     PX110
PX150    EQU   *
         LA    R4,1(R4)            POINT PAST LEFT PAREN
         BCTR  R3,0                DECREMENT FOR THE PAREN
         CLI   0(R4),C' '          ANYTHING HERE?
         BNE   PX160               YES - THEN GO SAVE IT
         LA    R4,1(R4)            NO - POINT TO NEXT
         BCT   R3,PX150             AND LOOP (GETTING DIZZY YET?)
         BAL   R2,PXGET            OFF TO THE COMMONS AGAIN
         B     PX150
PX160    EQU   *
         MVC   OUTPTF(7),0(R4)     SAVE PTF NUMBER
         B     PX010               BACK TO FIND THE FMID
         EJECT
*---------------------------------------------------------------------*
*        THIS SECTION GETS THE FMID FROM THE ++VER STATEMENT. IT      *
*        ASSUMES THE FMID TO BE THE FIRST NON-BLANK CHARACTER         *
*        AFTER THE FIRST LEFT PAREN AFTER THE 'FMID' KEYWORD.         *
*        WHEN THIS IS LOCATED, A RECORTED IS WRITTEN TO THE SORT FILE.*
*---------------------------------------------------------------------*
PX200    EQU   *
         LA    R4,3(R4)            POINT PAST THE VER KEYWORD
         SH    R3,=H'3'            AND DECREMENT THE LOOP CONTROL
         C     R3,=F'0'            OUT OF RECORD?
         BH    PX210               NO - ON WITH THE SHOW
         BAL   R2,PXGET            YES - OFF AND GET ANOTHER
PX210    EQU   *
         CLC   0(4,R4),=C'FMID'    IS THIS THE FMID?
         BE    PX220               YES - THEN GO FIND THE ACTUAL FMID
         LA    R4,1(R4)            NO - POINT TO NEXT LITTLE DEVIL
         BCT   R3,PX210            WE'LL GET HIM YET
         BAL   R2,PXGET            WOOPS OUT OF DATA
         B     PX210               BACK WITH A NEW RECORD
PX220    EQU   *
         LA    R4,4(R4)            POINT PAST THE KEYWORD
         SH    R3,=H'4'            DECREMENT THE LOOP CONTROL
         C     R3,=F'0'            OUT OF DATA?
         BH    PX230               NO - THEN FIND THE LITTLE DEVIL
         BAL   R2,PXGET            OUT OF DATA AGAIN
PX230    EQU   *
         CLI   0(R4),C'('          LEFT PAREN YET?
         BE    PX240               YES - ALMOST THERE
         LA    R4,1(R4)            POINT TO NEXT PLACE
         BCT   R3,PX230            LOOP DE LOOP
         BAL   R2,PXGET            WOOPS - THE SAFTEY VALVE AGAIN
         B     PX230               KEEP ON PLUGGIN.
PX240    EQU   *
         LA    R4,1(R4)            POINT PAST THE PAREN
         BCTR  R3,0                DON'T FORGET THE LOOP CONTROL
         CLI   0(R4),C' '          THEY DIDN'T SLIP IN ANOTHER BLANK?
         BNE   PX250               NOPE - THEN WE GOT IT ALL!!!!
         LA    R4,1(R4)            YES - THOSE LITTLE BUGGERS
         BCT   R3,PX240            LOOP ONE MORE TIME
         BAL   R2,PXGET            COMMONER AND COMMONER
         B     PX240
PX250    EQU   *
         MVC   OUTFMID,0(R4)       SAVE THE FMID NUMBER
         PUT   SORT,OUTPTF         WRITE THE RECORD
         B     PX010               BACK AND START AGAIN
               EJECT
*---------------------------------------------------------------------*
*        END OF DATA ROUTINE FOR THE PTF INPUT FILE  - DDNAME         *
*        IS INPUT.                                                    *
*---------------------------------------------------------------------*
EOF      EQU   *
         CLOSE SYSIN
         CLOSE SORT
         SPACE 3
*---------------------------------------------------------------------*
*        LINK TO SORT (THIS WAS DEVELOPED USING SYNCSORT)             *
*---------------------------------------------------------------------*
         LA    R1,SORTPARM         GET ADDRESS OF SORT PARMS
         LINK  EP=SORT             CALL THE SORT UTILITY
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   SORTERR             NO GOOD - ABEND
         EJECT
*---------------------------------------------------------------------*
*        THIS IS THE BEGINNING OF PHASE 2 - SETS UP THE               *
*        FILES, VERIFIES THE EXSISTANCE OF THE TITLES FILE, AND       *
*        WRITES THE FIRST SET OF HEADERS                              *
*---------------------------------------------------------------------*
         OPEN  (SORT,INPUT)
         OPEN  (TITLE,INPUT)
         OPEN  (PRINT,OUTPUT)
         LA    R5,TITLE            GET DCB ADDRESS
         TM    48(R5),X'10'        WAS OPEN SUCCESSFUL?
         BO    PX490               YES - BRANCH AROUND BAD OPEN
         OI    FLAG,NOTITLE        SHOW NOT TO READ THIS FILE
         WTO   'TITLE FILE OPEN UNSUCCESSFUL',ROUTCDE=(11),            X
               DESC=(7)
PX490    EQU   *
         BAL   R2,PXPUT            LINK TO HEADER PUT ROUTINE
         EJECT
*---------------------------------------------------------------------*
*        THIS SECTION FOOLS AROUND WITH THE SPECIAL HANDLING FOR      *
*        THE 1ST RECORD.                                              *
*---------------------------------------------------------------------*
         GET   SORT,OUTPTF         1ST READ ONLY
         MVC   HOLD,OUTFMID        SAVE FMID FOR COMPARISON
         MVC   OUTAREA+1(7),OUTFMID SETUP 1ST LINE
         LA    R5,OUTAREA+11       POINT TO FIRST PTF AREA
         LA    R6,12               SET UP LOOP FOR BCT
         TM    FLAG,NOTITLE        CAN WE READ THE TITLE FILE?
         BO    PX510               NO - WOOPS - FORGOT TO SAY
*                                         MOTHER MAY I
         LA    R7,PX499            SAVE RETURN POINT FOR EODAD
PX495    EQU   *
         GET   TITLE,INAREA        GET A TITLE RECORD
         CLC   INAREA+2(7),HOLD    DO FMID'S AGREE?
         BL    PX495               NO - LOWER, GO READ ANOTHER
         BH    PX499               NO - HIGHER, ERGO NO BRANCH, SKIP
         MVC   OUTAREA+21(34),INAREA+11 EQUAL - GET TITLE
PX499    EQU   *
         PUT   PRINT,LINE          WRITE HEADER
         MVI   OUTAREA+0,C' '      CLEAR LINE
         MVC   OUTAREA+1(131),OUTAREA+0
         AP    LINECNT,ONE         UPDATE LINE COUNT
*                       NOTE - PAGE OVERFLOW NOT POSSIBLE HERE
         B     PX510               BRANCH AROUND NORMAL READ
         EJECT
*---------------------------------------------------------------------*
*        THIS IS THE MAINLINE ROUTINE TO PRINT OUT THE CROSS          *
*        REFERENCE. IT READS THE SORTED INPUT, CHECKS TO SEE          *
*        IF IT IS THE SAME AS THE PREVIOUS FMID, AND BRANCHES         *
*        ACCORDINGLY.                                                 *
*---------------------------------------------------------------------*
PX500    EQU   *
         GET   SORT,OUTPTF         NORMAL READ
PX510    EQU   *
         CLC   HOLD,OUTFMID        SAME AS LAST RECORD?
         BNE   PX570               NO - GO WRITE PARTIAL LINE
PX520    EQU   *
         MVC   0(7,R5),OUTPTF      MOVE IN PTF NUMBER
         LA    R5,10(R5)           BUMP TO NEXT PRINT POSITION
         BCT   R6,PX500            LOOP IN HERE TOO.
         PUT   PRINT,LINE          END OF LOOP, WRITE LINE
         MVI   OUTAREA+0,C' '      BLANK OUT THE LINE
         MVC   OUTAREA+1(131),OUTAREA CLEAR DATA LINE
         LA    R5,OUTAREA+11       POINT BACK TO BEGINNING
         LA    R6,12               RESET LOOP CONTROL
         AP    LINECNT,ONE         ADD ONE TO CURRENT NUMBER OF LINES
         CP    LINECNT,=P'60'      ENOUGH LINES FOR BREAK?
         BL    PX500               NO - BACK TO TOP
         BAL   R2,PXPUT            YES - LINK TO HEADER PUT ROUTINE
         B     PX500               BACK TO TOP
         SPACE 2
PX570    EQU   *
         PUT   PRINT,LINE          WRITE SHORT LINE
         PUT   PRINT,BLANK         WRITE SPACER
         AP    LINECNT,THREE       ADD IN LINE COUNT
         CP    LINECNT,=P'59'      ALMOST AT END OF PAGE?
         BL    PX572               NO - BRANCH AROUND HDR ROUTINE
         BAL   R2,PXPUT            OFF TO SEE THE WIZARD
PX572    EQU   *
         LA    R5,11(R5)           RESET POINTER
         LA    R6,12               RESET BCT LOOP
         MVI   OUTAREA+0,C' '      CLEAR OUTPUT
         MVC   OUTAREA+1(131),OUTAREA
         MVC   OUTAREA+1(7),OUTFMID SETUP 1ST LINE
         MVC   HOLD,OUTFMID        SAVE NEW FMID
         TM    FLAG,NOTITLE        CAN WE READ THE TITLE FILE?
         BO    PX580               NO - FORGET THIS NONSENSE
         LA    R7,PX580            YES - GET RETURN ADDRESS FOR EODAD
         CLC   INAREA+2(7),HOLD    CURRENT ONE RIGHT?
         BL    PX575               NO - LOW, GO GET NEXT ONE
         BH    PX580               NO - HIGH, STILL NOT RIGHT
         MVC   OUTAREA+21(34),INAREA+11   YES - THIS IS RIGHT ONE
         B     PX580               GO DIRECTLY TO PUT
PX575    EQU   *
         GET   TITLE,INAREA        READ THE FILE
         CLC   INAREA+2(7),HOLD    THIS THE RIGHT GUY?
         BL    PX575               NO - LOW, GO BACK AND READ AGAIN
         BH    PX580               NO - HIGH, NONE FOR THIS ONEADDRESS
         MVC   OUTAREA+21(34),INAREA+11  MOVE IN TITLE
PX580    EQU   *
         PUT   PRINT,LINE          WITE DATA
         MVI   OUTAREA+0,C' '      CLEAR OUTPUT
         MVC   OUTAREA+1(131),OUTAREA
         LA    R5,OUTAREA+11       POINT BACK TO BEGINNING
         LA    R6,12               RESET LOOP CONTROL
         AP    LINECNT,ONE         ADD TO CURRENT LINE COUNTER
         CP    LINECNT,=P'60'      ENOUGH FOR A NEW PAGE?
         BL    PX520               NO - BACK TO PROCESS NEW FMID
         BAL   R2,PXPUT            YES - LINK TO HEADER PUT ROUTINE
         B     PX520               BACK AND CONTINUE ON
         EJECT
*---------------------------------------------------------------------*
*        EODAD ROUTINE FOR THE TITLE FILE. SINCE THIS CAN BE ENTERED  *
*        FROM TWO PLACES, EACH SETS A RETURN ADDRESS, SO THAT         *
*        PROCESSING CAN CONTINUE AFTER EOF IS REACHED.                *
*---------------------------------------------------------------------*
PX590    EQU   *
         OI    FLAG,NOTITLE        SHOW NO MORE READS TO BE DONE
         BR    R7                  RETURN VIA PRESET REG 7
         SPACE 3
*---------------------------------------------------------------------*
*        EODAD FOR THE SORT FILE OUTPUT. WHEN YOU GET HERE ITS OVER.  *
*---------------------------------------------------------------------*
PX599    EQU   *
         PUT   PRINT,LINE          END OF LOOP, WRITE LINE
         CLOSE SORT
         CLOSE PRINT
         L     R13,SAVE+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
*---------------------------------------------------------------------*
*        THIS IS THE COMMON READ ROUTINE FOR THE INPUT PTF FILE       *
*        USED DURING PHASE ONE PROCESSING.                            *
*---------------------------------------------------------------------*
PXGET    EQU   *                   COMMON READ ROUTINE FOR SYSIN
         GET   SYSIN,INAREA
         LA    R3,72               SET UP REG 3 FOR BCT
         LA    R4,INAREA           POINT TO DATA
         BR    R2                  RETURN TO CALLER
         SPACE 2
*---------------------------------------------------------------------*
*        THIS IS THE COMMON HEADER WRITTING ROUTINE.                  *
*---------------------------------------------------------------------*
PXPUT    EQU   *
         AP    PAGE,ONE            ADD TO PAGE NUMBER
         MVC   NUM,PATRN           INSERT EDIT PATTERN
         ED    NUM,PAGE            MAKE THE NUMBER PRINTABLE
         PUT   PRINT,HDR1
         PUT   PRINT,HDR2
         PUT   PRINT,HDR3
         ZAP   LINECNT,FOUR        RESET PAGE LINE COUNTER
         BR    R2                  RETURN TO CALLER
         SPACE 2
*---------------------------------------------------------------------*
*        IF THE SORT RETURNS A NON-ZERO CONDITION CODE,               *
*        CONTROL IS PASSED TO THIS ABEND MACRO.                       *
*---------------------------------------------------------------------*
SORTERR  EQU   *
         ABEND 128,DUMP
         EJECT
         LTORG
         EJECT
SYSIN    DCB   DSORG=PS,MACRF=GM,EODAD=EOF,DDNAME=INPUT,RECFM=FB,      X
               LRECL=80
         EJECT
SORT     DCB   DSORG=PS,MACRF=(PM,GM),DDNAME=SORTIN,RECFM=FB,LRECL=15, X
               BLKSIZE=1500,EODAD=PX599
         EJECT
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=PRINTER,RECFM=VBA,LRECL=137,   X
               BLKSIZE=141
         EJECT
TITLE    DCB   DSORG=PS,MACRF=GM,DDNAME=TITLES,RECFM=FB,               X
               LRECL=80,EODAD=PX590
         EJECT
SAVE     DS    18F
HOLD     DS      CL7
PAGE     DC      PL2'0'
LINECNT  DC      PL2'0'
ONE      DC      PL1'1'
THREE    DC      PL1'3'
FOUR     DC      PL1'4'
FLAG     DC      XL1'00'
NOTITLE  EQU     X'80'
PATRN    DC      XL4'40202120'
INAREA   DS      CL80
OUTPTF   DC      CL8' '
OUTFMID  DC      CL7' '
         DS     0F
SORTPARM DC      X'80'
         DC      AL3(PARMLIST)
         DC      H'0'
PARMLIST DC      AL2(PARMEND-PARMLIST)
         DC      A(SORTCARD)
         DC      A(SORTEND)
         DC      A(RECCARD)
         DC      A(RECEND)
         DC      A(0)
         DC      A(0)
PARMEND  EQU   *
SORTCARD DC      C' SORT FIELDS=(9,7,CH,A,1,7,CH,A)'
SORTEND  DC      C' '
RECCARD  DC      C' RECORD LENGTH=15,TYPE=F'
RECEND   DC      C' '
HDR1     DC      H'133'
         DC      H'0'
         DC      CL1'1'
         DC    47CL1' '
         DC      C'PTF BY FMID CROSS REFERENCE'
         DC    46CL1' '
         DC      CL4'PAGE'
NUM      DC      CL4'    '
HDR2     DC      H'135'
         DC      H'0'
         DC      CL1'0'
         DC      CL10'FMID'
         DC    12CL10'PTF'
HDR3     DC      H'135'
         DC      H'0'
         DC      CL1' '
         DC    13CL10'*******'
LINE     DC      H'137'
         DC      H'0'
OUTAREA  DC   133CL1' '
BLANK    DC      H'6'
         DC      H'0'
         DC      CL2'0 '
         EJECT
***********************************************************************
*              REGISTER EQUIVALENTS                                   *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYMF0199 JOB SY212ZZJWL1100A035,'TECHSYBFC **LAWSON**',MSGCLASS=1,
// MSGLEVEL=1,NOTIFY=@TSSY09,REGION=2000K
//*
//*
//*                @TSSY09.SYSTEM.CNTL(PUTXREF)               ****
//*
//*
//*  MAIN ORG=RM002
//*
//*
//*
//STEP1    EXEC PGM=PUTXREF
//STEPLIB  DD  DSN=TECH.SY.SMPLIB,DISP=SHR
//INPUT    DD  UNIT=T,VOL=SER=NLVOL1,DISP=SHR,
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=7200),
//             LABEL=(1,NL)
//SORTIN   DD  DSN=&&SORT,DISP=(NEW,PASS),UNIT=D,
//             DCB=(RECFM=FB,LRECL=15,BLKSIZE=4095),
//             SPACE=(CYL,(25,20))
//SORTOUT  DD  DSN=&&SORT,DISP=(SHR,PASS),VOL=REF=*.SORTIN
//TITLES   DD  DATA,DCB=BLKSIZE=80,DLM='/$'
  EAS1102  ASSEMBLER XF            5752SC103 R102  MVSS038G,H,I
  EBB1102  BASE CONTROL PROGRAM              R102  MVSS038G,H,I
  EBT1102  BTAM                    5752SC120 R102  MVSS038G,H,I
  ECM1200  3790/8100/DPCX HOST SUPPORT             3790HOST120
  EDE1102  DEMF                    5752CM100 R102  MVSS038G,H,I
  EDM1102  DATA MANAGEMENT         5752SC1D* R102  MVSS038G,H,I
  EDS1102  DATA MGMT SUPPORT ERP   5752SC1C* R102  MVSS038G,H,I
  EDU1602  DSF R060 FOR MVS/XA     565525701 R602  ICKDSF060
  EEP0100  370X-EP PEP             5735SC100 R100  EP030
  EEP0101  ACF/NCP/SSP EP R2.1     5735SC100 R101  EP030,
  EEP0200  EP/VS WITH ACF/NCP R2   5735SC100 R200  EP030,
  EEP0300  EP FOR NCP REL3         5735SC100 R300  EP030
  EER1100  EREP                             R100
  EER1300  EREP RELEASE 1.1        5744EREP1 R300  EREP111
  EER1400  EREP R1.3               5744EREP1 R400  EREP113
  EER1500  EREP R1.5               5744EREP1 R500  EREP115
  EER2100  EREP FOR MVS/XA R2.1    5744EREP1 R100
  EGA1102  GAM-GPS                 5752SC1G0 R102  MVSS038G,H,I
  EGS1102  GRAPHIC SUBROUTINE PACKAGE        R102  MVSS038G,H,I
  EHO1400  PSS HOST SUPPORT                        PSSHOST140
  EHS6700  3600 HOST SUPPORT       5744CA300 R700  3600HSPIR7
  EIP1102  IPCS                    5752SC132 R102  MVSS038G,H,I
  EJE1102  JES2                    5752SC1BH R102  MVSS038G
  EJE1103  JES2 3800 ENHANCEMENTS  5752SC1BH R103  MVSS038G,H,I
  EJS1102  JES3                    5752SC1BA R102  MVSS038G,H,I
  EMF1102  MF/1                              R102  MVSS038G,H,I
  EMG1100  3694 DOC PROC           565800101 R100  MSC9
  EMI1102  MICR/OCR                5752SC1DL R102  MVSS038G,H,I
  EML1102  MULTI-LEAVING WORK STA  5752SC1BL R102  MVSS038G,H,I
  EMS1102  MASS STORAGE SYSTEM     5752SC1DP R102  MVSS038G,H,I
  ENC0100  NCP/OS SMP4 R050        5735SC300 R050  NCP050
  ENC1201  ACF/NCP/SSP R2.1        5735SC300 R201  ACFNCP121,
  ENC1300  ACF/NCP/SSP R3.0 (SCP)  5735SC300 R300  ACFNCP130,
  EPM1102  PROGRAM MANAGEMENT                R102  MVSS038G,H,I
  ESC1326  AMDPSCAN (PSR SERVICE AID)              - DLL -
  ESP0100  SSP                               R050  NCP050
  ESP1201  ACF/NCP/SSP R2.1 UTIL   5740AN100 R201  ACFNCP121,
  ESP1300  ACF/NCP/SSP R3.0 UTIL   5740AN100 R300  ACFNCP130,
  ESS1102  SUBSYSTEM SUPPORT SERVICES        R102  MVSS038G,H,I
  EST1102  SYSTEM SUPPORT          5752SC1T* R102  MVSS038G,H,I
  ESU1102  SU BIT STRING           5752SC1SU R102  MVSS038G,H,I
  ESY1400  SMP4                    5744SC130 R400  MVSS038G,H,I
  ETC0108  TCAM10                  5752SC121 R108  MVSS038G,H,I
  ETC1102  ACF/TCAM SCP-TCAM11     5752SC1T3 R102  OSA789
  ETC2102  ACF/TCAM SCP-TCAM12     5752TC221 R102  OSA7730
  ETC2202  ACF/TCAM VER 2 REL 2    5752TC221 R202  MVSS038F
  ETC2302  ACF/TCAM VER 2 REL 3    5752TC221 R302  ACFTCAM230,
  ETC2402  ACF/TCAM V2R4           5752TC221 R402  ACFTCAM240
  ETD1100  SPPS II SUPPORT                         SPPSII010
  ETI1106  TERM IO CTL (TIOC)      5752SC1T3 R106  MVSS038G,H,I
  ETV0108  TSO/VTAM                5752SC1T9 R108  MVSS038G,H,I
  EUT1102  UTILITIES               5752SC1U* R102  MVSS038G,H,I
  EVP1102  VPSS                    5752SC1DV R102  MVSS038G,H,I
  EVT0108  VTAM2                   5752SC123 R108  MVSS038G,H,I
  EVT1102  ACF/VTAM                5752SC1T9 R102  MVSS038F
  EVT1202  ACF/VTAM REL 2          5752SC1T9 R202  ACFVTAM120
  EVT1302  ACF/VTAM REL 3 SCP      5752SC123 R302  ACFVTAM130,
  EXW1102  EXTERNAL WRITER         5752SC1B2 R102  MVSS038G,H,I
  FBB1221  MVS PROCESSOR SUPPORT 2           R221  MVSS038G,H,I
  FDM1133  3800 ENHANCEMENTS       5752SC1D* R133  MVSS038G,H,I
  FDS1122  MVS SU64 SYSGEN         5752SC1CA R122  MVSS038G,H,I
  FDS1133  3800 ENHANCEMENTS       5752SC1CB R133  MVSS038G,H,I
  FDS1143  ICKDSF REL2             5752SC1UN R143  MVSS038H
  FDS1220  DF/DSF STANDALONE       5752SC1UN R220
  FDS1243  DEV SUPP FAC (DSF) REL3 5752SC1UN R243  MVSS038G
  FDS1420  DF/DSF STANDALONE       5752SC1UN R420  ICKDSF040
  FDS1443  DSF REL4                5752SC1UN R443  ICKDSF040,
  FDS1520  DF/DSF REL5 STANDALONE  5752SC1UN R520  ICKDSF050
  FDS1543  DF/DSF REL5             5752SC1UN R543  ICKDSF050
  FDS1620  ICKDSF STANDALONE       5752SC1UN R620  ICKDSF060
  FDZ1610  ICKDSF FOR MVS          5752SC1UN R610  ICKDSF060
  FER1110  EREP ENHANCEMENTS       5744EREP1 R110  MVS038
  FER1410  EREP                    5744EREP1 R400  EREP113
  FJS1133  JES3 3800 ENHANCEMENTS  5752SC1BA R133  MVSS038G,H,I
  FSP1221  ACF/NCP/SSP R2.1 (SCP)  5744AN100 R221  ACFNCP121,
  FSP1320  ACF/NCP/SSP R3.0 MACROS 5744AN100 R320  ACFNCP130,
  FUT1133  3800 ENHANCEMENTS       5752SC1UM R133  MVSS038G,H,I
  HAE1200  DF/DSS                  5740CC100 RB00  DFDSS011,
  HAE1300  DF/DSS                  5740CC100 RC00  DFDSS012
  HAH2100  ASSEMBLER H             566896201 R100
  HAP1300  APL INDEPENDENT BASE    5748AP101 R030  APL031
  HAP1400  APL INDEPENDENT BASE    5748AP101 R040  APL040
  HBB2102  MVS SP VER2 (XA)                  RA02  MVS038
  HBC2103  DISOSS/8100/DOSF        566895501 R103
  HCI1500  CICS REL 1.5            5740XX100 R150  CICS150,
  HCI1602  CICS REL 1.6            5740XX100 R602  CICS160
  HCM1100  HOST PREP FOR 8100 DPCX 5740XR315 R100  HOSTPREP110
  HCM1200  HOST PREP               5740XR315 R200  HOSTPREP120
  HCM1300  HOST PREP               5740XR315 R300  HOSTPREP130
  HCM1400  HOST PREP               5740XR315 RD00  HOSTPREP140
  HCR1102  PROG CRYPTO GRAPHIC FACILITY            CRYPTO010,
  HCS1102  NCCF (NON SNA)          5752XX600 RA02  NCCF010
  HCS1302  NCCF (SNA 3,4)          5752XX600 RC02  NCCF010
  HCS1502  NCCF RELEASE 2          5752XX600 RE02  NCCF020,
  HCU1102  CRYPTO UNIT SUPPORT     5752CU134 RA02  CUSP010,
  HDC1400  ADC FOR STORES          5740BCS00 R400
  HDD1400  DATA DICTIONARY         5740XXF00 RD00  DD140,
  HDF1201  SCREEN DEF FAC-CICS  (SDF)        RB01  SDF020
  HDF1300  SDF                     5740XYF00 RC00  SDF030
  HDI1100  DMS-CICS REL2 PP BASE   5740XC500 RA00  DMS020
  HDI1310  DMS-CICS REL3 PP BASE   5740XC500 RC10  DMS030
  HDO2100  DISOSS/370              566598201
  HDP1102  DFP (VSAM) FOR XA       566528418 R102  MVSXADFP110
  HDT1100  3650 SDMS V1R1          5740D2200 R100
  HDT1200  3650 SDMS V1R2          5740D2200 R200
  HDT1300  3650 SDMS V1R3          5740D2200 R300
  HDV1102  ISPF PGM DEVELOPMENT    566526801 R102  ISPF110
  HDX1220  DIST SYS EXEC PGM (DSX) 5748XXG00 RB10  DSX022
  HDX2100  DSX V2R1                566898601 R100  DSX210
  HDX3100  DSX VERSION 2           566898601 R100
  HFC1100  4700 COBOL              566898701 R100
  HFL1100  FORTRAN LIB BASE REL1   5748LM303 RA00  VSFORTRAN110
  HFL1103  FRTRN LIB COMMON REL1.1 5748LM303 RA03  VSFORTRAN111
  HFL1111  FORTRAN VTAM LIBRARY                    VSFORTRAN110
  HFL1112  FORTRAN LIBRARY SYS INTERFACE           VSFORTRAN110
  HFL1200  FORTRAN LIB. COMMON                     VSFORTRAN120
  HFS1121  4700 HOST SUPPORT       566898901 R121  4700HOST100
  HFT1100  FORTRAN COMP & LIB      5748FO300 RA00  VSFORTRAN110
  HFT1103  FORTRAN COMP COMMON     5748FO300 RA03  VSFORTRAN111
  HFT1200  FORTRAN COMP COMMON     5748FO300 RB00  VSFORTRAN120
  HGD1100  GRAPHICAL DATA DISPLAY MGR (GDDM) RA00  GDDM110
  HGD1200  GDDM                    5748GD101 RB00  GDDM120
  HHC1100  HOST COMMAND FACILITY - TCAM --   RA00  HCF010,
  HHC1102  HOST COMMAND FACILITY - VTAM --   RA02  HCF010,
  HHC2102  HCF VERSION 2           566598501 R102  HCF020
  HHM1202  HIERARCHICAL STOR MGR   5752XRB00 RB02  - N/A -
  HHM1302  HSM REL 3               5752XRB00 RC02  HSM030
  HIA1100  IIAS                    566801101 R100
  HIF1102  ISPF FOR MVS/TSO        566526801 R102  ISPF110
  HIM1200  IMS DB DATA BASE        5740XX214 RB00  IMS120,
  HIM1500  IMS/VS DATA BASE        5740XX214 RA15
  HIM1504  IMS/VS REFRESH OF HIM1500
  HIM1600  IMS/VS REL 1.6          5740XX214 R600  MVSS038F
  HIP2102  IPCS FOR SP2.1.0        5752SC132 RA02  MVS038
  HIP2329  IPCS FOR SP1.3.3                  RC29  MVSS038J
  HIR1102  IMS RESOURCE LOCK MGR   5740IR100 RA02  IMS120
  HJE1100  APPLIC. DEVELOP FAC.    566893701       ADF2
  HJE1104  JES2 NJE WITH 3800E     5752SC1BH RA04  MVSS038G,H,I
  HJE2226  JES2 FOR SP1.3 & HIGHER 5752SC1BH RB26  MVS038,
  HJE2329  JES2 FOR SOMETHING      5752SC1BH RC29
  HJS2226  JES3 FOR SP1.3          5752SC1BA RB26  MVS038,
  HJS2327  JES3 FOR SP1.3.1        5752SC1BA RC27  MVS038
  HLA1101  OS CICS                 5740XX100       CICS150
  HMA1100  DASD MIGRATION AID-DF/DASDMAID    RA00  DASDMIG010,
  HMP1100  SMP/E V1R1                        R100  MVSS038J
  HNC2100  ACF/NCP V1R1                            MVSS038J
  HNO1102  NOSP REL2               5752XX200 RA02  NOSP010
  HNT1100  NETWORK TERMINAL OPTION 5735XX706 R100  NTO110
  HNT1200  NTO REL2                5735XX700 R200  NTO120
  HOC1300  OPER PLAN & CTL (OPC)   5740XT900 R300  OPC130
  HOP1102  IDWS                    5752AMA00 R102  IDWS102,
  HOZ1202  INFORMATIONS SYSTEMS    5752OZ135 R202  INFO020
  HOZ1203  INFO SYS REFRESH        5752OZ135 R203  INFOACC020
  HPD1103  NPDA COMMON            5740PD132 R103
  HPD1200  NPDA COMMON REL2       5740PD132 R200  NPDA020
  HPD1203  NPDA REL2 ENHANCED      5740PD132 R203  NPDA02E
  HPD2100  NPDA VER2               566898301 R100  NPDA210
  HPE1100  PEP EXTENDED FEATURES   5799PE100 RA00  PEPXF100
  HPF1102  SPF                     566500901 RA02  SPF010,
  HPI1102  INFO LIBRARY (TSO)      566527701 R102  INFOLIB110
  HPI1103  INFO LIBRARY (NCCF)     566527701 R103  INFOLIB110
  HPL0400  PL1 COMPILER            5734PL141 R040  PL1140,
  HPO1102  OCCF                    566528801 R102
  HPP1210  SPPS V1R2               5740D1601 R210
  HPP1300  SPPS V1R3               5740D1601 R300
  HPS1100  IIPS                    566801201 R100
  HRC1100  3650 ARC V1R1           5740D1500 R100
  HRC1200  3650 ARC V1R2           5740D1500 R200
  HRF1302  RACF                    5752XXH00 RC02  MVSS038G
  HRF1402  RACF REL4               5752XXH00 RD02  MVSS038I
  HRF1502  RACF V1R5                               MVS038
  HRL0400  PL1 RESIDENT LIBRARY              R040  PL1140,
  HRL1040  PL1                     5734LM441 R040
  HRL1400  PL1                     5734LM441 R040
  HRM2204   RMF V2 REL 2           5752XY400 RB04  MSC2137
  HRM2205   RMF V2R2 - SE SUPPORT  5752XY400 RB05  MSC1089
  HRM2305  RMF V2R3                5752XY400 RC05  MVSS038G
  HRM2405  RMF V2R4 FOR SP1.2      5752XY400 RD05
  HRM2406  RMF V2R4 FOR SP1.3      5752XY400 RD06  MVS038,
  HRM3102  RMF VER2                566527404 R102  MVS038
  HRP1100  RPG II                  5740RG137 RA00  RPG100
  HRS1200  SERVICE LEVEL REPORTER  5740DC300 R020  MVS038
  HSL1200  DOC LIB FACILITY (DLF)  5748XXE00 R020  DCF020,
  HSL1202  DLF MVS                 5748XXE00 R020  DCF020,
  HSM1500  SORT MERGE              5740SM105 R500  SORT150
  HSP2100  ACF/NCP V2R1                            MVSS038J
  HSR1200  DOC COMPOSITION FACLITY 5748XX900 R020  DCF020
  HSR1202  DCF BACKGROUND          5748XX900 R020  DCF020
  HTL0400  PL1 TRANSIENT LIBRARY   5734LM541 R040  PL1140,
  HTP1100  FILE TRANSFER PROG      5740SCFTP R110  FTP010
  HTR1100  NETWORK ROUTING FACLITY 566896301 R100  NRF110
  HTS1202  TSO SESSION MANAGER               RB02  MVSS038H
  HUT1135  DF/DSS                  5740CC100 RA35  DFDSS010
  HVC1202  VSPC                    5740XR600 R010  VSPC020
  HVC2102  VSPC VER 2 REL 1        566528301 R020  VSPC210
  HVT2102  ACF/VTAM VER2 REL1      566528001 R102  ACFVTAM210
  JAP1303   VSPC JCLIN FOR APL     5748AP101 R030  APL031
  JAP1305   COMMON INSTALL JCL FOR APL       R030  APL031
  JAP1320   CICS EXECUTOR FOR APL  5748AP101 R030  APL031
  JAP1330   VSPC EXECUTOR FOR APL  5748AP101 R030  APL031
  JAP1401  JCLIN FOR MVS TSO FOR APL         R040  APL040
  JAP1410  TSO FOR MVS             5748AP101 R040  APL040
  JAP1412  JCLIN FOR VSPC          5748AP101 R040  APL040
  JAP1413  JCLIN FOR TSO WITH VSPC 5748AP101 R040  APL040
  JAP1414  CICS JCLIN              5748AP101 R040  APL040
  JAP1420  CICS EXECUTOR           5748AP101 R040  APL040
  JAP1430  VSPC EXECUTOR           5748AP101 R040  APL040
  JAP1440  COMMON EXECUTOR         5748AP101 R040  APL040
  JBB1112  TSO COMMAND PACKAGE     5752SC1B4 RA12  MVSS038G,H
  JBB1113  TSO EXTENSIONS          56652850* RA13  MVS038
  JBB1114  TSO NETMAIL             566528504 RA14  MVS038
  JBB1122   MVS/SE PROG PROD                 RA22  MSC3062
  JBB1123   MVS/SYSTEM EXTENSION 1.1         RA23  MSC1719
  JBB1126  MVS SP1.1.0             5752BASIC RA26  MVSS038G
  JBB1127  MVS SP1.1.1             5752BASIC RA27  MVSS038G
  JBB1129  3375/3380 PRPQ
  JBB1222  MVS/SYSTEM EXTENSION 2.0          RB22  MSC1717
  JBB1226  MVS SYSTEM PROD REL1.2.0          RB26  MSC163,MSC144
  JBB1326  MVS/SYSTEM PROD REL1.3.0          RC26  MVSS038H,I
  JBB1327  MVS SYSTEM PROD REL1.3.1          RC27  MVS038
  JBB1328  MVS SYSTEM PROD REL1.3.2          RC28  MVS038
  JBB1329  MVS SP1.3.3                             MVS038
  JBB2115  TSO EXTENSIONS          566528501 R115  MVS038
  JBT1112  BTAM SP                 566527901 RA12
  JCS1512  TERM ACC FAC FOR NCCF   5752XX600 RE12  NCCF020
  JDD1410  CICS ONLINE SUPPORT     5740XXF00 R410  DD140,
  JDD1420  IMS ONLINE SUPPORT      5740XXF00 R420  DD140,
  JDI1110  DMS/CICS REL2 PP        5740XC500 R111  DMS020
  JDI1311  DMS/CICS REL3 PP        5740XC500 R311  DMS030
  JDM1112  MVS SAM - EXTENDED      5752SC1D* RA12  MVSS038G,H,I
  JDM1113  DF/EF                   5752SC1D* RA13  DFEF010,
  JDM1116  DF/EF CRYPTO FACILITY   5752SC1DK RA16  DFEF010,
  JDM1122  AMS CRYPTO                        RA22  MVSS038G,H,I
  JDM1132  MVS/SE SUPPORT FEATURE  5752SC1D1 RA32  MVSS038G,H,I
  JDM1134  DF/DS DATA MGMT         5752SC1D* RA34  MVSS038I
  JDM1136  DF/DS DATA MGMT         5752SC1D* RA36  MVSS038I
  JDM1137  DF/DS DATA MANAGEMENT   5752SC1D2 RA37  DFDS114
  JDM1138  DF/DS DATA MGMT         5752SC1D* RA38  MVSS038I
  JDM1139  DF/DS DATA MGMT         5752DM1CM RA39  DFDS115
  JDM1141  DF/DS VSAM              5752SC1DE RA41  DFDS115
  JDM1142  DF/DS OPEN/CLOSE/EOV    5752SC1D1 RA42  DFDS115
  JDM1145  DF/DS R1.1.6                      RA45  DFDS116
  JDS1112  MVS DASDR UTILITY                 RA12  MSC1039
  JDS1125  3375-80 PRPQ PRE-DFDS4  5752SC1S1 RA25  MVSS038H,I,
  JDS1134  DF/DS CHECKPT RESTART   5752SC1C* RA34  MVSS038I
  JDS1136  DF/DS DASD EREP         5752SC1CA RA36  MVSS038I
  JDS1137  DF/DS CHECKPT RESTART   5752SC1CA RA37  DFDS114
  JDS1139  DF/DS DATA MGMT         5752DS101 RA39  DFDS115
  JDS1140  DF/DS SYSGEN            5752SC1S1 RA40  DFDS115
  JDS1145  DF/DS R1.1.6                      RA45  DFDS116
  JFL1113  LIBRARY VTAM INTERFACE  5748LM303 RA13  VSFORTRAN111
  JFL1114  LIBRARY SYS INTERFACE   5748LM303 RA14  VSFORTRAN111
  JFL1211
  JFL1212
  JFT1111   FORTRAN COMP & LIB EXEC PROCEDURE      VSFORTRAN110
  JFT1112   SYSTEM INTERFACE       5748FO300 R112  VSFORTRAN110
  JFT1113  FORTRAN COMPILER EXEC   5748FO300 RA13  VSFORTRAN111
  JFT1114  FORTRAN COMPILER SYS INTERFACE    RA14  VSFORTRAN111
  JFT1211  FORTRAN COMP EXEC                       VSFORTRAN120
  JFT1212  FORTRAN COMP INTERFACE                  VSFORTRAN120
  JGD1101  PRESENTATION GRAPHICS   5748GD101 RA01  GDDM110
  JGD1201  PRESENTATION GRAPHICS   5748GD101 RB01  GDDM120
  JHM1310  HSM REL3.1              5752XRB00 RC10  HSM031
  JIF1177  ISPF EXITS              566526801 R102  ISPF110
  JIM1210  IMS SURVEYOR UTILITY    5740XX214       IMS120,
  JIM1220  IMS DC DATA COMM        5740XX214       IMS120,
  JIM1222  IMS DC/XA                               IMS120
  JIM1230  IMS MCS MULTI SYS COUPL 5740XY211       IMS120,
  JIM1232  MSC SP2.1               5740XY211
  JIM1240  IMS FP FAST PATH        5740I1214 R240  IMS120,
  JIM1242  IMS FAST PATH SP2.1     5740I1214 R242  IMS120
  JIM1250  IMS DBRC RECVRY CONTROL 5740I1214       IMS120
  JIM1262  IMS DB/XA                               IMS120
  JIM1450   IMS/VS RECOVERY CONTROL
  JIM1510   IMS/VS SURVEYOR
  JIM1514   IMS/VS REFRESH OF JIM1510
  JIM1520   IMS/VS DATA COMMUNICATIONS
  JIM1524   IMS/VS REFRESH OF JIM1520
  JIM1530   IMS/VS MULTI-SYSTEM
  JIM1534   IMS/VS REFRESH OF JIM1530
  JIM1540   IMS/VS FAST PATH
  JIM1544   IMS/VS REFRESH OF JIM1540
  JIM1610   IMS/VS SURVEYOR UTILITY
  JIM1620   IMS/VS DATA COMMUNICATION              MVSS038F
  JIM1630   IMS/VS MULTI SYS COUP   5740XY211 R630
  JIM1640   IMS/VS FAST PATH        5740I1214 R640 MVSS038F
  JIM1650   IMS/VS RECOVERY CONTROL 5740I2214 R650
  JIP1226  IPCS FOR SP1.2           5752SC132 RB26
  JIP1326  IPCS FOR SP1.3           5752SC132 RC26  MVS038
  JIP1327  IPCS FOR SP1.3.1        5752SC132 RC27  MVS038
  JJE1112  JES2 NJE                5752SC1BH RA12  MSC333
  JJS1134 JES3 NJE                 5752SC1BA RA34  MSC530
  JJS1234  JES3 NJE 3800E          5752SC1BA RB34  MVSS038G,H,I
  JJS1235  JES3 ERA (EXT REAL ADDR)5752SC1BA RB35  MVSS038H,I
  JMS1112  MSS EXTENSION           5752SC1D* RA12  MVSS038G,H,I
  JMS1114  MSS-E                             RA14  MVS038
  JNC1211  ACF/NCP/SSP R2.1 (PP)   5735SC300 RB11  ACFNCP121,
  JNC1310  ACF/NCP/SSP R3.0 (PP)   5735SC300 RC10  ACFNCP130,
  JOZ1222  INFORMATION MANAGEMENT  5752OZ135 R222  INFO020
  JOZ1223  INFO MGMT REFRESH       5752OZ135 R223  INFOACC020
  JOZ1232  INFO ACCESS             5752OZ135 R232  INFOACC020
  JPD1122   NPDA                   5752PD132 RA22
  JPD1212   NPDA                   5740PD132 R212
  JPD1233  NPDA REL2 ENHANCED TARA 5740PD132 RB33  NPDA02E
  JPD2103  NPDA VER2 3600 FEATURE  566898301 R103  NPDA210
  JPF1177  SPF EXITS               566500901 RA77  SPF010,
  JPM1137  DF/DS LINKEDIT                    RA37  DFDS114
  JRF1305  RACF REPORT WTR ENHANCE 5752RF100 R305  MVSS038G,
  JRM2214  RMF                     5752XY400 RB14
  JRM2215   RMF V2R2-SE 1.1                  RB15
  JRM2225   RMF V2R2-SE 2.0                  RB25
  JRM2315  RMF FOR SP1.1.1         5752XY400 RC15  MVSS038G
  JSM1502  SORT MERGE SVC          5740SM105 R500  SORT150
  JSP1211  ACF/NCP/SSP R2.1 UTIL   5744AN100 R211  ACFNCP121,
  JSP1231  ACF/NCP/SSP R2.1 MACROS 5740AN100 R231  ACFNCP121,
  JSP1310  ACF/NCP/SSP R3.0 UTIL   5740AN100 R310  ACFNCP130,
  JSP1330  ACF/NCP/SSP R3.0 MACROS 5744AN100 R330  ACFNCP130,
  JSR1201  DCF TSO                           R020  DCF020
  JSR1202  DCF ATMS                          R020  DCF020
  JST1113  DF/EF TSO UTILITIES     5752SC1T2 R113  DFEF010,
  JTC1112  ACF/TCAM BASE PP        5752SC121 RA12
  JTC1122  ACF/TCAM NETWORKING     5752SC121 RA22
  JTC2112  ACF/TCAM BASE PP        5752SC121 RA12
  JTC2122  ACF/TCAM NETWORKING     5752SC121 RA22
  JTC2212  ACF/TCAM V2R2           5752TC221 RB12  MVSS038F
  JTC2222  ACF/TCAM V2R2 NETWORK   5752TC221 RB22  MVSS038F
  JTC2312  ACF/TCAM VER 2 REL 3    5752TC221 R312  ACFTCAM230,
  JTC2322  ACF/TCAM V2R3 NETWORK   5752TC221 R322  ACFTCAM230,
  JTC2412  ACF/TCAM V2R4           5752TC221 RD12  ACFTCAM240
  JTC2416  ACF/TCAM V2R4           5752TC221 RD16  ACFTCAM240
  JTC2422  ACF/TCAM V2R4 MSNF      5752TC221 RD22  ACFTCAM240
  JUT1112  OFFLINE IBM 3800 UTIL             RA12
  JUT1113  DF/EF IEHLIST           5752SC1U2 R113  DFEF010,
  JUT1134  DF/DS UTILITY           5752SC1U2 RA34  DFDS112,
  JUT1137  DF/DS UTILITY           5752SC1U6 RA37  DFDS114,
  JUT1139  DF/DS UTILITIES         5752SC1U6 RA39  DFDS115
  JUT1145  DF/DS R1.1.6                      RA45  DFDS116
  JVP1121  VECTOR PROCESSING SUBSYSTEM
  JVT1112   ACF/VTAM BASE PP                 RA12
  JVT1122   ACF/VTAM NETWORKING FEATURE      RA22
  JVT1132   ENCRYPT-DECRYPT FEATURE          RA32
  JVT1212  ACF/VTAM BASE PP        5752SC123 RB12  ACFVTAM120
  JVT1222  ACF/VTAM MSNF                     RB22  ACFVTAM120
  JVT1232  ENCRYPT-DECRYPT FEATURE           RB32  ACFVTAM120
  JVT1311  ACF/VTAM PP BASE                        ACFVTAM130
  JVT1312  ACF/VTAM REL3 BASE PP   5752SC123 RC12  ACFVTAM130,
  JVT1322  MSNF FOR ACF/VTAM REL 3           RC22  ACFVTAM130,
  JVT1332  ENCRYPT/DECRYPT ACF/VTAM REL 3    RC32  ACFVTAM130
  JVT2112  ACF/VTAM V2R1 CRYPTO    566528001       ACFVTAM210
  JXX1100  NCP PACK SWITCH INTRFACE 566898101 R010 X25NPSI110
  JXX1200  NCP PACK SWITCH I'FACE   566898101 R020 X25NPSI120
  JXX1300  NCP PACK SWITCH I'FACE   566898101 R030 X25NPSI130
/$
//PRINTER  DD  SYSOUT=1
//SYSUDUMP DD  SYSOUT=1
//SYSPRINT DD  SYSOUT=1
//SYSOUT   DD  SYSOUT=1
//SYSIN    DD  *
 SORT FIELDS=(9,7,CH,A,1,7,CH,A)
/*
