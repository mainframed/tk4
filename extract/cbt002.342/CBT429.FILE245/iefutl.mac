IEFUTL   TITLE 'SMF USER TIME LIMIT EXIT'
*/* REFER:  NUSPS.MVSESA.ASM(IEFUTL)
*/* COMPID: SYSTEM
*/* DOC     THIS IS A SAMPLE SMF EXIT ROUTINE THAT IS INVOKED WHEN A
*/*         CPU TIME LIMIT OR CONTINUOUS WAIT TIME HAS BEEN EXCEEDED.
******************************************************************* RAG
*                                                                 * RAG
*   AMENDED BY R. GORAM ON AUGUST 19 1981 TO TAILOR IT TO NU      * RAG
*                                                                 * RAG
*   ALL CHANGES BY ME ARE IDENTIFIED BY 'RAG' IN COLS 69 - 71.    * RAG
*                                                                 * RAG
******************************************************************* RAG
******************************************************************* JGC
*                                                                 * JGC
*   AMENDED BY J. CULTIEN ON JULY 5TH, 1982 TO TAILOR IT TO NU    * JGC
*                                                                 * JGC
*   ALL CHANGES BY ME ARE IDENTIFIED BY 'JGC' IN COLS 69 - 71.    * JGC
*                                                                 * JGC
******************************************************************* JGC
******************************************************************* JGC
*                                                                 * JGC
*   AMENDED BY R. GORAM   ON MARCH 7,  1983 TO REMOVE THE USE OF  * JGC
*                                                                 * JGC
*   BYTE 1 OF THE USER COMM. AREA AS THIS CAUSES PROBLEMS WITH    * JGC
*                                                                 * JGC
*   THE INTERFACE WITH IEFUJV WHEN JOB REQUEUED.                  * JGC
*                                                                 * JGC
******************************************************************* JGC
******************************************************************* RT
*                                                                 * RT
*   AMENDED BY R.T. ON OCT 13 TO GIVE NUSPS01                     * RT
*                                                                 * RT
*   JEFF'S USER UNLIMITED TSO EXTENSION.                          * RT
*                                                                 * RT
******************************************************************* RT
******************************************************************* ART
*                                                                 * ART
*   AMENDED BY ART  ON AUGUST 21 1989 TO GIVE ZLH469E AND ZLH343E * ART
*                                                                 * ART
*   UNLIMITED TSO EXTENSION.                                      * ART
*                                                                 * ART
******************************************************************* ART
******************************************************************* ART
*                                                                 * ART
*   AMENDED BY ART ON SEPTEMBER 14 1989 TO GIVE ZLH347E AND USER  * ART
*                                                                 * ART
*   GROUPS NUOPD/NUOPS/NUCOS/NUCES UNLIMITED TSO EXTENSION.       * ART
*                                                                 * ART
*                                                                 * ART
*   LOGIC TO GIVE NUOPS9 USERS UNLIMITED TSO EXTENSION HAS BEEN   * ART
*                                                                 * ART
*   REMOVED AS IT HAS BEEN SUPERSEDED BY THE NUOPS EXTENSION.     * ART
*                                                                 * ART
******************************************************************* ART
******************************************************************* ART
*                                                                 * ART
*   AMENDED BY MJR ON DECEMBER  08 1989 TO REMOVE ZLH469E,        * ART
*                                                                 * ART
*   ZLH343E AND ZLH347E, REPLACING WITH NUUDP98,NUUDP99.          * ART
*                                                                 * ART
******************************************************************* ART
******************************************************************* RNH
*                                                                 * RNH
*   AMENDED BY RNH ON OCTOBER   09 1990 TO PREVENT TSO USERS      * RNH
*                                                                 * RNH
*   NUNPS34 THRU' NUNPS44 TIMING OUT                              * RNH
*                                                                 * RNH
******************************************************************* RNH
******************************************************************* CMH
*                                                                 * CMH
*   AMENDED BY CLIVE HALLAS FEBRUARY 6 1991 TO MAKE USERS         * CMH
*                                                                 * CMH
*   NUADS98 AND NUADS99 NON TIMEOUT.                              * CMH
*                                                                 * CMH
******************************************************************* CMH
******************************************************************* CMH
*                                                                 * CMH
*   AMENDED BY ANDY COULSON OCTOBER    1991 TO MAKE USERS         * CMH
*                                                                 * CMH
*   NUOCS01 THROUGH TO NUOCS21 NON TIMEOUT.                       * CMH
*                                                                 * CMH
******************************************************************* CMH
******************************************************************* RNH
*                                                                 * RNH
*   AMENDED BY RICHARD HAYDOCK DEC     1991 TO REMOVE HARD CODED  * RNH
*                                                                 * RNH
*   USERIDS. ADDED CALL TO RACF TO CHECK WHETHER EXTENSION IS     * RNH
*                                                                 * RNH
*   ALLOWED                                                       * RNH
*                                                                 * RNH
*   CURRENT PRIVELEGED IDS: ZLH*                                  * RNH
*                           NUOPS                                 * RNH
*                           NUCOS                                 * RNH
*                           NUCES                                 * RNH
*                           NUUDP98/99                            * RNH
*                           NUNPS34-44                            * RNH
*                           NUPKD                                 * RNH
*                                                                 * RNH
******************************************************************* RNH
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  MVS/SP SYSTEM IPO SUPPLIED          *
*                                 USER TIME LIMIT EXIT                *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW JOBS TO BE CANCELLLED IF JOB (STEP) TIME       *
*                   LIMIT EXCEEDED.                                   *
*                ALLOW TSO SESSIONS TO HAVE ONE MORE MINUTE OF CPU    *
*                   TIME IF JOB (STEP) TIME LIMIT EXCEEDED.           *
*                ALLOW JOBS TO HAVE 20 MORE MINUTES OF WAIT TIME  * RAG
*                   IF WAIT TIME LIMIT EXCEEDED.                      *
*                ALLOW TSO SESSIONS TO BE CANCELLED IF WAIT TIME      *
*                   LIMIT EXCEEDED.                                   *
*                                                                     *
*                OPERATION =                                          *
*                   ANALYZE SMF COMMON EXIT PARAMETER AREA.           *
*                   IF WAIT TIME LIMIT EXCEEDED SEE IF JOB.  NO,      *
*                      IT IS A TSO SESSION.                       * JGC
*                     IF JOB NAME = NUSPS01                       * ART
*                                   ZLH469E  ZLH343E  ZLH347E     * ART
*                                   NUOPD  NUOPS  NUCOS  NUCES    * ART
*                       RETURN TO CALLER WITH A RETURN CODE OF 8  * JGC
*                       IN REGISTER 15 TO INDICATE A WAIT TIME    * JGC
*                       EXTENSION  AND  THE NUMBER OF SECONDS     * JGC
*                       (3600 = 60 MINS) OF WAIT TIME EXTENSION   * JGC
*                       IN REGISTER 1                             * JGC
*                       (BECAUSE THE WAIT TIME EXTENSION IS       * JGC
*                       GREATER THAN THE ACCOUNTING INTERVAL,     * JGC
*                       THE EXTENSION WILL BE INFINITE.)          * JGC
*                     ELSE                                        * JGC
*                       CANCEL THE TSO SESSION                    * JGC
*                     ENDIF                                       * JGC
*                      YES, IT IS A JOB.               NOTIFY     * JGC
*                      OPERATOR THAT ONE 20 MINUTE EXTENSION WILL * RAG
*                      BE GRANTED.  RETURN TO CALLER WITH A RETURN    *
*                      CODE OF 8 IN REGISTER 15 AND THE NUMBER        *
*                     OF SECONDS (1200) OF WAIT TIME EXTENSION IN * RAG
*                      REGISTER 1.  INCREMENT THE USER COMMUNICA-     *
*                      TION FIELD TO INDICATE A WAIT TIME EXTENSION.  *
*                   IF JOB OR STEP TIME LIMIT IS EXCEEDED SEE IF      *
*                      TSO SESSION.  NO, CANCEL THE JOB (AS ABOVE).   *
*                      YES, SEE IF TIME HAS ALREADY BEEN INCREMENTED  *
*                      ONCE.  YES, CANCEL THE TSO SESSION (AS ABOVE). *
*                      NO, INFORM THE USER THAT THE CPU TIME LIMIT    *
*                      HAS BEEN EXCEEDED AND A 1 MINUTE EXTENSION     *
*                      IS BEING GRANTED.  RETURN TO CALLER WITH A     *
*                      RETURN CODE OF 8 IN REGISTER 15 AND THE        *
*                      NUMBER OF SECONDS (60) OF CPU TIME EXTENSION   *
*                      IN REGISTER 1.  INCREMENT THE USER COMMUNICA-  *
*                      TION FIELD TO INDICATE A CPU TIME EXTENSION.   *
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE  *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0/1     = USED BY SYSTEM                *
*                   REGISTER  2       = RETURN CODE (TEMPORARY)       *
*                   REGISTER  3       = WAIT TIME/EXTENSION (TEMP)    *
*                   REGISTERS 4 AND 5 = SECURE R0 & R1 AT START       *
*                   REGISTERS 6 AND 7 = UNUSED                        *
*                   REGISTERS 8 AND 9 = USED TO SET UP RACROUTE CALL  *
*                   REGISTER  10      = ADDRESS OF GETMAINED AREA     *
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA (JMR)     *
*                   REGISTER  12      = ADDRESSABILITY TO IEFUTL      *
*                                       CSECT                         *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                  'ZAP*'S)                                           *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 452 BYTES                              *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                USER COMMUNICATION FIELD - EXTENSION COUNT           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, WTO, RACROUTE, IEFJMR, TPUT              *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
IEFUTL   CSECT
IEFUTL   AMODE 31
IEFUTL   RMODE ANY
         USING IEFUTL,R15          ESTABLISH BASE REGISTER FOR CSECT
         B     UTLSTRT             BRANCH ROUND ID
         DC    C'IEFUTL'           CSECT NAME
         DC    C'&SYSDATE'         ASSEMBLY DATE
         DC    C'&SYSTIME'         ASSEMBLY TIME
         DS    0F                  ALIGNMENT
UTLSTRT  EQU   *
         STM   R14,R12,12(R13)     SAVE CALLERS REGS IN CALLERS SVAREA
         LR    R12,R15             SET UP BASE REGISTER
         DROP  R15
         USING IEFUTL,R12          ESTABLISH BASE REGISTER
*
         LR    R4,R0               SAVE R0
         LR    R5,R1               AND  R1
*
*        N.B. LOC=BELOW USED BECAUSE INPUT AREA FOR TPUT MACRO MUST BE
*             BELOW 16 MB    RNH JAN 1992
*
         GETMAIN R,LV=WRKDSLEN,LOC=BELOW        GET STORAGE
         LR    R10,R1                           SAVE ADDRESS
         USING WRKDSECT,R10                     ANCHOR THE DSECT
*
         LA    R14,SAVEAREA        PUT CALLER SVAREA ADDR IN OWN SVAREA
         ST    R14,8(R13)          SET FORWARD POINTER
         ST    R13,SAVEAREA+4      SET BACK CHAIN TO CALLER
         LA    R13,SAVEAREA        POINTER TO SAVE AREA
         EJECT
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,0(0,R5)             LOAD POINTER TO PARAMETER AREA
         USING JMR,R11                 ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R4,FULL4                SEE IF JOB/STEP CPU EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
*                                      TIME EXCEEDED
*
***********************************************************************
*                                                                     *
*        CPU TIME EXCEEDED - ONLY ALLOW TSO SESSION A 1 MINUTE        *
*                            EXTENSION UNLESS 'PRIVILEGED' (CHECK  RNH*
*                            THIS WITH A CALL TO RACF)             RNH*
*                                                                     *
***********************************************************************
*
         TM    JMRINDC,SMFTSO          SEE IF TSO SESSION
         BNO   CANCEL                  NO.....GO CANCEL - ONLY EXTEND
*                                      CPU TIME FOR TSO SESSIONS
*
         MVI   ENTITY,C' '             INITIALISE ENTITY TO SPACES
         MVC   ENTITY+1(254),ENTITY
         MVI   CLASS,C' '              INITIALISE CLASS TO SPACES
         MVC   CLASS+1(254),CLASS
         MVC   RACFLIST(RACFLEN),RACFMASK    INITIALISE
*
         LA    R9,6                    PUT 6 IN R9
         STH   R9,ENTITLEN             SET UP LENGTH OF 'TSOCPU'
         MVC   ENTITY(6),=C'TSOCPU'    SET UP ENTITY NAME OF 'TSOCPU'
*
         MVI   CLASSLEN,X'08'          SET UP LENGTH OF 'FACILITY'
         MVC   CLASS(8),=C'FACILITY'   SET UP CLASS NAME OF 'FACILITY'
*
         LA    R8,CLASSLEN             ADDRESS OF CLASS LENGTH BYTE
*
         RACROUTE REQUEST=AUTH,        RACHECK CALL                    X
               WORKA=RACFWORK,         512 BYTE WORK AREA              X
               ENTITY=(ENTITY),        ADDRESS OF 'TSOCPU' CONST.      X
               CLASS=(8),              CLASS                           X
               ATTR=READ,              READ ACCESS REQUIRED            X
               MF=(E,RACFLIST)         USE LISTED RACROUTE IN DSECT
*
         LTR   R15,R15                 TEST FOR ZERO RETURN CODE
         BZ    INFINEXT                NO CANCELLATION IF AUTHORISED
*
         TM    JMRUCOM,X'80'           SEE IF CPU TIME HAS NOT YET BEEN
*                                      EXTENDED ONCE
*
         BO    CANCEL                  NO....GO CANCEL TSO SESSION -
*                                      ALREADY EXTENDED ONCE
*
         MVC   TPUTLIST(TPUTLEN),TPUTMASK     SET UP TPUT BUFFER
         MVC   TPUTLIST(8),JMRJOB             MOVE IN JOBNAME (USERID)
         TPUT  TPUTLIST,TPUTLEN
*
         OI    JMRUCOM,X'80'           INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R3,FULL60               LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        WAIT TIME EXCEEDED                                           *
*           - ALLOW TSO SESSION TO BE CANCELLED                       *
*             UNLESS THEY ARE AUTHORISED TO RACF FACILITY 'TIMEOUT' RNH
*           - ALLOW JOB A 20 MINUTE EXTENSION AND NOTIFY OPERATOR   RAG
*                                                                     *
***********************************************************************
*
WAITTIME DS    0H
         TM    JMRINDC,SMFTSO          SEE IF TSO SESSION
         BNO   WAITTIM2                NO. PROCESS FOR BATCH JOB.   JGC
**********************************************************************
*                                                                    *
*        RNH DEC 1991 - NEW CALL TO RACF REPLACES ALL PREVIOUS CHECKS*
*                                                                    *
**********************************************************************
*
         MVI   ENTITY,C' '             INITIALISE ENTITY TO SPACES
         MVC   ENTITY+1(254),ENTITY
         MVI   CLASS,C' '              INITIALISE CLASS TO SPACES
         MVC   CLASS+1(254),CLASS
         MVC   RACFLIST(RACFLEN),RACFMASK    INITIALISE
*
         LA    R9,7                    PUT 7 IN R9
         STH   R9,ENTITLEN             SET UP LENGTH OF 'TSOWAIT'
         MVC   ENTITY(7),=C'TSOWAIT'   SET UP ENTITY NAME OF 'TSOWAIT'
*
         MVI   CLASSLEN,X'08'          SET UP LENGTH OF 'FACILITY'
         MVC   CLASS(8),=C'FACILITY'   SET UP CLASS NAME OF 'FACILITY'
*
         LA    R8,CLASSLEN             ADDRESS OF CLASS LENGTH BYTE
*
         RACROUTE REQUEST=AUTH,        RACHECK CALL                    X
               WORKA=RACFWORK,         512 BYTE WORK AREA              X
               ENTITY=(ENTITY),        ADDRESS OF 'TSOWAIT' CONST.     X
               CLASS=(8),              CLASS                           X
               ATTR=READ,              READ ACCESS REQUIRED            X
               MF=(E,RACFLIST)         USE LISTED RACROUTE IN DSECT
*
         LTR   R15,R15                 CHECK RETURN CODE
         BNZ   CANCEL                  TIMEOUT IF NOT AUTHORISED
*
**********************************************************************
*                                                                    *
*        INFINITE EXTENSION OF TSO SESSION FOR PRIVILEGED USERS      *
*                                                                    *
**********************************************************************
INFINEXT DS    0H
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -  JGC
*                                      EXTEND WAIT TIME IN SECONDS  JGC
*                                                                   JGC
         L     R3,WAIT3600             LOAD TIME EXTENSION IN       JGC
*                                      SECONDS INTO REGISTER 1.     JGC
*                                                                   JGC
*        BECAUSE THE WAIT TIME EXTENSION IS GREATER THAN THE        JGC
*        ACCOUNTING INTERVAL, THE EXTENSION WILL BE INFINITE.       JGC
*                                                                   JGC
         B     RETURN                                               JGC
WAITTIM2 EQU   *                       NO. IT IS NOT A TSO SESSION  JGC
*
***********************************************************************
*                                                                     *
*        ALLOW JOB A 20 MINUTE EXTENSION AND NOTIFY OPERATOR      * RAG
*                                                                     *
***********************************************************************
*
         CLI   JMRUCOM,ZERO            SEE IF WAIT TIME HAS NOT YET
*                                      BEEN EXTENDED ONCE
         BNE   CANCEL                  NO....GO CANCEL JOB - WAIT TIME
*                                      ALREADY EXTENDED ONCE
*
***********************************************************************
*                                                                     *
*        ROUTCDE=2 WAS USED SO THAT ENQ RET=TEST WAS UNNECESSARY      *
*        ENQ RET=TEST IS NECESSARY FOR ROUTCDE=11 (WTP) WHETHER       *
*           OR NOT THAT ROUTING OCCURS BY DEFAULT                     *
*        WTP CAUSES AN ENQ OF THE FOLLOWING FORM:                     *
*           MAJORNAME: SYSSJWTP                                       *
*           MINORNAME: RPL + ASID (LENGTH OF 6)                       *
*              RPL IS THE 4 BYTE REQUEST PARAMETER LIST POINTER       *
*                 LOCATED AT OFFSET JSCBSMLR (X'128') IN THE JSCB     *
*              ASID IS THE 2 BYTE ADDRESS SPACE IDENTIFIER            *
*                 LOCATED AT OFFSET ASCBASID (X'24') IN THE ASCB      *
*                                                                     *
***********************************************************************
*
         MVC   WTO1LIST(WTO1LEN),WTO1MASK SET UP WTO MASK
         MVC   WTO1LIST+92(8),JMRJOB   MOVE IN JOBNAME
         WTO   MF=(E,WTO1LIST)         ISSUE WTO
*
         MVI   JMRUCOM,ONE             INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R3,WAIT1200           LOAD TIME EXTENSION IN SECONDS RAG
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         SR    R2,R2                   INDICATE RETURN CODE OF ZERO -
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
*
         L     R13,SAVEAREA+4          PICK UP BACK CHAIN POINTER
         FREEMAIN R,LV=WRKDSLEN,A=(R10)
*
         LR    R1,R3                   LOAD WAIT TIME/EXTENSION INTO R1
         LR    R15,R2                  LOAD RETURN CODE FROM REGISTER 2
         L     R14,12(0,R13)           LOAD ADDRESS FOR RETURN
         LM    R2,R12,28(R13)          RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
         SPACE 5
**************************** CONSTANTS ********************************
FULL4    DC    F'4'
FULL8    DC    F'8'
FULL60   DC    F'60'
WAIT1200 DC    F'1200'                                             RAG
WAIT3600 DC    F'3600'            EXTEND WAIT TIME = 60 MINS       JGC
         SPACE 2
**************************** MESSAGES  ********************************
*
RACFMASK RACROUTE REQUEST=AUTH,   RACHECK CALL                         X
               MSGSUPP=YES,       MESSAGE SUPPRESSION                  X
               RELEASE=1.8.1,                                          X
               MF=L
*
WTO1MASK WTO   'WAIT TIME LIMIT HAS BEEN EXCEEDED. A SINGLE 20 MINUTE EX
               XTENSION WILL BE GRANTED FOR JOB 12345678',             X
               ROUTCDE=(2),DESC=(11),MF=L                           RAG
*
TPUTMASK DC    C'UUUUUUUU - CPU TIME LIMIT HAS BEEN EXCEEDED. A SINGLE X
               ONE MINUTE EXTENSION WILL BE GRANTED. PLEASE LOGOFF SHORX
               TLY'
*
         SPACE 2
***************************** PATCH AREA ******************************
         DS    0F
PATCH    DC    8CL4'ZAP*'
         EJECT
********************************* EQUATES *****************************
SMFTSO   EQU   X'01'
ONE      EQU   1
ZERO     EQU   0
         SPACE 5
***************************** REGISTER EQUATES ************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
************************ DSECTS (MAPPING MACROS) **********************
WRKDSECT DSECT                    WORK AREA FOR RACF CALLS
*
RACFLIST RACROUTE REQUEST=AUTH,   RACHECK CALL                         X
               MSGSUPP=YES,       MESSAGE SUPPRESSION                  X
               RELEASE=1.8.1,                                          X
               MF=L
RACFLEN  EQU   *-RACFLIST
*
RACFWORK DS    512C               WORK AREA FOR RACROUTE
SAVEAREA DS    18F                SAVE AREA
*
ENTITREG DS    0D                 ALIGNMENT
         DS    H                  PADDING
ENTITLEN DS    H                  RACF ENTITY NAME LENGTH BYTE
ENTITY   DS    255C               RACF ENTITY NAME
*
CLASSREG DS    0F                 ALIGNMENT
         DS    XL3                PADDING
CLASSLEN DS    X                  RACF CLASSNAME LENGTH BYTE
CLASS    DS    255C               RACF CLASSNAME
*
WTO1LIST WTO   'WAIT TIME LIMIT HAS BEEN EXCEEDED. A SINGLE 20 MINUTE EX
               XTENSION WILL BE GRANTED FOR JOB 12345678',             X
               ROUTCDE=(2),DESC=(11),MF=L
WTO1LEN  EQU   *-WTO1LIST
*
TPUTLIST DS    C'UUUUUUUU - CPU TIME LIMIT HAS BEEN EXCEEDED. A SINGLE X
               ONE MINUTE EXTENSION WILL BE GRANTED. PLEASE LOGOFF SHORX
               TLY'
TPUTLEN  EQU   *-TPUTLIST
*
WRKDSLEN EQU   *-WRKDSECT
*
         IEFJMR                   MAPPING OF JMR DATA AREA
         END   IEFUTL
