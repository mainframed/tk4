         MACRO
         DSMINFO
*        PUSH PRINT
*        PRINT ON,GEN
         SPACE
***********************************************************************
*                                                                     *
*        SPF DIALOG SERVICE MANAGEMENT ASSIST MACROS                  *
*                                                                     *
*        WRITTEN BY IAN L. RAMAGE                                     *
*                                                                     *
* &NAME   VDEF  &VAR,&T,&GRP,&LEN=,&TYP=,&ERROR=YES                   *
* &NAME   VDEL  &GRP,&ERROR=YES                                       *
* &NAME   VRESET &GRP,&ERROR=YES                                      *
* &NAME   VGET  &GRP,&ERROR=YES                                       *
* &NAME   VPUT  &GRP,&ERROR=YES,&POOL=PROF                            *
* &NAME   EDIT  &DSN,&VOL,&ERROR=YES                                  *
* &NAME   BROWSE &DSN,&VOL,&ERROR=YES                                 *
* &NAME   LOG &MSG,&ERROR=YES                                         *
* &NAME   CONTROL &TYPE,&MODE,&LINE,&ERROR=YES                        *
* &NAME   TBCREATE &TAB,&KEYS,&FORMAT,&OPT,&REP,&ERROR=YES,&LIB=      *
* &NAME   TBOPEN &TAB,&OPT,&ERROR=YES,&LIB=                           *
* &NAME   TBERASE &TAB,&ERROR=YES                                     *
* &NAME   TBEND &TAB,&ERROR=YES                                       *
* &NAME   TBSAVE &TAB,&COPY,&ALT,&PAD,&ERROR=YES                      *
* &NAME   TBCLOSE &TAB,&COPY,&ALT,&PAD,&ERROR=YES,&LIB=               *
* &NAME   TBSARG &TAB,&ARG,&ERROR=YES                                 *
* &NAME   TBSCAN &TAB,&ARG,&ERROR=YES                                 *
* &NAME   TBSKIP &TAB,&NUM,&ERROR=YES                                 *
* &NAME   TBPUT &TAB,&ERROR=YES                                       *
* &NAME   TBADD &TAB,&ERROR=YES                                       *
* &NAME   TBMOD &TAB,&ERROR=YES                                       *
* &NAME   TBEXIST &TAB,&ERROR=YES                                     *
* &NAME   TBTOP &TAB,&ERROR=YES                                       *
* &NAME   TBBOTTOM &TAB,&ERROR=YES                                    *
* &NAME   TBDELETE &TAB,&ERROR=YES                                    *
* &NAME   TBVCLEAR &TAB,&ERROR=YES                                    *
* &NAME   DISPLAY &PANEL,&MSG,&ERROR=YES                              *
* &NAME   TBDISPL &TAB,&PANEL,&MSG,&ERROR=YES                         *
* &NAME   VLEN  &VAR,&OPT                                             *
* &NAME   VMSG  &MSG,&MSGID                                           *
* &NAME   FTERASE &MEM,&ERROR=YES                                     *
* &NAME   FTCLOSE &MEM,&ERROR=YES                                     *
* &NAME   FTOPEN &T,&ERROR=YES                                        *
* &NAME   FTINCL &SKEL,&NOFT,&ERROR=YES                               *
* &NAME   BLK &N,&LEN,&FILL=' '                                       *
* &NAME   BLANK &N,&LEN,&FILL=' '                                     *
* &NAME   VTST  &GRP                                                  *
* &NAME   SELECT &BUF,&LEN=,&ERROR=YES                                *
* &NAME   SETMSG &MSGID,&ERROR=YES                                    *
* &NAME   VSPF                                                        *
* &NAME   ISPQRY                                                      *
*                                                                     *
***********************************************************************
         SPACE
*        POP  PRINT
         MEND
         MACRO
&NAME    VDEF  &VAR,&T,&GRP,&LEN=,&TYP=,&ERROR=YES,&LAST=N,&EQU=,&ORG=
.**********************************************************************
.*                                                                    *
.*       VDEF MACRO                                VDEF MACRO         *
.*                                                                    *
.**********************************************************************
         GBLA  &XL(200),&XGRP(200)
         GBLA  &NL,&NV,&NG,&NS,&TGRP,&MAXD
         GBLB  &DEF(21),&GET(21),&SHR(21),&MDEF,&MGET,&MSHR,&GRPT
         GBLC  &XVAR(200),&XGET(200),&XSHR(200),&VTYP
         LCLA  &C,&L,&GRX,&GRXX,&GRXY
         LCLC  &A
         AIF   (&MDEF).NOFS
&MDEF    SETB  1
         VDEF  DMFUNC,TYP=C,LEN=8
         VDEF  DMRCODE,TYP=F,LEN=4
.NOFS    ANOP
         VTYP  &VAR
         AIF   (T'&TYP EQ 'O').GETTYP
&A       SETC  '&TYP'
         AGO   .TYPS
.GETTYP  ANOP
&A       SETC  T'&VAR
.TYPS    AIF   ('&A' NE 'A').NOSETF
&A       SETC  'F'
.NOSETF  AIF   ('&A' EQ 'C' OR '&A' EQ 'F' OR '&A' EQ 'B' OR '&A' EQ 'X'
               ').TYPOK
         MNOTE 12,'INVALID TYPE - &A '
         MEXIT
.TYPOK   ANOP
         AIF   (T'&LEN EQ 'O').GETLEN
&L       SETA  &LEN
         AGO   .LENS
.GETLEN  ANOP
&L       SETA  L'&VAR
.LENS    ANOP
&C       SETA  1
         AIF   (T'&GRP EQ 'O').SETTYP
&GRX     SETA  &GRP+1
         AGO   .GOTTYP
.SETTYP  ANOP
&GRX     SETA  1
.GOTTYP  ANOP
         AIF   (NOT &GRPT).NOGTST
         AIF   (&TGRP EQ &GRX).NOGTST
&GRXX    SETA  &GRX-1
&GRXY    SETA  &TGRP-1
         MNOTE 4,'GROUP CHANGE FROM &GRXY TO &GRXX '
.NOGTST  ANOP
         AIF   (&NV EQ 0).FRSTV
         INVLEN &L
.NDUP    AIF   ('&VAR' EQ '&XVAR(&C)').DUPVV
&C       SETA  &C+1
         AIF   (&C LE &NV).NDUP
&XVAR(&C) SETC '&VAR'
&NV      SETA  &C
         AGO   .DUPV
.DUPVV   ANOP  AIF   (&GRX EQ &XGRP(&C)).DUPV
         MNOTE 12,'DUPLICATE VARIABLE &VAR'
         MEXIT
.FRSTV   ANOP
&XVAR(1) SETC  '&VAR'
&XL(1)   SETA  &L
&NV      SETA  1
&NL      SETA  1
&NG      SETA  1
.DUPV    ANOP
&XGRP(&C) SETA &GRX
         AIF   (&DEF(&GRX)).DUNGRP
         OI    GENGRP+&GRX-1,X'80'
&DEF(&GRX) SETB  1
         AIF   (&GRX LE &MAXD).DUNGRP
&MAXD    SETA  &GRX
.DUNGRP  ANOP
         AIF   ('&T' EQ 'S').GTSHR
         AIF   ('&T' EQ 'T').NOGET
&XGET(&NG) SETC '&VAR'
&NG      SETA  &NG+1
&GET(&GRX) SETB 1
&MGET    SETB  1
         AGO   .NOGET
.GTSHR   ANOP
&NS      SETA  &NS+1
&XSHR(&NS) SETC '&VAR'
&SHR(&GRX) SETB 1
&MSHR    SETB  1
.NOGET   ANOP
         SPACE
         MNOTE *,'VARIABLE NAME - &VAR, TYPE - &A, LENGTH - &L GROUP - &
               &GRP '
&NAME    LA    R14,VDEFINE    *DEFINE VARIABLE TO SPF
         AIF   ('&VTYP' NE 'C').VARSETC
         LA    R15,C&VAR       *VARIABLE NAME
         AGO   .VARSETA
.VARSETC AIF   ('&VTYP' EQ 'R').VARSETR
         LA    R15,&VAR
         AGO   .VARSETA
.VARSETR AIF   ('&VAR'(2,2) EQ '15').VARSETA
         LR    R15,&VAR
.VARSETA ANOP
         LA    R0,&VAR
         LA    R1,VARTYP&A    *VARIABLE TYPE
         STM   R14,R1,VISPPARM
         LA    R1,VAR&L       *VARIABLE LENGTH
         ST    R1,VISPPARM+16
         OI    VISPPARM+16,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF DEFINE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
         SPACE
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    VDEL  &GRP,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       VDEL MACRO                                VDEL MACRO         *
.*                                                                    *
.**********************************************************************
         GBLB  &DEF(21)
         LCLA  &GRZ,&GRX
         LCLC  &LAB
&LAB     SETC  'VDL&SYSNDX'
         AIF   (T'&GRP EQ 'O').SETTYP
         AIF   ('&GRP' EQ 'ALL').DOALL
&GRZ     SETA  &GRP
         AGO   .GOTTYP
.SETTYP  ANOP
&GRZ     SETA  0
.GOTTYP  ANOP
&GRX     SETA  &GRZ+1
         AIF   (NOT &DEF(&GRX)).NODEL
&NAME    TM    GENGRP+&GRX-1,X'80'
         BZ    &LAB
         XI    GENGRP+&GRX-1,X'80'
         LA    R14,VDELETE    *REMOVE VARIABLES FROM SPF
         LA    R15,CNDLST&GRZ
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF DELETE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG1
         BH    DSMERR
.NOMSG1  ANOP
&LAB     DS    0H
         AGO   .NODEL
.DOALL   ANOP
&NAME    LA    R14,VDELETE    *REMOVE VARIABLES FROM SPF
         ST    R14,VISPPARM
         OI    VISPPARM+4,128
         LA    R2,21
         LA    R3,CNDLST
&LAB.R   LCR   R15,R2
         LA    R15,GENGRP+21(R15)
         TM    0(R15),X'80'
         BZ    &LAB
         XI    0(R15),X'80'
         L     R15,0(0,R3)
         STCM  R15,7,VISPPARM+5
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF REMOVE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
&LAB     LA    R3,4(0,R3)
         BCT   R2,&LAB.R
.NODEL   ANOP
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    VRESET &GRP,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       VRESET MACRO                              VRESET MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,VRESET  *REMOVE VARIABLES FROM SPF
         ST    R14,VISPPARM
         OI    VISPPARM,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF RESET  FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    VGET  &GRP,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       VGET MACRO                                VGET MACRO         *
.*                                                                    *
.**********************************************************************
         GBLB  &GET(21),&SHR(21),&DEF(21),&GRPT
         GBLA  &TGRP,&MAXP
         LCLA  &GRZ,&GRX,&GRXX,&GRXY
         LCLC  &LAB
&LAB     SETC  'VGT&SYSNDX'
         AIF   (T'&GRP EQ 'O').SETTYP
&GRZ     SETA  &GRP
         AGO   .GOTTYP
.SETTYP  ANOP
&GRZ     SETA  0
.GOTTYP  ANOP
&GRX     SETA  &GRZ+1
         AIF   (NOT &GRPT).NOGTST
&GRPT    SETB  0
         AIF   (&TGRP EQ &GRX).NOGTST
&GRXX    SETA  &GRX-1
&GRXY    SETA  &TGRP-1
         MNOTE 4,'GROUP CHANGE FROM &GRXY TO &GRXX '
.NOGTST  ANOP
         AIF   (&GRX LE &MAXP).DUNGRP
&MAXP    SETA  &GRX
.DUNGRP  ANOP
         AIF   (NOT &DEF(&GRX)).NOGT
&NAME    DS    0H
         AIF   (NOT &SHR(&GRX)).NOSH
         LA    R14,VGET       *GET VARIABLES FROM SHARED VARIABLE POOL
         LA    R15,CSLIST&GRZ
         LA    R0,CSHR
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF GET FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOSH
         BH    DSMERR
.NOSH    ANOP
         AIF   (NOT &GET(&GRX)).NOGT
         TM    GENGRP+&GRX-1,X'40'
         BO    &LAB
         OI    GENGRP+&GRX-1,X'40'
         LA    R14,VGET       *GET VARIABLES FROM SPF PROFILE
         LA    R15,CPLIST&GRZ
         LA    R0,CPROF
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF GET FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
&LAB     DS    0H
.NOGT    ANOP
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    VPUT  &GRP,&ERROR=YES,&POOL=PROF
.**********************************************************************
.*                                                                    *
.*       VPUT MACRO                                VPUT MACRO         *
.*                                                                    *
.**********************************************************************
         LCLA  &GRZ,&GRX
         LCLC  &LAB
         GBLB  &GET(21)
&LAB     SETC  'VPT&SYSNDX'
         AIF   (T'&GRP EQ 'O').SETTYP
         AIF   ('&GRP' EQ 'ALL').DOALL
&GRZ     SETA  &GRP
         AGO   .GOTTYP
.SETTYP  ANOP
&GRZ     SETA  0
.GOTTYP  ANOP
&GRX     SETA  &GRZ+1
         AIF   (NOT &GET(&GRX)).NOPT
&NAME    DS    0H
         LA    R14,VPUT       *PUT VARIABLES INTO &POOL POOL
         LA    R15,CPLIST&GRZ
         LA    R0,C&POOL
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF PUT FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOPT
         BH    DSMERR
         AGO   .NOPT
.DOALL   ANOP
&NAME    LA    R14,VPUT       *PUT VARIABLES INTO SPF PROFILE
         LA    R0,CPROF
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         LA    R2,21
         LA    R3,CPLIST
&LAB.R   LCR   R15,R2
         LA    R15,GENGRP+21(R15)
         TM    0(R15),X'40'
         BZ    &LAB
         XI    0(R15),X'40'
         L     R15,0(0,R3)
         ST    R15,VISPPARM+4
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF PUT FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
&LAB     LA    R3,4(0,R3)
         BCT   R2,&LAB.R
.NOPT    ANOP
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    BUILDG &VAR,&T,&GRP,&LEN=,&TYP=,&LAST=N,&EQU=,&ORG=
.**********************************************************************
.*                                                                    *
.*       BUILDG MACRO                              BUILDG MACRO       *
.*                                                                    *
.**********************************************************************
         GBLC  &GKEYS,&GNAMES
         GBLB  &GK,&GN,&GO
         GBLA  &GOLEN
         AIF   (&GK).B030
         AIF   ('&GKEYS' NE '').B010
&GKEYS   SETC  '(&VAR'
         AGO   .B020
.B010    ANOP
&GKEYS   SETC  '&GKEYS.,&VAR'
.B020    ANOP
         AIF   ('&LAST'(1,1) NE 'Y').B060
&GKEYS   SETC  '&GKEYS.)'
&GK      SETB  1
         AGO   .B060
.B030    ANOP
         AIF   (&GN).B060
         AIF   ('&GNAMES' NE '').B040
&GNAMES  SETC  '(&VAR'
         AGO   .B050
.B040    ANOP
&GNAMES  SETC  '&GNAMES.,&VAR'
.B050    ANOP
         AIF   ('&LAST'(1,1) NE 'Y').B060
&GNAMES  SETC  '&GNAMES.)'
&GN      SETB  1
.B060    ANOP
         AIF   (T'&ORG EQ 'O').B070
         AIF   ('&ORG'(1,1) EQ '*').B070
&GO      SETB  1
.B070    ANOP
         AIF   (NOT &GO).B080
&GOLEN   SETA  &GOLEN+&LEN
.B080    ANOP
         AIF   (T'&ORG EQ 'O').B999
         AIF   ('&ORG'(1,1) NE '*').B999
&GO      SETB  0
.B999    ANOP
         MEND
*
         MACRO
&NAME    DEFINE &VAR,&T,&GRP,&LEN=,&TYP=,&LAST=N,&EQU=,&ORG=
         LCLC  &LN
.**********************************************************************
.*                                                                    *
.*       DEFINE MACRO                              DEFINE MACRO       *
.*                                                                    *
.**********************************************************************
         AIF   (T'&EQU EQ 'O').D020
&LN      SETC  ' '
         AIF   (T'&LEN EQ 'O').D010
&LN      SETC  ',&LEN'
.D010    ANOP
&VAR     EQU   &EQU&LN
         AGO   .D999
.D020    ANOP
         AIF   (T'&ORG EQ 'O').D030
         AIF   ('&ORG'(1,1) EQ '*').D030
         ORG   &ORG
.D030    ANOP
&VAR     DS    &TYP.L&LEN
         AIF   (T'&ORG EQ 'O').D999
         AIF   ('&ORG'(1,1) NE '*').D999
         ORG
.D999    ANOP
         MEND
*
         MACRO
&NAME    EDIT  &DSN,&VOL,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       EDIT MACRO                                EDIT MACRO         *
.*                                                                    *
.**********************************************************************
         GBLB  &ED
&ED      SETB  1
&NAME    LA    R14,EDIT       *INVOKE EDIT
         LA    R15,&DSN
         AIF   (T'&VOL EQ 'O').NOVOL
         LA    R0,&VOL
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NOVOL   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF EDIT
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    BROWSE &DSN,&VOL,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       BROWSE MACRO                              BROWSE MACRO       *
.*                                                                    *
.**********************************************************************
         GBLB  &BR
&BR      SETB  1
&NAME    LA    R14,BROWSE     *INVOKE BROWSE
         LA    R15,&DSN
         AIF   (T'&VOL EQ 'O').NOVOL
         LA    R0,&VOL
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NOVOL   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF BROWSE
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
         MACRO
&NAME    LOG &MSG,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       LOG MACRO                                 LOG MACRO          *
.*                                                                    *
.**********************************************************************
         GBLA  &NMM
         GBLB  &FT
         GBLC  &XMSG(100)
         LCLA  &C
         AIF   (T'&MSG EQ 'O').NOMSG
&C       SETA  1
.NDUPM   AIF   ('&MSG' EQ '&XMSG(&C)').DUPMES
&C       SETA  &C+1
         AIF   (&C LE &NMM).NDUPM
&NMM     SETA  &NMM+1
&XMSG(&NMM) SETC '&MSG'
.DUPMES  ANOP
&NAME    LA    R14,LOG        *INVOKE LOG
         LA    R15,&MSG
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF LOG
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
.NOMSG   MNOTE 12,'MSGID MISSING'
         MEND
*
         MACRO
&NAME    CONTROL &TYPE,&MODE,&LINE,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       CONTROL MACRO                             CONTROL MACRO      *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,CONTROL    *SWITCH SPF DISPLAY MODES
         AIF   ('&TYPE' EQ 'DISPLAY').DODISP
         AIF   ('&TYPE' EQ 'ERRORS').DOERR
         AIF   ('&TYPE' EQ 'NONDISPL').DONDSP
         AIF   ('&TYPE' EQ 'SPLIT').DOSPLT
         MNOTE 12,'INVALID TYPE - &TYPE '
         MEXIT
.DOERR   ANOP
         LA    R15,ERRORS
         AIF   ('&MODE' EQ 'CANCEL').SETSPL
         AIF   ('&MODE' EQ 'RETURN').SETSPL
         MNOTE 12,'INVALID ERRORS - &MODE '
         MEXIT
.DONDSP  ANOP
         LA    R15,NONDISPL
         AIF   ('&MODE' EQ 'ENTER').SETSPL
         AIF   ('&MODE' EQ 'END').SETSPL
         MNOTE 12,'INVALID NONDISPL - &MODE '
         MEXIT
.DOSPLT  ANOP
         LA    R15,SPLIT
         AIF   ('&MODE' EQ 'ENABLE').SETSPL
         AIF   ('&MODE' EQ 'DISABLE').SETSPL
         MNOTE 12,'INVALID SPLIT - &MODE '
         MEXIT
.SETSPL  ANOP
         LA    R0,&MODE
         AGO   .SPRM
.DODISP  ANOP
         LA    R15,DISPLAY
         AIF   ('&MODE' EQ 'LINE').LINE
         AIF   ('&MODE' EQ 'REFRESH').REFRESH
         AIF   ('&MODE' EQ 'LOCK').LOCK
         AIF   ('&MODE' EQ 'SAVE').SAVE
         AIF   ('&MODE' EQ 'RESTORE').RESTORE
         AIF   ('&MODE' EQ 'SM').SM
         MNOTE 12,'INVALID MODE - &MODE '
         MEXIT
.LOCK    ANOP
.SAVE    ANOP
.RESTORE ANOP
.REFRESH AIF   (T'&LINE EQ 'O').LINE
         MNOTE 12,'LINE NUMBER INVALID WITH MODE= &MODE '
         MEXIT
.LINE    ANOP
.SM      ANOP
         LA    R0,&MODE
         AIF   (T'&LINE NE 'O').DOLIN
.SPRM    ANOP
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.DOLIN   ANOP
         INVLEN &LINE
         LA    R1,VAR&LINE
         STM   R14,R1,VISPPARM
         OI    VISPPARM+12,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF CONTROL FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOGT    ANOP
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBCREATE &TAB,&KEYS,&FORMAT,&OPT,&REP,&ERROR=YES,&LIB=
.**********************************************************************
.*                                                                    *
.*       TBCREATE MACRO                            TBCREATE MACRO     *
.*                                                                    *
.**********************************************************************
         GBLA  &NT
         GBLB  &TB
         GBLC  &XTAB(100),&XTABN(100),&XTABK(100)
         LCLA  &C
         AIF   (&NT LT 1).NOTAB
&C       SETA  1
.NDUP    AIF   ('&TAB' EQ '&XTAB(&C)').DUPTAB
&C       SETA  &C+1
         AIF   (&C LE &NT).NDUP
.NOTAB   ANOP
&NT      SETA  &NT+1
&TB      SETB  1
&XTAB(&NT)  SETC '&TAB'
.DUPTAB  ANOP
&NAME    LA    R14,TBCREATE   *CREATE NEW SPF TABLE
         MNOTE *,'TABLE NAME - &TAB, NAME LIST AT C&TAB.N'
         LA    R15,C&TAB      *TABLE NAME
         AIF   (T'&KEYS NE 'O').KEYTHER
         SR    R0,R0          *NO KEYS
         AGO   .AFKY
.KEYTHER ANOP
         LA    R0,C&TAB.K
&XTABK(&NT) SETC '&KEYS'
.AFKY    ANOP
         LA    R1,C&TAB.N     *NAME LIST
&XTABN(&NT) SETC '&FORMAT'
         STM   R14,R1,VISPPARM
         AIF   (T'&OPT NE 'O').OPTHER
         LA    R1,WRITE       *DEFAULT WRITE TABLE AT END
         AGO   .OPTSET
.OPTHER  ANOP
         AIF   ('&OPT' EQ 'WRITE' OR '&OPT' EQ 'NOWRITE').WROK
         MNOTE 12,'INVALID WRITE/NOWRITE - &OPT'
         MEXIT
.WROK    ANOP
         LA    R1,&OPT        *SET  OPTION
.OPTSET  ANOP
         ST    R1,VISPPARM+16
         AIF   (T'&REP NE 'O').REPTHER
         AIF   (T'&LIB NE 'O').ENREP
         OI    VISPPARM+16,128
         AGO   .ENREP
.REPTHER ANOP
         AIF   ('&REP' EQ 'REPLACE').REPOK
         MNOTE 12,'INVALID REPLACE - &REP'
         MEXIT
.REPOK   ANOP
         LA    R1,&REP
         ST    R1,VISPPARM+20
         AIF   (T'&LIB NE 'O').ENREP
         OI    VISPPARM+20,128
.ENREP   ANOP
         AIF   (K'&LIB NE 1).NLIB1
         MVI   LIBRARY+6,C'&LIB'
.NLIB1   ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBCREATE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBOPEN &TAB,&OPT,&ERROR=YES,&LIB=
.**********************************************************************
.*                                                                    *
.*       TBOPEN   MACRO                            TBOPEN   MACRO     *
.*                                                                    *
.**********************************************************************
         GBLA  &NT
         GBLB  &TB
         GBLC  &XTAB(100)
         LCLA  &C
         AIF   (&NT LT 1).NOTAB
&C       SETA  1
.NDUP    AIF   ('&TAB' EQ '&XTAB(&C)').DUPTAB
&C       SETA  &C+1
         AIF   (&C LE &NT).NDUP
.NOTAB   ANOP
&NT      SETA  &NT+1
&TB      SETB  1
&XTAB(&NT)  SETC '&TAB'
.DUPTAB  ANOP
&NAME    LA    R14,TBOPEN
         LA    R15,C&TAB
         AIF   (T'&OPT EQ 'O').NOOPT
         AIF   ('&OPT' EQ 'WRITE' OR '&OPT' EQ 'NOWRITE').WROK
         MNOTE 12,'INVALID WRITE/NOWRITE - &OPT'
         MEXIT
.WROK    ANOP
         LA    R0,&OPT
         STM   R14,R0,VISPPARM
         AIF   (T'&LIB NE 'O').MLIB
         OI    VISPPARM+8,128
         AGO   .CALLDSP
.NOOPT   ANOP
         STM   R14,R15,VISPPARM
         AIF   (T'&LIB NE 'O').MLIB
         OI    VISPPARM+4,128
         AGO   .CALLDSP
.MLIB    ANOP
         MVC   VISPPARM+12(4),VISPPARM+24
.CALLDSP ANOP
         AIF   (K'&LIB NE 1).NLIB2
         MVI   LIBRARY+6,C'&LIB'
.NLIB2   ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBOPEN FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBERASE &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBERASE MACRO                            TBERASE MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBERASE
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBERASE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBEND &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBEND MACRO                                TBEND MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBEND
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBEND FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBCLOSE &TAB,&COPY,&ALT,&PAD,&ERROR=YES,&LIB=
.**********************************************************************
.*                                                                    *
.*       TBCLOSE MACRO                            TBCLOSE MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBCLOSE
         LA    R15,C&TAB
         AIF   (T'&COPY NE 'O').COPYHER
         LA    R0,CBLNK
         AGO   .COPYSET
.COPYHER ANOP
         AIF   ('&COPY' EQ 'NEWCOPY' OR '&COPY' EQ 'REPLCOPY').WROK
         MNOTE 12,'INVALID NEWCOPY/REPLCOPY - &COPY'
         MEXIT
.WROK    ANOP
         LA    R0,&COPY       *SET  COPY OPTION
.COPYSET ANOP
         AIF   (T'&ALT NE 'O').ALTHER
         LA    R1,CBLNK
         AGO   .ALTSET
.ALTHER ANOP
         LA    R1,&ALT        *SET  ALT OPTION
.ALTSET ANOP
         STM   R14,R1,VISPPARM
         AIF   (T'&PAD EQ 'O').NOPAD
         AIF   (T'&PAD EQ 'N').REALPAD
         LA    R0,&PAD
         AGO   .STPRM
.REALPAD ANOP
         INVLEN &PAD
         LA    R0,VAR&PAD
.STPRM   ANOP
         ST    R0,VISPPARM+16
         AIF   (T'&LIB NE 'O').LIBM
         OI    VISPPARM+16,128
         AGO   .CALS
.NOPAD   ANOP
         AIF   (T'&LIB NE 'O').LIBM
         OI    VISPPARM+12,128
         AGO   .CALS
.LIBM    ANOP
         MVC   VISPPARM+20(4),VISPPARM+24
.CALS    ANOP
         AIF   (K'&LIB NE 1).NLIB3
         MVI   LIBRARY+6,C'&LIB'
.NLIB3   ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBCLOSE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBSAVE &TAB,&COPY,&ALT,&PAD,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBSAVE MACRO                              TBSAVE MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBSAVE
         LA    R15,C&TAB
         AIF   (T'&COPY NE 'O').COPYHER
         LA    R0,CBLNK
         AGO   .COPYSET
.COPYHER ANOP
         AIF   ('&COPY' EQ 'NEWCOPY' OR '&COPY' EQ 'REPLCOPY').WROK
         MNOTE 12,'INVALID NEWCOPY/REPLCOPY - &COPY'
         MEXIT
.WROK    ANOP
         LA    R0,&COPY       *SET  COPY OPTION
.COPYSET ANOP
         AIF   (T'&ALT NE 'O').ALTHER
         LA    R1,CBLNK
         AGO   .ALTSET
.ALTHER ANOP
         LA    R1,&ALT        *SET  ALT OPTION
.ALTSET ANOP
         STM   R14,R1,VISPPARM
         AIF   (T'&PAD EQ 'O').NOPAD
         AIF   (T'&PAD EQ 'N').REALPAD
         LA    R0,&PAD
         AGO   .STPRM
.REALPAD ANOP
         INVLEN &PAD
         LA    R0,VAR&PAD
.STPRM   ANOP
         ST    R0,VISPPARM+16
         OI    VISPPARM+16,128
         AGO   .CALS
.NOPAD   ANOP
         OI    VISPPARM+12,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBSAVE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBSARG &TAB,&ARG,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBSARG MACRO                              TBSARG MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBSARG
         LA    R15,C&TAB
         AIF   (T'&ARG EQ 'O').NOARG
         LA    R0,C&ARG
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NOARG   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBSARG FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBSCAN &TAB,&ARG,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBSCAN MACRO                              TBSCAN MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBSCAN
         LA    R15,C&TAB
         AIF   (T'&ARG EQ 'O').NOARG
         LA    R0,C&ARG
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NOARG   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBSCAN FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBSKIP &TAB,&NUM,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBSKIP MACRO                              TBSKIP MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBSKIP
         LA    R15,C&TAB
         AIF   (T'&NUM EQ 'O').NONUM
         AIF   (T'&NUM EQ 'N').REALNUM
         LA    R0,&NUM
         AGO   .STPRM
.REALNUM ANOP
         INVLEN &NUM
         LA    R0,VAR&NUM
.STPRM   ANOP
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NONUM   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBSKIP FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBPUT &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBPUT MACRO                                TBPUT MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBPUT
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBPUT FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBADD &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBADD MACRO                                TBADD MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBADD
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBADD FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBMOD &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBMOD MACRO                                TBMOD MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBMOD
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBMOD FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBEXIST &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBEXIST MACRO                              TBEXIST MACRO     *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBEXIST
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBEXIST FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBTOP &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBTOP MACRO                                TBTOP MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBTOP
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBTOP FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBBOTTOM &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBBOTTOM MACRO                          TBBOTTOM MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBBOTTOM
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBBOTTOM FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBDELETE &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBDELETE MACRO                          TBDELETE MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBDELETE
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBDELETE FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBVCLEAR &TAB,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBVCLEAR MACRO                             TBVCLEAR MACRO    *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,TBVCLEAR
         LA    R15,C&TAB
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBVCLEAR FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    DISPLAY &PANEL,&MSG,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       DISPLAY MACRO                             DISPLAY MACRO      *
.*                                                                    *
.**********************************************************************
         GBLA  &NP,&NM,&NMM
         GBLC  &XPAN(100),&XMSG(100),&VTYP
         LCLA  &C
         VTYP  &PANEL
         AIF   ('&VTYP' NE 'C').DUPPAN
         AIF   (&NP LT 1).NOPAN
&C       SETA  1
.NDUP    AIF   ('&PANEL' EQ '&XPAN(&C)').DUPPAN
&C       SETA  &C+1
         AIF   (&C LE &NP).NDUP
.NOPAN   ANOP
&NP      SETA  &NP+1
&XPAN(&NP)  SETC '&PANEL'
.DUPPAN  ANOP
&NAME    LA    R14,DISPLAY
         AIF   ('&VTYP' NE 'C').PANSETC
         LA    R15,C&PANEL
         AGO   .PANSETA
.PANSETC AIF   ('&VTYP' EQ 'R').PANSETR
         LA    R15,&PANEL
         AGO   .PANSETA
.PANSETR AIF   ('&PANEL'(2,2) EQ '15').PANSETA
         LR    R15,&PANEL
.PANSETA ANOP
         AIF   (T'&MSG EQ 'O').NOMSG
&C       SETA  1
.NDUPM   AIF   ('&MSG' EQ '&XMSG(&C)').DUPMES
&C       SETA  &C+1
         AIF   (&C LE &NMM).NDUPM
&NMM     SETA  &NMM+1
&XMSG(&NMM) SETC '&MSG'
.DUPMES  ANOP
         LA    R0,&MSG
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALLDSP
.NOMSG   ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALLDSP ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF DISPLAY FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
         AIF   (T'&MSG EQ 'O').NOMSG2
         VMSG  &MSG,BLANK
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    TBDISPL &TAB,&PANEL,&MSG,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       TBDISPL MACRO                             TBDISPL MACRO      *
.*                                                                    *
.**********************************************************************
         GBLA  &NP,&NM,&NMM
         GBLC  &XPAN(100),&XMSG(100),&VTYP
         LCLA  &C
         AIF   (T'&PANEL NE 'O').PROCPAN
&VTYP    SETC  'O'
         AGO   .DUPPAN
.PROCPAN VTYP  &PANEL
         AIF   ('&VTYP' NE 'C').DUPPAN
         AIF   (&NP LT 1).NOPAN
&C       SETA  1
.NDUP    AIF   ('&PANEL' EQ '&XPAN(&C)').DUPPAN
&C       SETA  &C+1
         AIF   (&C LE &NP).NDUP
.NOPAN   ANOP
&NP      SETA  &NP+1
&XPAN(&NP)  SETC '&PANEL'
.DUPPAN  ANOP
&NAME    LA    R14,TBDISPL
         LA    R15,C&TAB
         AIF   ('&VTYP' EQ 'O').NOPAN2
         AIF   ('&VTYP' NE 'C').TABPC
         LA    R0,C&PANEL
         AGO   .TABPA
.TABPC   AIF   ('&VTYP' EQ 'R').TABPR
         LA    R0,&PANEL
         AGO   .TABPA
.TABPR   AIF   ('&PANEL'(2,2) EQ 'R0').TABPA
         LR    R0,&PANEL
.TABPA   ANOP
         AIF   (T'&MSG EQ 'O').NOMSG
&C       SETA  1
.NDUPM   AIF   ('&MSG' EQ '&XMSG(&C)').DUPMES
&C       SETA  &C+1
         AIF   (&C LE &NMM).NDUPM
&NMM     SETA  &NMM+1
&XMSG(&NMM) SETC '&MSG'
.DUPMES  ANOP
         LA    R1,&MSG
         STM   R14,R1,VISPPARM
         OI    VISPPARM+12,128
         AGO   .CALLDSP
.NOPAN2  ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         AGO   .CALLDSP
.NOMSG   ANOP
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
.CALLDSP ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF TBDISPL FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
         AIF   (T'&MSG EQ 'O').NOMSG2
         VMSG  &MSG,BLANK
.NOMSG2  MEXIT
         MEND
*
         MACRO
         VGEN  &TYP,&DSECT=NO
.**********************************************************************
.*                                                                    *
.*       VGEN MACRO                                   VGEN MACRO      *
.*                                                                    *
.**********************************************************************
         GBLA  &NL,&NV,&NG,&NT,&NP,&NM,&NMM,&NS,&NDATA,&NNODEF
         GBLA  &XL(200),&XGRP(200),&MAXP,&MAXD
         GBLB  &GET(21),&DEF(21),&TB,&MS,&ED,&BR,&SHR(21),&FT,&MDEF
         GBLB  &MGET,&MSHR,&X
         GBLC  &XVAR(200),&XGET(200),&XTAB(100),&XTABN(100),&XTABK(100)
         GBLC  &XPAN(100),&XMSGID(100),&XMSG(100),&XDATA(200)
         GBLC  &XNODEF(200),&XSHR(200)
         LCLA  &C,&L,&D,&I,&J,&N,&LL
         LCLC  &VAR,&TL,&TLL
&X       SETB  0
         AIF   (NOT &MDEF).OUT
         AIF   ('&TYP' EQ 'DATA').GENCAL
         SPACE
VISPLINK DC    V(ISPLINK)
VISPEXEC DC    V(ISPEXEC)
         SPACE
.*
.*       GENERATE VARIABLE NAMES FOR PUT
.*
&N       SETA  1
CPLIST   DS    0F
.PGRP    AIF   (&GET(&N) EQ 0).POGRP
&I       SETA  &N-1
         DC    A(CPLIST&I)
         AGO   .UPPG
.POGRP   ANOP
         DC    A(0)
.UPPG    ANOP
&N       SETA  &N+1
         AIF   (&N LE &MAXP).PGRP
.*
.*       GENERATE VARIABLE NAMES FOR DELETE
.*
&N       SETA  1
         SPACE
CNDLST   DS    0F
.EGRP    AIF   (&DEF(&N) EQ 0).DEGRP
&I       SETA  &N-1
         DC    A(CNDLST&I)
         AGO   .UPDG
.DEGRP   ANOP
         DC    A(0)
.UPDG    ANOP
&N       SETA  &N+1
         AIF   (&N LE &MAXD).EGRP
         SPACE
VARTYPC  DC    CL8'CHAR'
VARTYPF  DC    CL8'FIXED'
VARTYPB  DC    CL8'BIT'
VARTYPX  DC    CL8'HEX'
         SPACE
VDEFINE  DC    CL8'VDEFINE'
VDELETE  DC    CL8'VDELETE'
VRESET   DC    CL8'VRESET'
SELECT   DC    CL8'SELECT'
SETMSG   DC    CL8'SETMSG'
LOG      DC    CL8'LOG'
         AIF   (NOT &MGET).NGETD
VGET     DC    CL8'VGET'
VPUT     DC    CL8'VPUT'
CPROF    DC    CL8'PROFILE'
CSHR     DC    CL8'SHARED'
.NGETD   AIF   (NOT &BR).NOBR
BROWSE   DC    CL8'BROWSE'
.NOBR    AIF   (NOT &ED).NOED
EDIT     DC    CL8'EDIT'
.NOED    AIF   (NOT &TB).NOTABS
TBADD    DC    CL8'TBADD'
TBBOTTOM DC    CL8'TBBOTTOM'
TBCLOSE  DC    CL8'TBCLOSE'
TBCREATE DC    CL8'TBCREATE'
TBDELETE DC    CL8'TBDELETE'
TBVCLEAR DC    CL8'TBVCLEAR'
TBDISPL  DC    CL8'TBDISPL'
TBEND    DC    CL8'TBEND'
TBERASE  DC    CL8'TBERASE'
TBGET    DC    CL8'TBGET'
TBMOD    DC    CL8'TBMOD'
TBOPEN   DC    CL8'TBOPEN'
TBPUT    DC    CL8'TBPUT'
TBSAVE   DC    CL8'TBSAVE'
TBSCAN   DC    CL8'TBSCAN'
TBSARG   DC    CL8'TBSARG'
TBEXIST  DC    CL8'TBEXIST'
TBSKIP   DC    CL8'TBSKIP'
TBSORT   DC    CL8'TBSORT'
TBTOP    DC    CL8'TBTOP'
NEWCOPY  DC    CL8'NEWCOPY'
REPLCOPY DC    CL8'REPLCOPY'
.NOTABS  AIF   (NOT &FT).NOFT
FTCLOSE  DC    CL8'FTCLOSE'
FTERASE  DC    CL8'FTERASE'
FTINCL   DC    CL8'FTINCL'
FTOPEN   DC    CL8'FTOPEN'
CTEMP    DC    CL8'TEMP'
NOFT     DC    CL8'NOFT'
.NOFT    ANOP
         SPACE
CBLNK    DC    CL8' '
CTLD     DC    C'TLD'
CISPF    DC    C'ISPF'
ISPQRY   DC    CL8'ISPQRY'
DISPLAY  DC    CL8'DISPLAY'
CONTROL  DC    CL8'CONTROL'
LINE     DC    CL8'LINE'
LOCK     DC    CL8'LOCK'
SM       DC    CL8'SM'
SAVE     DC    CL8'SAVE'
RESTORE  DC    CL8'RESTORE'
REFRESH  DC    CL8'REFRESH'
REPLACE  DC    CL8'REPLACE'
WRITE    DC    CL8'WRITE'
NOWRITE  DC    CL8'NOWRITE'
NONDISPL DC    CL8'NONDISPL'
ENTER    DC    CL8'ENTER'
END      DC    CL8'END'
ERRORS   DC    CL8'ERRORS'
CANCEL   DC    CL8'CANCEL'
RETURN   DC    CL8'RETURN'
SPLIT    DC    CL8'SPLIT'
ENABLE   DC    CL8'ENABLE'
DISABLE  DC    CL8'DISABLE'
         SPACE
.*
.*
.*       SORT XDATA
.*
         AIF   (&NDATA EQ 0).SRT1X
         AIF   (&NDATA EQ 1).SRT1N
&C       SETA  1
.SRT1O   ANOP
&TL      SETC  '&XDATA(&C)'
&D       SETA  &C+1
.SRT1I   ANOP
         AIF   ('&TL' LT '&XDATA(&D)').SRT1C
&TLL     SETC  '&TL'
&TL      SETC  '&XDATA(&D)'
&XDATA(&D) SETC '&TLL'
.SRT1C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NDATA).SRT1I
&XDATA(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NDATA).SRT1O
.SRT1N   ANOP
.*
&C       SETA  1
&D       SETA  1
.XDATT   ANOP
         AIF   ('&XDATA(&C)' LT '&XPAN(&D)').XDATI
         AIF   ('&XDATA(&C)' EQ '&XPAN(&D)').XDATM
&D       SETA  &D+1
         AIF   (&D LE &NP).XDATT
.XDATI   MNOTE 12,'INVALID XDATA - &XDATA(&C)'
         MEXIT
.XDATM   ANOP
&C       SETA  &C+1
         AIF   (&C GT &NDATA).SRT1X
&D       SETA  &D+1
         AIF   (&D GT &NP).XDATI
         AGO   .XDATT
.SRT1X   ANOP
.*
.*       SORT XNODEF
.*
         AIF   (&NNODEF EQ 0).SRT2X
         AIF   (&NNODEF EQ 1).SRT2N
&C       SETA  1
.SRT2O   ANOP
&TL      SETC  '&XNODEF(&C)'
&D       SETA  &C+1
.SRT2I   ANOP
         AIF   ('&TL' LT '&XNODEF(&D)').SRT2C
&TLL     SETC  '&TL'
&TL      SETC  '&XNODEF(&D)'
&XNODEF(&D) SETC '&TLL'
.SRT2C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NNODEF).SRT2I
&XNODEF(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NNODEF).SRT2O
.SRT2N   ANOP
&C       SETA  1
&D       SETA  1
.XNDT    AIF   ('&XNODEF(&C)' LT '&XPAN(&D)').XNDI
         AIF   ('&XNODEF(&C)' EQ '&XPAN(&D)').XNDM
&D       SETA  &D+1
         AIF   (&D LE &NP).XNDT
.XNDI    MNOTE 12,'INVALID XNODEF - &XNODEF(&C)'
         MEXIT
.XNDM    ANOP
&C       SETA  &C+1
         AIF   (&C GT &NDATA).SRT2X
&D       SETA  &D+1
         AIF   (&D GT &NP).XNDI
         AGO   .XNDT
.SRT2X   ANOP
.*
.*       SORT PANEL NAMES
.*
         AIF   (&NP LE 1).SRT5N
&C       SETA  1
.SRT5O   ANOP
&TL      SETC  '&XPAN(&C)'
&D       SETA  &C+1
.SRT5I   ANOP
         AIF   ('&TL' LT '&XPAN(&D)').SRT5C
&TLL     SETC  '&TL'
&TL      SETC  '&XPAN(&D)'
&XPAN(&D) SETC '&TLL'
.SRT5C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NP).SRT5I
&XPAN(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NP).SRT5O
.SRT5N   ANOP
.*
.*       GENERATE PANEL NAMES
.*
&C       SETA  1
&I       SETA  1
&J       SETA  1
.NPAN    AIF   (&C GT &NP).ENPAN
&VAR     SETC  '&XPAN(&C)'
         AIF   (&I GT &NDATA).DATOK
         AIF   ('&VAR' LT '&XDATA(&I)').DATOK
&I       SETA  &I+1
         AGO   .MDATA
.DATOK   ANOP
C&VAR    DC    CL8'&VAR'
.MDATA   ANOP
&C       SETA  &C+1
         AGO   .NPAN
.ENPAN   ANOP
.*
.*       GENERATE VARIABLE NAMES FOR RETRIEVAL FROM SHARED POOL
.*
         AIF   (NOT &MSHR).NOSHRX
&N       SETA  1
.MGRP    AIF   (&SHR(&N) EQ 0).MOGRP
&I       SETA  &N-1
         SPACE
CSLIST&I DC    C'('
&I       SETA  1
&J       SETA  1
.MXL     ANOP
&TL      SETC  '&XSHR(&I)'
.MXJ     AIF   ('&TL' EQ '&XVAR(&J)').MTGRP
&J       SETA  &J+1
         AGO   .MXJ
.MTGRP   AIF   (&XGRP(&J) NE &N).MDGRP
         DC    C'&TL '
.MDGRP   ANOP
&I       SETA  &I+1
         AIF   (&I LE &NS).MXL
         DC    C')'
.MOGRP   ANOP
&N       SETA  &N+1
         AIF   (&N LE 21).MGRP
.NOSHRX  ANOP
.*
.*       GENERATE VARIABLE NAMES FOR RETRIEVAL FROM PROFILE
.*
         AIF   (NOT &MGET).NOGETX
&N       SETA  1
.NGRP    AIF   (&GET(&N) EQ 0).NOGRP
&I       SETA  &N-1
         SPACE
         ACTR  30000
CPLIST&I DC    C'('
&I       SETA  1
.NXL     ANOP
&TL      SETC  '&XGET(&I)'
&J       SETA  &I
.NXJ     AIF   ('&TL' EQ '&XVAR(&J)').TGRP
&J       SETA  &J+1
         AGO   .NXJ
.TGRP    AIF   (&XGRP(&J) NE &N).DGRP
         DC    C'&TL '
.DGRP    ANOP
&I       SETA  &I+1
         AIF   (&I LE &NG).NXL
         DC    C')'
.NOGRP   ANOP
&N       SETA  &N+1
         AIF   (&N LE 21).NGRP
.NOGETX  ANOP
.*
.*       SORT VARIABLE NAMES
.*
         AIF   (&NV LE 1).SRT7N
&C       SETA  1
.SRT7O   ANOP
&TL      SETC  '&XVAR(&C)'
&D       SETA  &C+1
.SRT7I   ANOP
         AIF   ('&TL' LT '&XVAR(&D)').SRT7C
&TLL     SETC  '&TL'
&TL      SETC  '&XVAR(&D)'
&XVAR(&D) SETC '&TLL'
&LL      SETA  &XGRP(&C)
&XGRP(&C) SETA &XGRP(&D)
&XGRP(&D) SETA &LL
.SRT7C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NV).SRT7I
&XVAR(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NV).SRT7O
.SRT7N   ANOP
.*
.*       GENERATE VARIABLE NAMES FOR DEFINE
.*
         SPACE
&C       SETA  1
.NGENC   ANOP
&VAR     SETC  '&XVAR(&C)'
C&VAR    DC    C'(&VAR)'
&C       SETA  &C+1
         AIF   (&C LE &NV).NGENC
.*
.*       GENERATE VARIABLE NAMES FOR DELETE
.*
         ACTR  30000
&N       SETA  1
.JGRP    AIF   (&DEF(&N) EQ 0).JOGRP
&I       SETA  &N-1
         SPACE
CNDLST&I DC    C'('
&I       SETA  1
.JXL     ANOP
         AIF   (&XGRP(&I) NE &N).JDGRP
         DC    C'&XVAR(&I) '
.JDGRP   ANOP
&I       SETA  &I+1
         AIF   (&I LE &NV).JXL
         DC    C')'
.JOGRP   ANOP
&N       SETA  &N+1
         AIF   (&N LE 21).JGRP
.*
.*       SORT NUMBER CONSTANTS
.*
         AIF   (&NL LE 1).SRT6N
&C       SETA  1
.SRT6O   ANOP
&L       SETA  &XL(&C)
&D       SETA  &C+1
.SRT6I   ANOP
         AIF   (&L LT &XL(&D)).SRT6C
&LL      SETA  &L
&L       SETA  &XL(&D)
&XL(&D)  SETA &LL
.SRT6C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NL).SRT6I
&XL(&C)  SETA &L
&C       SETA  &C+1
         AIF   (&C LT &NL).SRT6O
.SRT6N   ANOP
.*
.*       GENERATE NUMBER CONSTANTS
.*
&C       SETA  1
.NGENL   ANOP
&L       SETA  &XL(&C)
VAR&L    DC    F'&L'
&C       SETA  &C+1
         AIF   (&C LE &NL).NGENL
         SPACE
         AIF   (NOT &TB).NTABD
.*
.*       SORT TABLE NAMES
.*
         AIF   (&NT LE 1).SRT3N
&C       SETA  1
.SRT3O   ANOP
&TL      SETC  '&XTAB(&C)'
&D       SETA  &C+1
.SRT3I   ANOP
         AIF   ('&TL' LT '&XTAB(&D)').SRT3C
&TLL     SETC  '&TL'
&TL      SETC  '&XTAB(&D)'
&XTAB(&D) SETC '&TLL'
&TLL     SETC  '&XTABN(&C)'
&XTABN(&C) SETC '&XTABN(&D)'
&XTABN(&D) SETC '&TLL'
&TLL     SETC  '&XTABK(&C)'
&XTABK(&C) SETC '&XTABK(&D)'
&XTABK(&D) SETC '&TLL'
.SRT3C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NT).SRT3I
&XTAB(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NT).SRT3O
.SRT3N   ANOP
.*
.*       GENERATE TABLE NAMES AND FORMATS
.*
&C       SETA  1
.NGENT   ANOP
&VAR     SETC  '&XTAB(&C)'
C&VAR    DC    CL8'&VAR'
         AIF   (K'&XTABK(&C) EQ 0).NOCK
C&VAR.K  DC    C'&XTABK(&C)'
.NOCK    ANOP
         AIF   (K'&XTABN(&C) EQ 0).NOCN
C&VAR.N  DC    C'&XTABN(&C)'
.NOCN    ANOP
&C       SETA  &C+1
         AIF   (&C LE &NT).NGENT
         SPACE
.NTABD   ANOP
.*
.*       SORT MESSAGE NUMBERS
.*
         AIF   (NOT &MS).NMSGD
         AIF   (&NM LE 1).SRT4N
&C       SETA  1
.SRT4O   ANOP
&TL      SETC  '&XMSGID(&C)'
&D       SETA  &C+1
.SRT4I   ANOP
         AIF   ('&TL' LT '&XMSGID(&D)').SRT4C
&TLL     SETC  '&TL'
&TL      SETC  '&XMSGID(&D)'
&XMSGID(&D) SETC '&TLL'
.SRT4C   ANOP
&D       SETA  &D+1
         AIF   (&D LE &NM).SRT4I
&XMSGID(&C) SETC '&TL'
&C       SETA  &C+1
         AIF   (&C LT &NM).SRT4O
.SRT4N   ANOP
.*
.*       GENERATE MESSAGE NUMBERS
.*
&C       SETA  1
.NGENM   ANOP
&VAR     SETC  '&XMSGID(&C)'
C&VAR    DC    CL8'&VAR'
&C       SETA  &C+1
         AIF   (&C LE &NM).NGENM
.NMSGD   ANOP
.*
         AIF   ('&TYP' EQ 'CONSTANTS').OUT
.*
.* GENERATE DYNAMIC SECTION OF DATA
.*
.GENCAL  ANOP
         SPACE
VISPPARM CALL  ,(,,,,,),VL,MF=L
         DC    A(LIBRARY+X'80000000')
         SPACE
LIBRARY  DC    CL8'ISPTABL'
DMFUNC   DC    CL8' '
DMRCODE  DC    F'0'
.*
.*       GENERATE PANEL NAMES IN DYNAMIC STORAGE
.*
&C       SETA  1
.NDPAN   AIF   (&C GT &NDATA).ENDPN
&VAR     SETC  '&XDATA(&C)'
C&VAR    DC    CL8'&VAR'
&C       SETA  &C+1
         AGO   .NDPAN
.ENDPN   ANOP
         SPACE
         AIF   (&NMM EQ 0).NOMSI
.*
&C       SETA  1
.NGENMM  ANOP
&VAR     SETC  '&XMSG(&C)'
&VAR     DC    CL8' '
&C       SETA  &C+1
         AIF   (&C LT &NMM).NGENMM
.NOMSI   ANOP
GENGRP   DC    XL21'00'
         DS    0F
         SPACE
.OUT     ANOP
         MEND
*
         MACRO
&NAME    VLEN  &VAR,&OPT
.**********************************************************************
.*                                                                    *
.*       VLEN MACRO                                  VLEN MACRO       *
.*                                                                    *
.**********************************************************************
         LCLC  &SS
&SS      SETC  'L'''
&NAME    LA    R1,&VAR+&SS&VAR-1
         LA    R14,1
         LNR   R14,R14
         LA    R15,&VAR
         CLI   0(R1),C' '
         BNE   *+8
         BXH   R1,R14,*-8
         AIF   ('&OPT' EQ 'P').OUT
         SR    R1,R15
         AIF   ('&OPT' EQ 'X').OUT
         LA    R1,1(0,R1)
.OUT     ANOP
         MEXIT
         MEND
*
         MACRO
         INVLEN &L
.**********************************************************************
.*                                                                    *
.*       INVLEN MACRO                              INVLEN MACRO       *
.*                                                                    *
.**********************************************************************
         GBLA  &XL(200)
         GBLA  &NL
         LCLA  &C
&C       SETA  1
.NLEN    ANOP
         AIF   (&L EQ &XL(&C)).DUPL
&C       SETA  &C+1
         AIF   (&C LE &NL).NLEN
&XL(&C)  SETA  &L
&NL      SETA  &C
.DUPL    MEXIT
         MEND
*
         MACRO
&NAME    VMSG  &MSG,&MSGID
.**********************************************************************
.*                                                                    *
.*       VMSG MACRO                                VMSG MACRO         *
.*                                                                    *
.**********************************************************************
         GBLA  &NM
         GBLB  &MS
         GBLC  &XMSGID(100)
         LCLA  &C
         AIF   ('&MSGID' EQ 'BLANK').BLKMSG
         AIF   (&NM LT 1).NOMSG
&C       SETA  1
.NDUP    AIF   ('&MSGID' EQ '&XMSGID(&C)').DUPMSG
&C       SETA  &C+1
         AIF   (&C LE &NM).NDUP
.NOMSG   ANOP
&NM      SETA  &NM+1
&MS      SETB  1
&XMSGID(&NM) SETC '&MSGID'
.DUPMSG  ANOP
&NAME    MVC   &MSG.(8),C&MSGID
         MEXIT
.BLKMSG  ANOP
&NAME    MVC   &MSG.(8),CBLNK
         MEXIT
         MEND
*
         MACRO
&NAME    FTERASE &MEM,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       FTERASE MACRO                             FTERASE MACRO      *
.*                                                                    *
.**********************************************************************
         GBLB  &FT
&FT      SETB  1
&NAME    LA    R14,FTERASE    *INVOKE FTERASE
         LA    R15,&MEM
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF FTERASE
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    FTCLOSE &MEM,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       FTCLOSE MACRO                             FTCLOSE MACRO      *
.*                                                                    *
.**********************************************************************
         GBLB  &FT
&FT      SETB  1
&NAME    LA    R14,FTCLOSE    *INVOKE FTCLOSE
         AIF   (T'&MEM EQ 'O').NOMEM
         LA    R15,&MEM
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         AGO   .CALS
.NOMEM   ANOP
         ST    R14,VISPPARM
         OI    VISPPARM,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF FTCLOSE
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    FTOPEN &T,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       FTOPEN MACRO                              FTOPEN MACRO       *
.*                                                                    *
.**********************************************************************
         GBLB  &FT
&FT      SETB  1
&NAME    LA    R14,FTOPEN     *INVOKE FTOPEN
         AIF   (T'&T EQ 'O').NOTMP
         LA    R15,CTEMP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         AGO   .CALS
.NOTMP   ANOP
         ST    R14,VISPPARM
         OI    VISPPARM,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF FTOPEN
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    FTINCL &SKEL,&NOFT,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       FTINCL MACRO                              FTINCL MACRO       *
.*                                                                    *
.**********************************************************************
         GBLB  &FT
         GBLC  &VTYP
         VTYP  &SKEL
&FT      SETB  1
&NAME    LA    R14,FTINCL     *INVOKE FTINCL
         AIF   ('&VTYP' NE 'C').SKELTC
         LA    R15,&SKEL
         AGO   .SKELTA
.SKELTC  AIF   ('&VTYP' EQ 'R').SKELTR
         LA    R15,&SKEL
         AGO   .SKELTA
.SKELTR  AIF   ('&SKEL'(2,2) EQ '15').SKELTA
         LR    R15,&SKEL
.SKELTA  ANOP
         AIF   (T'&NOFT EQ 'O').NONOFT
         LA    R0,CNOFT
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         AGO   .CALS
.NONOFT  ANOP
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
.CALS    ANOP
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF FTINCL
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
         DSMERR &PANEL=DMERR
.**********************************************************************
.*                                                                    *
.*       DSMERR MACRO                              DSMERR MACRO       *
.*                                                                    *
.**********************************************************************
         INVLEN 8
         INVLEN 12
DSMERR   L     R1,VISPPARM
         MVC   DMFUNC,0(R1)
         ST    R15,DMRCODE
         DISPLAY &PANEL,ERROR=NO
         MEXIT
         MEND
*
         MACRO
         VDATA &VEC
.**********************************************************************
.*                                                                    *
.*       VDATA MACRO                                VDATA MACRO       *
.*                                                                    *
.**********************************************************************
         GBLA  &NDATA
         GBLC  &XDATA(200)
         LCLA  &CNT,&CC
&CNT     SETA  N'&SYSLIST(1)
         AIF   (&CNT EQ 0).NODATA
&CC      SETA  0
.NVAR    AIF   (&CC EQ &CNT).NODATA
&NDATA   SETA  &NDATA+1
&CC      SETA  &CC+1
&XDATA(&NDATA) SETC '&VEC(&CC)'
         AGO   .NVAR
.NODATA  ANOP
         MEXIT
         MEND
*
         MACRO
         VNODEF &VEC
.**********************************************************************
.*                                                                    *
.*       VNODEF MACRO                               VNODEF MACRO      *
.*                                                                    *
.**********************************************************************
         GBLA  &NNODEF
         GBLC  &XNODEF(200)
         LCLA  &CNT,&CC
&CNT     SETA  N'&SYSLIST(1)
         AIF   (&CNT EQ 0).NONODEF
&CC      SETA  0
.NVAR    AIF   (&CC EQ &CNT).NONODEF
&NNODEF  SETA  &NNODEF+1
&CC      SETA  &CC+1
&XNODEF(&NNODEF) SETC 'VEC(&CC)'
         AGO   .NVAR
.NONODEF ANOP
         MEXIT
         MEND
*
         MACRO
         VTYP  &VAR
.**********************************************************************
.*                                                                    *
.*       VTYP MACRO                                   VTYP MACRO      *
.*                                                                    *
.**********************************************************************
         GBLC  &VTYP
         LCLC  &LET
&LET     SETC  '&VAR'(1,1)
         AIF   ('&LET' EQ '(').VREG
         AIF   ('&LET' LT  '0').VCONST
&VTYP    SETC  'A'
         MEXIT
.VCONST  ANOP
&VTYP    SETC  'C'
         MEXIT
.VREG    ANOP
&VTYP    SETC  'R'
         MEND
*
         MACRO
&NAME    BLK &N,&LEN,&FILL=' '
.**********************************************************************
.*                                                                    *
.*       BLK MACRO                                   BLK MACRO        *
.*                                                                    *
.**********************************************************************
         LCLA  &L
         AIF   (T'&LEN EQ 'U').GOTL2
         AIF   (T'&LEN NE 'O').GOTL
&L       SETA  L'&N
         AGO   .GOTL2
.GOTL    ANOP
&L       SETA  &LEN
.GOTL2   ANOP
         AIF   ('&FILL' EQ 'ZERO').ZFIL
&NAME    MVI   &N,C&FILL
         AIF   (T'&LEN EQ 'U').MVCL
         AIF   (&L EQ 1).OUT
         MVC   &N+1(&L-1),&N
.OUT     MEXIT
.MVCL    ANOP
         MVC   &N+1(&LEN-1),&N
         MEXIT
.ZFIL    ANOP
         AIF   (T'&LEN EQ 'U').XCL
&NAME    XC    &N.(&L),&N
         MEXIT
.XCL     ANOP
&NAME    XC    &N.(&LEN),&N
         MEXIT
         MEND
*
         MACRO
&NAME    BLANK &N,&LEN,&FILL=' '
.**********************************************************************
.*                                                                    *
.*       BLANK MACRO                                  BLANK MACRO     *
.*                                                                    *
.**********************************************************************
         AIF   (T'&LEN NE 'O').GOTL
         MNOTE 12,'LENGTH OMITTED'
         MEXIT
.GOTL    ANOP
&NAME    LA    14,&N
         AIF   (T'&LEN EQ 'U').LDL
         AIF   (&LEN GT 4095).LDL
         LA    15,&LEN
         AGO   .LLL
.LDL     ANOP
         CNOP  0,4
         BAL   15,*+8
         DC    A(&LEN)
         L     15,0(0,15)
.LLL     ANOP
         SLR   0,0
         AIF   ('&FILL' EQ 'ZERO').ZFIL
         LA    R1,C&FILL
         SLL   R1,24
         AGO   .DUMUV
.ZFIL    ANOP
         SLR   R1,R1
.DUMUV   ANOP
         MVCL  14,0
         MEXIT
         MEND
         MACRO
&NAME    VTST  &GRP
.**********************************************************************
.*                                                                    *
.*       VTST MACRO                                VTST MACRO         *
.*                                                                    *
.**********************************************************************
         GBLA  &TGRP
         GBLB  &GRPT,&GET(21)
         LCLA  &GRX,&GRXX
         AIF   (T'&GRP EQ 'O').SETTYP
&GRX     SETA  &GRP
         AGO   .GOTTYP
.SETTYP  ANOP
&GRX     SETA  0
.GOTTYP  ANOP
         AIF   (NOT &GRPT).NOGTST
         AIF   (NOT &GET(&TGRP)).NOGTST
&GRXX    SETA  &TGRP-1
         MNOTE 4,'VGET NOT DONE FOR GROUP &GRXX '
.NOGTST  ANOP
&NAME    TM    GENGRP+&GRX,X'80'
&TGRP    SETA  &GRX+1
&GRPT    SETB  1
         MEND
*
         MACRO
&NAME    SELECT &BUF,&LEN=,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       SELECT MACRO                              SELECT MACRO       *
.*                                                                    *
.**********************************************************************
         GBLB  &MDEF
         LCLA &L
&MDEF    SETB  1
         AIF   (T'&LEN EQ 'O').GETLEN
&L       SETA  &LEN
         AGO   .LENS
.GETLEN  ANOP
&L       SETA  L'&BUF
.LENS    INVLEN &L
&NAME    LA    R14,SELECT     *INVOKE SELECT
         LA    R15,VAR&L
         LA    R0,&BUF
         STM   R14,R0,VISPPARM
         OI    VISPPARM+8,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF SELECT FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOGT    ANOP
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    SETMSG &MSGID,&ERROR=YES
.**********************************************************************
.*                                                                    *
.*       SETMSG MACRO                              SETMSG MACRO       *
.*                                                                    *
.**********************************************************************
         GBLA  &NM
         GBLB  &MS,&MDEF
         GBLC  &XMSGID(100)
         LCLA  &C
&MDEF    SETB  1
         AIF   (&NM LT 1).NOMSG
&C       SETA  1
.NDUP    AIF   ('&MSGID' EQ '&XMSGID(&C)').DUPMSG
&C       SETA  &C+1
         AIF   (&C LE &NM).NDUP
.NOMSG   ANOP
&NM      SETA  &NM+1
&MS      SETB  1
&XMSGID(&NM) SETC '&MSGID'
.DUPMSG  ANOP
&NAME    LA    R14,SETMSG
         LA    R15,C&MSGID
         STM   R14,R15,VISPPARM
         OI    VISPPARM+4,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF SETMSG FUNCTION
         C     R15,VAR8
         AIF   ('&ERROR' EQ 'NO').NOMSG2
         BH    DSMERR
.NOMSG2  MEXIT
         MEND
*
         MACRO
&NAME    VSPF
.**********************************************************************
.*                                                                    *
.*       VSPF   MACRO                              VSPF   MACRO       *
.*                                                                    *
.**********************************************************************
         LCLC  &LAB0,&LAB1,&LAB2
&LAB0    SETC  'L&SYSNDX'
&LAB1    SETC  'M&SYSNDX'
&LAB2    SETC  'N&SYSNDX'
         L     R15,540       * TCB ADDR
&LAB0    L     R1,112(0,R15) * TCBFSA
         LTR   R1,R1
         BZ    &LAB1
         CLC   40(4,R1),CISPF
         BNE   &LAB1
         CLI   24(R1),0
         BNE   &LAB1
         L     R1,24(0,R1)   * R1 ON ENTRY
         LTR   R1,R1         * R1=0?
         BZ    &LAB1
         L     R1,0(0,R1)
         CLC   0(3,R1),CTLD  * DOES R1 POINT TO TLD?
         BE    &LAB2
&LAB1    L     R15,132(0,R15) *OTCB
         C     R15,124(0,R15) *OTCB=FTCB?
         BNE   &LAB0          *NO - TRY AGAIN
         LA    R15,8          *YES -NOT SPF RC=8
         B     *+6
&LAB2    SR    R15,R15        *CORRECT SPF ENVIRONMENT RC=0
         MEXIT
         MEND
         MACRO
&NAME    ISPQRY
.**********************************************************************
.*                                                                    *
.*       ISPQRY MACRO                              ISPQRY MACRO       *
.*                                                                    *
.**********************************************************************
&NAME    LA    R14,ISPQRY     *INVOKE ISPQRY
         STM   R14,VISPPARM
         OI    VISPPARM,128
         LA    R1,VISPPARM
         L     R15,VISPLINK
         BALR  R14,R15        *CALL SPF ISPQRY
*                             *CORRECT SPF ENVIRONMENT RC=0
*                             *NOT     SPF ENVIRONMENT RC=20
         MEXIT
         MEND
         EJECT
