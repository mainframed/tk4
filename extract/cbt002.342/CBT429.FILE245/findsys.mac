*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*
*% Pgm Name: FINDSYS                                                 %*
*%                                                                   %*
*% Purpose : Chain up from the PSA to the CVT to get the current     %*
*%           system SMFID and pass it back as a REXX/CLIST variable  %*
*%           called SYSID                                            %*
*%                                                                   %*
*%           Create REXX/CLIST variables using pgm IKJCT441 as       %*
*%           follows :- SYSID    = CVTSNAME                          %*
*%                                                                   %*
*% Author  : David G. RALPH.       Date : 25/11/91                   %*
*% Dept    : Computer Security.                                      %*
*%                                                                   %*
*% Macro Libraries : SYS1.MODGEN                                     %*
*%                   SYS1.MACLIB                                     %*
*%                                                                   %*
*% Macros Mapping  : IHAPSA                                          %*
*%                 : CVT                                             %*
*%                 : IKJTSVT                                         %*
*%                                                                   %*
*% Macros Program  : STORAGE - OBTAIN/RELEASE                        %*
*%                 : CALL                                            %*
*%                 : LINK                                            %*
*%                                                                   %*
*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
FINDSYS  CSECT
FINDSYS  AMODE 31
FINDSYS  RMODE ANY
         STM   R14,R12,12(R13)         STORE CALLING PROGRAMS REGISTERS
         LR    R12,R15                 ESTABLISH R12 AS THE BASE
         USING FINDSYS,R12             REGISTER FOR THE CSECT
         LA    R14,SAVEAREA
         ST    R13,4(R14)              CHAIN SAVE AREAS BACKWARD....
         ST    R14,8(R13)              .............AND FORWARD
         LR    R13,R14                 R13 POINTS TO THIS PROGRAM'S
*
*---------------------------------------------------------------------*
* GET THE ADDRESS OF THE VARIABLE ACCESS ROUTINE (IKJCT441)           *
*---------------------------------------------------------------------*
*
GETTSVT  DS    0H
         USING PSA,R0
         L     R15,FLCCVT              POINTER TO THE CVT
*
         DROP  R0
         USING CVT,R15                 CVT ADDRESSABILITY
*
         MVC   SMFID,CVTSNAME          SAVE THE SYSTEM NAME
*
         L     R15,CVTTVT              POINTER TO THE TSO VECTOR TABLE
*
         DROP  R15
         USING TSVT,R15
         L     R15,TSVTVACC            POINTER TO IKJCT441
*
         DROP  R15
*
         LTR   R15,R15                 VALID ADDRESS FOR IKJCT441 ?
         BNZ   TSVTOKAY
*
         MVC   CT441ADR,=F'0'          SET IKJCT441 ADDRESS = 0's
         B     SETSYSV
*
TSVTOKAY DS    0H
         ST    R15,CT441ADR            SAVE IKJCT441 ADDRESS FOR LATER
*
*---------------------------------------------------------------------*
* SET ALL THE REXX/CLIST VARIABLES AND CALL IKJCT441                  *
*---------------------------------------------------------------------*
*
SETSYSV  DS    0H
         LA    R2,SYSID                POINT TO SYSID VARIABLE NAME
         LA    R3,L'SYSID              LENGTH OF SYSID VARIABLE NAME
         LA    R4,SMFID                POINT TO UID VARIABLE VALUE
         L     R5,SMFIDLEN             LENGTH OF UID VARIABLE VALUE
         BAS   R14,CALL441
*
*---------------------------------------------------------------------*
* RESTORE REGISTERS, SET RC AND EXIT                                  *
*---------------------------------------------------------------------*
*
         SR    R15,R15                 SET RC = 0
*
EXIT     DS    0H
         L     R13,4(R13)              RESTORE CALLING PROGRAMS
         L     R14,12(R13)             ...REGISTERS BYPASSING
         LM    R0,R12,20(R13)          ......REGISTER 15 (RC)
         BR    R14                     RETURN TO CALLER
*
*---------------------------------------------------------------------*
* EXECUTE THE TSO VARIABLE UPDATE ROUTINE (IKJCT441)                  *
*---------------------------------------------------------------------*
*
CALL441  DS    0H
         ST    R14,SAVER14             SAVE THE VALUE OF 14 FOR RETURN
*
         ST    R2,NAMEPTR              SET THE VARIABLE NAME POINTER
         ST    R3,NAMELEN              SET THE VARIABLE NAME LENGTH
         ST    R4,VALUEPTR             SET THE VARIABLE VALUE POINTER
         ST    R5,VALUELEN             SET THE VARIABLE VALUE LENGTH
*
         L     R15,CT441ADR            LOAD ADDR OF IKJCT441 FROM SAVE
         LTR   R15,R15                 IF TSVT ADDR NOT KNOW
         BZ    LINK441                 ...THEN USE LINK NOT CALL
*
         CALL  (15),                                                   X
               (ECODE,                                                 X
               NAMEPTR,                                                X
               NAMELEN,                                                X
               VALUEPTR,                                               X
               VALUELEN,                                               X
               TOKEN),                                                 X
               VL
*
         B     RESETR14
*
LINK441  DS    0H
         LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,                                           X
               NAMEPTR,                                                X
               NAMELEN,                                                X
               VALUEPTR,                                               X
               VALUELEN,                                               X
               TOKEN),                                                 X
               VL=1
*
RESETR14 DS    0H
         L     R14,SAVER14             RESTORE RETURN ADDRESS
         BR    R14                     AND GO BACK AGAIN
*
*
*---------------------------------------------------------------------*
* DATA STORAGE AND MAPPING MACROS                                     *
*---------------------------------------------------------------------*
*
*
SAVEAREA DS    18F                     A SAVE AREA
SAVER14  DS    F                       A SAVE AREA FOR REG 14
*
* IKJCT441 REQUIRED STORAGE
*
CT441ADR DS    F                       SAVE AREA FOR IKJCT441 ADDRESS
ECODE    DC    A(TSVEUPDT)             ENTRY CODE FOR VARIABLE CREATE
TOKEN    DC    F'0'                    REQUIRED BUT NOT USED
NAMELEN  DS    F                       LENGTH OF VARIABLE NAME
NAMEPTR  DS    A                       ADDRESS OF VARIABLE NAME
VALUELEN DS    F                       LENGTH OF VARIABLE VALUE
VALUEPTR DS    A                       ADDRESS OF VARIABLE VALUE
*
* NAMES OF VARIABLES TO BE PASSED TO REXX/CLIST
*
SYSID    DC    C'SYSID'                VARIABLE NAME FOR SYSTEM ID
*
* PROGRAM STORAGE FOR REXX/CLIST VALUES
*
SMFID    DC    CL4'    '               SPACE FOR SYSTEM ID
SMFIDLEN DC    F'4'                    SAVE LENGTH OF SYSTEM ID
*
DWORD    DC    D'0'
*
*
LITERALS EQU   *
         LTORG
*
FINDSYS  CSECT
*
         PRINT NOGEN
*
         IHAPSA
*
         CVT      DSECT=YES
*
         IKJTSVT
*
         END
