**********************************************************************
*
*  MODULE NAME       : ISPX16L1
*  ENTRY POINT NAME  : ISPX16L1
*  ATTRIBUTES        : RENT,AMODE(31),RMODE(ANY)
*  DESCRIPTIVE NAME  : ISPF EXIT 16 - TEMPORARY DATASET NAME EXIT
*  FUNCTION          : MODIFY THE PREFIX OF ISPF-ALLOCATED TEMPORARY
*                      DATASETS BY ADDING THE SYSTEM-ID
*                      REF:ISPF/PDF PLANNING & CUSTOMIZING SC34-4257-01
*  PARAMETERS        : R1 - PARAMATER LIST
*                      THIS IS DETAILLED IN THE VOLUME ABOVE
*                      THERE IS A STANDARD LIST USED BY ALL ISPF EXITS
*                      FOLLOWED BY AN EXIT 16-SPECIFIC LIST
*  RETURN CODES      : 0 (THIS IS IGNORED BY THE CALLER)
*  CALLING MODULES   : ISPXDT
*  CALLED MODULES    : NONE
*  MACROS USED       : CVT     (MAPPING MACRO)
*  MESSAGES ISSUED   : ?
*  AUTHOR            : NAME - RICHARD HAYDOCK         DATE - 03/11/92
*
*
*  CHANGE HISTORY
*
*  PROGRAMMER NAME : ?
*  DATE COMPLETED  : DD/MM/YY
*  BRIEF DESCIPTION: ?
*
**********************************************************************
*
*        REGISTER USAGE:
*
*        R0  - ?
*        R1  - PARAMETER LIST ADDRESS ON ENTRY
*        R2  - ?
*        R3  - ?
*        R4  - ?
*        R5  - ?
*        R6  - ?
*        R7  - ?
*        R8  - ?
*        R9  - POINTER TO CVT WHILE SEARCHING FOR SMF-ID
*        R10 - PARAMETER LIST ADDRESS
*        R11 - DSECT ADDRESS
*        R12 - BASE REGISTER
*        R13 - SAVE AREA POINTER
*        R14 - RETURN ADDRESS
*        R15 - ENTRY POINT ADDRESS (AT START)
*
**********************************************************************
*
ISPX16L1 CSECT
ISPX16L1 AMODE 31
ISPX16L1 RMODE ANY
         USING ISPX16L1,R15        ESTABLISH BASE REGISTER FOR CSECT
         B     ISPXSTRT            BRANCH ROUND ID
         DC    C'ISPX16L1'         CSECT NAME
         DC    C'&SYSDATE'         ASSEMBLY DATE
         DC    C'&SYSTIME'         ASSEMBLY TIME
         DS    0F                  ALIGNMENT
ISPXSTRT EQU   *
         STM   R14,R12,12(R13)     SAVE CALLERS REGS IN CALLERS SVAREA
         LR    R12,R15             SET UP BASE REGISTER
         DROP  R15
         USING ISPX16L1,R12        ESTABLISH BASE REGISTER
         LR    R10,R1              SAVE PARAMETER LIST ADDRESS
*
         GETMAIN RC,LV=WRKDSLEN,SP=0,LOC=BELOW
         LR    R11,R1              SAVE ADDRESS OF STORAGE
         USING WRKDSECT,R11        ANCHOR DSECT
*
         LA    R14,SAVEAREA        PUT CALLER SVAREA ADDR IN OWN SVAREA
         ST    R14,8(R13)          SET FORWARD POINTER
         ST    R13,SAVEAREA+4      SET BACK CHAIN TO CALLER
         LA    R13,SAVEAREA        POINTER TO SAVE AREA
         EJECT
*
ISPXMAIN EQU   *
*
         L     R9,CVTPTR               GET THE SYSTEM ID
         USING CVT,R9
         L     R5,CVTSMCA
*
         L     R8,28(R10)              ADDRESS OF PREFIX
         L     R7,24(R10)              ADDRESS OF PREFIX LENGTH
         LR    R6,R8                   ADDRESS OF PREFIX
         A     R6,0(0,R7)              PLUS PREFIX LENGTH
         MVI   0(R6),C'.'              ADD A DOT
         MVC   1(4,R6),16(R5)          AND THE SYSTEM ID
*
         L     R5,0(0,R7)              PUT PREFIX LENGTH IN R5
         A     R5,=F'5'                ADD 5 TO IT
         ST    R5,0(0,R7)              AND STORE IT BACK AGAIN
*
***********************************************************************
*                                                                     *
*      RETURN ROUTINE - RELEASE AREAS, RESTORE REGS, RETURN TO CALLER *
*                                                                     *
***********************************************************************
*
ISPXEND  EQU   *
         L     R13,SAVEAREA+4      PICK UP BACK CHAIN POINTER
*
         FREEMAIN RC,LV=WRKDSLEN,A=(R11),SP=0
*
         L     R14,12(0,R13)       RESTORE R14
         LM    R0,R12,20(R13)      RESTORE R0-R12
         SR    R15,R15             SET ZERO RETURN CODE
         BR    R14                 RETURN TO CALLER
         EJECT
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         LTORG
*
WORKAREA DS    0D
*
WRKDSECT DSECT
*
SAVEAREA DS    18F
*
WRKDSLEN EQU   *-WRKDSECT
         CVT   DSECT=YES
         END
