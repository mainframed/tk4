ARESPREX TITLE '**** DMS/OS AUTO RESTORE SCREENING EXIT &SYSDATE ****'
         PRINT  GEN
ARESPREX CSECT
R0      EQU  0
R1      EQU  1                       <=== ADDRESS OF PARMLIST
R2      EQU  2                       W
R3      EQU  3                        O
R4      EQU  4                         R
R5      EQU  5                          K
R6      EQU  6
R7      EQU  7                            R
R8      EQU  8                             E
R9      EQU  9                              G
R10     EQU  10                              S
R11     EQU  11
R12     EQU  12                      <=== BASE REGISTER
R13     EQU  13                      <=== SAVE AREA ADDRESS
R14     EQU  14                      <=== ENTRY POINT ADDRES
R15     EQU  15                      <=== RETURN CODE
*****
**
**       THIS EXIT IS INVOKED BY DMS/OS BEFORE A DATSET IS PROCESSED
**       FOR AUTO RESTORE.  IT IS USED TO CREATE AN SMF RECORD TYPE
**       127 TO AUDIT THE NUMBER OF RESTORES ARE DONE.
**
**       THIS MODULE LINKAGE ATTRIBUTES: AC=1,NORENT
**       MACROS: SMFWTM, WTO
**
**       PLEASE REFER TO THE DMS/OS SYSTEMS GUIDE FOR MORE INFO.
**
*****
*
         EJECT
         SAVE   (14,12),,ARESPREX-&SYSDATE-&SYSTIME
         LR     R12,R15              ESTABLISH BASE
         USING  ARESPREX,R12         AND MAKE IT USABLE
         LA     R11,SAVE             GET ADDRESS OF SAVE REG
         ST     R13,4(R11)           CHAIN FORWARD
         ST     R11,8(R13)           ... CHAIN BACK
         LR     R13,R11              SET OUR SAVE REG
*****
**
**       BUILD SMF RECORD TYPE 127
**
*****
         ST     R1,@PLIST            SAVE ADDRESS OF PLIST
         L      R2,0(R1)             GET ADDRESS OF DSNAME
         MVC    SMFDSN,0(R2)         MOVE TO RECORD
         L      R2,4(R1)             GET ADDRESS OF ARCHIVE FROM VOL
         MVC    SMFIVOL,0(R2)        MOVE TO RECORD
         L      R2,8(R1)             GET ADDRESS OF TO VOLUME
         MVC    SMFTVOL,0(R2)        MOVE TO RECORD
         L      R2,20(R1)            GET ADDRESS OF JOB NAME
         MVC    SMFJOB,0(R2)         MOVE TO SMF RECORD
         TIME   DEC                  GET CURRENT DATE AND TIME
         ST     R0,SMFTIMED          AND STORE INTO RECORD
*****
**
**       WRITE THE SMF RECORD
*****
         SMFWTM SMFREC               WRITE THE RECORD
         LTR    R15,R15              CHECK RETURN CODE
         BZ     LEAVE                RETURN CODE O.K.
*****
**
**       ISSUE MESSAGE TO CONSOLE LOG ABOUT BAD RETURN CODE
**       FROM SMF WRITE
*****
         CVD    R15,DWORD            CONVERT RETURN CODE TO DECIMAL
         UNPK   DWORD(2),DWORD+6(1)  UNPACK THE RETURN CODE
         OI     DWORD+1,X'F0'        FIX THE SIGN
         MVC    WTOLIST+41(2),DWORD  PUT INTO MESSAGE
         WTO    MF=(E,WTOLIST)       ISSUE ERROR MESSAGE
LEAVE    DS     0H
         L      R13,4(R13)           GET OLD SAVE AREA
         RETURN (14,12),RC=0         AND RETURN
         LTORG  ,
         EJECT
********
*
*        DATA AREAS
*
********
SAVE     DS     18F                  SAVE AREA
@PLIST   DS     F                    ADDRESS OF PARMLIST
SMFREC   DS     0F                   SMF RECORD
         DC     AL2(SMFRECE-SMFREC)  LENGTH OF SMF RECORD
         DC     H'0'                 SEGMENT DESCRIPTOR (0)
         DS     AL1                  SYSTEM INDICATOR
         DC     AL1(127)             SMF RECORD TYPE X'7F'
         DS     F                    TIME IN 1/100S SECONDS FROM 00:00
         DS     F                    DATE IN JULIAN FORMAT
         DS     CL4                  SMCA
SMFJOB   DS     CL8                  NAME OF JOB RESTORING DATASET
SMFDSN   DS     CL44                 DATASET NAME
SMFIVOL  DS     CL6                  DATASET ARCHIVE FROM
SMFTVOL  DS     CL6                  DATASET RESTORED TO
SMFTIMED DS     PL4                  TIME OF RESTORAL IN DECIAML
SMFRECE  DS     0C
DWORD    DS     D
WTOLIST  WTO    'BSA4000A ERROR IN SMFWTM MACRO, RC = XX.      ',      X
               ROUTCDE=(1,9),DESC=(2),MF=L
         DC     C'&SYSDATE_&SYSTIME'
         END    ARESPREX
