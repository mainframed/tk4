//S EXEC ASMFCL,MAC1='SAR.SOURCE',
//        PARM.LKED='LIST,LET,NCAL,XREF,RENT,AC=1'  ,AMODE=31'
         TITLE 'SARSECUX - SECURITY USER EXIT'
SARSECUX CSECT
***********************************************************************
*                                                                     *
* SARSECUX -                                                          *
*        USER EXIT TO DETERMINE IF USER/JOB IS ALLOWED TO             *
*        ACCESS A SYSOUT GROUP OR MOUNT A TAPE.                       *
*                                                                     *
* INPUTS -                                                            *
*        REG 0 - WHETHER THE EXIT WAS CALLED FROM ONLINE RETRIEVAL    *
*                OR THE BATCH UTILITY.                                *
*                0 - EXIT CALLED FROM ONLINE RETRIEVAL.               *
*                4 - EXIT CALLED FROM BATCH UTILITY.                  *
*        REG 1 - ADDRESS OF PARAMETER LIST (MAPPED BY SARCPL).        *
*        REG13 - ADDRESS OF REGISTER SAVE AREA                        *
*        REG14 - RETURN ADDRESS                                       *
*        REG15 - ENTRY POINT ADDRESS.                                 *
*                                                                     *
* RETURN CODES -                                                      *
*        0 - USER IS ALLOWED FULL ACCESS TO SYSOUT GROUP OR MAY       *
*            MOUNT TAPES.                                             *
*        4 - USER IS NOT ALLOWED TO ACCESS GROUP OR MAY NOT MOUNT     *
*            TAPES.                                                   *
*                                                                     *
* ATTRIBUTES -                                                        *
*        RENT, REUS.                                                  *
*        MUST BE LINKED AS AUTHORIZED, BUT EXECUTES IN UNAUTHORIZED   *
*        STATE.                                                       *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,SARSECUX.&SYSDATE
         LR    R12,R15             LOAD BASE REGISTER
         USING SARSECUX,R12
         LR    R2,R1               LOAD PLIST ADDRESS
         USING CPL,R2              ADDRESSABILITY FOR PLIST
         L     R3,CPLMCR           LOAD MCR ADDRESS
         USING MCR,R3              ADDRESSABILITY FOR MCR
         L     R4,CPLGCR           LOAD GCR ADDRESS
         USING GCR,R4              ADDRESSABILITY FOR GCR
         CLI   CPLFNCT,CPLFTMNT    TAPE MOUNT FUNCTION?
         BNE   SECRQ               NO
*
*        TAPE MOUNT AUTHORITY REQUEST
*
         CLI   CPLMODE,CPLMBCH     CHECK FOR BATCH
         BE    CKTMNT
         TM    MCRSWTC2,MCRS2NTM   TAPE MOUNTS ALLOWED FOR ONLINE?
         BO    RC4                 NO
         CLI   CPLMODE,CPLMSPF     CHECK FOR SPF
         BE    CKTMNT
         CLI   CPLMODE,CPLMTSO     CHECK FOR TSO
         BE    CKTMNT
         B     RC4                 TAPE MOUNTS NOT ALLOWED FOR OTHER
*                                  ONLINE RETRIEVAL SYSTEMS
CKTMNT   L     R1,PSATOLD          LOAD TCB ADDRESS
         L     R1,TCBJSCB(,R1)     LOAD JSCB ADDRESS
         L     R1,JSCBACT(,R1)     LOAD ACTIVE JSCB ADDRESS
         ICM   R1,B'1111',JSCBPSCB(R1) LOAD PSCB ADDRESS
         BZ    RC0                 NOT TSO
         TM    PSCBATR1(R1),PSCBVMNT TSO USER ALLOWED TAPE MOUNTS?
         BO    RC0                 YES
         B     RC4                 NO
*
*        SECURITY REQUEST FOR SYSOUT GROUP ACCESS
*
SECRQ    CLI   MCRRACF,C' '        RACF/ACF2 CHECKING REQUIRED?
         BNH   DOBSA               NO, DO BRIDGESTONE SECURITY CHECK
         CLI   CPLFNCT,CPLFSSL     BUILD SYSOUT LIST?
         BE    RC0                 YES, ALLOW ACCESS
         CLI   CPLMODE,CPLMSPF     DO RACF/ACF2 CHECK FOR SPF
         BE    DORACF
         CLI   CPLMODE,CPLMTSO     DO RACF/ACF2 CHECK FOR TSO
         BE    DORACF
         CLI   CPLMODE,CPLMBCH     DO RACF/ACF2 CHECK FOR BATCH
         BE    DORACF
         B     RC0                 ALLOW ACCESS
*
*        BRIDGESTONE SECURITY CHECK
*
DOBSA    DS    0H
         CLI   MCRCLSL,C'W'        IS THIS PRODUCTION DATABASE ?
         BNE   RC0                 NO, THEN BYPASS SECURITY
*        CLI   CPLMODE,CPLMCICS    IS THIS CICS ?
*        BE    PATCH               NO, THEN GOTO PATCH FOR NOW
         L     R7,CPLUID           GET UID
         LTR   R7,R7               IS THIS ONLINE RETRIEVAL ?
         BZ    RC0                 NO, THEN BYPASS SECURITY
         LA    R5,UIT#N3           R5 =  # OF ENTRIES IN UIT
         LA    R6,UIT              R6 -> TOP UIT ENTRY
FINDUID  DS    0H
         CLC   0(8,R7),0(R6)       IS UID = UID ON UIT ?
         BE    FOUNDUID            YES, THEN CHECK FLAG FOR SID
         LA    R6,UITEL(R6)        R6 -> NEXT UIT ENTRY
         BCT   R5,FINDUID          SEARCH FOR UID IN UIT
         B     PATCH               IF UID NOT FOUND, GOTO PATCH FOR NOW
         B     RC4                 IF UID NOT IN UIT, DO NOT ALLOW
FOUNDUID DS    0H
         L     R6,8(R6)            R6 -> FT ENTRY FOR UID
         L     R5,0(R6)            R5 =  # OF ENTRIES IN FT
         LA    R6,4(R6)            R6 -> TOP FT ENTRY
FINDSID  DS    0H
         L     R7,0(R6)            R7 -> SID ENTRY ON SIT
         CLC   GCRID,0(R7)         IS SID = SID ON SIT ?
         BE    FOUNDSID            YES, THEN CHECK FLAG WITH FUNCTION
         LA    R6,FTEL(R6)         R7 -> NEXT FT ENTRY
         BCT   R5,FINDSID          SEARCH FOR SID IN FT
         B     PATCH               IF SID NOT FOUND, GOTO PATCH FOR NOW
         B     RC4                 IF SID NOT FOUND, DO NOT ALLOW
FOUNDSID DS    0H
         LA    R7,4(R6)            GET FLAG FOR UID
         SR    R5,R5
         IC    R5,CPLFNCT          GET FUNCTION CODE
         CH    R5,=H'11'           IS THIS A VALID FUNCTION CODE ?
         BH    RC0                 NO, THEN IGNORE
         SLL   R5,2                MULTIPLY IT BY 4
         B     FLGTEST(R5)
FLGTEST  B     F00                 INTEREST
         B     F01                 LOAD
         B     F02                 PRINT BATCH (J)
         B     F03                 DELETE
         B     F04                 BROWSE
         B     F05                 PRINT ONLINE (P)
         B     F06                 KEEP
         B     F07                 KEEP TAPE
         B     F08                 NONE
         B     F09                 NONE
         B     F10                 VIEW N
         B     F11                 VIEW CHANGE
*
*        FUNCTION CHECKING
*
F00      DS    0H                  INTEREST
         TM    0(R7),X'01'         IF ANY VIEW IS ALLOWED,
         BO    RC0                  THEN ALLOW
         TM    1(R7),X'FF'
         BM    RC0
         TM    2(R7),X'80'
         BO    RC0
         B     PATCH               ELSE NOPE
F01      DS    0H                  LOAD
         TM    0(R7),X'10'
         BO    RC0
         B     RC4
F02      DS    0H                  PRINT BATCH
         TM    0(R7),X'08'
         BO    RC0
         B     RC4
F03      DS    0H                  DELETE
         TM    0(R7),X'80'
         BO    RC0
         B     RC4
F04      DS    0H                  BROWSE
         B     RC0
F05      DS    0H                  PRINT ONLINE
         TM    0(R7),X'08'
         BO    RC0
         B     RC4
F06      DS    0H                  KEEP
         TM    0(R7),X'20'
         BO    RC0
         B     RC4
F07      DS    0H                  KEEP ON TAPE
         TM    0(R7),X'20'
         BO    RC0
         B     RC4
F08      DS    0H                  TAPE MOUNT
         B     RC0
F09      DS    0H                  NONE
         B     RC0
F10      DS    0H                  VIEW N
         SR    R5,R5
         ICM   R5,B'0001',CPLVNUM  GET VIEW NUMBER
         CH    R5,=H'9'            VIEW NUMBER > 9 ?
         BH    RC0                 YES, IGNORE
         SLL   R5,2                MULTIPLY BY 4
         B     VIEWTST(R5)
VIEWTST  B     TV00                VIEW 0
         B     TV01                VIEW 1
         B     TV02                VIEW 2
         B     TV03                VIEW 3
         B     TV04                VIEW 4
         B     TV05                VIEW 5
         B     TV06                VIEW 6
         B     TV07                VIEW 7
         B     TV08                VIEW 8
         B     TV09                VIEW 9
*
TV00     DS    0H                  VIEW 0
         TM    0(R7),X'01'
         BO    RC0
         B     RC4
TV01     DS    0H                  VIEW 1
         TM    1(R7),X'80'
         BO    RC0
         B     RC4
TV02     DS    0H                  VIEW 2
         TM    1(R7),X'40'
         BO    RC0
         B     RC4
TV03     DS    0H                  VIEW 3
         TM    1(R7),X'20'
         BO    RC0
         B     RC4
TV04     DS    0H                  VIEW 4
         TM    1(R7),X'10'
         BO    RC0
         B     RC4
TV05     DS    0H                  VIEW 5
         TM    1(R7),X'08'
         BO    RC0
         B     RC4
TV06     DS    0H                  VIEW 6
         TM    1(R7),X'04'
         BO    RC0
         B     RC4
TV07     DS    0H                  VIEW 7
         TM    1(R7),X'02'
         BO    RC0
         B     RC4
TV08     DS    0H                  VIEW 8
         TM    1(R7),X'01'
         BO    RC0
         B     RC4
TV09     DS    0H                  VIEW 9
         TM    2(R7),X'80'
         BO    RC0
         B     RC4
F11      DS    0H                  VIEW CHANGE
         TM    0(R7),X'02'
         BO    RC0
         B     RC4
*
*        TEMPORARY PATCH UNTIL THE FULL FUNCTION OF SECURITY
*        IS CODED AND TESTED
*
*        IF SID IN QUESTION IS NOT IN THE SIT,
*           THEN ALLOW ALL FUNCTIONS
*
PATCH    DS    0H
         LA    R5,SIT              R6 -> TOP SIT ENTRY
         LA    R6,SIT#N3           R6 =  # OF ENTRIES IN SIT
PATCHA   DS    0H
         CLC   GCRID,0(R5)         IS SID = SID ON SIT ?
         BE    RC4                 YES, THEN DONT ALLOW
         LA    R5,SITEL(R5)        R5 -> NEXT SIT ENTRY
         BCT   R6,PATCHA           SEARCH FOR SID IN FT
         CLI   CPLFNCT,CPLFDEL     IS THIS DELETE ?
         BNE   RC0                 NO, THEN ALL IS OK FOR NOW
         L     R7,CPLUID           R7 -> UID
         CLC   =C'EKP',0(R7)       DELETE FOR ADMINISTRATOR ONLY
         BE    RC0
         CLC   =C'BNR',0(R7)
         BE    RC0
         CLC   =C'TTS',0(R7)
         BE    RC0
         B     RC4
*
*        CONSTRUCT RACF/ACF2 PSEUDO DATA SET NAME
*
DORACF   MVI   RFDSN,C' '          BLANK OUT DSNAME
         MVC   RFDSN+1(L'RFDSN-1),RFDSN
         MVC   RFDSN(L'MCRRACF),MCRRACF SET HIGH LEVEL-INDEX
         LA    R1,RFDSN            SCAN FOR END OF HIGH-LEVEL INDEX
RACDSCBK CLI   0(R1),C' '
         BE    RACDSCFD
         LA    R1,1(,R1)
         B     RACDSCBK
RACDSCFD MVI   0(R1),C'.'          APPEND PERIOD
         MVC   1(8,R1),GCRID       APPEND SYSOUT ID
         CLI   GCRID+8,C' '        SYSOUT ID LONGER THAN 8 CHAR?
         BE    CKACF2              YES
         MVC   9(2,R1),=CL2'.$'    SEPARATE ID INTO TWO QUALIFIERS
         MVC   11(L'GCRID-8,R1),GCRID+8
CKACF2   TM    MCRSWTC3,MCRS3ACF   ACF2 PROCESSING?
         BO    ACF2CHK             YES
*
*        ISSUE RACHECK
*
         MVC   RFPLIST,MDLRCHK     COPY RACF PLIST
         LA    R1,RFPLIST          LOAD PLIST ADDRESS
         LA    R5,RFDSN            LOAD ADDRESS OF PSEUDO DSNAME
         LA    R6,MCRRACF          LOAD ADDRESS RACF "VOLSER"
         CLI   CPLFNCT,CPLFPRT     BACKGROUND PRINT REQUEST?
         BE    RCUPD               YES, VERIFY UPDATE AUTHORITY
         CLI   CPLFNCT,CPLFOPRT    ONLINE PRINT REQUEST?
         BE    RCUPD               YES, VERIFY UPDATE AUTHORITY
         CLI   CPLFNCT,CPLFDEL     DELETE REQUEST?
         BE    RCALTER             YES, VERIFY ALTER AUTHORITY
         CLI   CPLFNCT,CPLFKEEP    PERMANENT KEEP REQUEST?
         BE    RCALTER             YES, VERIFY ALTER AUTHORITY
RCREAD   RACHECK ENTITY=((R5)),VOLSER=(R6),MF=(E,(1)),                 X
               ATTR=READ           VERIFY READ ACCESS
         B     RCRC                GO CHECK RETURN CODE
RCUPD    RACHECK ENTITY=((R5)),VOLSER=(R6),MF=(E,(1)),                 X
               ATTR=UPDATE         VERIFY UPDATE ACCESS
         B     RCRC                GO CHECK RETURN CODE
RCALTER  RACHECK ENTITY=((R5)),VOLSER=(R6),MF=(E,(1)),                 X
               ATTR=ALTER          VERIFY ALTER ACCESS
RCRC     CH    R15,=H'4'           AUTHORIZATION GIVEN?
         BH    RC4                 NO
         B     RC0                 ALLOW ACCESS
*
*        ISSUE ACF2 CHECK
*
ACF2CHK  LA    R1,RFPLIST          LOAD PLIST ADDRESS
         USING ACFSPARM,R1         ADDRESSABILITY
         XC    ACFSPARM(ACFSPRML),ACFSPARM ZERO OUT PLIST
         MVI   ACFSPREQ,ACFSPRDS   SET DSNAME REQUEST
         MVI   ACFSPID1,ACFSPIUR   SET USER REQUEST
         LA    R0,RFDSN            SET ADDRESS OF DSNAME
         ST    R0,ACFSPDSN
         CLI   CPLFNCT,CPLFPRT     BACKGROUND PRINT REQUEST?
         BE    A2UPD               YES, VERIFY UPDATE AUTHORITY
         CLI   CPLFNCT,CPLFOPRT    ONLINE PRINT REQUEST?
         BE    A2UPD               YES, VERIFY UPDATE AUTHORITY
         CLI   CPLFNCT,CPLFDEL     DELETE REQUEST?
         BE    A2SCR               YES, VERIFY ALTER AUTHORITY
         CLI   CPLFNCT,CPLFKEEP    PERMANENT KEEP REQUEST?
         BE    A2SCR               YES, VERIFY ALTER AUTHORITY
A2READ   MVI   ACFSPAC1,ACFSPAIN   SET INPUT ONLY ACCESS
         B     A2SVC               GO ISSUE ACF2 SVC
A2UPD    MVI   ACFSPAC1,ACFSPAUP   SET UPDATE ACCESS
         B     A2SVC               GO ISSUE ACF2 SVC
A2SCR    MVI   ACFSPAC1,ACFSPAOT   SET SCRATCH/RENAME/ALLOC ACCESS
         DROP  R1
A2SVC    IC    R5,MCRA2SVC         LOAD ACF2 SVC NUMBER
         EX    R5,MDLSVC           EXECUTE ACF2 SECURITY INTERFACE SVC
         LTR   R15,R15             AUTHORIZATION GIVEN?
         BNZ   RC4                 NO
RC0      RETURN (14,12),RC=0       RETURN TO ALLOW FULL ACCESS
RC4      RETURN (14,12),RC=4       RETURN TO PREVENT ACCESS
*
*        DATA AREA
*
MDLSVC   SVC   *-*                 *** USED BY EX INST ***
MDLRCHK  RACHECK CLASS='DATASET',DSTYPE=N,MF=L MODEL RACF PLIST
MDLRCHKL EQU   *-MDLRCHK           LENGTH OF RACF MODEL PLIST
         EJECT
*
*        BRIDGESTONE SECURITY TABLES
*
*
*        USER ID TABLE
*        (UID,@(ENT FT))
*
UIT      DS    0F
         DC    CL8'REA     ',A(T0002)
UITEL    EQU   *-UIT               UIT ENTRY LENGTH
         DC    CL8'TSO0102 ',A(T0002)
         DC    CL8'TSO0109 ',A(T0001)
         DC    CL8'TSO0323 ',A(T0001)
         DC    CL8'TSO0119 ',A(T0002)
         DC    CL8'TSO0106 ',A(T0001)
         DC    CL8'TSO0105 ',A(T0001)
         DC    CL8'TSO0104 ',A(T0001)
         DC    CL8'TSO0103 ',A(T0001)
         DC    CL8'TSO0101 ',A(T0001)
         DC    CL8'TSO0100 ',A(T0001)
         DC    CL8'TSO0334 ',A(T0001)
UIT#N3   EQU   (*-UIT)/UITEL       TOTAL UIT ENTRIES
*
*        FLAG TABLE
*        (@(ENT SIT),FLAG)
*
FT       DS    0F
T0001    DC    F'3'
         DC    A(S1),XL4'FFFF8000'
         DC    A(S2),XL4'FFFF8000'
         DC    A(S3),XL4'FFFF8000'
T0002    DC    F'3'
         DC    A(S1),XL4'1DFF8000'
         DC    A(S2),XL4'1DFF8000'
         DC    A(S3),XL4'1DFF8000'
FTEL     EQU   8                   FT ENTRY LENGTH
*
*        SYSOUT ID TABLE
*
SIT      DS    0F
S1       DC    CL12'G4CONS      '
SITEL    EQU   *-SIT               SIT ENTRY LENGTH
S2       DC    CL12'G4DETAIL    '
S3       DC    CL12'G4EXCL      '
SIT#N3   EQU   (*-SIT)/SITEL       TOTAL SIT ENTRIES
         EJECT
         LTORG
         EJECT
         SARCPL
RFDSN    EQU   CPLUWORK,44         RACF/ACF2 PSEUDO DSNAME
RFPLIST  EQU   CPLUWORK+44,MDLRCHKL RACF/ACF2 PARAMETER LIST
         EJECT
         SARMCR
         EJECT
         SARGCR
PSATOLD  EQU   X'21C'              OFFSET IN PSA TO TCB ADDRESS
TCBJSCB  EQU   X'B4'               OFFSET IN TCB TO JSCB ADDRESS
JSCBACT  EQU   X'15C'              OFFSET IN JSCB TO ACTIVE JSCB
JSCBPSCB EQU   X'108'              OFFSET IN JSCB TO PSCB
PSCBATR1 EQU   X'10'               OFFSET IN PSCB TO ATTRIBUTE FLAGS
PSCBVMNT EQU   X'10'               VOLUME MOUNT AUTHORITY BIT IN PSCB
         EJECT
***********************************************************************
*                                                                     *
* ACDSV -                                                             *
*        ACF2 ACCESS PARM LIST.                                       *
*                                                                     *
***********************************************************************
ACDSV    DSECT
ACFSPARM DS    0F                  ACF2 SECURITY PARM LIST
ACFSPREQ DS    X                   SECURITY REQUEST DEFINITION
ACFSPRDS EQU   X'01'               VALIDATE DSNAME ONLY
ACFSPID1 DS    X                   SECURITY REQUESTOR IDENT.
ACFSPIUR EQU   9                   USER CALLABLE REQUEST
ACFSPAC1 DS    X                   ACCESS TYPE FLAGS
ACFSPAIN EQU   X'00'               ACCESS IS INPUT ONLY
ACFSPAUP EQU   X'04'               ACCESS IS UPDATE
ACFSPAOT EQU   X'7F'               RENAME/SCRATCH/ALLOC ...
         DS    X
ACFSPDSN DS    F                   DSNAME ADDRESS
         DS    14F
ACFSPRML EQU   *-ACFSPARM          LENGTH OF ACFSPARM BLOCK
         END
/*
//LKED.SYSLMOD DD DSN=SYSB.LINKLIB,DISP=SHR
//LKED.SYSIN DD *
 NAME SARSECUX(R)
//
