***********************************************************************
* PROGRAM TO READ IN A DMS/OS SVD RAW DATA FILE, AND TO OUTPUT A LIST *
* OF DATASETS AND THE RACF PROFILES WHICH PROTECT THEM                *
***********************************************************************
GENERICS START 0
         STM   R14,R12,12(13)          SAVE MVS REGISTERS
         BALR  R12,R0                  ESTABLISH R12 AS BASE REGISTER
         USING *,R12                   INDICATE STARTING POINT FOR R12
         ST    R13,SAVEAREA+4          STORE MVS SAVEAREA ADDRESS
         LA    R13,SAVEAREA            LOAD SAVEAREA ADDRESS
         OPEN  (PROFILES,(OUTPUT))     OPEN OUTPUT FILE
         OPEN  (DMSOSSVD,(INPUT))      OPEN INPUT FILE
*
***********************************************************************
* READ IN A RECORD FROM THE DMS/OS SVD RAW FILE, AND MOVE THE VOLUME  *
* AND DSN DATA TO AN OUTPUT AREA, WHICH IS ALSO WHERE RACHECK WILL    *
* LOOK FOR IT.                                                        *
***********************************************************************
READSVD  EQU   *
         GET   DMSOSSVD,DMSRECRD       READ A DMS SVD RECORD
         MVC   VOLUME(6),RPTVOLNO      MOVE VOLSER TO OUTPUT FIELD
         MVC   DATASET(44),RPTDSNAM    MOVE DATASET TO OUTPUT FIELD
*
***********************************************************************
* LOCATE THE RACHECK PARAMETER LIST AND ZAP ON THE "PRIVATE" BIT THAT *
* WILL RESULT IN RACHECK RETURNING A PROFILE NAME. THE RACHECK ITSELF *
* MUST, HOWEVER, BE DONE IN KEY ZERO WHEN THE PRIVATE BIT IS ON.      *
***********************************************************************
RACFMAC  EQU   *
         LA    R2,LISTFORM             LOAD ADDRESS OF RACHECK PARMS
         USING ACHKLIST,R2             SET ADDRESSABILITY TO PARMS
         OI    ACHKFLG3,ACHKPRI        SET "PRIVATE" BIT
         DROP  R2                      DROP REGISTER ADDRESSABILITY
         MODESET KEY=ZERO              GO KEY ZERO FOR PRIVATE RACHECK
         RACHECK MF=(E,LISTFORM)       RETURN PROTECTING PROFILE
         C     R15,=XL4'00000004'      CHECK RACHECK RETURN CODE
         BP    ERROR                   BRANCH OUT IF RC > 4
         BM    GOTPROF                 WRITE OUTPUT RECORD IF RC < 4
         MVC   PROFILE(44),=CL44'*** NONE ***'  NOT PROTECTED IF RC = 4
         B     CHGKEY                  WRITE OUTPUT RECORD
GOTPROF  MVC   PROFILE(44),4(R1)       MOVE PROFILE NAME TO OUTPUT AREA
CHGKEY   MODESET KEY=NZERO             RETURN TO NON-KEY ZERO STATE
         PUT   PROFILES,OUTAREA        WRITE OUTPUT RECORD
         B     READSVD                 READ ANOTHER INPUT RECORD
*
***********************************************************************
* ERROR ROUTINE FOR RACHECK RETURN CODES GREATER THAN 4               *
***********************************************************************
ERROR    EQU   *
         MODESET KEY=NZERO             RETURN TO NON-KEY ZERO STATE
         ST    R15,ERRCODE             STASH R15 IN A SAFE PLACE
*
***********************************************************************
* CLOSE FILES AND TERMINATE PROCESSING                                *
***********************************************************************
CLOSING  CLOSE PROFILES                CLOSE OUTPUT FILE
         CLOSE DMSOSSVD                CLOSE INPUT FILE
         L     R15,ERRCODE             LOAD PROGRAM RETURN CODE
         L     R13,SAVEAREA+4          RESTORE MVS SAVEAREA ADDRESS
         L     R14,12(R13)             RESTORE MVS REGISTER 14
         LM    R0,R12,20(R13)          RESTORE MVS REGISTERS 0-12
         BR    R14                     RETURN TO MVS
         EJECT
***********************************************************************
* PROGRAM STORAGE                                                     *
***********************************************************************
SAVEAREA DS    18F                     SAVE AREA FOR SYSTEM REGISTERS
ERRCODE  DC    F'0'                    PROGRAM RETURN CODE
         SPACE
DMSRECRD DS    0CL98                   DMS/OS DEFINED INPUT RECORD
RPTBUILD DS    0F
         DC    H'100,0'
RPTTABLE DC    CL10'RPTNSVDICT'
RPTDEVCL DS    XL2                     DEVICE CLASS
RPTVOLNO DS    CL6                     VOLUME SERIAL
RPTDSNAM DS    CL44                    DATA SET NAME
RPTDSORG DS    XL2                     DSORG
RPTRECFM DS    XL1                     RECFM
RPTBLKSI DS    XL2                     BLOCKSIZE
RPTLRECL DS    XL2                     LRECL
RPTSCALO DS    XL1                     SECONDARY ALLOCATION
RPTSCQTY DS    XL3                     SECONDARY ALLOCATION QUANTITY
RPTTKSUM DS    XL3                     TOTAL TRACKS ALLOCATED
RPTCREDT DS    XL3                     CREATION DATE - YYDDD
RPTEXPDT DS    XL3                     EXPIRATION DATE - YYDDD
RPTDSIND DS    XL1                     SECURITY
RPTPRMAL DS    XL2                     PRIMARY ALLOCATION
RPTIDLET DS    XL2                     IDLE TRACKS
RPTVOLAD DS    CL4                     VOLUME ADDRESS
RPTCATLG DS    CL1                     CATALOG STATUS
RPTLUSDT DS    XL3                     LAST USED DATE - YYDDD
RPTNOEXT DS    XL1                     EXTENT COUNT
         SPACE
OUTAREA  DS    0CL94                   OUTPUT RECORD
VOLUME   DS    CL6                     DATASET RESIDENCE VOLUME
DATASET  DS    CL44                    DATASET NAME
PROFILE  DS    CL44                    PROTECTING PROFILE NAME
         EJECT
***********************************************************************
* DCB'S                                                               *
***********************************************************************
DMSOSSVD DCB   DDNAME=DMSOSSVD,DSORG=PS,MACRF=(GM),EODAD=CLOSING
PROFILES DCB   DDNAME=PROFILES,DSORG=PS,MACRF=(PM)
         EJECT
***********************************************************************
* REGISTER EQUATES                                                    *
***********************************************************************
R0       EQU   0                       USED BY SYSTEM MACORS
R1       EQU   1                       USED BY SYSTEM MACORS
R2       EQU   2                       ADDRESS OF RACHECK PARM LIST
R3       EQU   3                       *** UNUSED ***
R4       EQU   4                       *** UNUSED ***
R5       EQU   5                       *** UNUSED ***
R6       EQU   6                       *** UNUSED ***
R7       EQU   7                       *** UNUSED ***
R8       EQU   8                       *** UNUSED ***
R9       EQU   9                       *** UNUSED ***
R10      EQU   10                      *** UNUSED ***
R11      EQU   11                      *** UNUSED ***
R12      EQU   12                      GENERICS PROGRAM BASE REGISTER
R13      EQU   13                      REGISTER SAVEAREA ADDRESS
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      PROGRAM RETURN CODE
         EJECT
***********************************************************************
* LIST FORMAT MACROS                                                  *
***********************************************************************
LISTFORM RACHECK ENTITY=DATASET,CLASS='DATASET',RELEASE=1.7,           +
               ATTR=READ,DSTYPE=N,LOG=NOSTAT,RACFIND=NO,GENERIC=ASIS,  +
               STATUS=NONE,VOLSER=VOLUME,MF=L
         EJECT
***********************************************************************
* MAPPING MACROS                                                      *
***********************************************************************
         ICHACHKL                      RACHECK PARM LIST MAPPING MACRO
         END   GENERICS
