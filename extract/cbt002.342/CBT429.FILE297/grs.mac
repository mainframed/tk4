//&SYSUID.GRS JOB (GRS),'GRS',
//             CLASS=U,MSGCLASS=X,NOTIFY=&SYSUID.
//JESDS OUTPUT PAGEDEF=LDUP,FORMDEF=LDUP,JESDS=ALL,FORMS=3H25
//ASM     EXEC PGM=ASMA90,PARM='NODECK,OBJ,RENT'
//STEPLIB   DD DISP=SHR,DSN=SYS1.SASMMOD1
//SYSLIB    DD DISP=SHR,DSN=SYS1.MACLIB
//          DD DISP=SHR,DSN=SYS1.MODGEN
//          DD DISP=SHR,DSN=ZTGP01.MJCUTIL.MACLIB         <=== $COMMON
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSPRINT  DD SYSOUT=*,OUTPUT=*.JESDS
//SYSLIN    DD DSN=&&OBJECT,SPACE=(CYL,(9,9)),UNIT=SYSDA,
//             DISP=(NEW,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FBS)
//SYSIN     DD *
         TITLE 'GRS - GRS ISPF Interface'
GRS      START 0
**********************************************************************
*                                                                    *
* Name:                                                              *
*   GRS                                                              *
*                                                                    *
* Description:                                                       *
*   GRS ISPF Interface                                               *
*                                                                    *
* Function:                                                          *
*   Provides an interactive view of the Global Resource              *
*   Serialization (GRS) queue. A high level resource list is         *
*   displayed based on user specified selection criteria.  From      *
*   the high level resource list, individual resource details can    *
*   be accessed.                                                     *
*                                                                    *
* Installation:                                                      *
*   1) GRS PDS contains the following.                               *
*      Member    Type      Contents                                  *
*      ======    ====      ========                                  *
*      $COMMON   ASM/MACRO Common Macros                             *
*      GRS       ASM/JCL   GRS ISPF Interface                        *
*      GRS@PRIM  ISPPLIB   GRS ISPF Interface                        *
*      GRSMAJ    ISPPLIB   GRS ISPF Interface                        *
*      GRSP002   ISPPLIB   GRS ISPF Interface                        *
*      GRSP003   ISPPLIB   GRS ISPF Interface                        *
*                                                                    *
*   2) Assemble and link-edit GRS.  The assembly and linkedit        *
*   must both get a return code zero.                                *
*                                                                    *
*   3) The load module and panels need to be available on ISPLLIB    *
*   and ISPPLIB allocations respectively.  For the young at          *
*   heart, this can easily be accomplished via LIBDEF's in a         *
*   CLIST.                                                           *
*                                                                    *
*   PROC 0                                                           *
*   CONTROL MAIN MSG                                                 *
*   ISPEXEC LIBDEF ISPLLIB DATASET ID('ZTGP01.MJCUTIL.LINKLIB')      *
*   ISPEXEC LIBDEF ISPPLIB DATASET ID('ZTGP01.MJCUTIL.ISPPLIB')      *
*   ISPEXEC SELECT PGM(GRS)                                          *
*   ISPEXEC LIBDEF ISPLLIB                                           *
*   ISPEXEC LIBDEF ISPPLIB                                           *
*   EXIT                                                             *
*                                                                    *
* Contact:                                                           *
*   Michael J. Cleary                                                *
*   Mainframe Technical Services                                     *
*   Bergen Brunswig                                                  *
*   4000 Metropolitan Drive                                          *
*   Orange, CA  92668                                                *
*   Voice:  714.385.4052                                             *
*   Fax     714.704.7052                                             *
*   Email:  mike.cleary@bergenbrunswig.com                           *
*   http:   www.bergenbrunswig.com                                   *
*                                                                    *
* Abend codes:                                                       *
*   None                                                             *
*                                                                    *
* Addressing mode:                                                   *
*   31                                                               *
*                                                                    *
* ASC mode:                                                          *
*   Primary                                                          *
*                                                                    *
* Change Log:                                                        *
*   05/25/99 - Modified selection criteria manipulation              *
*   05/24/99 - Changed GRSSCSCO references from CL7 to CL8           *
*   05/20/99 - Add message - No Unknown Major Names                  *
*   04/30/99 - Add XSYS=NO as default to GQSCAN (GRSSCAN)            *
*   04/30/99 - Update the Major Enqueue Table ($COMMON)              *
*   04/29/99 - Update the Major Enqueue Table ($COMMON)              *
*   04/29/99 - Move Major Enqueue Tabble to $COMMON                  *
*   04/27/99 - Version 2 Release 6 Modification 0                    *
*                                                                    *
* Copyright:                                                         *
*   None                                                             *
*                                                                    *
* Control blocks:                                                    *
*   Resource Information Block (RIB)                                 *
*                                                                    *
* CSECTs:                                                            *
*   GRSEXPL        GRS Explode                                       *
*   GRSINIT        GRS Initialization                                *
*   GRS            GRS Mainline                                      *
*   GRSLIST        GRS List                                          *
*   GRSPROC        GRS Process                                       *
*   GRSSCAN        GRS Scan                                          *
*   GRSSWA         GRS Static Workarea                               *
*   GRSTERM        GRS Termination                                   *
*                                                                    *
* Dependencies:                                                      *
*   None                                                             *
*                                                                    *
* Disclaimer:                                                        *
*   Bergen Brunswig neither expresses nor implies any warranty as    *
*   to the fitness of these computer programs for any function.      *
*   The use of these programs or the results therefrom is            *
*   entirely at the risk of the user.  Consequently, the user may    *
*   modify these programs in any way they thinks fit.  These         *
*   programs are Freeware and may be freely copied.  They may be     *
*   freely distributed to any other party on condition that no       *
*   inducement beyond reasonable handling costs is offered or        *
*   accepted by either side for such distribution.                   *
*                                                                    *
* DSECTs:                                                            *
*   GRSDWA         GRS Dynamic Workarea (DWA)                        *
*   ISGRIB         Resource Information Block (RIB)                  *
*                                                                    *
* Entry points:                                                      *
*   GRS                                                              *
*                                                                    *
* Environment:                                                       *
*   MVS/ESA, TSO, ISPF                                               *
*                                                                    *
* Input:                                                             *
*   Selection criteria                                               *
*                                                                    *
* Key:                                                               *
*   8                                                                *
*                                                                    *
* Language:                                                          *
*   MVS 370 assembler                                                *
*                                                                    *
* Linkage:                                                           *
*   Standard linkage for entry/exit                                  *
*                                                                    *
* Location:                                                          *
*   Private                                                          *
*                                                                    *
* Macros:                                                            *
*   Name     Usage Description                                       *
*   ======== ===== ===========                                       *
*   $COMMON  1 2   Common Macros                                     *
*   CALL     1 2   Pass Control to a Control Section                 *
*   DELETE   1     Relinquish Control of a Load Module               *
*   GQSCAN     2   Extract Information From GRS Queue                *
*   LOAD     1     Bring a Load Module into Virtual Storage          *
*   MAJORTBL 1     Major Enqueue Description Table                   *
*   RETURN   1     Return Control                                    *
*   SAVE     1     Save Register Contents                            *
*   SPLEVEL  1     Set Macro Level                                   *
*   STORAGE  1 2   Obtain and Release Storage                        *
*                  Usage:    1 = Common Macros, 2 = GRS              *
*                                                                    *
* Messages:                                                          *
*   Invalid Selection                                                *
*     Major/System are mutually exclusive with Type=RES/ENQ          *
*   Invalid Selection                                                *
*     Major/Minor are mutually exclusive with System                 *
*   Invalid Selection                                                *
*     Owners/Waiters are mutually exclusive with Requestors          *
*   Not Found                                                        *
*     Specified resources not found                                  *
*   Storage Not Obtained                                             *
*     RIB Detail Storage Not Obtained                                *
*   Storage Not Obtained                                             *
*     RIB List Storage Not Obtained                                  *
*   Storage Not Released                                             *
*     RIB Detail Storage Not Released                                *
*   Storage Not Released                                             *
*     RIB List Storage Not Released                                  *
*   RIB Area Full'                                                   *
*     RIB area full, increase GRSRLL                                 *
*   GQSCAN Error'                                                    *
*     GQSCAN Encountered an Unrecoverable Error                      *
*   Invalid System                                                   *
*     Specified System Unknown to GRS                                *
*                                                                    *
* Mode:                                                              *
*   Task                                                             *
*                                                                    *
* Module type:                                                       *
*   Procedure                                                        *
*                                                                    *
* Output:                                                            *
*   GRS queue information                                            *
*                                                                    *
* Parameter list:                                                    *
*   None                                                             *
*                                                                    *
* Patch label:                                                       *
*   None                                                             *
*                                                                    *
* Reason codes:                                                      *
*   None                                                             *
*                                                                    *
* Recovery:                                                          *
*   None                                                             *
*                                                                    *
* Reentrancy:                                                        *
*   Reentrant                                                        *
*                                                                    *
* Registers:                                                         *
*   Saved:         0-12,14-15 via SAVE(14,12)                        *
*   Restored:      0-12,14 via RETURN(14,12),RC=(15)                 *
*   Base:          11/12                                             *
*   Dynamic work:  10                                                *
*   Static work:   9                                                 *
*                                                                    *
* Return codes:                                                      *
*   Zero - Normal                                                    *
*                                                                    *
* Residence mode:                                                    *
*   Any                                                              *
*                                                                    *
* Savearea:                                                          *
*   Dynamically obtained storage                                     *
*                                                                    *
* Serialization:                                                     *
*   None                                                             *
*                                                                    *
* Size:                                                              *
*   Approximately 15KB                                               *
*                                                                    *
* State:                                                             *
*   Problem                                                          *
*                                                                    *
* Status:                                                            *
*   Version 2 Release 6 Modification 0                               *
*                                                                    *
* Tables:                                                            *
*   Major queue name table                                           *
*                                                                    *
* User exits:                                                        *
*   None                                                             *
*                                                                    *
* Virtual Storage:                                                   *
*   Between 2MB and 10MB in the extended private area                *
*                                                                    *
* Wait states:                                                       *
*   None                                                             *
*                                                                    *
* X-memory mode:                                                     *
*   HASID=PASID=SASID                                                *
*                                                                    *
**********************************************************************
         TITLE '$COMMON - Common Macros'
**********************************************************************
         PRINT OFF                 Stop source and object printing
         COPY  $COMMON             Common Macros
         PRINT ON                  Resume source and object printing
**********************************************************************
         TITLE 'GRS - Mainline'
**********************************************************************
GRS      $ENTRY DWA=(GRSDWA,R10,GRSDWA#), Entry                        X
               SAVEAREA=YES,                                           X
               SWA=(GRSSWA,R9)
         GBLA  &COMMON             Common Macros Level
         AIF   (&COMMON GE 4).COMMON
         MNOTE 8,'Wrong level of $COMMON, Required=4, Found=&COMMON'
         AGO   .COMMON
.COMMON  ANOP
         CALL  GRSINIT             Initialization
         CLC   GRSMAXRC,=F'0'      OK ?
         BNE   GRSNOPRO            . No
         CALL  GRSPROC             Process
GRSNOPRO EQU   *
         CALL  GRSTERM             Termination
         L     R15,GRSMAXRC        Maximum return code
         B     GRSR                Exit
GRS      $EXIT                     Exit
**********************************************************************
         TITLE 'GRSEXPL - Explode'
**********************************************************************
GRSEXPL  $ENTRY                    Entry
         TBCREATE TABLE=GRS3,      Create table                        X
               NAMES=(GRS3JOB,GRS3SYS,GRS3USA,GRS3STA)
*
**       Save Selection Criteria
*
         LA    R0,GRSSCSAV         Selection criteria save address
         LA    R1,L'GRSSCSAV       Selection criteria save length
         LA    R14,GRSSCSCO        Selection criteria address
         LR    R15,R1              Selection criteria length
         MVCL  R0,R14              Save selection criteria
*
**       Current ?
*
         CLI   GRS2LC,C'C'         Current ?
         BE    EXPLSELC            . Yes
         L     R4,GRS2PTR          RIB extent pointer
         B     EXPLSELS
*
**       Process current
*
EXPLSELC EQU   *
         MVC   GRSSCMAJ,GRS2MAJ    Major
         MVC   GRSSCMIN,GRS2MIN    Minor
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRDA          RIB detail address
         ST    R15,W_GQSCAN+00     RIB detail address
         L     R15,=A(GRSRDL)      RIB detail length
         ST    R15,W_GQSCAN+04     RIB detail length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         CALL  GRSSCAN             Scan
         L     R15,GRSRIBC         RIB count
         LTR   R15,R15             RIB count ?
         BNZ   EXPLOK              . Yes
         MVC   ZEDSMSG,=CL24'Not Found'
         MVC   ZEDLMSG,=CL80'Specified resources not found'
         SETMSG MESSAGE=ISRZ001    Not found
         B     EXPLGRS3            Exit - RC=0
EXPLOK   L     R4,GRSRDA           RIB detail address
         USING RIB,R4              RIB area
         MVC   GRS2TO,RIBNTO       Tasks owning
         MVC   GRS2TWE,RIBNTWE     Tasks waiting exclusive
         MVC   GRS2TWS,RIBNTWS     Tasks waiting shared
EXPLSELS LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
EXPLEXT  EQU   *
         MVC   GRS3JOB,RIBEJBNM    Jobname
         MVC   GRS3SYS,RIBESYSN    Sysname
EXPLTYP0 EQU   *
         MVC   GRS3USA,=CL9'Shared' Shared
         TM    RIBERFLG,RIBETYPE   Type ?
         BO    EXPLTYPX            . Shared
         MVC   GRS3USA,=CL9'Exclusive'
EXPLTYPX EQU   *
EXPLSTA0 EQU   *
         MVC   GRS3STA,=CL8'Owner' Owner
         TM    RIBESFLG,RIBESTAT   Status ?
         BO    EXPLSTAX            . Owner
         MVC   GRS3STA,=CL8'Waiting'
EXPLSTAX EQU   *
         TBADD TABLE=GRS3          Add table entry
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,EXPLEXT          Process next extent, if any
*
**       Major Description
*
         LA    R15,MAJORNUM        Major table size
         L     R14,=A(MAJORTBL)    Major table address
CHKMAJOR EQU    *
         CLC   GRS2MAJ,0(R14)      Major match ?
         BE    GOTMAJOR            . Yes
         LA    R14,MAJORLEN(R14)   Next major
         BCT   R15,CHKMAJOR        Process next major, if any
GOTMAJOR EQU   *
         MVC   GRSMAJOR,8(R14)     Major
         TBSORT TABLE=GRS3,        Sort table                          X
               FIELDS=(GRS3JOB,C,A,GRS3SYS,C,A)
         TBTOP TABLE=GRS3          Top of table
GRSP003  TBDISPL TABLE=GRS3,       Display table                       X
               PANEL=GRSP003
EXPLGRS3 EQU   *                   Exit
         TBEND TABLE=GRS3          Delete table
*
**       Restore Selection Criteria
*
         LA    R0,GRSSCSCO         Selection criteria address
         LA    R1,L'GRSSCSAV       Selection criteria length
         LA    R14,GRSSCSAV        Selection criteria save address
         LR    R15,R1              Selection criteria save length
         MVCL  R0,R14              Restore selection criteria
GRSEXPL  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSINIT - Initialization'
**********************************************************************
GRSINIT  $ENTRY                    Entry
*
**       Initialize ISPF Environment
*
         ISPFINIT                  Initialize ISPF environment
*
**       Define ISPF Variables
*
         VDEFINE NAMES=(GRSMAJOR),FORMAT=CHAR,LENGTH=32
         VDEFINE NAMES=(GRSRIBC),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSRIBS),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCJOB),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCMAJ),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCMIN),FORMAT=CHAR,LENGTH=255
         VDEFINE NAMES=(GRSSCOWN),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCPRO),FORMAT=CHAR,LENGTH=3
         VDEFINE NAMES=(GRSSCREQ),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRSSCSCO),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCSYS),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRSSCTYP),FORMAT=CHAR,LENGTH=4
         VDEFINE NAMES=(GRSSCWAI),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2FLAG),FORMAT=CHAR,LENGTH=5
         VDEFINE NAMES=(GRS2LC),FORMAT=CHAR,LENGTH=1
         VDEFINE NAMES=(GRS2MAJ),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS2MIN),FORMAT=CHAR,LENGTH=255
         VDEFINE NAMES=(GRS2PTR),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2SCO),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS2TO),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2TWE),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS2TWS),FORMAT=FIXED,LENGTH=4
         VDEFINE NAMES=(GRS3JOB),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3LC),FORMAT=CHAR,LENGTH=1
         VDEFINE NAMES=(GRS3STA),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3SYS),FORMAT=CHAR,LENGTH=8
         VDEFINE NAMES=(GRS3USA),FORMAT=CHAR,LENGTH=9
*
**       Obtain RIB Storage
*
         STORAGE OBTAIN,LENGTH=(MB10,MB02),LOC=ANY Obtain RIB list
         LTR   R15,R15             OK ?
         BZ    INITRLOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Obtained'
         MVC   ZEDLMSG,=CL80'RIB List Storage Not Obtained'
         SETMSG MESSAGE=ISRZ001    Storage Not Obtained
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSINIT4            Exit
INITRLOK EQU   *
         ST    R0,GRSRLL           RIB list length
         ST    R1,GRSRLA           RIB list address
         STORAGE OBTAIN,LENGTH=GRSRDL,LOC=ANY  Obtain RIB detail
         LTR   R15,R15             OK ?
         BZ    INITRDOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Obtained'
         MVC   ZEDLMSG,=CL80'RIB Detail Storage Not Obtained'
         SETMSG MESSAGE=ISRZ001    Storage Not Obtained
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSINIT4            Exit
INITRDOK EQU   *
         ST    R1,GRSRDA           RIB detail address
GRSINIT  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSLIST - List'
**********************************************************************
GRSLIST  $ENTRY                    Entry
         TBCREATE TABLE=GRS2,      Create table                        X
               NAMES=(GRS2MAJ,GRS2MIN,GRS2SCO,GRS2TO,GRS2TWE,GRS2TWS,GRX
               S2FLAG,GRS2PTR)
         XC    GRSCSEL,GRSCSEL     Initialize selected count
         L     R7,GRSRIBC          RIB count
         L     R4,GRSRLA           RIB list address
         USING RIB,R4              RIB area
LISTRIB  EQU   *                   Process RIB
         ST    R4,GRS2PTR          RIB extent pointer
         MVI   GRSJBFND,C'Y'       Job found (default)
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTRIB2            . No
         MVI   GRSJBFND,C'N'       Job not found
LISTRIB2 EQU   *                   PROCESS RIB
         LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5               RIB address
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
         TM    RIBSCOPE,RIBSYS     System ?
         BNO   LISTSNO1            . No
         MVC   GRS2SCO,=CL8'System' . Yes
         B     LISTFLAG
LISTSNO1 EQU   *
         TM    RIBSCOPE,RIBSYSS    Systems ?
         BNO   LISTSNO2            . No
         MVC   GRS2SCO,=CL8'Systems' . Yes
         B     LISTFLAG
LISTSNO2 EQU   *
         TM    RIBSCOPE,RIBSTEP    Step ?
         BNO   LISTSNO3            . No
         MVC   GRS2SCO,=CL8'Step'  . Yes
         B     LISTFLAG
LISTSNO3 EQU   *
LISTFLAG EQU   *
         $FILL GRS2FLAG,C' '       Initialize
         TM    RIBERFLG,RIBEMC     Must complete ?
         BNO   LISTFNOM            . No
         MVI   GRS2FLAG+00,C'M'    Must complete
LISTFNOM EQU   *
         TM    RIBERFLG,RIBERESV   Reserve ?
         BNO   LISTFNOR            . No
         MVI   GRS2FLAG+01,C'R'    Reserve
LISTFNOR EQU   *
         TM    RIBERFLG,RIBERESC   Converted ?
         BNO   LISTFNOC            . No
         MVI   GRS2FLAG+02,C'C'    Converted
LISTFNOC EQU   *
         TM    RIBERFLG,RIBEAUTH   Authorized ?
         BNO   LISTFNOA            . No
         MVI   GRS2FLAG+03,C'A'    Authorized
LISTFNOA EQU   *
         TM    RIBERFLG,RIBETCBF   TCB abending ?
         BNO   LISTFNOT            . No
         MVI   GRS2FLAG+04,C'T'    TCB abending
LISTFNOT EQU   *
         MVC   GRS2MAJ,RIBQNAME    Major
         $FILL GRS2MIN,C' '        Clear minor
         SR    R15,R15             Minor length
         IC    R15,RIBRNMLN        Minor length
         $VLM  GRS2MIN,RIBRNAME,(R15) Minor
         MVC   GRS2TO,RIBNTO       Tasks owning
         MVC   GRS2TWE,RIBNTWE     Tasks waiting exclusive
         MVC   GRS2TWS,RIBNTWS     Tasks waiting shared
LISTEXT  EQU   *                   Process extent
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTJOB2            . No
         $FIND GRSSCJOB,C' '       Find first blank
         $VLC  GRSSCJOB,RIBEJBNM,(R15),DECREMENT=NO Match ?
         BNE   LISTJOB2            . No
         MVI   GRSJBFND,C'Y'       Job found
LISTJOB2 EQU   *
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,LISTEXT          Process next extent, if any
         CLI   GRSSCJOB,C' '       Job specified ?
         BE    LISTJOB3            . No
         CLI   GRSJBFND,C'Y'       Job found ?
         BNE   CONT4               . No
LISTJOB3 EQU   *
         TBADD TABLE=GRS2          Add table entry
         $INCR GRSCSEL             Increment selected count
CONT4    LR    R4,R6
         BCT   R7,LISTRIB          Process next RIB, if any
         L     R15,GRSCSEL         Selected count
         LTR   R15,R15             Any selected ?
         BNZ   LISTOK              . Yes
         MVC   ZEDSMSG,=CL24'Not Found'
         MVC   ZEDLMSG,=CL80'Specified resources not found'
         SETMSG MESSAGE=ISRZ001    Resource not found
         B     LISTTBEN            Exit
LISTOK   EQU   *
         L     R7,GRSRIBC          RIB count
         S     R4,GRSRLA           Determine RIB list address used
         SRL   R4,10               Convert to KB
         LA    R4,1(R4)            Round up
         ST    R4,GRSRIBS          RIB size
         TBSORT TABLE=GRS2,        Sort table                          X
               FIELDS=(GRS2MAJ,C,A,GRS2MIN,C,A)
         TBTOP TABLE=GRS2          Top of table
GRSP002  TBDISPL TABLE=GRS2,       Display table                       X
               PANEL=GRSP002,                                          X
               LC=(GRS2LC,C,GRSEXPL,S,GRSEXPL)
LISTTBEN EQU   *
         TBEND TABLE=GRS2          Delete table
GRSLIST  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSMAJ - Major Audit'
**********************************************************************
GRSMAJ   $ENTRY                    Entry
*
**       Save Selection Criteria
*
         LA    R0,GRSSCSAV         Selection criteria save address
         LA    R1,L'GRSSCSAV       Selection criteria save length
         LA    R14,GRSSCSCO        Selection criteria address
         LR    R15,R1              Selection criteria length
         MVCL  R0,R14              Save selection criteria
*
**       Process Major Audit
*
         MVC   GRSSCSCO,=CL8'ALL'  Scope - All
         $FILL GRSSCSYS,C' '       System - Blank
         MVC   GRSSCPRO,=CL3'YES'  GRSPlex - Propagate
         MVC   GRSSCTYP,=CL4'BOTH' Type - Both reserves and enqueues
         $FILL GRSSCJOB,C' '       Jobname - Blank
         $FILL GRSSCMAJ,C' '       Major - Blank
         $FILL GRSSCMIN,C' '       Minor - Blank
         MVC   GRSSCREQ,=F'0'      Requestors - Zero
         MVC   GRSSCOWN,=F'0'      Owners - Zero
         MVC   GRSSCWAI,=F'0'      Waiters - Zero
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRLA          RIB list address
         ST    R15,W_GQSCAN+00     RIB list address
         L     R15,GRSRLL          RIB list length
         ST    R15,W_GQSCAN+04     RIB list length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         XC    GRSCMAJ,GRSCMAJ     Initailize Major count
         CALL  GRSSCAN             Scan
         TBCREATE TABLE=GRSMAJ,    Create table                        X
               KEYS=(GRSSCMAJ)
         XC    GRSCSEL,GRSCSEL     Initialize selected count
         L     R7,GRSRIBC          RIB count
         L     R4,GRSRLA           RIB list address
         USING RIB,R4              RIB area
MAJRIB   EQU   *                   Process RIB
         ST    R4,GRS2PTR          RIB extent pointer
         LA    R5,40(R4)           RIB variable section
         USING RIBVAR,R5           RIB variable section
         LR    R6,R5               RIB address
         AH    R6,RIBVLEN          RIB variable section length
         USING RIBE,R6             RIB extent
         L     R8,RIBNRIBE         Extent count
         MVC   GRSSCMAJ,RIBQNAME   Major
*
**       Major Description
*
         LA    R15,MAJORNUM        Major table size
         L     R14,=A(MAJORTBL)    Major table address
CHKMAJ   EQU   *
         CLC   GRSSCMAJ,0(R14)     Major match ?
         BE    GOTMAJ              . Yes
         LA    R14,MAJORLEN(R14)   Next major
         BCT   R15,CHKMAJ          Process next major, if any
         $INCR GRSCMAJ             Increment Major count
         TBADD TABLE=GRSMAJ        Add table entry
GOTMAJ   EQU   *
MAJEXT   EQU   *                   Process RIB
         LA    R6,RIBEEND-RIBE(R6) Next extent
         BCT   R8,MAJEXT           Process next extent, if any
         LR    R4,R6
         BCT   R7,MAJRIB           Process next RIB, if any
         L     R15,GRSCMAJ         Major count
         LTR   R15,R15             Any ?
         BNZ   MAJOK               . Yes
         MVC   ZEDSMSG,=CL24'No Unknown Major Names'
         MVC   ZEDLMSG,=CL80'No unknown major enqueue names found'
         SETMSG MESSAGE=ISRZ001    No unknown major names
         B     MAJTBEN             Continue
MAJOK    EQU   *
         TBSORT TABLE=GRSMAJ,      Sort table                          X
               FIELDS=(GRSSCMAJ,C,A)
         TBTOP TABLE=GRSMAJ        Top of table
         TBDISPL TABLE=GRSMAJ,     Display table                       X
               PANEL=GRSMAJ
MAJTBEN  EQU   *
         TBEND TABLE=GRSMAJ        Delete table
*
**       Restore Selection Criteria
*
         LA    R0,GRSSCSCO         Selection criteria address
         LA    R1,L'GRSSCSAV       Selection criteria length
         LA    R14,GRSSCSAV        Selection criteria save address
         LR    R15,R1              Selection criteria save length
         MVCL  R0,R14              Restore selection criteria
GRSMAJ   $EXIT                     Exit
**********************************************************************
         TITLE 'GRSPROC - Process'
**********************************************************************
GRSPROC  $ENTRY                    Entry
         MVC   GRSSCSCO,=CL8'ALL'  Scope - All
         $FILL GRSSCSYS,C' '       System - Blank
         MVC   GRSSCPRO,=CL3'NO'   GRSPlex - Do not propagate
         MVC   GRSSCTYP,=CL4'BOTH' Type - Both reserves and enqueues
         $FILL GRSSCJOB,C' '       Jobname - Blank
         MVC   GRSSCMAJ,=CL8'SYSDSN' Major - SYSDSN
         $FILL GRSSCMIN,C' '       Minor - Blank
         MVC   GRSSCREQ,=F'0'      Requestors - Zero
         MVC   GRSSCOWN,=F'0'      Owners - Zero
         MVC   GRSSCWAI,=F'0'      Waiters - Zero
GRS@PRIM DISPLAY PANEL=GRS@PRIM,   Selection criteria                  X
               END=GRSPROC0,                                           X
               PC=(ZCMD(5),AUDIT,GRSMAJ)
         MVC   W_GQSCAN(L_GQSCAN),T_GQSCAN Copy GQSCAN parameter list
         L     R15,GRSRLA          RIB list address
         ST    R15,W_GQSCAN+00     RIB list address
         L     R15,GRSRLL          RIB list length
         ST    R15,W_GQSCAN+04     RIB list length
         XC    GRSTOKEN,GRSTOKEN   Initailize GQSCAN token
         CALL  GRSSCAN             Scan
         B     *+4(R15)
         B     FOUND               RC=00 Resource found
         B     NOTFOUND            RC=04 Resource not found
         B     RIBFULL             RC=08 RIB Area full
         B     ABEND               RC=12 System error
         B     SYSINV              RC=16 Sysname invalid
         B     ABEND               RC=20 Environmental error
FOUND    EQU   *
         CALL  GRSLIST             List
         B     GRS@PRIM            Selection criteria
NOTFOUND EQU   *
         MVC   ZEDSMSG,=CL24'Not Found'
         MVC   ZEDLMSG,=CL80'Specified resources not found'
         SETMSG MESSAGE=ISRZ001    Not found
         B     GRS@PRIM            Selection criteria
RIBFULL  EQU   *
         MVC   ZEDSMSG,=CL24'RIB Area Full'
         MVC   ZEDLMSG,=CL80'RIB area full, increase GRSRLL'
         SETMSG MESSAGE=ISRZ001    RIB Area Full
         B     GRS@PRIM            Selection criteria
ABEND    EQU   *
         MVC   ZEDSMSG,=CL24'GQSCAN Error'
         MVC   ZEDLMSG,=CL80'GQSCAN encountered an unrecoverable error'
         SETMSG MESSAGE=ISRZ001    GQSCAN error
         B     GRS@PRIM            Selection criteria
SYSINV   EQU   *
         MVC   ZEDSMSG,=CL24'Invalid System'
         MVC   ZEDLMSG,=CL80'Specified system unknown to GRS'
         SETMSG MESSAGE=ISRZ001    Invalid system
         B     GRS@PRIM            Selection criteria
GRSPROC  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSSCAN - Scan'
**********************************************************************
GRSSCAN  $ENTRY                    Entry
*
**       Major name
*
         CLI   GRSSCMAJ,C' '       Major specified ?
         BE    MAJEND              . No
         LA    R15,GRSSCMAJ        Major address
         ST    R15,W_GQSCAN+08     Major address
         $FIND GRSSCMAJ,C' ',DECREMENT=NO Find first blank
         STCM  R15,B'0001',W_GQSCAN+47 Major length
         OC    W_GQSCAN+45(1),=BL1'00000010' Major length specified
MAJEND   EQU   *
*
**       Minor name
*
         CLI   GRSSCMIN,C' '       Minor specified ?
         BE    MINEND              . No
         LA    R15,GRSSCMIN        Minor address
         ST    R15,W_GQSCAN+12     Minor address
         $FIND GRSSCMIN,C' ',DECREMENT=NO Find first blank
         STCM  R15,B'0001',W_GQSCAN+46 Minor length
         OC    W_GQSCAN+45(1),=BL1'00000100' Generic minor
MINEND   EQU   *
*
**       Sysname
*
         CLI   GRSSCSYS,C' '       Sysname specified ?
         BE    SYSEND              . No
         LA    R15,GRSSCSYS        Sysname address
         ST    R15,W_GQSCAN+16     Sysname address
         OC    W_GQSCAN+45(1),=BL1'10000000' Sysname specified
SYSEND   EQU   *
*
**       Request count
*
         CLC   GRSSCREQ,=F'0'      Request count specified ?
         BE    REQEND              . No
         MVC   W_GQSCAN+24(4),GRSSCREQ Request count
         OC    W_GQSCAN+45(1),=BL1'01000000' Request count specified
REQEND   EQU   *
*
**       Owner count
*
         CLC   GRSSCOWN,=F'0'      Owner count specified ?
         BE    OWNEND              . No
         MVC   W_GQSCAN+28(4),GRSSCOWN Owner count
         OC    W_GQSCAN+45(1),=BL1'00100000' Owner count specified
OWNEND   EQU   *
*
**       Waitor count
*
         CLC   GRSSCWAI,=F'0'      Wait count specified ?
         BE    WAIEND              . No
         MVC   W_GQSCAN+32(4),GRSSCWAI Wait count
         OC    W_GQSCAN+45(1),=BL1'00010000' Wait count specified
WAIEND   EQU   *
*
**       Scope
*
         LA    R15,SCOPENUM        Scope table size
         LA    R14,SCOPETBL        Scope table address
CHKSCOPE EQU    *
         CLC   GRSSCSCO,0(R14)     Scope match ?
         BE    GOTSCOPE            . Yes
         LA    R14,SCOPELEN(R14)   Next scope
         BCT   R15,CHKSCOPE        Process next scope, if any
GOTSCOPE EQU   *
         NC    W_GQSCAN+44(1),=BL1'00000111' Clear scope
         OC    W_GQSCAN+44(1),8(R14) Set scope
*
**       Reserve
*
         CLI   GRSSCTYP,C'B'       Reserves and enqueues specified ?
         BE    RESEND              . Yes
         CLI   GRSSCTYP,C'R'       Reserve specified ?
         BE    RESRES              . No
         CLI   GRSSCTYP,C'E'       Enqueue specified ?
         BE    RESENQ              . Yes
         B     RESEND
RESRES   EQU   *
         OC    W_GQSCAN+44(1),=BL1'00000010' Reserve specified
         B     RESEND
RESENQ   EQU   *
         OC    W_GQSCAN+44(1),=BL1'00000100' Enqueue specified
         B     RESEND
RESEND   EQU   *
*
**       GRSPlex
*
         CLI   GRSSCPRO,C'Y'       GRSPlex Yes specified ?
         BE    PLXYES              . No
         CLI   GRSSCPRO,C'N'       GRSPlex No specified ?
         BE    PLXNO               . No
         B     PLXNO               Default to No
PLXYES   EQU   *
         NC    W_GQSCAN+45(1),=BL1'00000001' Set GRSPlex Yes specified
         B     PLXEND
PLXNO    EQU   *
         OC    W_GQSCAN+45(1),=BL1'00000001' Set GRSPlex No specified
         B     PLXEND
PLXEND   EQU   *
*
**       Issue GQSCAN
*
         GQSCAN MF=(E,W_GQSCAN)    GQSCAN
         ST    R1,GRSRIBC          RIB count
         B     GRSSCANR            Exit - RC=R15
GRSSCAN  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSTERM - Termination'
**********************************************************************
GRSTERM  $ENTRY                    Entry
*
**       Terminate ISPF Environment
*
         ISPFTERM                  Terminate ISPF environment
*
**       Release RIB Storage
*
         L     R0,GRSRLL           RIB list length
         STORAGE RELEASE,ADDR=GRSRLA,LENGTH=(0)     Release RIB list
         LTR   R15,R15             OK ?
         BZ    TERMRLOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Released'
         MVC   ZEDLMSG,=CL80'RIB List Storage Not Released'
         SETMSG MESSAGE=ISRZ001    Storage Not Released
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSTERM4            Exit
TERMRLOK EQU   *
         STORAGE RELEASE,ADDR=GRSRDA,LENGTH=GRSRDL  Release RIB detail
         LTR   R15,R15             OK ?
         BZ    TERMRDOK            . Yes
         MVC   ZEDSMSG,=CL24'Storage Not Released'
         MVC   ZEDLMSG,=CL80'RIB Detail Storage Not Released'
         SETMSG MESSAGE=ISRZ001    Storage Not Released
         MVC   GRSMAXRC,=F'4'      Set maximum return code
         B     GRSTERM4            Exit
TERMRDOK EQU   *
GRSTERM  $EXIT                     Exit
**********************************************************************
         TITLE 'GRSDWA - Dynamic Workarea'
**********************************************************************
GRSDWA   DSECT                     Dynamic Workarea
GRSCSEL  DS    F                   Count - Selected
GRSJBFND DS    CL001               Job found
GRSCMAJ  DS    F                   Major count
GRSMAJOR DS    CL032               Major Description
GRSRDA   DS    F                   RIB detail adress
GRSRDL   EQU   (128*1024)          RIB detail length (128KB)
GRSRIBC  DS    F                   RIB count
GRSRIBS  DS    F                   RIB size
GRSMAXRC DS    F                   Maximum Return Code
GRSRLA   DS    F                   RIB list address
GRSRLL   DS    F                   RIB list length
GRSTOKEN DS    F                   GQSCAN Token
*
**       Selection Criteria
*
GRSSCSCO DS    CL008               Selection Criteria - Scope
GRSSCSYS DS    CL008               Selection Criteria - System
GRSSCPRO DS    CL003               Selection Criteria - GRSPlex
GRSSCTYP DS    CL004               Selection Criteria - Type
GRSSCJOB DS    CL008               Selection Criteria - Job
GRSSCMAJ DS    CL008               Selection Criteria - Major
GRSSCMIN DS    CL255               Selection Criteria - Minor
GRSSCREQ DS    F                   Selection Criteria - Requestors
GRSSCOWN DS    F                   Selection Criteria - Owners
GRSSCWAI DS    F                   Selection Criteria - Waiters
GRSSCSAV DS    CL(*-GRSSCSCO)      Selection Criteria - Save
*
**
*
GRS2CRP  DS    CL008               ISPF Current Row Pointer
GRS2FLAG DS    CL005               List - Flags
GRS2LC   DS    CL001               ISPF Line Command
GRS2MAJ  DS    CL008               List - Major
GRS2MIN  DS    CL255               List - Minor
GRS2PTR  DS    F                   List - RIB extent pointer
GRS2SCO  DS    CL008               List - Scope
GRS2SEL  DS    CL004               ISPF Selections
GRS2TO   DS    F                   List - Tasks owning
GRS2TWE  DS    F                   List - Tasks waiting exclusive
GRS2TWS  DS    F                   List - Tasks waiting shrared
GRS3CRP  DS    CL008               ISPF Current Row Pointer
GRS3JOB  DS    CL008               Detail - Job
GRS3LC   DS    CL001               ISPF Line Command
GRS3SELS DS    CL004               ISPF Selections
GRS3STA  DS    CL008               Detail - Status
GRS3SYS  DS    CL008               Detail - System
GRS3USA  DS    CL009               Detail - Usage
MB02     EQU   (02*1024*1024)      RIB list minimum length (MB02)
MB10     EQU   (10*1024*1024)      RIB list maximum length (MB10)
         DS    0F                  Fullword alignment
W_GQSCAN EQU   *                   GQSCAN parameter list
         ORG   *+L_GQSCAN          GQSCAN parameter list length
         ISPFVARS                  Define ISPF variables
GRSDWA#  EQU   *-GRSDWA            Dynamic workarea length
**********************************************************************
         TITLE 'GRSSWA - Static Workarea'
**********************************************************************
GRSSWA   $ENTRY BASE=,DWA=,MODID=NO,SAVE=NO,SAVEAREA=NO,SWA=
T_GQSCAN GQSCAN QUIT=NO,REQLIM=MAX,MF=L GQSCAN list
L_GQSCAN EQU   *-T_GQSCAN          GQSCAN length
SCOPETBL DC    CL008'STEP    ',BL1'10000000'
SCOPELEN EQU   *-SCOPETBL          Scope table entry length
         DC    CL008'SYSTEM  ',BL1'01000000'
         DC    CL008'SYSTEMS ',BL1'00100000'
         DC    CL008'ALL     ',BL1'11100000'
SCOPENUM EQU   (*-SCOPETBL)/SCOPELEN Scope table entry count
         DC    CL008'ALL     ',BL1'11100000' Default
**********************************************************************
         TITLE 'Major Enqueue Table'
**********************************************************************
MAJORTBL $ENTRY BASE=,DWA=,MODID=NO,SAVE=NO,SAVEAREA=NO,SWA=
         MAJORTBL                  Major Enqueue Table
**********************************************************************
         TITLE 'Resource Information Block (RIB)'
**********************************************************************
RIB      DSECT                     Resource Information Block (RIB)
RIB      ISGRIB                    Resource Information Block (RIB)
         END
//LINK    EXEC PGM=HEWL,PARM='MAP,LET,LIST',COND=(0,NE)
//SYSLIN    DD DSN=&&OBJECT,DISP=(OLD,DELETE)
//SYSLMOD   DD DISP=SHR,DSN=ZTGP01.MJCUTIL.LINKLIB(GRS) <= DESTINATION
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSPRINT  DD SYSOUT=*,OUTPUT=*.JESDS
