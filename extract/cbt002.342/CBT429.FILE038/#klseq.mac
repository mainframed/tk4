 PROC 0 DEBUGUID(USERID)        -
 LOADLIB(USERID.PROGRAMS.LOAD)  -
 FREEZEDS(KL.FREEZE)            -
 HELPFILE(USERID.KL.CNTL)       -
 GRAPHIC(NULLFILE)
 /*********************************************************************
 /*
 /*  COMMENTS ON PARMS PASSED TO THIS CLIST
 /*
 /*  DEBUGUID = USERID THAT THE TRACE DEBUGING FILE (10) IS TO
 /*             BE ALLOCATED FOR, SPECIFY AS DUMMY IF NOT DESIRED
 /*             (I.E. DEBUGUID(DUMMY))
 /*
 /*  LOADLIB  = NAME OF LOAD LIBRARY WHERE THE KL AND KLINIT PROGRAMS
 /*             ARE LINKED IN
 /*
 /*  FREEZEDS = THE NAME (UNQUALIFIED) FOR THE FREEZE FILE, IN CASE
 /*             GAME FROZEN
 /*
 /*  HELPFILE = NAME OF DATASET WHERE KLINGON HELP DATA IS KEPT
 /*
 /*  GRAPHIC  = NOT USED FOR SEQUENTIAL MODE
 /*
 /*********************************************************************
CONTROL NOFLUSH NOMSG END(OD)
ERROR DO
 WRITE
 WRITE *** NOW ALLOCATING THE FREEZE FILE, PLEASE STAND BY...
 WRITE
 /*********************************************************************
 /*
 /*  NOTICE THE FREEZE FILE IS ALLOC WITHOUT QUALIFIER, SO
 /*  PROPER PREFIX WILL BE USED.
 /*
 /*********************************************************************
 ALLOC DA(&FREEZEDS) SP(1 1) TRACKS REL DD(FT27F001)
 WRITE
 WRITE *** NOW INITIALIZING FREEZE FILE, PLEASE STAND BY...
 WRITE
 CALL '&LOADLIB(INITKL)'    /*  CALL THE INIT PROGRAM TO MAKE ZEROES */
 GOTO ALLOCD
OD
 /*********************************************************************
 /*
 /* FT06F001 IS NOT USED, BUT SOME VERSIONS OF FORTRAN INSIST ON IT
 /* SO ALLOCATED HERE.
 /*
 /*********************************************************************
ALLOC DD(FT06F001) DA(*) REUSE
WRITE
WRITE
WRITE
WRITE *** NOW ATTEMPTING TO ALLOCATE YOUR FREEZE FILE
WRITE *** IF NOT FOUND, IT WILL BE CREATED
WRITE
ALLOC DD(FT27F001) DA(&FREEZEDS) OLD
ALLOCD: -
WRITE
WRITE *** FREEZE FILE ALLOCATION COMPLETE
WRITE
ERROR OFF
FREE ATTR(F)
ATTR F RECFM(F B) LRECL(80) BLKSIZE(3200) DSORG(PS)
WRITE
WRITE *** NOW CREATING HIGH SPEED WORK FILES
WRITE
ALLOC DD(FT99F001) UNIT(VIO) USING(F) SP(1 1) CYL REUSE
ALLOC DD(FT98F001) UNIT(VIO) USING(F) SP(1 1) CYL REUSE
ALLOC DD(FT97F001) UNIT(VIO) USING(F) SP(1 1) CYL REUSE
 /********************************************************************
 /*
 /* UNIT=10 IS THE DEBUG FILE, WILL NOT ACTUALLY BE USED UNLESS
 /* DEBUG MODE IS ACTIVATED
 /* (ACTIVATED BY XDEBUG COMMAND WITH PASSWORD)
 /* (LIMITED HERE TO ALLOCATE ONLY IF THE DEBUGUID MATCHES
 /*  LOGONID BEING USED)
 /*
 /********************************************************************
IF &SYSUID = &DEBUGUID THEN ALLOC DD(FT10F001) SYSOUT(X) HOLD REUSE
 /********************************************************************
 /*
 /*  UNIT 5  = INPUT FROM TERMINAL
 /*
 /*  UNIT 8  = SYSOUT TO SEND CERTIFICATES OF MERIT TO
 /*            (FOR WINNERS OF EXPERT AND EMERITUS GAMES)
 /*
 /*  UNIT 25 = FILES WHERE GRAPHICS FILES ARE KEPT, COPIED TO
 /*            HIGH SPEED WORK FILES
 /*
 /*  UNIT 26 = KLINGON HELP FILE (NOT IBM STANDARD HELP)
 /*
 /********************************************************************
 /*
ALLOC DD(FT05F001) DA(*) SHR REUSE
ALLOC DD(FT08F001) SYSOUT(A) DEST(RMT6) REUSE NOHOLD
ALLOC DD(FT25F001) DA('&GRAPHIC') SHR REUSE
ALLOC DD(FT26F001) DA('&HELPFILE') SHR REUSE INPUT
 /*
CALL '&LOADLIB(KL)'
 /*
FREE DA('&HELPFILE')
FREE DA('&GRAP43')
FREE DA('&GRAP24')
FREE DA('&LOADLIB')
 /*
FREE DD(FT27F001,FT08F001,FT26F001,FT99F001,FT98F001,FT06F001)
FREE DD(FT25F001,FT97F001,FT10F001,FT05F001)
 /*
WRITE
WRITE ***********************************************************
WRITE *** PLEASE ONLY KEEP THE FREEZE FILE IF YOU FROZE A GAME  *
WRITE ***********************************************************
WRITE
WRITENR   DO YOU WISH TO KEEP THE FREEZE FILE (Y/N) ?
READ &OPT
IF &OPT EQ Y OR &OPT EQ YES THEN GOTO KEEP
DELETE &FREEZEDS
WRITE
WRITE *** FREEZE FILE DELETED
WRITE
KEEP: END
