         TITLE 'FS IO ROUTINE'
**********************************************************************
*
*  ROUTINE FSIO2 - DO FULLSCREEN TPUT FOR GRAFICS ROUTINES
*
**********************************************************************
FSIO2    CSECT                          INITIALIZATION
         STM   R14,R12,12(R13)          STORE REGS IN SUPPLIED SAVEAREA
         LR    R12,R15                  ESTABLISH ADDRESSABILITY
         USING FSIO2,R12                WE GOT IT
         ST    R13,SAVEAREA+4           NON-RENT, NO BIG DEAL HERE
         LA    R15,SAVEAREA             SET FORWARD/BACKWORD POINTERS
         ST    R15,8(,R13)                 *
         LA    R13,SAVEAREA                *
*
* 1) ADDRESS WORK BUFFER
*
         L     R11,=V(DISPLY)           ADDRESS OF BUFFER CSECT
         USING $DSBUFF,R11              ADDRESS IT
*
* 2) OBTAIN INFO ABOUT TERMINAL (IBM3270) IS IT 24, 32 OR 43 LINES?
*
         L     R3,=V(TERMSZ)            TERMINAL SIZE CSECT
         ICM   R0,B'1111',0(R3)         FILLED IN?
         BNZ   NOGTSIZE
         GTSIZE ,                       OBTAIN IBM TERMINAL INFO
         LTR   R0,R0                    DETERMINE IF DISPLAY TERMINAL
         BZ    RETURN                   IF R0=0, WE IN WRONG PLACE, OUT
         ST    R0,0(R3)                 SAVE IT
NOGTSIZE DS    0H
         CLC   =F'43',0(R3)             EXACTLY 43 LINES ?
         BE    PROCEED                  CONTINUE IF OK
         CLC   =F'32',0(R3)             EXACTLY 32 LINES ?
         BE    PROCEED                  CONTINUE IF OK
         CLC   =F'24',0(R3)             EXACTLY 24 LINES ?
         BE    PROCEED                  CONTINUE IF OK
         LA    R1,NOT24#48              OTHERWISE LET USER KNOW
         LA    R0,NOT24LEN              WE CAN'T HANDLE THEM
         TPUT  (1),(0),R                NON-FS TPUT
         B     RETURN                   GET OUT
PROCEED  DS    0H
         L     R3,0(R3)                 INIT LENGTH
*
*    FILL IN SBA
*
         MVC   8-WCCSBAL(WCCSBAL,R11),WCCSBA  WCC-SBA ORDERS
         MH    R3,=AL2(80)              80 BYTE LINES
         LA    R3,WCCSBAL(,R3)
         LR    R0,R3                    SETUP LENGTH
         LA    R1,8-WCCSBAL(,R11)       ADDRESS OF BUFFER
         ICM   R1,8,TPUTFLG             SETUP TPUT FLAGS
         TPUT  (1),(0),R                ISSUE FULLSCREEN TPUT
*
*    RESTORE REGS AND RETURN
*
RETURN   DS    0H
         L     R13,SAVEAREA+4           GET ORIGINAL REGISTER 13
         LM    R14,R12,12(R13)          RESTORE INPUT REGISTERS
         SR    R15,R15                  SET RETURN CODE
         BR    R14                      AND RETURN ...
         EJECT
         LTORG
***********************************************************************
*        REGISTERS, CONSTANTS, AND STUFF                              *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*   WORK AREAS (NON-RENT CODE)
*
SAVEAREA DS    9D                       REG SAVE AREA
NOT24#48 DC    C'THIS PROGRAM SUPPORTS ONLY 24, 32 OR 43 LINE 3270'
NOT24LEN EQU   *-NOT24#48               LENGTH OF MSG
         DS    0F
WCCSBA   DC    X'C3114040'              WCC-SBA ORDERS
WCCSBAL  EQU   *-WCCSBA                 AND IT'S LENGTH
*
TPUTFLG  DC    X'03'                    TPUT FULLSCREEN FLAGS
$DSBUFF  DSECT                          START OF MAP FOR FSBUFF DSECT
$PREFIX  DS    D                        ROOM FOR SBA
$AREA    DS    (80*43)CL1               TPUT WORK AREA
         END
