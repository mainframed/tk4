/* ---------------------------------------------------------------- */
/*  POFFLOAD EXEC                                                   */
/*  AUTHOR:  MARTIN                                                 */
/*  DATE:    13 Feb 1992                                            */
/* ---------------------------------------------------------------- */
/*  ABSTRACT:                                                       */
/*  Liest ein Band, welches im Power Offload Format vorliegt        */
/*  und druckt es in den READER                                     */
/*                                                                  */
/*  PARAMETER:                                                      */
/*  NR:      Anzahl der Dateien auf dem Band                        */
/*  OP:      OnePrintfile (J / N)  gibt an, ob alle Dateien in ein  */
/*           PRT-file geschrieben werden soll oder auf mehrere (nr) */
/*           verteilt wird                                          */
/*  PRTNAME: Name des PRT-Files. Der filetype ist PWDUMP,           */
/*           bei mehreren PWDUMP1, PWDUMP2 ...                      */
/*                                                                  */
/*  Wenn die Dateien mit einer TapeMark abgeschlossen werden, kann  */
/*  man fuer NR eine grosse Zahl (zB 99) angeben, bei erreichen der */
/*  TapeMark wird mit RC 4 abgebrochen.                             */
/*                                                                  */
/* ---------------------------------------------------------------- */

TRACE n;

/* ---------------------------------------------------------------- */
/*  Defaults besetzen                                               */
/* ---------------------------------------------------------------- */
nr        = 99;
op        = N;
prtname   = TAPE;

fileno    = 1;
retcode   = 0;

parse upper arg kette

/* ---------------------------------------------------------------- */
/*  Eingabeschleife                                                 */
/* ---------------------------------------------------------------- */
DO forever

   parse upper value kette with 'NR ' input .
   IF input /= "" THEN nr      = input;
   parse upper value kette with 'OP ' input .
   IF input /= "" THEN op      = input;
   parse upper value kette with 'PRTNAME ' input .
   IF input /= "" THEN prtname = input;

   IF op /= 'N' THEN op = 'J';

   call out;
   pull kette;

   IF kette = '' THEN leave;
   IF kette = 'ENDE' THEN EXIT 255;
END

say 'Bitte Band auf 181 legen und bestdtigen';
pull kette;
IF kette = 'ENDE' THEN EXIT 255;
   'REW 181'

'FILEDEF SYSPRINT PRINTER'
'CP SPOOL PRT TO * NOHOLD CONT'
/* 'TAPE FSR '  */
   'FILEDEF PWRTAPE TAP1 LABOFF '
DO WHILE (fileno <= nr) & (retcode == 0)

   say 'Datei Nr. 'fileno' wird gerade gedruckt';

   fileno = fileno + 1;
   'POFFLOAD'
   'POFFLOAD'
   retcode = rc;

   IF op /= 'J' THEN
   DO
      'CP SPOOL PRT NOCONT';
      'CP CLOSE PRT NAME 'prtname' PWDUMP'fileno-1;
      'CP SPOOL PRT TO * NOHOLD CONT';
   END

END


'CP SPOOL PRT NOCONT'
'CP CLOSE PRT NAME 'prtname' PWDUMP';

exit retcode;

/* ---------------------------------------------------------------- */
/*  Ausgabemaske                                                    */
/* ---------------------------------------------------------------- */
out:

tab1=25;
OPJ = '(J/N) Es wird in eine PRT-Datei gedruckt';
OPN = '(J/N) Es wird in mehrere PRT-Dateien gedruckt';

l.1 =   "+---------------------------------------"
l.1 = l.1"---------------------------------------+"
l.2 =   "!                                       "
l.2 = l.2"                                       !"

l.2 = overlay('POFFLOAD',l.2,4,);
l.2 = overlay(date(european), l.2, 60);
l.2 = overlay(time(), l.2, 70);
l.2 = overlay('Fuer User 'userid(), l.2, tab1);
l.3 = l.1;
l.4 = ''
l.5 = 'NR        = 'nr;
l.5 = overlay('Anzahl der zu druckenden Dateien', l.5, tab1);
l.6 = 'OP        = 'op;

IF OP == 'N' THEN
   l.6 = overlay(OPN, l.6, tab1);
ELSE
   l.6 = overlay(OPJ, l.6, tab1);

l.7 = 'PRTNAME   = 'prtname;
l.7 = overlay('Name der Druckdatei', l.7, tab1);
l.8 = ''
l.9 = 'Wenn diese Werte richtig sind, mit DatenFreigabe bestdtigen'
l.10= 'oder neue Werte eingeben, (zB nr 6 op yes)';
l.11= 'oder ENDE fuer Ende, Band wird nicht gelesen'
l.12= ''
'CLS'
DO i = 1 to 12
   say l.i
END
return;
