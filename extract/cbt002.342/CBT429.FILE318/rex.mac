/* ---------------------------------------------------------------- */
/*  POFFLOAD EXEC                                                   */
/*  AUTHOR:  MARTIN                                                 */
/*  DATE:    13 Feb 1992  translated 05 Mar 1998 by Knut            */
/* ---------------------------------------------------------------- */
/*  ABSTRACT:                                                       */
/*  Reads a tape in Power Offload Format                            */
/*  and prints it to your VM READER                                 */
/*                                                                  */
/*  PARAMETER:                                                      */
/*  NR:      Number of files on tape                                */
/*  OP:      OnePrintfile (Y / N)  specifies whether all files are  */
/*           to be printed to one PRT file or to print them         */
/*           separately.  (Program POFFLOAD should not close PRT    */
/*           output. See comment in program)                        */
/*  PRTNAME: Name of the PRT file. Filetype is  PWDUMP,             */
/*           for separate files PWDUMP1, PWDUMP2 ...                */
/*                                                                  */
/*  If the files are limited by a TapeMark you may specify a large  */
/*  number for NR (e.g. 99). Reaching the TapeMark the program      */
/*  stops with a RC of 4.                                           */
/*                                                                  */
/* ---------------------------------------------------------------- */

TRACE n;

/* ---------------------------------------------------------------- */
/*  Set Defaults                                                    */
/* ---------------------------------------------------------------- */
nr        = 99;
op        = N;
prtname   = TAPE;

fileno    = 1;
retcode   = 0;

parse upper arg kette

/* ---------------------------------------------------------------- */
/*  Input loop                                                      */
/* ---------------------------------------------------------------- */
DO forever

   parse upper value kette with 'NR ' input .
   IF input /= "" THEN nr      = input;
   parse upper value kette with 'OP ' input .
   IF input /= "" THEN op      = input;
   parse upper value kette with 'PRTNAME ' input .
   IF input /= "" THEN prtname = input;

   IF op /= 'N' THEN op = 'Y';

   call out;
   pull kette;

   IF kette = '' THEN leave;
   IF KETTE = 'END' THEN EXIT 255;
END

say 'Please mount tape on 181 and confirm   ';
pull kette;
IF kette = 'END' THEN EXIT 255;
   'REW 181'

'FILEDEF SYSPRINT PRINTER'
'CP SPOOL PRT TO * NOHOLD CONT'
/* 'TAPE FSR '  */
   'FILEDEF PWRTAPE TAP1 LABOFF '
DO WHILE (fileno <= nr) & (retcode == 0)

   say 'File  No. 'fileno' is being printed    ';

   fileno = fileno + 1;
   'POFFLOAD'
   'POFFLOAD'
   retcode = rc;

   IF op /= 'J' THEN
   DO
      'CP SPOOL PRT NOCONT';
      'CP CLOSE PRT NAME 'prtname' PWDUMP'fileno-1;
      'CP SPOOL PRT TO * NOHOLD CONT';
   END

END


'CP SPOOL PRT NOCONT'
'CP CLOSE PRT NAME 'prtname' PWDUMP';

exit retcode;

/* ---------------------------------------------------------------- */
/*  Output mask                                                     */
/* ---------------------------------------------------------------- */
out:

tab1=25;
OPJ = '(Y/N) One PRT file is created           ';
OPN = '(Y/N) Separate PRT files are created         ';

l.1 =   "+---------------------------------------"
l.1 = l.1"---------------------------------------+"
l.2 =   "!                                       "
l.2 = l.2"                                       !"

l.2 = overlay('POFFLOAD',l.2,4,);
l.2 = overlay(date(european), l.2, 60);
l.2 = overlay(time(), l.2, 70);
l.2 = overlay('For User 'userid(), l.2, tab1);
l.3 = l.1;
l.4 = ''
l.5 = 'NR        = 'nr;
l.5 = overlay('Number of files to be printed   ', l.5, tab1);
l.6 = 'OP        = 'op;

IF OP == 'N' THEN
   l.6 = overlay(OPN, l.6, tab1);
ELSE
   l.6 = overlay(OPJ, l.6, tab1);

l.7 = 'PRTNAME   = 'prtname;
l.7 = overlay('Name of print file ', l.7, tab1);
l.8 = ''
l.9 = 'If these values are correct please confirm with ENTER      '
l.10= 'or key in new values (e.g. nr 6 op y)     ';
l.11= 'or END for stop, tape will not be read.    '
l.12= ''
'CLS'
DO i = 1 to 12
   say l.i
END
return;
