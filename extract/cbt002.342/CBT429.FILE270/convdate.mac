*          DATA SET 752DZCONV3 AT LEVEL 003 AS OF 06/04/82
*          DATA SET 140SWCONVD AT LEVEL 001 AS OF 08/25/71
*        DATE CONVERSION
*
**   MODIFIED BY WDPSC TO MAKE CODE RE-ENTRANT.
*        THE UNUSED STORAGE IN THE INPUT SAVEAREA IS USED.
*
*        A COBOL DRIVER PROGRAM TO TEST THIS MODULE FOLLOWS THE END
*        CARD IN THIS PANVALET DATASET.  IT MAY BE COMPILED BY
*        USING THE PROPER 'SELECT WORK' CARD.
*
         PUNCH ' ALIAS  JTOSDATE '
         PUNCH ' ALIAS  STOJDATE '
*
**   SYMBOLIC REGISTERS
R15 EQU 15 - BASE
R14 EQU 14 - RETURN
R13 EQU 13 - SAVE AREA
R12      EQU   12
R11      EQU   11
R5       EQU   5
R1  EQU 1  - PARM POINTER
*
*
CONVDATE CSECT
*
*        THIS SUBPROGRAM CONVERTS JULIAN DATES TO STANDARD OR
*              STANDARD DATES TO JULIAN BY USING ALTERNATE
*              ENTRY POINTS
*
*           JULIAN TO STANDARD--CALL 'JTOSDATE' AND PROVIDE JULIAN
*                   DATE AND AN AREA TO RECEIVE STANDARD DATE
*           STANDARD TO JULIAN--CALL 'STOJDATE' AND PROVIDE STANDARD
*                   DATE AND AN AREA TO RECEIVE JULIAN DATE
*
         ENTRY JTOSDATE,STOJDATE
*
*        THIS SUBROUTINE CONVERTS ANY 5-DIGIT JULIAN DATE PROVIDED
*              IN EXTERNAL DECIMAL FORMAT (YYDDD) TO A 6-DIGIT
*              STANDARD DATE IN EXTERNAL DECIMAL FORMAT (YYMMDD)
*
**     ATTRIBUTES - RE-ENTRANT
*
JTOSDATE EQU   *    ENTRY FOR JULIAN-TO-STANDARD CONVERSION
         SAVE  (14,1)    SAVE REGS 14, 15, 0, 1
         USING JTOSDATE,R15        MODULE BASE
         USING SAVEAREA,R13        STORAGE BASE
         L     R1,0(,R1)           LOAD PARM POINTER
         PACK  JYR,0(2,R1)         PACK YEAR
         PACK  JDAY,2(3,R1)        PACK JULIAN DAY
         ZAP   MO1,=P'1'           INITIALIZE FIELD 'MO1'
         ZAP   MOCNT,MO1           INITIALIZE MONTH
         LA    R14,TABLE
*                         CONSIDER LEAP YEAR
         ZAP   LYR,JYR
         DP    LYR,=P'4'
         CP    LYR+3(1),=P'0'
         BNE   NOTLYR         IF NOT A LEAP YEAR
         CP    JDAY,=P'60'    LEAP YEAR
         BL    NOTLYR
         BH    PASTFEB
         MVC   SMO,=C'02'     FEB 29
         MVC   SDAY,=C'29'
         B     MVCYR
PASTFEB  SP    JDAY,=P'1'     PAST FEB 29
*                         CONVERT ANY DATE EXCEPT FEB 29
NOTLYR   CP    0(2,R14),JDAY       TO STANDARD DATE
         BNL   MVCDATE
         SP    JDAY,0(2,R14)       SET UP TO TEST NEXT
         AP    MOCNT,=P'1'         MONTH AND
         LA    R14,2(,R14)         LOOP BACK
         B     NOTLYR
*                         UNPACK AND FORMAT STANDARD DATE
MVCDATE  UNPK  DAY,JDAY       DAY
         MVZ   DAY+2(1),DAY+1
         MVC   SDAY,DAY+1
         UNPK  MONTH,MOCNT    MONTH
         MVZ   MONTH+2(1),MONTH+1
         MVC   SMO,MONTH+1
MVCYR    UNPK  YEAR,JYR       YEAR
         MVZ   YEAR+2(1),YEAR+1
         MVC   SYR,YEAR+1
*                         PLACE STANDARD DATE IN PASSED
         L     R1,S1               PARAMETER LIST
         L     R14,4(R1)           CALLER'S AREA
         MVC   0(6,R14),SDATE      MOVE SDATE INTO CALLER'S AREA
         LA    R15,0               ZERO R/C
         L     R14,S14             RETURN TO
         BR    R14                 CALLER
         SPACE
*        THIS SUBROUTINE CONVERTS ANY 6-DIGIT STANDARD DATE PROVIDED
*              IN EXTERNAL DECIMAL FORMAT (YYMMDD) TO A 5-DIGIT
*              JULIAN DATE IN EXTERNAL DECIMAL FORMAT (YYDDD)
*
         DROP  R15
STOJDATE DS    0F   ENTRY FOR STANDARD-TO-JULIAN CONVERSION
*
         SAVE  (14,1)    SAVE REGS 14, 15, 0, 1
         ST    R12,S12
         ST    R11,S11
         LR    R12,R15
         USING STOJDATE,R12
         SPIE  DATAEXCP,(7)
         ST    R1,HOLD
         L     R1,S1                   LOAD PARM POINTER
         L     R1,0(,R1)
         PACK  JYR,0(2,R1)         YEAR
         PACK  PMONTH,2(2,R1)      MONTH
         PACK  PDAY,4(2,R1)        DAY
*
*
         CP    PMONTH,=P'12'            D ZEIGLER  8-1-77
         BH    ABEND12                  D ZEIGLER  8-1-77
*                         INITIALIZE COUNTERS
         ZAP   MO1,=P'1'           INITIALIZE FIELD 'MO1'
         ZAP   MOCNT,MO1
         ZAP   JDAY,=P'0'
         LA    R14,TABLE
*                         CONVERT ANY DATE TO JULIAN DATE
CKMONTH  CP    PMONTH,MOCNT
         BE    CKLYR
         AP    JDAY,0(2,R14)       SET UP TO CHECK NEXT
         AP    MOCNT,=P'1'         MONTH AND
         LA    R14,2(,R14)         LOOP BACK
         B     CKMONTH
CKLYR    AP    JDAY,PDAY
*                         CONSIDER LEAP YEAR
         ZAP   LYR,JYR
         DP    LYR,=P'4'
         CP    LYR+3(1),=P'0'
         BNE   CMPDAY                   D ZEIGLER  8-1-77
         CP    PMONTH,=P'2'
         BE    CHK29                    D ZEIGLER  8-1-77
         BL    CMPDAY
         CP    0(2,R14),PDAY
         BL    ABEND31
         AP    JDAY,=P'1'     PAST FEB
*                         UNPACK AND FORMAT JULIAN DATE
SKPLYR   UNPK  YEAR,JYR
         MVZ   YEAR+2(1),YEAR+1
         UNPK  DAY,JDAY
         MVZ   DAY+2(1),DAY+1
*                         PLACE JULIAN DATE IN PASSED
         L     R1,S1               PARAMETER LIST
         L     R14,4(R1)           CALLER'S AREA
         MVC   0(5,R14),JULDATE    MOVE JDATE INTO CALLER'S AREA
*
*
*
*
*
*
*
*
*
*
*
         L     R12,S12
         L     R11,HOLD
         SPIE  MF=(E,(11))
         L     R11,S11
         SR    R15,R15
         L     R14,S14             RETURN TO
         BR    R14                 CALLER
CMPDAY   CP    0(2,R14),PDAY            D ZEIGLER   8-1-77
         BL    ABEND31                  D ZEIGLER   8-1-77
         B     SKPLYR                   D ZEIGLER   8-1-77
DATAEXCP ABEND 7,DUMP
CHK29    CP    PDAY,=P'29'              D ZEIGLER   8-1-77
         BH    ABEND31                  D ZEIGLER   8-1-77
         B     SKPLYR                   D ZEIGLER   8-1-77
ABEND12 ABEND 12                       D ZEIGLER  8-1-77
ABEND31 ABEND 31                       D ZEIGLER  8-1-77
         SPACE
*                         CONSTANTS AND WORK AREAS
         SPACE
SAVEAREA DSECT      REGISTER SAVE AREA; ALSO USED FOR STORAGE
         DS    3A
S14      DS    A
S15      DS    A
S0       DS    A
S1       DS    A
*
SDATE    DS    0CL6           STANDARD DATE--EXTERN DEC
SYR      DS    CL2
SMO      DS    CL2
SDAY     DS    CL2
*
YEAR     DS    0CL3
         DS    CL1
JULDATE  DS    0CL5           JULIAN DATE--EXTERN DEC
         DS    CL2
DAY      DS    CL3
MONTH    DS    CL3
*
JYR      DC    PL2'0'         JULIAN DATE--PACKED
JDAY     DC    PL2'0'
*
PMONTH   DC    PL2'0'         INTERMEDIATE WORK AREAS
PDAY     DC    PL2'0'
LYR      DC    PL4'0'
MO1      DC    P'1'              INITIALIZE MONTH COUNT
MOCNT    DC    PL2'0'            MONTH COUNTER
*                             TABLE OF MONTH LENGTHS
*
HOLD     DC    F'0'
S11      DS    A
S12      DS    A
*
         ORG   SAVEAREA+72
         SPACE
CONVDATE CSECT
         LTORG
         SPACE
TABLE    DS    0CL24
         DC    P'31'             JAN
         DC    P'28'             FEB
         DC    P'31'             MAR
         DC    P'30'             APR
         DC    P'31'             MAY
         DC    P'30'             JUNE
         DC    P'31'             JULY
         DC    P'31'             AUG
         DC    P'30'             SEPT
         DC    P'31'             OCT
         DC    P'30'             NOV
         DC    P'31'             DEC
*
         END
