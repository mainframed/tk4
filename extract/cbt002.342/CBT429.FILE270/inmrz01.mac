INMRZ01  TITLE 'INMRZ01 - ACF2/TSOE-RECEIVE COMMAND SECURITY EXIT'
********************************************************************
*
*  THIS EXIT CALLED BY TSOE RECEIVE COMMAND TO AUTHORIZE REQUESTS  *
*       BY GENERATING A PSUEDO DATASET NAME FOR ACF2 CHECKING      *
*
********************************************************************
         MACRO
&LAB1    ENTERR  &SA=SAVEAREA,&WA=WORKAREA,&WL=WORKLEN,&LEVEL=,&R=,   XX
               &CLEAR=YES
         MNOTE ' CLEAR=&CLEAR,SA=&SA,WA=&WA,WL=&WL,LEVEL=&LEVEL'
&LAB1    CSECT
         SAVE  (14,12),,&LAB1-&LEVEL
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         LR    R10,R1             SAVE PARM PTR R10->PARM PTR
         L     R0,=A(&WL)         R0=GET LENGTH
         GETMAIN R,LV=(0)         R1->WORKAREA
         LR    R11,R13            R11->CALLERS SAVEAREA
         LR    R13,R1             R13->WORKAREA
         USING &WA.,R13
         AIF   ('&CLEAR' NE 'YES').NCLEAR
         L     R15,=A(&WL)        R0=GET LENGTH
         S     R15,=F'72'         SKIP REGS
         MOVE  72(13),(15),0,0,PAD=X'00'
.NCLEAR  ANOP
         ST    R11,&SA.+4         SAVE HIS SAVEAREA PTR
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         LR    R11,R1             R11->WORKAREA IN CASE NOT SAME AS R13
*                       WORKAREA ADDR IS STILL R13 FOR
*                       THE ASSEMBLER - IF DIFFERENT FROM R13
*                       THEN USE: DROP R13 AND USING &WA.,R11
         LR    R1,R10             RESTORE PARM PTR PTR
         AIF   ('&R' EQ 'NO').NRE
         REGEQU
.NRE     ANOP
         MEND
         MACRO
&LAB     LEAVER &WR=R13,&WL=WORKLEN
&LAB     LR    R1,&WR             WORKAREA ADDR FOR FREEMAIN
         L     R0,=A(&WL)         WORKAREA LEN   "    "
         L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         LR    R11,R15            SAVE RETURN CODE
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R11            RESTORE RETURN CODE
         RETURN (14,12),RC=(15)
         MEND
         EJECT
INMRZ01  ENTERR LEVEL=V1M0
         EJECT
         LR    R11,R1      R11->PARM LIST
         L     R1,20(R11)  R1->FLAG BYTE
         TM    0(R1),X'80' SPOOL REQUEST?
         BO    EXIT        NO-> ACF2 WILL HANDLE
         L     R10,24(R11) R10->CURRENT USERID
         L     R9,28(R11)  R9->REQUESTED USERID
         CLC   0(8,R9),0(R10)     NORMAL REQUEST?
         BNE   CHEKACF2    NO->BETTER CROSS CHECK
EXIT     SLR   R15,R15            RC=0
RET      LEAVER
         EJECT
*
*  FIND ACF2 CVT
*
CHEKACF2 ACFGACVT R8,NONE=EXIT
         USING ACCVT,R8           R8 = A(ACF2 CVT)
         SPACE
         L     R3,8(R11)          R3->ACTION FLAGS
         L     R6,4(R11)          R6->PARM STRING
         LH    R7,0(R6)           R7=PARM LEN
         CH    R7,=H'5'           <5?
         BL    NOTWDPSC
         CLC   2(5,R6),=C'WDPSC'  WDPSC SPOOL MAINT JOB?
         BNE   NOTWDPSC           NOPE
         EJECT
*
*   ACCESS THE USER'S ACUCB (THIS CODE IS WDPSC SPECIFIC):
*        CHECK TO OK SPOOL MAINT TYPE JOBS
*
         ACFGUCB R4,NONE=EXIT
         USING ACUCB,R4
         L     R5,ACUUIDP         R5->WDPSC UID STRING
         CLC   0(8,R5),=CL24'150D2100'   OS SUPPORT?
         BE    OKDOKE
         CLC   0(8,R5),=CL24'150D2300'   NET SERVICES?
         BE    OKDOKE
         EJECT
*
*   IF OPER BIT IS NOT ON, THEN ASK ACF2 FOR AUTH:
*
NOTWDPSC L     R1,12(R11)         R1->CPPL
         L     R2,8(R1)           R2->PSCB PASSED
         TM    16(R2),X'80'       OPER BIT ON?
         BO    EXIT               YES->HE'S OK
*
*   BUILD PARM LIST TO ASK ACF2 IF RECEIVE IS OK:
*
         XC    ACFSPARM(ACFSPRML),ACFSPARM  CLEAR ACF2 PARM BLOCK
         MVI   ACFSPREQ,ACFSPRDS      DSNAME CHECK
         MVI   ACFSPID1,ACFSPIUR   USER CALL
         MVI   ACFSPAC1,ACFSPAOT+ACFSPAXX ALLOC TYPE,LIST END
         MVC   FONYDSN,=CL44' '   CLEAR
         MVC   FONYDSN(8),0(R9)   COPY PSUEDO USERID
         LA    R6,FONYDSN         R6->DSN BLD AREA
FLOOP    CLI   0(R6),C' '         FIND BLANK?
         BE    GOT1               YUP
         LA    R6,1(R6)           R6+1
         B     FLOOP
GOT1     MVC   0(13,R6),=C'.RECEIVE.FILE'
         LA    R6,FONYDSN         R6->DSN BLD AREA
         ST    R6,ACFSPDSN        ->DSNAME
*
* NOW CHECK THE PSUEDO DSN WITH ACF2 FOR AUTHORIZATION:
*
         ACFSVC ACDSV,TYPE=S,CVT=HAVE,NONE=EXIT
         LTR   R15,R15            OK?
         BZ    OKDOKE             YUP!
ABORT    OI    0(R3),X'04'        SAY NONO!!!!
         B     EXIT
OKDOKE   OI    0(R3),X'08'        SAY OK!
         B     EXIT               STILL OK!
         EJECT
         LTORG
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
FONYDSN  DS    CL44
         EJECT
         ACDSV DSECT=NO
WORKLEN  EQU   *-WORKAREA
         EJECT
         ACCVT
         EJECT
         ACUCB
         EJECT
         ACFASVT
         END
