*          DATA SET CBT932     AT LEVEL 001 AS OF 02/25/79
* PSWSAMP           VERSION    1.0       WDPSC
* PSWSAMP SOURCE IS IN  WDPSC.DN00150.FILE(PSWSAMP)
*         IS THE SAMPLE TAKER FOR PACKLIST. THE TECHNIQUE IS TO MOVE
* THE TRACE TABLE TO AN OUTPUT RECORD. IT DOES NOT MATTER THAT THE
* MOVE LONG OF THE TABLE CAN BE INTERUPTED. THE VALUES TAKEN AT ANY
* POINT ARE A SAMPLE, WHETHER INTERUPTED OR NOT.
* THE SAMPLE WILL NOT BEGIN UNLESS THE FIRST ENTRY OF THE TABLE IS
* NOW DIFFERENT THAN THE PREVIOUS SAMPLE. THE SAMPLE RATE IS
* APPROXIMATELY ONCE EVERY 2.5 SECONDS (THE FIELD HOW LONG CAN BE
* ADJUSTED).
* THE TECHNIQUE FOR OVERALL SAMPLING AND END USE IS COVERED IN  THE
* PACKLIST PROGRAM .
*
* PACKLIST WILL RUN FOR THE TIME INTERVAL SET WITHIN THIS PROGRAM
* AND THEN WILL AUTOMATICALLY TERMINATE.  IT CAN ALSO BE STOPPED BY
* REPLYING TO THE WTOR ISSUED.  AT TERMINATION A MESSAGE IS ISSUED
* SUPPLYING THE NAME OF THE TAPE USED TO COLLECT THE DATA.  THE TAPE
* IS NOT CATALOGED SO WRITE ITS VOLSER DOWN FOR USE IN PACKLIST WHEN
* THE OPERATOR CALLS.
*
* INVOKING JCL     AVAILABLE IN 'SYS1.PROCLIB(PSWSAMP)'
* //PSWSAMP PROC
* //SAMPLE  EXEC PGM=PSWSAMP
* //STEPLIB DD DSN=WDPSC.SOFTLOAD,DISP=SHR
* //TAPE DD DDNAME=IEFRDER
* //IEFRDER DD DSN=PSWS,UNIT=TAPE,DISP=(,KEEP),LABEL=RETPD=15
*
         PRINT OFF
         MACRO
         REGISTER
         LCLA  &I
.LOOP    ANOP
R&I      EQU   &I
&I       SETA  &I+1
         AIF   (&I LT 16).LOOP
         MEND
         PRINT ON
         TITLE  'PACKLIST   SAMPLE ROUTINE'
PSWSAMP CSECT
         REGISTER              REGISTER EQUATES
         SAVE   (14,12),,*     ENTRY SAVE
         LR    R12,R15         BASE REGISTER
         USING PSWSAMP,R12
         LA    R8,SAVEAREA     SAVE  AREA
         ST    R8,8(,R13)      BACK CHAIN
         ST    R13,4(,R8)      FORWARD CHAIN
         LR    R13,R8
         TIME  BIN             TIME CHECK
         AL    R0,MAXTIME      MAX IS TIME TO RUN
         ST    R0,TIME         SAVE FOR REFERENCE
* MESSAGE TO OPERATOR SO PROGRAM CAN BE ENDED
         WTOR  '*** PSW ACTIVITY SAMPLER, ENTER STOP TO STOP',REPLY,   X
               L'REPLY,ECB,ROUTCDE=(2,11)
* CONTROL CONSTANTS
         L     R4,84           TRACE TABLE POINTER
         LM    R4,R5,4(R4)     ENTRIES IN TRACE TABLE
         SR    R5,R4        TRACE TABLE LENGTH IN R5
         STH   R5,TAPE+82     LRECL
         STH   R5,TAPE+62     BLKSIZE
         GETMAIN R,LV=(R5)     WORK AREA
         LR    R6,R1           WORK AREA
         LR    R7,R5           LENGTH
         OPEN  (TAPE,(OUTPUT)) OUTPUT FILE
         STM   R4,R7,MOVLONG   SAVE REGISTERS FOR LOADING
REWRITE  LM    R4,R7,MOVLONG   LOAD MOVE REGISTERS
         MVCL  R6,R4           MOVE DATA FROM TRACE TABLE TO OUTPUT
         L     R6,MOVLONG+8    ADDRESS OF OUTPUT AREA
         PUT   TAPE,(R6)       EACH COPY IS SEPARATE RECORD
         CLC   REPLY,STOP      OPERATOR STOPPING SAMPLE
         BE    GOHOME          YES
         TIME  BIN             TIME CHECK
         CL    R0,TIME         HAS MAX TIME BEEN REACHED
         BH    GOHOME          YES, THEN STOP
WHEN     EQU   *
         STIMER WAIT,MICVL=HOWLONG   WAIT A FEW SECONDS
         CLC   28(4,R4),28(R6)       HAS ENTIRE TRACE TABLE CHANGED
         BNE   REWRITE               YES
         B     WHEN                  NO, WAIT SOME MORE
GOHOME   EQU   *
         L     R4,TAPE+44         GET DEB POINTER
         L     R5,32(,R4)         GET UCB ADDRESS
         MVC   ENDWTO-6(L'VOLSER),36(R5)   SAVE THE VOLSER
         WTO   'PLEASE TELL SOFTWARE THAT PSWSAMP HAS BEEN STOPPED',  **
               ROUTCDE=(2,11)
         LA    R1,WTOMSG
         WTO   MF=(E,(1))      OUTPUT VOUME SERIAL NUMBER IN WTO
         CLOSE TAPE            CLOSE THE OUTPUT FILE
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14             RETURN
SAVEAREA DS   18F
MOVLONG  DC    5F'0'
MAXTIME  DC    F'90000'
TIME     DC    F'0'
STOP     DC    C'STOP'
WTOMSG   WTO   'AND THE VOLSER OF THE TAPE IS 000000',MF=L
ENDWTO   EQU   *
REPLY    DC    CL4' '
VOLSER   DS    CL6
ECB      DC    F'0'
WAITBLK  DC    F'0'
HOWLONG  DS    0D
         DC    X'0000000244400000'
TAPE     DCB   DDNAME=TAPE,DSORG=PS,MACRF=PM,RECFM=F,BUFNO=1
         END
