//KKA00150 JOB (2100,56-D),'KISER 2215 T',CLASS=H,MSGCLASS=T
//F EXEC ASMHCL,LIB3='SYS1.AMODGEN',LIBRARY='WDPSC.PROGLIB',
//  OPTION2='AC=1',LNDSP=SHR OPTION1=TERM
         TITLE 'COMMANDR - '
COMMANDR CSECT
         SAVE  (14,12),,*
         BALR  R12,0            ESTABLISH PROGRAM BASE
         USING *,R12
         ST    R13,SAVE+4       SAVE AREA
         LA    R2,SAVE
         ST    R2,8(,R13)       BACK POINTERS
         LR    R13,R2
         L     R4,0(,R1)        PARAMETER STRING
         LH    R5,0(,R4)        LENGTH OF COMMAND
         CH    R5,=H'120'       MAXIMUM COMMAND LENGTH
         BH LETSTOP
         BCTR  R5,0              PREPARE FOR MOVE
         EX    R5,CMDMOVE
CMDMOVE  MVC   CMDAREA(0),2(R4)
*
         SR    R0,R0
         WTO   MF=(E,CMDMSG)
         WTO   MF=(E,CMDWTO)
         MODESET KEY=ZERO          CHANGE TO KEY ZERO, NO CHANGE BACK
         SR    R0,R0
         LA    R1,CMDWTO
         SVC   34                  ISSUE  COMMAND
         LA    R9,0
* IF DDNAME NOWAIT IS PRESENT, SKIP 10 SECOND WAITER
         DEVTYPE =CL8'NOWAIT',DEVTAREA
         BXLE  R15,R15,LETSTOP
*
      STIMER WAIT,DINTVL=SEC10    GIVE TIME FOR YOUR STOMACH TO SETTLE
LETSTOP  EQU   *
         L     R13,SAVE+4
         LR    R15,R9
         RETURN (14,12),RC=(15)
SAVE     DC    18F'0'
         DS    0D
SEC10    DC    C'00001000'
CMDMSG   WTO   'THIS COMMAND HAS BEEN ISSUED:   ',                     X
               ROUTCDE=(1,9),DESC=4,MF=L
CMDWTO   DS    0F
         DC    AL2(L'CMDAREA)
         DC    AL2(0)
CMDAREA  DC    CL101' '
         DC    X'1000',X'8080'   DESCRIPTOR AND ROUTE CODES
         DS    0H
DEVTAREA DS    6F
         PRINT OFF
         REGISTER ,
         PRINT ON
         END
//LKED.SYSIN DD *
   NAME COMMANDR(R)
