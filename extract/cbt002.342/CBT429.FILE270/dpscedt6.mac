       ID DIVISION.
       PROGRAM-ID.  DPSCEDT6.

      ****************************************************************
      *                                                              *
      *     THIS IS THE BETTER/EDIT PROGRAM                          *
      *                                                              *
      *     MODIFIED AUGUST 1984 TO INCORPORATE NEW FIND FEATURE     *
      *                                                              *
      *     MODIFIED NOVEMBER 1984 SO THAT THE PANEL-NAME COULD BE   *
      *        SPECIFIED AT EXECUTION TIME RATHER THAN BEING         *
      *        HARD CODED IN THE PROGRAM                             *
      *                                                              *
      *     MODIFIED DECEMBER 1984 TO ALLOW NEW OPTION "X" WHICH     *
      *        IS ANOTHER KIND OF "FIND" FUNCTION WHICH SEARCHES     *
      *        ALL OF THE FIELDS OF THE MEMBER SELECTION LIST        *
      *        RATHER THAN THE DATA.                                 *
      *                                                              *
      *     MODIFIED FEBRUARY 1985 TO VGET THE PREFIX RATHER THAN    *
      *        OBTAINING IT FROM THE PARM.  THIS WAS DONE SO THAT    *
      *        THE PROGRAM COULD BE CALLED VIA PANVALET PROGRAM      *
      *        ISPNRECV.                                             *
      *           I.E.                                               *
      *               ISPEXEC SELECT PGM(IPNRECV) PARM(DPSCEDT6)     *
      *                                                              *
      *     MODIFIED FEBRUARY 1986 TO FIX THE PROBLEM WITH THE       *
      *        FIND OPTION ALWAYS REFERENCING THE DATASET BY THE     *
      *        CATALOG INSTEAD OF TAKING THE VOLUME SERIAL NUMBER    *
      *        IN CONSIDERATION IF SPECIFIED.                        *
      *                                                              *
      ****************************************************************
           EJECT
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
      *
      *    I N   A L P H A B E T I C A L   O R D E R
      *
       77  DISPLAY-RETURN-CODE                     PIC 9(4).
       77  FINAL-DSNAME                            PIC X(56).
       77  GENERIC-LOC-OF-CURSOR                   PIC X(8).
       77  GOOD-BREAKDOWN                          PIC X(3).
       77  HOLD-FIRST                              PIC X.
       77  HOLD-SECOND                             PIC X.
       77  NEXT-CURSOR-POSITION                    PIC X(8).
       77  PROFILE-PREFIX-LENGTH                   PIC S9(4) COMP.
       77  PROCESS-SWITCH                          PIC X(3).
       77  SPF-EDIT-PGM-LENGTH                     PIC S9(8) COMP.
       77  SPF-EDIT-SUB                            PIC S9(4) COMP.
       77  SUB                                     PIC S9(5) COMP.
       77  SUB2                                    PIC S9(5) COMP.
       77  SUB3                                    PIC S9(5) COMP.
       77  THE-DSNAME                              PIC X(44).
       77  THE-MEMBER                              PIC X(8).
       77  THE-VOL-SER                             PIC X(6).
       77  TYPE-OF-SERVICE                         PIC X(6).
       77  VGET-RETURN-CODE                        PIC 9(4).
           EJECT
      *
      *    I S P L I N K   P A R A M E T E R S
      *
       01  SELEKT                VALUE 'SELECT'    PIC X(6).
       01  SHARED                VALUE 'SHARED'    PIC X(6).
       01  VGET                  VALUE 'VGET'      PIC X(4).
       01  VPUT                  VALUE 'VPUT'      PIC X(4).
       01  PROFILE               VALUE 'PROFILE'   PIC X(7).
       01  VDEFINE               VALUE 'VDEFINE'   PIC X(7).
       01  CHAR                  VALUE 'CHAR'      PIC X(4).
       01  KONTROL               VALUE 'CONTROL'   PIC X(7).
       01  RFRESH                VALUE 'REFRESH'   PIC X(7).
       01  ERRORS                VALUE 'ERRORS'    PIC X(6).
       01  RETERN                VALUE 'RETURN'    PIC X(6).
       01  EDIT                  VALUE 'EDIT'      PIC X(4).
       01  BROWSE                VALUE 'BROWSE'    PIC X(6).
       01  DISPLY                VALUE 'DISPLAY'   PIC X(7).
       01  PANEL-NAME                              PIC X(8).
       01  CURSOR-FIELD                            PIC X(8).
       01  MESSAGE-ID                              PIC X(8).
       01  LENGTH-FIELD                            PIC S9(8) COMP.
       01  FNDSMEM-CMD      VALUE 'CMD(%FNDSMEM)'  PIC X(13).
       01  SPFYFND-CMD      VALUE 'CMD(%SPFYFND)'  PIC X(13).

       01  INITIAL-MSG-ID.
           05  FILLER            VALUE 'TRIK10'    PIC X(06).
           05  UNITS-POS                           PIC 9.
           05  FILLER            VALUE SPACE       PIC X(01).
           EJECT
      *
      *    S P F   S Y M B O L I C   N A M E S
      *
       01  SPF-SYMBOLIC-NAMES.
           05  DYNPNAME VALUE '(DYNPNAME)'         PIC X(10).
           05  OPTION2  VALUE '(OPTION2)'          PIC X(9).
           05  A00      VALUE '(A00)'              PIC X(5).
           05  OTHERDSN VALUE '(OTHERDSN)'         PIC X(10).
           05  OTHVOL   VALUE '(OTHVOL)'           PIC X(8).
           05  MEMBER   VALUE '(MEMBER)'           PIC X(8).
           05  SAVEDMSG VALUE '(SAVEDMSG)'         PIC X(10).
           05  GENERMSG VALUE '(GENERMSG)'         PIC X(10).
           05  ZERRMSG  VALUE '(ZERRMSG)'          PIC X(9).
           05  ZPREFIX  VALUE '(ZPREFIX)'          PIC X(9).
           05  FNDSERR  VALUE '(FNDSERR)'          PIC X(9).
           05  FNDSDSN  VALUE '(FNDSDSN)'          PIC X(9).
           05  FNDSRCH  VALUE '(FNDSRCH)'          PIC X(9).
           05  FNDSVOL  VALUE '(FNDSVOL)'          PIC X(9).
           05  A01-A17.
               10  FILLER                          PIC X(48) VALUE
             '(A01,A02,A03,A04,A05,A06,A07,A08,A09,A10,A11,A12'.
               10  FILLER                          PIC X(21) VALUE
             ',A13,A14,A15,A16,A17)'.
           05  W88DSN01-W88DSN17.
               10  FILLER                          PIC X(54) VALUE
             '(W88DSN01,W88DSN02,W88DSN03,W88DSN04,W88DSN05,W88DSN06'.
               10  FILLER                          PIC X(54) VALUE
             ',W88DSN07,W88DSN08,W88DSN09,W88DSN10,W88DSN11,W88DSN12'.
               10  FILLER                          PIC X(46) VALUE
             ',W88DSN13,W88DSN14,W88DSN15,W88DSN16,W88DSN17)'.
           EJECT
      *
      *    C O B O L   V A R I A B L E   N A M E S
      *

       01  COBOL-VARIABLE-NAMES.
           05  DYNAMIC-PANEL-NAME                  PIC X(8).
           05  OPTION-2-VALUE                      PIC X(3).
           05  OTHER-DATASET-SEL-CODE              PIC X(3).
           05  OTHER-DATASET-DSNAME                PIC X(44).
           05  SUPPLIED-VOL-SER                    PIC X(6).
           05  MEMBER-NAME-OR-SEARCH-CRITERIA.
               10  BYTES-1-8                       PIC X(08).
               10  FILLER                          PIC X(42).
           05  DATASET-SAVED-MSG                   PIC X(21).
           05  GENERIC-ERROR-MSG                   PIC X(30).
           05  CNTL-ERRS-MSG-ID                    PIC X(8).
           05  PROFILE-PREFIX-SVP                  PIC X(8).
           05  FIND-OPTION-ERRMSG                  PIC X(30).
           05  FIND-OPTION-DSNAME                  PIC X(44).
           05  FIND-OPTION-SEARCH-ARG              PIC X(50).
           05  FIND-OPTION-VOLUME                  PIC X(6).
           05  SELECTION-CODE-ARRAY.
               10  SEL-CODE-ELEMENT OCCURS 17 TIMES PIC X(03).
           05  DSNAMES-ARRAY.
               10  DSNAME-ELEMENT   OCCURS 17 TIMES PIC X(44).

           EJECT
      *
      *    O T H E R   W O R K I N G   S T O R A G E   F I E L D S
      *
       01  ABOUT-TO-ABORT                          PIC X.

       01  ABORT-NUMBER
               REDEFINES ABOUT-TO-ABORT            PIC 9.

       01  BREAKDOWN-TIME-OF-DAY.
           05  THE-HOUR                            PIC 99.
           05  THE-MINUTES                         PIC 99.
           05  THE-SECONDS.
               10  FILLER                          PIC 9.
               10  UNITS-POS                       PIC 9.

       01  CURSOR-LOC-TABLE.
           05  FILLER    VALUE '01W88DSN01'        PIC X(10).
           05  FILLER    VALUE '02W88DSN02'        PIC X(10).
           05  FILLER    VALUE '03W88DSN03'        PIC X(10).
           05  FILLER    VALUE '04W88DSN04'        PIC X(10).
           05  FILLER    VALUE '05W88DSN05'        PIC X(10).
           05  FILLER    VALUE '06W88DSN06'        PIC X(10).
           05  FILLER    VALUE '07W88DSN07'        PIC X(10).
           05  FILLER    VALUE '08W88DSN08'        PIC X(10).
           05  FILLER    VALUE '09W88DSN09'        PIC X(10).
           05  FILLER    VALUE '10W88DSN10'        PIC X(10).
           05  FILLER    VALUE '11W88DSN11'        PIC X(10).
           05  FILLER    VALUE '12W88DSN12'        PIC X(10).
           05  FILLER    VALUE '13W88DSN13'        PIC X(10).
           05  FILLER    VALUE '14W88DSN14'        PIC X(10).
           05  FILLER    VALUE '15W88DSN15'        PIC X(10).
           05  FILLER    VALUE '16W88DSN16'        PIC X(10).
           05  FILLER    VALUE '17W88DSN17'        PIC X(10).

       01  CURSOR-LOC-ARRAY
              REDEFINES CURSOR-LOC-TABLE.
           05   CURSOR-SEARCH-TABLE OCCURS 17 TIMES
                    ASCENDING KEY IS CURSOR-LOC-ARGUMENT
                       INDEXED BY CURSOR-LOC-INDEX.
                 10  CURSOR-LOC-ARGUMENT           PIC 99.
                 10  CURSOR-LOC-RESULT             PIC X(8).

       01  RAW-DSN.
           05  RAW-DSN-BYTE OCCURS 44 TIMES        PIC X.

       01  TEMP-DSNAME.
           05  TEMP-DSN-BYTE OCCURS 44 TIMES       PIC X.

       01  INTERMEDIATE-DSNAME.
           05  INT-DSN-BYTE OCCURS 44 TIMES        PIC X.

       01  FINAL-DSNAME-TABLE.
           05  FINAL-DSNAME-ELEMENT OCCURS 56 TIMES PIC X.

       01  MEMBER-TABLE.
           05  MEMBER-ELEMENT       OCCURS 8  TIMES PIC X.

       01  TEMP-MEMBER.
           05  TEMP-MEMBER-BYTE     OCCURS 8  TIMES PIC X.

       01  PROFILE-PREFIX-TABLE.
           05  PROFILE-PREFIX-BYTE  OCCURS 8  TIMES PIC X.

       01  SPF-EDIT-PGM.
           05  SPF-EDIT-PGM-BYTE  OCCURS 100 TIMES PIC X.

       01  BREAKDOWN-FILEATTR-ERR-MSG.
           05  BYTES-1-13                           PIC X(13).
           05  FILLER                               PIC X(17).

       01  BREAKDOWN-FIND-OPT-MSG.
           05  BYTES-1-8                            PIC X(08).
           05  FILLER                               PIC X(22).

       01  MEMBER-SAVED-MSG.
           05  FILLER     VALUE 'MEMBER '           PIC X(07).
           05  MEMBER-BEING-SAVED                   PIC X(08).
           05  FILLER     VALUE ' SAVED'            PIC X(06).

           EJECT
      *
      *    F I L E A T T R   P A R A M E T E R S
      *
       01  FILEATTR-IN-DSN                         PIC X(44).
       01  FILEATTR-OUT-VALID-SWITCH               PIC X(3).
       01  FILEATTR-OUT-ERROR-MESSAGE              PIC X(30).
       01  FILEATTR-OUT-VOLSER                     PIC X(6).
       01  FILEATTR-OUT-LRECL                      PIC 9(5).
       01  FILEATTR-OUT-BLKSIZE                    PIC 9(5).
       01  FILEATTR-OUT-DSORG                      PIC X(2).
       01  FILEATTR-OUT-RECFM                      PIC X(1).
       01  FILEATTR-OUT-DEVICE                     PIC X(1).
       01  FILEATTR-IN-VOLSER                      PIC X(6).
           EJECT
      *
      *    B R E A K D S N   P A R A M E T E R S
      *
       01  BREAKDSN-IO-AREA.
           05  INPUT-DSN                       PIC X(44).
           05  OUTPUT-DSN                      PIC X(44).
           05  OUTPUT-MEMBER-NAME              PIC X(8).
           05  MEMBER-NAME-PROVIDED            PIC X(3).
           05  GDG-TYPE-DATASET                PIC X(3).
           05  VALID-DSN-PASSED                PIC X(3).
           05  ERROR-MESSAGE                   PIC X(30).
           EJECT
       PROCEDURE DIVISION.
      *
      **************************************************************
      *                                                            *
      *                    M A I N   L I N E                       *
      *                                                            *
      **************************************************************
      *
           CALL 'ISPLINK'
                USING KONTROL ERRORS RETERN.

           PERFORM 1000-INITIALIZATION
              THRU 1999-EXIT-INITIALIZATION.

           MOVE SPACES TO OTHER-DATASET-DSNAME.
           MOVE SPACES TO SUPPLIED-VOL-SER.
           MOVE SPACES TO MEMBER-NAME-OR-SEARCH-CRITERIA.
           MOVE 'MEMBER  ' TO CURSOR-FIELD.
           MOVE ALL '-' TO DATASET-SAVED-MSG.

      *
      *    BEGIN LOADING INITIAL FUNNY MSG
      *

           MOVE TIME-OF-DAY TO BREAKDOWN-TIME-OF-DAY.

           MOVE UNITS-POS OF BREAKDOWN-TIME-OF-DAY
             TO UNITS-POS OF INITIAL-MSG-ID.

           MOVE INITIAL-MSG-ID TO MESSAGE-ID.

      *
      *    END LOADING INITIAL FUNNY MSG
      *

       0010-DISPLAY-PANEL.

           PERFORM 2000-DISPLAY-PANEL
              THRU 2999-EXIT-DISPLAY-PANEL.

           IF DISPLAY-RETURN-CODE = 8
                PERFORM 9000-END-OF-JOB
                   THRU 9999-EXIT-END-OF-JOB
                STOP RUN.

           IF  (OTHER-DATASET-SEL-CODE = 'S  '
                                      OR 'E  '
                                      OR 'B  '
                                      OR 'F  '
                                      OR 'X  ')
             AND
               (OTHER-DATASET-DSNAME = SPACES)
                    MOVE 'WDED105 ' TO MESSAGE-ID
                    MOVE 'OTHERDSN' TO CURSOR-FIELD
                    MOVE ALL '-' TO DATASET-SAVED-MSG
                    GO TO 0010-DISPLAY-PANEL.

           IF OTHER-DATASET-DSNAME = SPACES
               GO TO 0020-PROCESS-TABLE-OF-DSNS.

           MOVE OTHER-DATASET-DSNAME TO RAW-DSN.
           PERFORM 3000-BREAKDOWN-RAW-DSN
              THRU 3999-EXIT-BREAK-RAW-DSN.

           IF GOOD-BREAKDOWN = 'YES'
               NEXT SENTENCE
           ELSE
               MOVE 'OTHERDSN' TO CURSOR-FIELD
               MOVE ALL '-' TO DATASET-SAVED-MSG
               GO TO 0010-DISPLAY-PANEL.

           IF SUPPLIED-VOL-SER = SPACES
               MOVE 'ABSENT' TO THE-VOL-SER
           ELSE
               MOVE SUPPLIED-VOL-SER TO THE-VOL-SER.

      *    START CASE STATEMENT

               IF OTHER-DATASET-SEL-CODE
                    EQUAL '   ' OR '-  ' OR 'S  ' OR 'E  '
                        MOVE 'EDIT  ' TO TYPE-OF-SERVICE
           ELSE
               IF OTHER-DATASET-SEL-CODE = 'B  '
                        MOVE 'BROWSE' TO TYPE-OF-SERVICE
           ELSE
                        MOVE 'FIND  ' TO TYPE-OF-SERVICE.

      *    END CASE STATEMENT

           IF   (TYPE-OF-SERVICE = 'FIND  ')
              AND
                (THE-MEMBER NOT EQUAL TO 'ABSENT  ')
                     MOVE 'WDED109 ' TO MESSAGE-ID
                     MOVE 'OTHERDSN' TO CURSOR-FIELD
                     MOVE ALL '-' TO DATASET-SAVED-MSG
                     GO TO 0010-DISPLAY-PANEL.

           IF   (TYPE-OF-SERVICE = 'FIND  ')
              AND
                (MEMBER-NAME-OR-SEARCH-CRITERIA = SPACES)
                     MOVE 'WDED109A' TO MESSAGE-ID
                     MOVE 'MEMBER  ' TO CURSOR-FIELD
                     MOVE ALL '-' TO DATASET-SAVED-MSG
                     GO TO 0010-DISPLAY-PANEL.

           PERFORM 4000-PROCESS-THE-REQUEST
              THRU 4999-EXIT-PROCESS-REQ.

           IF GENERIC-LOC-OF-CURSOR = 'DATASET '
               MOVE 'OTHERDSN' TO CURSOR-FIELD
           ELSE
               MOVE 'MEMBER  ' TO CURSOR-FIELD.

           IF PROCESS-SWITCH = 'YES'
                MOVE SPACES TO MEMBER-NAME-OR-SEARCH-CRITERIA.

           GO TO 0010-DISPLAY-PANEL.

       0020-PROCESS-TABLE-OF-DSNS.

           MOVE ALL '-' TO DATASET-SAVED-MSG.
           MOVE +1 TO SUB.

       0030.

           IF SUB GREATER THAN +17
               MOVE 'EDIT  ' TO TYPE-OF-SERVICE
               MOVE +1 TO SUB
               MOVE 'S' TO SEL-CODE-ELEMENT (1)
               GO TO 0040-BREAKDOWN-THE-DSN.

           IF SEL-CODE-ELEMENT (SUB) = 'S  ' OR 'E  '
               MOVE 'EDIT  ' TO TYPE-OF-SERVICE
               GO TO 0040-BREAKDOWN-THE-DSN.

           IF SEL-CODE-ELEMENT (SUB) = 'B  '
               MOVE 'BROWSE' TO TYPE-OF-SERVICE
               GO TO 0040-BREAKDOWN-THE-DSN.

           IF SEL-CODE-ELEMENT (SUB) = 'F  ' OR 'X  '
               MOVE 'FIND  ' TO TYPE-OF-SERVICE
               GO TO 0040-BREAKDOWN-THE-DSN.

           ADD +1 TO SUB.
           GO TO 0030.

       0040-BREAKDOWN-THE-DSN.

           IF DSNAME-ELEMENT (SUB) = SPACES
               MOVE 'WDED105 ' TO MESSAGE-ID
               PERFORM 6000-DETERMINE-CURSOR-POSITION
                  THRU 6999-EXIT-DET-CUR-POS
               MOVE NEXT-CURSOR-POSITION TO CURSOR-FIELD
               MOVE ALL '-' TO DATASET-SAVED-MSG
               GO TO 0010-DISPLAY-PANEL.

           MOVE DSNAME-ELEMENT (SUB) TO RAW-DSN.
           PERFORM 3000-BREAKDOWN-RAW-DSN
              THRU 3999-EXIT-BREAK-RAW-DSN.

           IF GOOD-BREAKDOWN = 'YES'
               NEXT SENTENCE
           ELSE
               PERFORM 6000-DETERMINE-CURSOR-POSITION
                  THRU 6999-EXIT-DET-CUR-POS
               MOVE NEXT-CURSOR-POSITION TO CURSOR-FIELD
               MOVE ALL '-' TO DATASET-SAVED-MSG
               GO TO 0010-DISPLAY-PANEL.

           IF   (TYPE-OF-SERVICE = 'FIND  ')
              AND
                (THE-MEMBER NOT EQUAL TO 'ABSENT  ')
                     MOVE 'WDED109 ' TO MESSAGE-ID
                     PERFORM 6000-DETERMINE-CURSOR-POSITION
                        THRU 6999-EXIT-DET-CUR-POS
                     MOVE NEXT-CURSOR-POSITION TO CURSOR-FIELD
                     MOVE ALL '-' TO DATASET-SAVED-MSG
                     GO TO 0010-DISPLAY-PANEL.

           IF SUPPLIED-VOL-SER = SPACES
               MOVE 'ABSENT' TO THE-VOL-SER
           ELSE
               MOVE SUPPLIED-VOL-SER TO THE-VOL-SER.

           IF   (TYPE-OF-SERVICE = 'FIND  ')
              AND
                (MEMBER-NAME-OR-SEARCH-CRITERIA = SPACES)
                     MOVE 'WDED109A' TO MESSAGE-ID
                     MOVE 'MEMBER  ' TO CURSOR-FIELD
                     MOVE ALL '-' TO DATASET-SAVED-MSG
                     GO TO 0010-DISPLAY-PANEL.

           IF  (MEMBER-NAME-OR-SEARCH-CRITERIA = SPACES)
             OR
               (THE-MEMBER = 'ABSENT  ')
                     GO TO 0060-DO-THE-EDIT-OR-BROWSE.

           MOVE 'WDED108 ' TO MESSAGE-ID.
           MOVE 'MEMBER  ' TO CURSOR-FIELD.
           MOVE ALL '-' TO DATASET-SAVED-MSG.
           GO TO 0010-DISPLAY-PANEL.

       0060-DO-THE-EDIT-OR-BROWSE.

           PERFORM 4000-PROCESS-THE-REQUEST
              THRU 4999-EXIT-PROCESS-REQ.

           IF PROCESS-SWITCH = 'YES'
               MOVE 'MEMBER  ' TO CURSOR-FIELD
               MOVE SPACES TO MEMBER-NAME-OR-SEARCH-CRITERIA
               GO TO 0010-DISPLAY-PANEL.

           IF GENERIC-LOC-OF-CURSOR = 'DATASET '
               PERFORM 6000-DETERMINE-CURSOR-POSITION
                  THRU 6999-EXIT-DET-CUR-POS
               MOVE NEXT-CURSOR-POSITION TO CURSOR-FIELD
           ELSE
               MOVE 'MEMBER  ' TO CURSOR-FIELD.

           GO TO 0010-DISPLAY-PANEL.
           EJECT
       1000-INITIALIZATION.

           MOVE +08 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE
               ZPREFIX
               PROFILE-PREFIX-SVP
               CHAR
               LENGTH-FIELD.

           CALL 'ISPLINK'
                USING VGET ZPREFIX SHARED.

           MOVE PROFILE-PREFIX-SVP TO PROFILE-PREFIX-TABLE.

           MOVE +0 TO PROFILE-PREFIX-LENGTH.

       1100-BUMP-THE-SUBSCRIPT.

           ADD +1 TO PROFILE-PREFIX-LENGTH.

           IF (PROFILE-PREFIX-LENGTH GREATER THAN +8)
             OR
              (PROFILE-PREFIX-BYTE (PROFILE-PREFIX-LENGTH) = SPACE)
                  NEXT SENTENCE
           ELSE
                  GO TO 1100-BUMP-THE-SUBSCRIPT.

           COMPUTE PROFILE-PREFIX-LENGTH = PROFILE-PREFIX-LENGTH - 1.

           MOVE +08 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE
               DYNPNAME
               DYNAMIC-PANEL-NAME
               CHAR
               LENGTH-FIELD.

           MOVE +03 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE A00 OTHER-DATASET-SEL-CODE CHAR LENGTH-FIELD.

           MOVE +44 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE OTHERDSN OTHER-DATASET-DSNAME CHAR LENGTH-FIELD.

           MOVE +06 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE OTHVOL SUPPLIED-VOL-SER CHAR LENGTH-FIELD.

           MOVE +50 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE MEMBER MEMBER-NAME-OR-SEARCH-CRITERIA
                   CHAR LENGTH-FIELD.

           MOVE +21 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE SAVEDMSG DATASET-SAVED-MSG CHAR LENGTH-FIELD.

           MOVE +30 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE GENERMSG GENERIC-ERROR-MSG CHAR LENGTH-FIELD.

           MOVE +8 TO LENGTH-FIELD.
           CALL 'ISPLINK'
            USING
             VDEFINE ZERRMSG CNTL-ERRS-MSG-ID CHAR LENGTH-FIELD.

           MOVE +30 TO LENGTH-FIELD.
           CALL 'ISPLINK'
            USING
             VDEFINE FNDSERR FIND-OPTION-ERRMSG CHAR LENGTH-FIELD.

           MOVE +44 TO LENGTH-FIELD.
           CALL 'ISPLINK'
            USING
             VDEFINE FNDSDSN FIND-OPTION-DSNAME CHAR LENGTH-FIELD.

           MOVE +50 TO LENGTH-FIELD.
           CALL 'ISPLINK'
            USING
             VDEFINE FNDSRCH FIND-OPTION-SEARCH-ARG CHAR LENGTH-FIELD.

           MOVE +6 TO LENGTH-FIELD.
           CALL 'ISPLINK'
            USING
             VDEFINE FNDSVOL FIND-OPTION-VOLUME CHAR LENGTH-FIELD.

           MOVE +03 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE A01-A17 SELECTION-CODE-ARRAY CHAR LENGTH-FIELD.

           MOVE +44 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE
               W88DSN01-W88DSN17
               DSNAMES-ARRAY
               CHAR
               LENGTH-FIELD.

           MOVE SPACES TO DSNAMES-ARRAY.

           CALL 'ISPLINK'
                USING VGET W88DSN01-W88DSN17 PROFILE.

           CALL 'ISPLINK'
                USING VGET DYNPNAME SHARED.

           IF RETURN-CODE NOT EQUAL ZERO
               MOVE 'MULTEDIT' TO PANEL-NAME
           ELSE
               MOVE DYNAMIC-PANEL-NAME TO PANEL-NAME.

       1999-EXIT-INITIALIZATION.
           EXIT.
           EJECT
       2000-DISPLAY-PANEL.
      *
      **************************************************************
      *                                                            *
      *    DO THE EQUIVALENT OF:                                   *
      *                                                            *
      *         ISPEXEC DISPLAY PANEL(MULTEDIT) -                  *
      *              MSG(MSG-ID) CURSOR(FIELD-NAME)                *
      *                                                            *
      **************************************************************
      *
           IF MESSAGE-ID = 'ABSENT  '
               MOVE SPACES TO MESSAGE-ID.

           CALL 'ISPLINK'
                USING
                   DISPLY
                   PANEL-NAME
                   MESSAGE-ID
                   CURSOR-FIELD.

           MOVE RETURN-CODE TO DISPLAY-RETURN-CODE.

       2999-EXIT-DISPLAY-PANEL.
           EXIT.
           EJECT
       3000-BREAKDOWN-RAW-DSN.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE ACCEPTS A DATASET NAME IN RAW-DSN AND      *
      *    THEN CREATES THE FOLLOWING TWO DATA FIELDS:             *
      *                                                            *
      *           THE-DSNAME                                       *
      *           THE-MEMBER                                       *
      *                                                            *
      *       WHERE THE-DSNAME IS THE DSNAME OF THE PDS WITHOUT    *
      *       THE MEMBER SPECIFIED (IF ONE WAS SPECIFIED) AND      *
      *       THE-MEMBER IS THE MEMBER NAME (IF WAS ONE SPECIFIED) *
      *                                                            *
      *       E.G.                                                 *
      *           IF RAW-DSN PASSED = AGY999.MY.PDS(MASK1)         *
      *                                                            *
      *              THEN THIS ROUTINE CREATES THE-DSNAME AND      *
      *              THE-MEMBER AS FOLLOWS:                        *
      *                                                            *
      *                   THE-DSNAME = AGY999.MY.PDS               *
      *                   THE-MEMBER = MASK1                       *
      *                                                            *
      *       IF NO MEMBER WAS INCLUDED IN RAW-DSN OR IF THE       *
      *       DATASET WAS NOT A PDS, THEN THE-MEMBER IS SET        *
      *       EQUAL TO 'ABSENT  '                                  *
      *                                                            *
      *       IF ALL GOES WELL IN THE BREAKDOWN, THEN THIS ROUTINE *
      *       MOVES 'YES' TO GOOD-BREAKDOWN ELSE IT WILL MOVE 'NO '*
      *       TO GOOD-BREAKDOWN AND LOADS MESSAGE-ID WITH THE      *
      *       APPROPRIATE MESSAGE                                  *
      *                                                            *
      **************************************************************
      *

           MOVE 'YES' TO GOOD-BREAKDOWN.

           MOVE RAW-DSN TO INPUT-DSN OF BREAKDSN-IO-AREA.

           CALL 'BREAKDSN' USING BREAKDSN-IO-AREA.

           IF VALID-DSN-PASSED OF BREAKDSN-IO-AREA = 'YES'
               NEXT SENTENCE
           ELSE
               MOVE 'NO ' TO GOOD-BREAKDOWN
               MOVE ERROR-MESSAGE OF BREAKDSN-IO-AREA
                 TO GENERIC-ERROR-MSG
               MOVE 'WDED101' TO MESSAGE-ID
               GO TO 3999-EXIT-BREAK-RAW-DSN.

           MOVE OUTPUT-DSN OF BREAKDSN-IO-AREA TO TEMP-DSNAME.

           IF MEMBER-NAME-PROVIDED OF BREAKDSN-IO-AREA = 'YES'
                 MOVE OUTPUT-MEMBER-NAME OF BREAKDSN-IO-AREA
                   TO THE-MEMBER
           ELSE
                 MOVE 'ABSENT  ' TO THE-MEMBER.

           IF TEMP-DSN-BYTE (1) = QUOTE
                PERFORM 3600-STRIP-OFF-QUOTES
                   THRU 3699-EXIT-STRIP-QUOTES
                GO TO 3400-GENERATE-THE-DSNAME.

           PERFORM 3700-PREFIX-DSNAME
              THRU 3799-EXIT-PREFIX-DSNAME.

           IF PROCESS-SWITCH = 'YES'
               NEXT SENTENCE
           ELSE
               MOVE 'NO ' TO GOOD-BREAKDOWN
               GO TO 3999-EXIT-BREAK-RAW-DSN.

       3400-GENERATE-THE-DSNAME.

           MOVE INTERMEDIATE-DSNAME TO THE-DSNAME.
           GO TO 3999-EXIT-BREAK-RAW-DSN.

           EJECT
       3600-STRIP-OFF-QUOTES.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE INPUTS TEMP-DSNAME (WHICH HAS ONLY THE     *
      *    DSNAME WITHOUT A MEMBER SPECIFIED) AND IT GENERATES     *
      *    THE DATA FIELD INTERMEDIATE-DSNAME WHICH HAS THE        *
      *    BEGINNING AND ENDING QUOTES STRIPPED OFF                *
      *                                                            *
      *    IN SOME CASES, TEMP-DSNAME WILL NOT HAVE AN ENDING      *
      *    QUOTE                                                   *
      *                                                            *
      **************************************************************
      *
           MOVE SPACES TO INTERMEDIATE-DSNAME.

           MOVE +1 TO SUB2.

       3610.

           ADD +1 TO SUB2.
           COMPUTE SUB3 = SUB2 - 1.

           IF  (SUB2 GREATER THAN +44)
             OR
               (TEMP-DSN-BYTE (SUB2) = SPACE)
             OR
               (TEMP-DSN-BYTE (SUB2) = QUOTE)
                    GO TO 3699-EXIT-STRIP-QUOTES.

           MOVE TEMP-DSN-BYTE (SUB2)
             TO INT-DSN-BYTE (SUB3).

           GO TO 3610.

       3699-EXIT-STRIP-QUOTES.
           EXIT.
           EJECT
       3700-PREFIX-DSNAME.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE INPUTS TEMP-DSNAME (WHICH HAS ONLY THE     *
      *    DSNAME WITHOUT A MEMBER SPECIFIED) AND IT GENERATES     *
      *    THE DATA FIELD INTERMEDIATE-DSNAME WHICH HAS THE        *
      *    PROFILE PREFIX APPENDED TO THE BEGINNING.  THE PREFIX   *
      *    IS LOCATED IN THE FIELD PROFILE-PREFIX-TABLE.           *
      *                                                            *
      *    IF ALL GOES OK, THEN THIS ROUTINE DOES THE FOLLOWING:   *
      *             MOVE 'YES' TO PROCESS-SWITCH                   *
      *             MOVE 'ABSENT  ' TO MESSAGE-ID                  *
      *             GO TO EXIT                                     *
      *                                                            *
      *    IF AFTER APPENDING THE PROFILE PREFIX, THE DSNAME       *
      *    BECOMES LONGER THAN 44 BYTES, THEN THIS ROUTINE DOES    *
      *    THE FOLLOWING:                                          *
      *             MOVE 'NO ' TO PROCESS-SWITCH                   *
      *             MOVE 'WDED100 ' TO MESSAGE-ID                  *
      *             GO TO EXIT                                     *
      *                                                            *
      **************************************************************
      *
           MOVE 'YES' TO PROCESS-SWITCH.
           MOVE 'ABSENT  ' TO MESSAGE-ID.
           MOVE SPACES TO INTERMEDIATE-DSNAME.
           MOVE +0 TO SUB3.

       3710.

           ADD +1 TO SUB3.

           IF SUB3 GREATER THAN PROFILE-PREFIX-LENGTH
               MOVE '.' TO INT-DSN-BYTE (SUB3)
               ADD +1 TO SUB3
           ELSE
               MOVE PROFILE-PREFIX-BYTE (SUB3)
                 TO INT-DSN-BYTE (SUB3)
               GO TO 3710.

           MOVE +0 TO SUB2.

       3720.

           ADD +1 TO SUB2.

           IF SUB3 GREATER THAN +44
              MOVE 'WDED100 ' TO MESSAGE-ID
              MOVE 'NO ' TO PROCESS-SWITCH
              GO TO 3799-EXIT-PREFIX-DSNAME.

           IF TEMP-DSN-BYTE (SUB2) = SPACE
              GO TO 3799-EXIT-PREFIX-DSNAME.

           MOVE TEMP-DSN-BYTE (SUB2)
             TO INT-DSN-BYTE (SUB3).
           ADD +1 TO SUB3.
           GO TO 3720.

       3799-EXIT-PREFIX-DSNAME.
           EXIT.
           EJECT
       3999-EXIT-BREAK-RAW-DSN.
           EXIT.
           EJECT
       4000-PROCESS-THE-REQUEST.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE BASICALLY DOES THE FOLLOWING:              *
      *                                                            *
      *         1.  CALLS 'FILEATTR' TO SEE IF THE DATASET EXISTS  *
      *         2.  IF FILEATTR FINDS A PROBLEM WITH THE DATASET,  *
      *                THEN THIS ROUTINE MOVES 'NO ' TO            *
      *                PROCESS-SWITCH, MOVES THE APPROPRIATE MSG   *
      *                TO MESSAGE-ID, AND GOES TO THE EXIT.        *
      *         3.  IF THE TYPE OF SERVICE IS BROWSE, THEN THIS    *
      *                ROUTINE GOES TO 12                          *
      *         4.  IF RECFM = U, THEN THIS ROUTINE MOVES 'NO '    *
      *                TO PROCESS-SWITCH, MOVE THE APPROPRIATE MSG *
      *                TO MESSAGE-ID, AND GOES TO THE EXIT.        *
      *         5.  IF LRECL GT 255, THEN THIS ROUTINE MOVES 'NO ' *
      *                TO PROCESS-SWITCH, MOVE THE APPROPRIATE MSG *
      *                TO MESSAGE-ID, AND GOES TO THE EXIT.        *
      *         6.  IF DSORG NE 'PS' OR 'PO', THEN IT  MOVES 'NO ' *
      *                TO PROCESS-SWITCH, MOVE THE APPROPRIATE MSG *
      *                TO MESSAGE-ID, AND GOES TO THE EXIT.        *
      *         7.  IF A MEMBER NAME WAS SPECIFIED BUT THE DATASET *
      *                IS NOT A PDS, THEN THIS ROUTINE MOVES 'NO ' *
      *                TO PROCESS-SWITCH, MOVE THE APPROPRIATE MSG *
      *                TO MESSAGE-ID, AND GOES TO THE EXIT.        *
      *         8.  IF A MEMBER NAME WAS SPECIFIED, THEN IT        *
      *                PERFORMS 5000-ATTACH-MEMBER WHICH ATTACHES  *
      *                THE MEMBER NAME FOUND IN THE-MEMBER AND     *
      *                CREATES FINAL-DSNAME                        *
      *         9.  PEFORMS 7000-ENCLOSE-IN-QUOTES WHICH INPUTS    *
      *                FINAL-DSNAME AND OUTPUTS FINAL-DSNAME WITH  *
      *                QUOTES AROUND IT                            *
      *         10. DOES THE EQUIVALENT OF THE FOLLOWING:          *
      *                ISPEXEC EDIT DATASET(FINAL-DSNAME) -        *
      *                     VOLUME(THE-VOL-SER)                    *
      *         11. GO TO EXIT                                     *
      *         12. IF A MEMBER NAME WAS SPECIFIED, THEN IT        *
      *                PERFORMS 5000-ATTACH-MEMBER WHICH ATTACHES  *
      *                THE MEMBER NAME FOUND IN THE-MEMBER AND     *
      *                CREATES FINAL-DSNAME                        *
      *         13. PEFORMS 7000-ENCLOSE-IN-QUOTES WHICH INPUTS    *
      *                FINAL-DSNAME AND OUTPUTS FINAL-DSNAME WITH  *
      *                QUOTES AROUND IT                            *
      *         14. DOES THE EQUIVALENT OF THE FOLLOWING:          *
      *                ISPEXEC BROWSE DATASET(FINAL-DSNAME) -      *
      *                     VOLUME(THE-VOL-SER)                    *
      *         15. GO TO EXIT                                     *
      *                                                            *
      **************************************************************
      *
           MOVE 'YES' TO PROCESS-SWITCH.
           MOVE ALL '-' TO DATASET-SAVED-MSG.
           MOVE 'DATASET ' TO GENERIC-LOC-OF-CURSOR.

           IF  (MEMBER-NAME-OR-SEARCH-CRITERIA = SPACES)
              OR
               (TYPE-OF-SERVICE = 'FIND  ')
                   NEXT SENTENCE
           ELSE
                   MOVE BYTES-1-8 OF MEMBER-NAME-OR-SEARCH-CRITERIA
                     TO THE-MEMBER.

           MOVE THE-DSNAME TO FILEATTR-IN-DSN.
           MOVE THE-VOL-SER TO FILEATTR-IN-VOLSER.

           CALL 'FILEATTR' USING FILEATTR-IN-DSN
                                 FILEATTR-OUT-VALID-SWITCH
                                 FILEATTR-OUT-ERROR-MESSAGE
                                 FILEATTR-OUT-VOLSER
                                 FILEATTR-OUT-LRECL
                                 FILEATTR-OUT-BLKSIZE
                                 FILEATTR-OUT-DSORG
                                 FILEATTR-OUT-RECFM
                                 FILEATTR-OUT-DEVICE
                                 FILEATTR-IN-VOLSER.

           IF FILEATTR-OUT-VALID-SWITCH = 'YES'
                GO TO 4100-CHEK-SERV-TYPE.

           MOVE 'NO ' TO PROCESS-SWITCH.
           MOVE FILEATTR-OUT-ERROR-MESSAGE
             TO BREAKDOWN-FILEATTR-ERR-MSG.

      *        START CASE STATEMENT

               IF BYTES-1-13 OF BREAKDOWN-FILEATTR-ERR-MSG
                    EQUAL 'FORMAT-1 DSCB'
                         MOVE 'DATASET NOT FOUND             '
                           TO GENERIC-ERROR-MSG
           ELSE
               IF BYTES-1-13 OF BREAKDOWN-FILEATTR-ERR-MSG
                    EQUAL 'INVALID SYNTA'
                         MOVE 'INVALID DATASET NAME          '
                           TO GENERIC-ERROR-MSG
           ELSE
               MOVE FILEATTR-OUT-ERROR-MESSAGE
                 TO GENERIC-ERROR-MSG.

      *        END CASE STATEMENT

           MOVE 'WDED101 ' TO MESSAGE-ID.
           GO TO 4999-EXIT-PROCESS-REQ.

       4100-CHEK-SERV-TYPE.

           IF TYPE-OF-SERVICE = 'BROWSE'
              GO TO 4300-DO-BROWSE.

           IF FILEATTR-OUT-RECFM = 'U'
              MOVE 'WDED103 ' TO MESSAGE-ID
              MOVE 'NO ' TO PROCESS-SWITCH
              GO TO 4999-EXIT-PROCESS-REQ.

           IF FILEATTR-OUT-LRECL GREATER THAN 255
              MOVE 'WDED102 ' TO MESSAGE-ID
              MOVE 'NO ' TO PROCESS-SWITCH
              GO TO 4999-EXIT-PROCESS-REQ.

           IF FILEATTR-OUT-DSORG = 'PO' OR 'PS'
              NEXT SENTENCE
           ELSE
              MOVE 'WDED104 ' TO MESSAGE-ID
              MOVE 'NO ' TO PROCESS-SWITCH
              GO TO 4999-EXIT-PROCESS-REQ.

           IF  (TYPE-OF-SERVICE = 'FIND  ')
             AND
               (FILEATTR-OUT-DSORG NOT EQUAL 'PO')
                    MOVE 'WDED109B' TO MESSAGE-ID
                    MOVE 'NO ' TO PROCESS-SWITCH
                    GO TO 4999-EXIT-PROCESS-REQ.

           IF TYPE-OF-SERVICE = 'FIND  '
               PERFORM 8000-DO-FIND-FUNCTION
                  THRU 8999-EXIT-FIND
               GO TO 4999-EXIT-PROCESS-REQ.

           IF FILEATTR-OUT-DSORG = 'PS'
                IF THE-MEMBER = 'ABSENT  '
                    MOVE '--------DATASET SAVED'
                      TO DATASET-SAVED-MSG
                ELSE
                    MOVE 'WDED107 ' TO MESSAGE-ID
                    MOVE 'NO ' TO PROCESS-SWITCH
                    GO TO 4999-EXIT-PROCESS-REQ
           ELSE
                IF THE-MEMBER = 'ABSENT  '
                    NEXT SENTENCE
                ELSE
                    MOVE THE-MEMBER TO MEMBER-BEING-SAVED
                                         OF MEMBER-SAVED-MSG
                    MOVE MEMBER-SAVED-MSG TO DATASET-SAVED-MSG.

           IF THE-MEMBER = 'ABSENT  '
               MOVE THE-DSNAME TO FINAL-DSNAME
           ELSE
               PERFORM 5000-ATTACH-MEMBER
                  THRU 5999-EXIT-ATTACH-MEMBER.

           PERFORM 7000-ENCLOSE-IN-QUOTES
              THRU 7999-EXIT-PUT-IN-QUOTES.

           IF THE-VOL-SER = 'ABSENT'
                CALL 'ISPLINK'
                     USING EDIT
                           FINAL-DSNAME
           ELSE
                CALL 'ISPLINK'
                     USING EDIT
                           FINAL-DSNAME
                           THE-VOL-SER.

           IF RETURN-CODE = 0
                 MOVE 'ABSENT  ' TO MESSAGE-ID
                 GO TO 4999-EXIT-PROCESS-REQ.

           IF RETURN-CODE = 4
                 MOVE 'ABSENT  ' TO MESSAGE-ID
                 MOVE ALL '-' TO DATASET-SAVED-MSG
                 GO TO 4999-EXIT-PROCESS-REQ.

           MOVE CNTL-ERRS-MSG-ID TO MESSAGE-ID.
           MOVE 'NO ' TO PROCESS-SWITCH.
           MOVE ALL '-' TO DATASET-SAVED-MSG.
           GO TO 4999-EXIT-PROCESS-REQ.

       4300-DO-BROWSE.

           IF FILEATTR-OUT-DSORG = 'PO' OR 'PS'
              NEXT SENTENCE
           ELSE
              MOVE 'WDED104 ' TO MESSAGE-ID
              MOVE 'NO ' TO PROCESS-SWITCH
              GO TO 4999-EXIT-PROCESS-REQ.

           IF FILEATTR-OUT-DSORG = 'PO'
              NEXT SENTENCE
           ELSE
              IF THE-MEMBER = 'ABSENT  '
                  NEXT SENTENCE
              ELSE
                  MOVE 'WDED107 ' TO MESSAGE-ID
                  MOVE 'NO ' TO PROCESS-SWITCH
                  GO TO 4999-EXIT-PROCESS-REQ.

           IF THE-MEMBER = 'ABSENT  '
               MOVE THE-DSNAME TO FINAL-DSNAME
           ELSE
               PERFORM 5000-ATTACH-MEMBER
                  THRU 5999-EXIT-ATTACH-MEMBER.

           PERFORM 7000-ENCLOSE-IN-QUOTES
              THRU 7999-EXIT-PUT-IN-QUOTES.

           IF THE-VOL-SER = 'ABSENT'
                CALL 'ISPLINK'
                     USING BROWSE
                           FINAL-DSNAME
           ELSE
                CALL 'ISPLINK'
                     USING BROWSE
                           FINAL-DSNAME
                           THE-VOL-SER.

           IF RETURN-CODE = 0
                MOVE 'ABSENT  ' TO MESSAGE-ID
                GO TO 4999-EXIT-PROCESS-REQ.

           MOVE CNTL-ERRS-MSG-ID TO MESSAGE-ID.
           MOVE 'NO ' TO PROCESS-SWITCH.
           MOVE ALL '-' TO DATASET-SAVED-MSG.

           IF MEMBER-NAME-OR-SEARCH-CRITERIA = SPACES
                NEXT SENTENCE
           ELSE
                MOVE 'MEMBER  ' TO GENERIC-LOC-OF-CURSOR.

       4999-EXIT-PROCESS-REQ.
           EXIT.
           EJECT
       5000-ATTACH-MEMBER.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE IS GIVEN THE-DSNAME AND THE-MEMBER AND     *
      *    CREATES FINAL-DSNAME WHICH HAS THE MEMBER ATTACHED TO   *
      *    IT                                                      *
      *                                                            *
      *        E.G.                                                *
      *                                                            *
      *            IF THE-DSNAME IS AGY999.MY.PDS AND              *
      *               THE-MEMBER IS MASK1                          *
      *                                                            *
      *                THEN THIS ROUTINE CREATES                   *
      *                    FINAL-DSNAME = AGY999.MY.PDS(MASK1)     *
      *                                                            *
      *                                                            *
      *                                                            *
      **************************************************************
      *
           MOVE THE-DSNAME TO FINAL-DSNAME-TABLE.
           MOVE +0 TO SUB2.

       5100.

           ADD +1 TO SUB2.

           IF  (SUB2 GREATER THAN +44)
             OR
               (FINAL-DSNAME-ELEMENT (SUB2) = SPACE)
                    NEXT SENTENCE
           ELSE
                    GO TO 5100.

           MOVE '(' TO FINAL-DSNAME-ELEMENT (SUB2).

           ADD +1 TO SUB2.

           MOVE THE-MEMBER TO MEMBER-TABLE.
           MOVE +0 TO SUB3.

       5200.

           ADD +1 TO SUB3.

           IF  (SUB3 GREATER THAN +8)
             OR
               (MEMBER-ELEMENT (SUB3) = SPACE)
                    NEXT SENTENCE
           ELSE
                    MOVE MEMBER-ELEMENT (SUB3)
                      TO FINAL-DSNAME-ELEMENT (SUB2)
                    ADD +1 TO SUB2
                    GO TO 5200.

           MOVE ')' TO FINAL-DSNAME-ELEMENT (SUB2).

           MOVE FINAL-DSNAME-TABLE TO FINAL-DSNAME.

       5999-EXIT-ATTACH-MEMBER.
           EXIT.
           EJECT
       6000-DETERMINE-CURSOR-POSITION.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE IS GIVEN SUB (WHICH RANGES FROM 1 THRU 17) *
      *    AND IT RETURNS NEXT-CURSOR-POSITION WHICH IS EQUAL TO   *
      *    THE APPROPRAIE SPF VARIABLE NAME FOR THE DSNAME IN THE  *
      *    TABLE                                                   *
      *                                                            *
      *        E.G.                                                *
      *                                                            *
      *            IF SUB IS EQUAL TO 4                            *
      *                 THEN THIS ROUTINE MOVES 'W88DSN04'         *
      *                    TO NEXT-CURSOR-POSITION                 *
      *                                                            *
      **************************************************************
      *
           SEARCH ALL CURSOR-SEARCH-TABLE
              AT END
                  GO TO 6500
              WHEN
                  CURSOR-LOC-ARGUMENT (CURSOR-LOC-INDEX) = SUB
                       MOVE CURSOR-LOC-RESULT (CURSOR-LOC-INDEX)
                         TO NEXT-CURSOR-POSITION
                       GO TO 6999-EXIT-DET-CUR-POS.

       6500.

           DISPLAY '*******************************'.
           DISPLAY '*                             *'.
           DISPLAY '*  IF THIS MESSAGE IS EVER    *'.
           DISPLAY '*  DISPLAYED, THEN IT IS      *'.
           DISPLAY '*  EVEN POSSIBLE FOR AN EVEN  *'.
           DISPLAY '*  NUMBER NOT TO BE DIVISIBLE *'.
           DISPLAY '*  BY 2 !!!!!                 *'.
           DISPLAY '*                             *'.
           DISPLAY '*          OR (MORE LIKELY)   *'.
           DISPLAY '*                             *'.
           DISPLAY '*  SOMEONE HAS ALTERED THIS   *'.
           DISPLAY '*  PROGRAM !!!                *'.
           DISPLAY '*                             *'.
           DISPLAY '*******************************'.
           MOVE SPACE TO ABOUT-TO-ABORT.
           ADD +1 TO ABORT-NUMBER.

       6999-EXIT-DET-CUR-POS.
           EXIT.
           EJECT
       7000-ENCLOSE-IN-QUOTES.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE INPUTS FINAL-DSNAME (WHICH DOES NOT HAVE   *
      *    QUOTES AROUND IT) AND OUTPUTS FINAL-DSNAME WHICH HAS    *
      *    QUOTES SURROUNDING IT                                   *
      *                                                            *
      *    FINAL-DSNAME MAY OR MAY NOT HAVE A MEMBER NAME INCLUDED *
      *                                                            *
      **************************************************************
      *
           MOVE FINAL-DSNAME TO FINAL-DSNAME-TABLE.
           MOVE FINAL-DSNAME-ELEMENT (1) TO HOLD-FIRST.
           MOVE QUOTE TO FINAL-DSNAME-ELEMENT (1).
           MOVE +1 TO SUB2.

       7100.

           ADD +1 TO SUB2.

           IF  (SUB2 GREATER THAN +54)
             OR
               (FINAL-DSNAME-ELEMENT (SUB2) = SPACE)
                    MOVE HOLD-FIRST TO FINAL-DSNAME-ELEMENT (SUB2)
                    ADD +1 TO SUB2
                    MOVE QUOTE TO FINAL-DSNAME-ELEMENT (SUB2)
                    MOVE FINAL-DSNAME-TABLE TO FINAL-DSNAME
                    GO TO 7999-EXIT-PUT-IN-QUOTES.

           MOVE FINAL-DSNAME-ELEMENT (SUB2) TO HOLD-SECOND.
           MOVE HOLD-FIRST TO FINAL-DSNAME-ELEMENT (SUB2).
           MOVE HOLD-SECOND TO HOLD-FIRST.
           GO TO 7100.

       7999-EXIT-PUT-IN-QUOTES.
           EXIT.
           EJECT
       8000-DO-FIND-FUNCTION.

           MOVE THE-DSNAME
             TO FIND-OPTION-DSNAME.
           MOVE THE-VOL-SER
             TO FIND-OPTION-VOLUME.
           MOVE MEMBER-NAME-OR-SEARCH-CRITERIA
             TO FIND-OPTION-SEARCH-ARG.

           CALL 'ISPLINK'
                USING VPUT FNDSDSN SHARED.
           CALL 'ISPLINK'
                USING VPUT FNDSRCH SHARED.
           CALL 'ISPLINK'
                USING VPUT FNDSVOL SHARED.

           MOVE +13 TO LENGTH-FIELD.

           IF  (OTHER-DATASET-DSNAME NOT EQUAL SPACES)
                         AND
               (OTHER-DATASET-SEL-CODE = 'F  ')
            OR
               (OTHER-DATASET-DSNAME EQUAL SPACES)
                         AND
               (SEL-CODE-ELEMENT (SUB) = 'F  ')

                    CALL 'ISPLINK'
                       USING
                          SELEKT
                          LENGTH-FIELD
                          FNDSMEM-CMD
           ELSE
                    CALL 'ISPLINK'
                       USING
                          SELEKT
                          LENGTH-FIELD
                          SPFYFND-CMD.

           CALL 'ISPLINK'
                USING VGET FNDSERR SHARED.

           MOVE FIND-OPTION-ERRMSG
             TO BREAKDOWN-FIND-OPT-MSG.

           IF BYTES-1-8 OF BREAKDOWN-FIND-OPT-MSG = 'NO ERROR'
              MOVE 'ABSENT  ' TO MESSAGE-ID
              GO TO 8999-EXIT-FIND.

           IF BYTES-1-8 OF BREAKDOWN-FIND-OPT-MSG = 'NO HITS '
              MOVE 'WDED109C' TO MESSAGE-ID
              GO TO 8999-EXIT-FIND.

           IF BYTES-1-8 OF BREAKDOWN-FIND-OPT-MSG = 'CANNOT S'
              MOVE 'WDED109D' TO MESSAGE-ID
              GO TO 8999-EXIT-FIND.

           MOVE FIND-OPTION-ERRMSG TO GENERIC-ERROR-MSG.
              MOVE 'WDED101 ' TO MESSAGE-ID.

       8999-EXIT-FIND.
           EXIT.
           EJECT
       9000-END-OF-JOB.
      *
      **************************************************************
      *                                                            *
      *    THIS ROUTINE VPUTS THE FOLLOWING 17 VARIABLES TO THE    *
      *    USER PROFILE:                                           *
      *                                                            *
      *              W88DSN01                                      *
      *              W88DSN02                                      *
      *              W88DSN03                                      *
      *              W88DSN04                                      *
      *              W88DSN05                                      *
      *              W88DSN06                                      *
      *              W88DSN07                                      *
      *              W88DSN08                                      *
      *              W88DSN09                                      *
      *              W88DSN10                                      *
      *              W88DSN11                                      *
      *              W88DSN12                                      *
      *              W88DSN13                                      *
      *              W88DSN14                                      *
      *              W88DSN15                                      *
      *              W88DSN16                                      *
      *              W88DSN17                                      *
      *                                                            *
      **************************************************************
      *
           CALL 'ISPLINK'
                USING VPUT W88DSN01-W88DSN17 PROFILE.

       9999-EXIT-END-OF-JOB.
           EXIT.
