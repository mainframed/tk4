         SPACE 3
**********************************************************************
*                                                                    *
*        LISTMEMS                                                    *
*        --------                                                    *
*                                                                    *
*              MODIFIED APRIL 1985 TO INCLUDE A PARM WHICH WILL      *
*              ALLOW SUBSETTING THE OUTPUT FILE BASED UPON A         *
*              PARM MASK.  FOR EXAMPLE, PARM='MASK=ABC**999'         *
*              WILL OUTPUT ONLY THOSE MEMBER NAMES BEGINNING WITH    *
*              'ABC', FOLLOWED BY ANY TWO CHARACTERS AND THEN        *
*              FOLLOWED BY '999'.  ANOTHER EXAMPLE PARM='MASK=A15'   *
*              WILL OUTPUT ALL MEMBER NAMES BEGINNING WITH 'A15'.    *
*                                                                    *
**********************************************************************
*
LISTMEMS CSECT
         SAVE  (14,12)
         BALR  11,0
         USING *,11
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         EJECT
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
********************************************************************
*                                                                  *
*        SAVE REGISTER 1 CONTENTS IN REGISTER 11 AND PLACE         *
*        PARAMETER AREA INTO PARMAREA                              *
*                                                                  *
********************************************************************
         SPACE 3
STARTIT  DS    0D
         LR    R10,R1                 SAVE REG 1 CONTENTS IN REG 10
         C     R10,=F'0'
         BE    NOPARM
         L     R9,0(R10)
         C     R9,=F'0'
         BE    NOPARM
         MVC   PARMAREA,0(R9)
         B     MAINLINE
NOPARM   MVC   PARMLEN,=XL2'0000'
         B     MAINLINE
         EJECT
******************************************
*                                        *
*      M A I N   L I N E   L O G I C     *
*                                        *
******************************************
*
MAINLINE MVC   RETRNCD,=F'8'
*
         OPEN  (DIRDCB,(INPUT))
         OPEN  (OUTMEMS,(OUTPUT))
         BAL   14,PROCDIR           PERFORM ROUTINE TO PROCESS THE
*                                      DIRECTORY OUTPUTTING MEMBER
*                                      NAMES TO THE OUTPUT FILE
*                                      EACH AS ONE RECORD
         B     SHUTDOWN             BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
PROCDIR  ST    14,SAVEIT1          STORE R14 CONTENTS IN SAVEIT1
READBLK  READ  DIRECB,SF,DIRDCB,DIRECTRY,'S'
         CHECK DIRECB
         LA    R6,DIRECTRY+10      GET ADDR OF FIRST ENTRY
TABLLOOP BAL   14,PUTRECRD         PERFORM ROUTINE TO WRITE OUTPUT REC
         CLC   0(8,R6),DIRECTRY    IS THIS THE LAST GUY IN THIS BLOCK?
         BNE   CONTINUE            NO GO AHEAD WITH THIS BLOCK
         CLC   DIRECTRY(8),FFFF    YES,IS THIS LAST DIRECTORY BLOCK?
         BNE   READBLK             NO, GO GET ANOTHER BLOCK
         B     EXITDIR             YES, EXIT THIS ROUTINE
CONTINUE NI    11(R6),X'1F'        GET RID OF TOP 3 BITS OF INFO
         XR    R2,R2               CLEAR R2
         IC    R2,11(R6)           GET LENGTH OF VARIABLE PORTION IN
*                                  HALF WORDS
         SLA   R2,1                CHANGE TO NUMBER OF BYTES
         AR    R6,R2               ADD TO BASE ADDRESS
         LA    R6,12(R6)           ADD LENGTH OF FIXED PORTION
         B     TABLLOOP            BRANCH TO TABLLOOP
EXITDIR  L     14,SAVEIT1          RESTORE R14 WITH SAVEIT1 CONTENTS
         BR    14                  RETURN
         EJECT
         SPACE 3
*
PUTRECRD ST    14,SAVEIT2          STORE R14 CONTENTS IN SAVEIT2
         CLC   0(8,R6),FFFF
         BE    EXITPUT
         BAL   14,CHEK4HIT
         CLC   HITFOUND,=CL3'YES'
         BNE   EXITPUT
         MVC   RECRDOUT,=CL80' '
         MVC   RECRDOUT(8),0(R6)
         PUT   OUTMEMS,RECRDOUT
         MVC   RETRNCD,=F'0'
EXITPUT  L     14,SAVEIT2          RESTORE R14 WITH SAVEIT2 CONTENTS
         BR    14                  RETURN
         EJECT
*
*************************************************************
*                                                           *
*        THIS ROUTINE CHECKS THE PARM TO SEE IF THE USER    *
*        WISHES TO "SUBSET" HIS OUTPUT FILE.  IF THE PARM   *
*        IS ABSENT, THEN THIS ROUTINE MOVES "YES" TO        *
*        HITFOUND AND AS SUCH ALL MEMBER NAMES ARE OUTPUT.  *
*                                                           *
*        IF THERE IS A SIGNIFICANT PARM,  THEN IT IS        *
*        COMPARED AGAINST THE MEMBER NAME TO SEE IF THE     *
*        MEMBER NAME SHOULD BE OUTPUT.  IF NOT, THIS        *
*        ROUTINE MOVES "NO " TO HITFOUND.                   *
*                                                           *
*************************************************************
         SPACE 3
*
CHEK4HIT ST    14,SAVEIT3          STORE R14 CONTENTS IN SAVEIT3
         MVC   HITFOUND,=CL3'YES'
         CLC   PARMLEN,=H'0'       IS THE PARM ABSENT ?
         BE    EXITCHEK            IF SO, BRANCH TO EXITCHEK
         CLC   PARMID,=CL5'MASK='  IS PARM ID = MASK=
         BNE   EXITCHEK            IF NOT, BRANCH TO EXITCHEK
         LH    R9,PARMLEN
         CH    R9,=H'13'           IS LENGTH OF PARM > 13 ?
         BH    EXITCHEK            IF SO, BRANCH TO EXITCHEK
         CH    R9,=H'6'            IS LENGTH OF PARM < 6 ?
         BL    EXITCHEK            IF SO, BRANCH TO EXITCHEK
         LA    R4,5
         SR    R9,R4
         STH   R9,MASKLEN          MASKLEN=SIGNIF LENGTH OF PARM MASK
         LA    R9,7(R6)
         LA    R4,8
CHKLOOP1 C     R4,=F'1'
         BL    EXITCHEK
         CLI   0(R9),C' '
         BNE   CHEK2
         SH    R4,=H'1'
         S     R9,=F'1'
         B     CHKLOOP1
CHEK2    STH   R4,MEMBRLEN         MEMBRLEN=SIGNIF LEN OF THE MEMBER
         LA    R4,PARMMASK         R4==> ADDRESS OF PARM MASK
         LR    R5,R6               R5==> ADDRESS OF THE MEMBER
         LH    R9,MASKLEN
         CH    R9,MEMBRLEN
         BNH   CHEK3
         MVC   HITFOUND,=CL3'NO '
         B     EXITCHEK
CHEK3    LA    R9,1
CHEK4    CH    R9,MASKLEN
         BH    EXITCHEK
         CLI   0(R4),C'*'
         BE    BUMPREGS
         CLC   0(1,R4),0(R5)
         BE    BUMPREGS
         MVC   HITFOUND,=CL3'NO '
         B     EXITCHEK
BUMPREGS A     R4,=F'1'
         A     R5,=F'1'
         A     R9,=F'1'
         B     CHEK4
EXITCHEK L     14,SAVEIT3          RESTORE R14 WITH SAVEIT3 CONTENTS
         BR    14                  RETURN
         EJECT
*
*************************************************************
*                                                           *
*        E N D - O F - J O B     P R O C E S S I N G        *
*                                                           *
*************************************************************
*
SHUTDOWN CLOSE (DIRDCB)          CLOSE DIRDCB
         CLOSE (OUTMEMS)         CLOSE OUTMEMS
         L     15,RETRNCD
         L     13,SAVEAREA+4
         RETURN (14,12),RC=(15)
         EJECT
*
*************************************************************
*                                                           *
*                       D C B ' S                           *
*                                                           *
*************************************************************
*
DIRDCB   DCB   DDNAME=INPDS,                                           X
               BLKSIZE=256,                                            X
               KEYLEN=8,                                               X
               LRECL=256,                                              X
               DSORG=PS,                                               X
               MACRF=R,                                                X
               RECFM=F
*
OUTMEMS DCB    DDNAME=OUTMEMS,                                         X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               BLKSIZE=800,                                            X
               MACRF=(PM)
*
RECRDOUT DS    CL80
*
*
         EJECT
*************************************************************
*                                                           *
*               D A T A   A R E A S                         *
*                                                           *
*************************************************************
*
SAVEIT1  DS    F
SAVEIT2  DS    F
SAVEIT3  DS    F
RETRNCD  DS    F
PARMAREA DS    0CL15
PARMLEN  DS    H
PARMID   DS    CL5
PARMMASK DS    CL8
HITFOUND DS    CL3
MASKLEN  DS    H
MEMBRLEN DS    H
FFFF     DC    8X'FF'
DIRECTRY DS    CL264
         END   LISTMEMS
