./ ADD NAME=$CALL                                  17OCT89 12.47.53
         MACRO
&NAME    $CALL &EP=,&EPLOC=,&PARAM=,&PARMLOC=
         MNOTE *,'       $CALL     VERSION 001 06/06/75  06/06/75  GPW'
.**********************************************************************
.*                                                                    *
.* $CALL                                                              *
.*                                                                    *
.* FUNCTION       GENERATE CODE REQUIRED TO PASS CONTROL TO ANOTHER   *
.*                CSECT AND PASS PARAMETERS IF REQUIRED.              *
.*                                                                    *
.* DESCRIPTION    STANDARD IBM LINKAGE CODE IS GENERATED BY THE MACRO *
.*                TO PASS CONTROL TO A SPECIFIED LOAD MODULE.  A LIST *
.*                OF PARAMETERS MAY ALSO BE CONSTRUCTED.              *
.*                                                                    *
.*                A FULLWORD NO-OP CONTAINING AN IDENTIFICATION       *
.*                NUMBER WILL BE PLACED AFTER THE BALR USED TO        *
.*                TRANSFER CONTROL.  THIS VALUE WILL BE DISPLAYED IN  *
.*                DUMPS.  THE STARTING VALUE IS 10 AND IS INCREMENTED *
.*                BY 10 FOR EACH CALL.  THE VALUE IS DEFINED BY THE   *
.*                GLOBAL SYMBOL $CALLID.                              *
.*                                                                    *
.* SYNTAX         NAME     $CALL     EP=SYM1                          *
.*                                   EPLOC=SYM2                       *
.*                                                                    *
.*                                   PARAM=(SYM-LIST)                 *
.*                                   PARMLOC=SYM3                     *
.*                                                                    *
.*                                                                    *
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       *
.*                         INSTRUCTION GENERATED.                     *
.*                                                                    *
.*                EP     - THE NAME OF THE CSECT TO WHICH CONTROL IS  *
.*                         TO BE TRANSFERRED.  A V TYPE ADDRESS       *
.*                         CONSTANT WILL BE GENERATED.                *
.*                                                                    *
.*                EPLOC  - THE NAME OF A FULLWORD CONTAINING THE      *
.*                         ADDRESS OF THE CSECT TO WHICH CONTROL IS   *
.*                         TO BE TRANSFERRED.                         *
.*                                                                    *
.*                         NOTE - IF EP IS SPECIFIED, EPLOC IS        *
.*                                IGNORED.                            *
.*                                                                    *
.*                PARAM  - A LIST OF SYMBOLIC NAMES OF PARAMETERS TO  *
.*                         BE PASSED TO THE CALLED ROUTINE.  AN       *
.*                         IN-LINE PARAMETER LIST WILL BE GENERATED.  *
.*                         THE ADDRESS OF THE LIST WILL BE PLACED IN  *
.*                         REGISTER 1.  THE HIGH ORDER BIT OF THE     *
.*                         LAST WORD IN THE LIST WILL BE SET TO 1.    *
.*                                                                    *
.*                PARMLOC- THE NAME OF THE FIRST WORD OF A USER       *
.*                         SUPPLIED PARAMETER LIST.                   *
.*                                                                    *
.*                         NOTE - IF PARAM IS SPECIFIED, PARMLOC IS   *
.*                                IGNORED.                            *
.*                                                                    *
.* ERRORS         EP OR EPLOC MUST BE SPECIFIED.  IF NEITHER VALUE    *
.*                IS PROVIDED, AN ERROR MESSAGE WILL BE GENERATED.    *
.*                THE ERROR IS SEVERITY CODE 8.                       *
.*                                                                    *
.* EXAMPLE        EX1      $CALL EP=GDATE,PARAM=DATE                  *
.*                              .                                     *
.*                              .                                     *
.*                              .                                     *
.*                DATE     DC    CL12' '                              *
.*                                                                    *
.*                                                                    *
.*                EX2      $CALL EPLOC=SUBRADR,PARMLOC=PARMLIST       *
.*                              .                                     *
.*                              .                                     *
.*                              .                                     *
.*                SUBRADR  DC    V(GDATE)                             *
.*                PARMLIST DC    A(DATE)                              *
.*                              .                                     *
.*                              .                                     *
.*                              .                                     *
.*                DATE     DC    CL12' '                              *
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                NAME     TYPE  USE                                  *
.*                                                                    *
.*                &$CALLID   A   USED TO GENERATE AN IDENTIFICATION   *
.*                               NUMBER IN A NO-OP FOLLOWING THE BALR *
.*                               USED TO TRANSFER CONTROL.  THE VALUE *
.*                               IS INCREMENTED BY 10 FOLLOWING EACH  *
.*                               CALL.  THIS VALUE WILL BE DISPLAYED  *
.*                               IN THE TRACEBACK PROVIDED WITH       *
.*                               DUMPS.                               *
.* MACROS CALLED                                                      *
.*                                                                    *
.*                NONE                                                *
.*                                                                    *
.**********************************************************************
.*
         GBLA  &$CALLID
.*
         LCLA  &COUNT
         LCLC  &ID
.*
&ID      SETC  '&NAME'
.*-------DO I SET UP PARM LIST?
         AIF   ('&PARAM' EQ '').PARMADR
         AIF   ('&PARAM' EQ '(R1)').PARMADR
         CNOP  0,4                      ALIGN TO FULL WORD
&COUNT   SETA  4*N'&PARAM+4
&ID      BAL   R1,*+&COUNT              BRANCH AROUND LIST
&ID      SETC  ''
&COUNT   SETA  1
.LOOP    ANOP
         AIF   (&COUNT EQ N'&PARAM).LAST
         DC    A(&PARAM(&COUNT))        PARAMETER &COUNT
&COUNT   SETA  &COUNT+1
         AGO   .LOOP
.LAST    DC    X'80',AL3(&PARAM(&COUNT)) LAST PARAMETER
         AGO   .ENTRY
.*-------SET UP PARM ADDR
.PARMADR AIF   ('&PARMLOC' EQ '').ENTRY
&ID      LA    R1,&PARMLOC              LOAD PARM LIST ADDRESS
&ID      SETC  ''
.*-------SET UP ENTRY POINT
.ENTRY   AIF   ('&EP' EQ '').EPLOC
&ID      L     R15,=V(&EP)              LOAD ENTRY ADDRESS    120475SH
         AGO   .BALR
.*-------SET UP ENTRY POINT
.EPLOC   AIF   ('&EPLOC' EQ '').BAD
&ID      L     R15,&EPLOC               LOAD ENTRY POINT ADDRESS
.*-------BRANCH TO SUBROUTINE
.BALR    BALR  R14,R15                  BRANCH TO SUBROUTINE
&$CALLID SETA  &$CALLID+10
         DC    X'4700',AL2(&$CALLID)    NO-OP, ID
         MEXIT
.*-------NO ENTRY POINT
.BAD     MNOTE 8,'*** ERROR - NO ENTRY POINT SPECIFIED'
         MEND
./ ADD NAME=$ENTER                                 17OCT89 12.47.53
         MACRO
&NAME    $ENTER &BASE=R3,&CSECT=YES,&SAVE=,&RENT=,&SP=1,&SPM=YES,      +
               &CHAIN=YES
         MNOTE *,'       $ENTER    VERSION 003 05/07/75  04/29/76  GPW'
.**********************************************************************
.*                                                                    *
.* $ENTER                                                             *
.*                                                                    *
.* FUNCTION       PROVIDE ENTRY CODING TO ESTABLISH BASE REGISTERS,   *
.*                ALLOCATE AND CHAIN SAVE AREAS, SET PROGRAM MASK,    *
.*                AND OBTAIN WORK AREA FOR RE-ENTRANT PROGRAMS.       *
.*                                                                    *
.* DESCRIPTION    THE MACRO WILL GENERATE CODE TO ESTABLISH ONE OR    *
.*                MORE BASE REGISTERS.  IT IS ASSUMED THAT STANDARD   *
.*                IBM LINKAGE CONVENTIONS HAVE BEEN FOLLOWED AND THAT *
.*                REGISTER 15 CONTAINS THE ADDRESS OF THE ENTRY       *
.*                POINT.  USER SPECIFIED OR DEFAULT BASE REGISTERS    *
.*                ARE INITIALIZED.  THE FIRST BASE REGISTER CONTAINS  *
.*                THE ADDRESS OF THE ENTRY POINT, AND SUCCESSIVE BASE *
.*                REGISTER ADDRESSES ARE INCREMENTED BY 4096.  THE    *
.*                DEFAULT BASE REGISTER IS REGISTER 3.  IF REGISTER 2 *
.*                IS SPECIFIED AS A BASE REGISTER, IT WILL BE         *
.*                ALLOWED, BUT A WARNING MESSAGE WILL BE DISPLAYED.   *
.*                REGISTERS 0, 1, 13, 14, AND 15 MAY NOT BE           *
.*                SPECIFIED AS BASE REGISTERS.  ALL REGISTER          *
.*                SPECIFICATIONS MUST BE MADE AS MNEMONICS (EG,       *
.*                REGISTER 3 IS R3).                                  *
.*                                                                    *
.*                IDENTIFICATION CONSTANTS SPECIFYING THE DATE AND    *
.*                TIME OF ASSEMBLY ARE CONSTRUCTED IN THE ENTRY       *
.*                CODING.  THESE VALUES ARE DISPLAYED IN DUMPS AND    *
.*                MAY BE USED TO VERIFY THAT THE PROPER VERSION OF    *
.*                THE ROUTINE HAS BEEN USED.                          *
.*                                                                    *
.*                ALL BITS OF THE PROGRAM MASK ARE SET.  IBM          *
.*                TRANSFERS CONTROL TO THE USER PROGRAM WITH ALL      *
.*                PROGRAM MASK BITS OFF.  IF SPM=NO IS SPECIFIED,     *
.*                THE PROGRAM MASK WILL BE UNALTERED.                 *
.*                                                                    *
.*                THE USER MAY SPECIFY THE NAME OF A WORK AREA OF 18  *
.*                FULLWORDS TO BE USED AS A SAVE AREA.  IF A USER     *
.*                AREA IS NOT SUPPLIED, AN 18 FULLWORD AREA IS        *
.*                ALLOCATED AND INITIALIZED TO ZERO.  THE ADDRESS OF  *
.*                THE SAVE AREA IS LOADED INTO REGISTER 13 AND IT IS  *
.*                CHAINED TO THE SAVE AREA OF THE CALLING PROGRAM.    *
.*                                                                    *
.*                THE USER MAY SPECIFY THAT THE MACRO IS NOT TO       *
.*                OBTAIN A SAVE AREA OR CHAIN SAVE AREAS BY           *
.*                SPECIFYING CHAIN=NO.  THE USER IS THEN              *
.*                RESPONSIBLE FOR SAVE AREA CHAINING.                 *
.*                                                                    *
.*                REGISTER EQUIVALENCES (R0  EQU  0, ETC.) ARE        *
.*                GENERATED FOR THE FIRST USAGE OF THE MACRO.         *
.*                                                                    *
.*                A CSECT DEFINITION WILL BE GENERATED UNLESS         *
.*                CSECT=NO IS SPECIFIED.  IF CSECT=NO IS SPECIFIED,   *
.*                AN ENTRY STATEMENT WILL BE GENERATED.               *
.*                                                                    *
.*                RE-ENTRANT CODING IS SUPPORTED.  FOR RE-ENTRANT     *
.*                CODING, THE USER MUST SPECIFY THE LENGTH OF A       *
.*                WORK AREA.  THE WORK AREA IS OBTAINED FROM SUBPOOL  *
.*                1 UNLESS OTHERWISE SPECIFIED.  THE FIRST 18 WORDS   *
.*                OF THE WORK AREA ARE USED FOR THE SAVE AREA.        *
.*                                                                    *
.* SYNTAX         NAME     $ENTER    BASE=(REG1,...,REGN)             *
.*                                   CSECT=NO                         *
.*                                   SAVE=SYM                         *
.*                                   RENT=LEN                         *
.*                                   SP=NUMBER                        *
.*                                   SPM=NO                           *
.*                                   CHAIN=NO                         *
.*                                                                    *
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       *
.*                         INSTRUCTION GENERATED.                     *
.*                                                                    *
.*                BASE   - THE REGISTERS TO BE USED AS BASE           *
.*                         REGISTERS.  THE DEFAULT IS R3.  THE FIRST  *
.*                         REGISTER SPECIFIED WILL CONTAIN THE        *
.*                         ADDRESS OF THE ENTRY POINT, AND SUCCEEDING *
.*                         BASE VALUES WILL BE INCREMENTED BY 4096.   *
.*                         REGISTERS 0, 1, 13, 14, AND 15 MAY NOT BE  *
.*                         SPECIFIED AS BASE REGISTERS.  REGISTERS    *
.*                         MUST BE SPECIFIED IN MNEMONIC FORM (EG,    *
.*                         R3 FOR REGISTER 3).                        *
.*                                                                    *
.*                CSECT  - CSECT=NO SPECIFIES THAT CODING FOR AN      *
.*                         ENTRY POINT RATHER THAN A CSECT IS TO BE   *
.*                         GENERATED.                                 *
.*                                                                    *
.*                SAVE   - SPECIFIES THE NAME OF A USER DEFINED 18    *
.*                         WORD SAVE AREA TO BE USED INSTEAD OF       *
.*                         GENERATING AN IN-LINE SAVE AREA.  IF RENT  *
.*                         IS SPECIFIED, SAVE MUST SPECIFY THE NAME   *
.*                         ASSIGNED TO THE FIRST 18 WORDS IN THE      *
.*                         WORK AREA.                                 *
.*                                                                    *
.*                RENT   - SPECIFIES THAT RE-ENTRANT CODE IS TO BE    *
.*                         GENERATED.  LEN IS THE LENGTH OF A WORK    *
.*                         AREA TO BE OBTAINED BY A GETMAIN.          *
.*                                                                    *
.*                SP     - SPECIFIES THE SUBPOOL FROM WHICH THE WORK  *
.*                         AREA FOR RE-ENTRANT CODING IS TO BE        *
.*                         OBTAINED.  DEFAULT IS SUBPOOL 1.           *
.*                                                                    *
.*                SPM    - SPM=NO SPECIFIES THAT THE PROGRAM MASK IS  *
.*                         NO TO BE ALTERED.                          *
.*                                                                    *
.*                CHAIN  - CHAIN=NO SPECIFIES THAT SAVE AREA ARE NOT  *
.*                         TO BE CHAINED.  THIS OPTION IS INTENDED    *
.*                         FOR USE ONLY BY HIGH ACTIVITY RE-ENTRANT   *
.*                         MODULES WHERE THE OVERHEAD OF              *
.*                         GETMAIN/FREEMAIN IS TO BE AVOIDED.         *
.*                                                                    *
.* ERRORS         THE NAME FIELD MUST BE SPECIFIED.  IF IT IS NOT, A  *
.*                GENERATED NAME, $ENTNNNN WILL BE GENERATED AND A    *
.*                SEVERITY 8 MNOTE IS GENERATED.  IF NO CODE WERE     *
.*                GENERATED AND NO BASE REGISTER DEFINED, THE ERROR   *
.*                LISTING WOULD BE LARGE.  TO REDUCE THE SIZE OF THE  *
.*                ERROR LISTING AND ALLOW OTHER ERRORS TO BE FOUND,   *
.*                THE MACRO WILL EXPAND.                              *
.*                                                                    *
.*                                                                    *
.* EXAMPLE        EX1      $ENTER                                     *
.*                                                                    *
.*                EX2      $ENTER CSECT=NO                            *
.*                                                                    *
.*                EX3      $ENTER BASE=(R3,R4,R5)                     *
.*                                                                    *
.*                EX4      $ENTER BASE=R12,RENT=DSECTLEN,SAVE=SAVEAREA*
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                NAME     TYPE  USE                                  *
.*                                                                    *
.*                &ENCOUNT   A   SET TO 1 AFTER REGISTER EQUIVALENCES *
.*                               GENERATED TO PREVENT EQUIVALENCES    *
.*                               FROM BEING GENERATED FOR LATER USES. *
.*                                                                    *
.* MACROS USED                                                        *
.*                                                                    *
.*                GETMAIN                                             *
.*                                                                    *
.* UPDATE SUMMARY                                                     *
.*                                                                    *
.*      VERSION   DATE     CHANGE                                     *
.*                                                                    *
.*        003   04/29/76   CHAIN KEYWORD ADDED                        *
.*                                                                    *
.*                                                                    *
.**********************************************************************
.*
         GBLA  &ENCOUNT
.*
         LCLA  &PARMNO,&REGNO
         LCLC  &REG,&CHAR,&LAST,&USING,&TEMP,&ID,&FIRST
.*
&ID      SETC  '&NAME'
         AIF   ('&NAME' NE '').CKCSECT
&ID      SETC  '$ENT&SYSNDX'
         MNOTE 8,'NAME OPERAND REQUIRED, NOT SPECIFIED. &ID WILL BE USE+
               D.'
.CKCSECT AIF   ('&CSECT' NE 'NO').CSECT
         AIF   ('&SYSECT' NE '').CKEQU
         MNOTE 8,'ENTRY POINT SPECIFIED, BUT NO CSECT DEFINED'
.CSECT   ANOP
         SPACE
&ID      CSECT
.CKEQU   AIF   (&ENCOUNT EQ 1).SKIPEQU
         SPACE
***********************************************************************
*                                                                     *
*                      REGISTER EQUIVALENCES                          *
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.*
&ENCOUNT SETA  1
         SPACE
.SKIPEQU AIF   ('&CSECT' NE 'NO').CSECT2
         AIF   ('&SYSECT' EQ '').CSECT2
.*-------ENTRY POINT
         ENTRY &ID
         USING &ID,R15                  DEFINE BASE REGISTER
&ID      B     14(R15)                  BRANCH AROUND ID
         DC    AL1(8)                   IDENTIFIER LENGTH
         DC    CL8'&ID'                 ENTRY POINT NAME
         DC    CL1' '                   SPACER
         AGO   .STREGS
.*-------CSECT
.CSECT2  USING &ID,R15                  DEFINE BASE REGISTER
         B     28(0,R15)                BRANCH AROUND ID
         DC    AL1(23)                  IDENTIFIER LENGTH
         DC    CL8'&ID'                 CSECT NAME
         DC    CL1' '                   SPACER
         DC    CL8'&SYSDATE'            DATE OF ASSEMBLY
         DC    CL1' '                   SPACER
         DC    CL5'&SYSTIME'            TIME OF ASSEMBLY
.STREGS  STM   R14,R12,12(R13)          SAVE REGISTERS
&PARMNO  SETA  1                        INITIALIZE COUNTER
.CKBASE  ANOP
&REG     SETC  '&BASE(&PARMNO)'(1,3)
         AIF   ('&REG'(1,1) NE 'R').SKIPBAS
&TEMP    SETC  '&REG'(2,2)
&REGNO   SETA  &TEMP
         AIF   (&REGNO LT 2).BADBASE
         AIF   (&REGNO GT 12).BADBASE
         AIF   (&REGNO NE 2).SETBASE
         MNOTE 0,'*** WARNING - R2 IS A BASE REGISTER. TRANSLATE AND TE+
               ST INSTRUCTION WILL DESTROY CONTENTS.'
.SETBASE ANOP
         AIF   ('&FIRST' NE '').SETBAS2
         LR    &REG,R15                 LOAD BASE ADDRESS
&FIRST   SETC  '&REG'                   SAVE REGISTER
         AGO   .SETLAST
.SETBAS2 LA    &REG,4095(&LAST)         ADD 4095 TO LAST BASE
         LA    &REG,1(&REG)             ADD 1 MORE
.SETLAST ANOP
&LAST    SETC  '&REG'
&USING   SETC  '&USING.,&REG'
         AGO   .NEXTBAS
.SKIPBAS MNOTE 8,'*&REG* IS AN INVALID REGISTER FORM, IGNORED'
         AGO   .NEXTBAS
.BADBASE MNOTE 8,'*&REG* IS AN INVALID BASE REGISTER, IGNORED'
.NEXTBAS ANOP
&PARMNO  SETA  &PARMNO+1
         AIF   (&PARMNO LE N'&BASE).CKBASE
         DROP  R15                      DISCONTINUE R15 BASE
         USING &ID.&USING               DEFINE BASE REGISTERS
.*-------SEE IF PROGRAM MASK IS TO BE SET
         AIF   ('&SPM' EQ 'NO').NOSPM
         LA    R15,15                   LOAD PGM MASK SETTING
         SLA   R15,24                   SHIFT TO BITS 4-7
         SPM   R15                      SET PGM MASK AND COND
.NOSPM   AIF   ('&CHAIN' EQ 'NO').DONE
         AIF   ('&RENT' NE '').GETMAIN
         LR    R15,R13                  SAVE OLD SAVEAREA ADDR
         AIF   ('&SAVE' NE '').LOADSAV
         CNOP  0,4                      FULL WORD ALIGNMENT
         BAL   R13,*+76                 LOAD SAVEAREA ADDR
         DC    18F'0'                   SAVE AREA
         AGO   .CHAIN
.LOADSAV LA    R13,&SAVE                LOAD NEW SAVEAREA ADDR
         AGO   .CHAIN
.GETMAIN MNOTE *,' GETMAIN R,LV=&RENT,SP=&SP'
         GETMAIN R,LV=&RENT,SP=&SP      GET STORAGE
* END OF GETMAIN - $ENTER
         LR    R15,R13                  SAVE OLD SAVEAREA ADDR
         LR    R13,R1                   LOAD STORAGE ADDRESS
         USING &SAVE,R13                DEFINE BASE REGISTER
         L     R1,24(R15)               RESTORE REG 1 CONTENTS
.CHAIN   ST    R15,4(R13)               CHAIN SAVE AREAS
         ST    R13,8(R15)
.DONE    ANOP
         MEND
./ ADD NAME=$EPILOG  0100-83301-83301-1318-00019-00019-00000-$A1238
         MACRO
&LABEL   $EPILOG &RC
         GBLC  &PROGM
&LABEL   LR    R1,R13              GET SAVEAREA ADDRESS
         L     R13,4(R13)          GET BACK CHAIN POINTER
         AIF   ('&PROGM' NE 'GETMAIN').NOFREE
         L     R0,16(R13)          GET SAVEAREA LENGTH
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA
         AGO   .LM
.NOFREE  ANOP
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
.LM      ANOP
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS
         AIF   (T'&RC EQ 'O').SPEC
         LA    R15,&RC             SET RETURN CODE
.SPEC    ANOP
         BR    R14                 RETURN TO CALLER
         MEND
./ ADD NAME=$PROLOG  0100-83301-83301-1319-00101-00101-00000-$A1238
         MACRO
&LABEL   $PROLOG &LV=0,&GM=Y
.**********************************************************************
.*
.*       THIS MACRO WILL PROVIDE ENTRY LINKAGE AND OPTIONALLY
.*       MULTIPLE BASE REGISTERS.  ALSO, VIA THE 'LV=' KEYWORD
.*       PROVIDE ADDITIONAL USER STORAGE (APPENDED TO THE
.*       SAVE AREA) ADDRESSABLE FROM REG 13.  IF NO OPERANDS
.*       ARE CODED, REG 12 IS ASSUMED THE BASE. EXAMPLE:
.*              SECTNAME $PROLOG          = STANDARD REG 12 BASE
.*              SECTNAME $PROLOG 5        = STANDARD, REG 5 BASE
.*              SECTNAME $PROLOG 10,LV=20 = ADD 20 BYTES TO SAVE AREA
.*                                             REG 10 IS BASE
.*              SECTNAME $PROLOG R10,R11  = REGS 10 AND 11 ARE BASES
.*
.**********************************************************************
         LCLA  &AA,&AB,&AC
         GBLB  &PRORG
         GBLC  &PROGM
&AC      SETA  4096
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    AL1(26)
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REG
         USING &LABEL,R12          INFORM ASSEMBLER
         AIF   (&LV GT 4023).MERR
         AIF   ('&GM' EQ 'N').NOGM
&PROGM   SETC  'GETMAIN'
         LA    R0,&LV+72           LOAD REG 0 WITH LENGTH VARIABLE
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA AND USER
         AIF   (&LV+72 LE 256).XC2
         AIF   (&LV+72 LE 512).XC1
         MVI   0(R1),X'00'         MOVE X'00' TO FIRST BYTE
         LR    R2,R1               SAVE POINTER IN EVEN REG
         LA    R4,1(R1)            SET RECEIVING POINTER
         LR    R5,R0               SET RECEIVING LENGTH
         BCTR  R5,R0               DECREMENT LENGTH
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         LA    R3,1                SET SENDING LENGTH
         MVCL  R4,R2               INSTRUCTION PADS WITH X'00'
         AGO   .STORE
.XC1     ANOP
         XC    256(&LV-184,R1),256(R1)  CLEAR SAVE AREA
         XC    0(256,R1),0(R1)          CLEAR SAVE AREA
         AGO   .STORE
.XC2     ANOP
         XC    0(&LV+72,R1),0(R1)       CLEAR SAVE AREA
         AGO   .STORE
.NOGM    ANOP
         CNOP  0,4
         LA    R1,SAVE&SYSNDX
         B     *+76
SAVE&SYSNDX DC 18F'0'
.STORE   ANOP
         ST    R13,4(R1)           SAVE BACK CHAIN
         ST    R1,8(R13)           SET FORWARD CHAIN
         LR    R11,R1              SAVE NEW SAVEAREA ADDRESS
         L     R15,16(R13)         RESTORE REG 15
         ST    R0,16(R13)          SAVE SAVEAREA LENGTH
         LM    R0,R1,20(R13)       RESTORE REGS USED IN GETMAIN
         LR    R13,R11             SET SAVEAREA POINTER
         AIF   (N'&SYSLIST EQ 0).MEND
         AIF   ('&SYSLIST(1)' EQ 'R12').SKIPIT
         AIF   ('&SYSLIST(1)' EQ '12').SKIPIT
         LA    &SYSLIST(1),&LABEL  LOAD REQUESTED BASE REG
         DROP  R12                 DROP ASSUMED BASE REG
         USING &LABEL,&SYSLIST(1)  INFORM ASSEMBLER
.SKIPIT  ANOP
&AA      SETA  2
.LOOP    ANOP
         AIF   (&AA GT N'&SYSLIST).MEXIT
&AB      SETA  &AA-1
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AB))  LOAD NEXT BASE REG
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AA))  LOAD NEXT BASE REG
         USING &LABEL+&AC,&SYSLIST(&AA) INFORM ASSEMBLER
&AC      SETA  &AC+4096
&AA      SETA  &AA+1
         AGO   .LOOP
.MEXIT   ANOP
         AIF   (&PRORG).MEX2
         SPACE
         $REGS
         SPACE
.MEX2    ANOP
&AA      SETA  &LV+72
         MNOTE *,'TOTAL STORAGE AREA RECEIVED = &AA'
         MEXIT
.MEND    ANOP
         MNOTE *,'NO REGISTER SPECIFIED - R12 ASSUMED'
         AGO   .MEXIT
.MERR    ANOP
         MNOTE 12,'LV > 4023 - REQUEST IGNORED'
         AGO   .MEXIT
         MEND
./ ADD NAME=$REGS    0100-84191-84191-1813-00026-00026-00000-KK00150
         MACRO
         $REGS
**                           EQUATES FOR SYMBOLIC REG USAGE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         MEND
./ ADD NAME=$REQU                                  17OCT89 12.47.53
         MACRO
         REQU
**                           EQUATES FOR SYMBOLIC REG USAGE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         MEND
./ ADD NAME=$RTRN                                  17OCT89 12.47.53
         MACRO
&NAME    $RTRN &RC=0,&RENT=,&SP=1,&SPM=YES,&REST01=YES
         MNOTE *,'       $RTRN     VERSION 002 05/07/75  04/29/76  GPW'
.**********************************************************************
.*                                                                    *
.* $RTRN                                                              *
.*                                                                    *
.* FUNCTION       GENERATE CODE REQUIRED TO RETURN CONTROL TO THE     *
.*                CALLING PROGRAM.                                    *
.*                                                                    *
.* DESCRIPTION    STANDARD IBM LINKAGE CODE IS GENERATED BY THE MACRO *
.*                TO RETURN CONTROL TO THE CALLING PROGRAM.  THE USER *
.*                MAY SPECIFY THE RETURN CODE TO BE RETURNED.         *
.*                RE-ENTRANT CODE IS SUPPORTED.                       *
.*                                                                    *
.*                REGISTERS 0 THROUGH 14 (OR OPTIONALLY, REGISTERS    *
.*                2 THROUGH 14) WILL BE RESTORED TO THEIR STATUS      *
.*                UPON ENTRY TO THE ROUTINE.  REGISTER 15 WILL BE     *
.*                USED FOR A DEFAULT OR USER SPECIFIED RETURN CODE.   *
.*                AN X'FF' WILL BE PLACED IN THE HIGH ORDER BYTE OF   *
.*                WORD 4 OF THE OLD SAVE AREA TO INDICATE THAT        *
.*                CONTROL HAS BEEN RETURNED TO THE CALLING PROGRAM    *
.*                                                                    *
.*                CODE IS ALSO GENERATED TO RESTORE THE PROGRAM MASK  *
.*                TO ITS STATUS UPON ENTRY TO THE ROUTINE.  THE       *
.*                VALUE OF THE MASK IS OBTAINED FROM THE HIGH ORDER   *
.*                BYTE OF REGISTER 14.  THE ASSUMPTION IS MADE THAT   *
.*                STANDARD LINKAGE WAS USED TO INVOKE THE ROUTINE,    *
.*                THAT IS, THAT ENTRY WAS MADE BY A  BALR  R14,R15.   *
.*                IF THIS IS NOT TRUE, THE PROGRAM MASK WILL BE       *
.*                RESTORED INCORRECTLY.  IF SPM=NO IS SPECIFIED, THE  *
.*                PROGRAM MASK WILL NOT BE ALTERED.                   *
.*                                                                    *
.*                IF RE-ENTRANT CODE IS BEING GENERATED, A DROP R2    *
.*                STATEMENT IS GENERATED, SO THE USER SHOULD NOT USE  *
.*                REGISTER 2 AS A BASE REGISTER.                      *
.*                                                                    *
.* SYNTAX         NAME     $RTRN     RC=NUM1                          *
.*                                      (R15)                         *
.*                                   RENT=LEN                         *
.*                                   SP=NUM2                          *
.*                                   SPM=NO                           *
.*                                   REST01=NO                        *
.*                                                                    *
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       *
.*                         INSTRUCTION GENERATED.                     *
.*                                                                    *
.*                RC     - SPECIFIES THE RETURN CODE TO BE PLACED IN  *
.*                         REGISTER 15.  A NUMBER MAY BE SPECIFIED,   *
.*                         OR (R15) INDICATES THAT THE RETURN CODE    *
.*                         HAS ALREADY BEEN PLACED IN REGISTER 15 BY  *
.*                         THE USER.  THE DEFAULT RETURN CODE IS 0.   *
.*                                                                    *
.*                RENT   - INDICATES THAT THE WORK AREA OBTAINED BY   *
.*                         $ENTER IS TO BE FREED.  LEN SPECIFIES THE  *
.*                         LENGTH OF THE AREA.  THE ADDRESS IS        *
.*                         OBTAINED FROM REGISTER 13.                 *
.*                                                                    *
.*                SP     - SPECIFIES THE SUBPOOL OF THE WORK AREA.    *
.*                         IT MUST AGREE WITH THE SUBPOOL SPECIFIED   *
.*                         IN $ENTER.  THE DEFAULT IS 1.              *
.*                                                                    *
.*                SPM    - SPM=NO SPECIFIES THAT THE PROGRAM MASK     *
.*                         IS NOT TO BE ALTERED.                      *
.*                                                                    *
.*                REST01 - REST01=NO SPECIFIES THAT THE CONTENTS      *
.*                         OF REGISTERS 0 AND 1 ARE NOT TO BE         *
.*                         RESTORED.  THIS OPTION SHOULD BE           *
.*                         SPECIFIED WHEN DATA IS TO BE PASSED        *
.*                         BACK TO THE CALLING PROGRAM IN             *
.*                         REGISTERS 0 AND 1.                         *
.*                                                                    *
.* ERRORS         NO ERROR MESSAGES ARE GENERATED                     *
.*                                                                    *
.* EXAMPLE        EX1      $RTRN                                      *
.*                                                                    *
.*                EX2      $RTRN  RC=4                                *
.*                                                                    *
.*                EX3      $RTRN  RC=(R15),RENT=DSECTLEN              *
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                NONE                                                *
.*                                                                    *
.* MACROS CALLED                                                      *
.*                                                                    *
.*                FREEMAIN                                            *
.*                                                                    *
.* UPDATE SUMMARY                                                     *
.*                                                                    *
.*      VERSION   DATE     CHANGE                                     *
.*                                                                    *
.*        002   04/29/76   REST01 KEYWORD ADDED                       *
.*                                                                    *
.**********************************************************************
.*
         AIF   ('&RENT' EQ '').REGULAR
&NAME    LR    R1,R13                   LOAD STORAGE ADDRESS
         BALR  R2,0                     ESTABLISH BASE REG
         USING *,R2                     DEFINE BASE REGISTER
         L     R3,4(R13)                SAVE OLD SAVEAREA ADDR
         LR    R4,R15                   SAVE RETURN CODE
.FREE    MNOTE *,' FREEMAIN R,LV=&RENT,A=(1),SP=&SP'
         FREEMAIN R,LV=&RENT,A=(1),SP=&SP FREE STORAGE
* END FREEMAIN - $RTRN
         AIF   ('&RC' NE '(R15)').RESSAV
         LR    R15,R4                   RESTORE RETURN CODE
.RESSAV  LR    R13,R3                   RESTORE OLD SAVEAREA
         DROP  R2
         AGO   .COMMON
.REGULAR ANOP
&NAME    L     R13,4(R13)               RESTORE OLD SAVEAREA
.COMMON  AIF   ('&RC' EQ '(R15)').RES14
         LA    R15,&RC                  SET RETURN CODE
.RES14   L     R14,12(R13)              RESTORE RETURN ADDRESS
         AIF   ('&REST01' EQ 'NO').REST2
         LM    R0,R12,20(R13)           RESTORE REGS 0 - 12
         AGO   .SETFLAG
.REST2   LM    R2,R12,28(R13)           RESTORE REGS 2 - 12
.SETFLAG MVI   12(R13),X'FF'            SET RETURNED FLAG
.*-------TEST FOR SPM RESET
         AIF   ('&SPM' EQ 'NO').NOSPM
         SPM   R14                      RESTORE PROGRAM MASK
.NOSPM   BR    R14                      RETURN
         MEND
./ ADD NAME=#DIE     0100-84209-84209-1801-00142-00142-00000-KK00150
         MACRO
&NME     #DIE  &O,&A,&B,&C,&TYPE=MSG
.*
.*
.*
.* LAST CHANGE DATE - APRIL 21, 1981
.*                  - MACRO NAME CHANGED FROM $DIE TO #DIE.
.*
.* LAST CHANGE DATE - JAUNARY 27, 1981
.*                  - THE MACRO HAS BEEN REWRITTEN TO
.*                    GENERATE INLINE TRAPS WHENEVER
.*                    EXTENDED MNEMONICS FOR THE 'BC' AND
.*                    'BCR' INSTRUCTIONS ARE USED. THIS IS
.*                    ACCOMPLISHED BY USING THE INVERSE OF
.*                    OF THE GIVEN BRANCH CONDITION TO
.*                    SKIP AROUND THE INLINE TRAP.
.*                    IN THOSE CASES WHERE AN INLINE TRAP
.*                    IS NOT DESIRED (SUCH AS WITHIN A
.*                    BRANCH VECTOR TABLE), THE
.*                    NON-EXTENDED FORM OF THE 'BC'
.*                    INSTRUCTION CAN BE USED; E.G.:
.*                         #DIE C,8   INSTEAD OF   #DIE Z
.*                  - A UNIQUE NUMBER IS NOW APPENDED TO
.*                    GIVEN MESSAGES.
.*
.* LAST CHANGE DATE - FEBRUARY 12, 1980
.*                  - TYPE=CMD SUPPORT HAS BEEN ADDED. THIS PROVIDES A
.*                    MEANS OF COMMUNICATING COMMANDS TO THE "DEBUGGING
.*                    CONTROLLER" PROGRAM.
.*                  - IF NO BRANCH CONDITIONALS ARE GIVEN, THEN THE
.*                    "DEAD" CODE IS NOW GENERATED INLINE. BEFORE, AN
.*                    UNCONDITIONAL BRANCH TO A "DEAD" LITTERAL WAS
.*                    MADE.
.*
.* LAST CHANGE DATE - APRIL 24, 1978
.*                  - THE MACRO HAS BEEN REWRITTED TO GENERATE MESSAGE
.*                    STRINGS (INSTEAD OF JUST A SEQUENCE NUMBER) AT
.*                    THE BRANCH ADDRESS. SUCH MESSAGES COULD BE
.*                    REASONABLY DISPLAYED VIA AN ESTAE ROUTINE.
.*
.* LAST CHANGE DATE - DECEMBER 16, 1977
.*                  - 2ND AND 3RD OPERANDS HAVE BEEN ADDED TO THE
.*                    MACRO PROTOTYPE SO AS TO SUPPORT THE GENERATION
.*                    OF BRANCH INSTRUCTIONS OTHER THAN JUST
.*                    CONDITIONAL-BRANCH.
.*                  - WHEN NO OPERANDS ARE GIVEN, AN UNCONDITIONAL
.*                    BRANCH IS NOW GENERATED.
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MAILING ADDRESS CHANGE
.*
.* LAST CHANGE DATE - SEPTEMBER 10, 1976
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE.  ANY QUESTIONS CONCERNING
.* IT MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
.*
         GBLA  &#DIECOD
         GBLC  &#EBCDIC(256)
         LCLA  &L,&M
         LCLC  &MSG,&LAB,&OP,&N,&#
         AIF   ('&TYPE' EQ 'MSG' OR '&TYPE' EQ 'CMD').TYPEOK
         MNOTE 8,'TYPE=&TYPE NOT RECOGNIZED. TYPE=CMD ASSUMED.'
.TYPEOK  ANOP
&N       SETC  '&NME'
&#DIECOD SETA  &#DIECOD+1
&#       SETC  '000&#DIECOD'
&#       SETC  '&#'(K'&#-3,4)
&M       SETA  N'&SYSLIST+1
.MLP     ANOP
&M       SETA  &M-1
         AIF   (&M EQ 0).NOMSG
         AIF   (K'&SYSLIST(&M) EQ 0).MLP
         AIF   ('&SYSLIST(&M)' EQ '''''').NOMSG
&L       SETA  K'&SYSLIST(&M)-2
         AIF   (&L LE 0).NOMSG2
         AIF   ('&SYSLIST(&M)'(1,1) NE '''').NOMSG2
&MSG     SETC  '&SYSLIST(&M)'(2,&L)
         AIF   ('&TYPE' NE 'MSG').MSGDONE
&MSG     SETC  '&#DIECOD &MSG'
&L       SETA  &L+1+K'&#DIECOD
         AGO   .COMN
.*
.NOMSG2  ANOP
&M       SETA  &M+1
.NOMSG   AIF   ('&TYPE' NE 'MSG').MSGDONE
&MSG     SETC  '&#DIECOD'
&L       SETA  K'&MSG
.*
.COMN    AIF   (&L/2*2 EQ &L).MSGDONE
&L       SETA  &L+1
&MSG     SETC  '&MSG '
.MSGDONE ANOP
.*
         AIF   (&M LE 1 OR '&O' EQ '').INLINE
         AIF   (&M EQ 2).INVERT
         AIF   (&M NE 3).GENLIT
         AIF   ('&O'(K'&O,1) NE 'R').GENLIT
.*
.INVERT  ANOP
&OP      SETC  'BN&O'
         AIF   ('&O'(1,1) NE 'N').NVRTDON
&OP      SETC  'B'.'&O'(2,K'&O-1)
.NVRTDON ANOP
.*
         AIF   (&M EQ 2).SETLAB
&N       &OP   &A                  BRANCH IF OK
&N       SETC  ''
         AGO   .INLINE
.SETLAB  ANOP
&LAB     SETC  'DIE&#.Z'
&N       &OP   &LAB                SKIP IF OK
&N       SETC  ''
.INLINE  ANOP
&N       DC    X'00DEAD'           TRAP CODE
         AIF   ('&TYPE' EQ 'MSG').INLMSG
         DC    X'0100'             DBC COMMAND SIGNAL
         DC    C'&MSG'
         DC    X'00'               DBC COMMAND TERMINATOR
         AGO   .TRAIL
.INLMSG  DC    AL1(&L)             MESSAGE LENGTH
         DC    C'&MSG'
.TRAIL   AIF   ('&LAB' EQ '').MEND
&LAB     DS    0H                  RECEIVE SKIP AROUND TRAP
         AGO   .MEND
.*
.GENLIT  AIF   ('&TYPE' EQ 'MSG').LITMSG
&MSG     SETC  '
         AGO   .GOTLIT
.LITMSG  #TEST GEN=EBCDIC
&MSG     SETC  '
.GOTLIT  ANOP
.*
         AIF   (&M NE 3).OP4
&N       B&O   &A,=C'&MSG'
         AGO   .MEND
.OP4     ANOP
&N       B&O   &A,&B,=C'&MSG'
.MEND    MEND
./ ADD NAME=#TSOMSG  0100-87083-87083-1609-00070-00070-00000-GS00150
         MACRO
&NAME    #TSOMSG &MSG,&OFFSET=0,&LENGTH=
         MNOTE *,'       #TSOMSG   VERSION 001 09/22/75  09/22/75  GPW'
.**********************************************************************
.*                                                                    *
.* #TSOMSG                                                            *
.*                                                                    *
.* FUNCTION        GENERATE A MESSAGE LINE IN PUTLINE FORMAT WITH     *
.*                 LENGTH AND OFFSET HEADERS.                         *
.*                                                                    *
.* DESCRIPTION     THE USER SPECIFIES A MESSAGE TEST ENCLOSED IN      *
.*                 SINGLE QUOTES.  AN OFFSET MAY ALSO BE SPECIFIED.   *
.*                 A MESSAGE IN PUTLINE FORMAT (WITH HALFWORD HEADERS *
.*                 CONTAINING THE TOTAL LENGTH OF HEADERS AND         *
.*                 MESSAGE AND OFFSET) IS CREATED.  THE LENGTH OF     *
.*                 THE MESSAGE IS ROUNDED UP TO THE NEXT FULLWORD     *
.*                 MULTIPLE.  THE USER MAY OPTIONALLY SPECIFY THE     *
.*                 LENGTH OF THE MESSAGE TEXT.                        *
.*                                                                    *
.* SYNTAX          NAME     #TSOMSG 'MESSAGE-TEXT'                    *
.*                                  OFFSET=N                          *
.*                                  LENGTH=N                          *
.*                                                                    *
.*                 MESSAGE-TEXT IS THE MESSAGE TO BE GENERATED.       *
.*                                                                    *
.*                 OFFSET       SPECIFIES THE VALUE OF THE OFFSET     *
.*                              HEADER HALFWORD.  DEFAULT IS 0.       *
.*                                                                    *
.*                 LENGTH       SPECIFIES THE LENGTH IN BYTES OF      *
.*                              THE MESSAGE TEXT.  THIS LENGTH DOES   *
.*                              NOT INCLUDE THE HEADER LENGTHS.  IF   *
.*                              LENGTH IS NOT SPECIFIED, THE LENGTH   *
.*                              WILL BE THE LENGTH OF THE MESSAGE     *
.*                              TEXT ROUNDED TO THE NEXT FULLWORD.    *
.*                                                                    *
.* ERRORS          NO ERROR MESSAGES ARE DISPLAYED.                   *
.*                                                                    *
.* EXAMPLE         GENERATE A TSO MESSAGE.  LENGTH IS NOT SPECIFIED,  *
.*                 AND OFFSET IS 0.                                   *
.*                                                                    *
.*                 ERROR1   #TSOMSG 'NAME NOT FOUND'                  *
.*                                                                    *
.*                 GENERATE A DUMMY MESSAGE.  THE MESSAGE TEXT WILL   *
.*                 BE FILLED IN BY THE PROGRAM.  THE LENGTH OF THE    *
.*                 MESSAGE MAY BE UP TO 120 CHARACTERS.               *
.*                                                                    *
.*                 OUTLINE  #TSOMSG ' ',LENGTH=120                    *
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                 NONE                                               *
.*                                                                    *
.* MACROS CALLED                                                      *
.*                                                                    *
.*                 NONE                                               *
.*                                                                    *
.**********************************************************************
.*
         LCLA  &LEN,&LEN2
.*
         AIF   ('&LENGTH' EQ '').CALCLEN
&LEN     SETA  &LENGTH
         AGO   .GETLEN2
.CALCLEN ANOP
&LEN     SETA  K'&MSG-2
&LEN     SETA  ((&LEN-1)/4+1)*4
.GETLEN2 ANOP
&LEN2    SETA  &LEN+4
&NAME    DC    H'&LEN2,&OFFSET',CL&LEN.&MSG
         MEND
./ ADD NAME=BITSW                                  17OCT89 12.47.53
         MACRO
&LABEL   BITSW   &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7,&DSECT
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT0' EQ '').A
&BIT0    EQU   *  .          BIT 0.
&BIT0&#  EQU   128  .        1... ....      BIT POSITION
.A       AIF   ('&BIT1' EQ '').B
&BIT1    EQU   *  .          BIT 1.
&BIT1&#  EQU   64  .         .1.. ....      BIT POSITION
.B       AIF   ('&BIT2' EQ '').C
&BIT2    EQU   *  .          BIT 2.
&BIT2&#  EQU   32  .         ..1. ....      BIT POSITION
.C       AIF   ('&BIT3' EQ '').D
&BIT3    EQU   *  .          BIT 3.
&BIT3&#  EQU   16  .         ...1 ....      BIT POSITION
.D       AIF   ('&BIT4' EQ '').E
&BIT4    EQU   *  .          BIT 4.
&BIT4&#  EQU   8  .          .... 1...      BIT POSITION
.E       AIF   ('&BIT5' EQ '').F
&BIT5    EQU   *  .          BIT 5.
&BIT5&#  EQU   4  .          .... .1..      BIT POSITION
.F       AIF   ('&BIT6' EQ '').G
&BIT6    EQU   *  .          BIT 6.
&BIT6&#  EQU   2  .          .... ..1.      BIT POSITION
.G       AIF   ('&BIT7' EQ '').H
&BIT7    EQU   *  .          BIT 7.
&BIT7&#  EQU   1  .          .... ...1      BIT POSITION
.H       ANOP
         AIF   ('&DSECT' EQ '').I
&LABEL   DS    XL1  .        BIT BYTE.
         SPACE
         MEXIT
.I       ANOP
&LABEL   DC    XL1'0' .      BIT BYTE.
         SPACE
         MEND
./ ADD NAME=BPPL     0106-80178-82298-1145-00032-00049-00011-KK00150
         MACRO
&LAB1    BPPL   &DSECT
         LCLC  &LAB2
*
*               PARAMETER LIST PASSED TO OPERSCAN SUBROUTINE :
*
         AIF   (T'&LAB1 NE 'O').LOK
&LAB2    SETC  'BPPL'
         AGO   .DSCK
.LOK     ANOP
&LAB2    SETC  '&LAB1'
.DSCK    AIF   ('&DSECT' EQ 'DSECT').DSL
         DS    0A
&LAB2    DS    0XL44
         AGO   .ADSL
.DSL     ANOP
&LAB2    DSECT
.ADSL    ANOP
BUFFPTR  DS    F                        BUFFER PTR (CBUF)
LENPTR   DS    F                        LENGTH PTR (CBUF LEN)
STARTPTR DS    F                        START SEARCH PTR
OPERPTR  DS    F                        NEXT OPER LOC
OPLENPTR DS    F                        NEXT OPER LEN PTR
SUBPTR   DS    F                        SUBFIELD PTR (PARENS INCLUDED)
SUBLENPT DS    F                        SUBFIELD LEN PTR
WORKPTR  DS    F                        WORK PTR (OPTIONAL 350 BYTES)
OPDESCP  DS    F                        PTR TO OPERAND DESCRIPTOR
         DS    F                        RESERVED (ZERO)
*
OPLEN    DS    H                        POINTED TO BY OPLENPTR
SUBLEN   DS    H                        POINTED TO BY SUBLENPT
         MEND
./ ADD NAME=BRKELEM                                17OCT89 12.47.53
         MACRO
         BRKELEM
***********************************************************************
*                                                                     *
*        MAPPING DSECT FOR BREAK ELEMENTS AND ASSOCIATED FLAGS.       *
*                                                                     *
*        STATUS -- VERSION NO. 01 -  RELEASE NO. 01 - OS/VS2          *
*                                                                     *
*        CHANGE LEVEL -- 00 - DATE 03/09/72                           *
*                                                                     *
***********************************************************************
BRKELEM  DSECT
BRKLINK  DS    F -                POINTER TO NEXT BREAK ELEMENT.
BRKADDR  DS    F -                PROBLEM PROGRAM INSTRUCTION ADDRESS.
BRKINST  DS    2F -               ORIGINAL INSTRUCTION AND 2 BYTE SVC.
BRKFLGS  DS    1X -               ONE BYTE FOR FLAGS.
BALSW    EQU   B'10000000' -      BAL OR BALR IN ORIGINAL INSTRUCTION.
BRKRANGE EQU   B'01000000' -      THIS BREAK ELEMENT IS ONE OF A RANGE.
BRKLIST  EQU   B'00100000' -      THIS BREAK ELEMENT IS ONE OF A LIST.
BRKNONOT EQU   B'00010000' -      USER IS NOT TO BE NOTIFIED IF THIS
*                                 BREAKPOINT IS ENCOUNTERED.
         DS    X -                RESERVED.
BRKDISP  DS    H -                DISPLACEMENT FROM FIRST ADDRESS OF
*                                 A RANGE.
BRKNAME  DS    F -                POINTER TO THE ADDRESS STRING.
BRKCHAIN DS    F -                POINTER TO THE SUB-COMMAND CHAIN.
BRKCOUNT DS    F -                COUNT INFORMATION.
BRKRB    DS    F -                POINTER TO PROBLEM PROGRAM RB.
         DS    0D -               FORCE LENGTH EQUATE TO DOUBLE WORD.
BRKLEN   EQU   *-BRKELEM -        BREAK ELEMENT LENGTH.
         MEND
./ ADD NAME=CANDO    0103-83139-83139-1629-00022-00020-00005-KK00150
         MACRO
&L1      CANDO
         LCLA  &I,&J,&K,&L
         LCLC  &O(3)
&O(1)    SETC  'PAL'
&O(2)    SETC  'DIALOG'
&O(3)    SETC  'NOPARM'
.L1      ANOP
&I       SETA  &I+1
         AIF   (&I GT 3).MEX
&K       SETA  2*&K
&J       SETA  0
.L2      ANOP
&J       SETA  &J+1
         AIF   (&J GT N'&SYSLIST).L1
         AIF   ('&SYSLIST(&J)' NE '&O(&I)').L2
&K       SETA  &K+1
         AGO   .L1
.MEX     ANOP
&L       SETA  K'&L1
         DC    AL2(&L),AL2(&K),CL8'&L1'
.X       MEND
./ ADD NAME=CCALL    0101-89087-89096-1622-00010-00011-00001-KX00155
         MACRO
&L1      CCALL &ENTRY
         AIF   ('&SYSECT' EQ 'C$START').ENTRY
         DC    V(&ENTRY)
         MEXIT
.ENTRY   ANOP
&ENTRY   B     COMMON-*(15)
         ENTRY &ENTRY
         SPACE
         MEND
./ ADD NAME=CCALLS   0103-89087-89096-1622-00133-00129-00007-KX00155
* THE CALL VERSION MUST BE CHANGED IF THIS TABLE CHANGES;
* UNLESS NEW STUFF IS ADDED TO THE END TO MAINTAIN COMPATIBILITY
CALLVERS DC   CL4'V1M0'
CALLTAB  DS   0F
* THE CCALL MACRO IS IN WDPSC.MACLIB(CCALL)
         CCALL    @FLSTAT
         CCALL    ABORT
         CCALL    ABS
         CCALL    ACOS
         CCALL    ASCTIME
         CCALL    ASIN
         CCALL    ATAN
         CCALL    ATAN2
         CCALL    ATOF
         CCALL    ATOI
         CCALL    ATOL
         CCALL    CALLOC
         CCALL    CEIL
         CCALL    CLEARERR
         CCALL    CLOCK
         CCALL    COS
         CCALL    COSH
         CCALL    CTIME
         CCALL    DIFFTIME
         CCALL    ERF
         CCALL    ERFC
         CCALL    EXIT
         CCALL    EXP
         CCALL    FABS
         CCALL    FCLOSE
         CCALL    FEOF
         CCALL    FERROR
         CCALL    FFLUSH
         CCALL    FGETC
         CCALL    FGETS
         CCALL    FLOOR
         CCALL    FMOD
         CCALL    FOPEN
         CCALL    FPRINTF
         CCALL    FPUTC
         CCALL    FPUTS
         CCALL    FREAD
         CCALL    FREE
         CCALL    FREOPEN
         CCALL    FREXP
         CCALL    FSCANF
         CCALL    FSEEK
         CCALL    FTELL
         CCALL    FWRITE
         CCALL    GAMMA
         CCALL    GETC
         CCALL    GETCHAR
         CCALL    GETENV
         CCALL    GETS
         CCALL    GMTIME
         CCALL    HYPOT
         CCALL    ISALNUM
         CCALL    ISALPHA
         CCALL    ISCNTRL
         CCALL    ISDIGIT
         CCALL    ISGRAPH
         CCALL    ISLOWER
         CCALL    ISPRINT
         CCALL    ISPUNCT
         CCALL    ISSPACE
         CCALL    ISUPPER
         CCALL    ISXDIGIT
         CCALL    JN
         CCALL    J0
         CCALL    J1
         CCALL    KILL
         CCALL    LDEXP
         CCALL    LOCALTIM
         CCALL    LOG
         CCALL    LOG10
         CCALL    MALLOC
         CCALL    MEMCHR
         CCALL    MEMCMP
         CCALL    MEMCPY
         CCALL    MEMSET
         CCALL    MODF
         CCALL    ONEXIT
         CCALL    PERROR
         CCALL    POW
         CCALL    PRINTF
         CCALL    PUTC
         CCALL    PUTCHAR
         CCALL    PUTS
         CCALL    RAND
         CCALL    REALLOC
         CCALL    REMOVE
         CCALL    RENAME
         CCALL    REWIND
         CCALL    SCANF
         CCALL    SETBUF
         CCALL    SETVBUF
         CCALL    SIGNAL
         CCALL    SIN
         CCALL    SINH
         CCALL    SPRINTF
         CCALL    SQRT
         CCALL    SRAND
         CCALL    SSCANF
         CCALL    STRCAT
         CCALL    STRCHR
         CCALL    STRCMP
         CCALL    STRCPY
         CCALL    STRCSPN
         CCALL    STRLEN
         CCALL    STRNCAT
         CCALL    STRNCMP
         CCALL    STRNCPY
         CCALL    STRPBRK
         CCALL    STRRCHR
         CCALL    STRSPN
         CCALL    STRTOD
         CCALL    STRTOK
         CCALL    STRTOL
         CCALL    SYSTEM
         CCALL    TAN
         CCALL    TANH
         CCALL    TIME
         CCALL    TMPFILE
         CCALL    TMPNAM
         CCALL    TOLOWER
         CCALL    TOUPPER
         CCALL    UNGETC
         CCALL    VFPRINTF
         CCALL    VPRINTF
         CCALL    VSPRINTF
         CCALL    YN
         CCALL    Y0
         CCALL    Y1
./ ADD NAME=CMACROS  0101-89061-89061-1855-00041-00041-00000-KX00155
#define ENDASM    /* no-op */
#define endasm    /* no-op */
#define startasm  STARTASM
#pragma linkage(STARTASM,OS)
#define R0  regs.r0
#define R1  regs.r1
#define R2  regs.r2
#define R3  regs.r3
#define R4  regs.r4
#define R5  regs.r5
#define R6  regs.r6
#define R7  regs.r7
#define R8  regs.r8
#define R9  regs.r9
#define R10 regs.r10
#define R11 regs.r11
#define R12 regs.r12
#define R13 regs.r13
#define R14 regs.r14
#define R15 regs.r15
#define  SVC(svcno)  SVCCALL((svcno+2560),&regs)
#define  ADDR(item)  ((int)&##item)
struct regset {
         int r0 ;
         int r1 ;
         int r2 ;
         int r3 ;
         int r4 ;
         int r5 ;
         int r6 ;
         int r7 ;
         int r8 ;
         int r9 ;
         int r10 ;
         int r11 ;
         int r12 ;
         int r13 ;
         int r14 ;
         int r15 ; } ;
struct regset regs;
extern int SVCCALL(int s, struct regset *x);
./ ADD NAME=CMAC2                                  17OCT89 12.47.53
#define ENDASM    ENDASM: ;
#define STARTASM(x) STARTASM(x); goto ENDASM;
#pragma linkage(STARTASM,OS)
#define R0  regs.r0
#define R1  regs.r1
#define R2  regs.r2
#define R3  regs.r3
#define R4  regs.r4
#define R5  regs.r5
#define R6  regs.r6
#define R7  regs.r7
#define R8  regs.r8
#define R9  regs.r9
#define R10 regs.r10
#define R11 regs.r11
#define R12 regs.r12
#define R13 regs.r13
#define R14 regs.r14
#define R15 regs.r15
#define  SVC(svcno)  SVCCALL((svcno+2560),&regs)
#define  ADDR(item)  ((int)&##item)
struct regset {
         int r0 ;
         int r1 ;
         int r2 ;
         int r3 ;
         int r4 ;
         int r5 ;
         int r6 ;
         int r7 ;
         int r8 ;
         int r9 ;
         int r10 ;
         int r11 ;
         int r12 ;
         int r13 ;
         int r14 ;
         int r15 ; } ;
struct regset regs;
extern int SVCCALL(int s, struct regset *x);
./ ADD NAME=CMAC3    0105-88097-89061-1813-00040-00036-00000-KX00155
#define ENDASM    ENDASM:
#define STARTASM(x) STARTASM(x)
#pragma linkage(STARTASM,OS)
#define R0  regs.r0
#define R1  regs.r1
#define R2  regs.r2
#define R3  regs.r3
#define R4  regs.r4
#define R5  regs.r5
#define R6  regs.r6
#define R7  regs.r7
#define R8  regs.r8
#define R9  regs.r9
#define R10 regs.r10
#define R11 regs.r11
#define R12 regs.r12
#define R13 regs.r13
#define R14 regs.r14
#define R15 regs.r15
#define  SVC(svcno)  SVCCALL((svcno+2560),&regs)
#define  ADDR(item)  ((int)&##item)
struct regset {
         int r0 ;
         int r1 ;
         int r2 ;
         int r3 ;
         int r4 ;
         int r5 ;
         int r6 ;
         int r7 ;
         int r8 ;
         int r9 ;
         int r10 ;
         int r11 ;
         int r12 ;
         int r13 ;
         int r14 ;
         int r15 ; } ;
struct regset regs;
extern int SVCCALL(int s, struct regset *x);
./ ADD NAME=C370WORK 0100-89087-89087-1524-00008-00008-00000-KX00155
WORKAREA DSECT
SAVEAREA DS    18F
REALPARM DS    A
TEMPPARM DS    A
C370LPTR DS    A
CMAINROK DS    A
FLAG1    DS    H
WORKLEN  EQU   *-WORKAREA
./ ADD NAME=DATALIST 0105-85330-85331-1133-00026-00279-00000-LL00150
        MACRO
        DATALIST
DATALIST DSECT
*    SOURCE FOR DATALIST FOUND IN PANSPACE SECEXIT REL 11.D
*  CHANGED TO MATCH THE FORMAT USED BY SKK WHEN INTERFACE WRITTEN
*       USED BY THE ACF2/PANVALET INTERFACE
*
*        DATA LIST BLOCK
*
DLCOUNT  DS    F             DATA LIST COUNT -- THE NUMBER OF DATA
*                            LIST ENTRIES (0 TO N) FOLLOWING:
DLFIRST  DS    0X            FIRST DATA LIST ENTRY
*
*        DATA LIST ENTRY
*
DLENTRY  DS    0XL16         DATA LIST ENTRY:
DLEID    DS    CL8           DATA ID -- THE NAME OF THE DATA ITEM
*                            (E.G. ELEMFILE, JOBNAME ETC...)
DLESTAT  DS    CL1           DATA ITEM STATUS -- VALUES ARE:
*ACTIVE   EQU   C'A'         'A' - ACTIVE
*INACTIVE EQU   C'I'         'I' - INACTIVE
*        DS    CL3           RESERVED
DLEITEM  DS    A             DATA ITEM ADDRESS -- THE ADDRESS OF THE
*                            ITEM
DLENEXT  DS    0X            BEGINNING OF NEXT ENTRY
        MEND
./ ADD NAME=DATALSTP 0100-85331-85331-1129-00025-00025-00000-LL00150
        MACRO
        DATALIST
DATALIST DSECT
*    SOURCE FOR DATALIST FOUND IN PANSPACE SECEXIT REL 11.D
*       USED BY THE ACF2/PANVALET INTERFACE
*
*        DATA LIST BLOCK
*
DLCOUNT  DS    F             DATA LIST COUNT -- THE NUMBER OF DATA
*                            LIST ENTRIES (0 TO N) FOLLOWING:
DLFIRST  DS    0X            FIRST DATA LIST ENTRY
*
*        DATA LIST ENTRY
*
DLENTRY  DSECT               DATA LIST ENTRY:
DLEID    DS    CL8           DATA ID -- THE NAME OF THE DATA ITEM
*                            (E.G. ELEMFILE, JOBNAME ETC...)
DLESTAT  DS    CL1           DATA ITEM STATUS -- VALUES ARE:
ACTIVE   EQU   C'A'          'A' - ACTIVE
INACTIVE EQU   C'I'          'I' - INACTIVE
*        DS    CL3           RESERVED
DLEITEM  DS    A             DATA ITEM ADDRESS -- THE ADDRESS OF THE
*                            ITEM
DLENEXT  DS    0X            BEGINNING OF NEXT ENTRY
        MEND
./ ADD NAME=DDNTU    0115-81153-87316-1700-00032-00018-00025-KK00155
         MACRO
&L1      DDNTU  &N,&NLP,&MF=L
         LCLC  &TUN
         LCLA  &NL
         AIF   (T'&NLP EQ 'O').O
         AIF   (T'&NLP EQ 'N').N
&NL      SETA  L'&NLP
         AGO   .NO
.N       ANOP
&NL      SETA  &NLP
         AGO   .NO
.O       ANOP
&NL      SETA  K'&N
.NO      AIF   ('&MF' NE 'L').ET
&L1      DC    X'00010001',H'&NL',CL&NL'&N'
         AGO   .X
.ET      AIF   (N'&MF LE 1).X
         AIF   ('&MF(1)' NE 'E').X
&TUN     SETC  '&MF(2)'
&L1      MVC   &TUN.(4),=X'00010001'
         AIF   (T'&NLP EQ 'N').NH
         AIF   (T'&NLP EQ 'O').NH
         MVC   &TUN+4(2),&NLP
         LH    R1,&NLP
         BCTR  R1,R0
M&SYSNDX MVC   &TUN+6(0),&N
         EX    R1,M&SYSNDX
         AGO   .X
.NH      ANOP
         MVC   &TUN+4(2),=H'&NL'
         MVC   &TUN+6(&NL),=C'&N'
.X       MEND
./ ADD NAME=DSNTU    0111-81153-87316-1659-00031-00020-00018-KK00155
         MACRO
&L1      DSNTU  &N,&NLP,&MF=L
         LCLA  &NL
         AIF   (T'&NLP EQ 'O').O
         AIF   (T'&NLP EQ 'N').N
&NL      SETA  L'&NLP
         AGO   .NO
.N       ANOP
&NL      SETA  &NLP
         AGO   .NO
.O       ANOP
&NL      SETA  K'&N
.NO      AIF   ('&MF' NE 'L').ET
&L1      DC    X'00020001',H'&NL',CL&NL'&N'
         AGO   .X
.ET      AIF   (N'&MF LE 1).X
         AIF   ('&MF(1)' NE 'E').X
&TUN     SETC  '&MF(2)'
&L1      MVC   &TUN.(4),=X'00020001'
         AIF   (T'&NLP EQ 'O').NH
         AIF   (T'&NLP EQ 'N').NH
         MVC   &TUN+4(2),&NLP
         LH    R1,&NLP
         BCTR  R1,R0
M&SYSNDX MVC   &TUN+6(0),&N
         EX    R1,M&SYSNDX
         AGO   .X
.NH      ANOP
         MVC   &TUN+4(2),=H'&NL'
         MVC   &TUN+6(&NL),=C'&N'
.X       MEND
./ ADD NAME=DSPTU    0105-81153-81153-1719-00020-00019-00010-KK00150
         MACRO
&L1      DSPTU  &N,&MF=L
         LCLC  &TUN,&HS
         AIF   ('&N' NE 'OLD').T2
&HS      SETC  '01'
.T2      AIF   ('&N' NE 'MOD').T4
&HS      SETC  '02'
.T4      AIF   ('&N' NE 'NEW').T8
&HS      SETC  '04'
.T8      AIF   ('&N' NE 'SHR').TO
&HS      SETC  '08'
.TO      AIF   ('&MF' NE 'L').ET
&L1      DC    X'00040001',X'0001',X'&HS'
         AGO   .X
.ET      AIF   (N'&MF LE 1).X
         AIF   ('&MF(1)' NE 'E').X
&TUN     SETC  '&MF(2)'
&L1      MVC   &TUN.(6),=X'000400010001'
         MVI   &TUN+6,X'&HS'
.X       MEND
./ ADD NAME=DYNABLK  0103-81153-81153-1714-00039-00039-00006-KK00150
         MACRO
&L1      DYNABLK &TN,&MF=L
         LCLA  &I,&J
         LCLC  &LP,&RLN
&I       SETA  &TN
&J       SETA  1
         AIF   ('&MF' EQ 'L').LT
         AIF   (N'&MF LE 1).X
         AIF   ('&MF(1)' NE 'E').X
&RLN     SETC  '&MF(2)'
&LP      SETC  '&RLN'(1,4)
&L1      LA    R1,&LP.RB
         ST    R1,&RLN
         MVI   &RLN,X'80'
         MVC   &LP.RB(8),=X'1401200000000000'
         XC    &LP.TPLA(12),&LP.TPLA
         LA    R1,&LP.TPL
         ST    R1,&LP.TPLA
.STP     AIF   (&J GT &I).SEF
         LA    R1,&LP.TU&J
         ST    R1,&LP.TP&J
&J       SETA  &J+1
         AGO   .STP
.SEF     ANOP
         MVI   &LP.TP&I,X'80'
         AGO   .X
.LT      ANOP
&LP      SETC  '&L1'(1,4)
         DS    0F
&L1      DC    X'80',AL3(&LP.RB)
&LP.RB   DC    X'14',X'01',X'2000'
         DC    X'0000',X'0000'
&LP.TPLA DC    A(&LP.TPL),2F'0'
&LP.TPL  DS    0H
.TPL     AIF   (&J GT &I).X
&LP.TP&J DC    A(&LP.TU&J)
&J       SETA  &J+1
         AGO   .TPL
.X       MEND
./ ADD NAME=EDT                                    17OCT89 12.47.53
*
* COMMON NAME   -   ELIGIBLE DEVICE TABLE
* MACRO         -   EDT
* DSECT NAMES   -   EDTHEAD,LOOKSEC,GENSEC,GPTRSEC,GRPSEC
* CREATED BY    -   SYSGEN
* POINTED TO BY -   JESCT
* SERIALIZATION -   NONE
* FUNCTION      -   ASSOCIATE DEVICES WITH UNIT NAMES
*
EDT      DSECT                         ELIGIBLE DEVICE TABLE
EDTHEAD  EQU   *                       EDT HEADER
LOOKPTR  DS    A                       ->LOOKUP SECTION
         ORG   EDT
LOOKSEC  EQU   *                       LOOKUP SECTION
LOOK#ENT DS    F                       # ENTRIES
LOOKLENT DS    F                       LENGTH OF AN ENTRY
LOOKUNIT DS    CL8                     UNIT NAME
LOOKVAL  DS    F                       LOOKUP VALUE
LOOKMPTR DS    A                       ->GROUP MASK ENTRY
LOOK#GEN DS    F                       # GENERICS
LOOKGPTR DS    A                       ->GENERIC SECTION
         ORG   EDT
GENSEC   EQU   *                       GENERIC SECTION
GENDEV   DS    XL4                     GENERIC DEVICE TYPE
GEN#GRP  DS    F                       # GROUPS
GENGPTR  DS    A                       ->FIRST GROUP POINTER
         ORG   EDT
GPTRSEC  EQU   *                       GROUP POINTER TABLE
GPTRGRP  DS    A                       ->FIRST GROUP ENTRY
         ORG   EDT
GRPSEC   EQU   *                       GROUP SECTION ENTRY
GRPID    DS    F                       GROUP ID
GRP#UCB  DS    F                       # UCBS
GRPOUCB  DS    A                       ->FIRST UCB ENTRY
./ ADD NAME=ENTER                                  17OCT89 12.47.53
         MACRO
&SUBR    ENTER &REGS,&BASES,&SAVE
         GBLC  &LV,&SP,&SAVED(2)
         LCLA  &K,&N
         LCLC  &AREA,&B(16),&SUBNAME,&S
&SAVED(1) SETC '&REGS(1)'
&SAVED(2) SETC '&REGS(2)'
&SUBNAME SETC  '&SUBR'
         AIF   ('&SUBNAME' NE '').P1
&SUBNAME SETC  'MAIN'
.P1      ANOP
&SUBNAME CSECT
         AIF   ('&REGS' EQ '').PA
         SAVE  &REGS,T,*
.PA      AIF   ('&BASES(1)' EQ '15' OR '&BASES' EQ '').PC
         AIF   ('&BASES(1)' EQ '13' AND '&SAVE' NE '').PC
         LR    &BASES(1),15
.PC      CNOP  0,4
&S       SETC  '&SUBNAME'
         AIF   (N'&SAVE EQ 2).P4
         AIF   ('&SAVE' EQ '').P3
&AREA    SETC  '&SAVE'
         AIF   ('&SAVE' NE '*').P2
&AREA    SETC  'SAVEAREA'
.P2      AIF   ('&SAVE' NE '+').PB
&AREA    SETC  'SAVE'.'&SYSNDX'
.PB      AIF   ('&BASES(1)' NE '13').P4
&S       SETC  '*'
         USING &SUBNAME,15
         AIF   ('&REGS' EQ '').PD
         ST    14,&AREA+4
         LA    14,&AREA
         ST    14,8(13)
         L     14,&AREA+4
         ST    13,&AREA+4
.PD      BAL   13,*+76
         DROP  15
         AGO   .P4
.P3      AIF   ('&BASES(1)' NE '13').P4
         MNOTE 8,'*** THE CONTENTS OF REG. 13 ARE LOST. NO SAVE AREA WAX
               S ESTABLISHED.'
.P4      AIF   ('&BASES(1)' NE '14' OR '&SAVE' EQ '').P5
         MNOTE 8,'*** MACRO RESTRICTION - REG. 14 MUST NOT BE USED AS TX
               HE FIRST BASE REGISTER IF A SAVE AREA IS USED.'
.P5      AIF   ('&BASES' EQ '').P9
&N       SETA  N'&BASES
.P6      ANOP
&K       SETA  &K+1
&B(&K)   SETC  ','.'&BASES(&K)'
         AIF   (N'&SAVE EQ 1).PE
         AIF   ('&BASES(&K)' NE '13').P7
         MNOTE 8,'*** REG. 13 MAY NOT BE USED AS A BASE REGISTER FOR REX
               ENTRANT CODE.'
         AGO   .P7
.PE      AIF   ('&BASES(&K+1)' NE '13' OR '&SAVE' EQ '').P7
         MNOTE 8,'*** WHEN USING A SAVE AREA, REG. 13 MAY NOT BE USED AX
               S A SECONDARY BASE REGISTER.'
.P7      AIF   ('&BASES(&K+1)' NE '').P6
         USING &S&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)&B(9)&B(10)&B(X
               11)&B(12)&B(13)&B(14)&B(15)&B(16)
&K       SETA  1
         AIF   ('&BASES(1)' NE '13' OR '&SAVE' EQ '').P8
&AREA    DC    18F'0'
.P8      AIF   (&K GE &N).P10
         LA    &BASES(&K+1),X'FFF'(&BASES(&K))
         LA    &BASES(&K+1),1(&BASES(&K+1))
&K       SETA  &K+1
         AGO   .P8
.P9      USING &SUBNAME,15
.P10     AIF   (N'&SAVE EQ 2).P13
         AIF   ('&SAVE' EQ '' OR '&BASES(1)' EQ '13').P12
         AIF   ('&REGS' EQ '').P11
         ST    14,&AREA+4
         LA    14,&AREA
         ST    14,8(13)
         L     14,&AREA+4
         ST    13,&AREA+4
.P11     BAL   13,*+76
&AREA    DC    18F'0'
.P12     MEXIT
.P13     ANOP
&LV      SETC  '&SAVE(2)'
&SP      SETC  '0'
         AIF   ('&SAVE(1)' EQ '').P14
&SP      SETC  '&SAVE(1)'
.P14     GETMAIN R,LV=&LV,SP=&SP
         ST    13,4(1)
         ST    1,8(13)
         LR    2,13
         LR    13,1
         LM    0,2,20(2)
         MEND
./ ADD NAME=ENTERNR  0101-77221-80274-1650-00016-00015-00003-KK00150
         MACRO
&LAB1    ENTERNR &SA=SAVEAREA,&LEVEL=
         MNOTE '        SA=&SA,LEVEL=&LEVEL'
&LAB1    START
         SAVE  (14,12),,&LAB1-&LEVEL
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         ST    R13,&SA.+4         SAVE HIS SAVEAREA PTR
         LR    R11,R13            SAVE THE SAVE POINTER
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         B     ENTEX
&SA      DS    18F
         REGEQU
ENTEX    DS    0H
         MEND
./ ADD NAME=ENTERR   0106-83188-88110-1819-00031-00031-00006-KK00155
         MACRO
&LAB1    ENTERR  &SA=SAVEAREA,&WA=WORKAREA,&WL=WORKLEN,&LEVEL=,&R=,   XX
               &CLEAR=YES
         MNOTE ' CLEAR=&CLEAR,SA=&SA,WA=&WA,WL=&WL,LEVEL=&LEVEL'
&LAB1    CSECT
         SAVE  (14,12),,&LAB1-&LEVEL
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         LR    R10,R1             SAVE PARM PTR R10->PARM PTR
         L     R0,=A(&WL)         R0=GET LENGTH
         GETMAIN R,LV=(0)         R1->WORKAREA
         LR    R11,R13            R11->CALLERS SAVEAREA
         LR    R13,R1             R13->WORKAREA
         USING &WA.,R13
         AIF   ('&CLEAR' NE 'YES').NCLEAR
         L     R15,=A(&WL)        R0=GET LENGTH
         S     R15,=F'72'         SKIP REGS
         MOVE  72(13),(15),0,0
.NCLEAR  ANOP
         ST    R11,&SA.+4         SAVE HIS SAVEAREA PTR
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         LR    R11,R1             R11->WORKAREA IN CASE NOT SAME AS R13
*                       WORKAREA ADDR IS STILL R13 FOR
*                       THE ASSEMBLER - IF DIFFERENT FROM R13
*                       THEN USE: DROP R13 AND USING &WA.,R11
         LR    R1,R10             RESTORE PARM PTR PTR
         AIF   ('&R' EQ 'NO').NRE
         REGEQU
.NRE     ANOP
         MEND
./ ADD NAME=ENTERWW  0102-82328-83068-1125-00035-00030-00006-KK00150
         MACRO
&LAB1    ENTERWW &WC,&SA=SAVEAREA,&WA=WORKAREA,&LEVEL=,&R=
         MNOTE '             SA=&SA,WA=&WA,LEVEL=&LEVEL'
&LAB1    CSECT
         SAVE  (14,12),,&LAB1-&LEVEL
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         LR    R10,R1             SAVE PARM PTR R10->PARM PTR
         LCLA  &I
&I       SETA  1
.WL      AIF   (&I GT N'&WC).GW
         AIF   (T'&WC(&I) NE 'N').ASIS
         L     R1,4*&WC(&I)-4(,R1)
         AGO   .NSC
.ASIS    ANOP
         L     R1,&WC(&I)(,R1)
.NSC     ANOP
&I       SETA  &I+1
         AGO   .WL
.GW      ANOP
         LR    R11,R13            R11->CALLERS SAVEAREA
         LR    R13,R1             R13->WORKAREA
         USING &WA.,R13
         ST    R11,&SA.+4         SAVE HIS SAVEAREA PTR
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         LR    R11,R1             R11->WORKAREA IN CASE NOT SAME AS R13
*                       WORKAREA ADDR IS STILL R13 FOR
*                       THE ASSEMBLER - IF DIFFERENT FROM R13
*                       THEN USE: DROP R13 AND USING &WA.,R11
         LR    R1,R10             RESTORE PARM PTR PTR
         AIF   ('&R' EQ 'NO').NRE
         REGEQU
.NRE     ANOP
         MEND
./ ADD NAME=ENVIRBLK 0104-85330-85330-1516-00021-00018-00000-DZ00150
         MACRO
         ENVIRBLK
ENVIRBLK DSECT
*     SOURCE FOR ENVIRBLK FOUND IN SECEXIT IN PANSPACE  REL 11.D
*         USED BY THE ACF2/PANVALET INTERFACE
*
*        ENVIRONMENT BLOCK
*
ENPRODID DS    CL10          PRODUCT NAME -- VALUE IS ALWAYS "PANVALET"
ENPRODOP DS    CL10          PRODUCT OPTION -- (E.G. TSO, ISPF)
ENPRODVR DS    CL4           PRODUCT VERSION -- THE RELEASE OF THE
*                            PRODUCT (I.E. PANVALET 11.0 = "1100")
ENOPSYS  DS    CL10          OPERATING SYSTEM -- THE OPERATING SYSTEM
*                            UNDER WHICH THE PRODUCT IS RUNNING
*                            ("OS/MVS" OR "OS/VS1" OR "OS/MVS/XA")
ENDCMON  DS    CL10          DC MONITOR
ENDBMGR  DS    CL10          DB MANAGER
*        DS    XL2           RESERVED
ENDCPARM DS    A             ADDRESS OF CICS ENVIRONMENT BLOCK OR 0
ENDBPARM DS    A             ADDRESS OF DB MANAGER CONTROL BLOCK OR 0
         MEND
./ ADD NAME=EPILOG                                 17OCT89 12.47.53
         MACRO
&NAME    EPILOG &REG,&RC=,&RENT=,&SP=0,&SREG=13,&GPR=R,&LIST=
.*
.*       GLOBAL VARIABLES DESCRIPTION
.*
.*       &REGSNM   CHARACTER STRING THAT CONTAINS THE EQUATE NAME
.*                    PREFIX FOR THE GENERAL REGISTERS (SEE &RN
.*                    VALUE DESCRIPTION BELOW)
.*
         GBLC  &REGSNM
         LCLA  &A
         LCLC  &R,&RN,&T,&X
*  EPILOG  MACRO -- ASSEMBLY EPILOG  01/14/78 MARK GOTO
         AIF   ('&LIST' EQ 'YES').COMNT
*           (CODE LIST=YES FOR FURTHER DESCRIPTION)
*
         AGO   .NOCOMNT
.COMNT   ANOP
*    &REG      REGISTER SPECIFICATION INDICATING WHICH REGISTERS
*              ARE TO BE RESTORED
*
*        *NOTE* = THE REGS. NOS. MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
*    ,&RC=     RETURN CODE VALUE
*                OR
*    ,&RC=     RETURN CODE LOCATION
*                OR
*    ,&RC=     (REGISTER CONTAINING RETURN CODE)
*
*        OPERATION = LOAD RETURN CODE INTO REGISTER 15 AND PASS IT
*                    TO CALLER UPON EXIT
*
*           LA 15,"RETURN CODE VALUE"
*                OR
*           L  15,"RETURN CODE LOC."
*                OR
*           LR 15,"REGISTER"
*
*        *NOTE* = THE REGISTER MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
*    ,&RENT=   (DSECT ADDR LOC.  ,  DSECT SIZE)
*                OR
*    ,&RENT=   (DSECT ADDR REG.  ,  DSECT SIZE)
*
*        OPERATION = ISSUE FREEMAIN MACRO AS FOLLOWS:
*
*           L  1,"DSECT ADDR LOC."
*           FREEMAIN R,LV="DSECT SIZE",A=(1)
*                OR
*           LR 1,"DSECT ADDR REG."
*           FREEMAIN R,LV="DSECT SIZE",A=(1)
*
*        *NOTE* = THE DSECT REG. MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
*    ,&SP=     SUB-POOL NUMBER FOR RE-ENTRANT FREEMAIN
*
*        OPERATION = SPECIFY THE FREEMAIN SUB-POOL OPERAND:
*
*          FREEMAIN R,LV=...,SP=...
*
*        *NOTE* = THIS PARAMETER DEFAULTS TO "SP=0" (IE. SUB-POOL
*                 NUMBER ZERO)
*
*
*    ,&SREG=   SAVE REGISTER
*
*        OPERATION = GENERATE INSTRUCTIONS TO GET OLD SAVE ADDR:
*
*           L  13,4(&SREG)
*
*        *NOTE* = THE DSECT REG. MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
*    ,&GPR=    PREFIX FOR EQU NAMES OF THE GENERAL REGISTERS
*
*        OPERATION = SPECIFY PREFIX WHEN NO GLOBAL PREFIX EXISTS
*
*
.NOCOMNT ANOP
&RN      SETC  '&REGSNM'
         AIF   ('&RN' NE '').RTYPE
&RN      SETC  '&GPR'
         AIF   ('&RN' NE '').RTYPE
&RN      SETC  'R'
.RTYPE   ANOP
&T       SETC  'O'
         AIF   ('&RC' EQ '').LABEL
&T       SETC  'Z'
         AIF   ('&RC' EQ '0').LABEL
&T       SETC  'R'
         AIF   ('&RC'(1,1) EQ '(').RCREG
&T       SETC  'N'
         AIF   (T'&RC EQ 'N').LABEL
&T       SETC  'A'
         AGO   .LABEL
.RCREG   ANOP
&R       SETC  '&RC'(2,K'&RC-2)
         AIF   ('&R'(1,K'&RN) NE '&RN').LABEL
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)
         AIF   ('&R' GE '0' AND '&R' LE '15').LABEL
         AGO   .E3
.LABEL   AIF   ('&NAME' EQ '').NOLABEL
&NAME    DS    0H                           DEFINE LABEL FOR RETURN
.NOLABEL AIF   ('&REG' EQ '').E1
         AIF   ('&T' NE 'A').RENT
         L     15,&RC                       LOAD RETURN CODE
&T       SETC  'R'
&R       SETC  '15'
.RENT    AIF   (N'&RENT NE 2).REGS
&X       SETC  '&RENT(1)'
         AIF   ('&X'(1,1) NE '(').RENTA
&X       SETC  '&X'(2,K'&X-2)
         AGO   .RENTB
.RENTA   AIF   ('&X'(1,K'&RN) NE '&RN').RENTB
&X       SETC  '&X'(K'&RN+1,K'&X-K'&RN)
         AIF   ('&X' GE '0' AND '&X' LE '15').RENTC
.RENTB   ANOP
&X       SETC  '13'
.RENTC   AIF   ('&X' EQ '1').RENTD
         LR    1,&X                         RESTORE DSECT ADDR
.RENTD   AIF   (T'&SREG EQ 'O').RENTE
         L     13,4(,&SREG)                 RESTORE OLD SAVE AREA ADDR
.RENTE   AIF   ('&T' NE 'R').RENTF
         ST    &R,16(,13)                   SAVE RETURN CODE
.RENTF   ANOP
         FREEMAIN R,LV=&RENT(2),A=(1),SP=&SP
         AGO   .REGSB
.REGS    AIF   (T'&SREG EQ 'O').REGSA
         L     13,4(,&SREG)                 RESTORE OLD SAVE AREA ADDR
.REGSA   AIF   ('&T' NE 'R').REGSB
         ST    &R,16(,13)                   SAVE RETURN CODE
.REGSB   ANOP
&X       SETC  '&REG(1)'
         AIF   (T'&REG(1) EQ 'N').REGSD
         AIF   ('&X'(1,K'&RN) NE '&RN').REGSC
&X       SETC  '&X'(K'&RN+1,K'&X-K'&RN)
         AIF   ('&X' GE '0' AND '&X' LE '15').REGSD
.REGSC   ANOP
&X       SETC  '14'
.REGSD   ANOP
&A       SETA  &X*4+20
         AIF   (&A LE 75).REGSE
&A       SETA  &A-64
.REGSE   AIF   (N'&REG NE 2).REGSF
         LM    &RN.&X,&REG(2),&A.(&RN.13)   RESTORE REGISTERS
         AGO   .RETC
.REGSF   AIF   (N'&REG NE 1).E2
         L     &RN.&X,&A.(,&RN.13)          RESTORE REGISTER
.RETC    AIF   ('&T' NE 'R' OR '&R' EQ '15').RETCA
         L     15,16(,13)                   PICK UP SAVED RETURN CODE
         AGO   .MEND
.RETCA   AIF   ('&T' NE 'N').RETCB
         LA    15,&RC                       SET RETURN CODE
         AGO   .MEND
.RETCB   AIF   ('&T' NE 'Z').MEND
         SLR   15,15                        SET RETURN CODE
.MEND    BR    14                           RETURN
         MEXIT
.E1      IHBERMAC 18,360                   .REG PARAMETER MISSING
         MEXIT
.E2      IHBERMAC 36,360,&REG              .INVALID REGS. SPECIFIED
         MEXIT
.E3      IHBERMAC 61,360,&RC               .INVALID RC SPECIFIED
         MEND
./ ADD NAME=EQUREGS  0100-84209-84209-1749-00036-00036-00000-KK00150
         MACRO
&LABEL   EQUREGS &L=R,&DO=(0,15,1),&SYM=
.*--> MACRO: EQUREGS    GENERATE SYMBOLIC REGISTER EQUATES  . . . . . .
.*                                 JOHN R. MASHEY/JULY'69/PSU 360/67  *
.*       MACRO FOR SETTING UP SETS OF REGISTER EQUATES.               *
.*       *** ARGUMENTS ***                                            *
.*       L=        SYMBOL USED TO BEGIN EQUATES, SUCH AS R, REG,ETC.  *
.*       DO=       (INITIAL,LIMIT,INCREMENT) WILL SET UP REGISTERS    *
.*             EQUATED TO THE VALUE AS CONTROLLED BY THE DO PARAMATER.*
.*             BEHAVES LIKE FORTRAN DO, INCLUDING ABILITY TO LEAVE OUT*
.*             INCREMENT.                                             *
.*       SYM=      LIST OF SYMBOLS TO BE CONCATENATED TO L PARM.      *
.*             LIST WILL SET UP EQUATES INCLUDING SYM VALUES, FOR     *
.*             FIRST SET OF EQUATES IN LIST, AND WILL THEN SET UP     *
.*             NUMERIC EQUATES IF DO VALUES EXCEED NUMBER OF ELEMENTS *
.*             IN SYM OPERAND.  MAY BE OMITTED ENTIRELY.              *
.*  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LCLA  &I,&J,&K            COUNTER,INCREMENT,SYM COUNTER
         AIF   (N'&DO LT 2).XERROR           NOT ENOUGH ARGUMENTS-ERR
&K       SETA  1                   INIT
&I       SETA  &DO(1)              SET TO INITIAL VALUE
&J       SETA  1                   SET TO DEFAULT VALUE
         AIF   (N'&DO LT 3).XLOOP            DEFAULT VALUE IS OK
&J       SETA  &DO(3)              USE VALUE PROVIDED
.XLOOP   AIF   ('&SYM(&K)' EQ '').XLOOP1     USE NUMBER IF NO SYM VAL
&L&SYM(&K) EQU &I
&K       SETA  &K+1      INCREMENT TO GET NEXT SYM OPERAND
         AGO   .XLOOP2             SKIP OVER NORMAL GENRATION
.XLOOP1  ANOP
&L&I     EQU   &I
.XLOOP2  ANOP
&I       SETA  &I+&J               ADD INCREMENT TO COUNTER
         AIF   (&I LE &DO(2)).XLOOP          CONTINUE UNTIL DONE
         MEXIT
.XERROR  MNOTE 0,'** ERROR - EQUREGS REQUIRES AT LEAST 2 VALUES IN DO'
         MEND
./ ADD NAME=EVENTBLK 0104-85330-85330-1516-00036-00032-00000-DZ00150
       MACRO
       EVENTBLK
EVENTBLK DSECT
*    SOURCE FOR EVENTBLK FOUND IN PANSPACE SECEXIT  PAN REL 11.D
*          USED BY THE ACF2/PANVALET INTERFACE
*
*        EVENT BLOCK
*
EVEVENT  DS    CL8           EVENT-ID -- THE NAME OF THE EVENT
*                            (E.G. INIT,$OPEN001,$MEM001,TERM ETC...)
EVEXTYPE DS    CL8           EXIT TYPE -- ANY USER EXIT IDENTIFIER
*                            (GIVEN ON THE EXIT TYPE= MACRO OR BLANKS)
EVRESPON DS    CL1           RESPONSE CODE -- INITIALLY 'C', SET BY
*                            USER EXIT TO:
CONTINUE EQU   C'C'          'C' - CONTINUE NORMAL PROCESSING
ABORT    EQU   C'A'          'A' - ABORT PANVALET PROCESSING
TERMINAT EQU   C'T'          'T' - TERMINATE THIS EXIT
*                                  (PANVALET WILL DISCONNECT THE EXIT
*                                  FROM ALL THE EVENTS FOR THE
*                                  DURATION OF THE RUNNING TASK ONLY
*                                  AND PROCEEDS AS IF 'C' HAD BEEN
*                                  SPECIFIED.  THIS INCLUDES THE TERM
*                                  EVENT.  AN EXIT RESPONDING WITH A
*                                  "T" SHOULD DO ALL CLEANUP REQUIRED
*                                  BEFORE RETURNING.  OTHER EXIT(S),
*                                  IF ANY, WILL CONTINUE TO BE CALLED
*                                  BY PANVALET.)
VERIFY   EQU   C'V'          'V' - ACTIVATE VERIFICATION
*                                  (VALID ONLY FROM THE INIT EVENT;
*                                  SEE THE INIT EVENT FOR DETAILS)
*        DS    CL3           RESERVED
EVUSER   DS    F             USER FULLWORD -- INITIALLY ZEROES; TO BE
*                            USED BY USER AS DESIRED (E.G. ADDRESS OF
*                            A WORK AREA ETC...); THIS FIELD IS UNIQUE
*                            TO EACH EXIT DEFINED BY A EXIT MACRO
        MEND
./ ADD NAME=EXITDEF                                17OCT89 12.47.53
*          DATA SET EXITDEF    AT LEVEL 001 AS OF 06/13/85
*          DATA SET EXITDEF    AT LEVEL 002 AS OF 03/03/83
         MACRO
&LABEL   EXITDEF &FUNC,&NAME=,&TYPE=,&EVENTS=
.*
.*-------------------------------------------------------------------
.*   EXIT DEFINITION FUNCTION:
.*
.*   AN EXIT IS DEFINED TO THE LCS EXIT INTERFACE IN THE USER PROFILE.
.*
.*   EACH EXITDEF MACRO GENERATES INFORMATION FOR AN ENTRY IN THE
.*   EXIT-EVENT TABLE.  THE ORDER OF THE EXITDEF MACROS DICTATES
.*   THE SEQUENCE IN WHICH A SET OF EXITS IS INVOKED, WHEN AN EVENT
.*   TRIGGERS MORE THAN ONE EXIT ROUTINE.
.*
.*   LIMITATIONS:
.*     THIS MACRO PLACES A LIMIT OF 4095 EXIT NAMES AND EVENT NAMES.
.*     BECAUSE IT USES SUBSCRIPTED GLOBALS TO KEEP TRACK OF THEM.
.*     4095 IS THE MAXIMUM SUBSCRIPT CURRENTLY ALLOWED IN DOS.
.*
.*   PARAMETERS:
.*
.*   FUNCTION         : OPTIONAL.
.*                    : ONLY 2 SPECIAL CASES HAVE FUNCTIONS.
.*      BEGIN         : BEGIN SIGNALS THE BEGINNING OF THE EXITDEF
.*                    :       MACROS.
.*      END           : END SIGNALS THE END OF THE EXITDEF MACROS.
.*                    : THE FIRST EXITDEF MACRO MUST BE A BEGIN
.*                    :       FUNCTION.
.*                    : THE LAST EXITDEF MACRO MUST BE AN END
.*                    :       FUNCTION.
.*                    : THERE ARE NO OTHER VALID PARAMETERS ON A
.*                    : EXITDEF MACRO WITH A FUNCTION.
.*
.*   NAME=SYMBOL      : REQUIRED.
.*                    : THE NAME OF THIS EXIT.
.*
.*   TYPE=SYMBOL      : OPTIONAL.
.*                    : THE TYPE OF EXIT.  EXIT TYPES ARE USER DEFINED.
.*
.*   EVENTS=(A,...)   : REQUIRED.
.*                    : A LIST OF ALL EVENTS AT WHICH THIS EXIT IS TO
.*                    : GET CONTROL.
.*                    : A,... ARE EVENT-IDS.
.*
.* AUTHOR:  KEITH STIDLEY
.*
.*-------------------------------------------------------------------
         GBLA  &GEXDCTR        NUMBER OF EXITDEFS
         GBLC  &GEXDEF         BEGIN/END. SIGNALS EXITDEFS IN PROGESS
         GBLC  &GEXITID(256)   EXITIDS DEFINED BY PREVIOUS EXITDEFS
.*
         LCLA  &GEXNDX         INDEX FOR SEARCH OF EXITID GLOBALS
         LCLA  &GEVNDX         INDEX FOR EVENTS ON THIS MACRO
         LCLA  &GXEVNDX        INDEX FOR SEARCH OF EVENTIDS IN &GXEVID
         LCLC  &GXEVID(256)    EVENTIDS FOR DUPLICATE CHECK
         LCLC  &GEXTYPE        EXIT TYPE
.*
.*
&GEXDCTR SETA  &GEXDCTR+1      INCREMENT COUNT OF EXITDEFS
&GEXTYPE SETC  '&TYPE'         SAVE EXIT TYPE
.*
.*
         AIF   ('&FUNC' EQ '').NOFUNC
.*
.*
         AIF   (N'&SYSLIST NE 1).INVPARM
         AIF   ('&EVENTS' NE '').INVPARM
         AIF   ('&NAME' NE '').INVPARM
         AIF   ('&TYPE' NE '').INVPARM
.*
.*
         AIF   ('&FUNC' EQ 'BEGIN').BEGIN
         AIF   ('&FUNC' EQ 'END').END
         MNOTE 12,'INVALID FUNCTION ON EXITDEF MACRO'
         MEXIT
.*
.*
.INVPARM ANOP
         MNOTE 12,'INVALID SYNTAX ON EXITDEF MACRO'
         MEXIT
.*
.************************************
.*       BEGIN FUNCTION             *
.************************************
.*
.BEGIN   ANOP
         AIF   ('&GEXDEF' EQ '').BEGIN2
         MNOTE 12,'THERE CAN BE NO EXITDEFS BEFORE EXITDEF BEGIN'
         MEXIT
.*
.*
.BEGIN2  ANOP
&GEXDEF SETC   'BEGIN'
.*
.*
&LABEL   DS    0A                          LABEL FOR EXITDEFS
GEMEXBEG DC    A(GEMEX1ST)                 BEGINNING OF EXITDEFS
         DC    A(GEMEXEND)                 END OF EXITDEFS
GEMEX1ST DS    0C                          1ST DATADEF
         AGO   .DONE
.*
.************************************
*        CSECT
*        DC    CL21'001EXITDEF   06/13/85'
.*       END FUNCTION               *
.************************************
.*
.END     ANOP
         AIF   ('&GEXDEF' EQ 'BEGIN').END2
         MNOTE 12,'EXITDEF END MUST BE PRECEDED BY EXITDEF BEGIN'
         MEXIT
.END2    ANOP
&GEXDEF SETC   'END'
GEMEXEND DS    0H                          END OF EXIT DEFS
         AGO   .DONE
.*
.*********************************************************
.*       NO FUNCTION, I.E., EXITDEF DESCRIBING AN EXIT   *
.*********************************************************
.*
.NOFUNC  ANOP
         AIF   ('&GEXDEF' EQ 'BEGIN').NOFUNC1
         MNOTE 12,'ALL EXITDEFS MUST BE BETWEEN EXITDEF BEGIN AND END'
         MEXIT
.*
.*
.NOFUNC1 ANOP
         AIF   ('&NAME' NE '').NOFUNC2
         MNOTE 12,'NAME IS A REQUIRED PARAMETER ON EXITDEF'
         MEXIT
.*
.*
.NOFUNC2 ANOP
         AIF   ('&EVENTS' NE '').NOFUNC3
         MNOTE 12,'EVENTS IS A REQUIRED PARAMETER ON EXITDEF'
         MEXIT
.*
.*
.NOFUNC3 ANOP
         AIF   ('&GEXTYPE' NE '').NOFUNC4
&GEXTYPE   SETC  ' '
.*
.*
.NOFUNC4 ANOP
         AIF   (K'&NAME LE 8).NOFUNC5
         MNOTE 12,'EXIT NAMES ARE 1 - 8 CHARACTERS'
         MEXIT
.NOFUNC5 ANOP
         AIF   (K'&TYPE LE 8).LOOP1
         MNOTE 12,'EXIT TYPES ARE 1 - 8 CHARACTERS'
         MEXIT
.*
.******************************************************************
.* THIS LOOP CHECKS FOR DUPLICATE EXITDEFS                        *
.* ALL VALID EXITIDS IN EXITDEFS ARE SAVED AS GLOBALS.            *
.* IF A MATCH OCCURS WHEN CHECKING THE GLOBALS IT IS A DUPLICATE. *
.******************************************************************
.*
.LOOP1   ANOP
&GEXNDX SETA   &GEXNDX+1
         AIF   (&GEXNDX GE &GEXDCTR).ENDLP1
         AIF   ('&NAME' NE '&GEXITID(&GEXNDX)').LOOP1
         MNOTE 12,'DUPLICATE EXIT NAME ON EXITDEF MACRO'
         MEXIT
.ENDLP1  ANOP
.*
.**********************************************************
.*       VERIFY EACH EVENT ON THE MACRO AND GENERATE DC'S *
.**********************************************************
.*
.LOOP2   ANOP
&GEVNDX  SETA  &GEVNDX+1
         AIF   (&GEVNDX GT N'&EVENTS).DONE
         AIF   (K'&EVENTS(&GEVNDX) EQ 0).EVNTNUL
         AIF   (K'&EVENTS(&GEVNDX) GT 8).EVNTBAD
         AGO   .EVNTOK
.EVNTNUL ANOP
         MNOTE 12,'NULL EVENT NAMES NOT VALID, MUST BE 1-8 CHARACTERS'
         MEXIT
.EVNTBAD ANOP
         MNOTE 12,'EVENT NAMES ARE 1 - 8 CHARACTERS'
         MEXIT
.*
.EVNTOK  ANOP
.*
.*
&GXEVNDX SETA  0
.*
.*****************************************************************
.*       VERIFY THAT THIS EVENT IS NOT ON THIS EXITDEF ALREADY   *
.*       THIS IS DONE BY CHECKING LOCALS SAVED FOR ALL PREVIOUS  *
.*       EVENTS ON THIS MACRO                                    *
.*****************************************************************
.*
.LOOP3   ANOP
&GXEVNDX SETA  &GXEVNDX+1
         AIF   (&GXEVNDX GE &GEVNDX).ENDLP3
         AIF   ('&EVENTS(&GEVNDX)' NE '&GXEVID(&GXEVNDX)').LOOP3
         MNOTE 12,'DUPLICATE EVENT NAME ON EXITDEF'
         MEXIT
.ENDLP3  ANOP
.*
.**************************************************
.* THIS IS FOR THE SPECIAL CASE OF EVENTS=()      *
.**************************************************
.*
         AIF   ('&EVENTS(&GEVNDX)'(1,1) NE '(').EVGO
         MNOTE 12,'NULL EVENT NAMES NOT VALID, MUST BE 1-8 CHARACTERS'
         MEXIT
.EVGO    ANOP
.*
.***************************************************
.*       SAVE EVENTID IN LOCAL TO BE ABLE TO MAKE  *
.*       SURE IT IS NOT ON THIS MACRO AGAIN.       *
.***************************************************
.*
&GXEVID(&GXEVNDX) SETC '&EVENTS(&GEVNDX)'
.*
.*
&LABEL   DC    CL8'&NAME'                EXIT NAME
         DC    CL8'&GEXTYPE'             EXITTYPE
         DC    CL8'&EVENTS(&GEVNDX)'     EVENT NAME
         AGO   .LOOP2
.*
.DONE    ANOP
.*
.***********************************
.*       SAVE EXITID IN GLOBAL     *
.***********************************
.*
&GEXITID(&GEXDCTR) SETC '&NAME'
.*
         MEND
./ ADD NAME=FORMAT                                 17OCT89 12.47.53
         MACRO
&LAB     FORMAT &STUFF,&TO=,&WKA=
.* MACRO 'FORMAT' WRITTEN MAY,1975 BY HOWARD GILBERT
.* FUNCTION -- GENERATE MESSAGE WITH INSERTS FOR OUTPUT
.* CODE GENERATED IS INLINE AND POTENTIALLY REENTRANT
.* REGISTER 0 IS USED BY THE EXPANSION OF THIS MACRO
.* THERE ARE A VARIABLE NUMBER OF POSITONAL OPERANDS, EACH A SUBLIST OF
.*     THREE ELEMENTS:
.*          1) THE DATA ELEMENT OR LITERAL. MUST BE A RELOCATABLE
.*             EXPRESSION OR CHARACTER STRING ENCLOSED IN QUOTES,
.*             OR A SELF-DEFINING COLUMN SPECIFICATION.
.*          2) THE DATA TYPE OF THE ELEMENT. MUST BE EXPLICITLY
.*             DECLARED SO AS TO BE AVAILABEL AT PRE-ASSEMBLY TIME.
.*               VALID VALUES ARE '1','2','3','4' FOR 1-4 BYTE FIX BIN
.*                  'A' FOR SYMBOLIC TAG OF CHARACTER DATA.
.*                  'P' FOR PACKED DECIMAL
.*                  ''  (NULL) FOR CHARACTER LITERAL AS FIRST ELEMENT
.*          3) FORMAT OF OUTPUT. OPTIONS INCLUDE:
.*                  A PICTURE SPECIFICATION OF 'Z','9','$', AND STATIC
.*                     EDITING CAHRACTERS LIKE '/' AND '.'. VALID FOR
.*                     1,2,3,4, AND P TYPES.
.*                  A STRING OF 'X'S SIGNIFYING A HEX FIELD. ONE 'X'
.*                     FOR EACH HEX DIGIT. VALID TYPES ARE 1,2,3,4.
.*                  THE CONSTANT LENGTH OF THE REMOTE CHARACTER FIELD
.*                     FOR TYPE 'A'.
.*                  (OMITTED FOR LITERAL CONSTANT)
.* TWO KEYWORD ARGUMENTS ARE REQUIRED.
.* TO=  SPECIFIES THE TARGET MESSAGE BUFFER. MAY BE A SUBLIST
.* AND IF IT IS THE SECOND ELEMENT MUST BE A SELF-DEFINING
.* TERM GIVING THE LENGTHSO THE BUFFER CAN BE CLEARED WITH
.* BLANKS.
.* WKA=  SPECIFIES THE WORK AREA. IT MUST BE A 12  BYTE FIELD
.* STARTING ON A DOUBLE WORD BOUNDARY. IF HEX CONVERSION IS
.* NOT REQUESTED, AN 8 BYTE FIELD WILL DO. AFTER THE FIRST
.* MACRO CALL TO FORMAT, THIS KEYWORD MAY BE OMITTED.
         GBLC  &FMTWKA
         LCLA  &ARGN,&POSIT,&DIG,&LP
         LCLC  &CHR
&ARGN    SETA  1
&POSIT   SETA  0
&LAB     DS    0H
.* CHECK ENTRY CONDITIONS
         AIF   ('&TO(1)' EQ '').ERROR1
         AIF   ('&WKA' EQ '' AND '&FMTWKA' EQ '').ERROR2
         AIF   ('&WKA' EQ '').N0
&FMTWKA  SETC  '&WKA'         SAVE WORK AREA NAME
.* NOW WE DECIDE IF THE BUFFER MUST BE BLANKED AND
.* GENERATE A MVI,MVC COMBINATION TO BLANK IT
.N0      ANOP
&CHR     SETC  T'&TO(2)
         AIF   ('&CHR' EQ 'O').N1
         MVI   &TO(1),C' '
         MVC   &TO(1)+1(&TO(2)-1),&TO(1)
.N1      AIF   ('&SYSLIST(&ARGN,1)' EQ '').ENDER
.* NOW BASED ON THE TYPE DECLARATION WE PROVIDE UNIFORM
.* STATUS FOR VARIOUS VALUES. FOR 1,2,3 OR 4 THE VALUE IS
.* LOADED IN R0 BY IC,ICM,LH,L.
         AIF   ('&SYSLIST(&ARGN,2)' NE '1').N2
         SR    0,0
         IC    0,&SYSLIST(&ARGN,1)
         AGO   .CVD
.N2      AIF   ('&SYSLIST(&ARGN,2)' NE '2').N3
         AIF   ('&SYSLIST(&ARGN,3)'(1,1) NE 'X').N25
         SR    0,0
         ICM   0,B'0011',&SYSLIST(&ARGN,1)
         AGO   .CVD
.N25     LH    0,&SYSLIST(&ARGN,1)
         AGO   .CVD
.N3      AIF   ('&SYSLIST(&ARGN,2)' NE '3').N4
         SR    0,0
         ICM   0,B'0111',&SYSLIST(&ARGN,1)
         AGO   .CVD
.N4      AIF   ('&SYSLIST(&ARGN,2)' NE '4').N5
         ICM   0,15,&SYSLIST(&ARGN,1)
         AGO   .CVD
.* PACKED DECIMAL FIELDS ARE PROCESSED SEPARATELY.
.N5      AIF   ('&SYSLIST(&ARGN,2)' NE 'P').N6
         EDIT  FROM=&SYSLIST(&ARGN,1),TO=&TO(1)+&POSIT,MASK=' &SYSLIST(X
               &ARGN,3)'
&POSIT   SETA  &POSIT+K'&SYSLIST(&ARGN,3)+1
         AGO   .NXT
.* REMOTE CHARACTER DATA IS MOVED INTO THE BUFFER WITH A MVC
.N6      AIF   ('&SYSLIST(&ARGN,2)' NE 'A').N7
         MVC   &TO(1)+&POSIT.(&SYSLIST(&ARGN,3)),&SYSLIST(&ARGN,1)
&POSIT   SETA  &POSIT+&SYSLIST(&ARGN,3)
         AGO   .NXT
.* THE NEXT BLOCK HANDLES THE NON SUBLIST ELEMENTS, EITHER
.* QUOTED TEXT OR COLUMN TAB SPECIFICATIONS
.N7      AIF   ('&SYSLIST(&ARGN,2)' NE '').ERROR3
         AIF   (T'&SYSLIST(&ARGN,1) NE 'N').N75
.* TAB SPECIFICATION SIMPLY BUMP THE CURRENT VALUE OF POSIT,
.* THE POSITION-REMEMBERING VARIABLE
&POSIT   SETA  &SYSLIST(&ARGN,1)
         AGO   .NXT
.N75     ANOP
.* CHARACTER LITERAL DATA IS CONVERTED TO AN ASSEMBLER LITERAL
.* NOTATION AMD MOVED INTO THE BUFFER
         AIF   (K'&SYSLIST(&ARGN,1) NE 3).N8
         MVI   &TO(1)+&POSIT,C&SYSLIST(&ARGN,1)
&POSIT   SETA  &POSIT+1
         AGO   .NXT
.N8      ANOP
&LP      SETA  K'&SYSLIST(&ARGN,1)-2
         MVC   &TO(1)+&POSIT.(&LP),=C&SYSLIST(&ARGN,1)
&POSIT   SETA  &POSIT+K'&SYSLIST(&ARGN,1)-2
         AGO   .NXT
.CVD     AIF   ('&SYSLIST(&ARGN,3)'(1,1) EQ 'X').HEX
.* THE FOLLOWING  CODE HANDLES THE OUTPUT OF 1,2,3 OR 4 TYPE
.* DATA ACCORDING TO A PICTURE FORMAT.
         CVD   0,&FMTWKA
.* THE EDIT MACRO WILL CONVERT DATA FROM LEFT TO RIGHT.
.* TO GET GOOD OUTPUT, AN OFFSET MUST BE ADDED TO THE
.* ADDRESS OF &WKA SO THE OUTPUT WILL CONTAIN THE RIGHTMOST
.* DIGITS. THIS LOOP COUNTS UP DIGIT SELECTORS IN THE FORMAT
.* TO CALCULATE THE OFFSET.
&DIG     SETA  0
&LP      SETA  1
.LOP     ANOP
&CHR     SETC  '&SYSLIST(&ARGN,3)'(&LP,1)
         AIF   ('&CHR' NE '9' AND '&CHR' NE 'Z' AND '&CHR' NE '$').N10
&DIG     SETA  &DIG+1
.N10     ANOP
&LP      SETA  &LP+1
         AIF   (&LP LE K'&SYSLIST(&ARGN,3)).LOP
.* END OF LOOP
&DIG     SETA  &DIG/2+1
         EDIT  TO=&TO(1)+&POSIT,FROM=&FMTWKA+8-&DIG,MASK=' &SYSLIST(&ARX
               GN,3)'
&POSIT   SETA  &POSIT+K'&SYSLIST(&ARGN,3)+1
         AGO   .NXT
.* THE FOLLOWING CODE HANDLES THE CONVERSION TO HEX.
.* IT FOLLOWS THE USUAL SORT OF CUTE TIRCKS
.HEX     ST    0,&FMTWKA+8
         UNPK  &FMTWKA.(7),&FMTWKA+8(4)
         MVC   &FMTWKA+7(1),&FMTWKA+11
         NC    &FMTWKA.(8),=X'0F0F0F0F0F0F0F0F'
         TR    &FMTWKA.(8),=C'0123456789ABCDEF'
&LP      SETA  K'&SYSLIST(&ARGN,3)
         MVC   &TO(1)+&POSIT.(&LP),&FMTWKA+8-&LP
&POSIT   SETA  &POSIT+K'&SYSLIST(&ARGN,3)
.NXT     ANOP
&ARGN    SETA  &ARGN+1
         AGO   .N1
.ERROR1  MNOTE 8,'NO ''TO'' STRING'
         MEXIT
.ERROR2  MNOTE 8,'NO WORK AREA PROVIDED'
         MEXIT
.ERROR3  MNOTE 8,'INVALID ITEM FORMAT &SYSLIST(&ARGN,2)'
.ENDER   MEND
./ ADD NAME=FULLRTRN                               17OCT89 12.47.53
         MACRO
&NAME    FULLRTRN   &LV=,&SP=,&RC=0,&RENT=YES
         LCLC  &LVA,&SPA
         AIF   ('&RENT' EQ 'YES' OR '&RENT' EQ 'SPECIAL').ISRENT
         AIF   ('&RENT' EQ 'NO').NOTRENT
         MNOTE 8,'IF RENT PARAMETER IS SPECIFIED, IT MUST HAVE THE VALUX
               E ''YES'', ''NO'', OR ''SPECIAL''.'
         MEXIT
.NOTRENT AIF   (T'&LV EQ 'O').TSTSP
.LVSPMSG MNOTE 8,'IF RENT=NO, NEITHER LV NOR SP MAY BE SPECIFIED'
         MEXIT
.TSTSP   AIF   (T'&SP NE 'O').LVSPMSG
&NAME    L     13,4(13)  GET CALLER'S SAVEAREA ADDRESS
         AGO   .RETURN
.ISRENT  AIF   (T'&SP NE 'O').TSTLV
&SPA     SETC  '0'
.TSTLV   AIF   (T'&LV    NE 'O').GENER
&LVA     SETC  '72'
         AGO   .LR
.GENER   AIF   ('&LV' NE '0').LR
&NAME    SR    1,1 ZERO REGISTER TO INDICATE ENTIRE SUBPOOL BEING FREED
         AGO   .LOAD13
.LR      ANOP
&NAME    LR    1,13 LOAD CURRENT SAVEAREA ADDRESS IN REG 1
.LOAD13  L     13,4(13)  GET CALLER'S SAVEAREA ADDRESS
*              EXECUTE FREEMAIN MACRO
         AIF   ('&RENT' EQ 'SPECIAL').SPECIAL
         FREEMAIN R,LV=&LV.&LVA,SP=&SP.&SPA,A=(1)
         AGO   .RETURN
.SPECIAL FREECORE   R,LV=&LV.&LVA,SP=&SP.&SPA,A=(1)
.RETURN  ANOP
*              RETURN TO CALLER
         RETURN (14,12),T,RC=&RC
         MEND
./ ADD NAME=FULLSAVE                               17OCT89 12.47.53
*        FULLSAVE
         MACRO
&NAME    FULLSAVE &BASEREG=12,&LV=,&SP=,&RENT=YES,&AREA=,&IDENT=*
         LCLA  &LCLA
         LCLC  &C(9),&LVA,&SPA
         AIF   ('&RENT' NE 'NO').TESTYES
         AIF   (T'&LV NE 'O').MNOTE
         AIF   (T'&SP EQ 'O').SAVE
.MNOTE   MNOTE 8,'IF RENT=NO, NEITHER LV NOR SP MAY BE SPECIFIED'
         MEXIT
.TESTYES AIF   ('&RENT' EQ 'YES' OR '&RENT' EQ 'SPECIAL').ISRENT
         MNOTE 8,'IF RENT PARAMETER IS SPECIFIED, IT MUST HAVE THE VALUX
               E ''YES'', ''NO'', OR ''SPECIAL''.'
         MEXIT
.ISRENT  AIF   (T'&LV NE 'O').TSTSP
&LVA     SETC  '72'
.TSTSP   AIF   (T'&SP NE 'O').SAVE
&SPA     SETC  '0'
.SAVE    ANOP
         AIF   (N'&BASEREG LT 10).GENBASE
         MNOTE 8,'TOO MANY BASE REGISTERS SPECIFIED. MAXIMUM PERMITTED X
               IS 9.'
         MEXIT
.GENBASE ANOP
&LCLA    SETA  &LCLA+1
&C(&LCLA) SETC ','
         AIF   (&LCLA LT N'&BASEREG).GENBASE
         DS    0H   ALIGN FOR FOLLOWING USING STATEMENT
 USING *&C(1)&BASEREG(1)&C(2)&BASEREG(2)&C(3)&BASEREG(3)&C(4)&BASEREG(4X
               )&C(5)&BASEREG(5)&C(6)&BASEREG(6)&C(7)&BASEREG(7)&C(8)&BX
               ASEREG(8)&C(9)&BASEREG(9) ESTABLISH ADDRESSABILITY
*              SAVE CALLER'S REGISTERS
&NAME    SAVE  (14,12),T,&IDENT
         LR    &BASEREG(1),15 LOAD BASE REGISTER FROM INITIAL ENTRY REGX
               ISTER
         AIF   ('&RENT' EQ 'NO').NOTRENT
*              GETMAIN FOR NEW SAVEAREA
         AIF   ('&RENT' EQ 'SPECIAL').SPECIAL
         GETMAIN    R,LV=&LV.&LVA,SP=&SP.&SPA
         AGO   .STORE13
.SPECIAL GETCORE    R,LV=&LV.&LVA,SP=&SP.&SPA
         AGO   .STORE13
.NOTRENT AIF   (T'&AREA EQ 'O').DS18F
         B     LOAD&SYSNDX    SPACE OVER SAVEAREA ADCON
         DC    A(&AREA)  SAVEAREA POINTER
LOAD&SYSNDX L  1,*-4  GET ADDRESS OF NEW SAVEAREA
         AGO   .STORE13
.DS18F   B     LOAD&SYSNDX    SPACE OVER NEW SAVEAREA
         DS    18F  NEW SAVEAREA
LOAD&SYSNDX LA 1,*-72    GET ADDRESS OF NEW SAVEAREA
.STORE13 ST    13,4(1)   STORE OLD SAVEAREA ADDRESS IN NEW SAVEAREA
         ST    1,8(13)   STORE NEW SAVEAREA ADDRESS IN OLD SAVEAREA
         LR    0,1  TEMPORARILY MOVE NEW SAVEAREA ADDRESS TO REG 0
         LR    1,13 MOVE OLD SAVEAREA ADDRESS TO REG 1
         LR    13,0 MOVE NEW SAVEAREA ADDRESS TO REG 13
         AIF   (N'&BASEREG EQ 1).AIFAREA
&LCLA    SETA  1
         LA    &BASEREG(N'&BASEREG),4095 SET INCREMENT FOR ADDITIONAL BX
               ASE REGISTERS
.BASELA  LA    &BASEREG(&LCLA+1),1(&BASEREG(&LCLA),&BASEREG(N'&BASEREG)X
               ) SET NEXT BASE REGISTER
&LCLA    SETA  &LCLA+1
         AIF   (&LCLA LT N'&BASEREG).BASELA
.AIFAREA AIF   ('&AREA' EQ '').LM
         USING &AREA,13  MAKE NEW SAVEAREA ADDRESSABLE
.LM      LM    0,1,20(1) RESTORE REGISTERS 0 AND 1 FROM OLD SAVEAREA
         MEND
*        TRANSFERRED FROM HAMMER   - 09/16/70
*
./ ADD NAME=HAL      0100-84209-84209-1748-00456-00456-00000-KK00150
***************************************************************
*                                                             *
* MODULE NAME = HAL                                           *
*                                                             *
* DESCRIPTIVE NAME = STRUCTURED ASSEMBLER MACROS              *
*                                                             *
* COPYRIGHT = NONE                                            *
*                                                             *
* VERSION = OS/VS/VM ASSEMBLERS                               *
*                                                             *
* STATUS = RELEASE 2 LEVEL 0                                  *
*                                                             *
* NOTES =                                                     *
*                                                             *
*     THESE MACROS ARE INTENDED TO BE COPIED INTO THE         *
*     BEGINNING OF ASSEMBLIES WHERE THEY ARE TO BE            *
*     USED. THIS AVOIDS NAMING CONFLICTS WITH OTHER           *
*     MACROS IN THE SYSTEM LIBRARIES.                         *
*                                                             *
***************************************************************
         SPACE 5
*        ******************************************************
*        *     BEND                                           *
*        ******************************************************
         MACRO
         BEND  &BLKNM,&EPILOG=NO
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         AIF   (&YCCLVL GT 0).OKLVL
         MNOTE 8,'BEND OR EPILOG STATEMENT OUTSIDE OF BLOCK'
         SPACE 1
         MEXIT
.OKLVL   AIF   ('&BLKNM' NE '&YCCBLKN(&YCCLVL)').ERR1
         AIF   (&YCCTYPE(&YCCLVL) EQ 0).DCRMT
         AIF   (&YCCTYPE(&YCCLVL) NE 1 OR NOT &YCCEPLG(&YCCLVL) OR '&EP*
               ILOG' NE 'NO').OK2
         MNOTE 8,'MISSING EPILOG FOR THIS BLOCK'
.OK2     ANOP
         AIF   (&YCCTYPE(&YCCLVL) NE 2 AND &YCCTYPE(&YCCLVL) NE 3).NLP
         B     TS&YCCLVL.@&YCCBLK#(&YCCLVL)
&YCCL    SETC  'EN&YCCLVL.@&YCCEN#(&YCCLVL)'
&YCCL    DS    0H
&YCCL    SETC  'EL&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
         AGO   .DCRMT
.NLP     AIF   (&YCCTYPE(&YCCLVL) EQ 1 OR &YCCTYPE(&YCCLVL) EQ 5).NELS
&YCCL    SETC  'EL&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
.NELS    ANOP
&YCCL    SETC  'EN&YCCLVL.@&YCCEN#(&YCCLVL)'
&YCCL    DS    0H
.DCRMT   ANOP
         AIF   ('&EPILOG' EQ 'YES').EXIT
&YCCLVL  SETA  &YCCLVL-1
.EXIT    MEXIT
.ERR1    MNOTE 8,'BLOCK NAME OMITTED OR DOES NOT MATCH CURRENT BLOCK'
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     BLOCK                                          *
*        ******************************************************
         MACRO
&LAB     BLOCK &DUMMY,&EPILOG=NO
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         YCCPUSH NAME=&LAB,TYPE=BLOCK
         AIF   ('&EPILOG' NE 'YES').EXITOK
&YCCEPLG(&YCCLVL) SETB (1)
.EXITOK  ANOP
&LAB     DS    0H
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     DO                                             *
*        ******************************************************
         MACRO
&LAB     DO    &DUMMY
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         LCLA  &I
&I       SETA  2
         AIF   ('&DUMMY' NE 'WHILE' AND '&DUMMY' NE 'INF').TIL
         YCCPUSH NAME=&LAB,TYPE=DOWILE
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
         AGO   .TESTS
.TIL     AIF   ('&DUMMY' NE 'UNTIL').ERR1
         YCCPUSH NAME=&LAB,TYPE=DOTIL
         AIF   ('&LAB' EQ '').NOLAB2
&LAB     DS    0H
.NOLAB2  ANOP
         B     ST&YCCLVL.@&YCCBLK#(&YCCLVL)
         AGO   .TESTS
.ERR1    MNOTE 8,'DO MUST BE (UNTILWHILEINF)'
.TESTS   ANOP
&YCCL    SETC  'TS&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
.LP      AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1)
&I       SETA  &I+2
         AGO   .LP
.DONE    ANOP
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     EPILOG                                         *
*        ******************************************************
         MACRO
&LAB     EPILOG &BLKNM
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
&LAB     DS    0H
         AIF   (&YCCTYPE(&YCCLVL) NE 1).ERR1
         BEND  &BLKNM,EPILOG=YES
&YCCTYPE(&YCCLVL) SETA 0
&YCCEPLG(&YCCLVL) SETB (0)
         MEXIT
.ERR1    MNOTE 8,'EPILOG APPEARS OUT OF CONTEXT'
         MEND ,
         SPACE 5
*        ******************************************************
*        *     EXIT                                           *
*        ******************************************************
         MACRO
&LAB     EXIT  &LABNAM,&IF
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL,&O
         LCLA  &I
&O       SETC  'B'
         AIF   ('&LABNAM' EQ '').NOTFAR
         AIF   ('&LABNAM'(1,1) NE '(').NOTFAR
&O       SETC  'B'
.NOTFAR  ANOP
         AIF   ('&LABNAM' NE '' AND '&LABNAM' NE '()').IF
         AIF   (&YCCTYPE(&YCCLVL) EQ 1).BLKOK
         MNOTE 8,'EXIT CODE DEFINION SHOULD BE IN A "BLOCK"'
.BLKOK   ANOP
         &O    EN&YCCLVL.@&YCCEN#(&YCCLVL)
&LAB     DS    0H
         SPACE 1
         MEXIT
.IF      ANOP
&YCCGO   SETC  '&LABNAM(1)'
         AIF   ('&IF' EQ '').GOTO
         YCCPUSH NAME=&LAB,TYPE=IFLEAVE
&I       SETA  3
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
.LP2     AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1),OP=&O
&I       SETA  &I+2
         AGO   .LP2
.DONE    ANOP
&YCCLVL  SETA  &YCCLVL-1
&YCCL    DS    0H
         SPACE 1
         MEXIT
.GOTO    ANOP
&LAB     &O    &YCCGO
         SPACE 1
.END     MEND ,
         SPACE 5
*        ******************************************************
*        *     IF                                             *
*        ******************************************************
         MACRO
&LAB     IF    &DUMMY
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         LCLA  &I
         YCCPUSH NAME=&LAB,TYPE=IF
&I       SETA  1
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
.LP      AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1)
&I       SETA  &I+2
         AGO   .LP
.DONE    ANOP
&YCCL    DS    0H
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     LEAVE                                          *
*        ******************************************************
         MACRO
&LAB     LEAVE &BLKNM,&IF
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL,&O
         LCLA  &LVL,&I
&O       SETC  'B'
         AIF   ('&BLKNM' EQ '' OR '&BLKNM'(1,1) NE '(').NOTFAR
&O       SETC  'B'
.NOTFAR  ANOP
&LVL     SETA  &YCCLVL
         AIF   ('&YCCBLKN(&LVL)' EQ '&BLKNM(1)' AND &YCCTYPE(&LVL) EQ 0*
               ).ERR2
.LP      AIF   (&LVL EQ 0).ERR
         AIF   ('&BLKNM(1)' EQ '&YCCBLKN(&LVL)').OK
         AIF   (&YCCEPLG(&LVL)).ERREXIT
&LVL     SETA  &LVL-1
         AGO   .LP
.OK      ANOP
         AIF   ('&IF' EQ 'IF').IF
&LAB     &O    EN&LVL.@&YCCEN#(&LVL)
         SPACE 1
         MEXIT
.IF      ANOP
&YCCGO   SETC  'EN&LVL.@&YCCEN#(&LVL)'
         YCCPUSH NAME=&LAB,TYPE=IFLEAVE
&I       SETA  3
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
.LP2     AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1),OP=&O
&I       SETA  &I+2
         AGO   .LP2
.DONE    ANOP
&YCCLVL  SETA  &YCCLVL-1
&YCCL    DS    0H
         SPACE 1
         MEXIT
.ERR     MNOTE 8,'ATTEMPT TO LEAVE UNRECOGNIZED BLOCK'
         SPACE 1
.ERREXIT MNOTE 8,'CANNOT EXIT BEYOND BLOCK &YCCBLKN(&LVL)'
         SPACE 1
         MEXIT
.ERR2    MNOTE 8,'YOU CANNOT LEAVE A BLOCK FROM ITS OWN EPILOG'
.END     MEND ,
         SPACE 5
*        ******************************************************
*        *     ELSE                                           *
*        ******************************************************
         MACRO
         ELSE  &DUMMY
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         B     EN&YCCLVL.@&YCCEN#(&YCCLVL)
         AIF   (&YCCTYPE(&YCCLVL) NE 4).ERR1
&YCCTYPE(&YCCLVL) SETA 5
&YCCL    SETC  'EL&YCCLVL.@&YCCBLK#(&YCCLVL)'
&YCCL    DS    0H
         SPACE 1
         MEXIT
.ERR1    MNOTE 8,'MISPLACED ELSE STATEMENT'
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     ELSEIF                                         *
*        ******************************************************
         MACRO
&LAB     ELSEIF &DUMMY
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         LCLA  &I
&I       SETA  1
         ELSE
&YCCTYPE(&YCCLVL) SETA  4         RESET TO "IF" TYPE
&YCCBLK#(&YCCLVL) SETA &YCCBLK#(&YCCLVL)+1
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
&YCCL    SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
.LP      AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1)
&I       SETA  &I+2
         AGO   .LP
.DONE    ANOP
&YCCL    DS    0H
         SPACE 1
         MEND ,
         SPACE 5
*        ******************************************************
*        *     INTERNAL MACROS YCCTEST AND YCCPUSH            *
*        ******************************************************
         MACRO
         YCCTEST &A,&B,&OP=B
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         LCLC  &N,&C,&GO,&O
         LCLB  &NEG,&LOGIC
         LCLA  &I
         AIF   ('&B' EQ 'AND' OR '&B' EQ 'OR' OR '&B' EQ '' OR '&B' EQ *
               'THEN').TCONJ
         MNOTE 8,'&B SHOULD BE (ANDORTHEN)'
.TCONJ   ANOP
         AIF   (&YCCTYPE(&YCCLVL) NE 5).NOLEV
         AIF   ('&B' EQ 'AND').LVAND
&GO      SETC  '&YCCGO'
&NEG     SETB  (0)
         AGO   .OKCONJ
.LVAND   ANOP
&GO      SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
&NEG     SETB  (1)
         AGO   .OKCONJ
.NOLEV   AIF   (&YCCTYPE(&YCCLVL) EQ 2).TIL
         AIF   ('&B' NE 'OR').IFAND
&GO      SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
&NEG     SETB  (0)
         AGO   .OKCONJ
.IFAND   ANOP
&GO      SETC  'EL&YCCLVL.@&YCCBLK#(&YCCLVL)'
&NEG     SETB  (1)
         AGO   .OKCONJ
.TIL     AIF   ('&B' EQ 'AND').TILAND
&GO      SETC  'EL&YCCLVL.@&YCCBLK#(&YCCLVL)'
&NEG     SETB  (0)
         AGO   .OKCONJ
.TILAND  ANOP
&GO      SETC  'ST&YCCLVL.@&YCCBLK#(&YCCLVL)'
&NEG     SETB  (1)
.OKCONJ  ANOP
         AIF   (N'&A NE 1).COMPND1
&C       SETC  '&A'
         AGO   .CONDS1
.COMPND1 ANOP
&C       SETC  '&A(2)'
.CONDS1  ANOP
         AIF   ('&C' EQ 'N').CONDS2
         AIF   ('&C'(1,1) NE 'N').CONDS2
&NEG     SETB  (NOT &NEG)
&I       SETA  K'&C-1
&C       SETC  '&C'(2,&I)
.CONDS2  ANOP
         AIF   ('&C' NE 'EQ').NOTEQ
&C       SETC  'E'
.NOTEQ   ANOP
         AIF   ('&C' NE 'LT').NLT
&C       SETC  'L'
         AGO   .NOTNOT
.NLT     ANOP
         AIF   ('&C' NE 'GT').NGT
&C       SETC  'H'
         AGO   .NOTNOT
.NGT     ANOP
         AIF   ('&C' NE 'LE').NLE
&NEG     SETB  (NOT &NEG)
&C       SETC  'H'
.NLE     ANOP
         AIF   ('&C' NE 'GE').NGE
&NEG     SETB  (NOT &NEG)
&C       SETC  'L'
         AGO   .NOTNOT
.NGE     ANOP
         AIF   ('&C' NE 'ON').NON
&C       SETC  'O'
&LOGIC   SETB  (1)
         AGO   .NOTNOT
.NON     ANOP
         AIF   ('&C' NE 'OFF').NOFF
&C       SETC  'Z'
&LOGIC   SETB  (1)
         AGO   .NOTNOT
.NOFF    ANOP
         AIF   ('&C' NE 'MIX').NMIX
&C       SETC  'M'
&LOGIC   SETB  (1)
         AGO   .NOTNOT
.NMIX    ANOP
.NOTNOT  ANOP
&N       SETC  'N'
         AIF   (&NEG).NOTOK
&N       SETC  ''
.NOTOK   ANOP
&O       SETC  '&OP.&N.&C'
         AIF   (N'&A NE 1).COMPND2
         &O    &GO
         MEXIT
.COMPND2 ANOP
         AIF   ('&A(4)' EQ '').DFLT
         &A(4) &A(1),&A(3)
.NMZ     &O    &GO
         MEXIT
.DFLT    AIF   (&LOGIC).TM
         AIF   (T'&A(3) NE 'N').NCH
         AIF   ('&A(3)' NE '0').NLTR
         LTR   &A(1),&A(1)
         AGO   .TST
.NLTR    ANOP
         CH    &A(1),=H'&A(3)'
         AGO   .TST
.NCH     C     &A(1),&A(3)
.TST     ANOP
         &O    &GO
         MEXIT
.TM      TM    &A(1),&A(3)
         &O    &GO
         MEND ,
         SPACE 5
         MACRO
         YCCPUSH &NAME=,&TYPE=
         GBLA  &YCCLVL,&YCCBLK#(30),&YCCTYPE(30),&YCCEN#(30)
         GBLC  &YCCBLKN(30)
         GBLB  &YCCEPLG(30)
         LCLC  &YCCL
         AIF   (&YCCLVL EQ 30).OVFLOW
&YCCLVL  SETA  &YCCLVL+1
&YCCBLK#(&YCCLVL) SETA  &YCCBLK#(&YCCLVL)+1
&YCCEN#(&YCCLVL) SETA  &YCCBLK#(&YCCLVL)
&YCCBLKN(&YCCLVL) SETC  '&NAME'
&YCCEPLG(&YCCLVL) SETB (0)
         AIF   ('&TYPE' NE 'BLOCK').NBLK
&YCCTYPE(&YCCLVL) SETA 1
         MEXIT
.NBLK    AIF ('&TYPE' NE 'DOTIL').NDOTIL
&YCCTYPE(&YCCLVL) SETA 2
         MEXIT
.NDOTIL   AIF   ('&TYPE' NE 'DOWILE').NDOWILE
&YCCTYPE(&YCCLVL) SETA 3
         MEXIT
.NDOWILE AIF   ('&TYPE' NE 'IF').NOTIF
&YCCTYPE(&YCCLVL) SETA 4
         MEXIT
.NOTIF   AIF   ('&TYPE' NE 'IFLEAVE').NOTLEV
&YCCTYPE(&YCCLVL) SETA 5
         MEXIT
.NOTLEV  MNOTE 8,'UNRECOGNIZED BLOCK TYPE'
         MEXIT
.OVFLOW  MNOTE 16,'BLOCKS NESTED TOO DEEPLY FOR HAL MACROS'
         MEND
./ ADD NAME=IFG0EX0B                               17OCT89 12.47.53
         TITLE '"IFG0EX0B" - OPEN EXIT (RELEASE SEC SPACE)'
**
**       REGISTER DEFINITIONS
**
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         EJECT
         PRINT NOGEN
JFCB     DSECT
         IEFJFCBN LIST=NO
**
         IECOIEXL
**
         IHAPSA
**
         CVT   DSECT=YES,LIST=NO
**
         IEFJESCT
**
         IEFJSCVT
**
         SIO5GBL TYPE=DSECT
**
         SIO5MID TYPE=DSECT
         EJECT
**
         SIOSCVT
         PRINT GEN
         EJECT
**
WRKDSCT  DSECT
**
**       EXIT WORK AREA
**
WRKSAVE  DC    18F'0'                  REG SAVE AREA
WRKLEN   EQU   *-WRKDSCT               LENGTH OF AREA
         EJECT
IFG0EX0B CSECT
**                                       CONTROL OF PERMISSION)
**
**       SAVE ALL THE REGISTERS NOW
**
         USING IFG0EX0B,RF             EPA REG
         SAVE  (14,12)                 SAVE THE REGS
         SPACE 2
**
**       IDENTIFY THIS MODULE
**
         B     ARNIDN                  GO AROUND THE ID
         DC    CL8'IFG0EX0B'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
ARNIDN   DS    0H
         SPACE 2
**
**       SET THE BASE REG
**
         LR    RC,RF                   SET THE NEW BASE
         DROP  RF
         USING IFG0EX0B,RC             AND TELL THE ASSEMBLER
         SPACE 2
**
**       SET PARMS BASE
**
         LR    RB,R1                   POINT TO THE PARMS
         USING OIEXL,RB                AND MAP THEM
         SPACE 2
**
**       GET THE WORK AREA
**
         GETMAIN R,LV=WRKLEN           POINT TO THE LENGTH
         ST    RD,4(,R1)               SAVE CALLER REG
         ST    R1,8(,RD)               AND OUR SAVE
         LR    RD,R1                   POINT TO THE WORK AREA
         USING WRKDSCT,RD              AND MAP THE AREA
         SPACE 2
**
**       LOCATE THE S.C.I. SSCT
**
         L     RF,FLCCVT-PSA           POINT AT CVT
         L     RF,CVTJESCT-CVT(RF)     POINT AT JESCT
         LTR   RF,RF                   IS THERE A JESCT?
         BZ    IFGEXIT                 NO, EXIT NOW
         L     RF,JESSSCT-JESCT(RF)    POINT AT SSCT
         LTR   RF,RF                   IS THERE AN SSCT?
         BZ    IFGEXIT                 NO, EXIT NOW
SSCTSCAN DS    0H
         CLC   =C'SCI1',SSCTSNAM-SSCT(RF) IS THIS OUR SSCT?
         BE    SSCTFND                 YES
         L     RF,SSCTSCTA-SSCT(RF)    POINT AT NEXT SSCT
         LTR   RF,RF                   IS THERE A NEXT SSCT?
         BNZ   SSCTSCAN                YES, CONTINUE SCAN
         B     IFGEXIT                 NO, EXIT
         SPACE 2
SSCTFND  DS    0H
**
**       SEE IF S.I.O. INITIALIZED
**
         L     RF,SSCTSUSE-SSCT(RF)    LOAD SIO CVT ADDRESS
         LTR   RF,RF                   DOES SIO CVT EXIST?
         BZ    IFGEXIT                 NO, EXIT
         SPACE 2
**
**       SEE IF RUNNING S.I.O. RELEASE 5.0
**
         CLC   =CL8'SIO5GBLT',0(RF)    IS IT RELEASE 5?
         BNE   NOTREL5                 NOPE, DO IT THE OLD WAY
**
**       SEE IF S.I.O. IS ACTIVE NOW
**
         CLI   GBTPRFL-SIO5GBLD(RF),GBTPRUP TEST IS SIO UP?
         BNE   IFGEXIT                 NO, EXIT
         SPACE 2
**
**       POINT TO THE RELEASE INTERFACE MODULE
**
         L     RF,GBTRELP-SIO5GBLD(,RF) LOAD SIO EXIT ADDRESS
         LTR   RF,RF                   IS IT AVAILABLE?
         BZ    IFGEXIT                 NO-RETURN TO OPEN W/O RLSE
         LA    RF,MIDLENG(,RF)          PASS THE ID
         B     RELCHECK                GO CHECK FOR RELEASE
         SPACE 2
NOTREL5  DS    0H
**
**       SEE IF S.I.O. IS ACTIVE NOW
**
         TM    SIOSFLAG-SIOSCVT(RF),SIOSSHFG TEST IS SIO UP?
         BNO   IFGEXIT                 NO, EXIT
         SPACE 2
**
**       POINT TO THE RELEASE INTERFACE MODULE
**
         L     RF,SIOSRELP-SIOSCVT(RF) LOAD SIO EXIT ADDRESS
         LTR   RF,RF                   IS IT AVAILABLE?
         BZ    IFGEXIT                 NO-RETURN TO OPEN W/O RLSE
         SPACE 2
RELCHECK DS    0H
**
**       GO TEST FOR RELEASE ELIGIBILITY
**
         LR    R1,RB                   SET THE PARM POINTER
         BALR  RE,RF                   CHECK TABLE FOR RELEASING
         SPACE 2
**
**       THESE RETURN CODES ARE SET IN BYTE 3 OF REG 15.
**
**       00 - DATA SET IS RELEASE CANDIDATE
**       04 - S.I.O. IS INACTIVE
**       08 - S.I.O. RUNNING IN WARNING MODE ONLY
**       0C - DATA SET NOT OPEN FOR OUTPUT
**       10 - PROTECT KEY LOWER THAN 8
**       14 - NOT A DISK FILE BEING OPENED
**       18 - NOT A PS FILE BEING OPENED
**       1C - NOT A QSAM OR BSAM FILE
**       20 - TABLE "SIOSRLXX" CAN NOT BE LOADED
**       24 - TABLE "SIOSRLXX" IS INVALID
**       28 - BYPASS ENTRY FOUND FOR THIS FILE
**       2C - NO SELECT ENTRY FOR THIS FILE
**       30 - FORMAT 1 DSCB NOT BEING PROCESSED
**       34 - DATA SET IS OPENED WITH DISP=MOD
**
**       THE FOLLOWING RETURN CODES ARE ONLY SET FOR DASD DATA SETS
**       THAT USE AN AVERAGE BLOCK LENGTH FOR SPACE ALLOCATION.
**
**       THIS RETURN CODE IS SET IN BYTE 2 OF REG 15
**
**       04 - SPACE ALLOCATION HAS BEEN RE-CALCULATED TO ACCOUNT
**            FOR THE BLOCK SIZE CHANGED BY S.I.O.
**
         SPACE 2
**
**       THIS EXIT ONLY TESTS FOR A RETURN CODE OF 00000000 AND
**       000004XX.   IS UP TO THE USER TO TEST FOR OTHER RETURN
**       CODES AS REQUIRED.
**
         LTR   RF,RF                   RELEASE SPACE ONLY?
         BZ    IFGRLSE                 YES, SET THE RELEASE
**
         CLM   RF,3,=X'0400'           RELEASE/SEC SPACE?
         BE    IFGRLSE                 YES, SET THE RELEASE
**
         CLM   RF,2,=X'04'             SEC SPACE ONLY?
         BE    IFGMODF                 YES, SET JFCB MODIFIED
**
         XR    RF,RF                   CLEAR THE RC (JFCB NOT CHANGED)
         B     IFGEXIT                 RETURN TO OPEN NOW
         SPACE 2
IFGRLSE  DS    0H
**
**       SET THE RELEASE FLAG IN THE JFCB
**
         L     R1,OIEXJFCB             POINT TO THE JFCB
         OI    JFCBIND1-JFCB(R1),JFCRLSE SET THE RELEASE INDICATOR
**
IFGMODF  DS    0H
**
         LA    RF,4                    INDICATE JFCB MODIFIED
         SPACE 2
IFGEXIT  DS    0H
**
**       RELEASE THE WORK AREA
**
         LR    R2,RD                   COPY THE POINTER
         LR    R3,RF                   SAVE THE RC FOR NOW
         L     RD,4(,RD)               POINT TO CALLER SAVE
         XC    0(WRKLEN,R2),0(R2)      CLEAR THE AREA
         FREEMAIN R,LV=WRKLEN,A=(R2)
         SPACE 2
**
**       RETURN TO CALLER NOW
**
         LR    RF,R3                   GET THE RETURN CODE
         L     RE,12(,RD)              RETURN REG
         LM    R0,RC,20(RD)            REST OF REGS
         BR    RE                      AND GO BACK
         SPACE 2
**
**       LITERALS
**
         LTORG
         END
./ ADD NAME=IFOFF                                  17OCT89 12.47.53
         MACRO
&LABEL   IFOFF &BIT,&BROF,&BRON
         LCLB  &BL121,&BL122
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT' NE '').A
         MNOTE 1,'OPERAND 1 MISSING - NO GENERATION'
         MEXIT
.A       ANOP
&BL121   SETB  ('&BRON' NE '')
&BL122   SETB  ('&BROF' NE '')
         AIF   (&BL121 OR &BL122).AA
         MNOTE 1,'NO TRANSFER ADDRESS'
         MEXIT
.AA      ANOP
&LABEL   TM    &BIT,&BIT&#  .          TEST FOR BIT.
         AIF   (NOT &BL122).OTHER
         BZ    &BROF  .                BRANCH NOT ON.
         AIF   (&BL121).OTHER
         SPACE
         MEXIT
.OTHER   BO    &BRON  .                BRANCH ON.
         SPACE
         MEND
./ ADD NAME=IFON                                   17OCT89 12.47.53
         MACRO
&LABEL   IFON  &BIT,&BRYES,&BRNO
         LCLB  &BL121,&BL122
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT' NE '').A
         MNOTE 1,'OPERAND 1 MISSING - NO GENERATION'
         MEXIT
.A       ANOP
&BL121   SETB  ('&BRYES' NE '')
&BL122   SETB  ('&BRNO' NE '')
         AIF   (&BL121 OR &BL122).AA
         MNOTE 1,'NO TRANSFER ADDRESS'
         MEXIT
.AA      ANOP
&LABEL   TM    &BIT,&BIT&#  .          TEST FOR BIT.
         AIF   (NOT &BL121).TAKNO
         BO    &BRYES  .               BRANCH ON.
         AIF   (&BL122).TAKNO
         SPACE
         MEXIT
.TAKNO   ANOP
         BZ    &BRNO  .                BRANCH NOT ON.
         SPACE
         MEND
./ ADD NAME=IKJCVT   0101-80025-87208-1200-00015-00013-00000-KX00155
         MACRO
&N       IKJCVT
CVTPTR   EQU   16
CVTMAP   DSECT
         ORG   CVTMAP+X'9C'
CVTTVT   DS    A
         ORG   CVTMAP+480
CVTSCAN  DS    A
         ORG   CVTMAP+524
CVTPARS  DS    A
         ORG   CVTMAP+732
CVTDAIR  DS    A
CVTEHDEF DS    A
CVTEHCIR DS    A
         MEND
./ ADD NAME=IKJEGSIO 0100-87083-87083-1611-00846-00846-00000-GS00150
**********************************************************************
*                                                                    *
*        IKJEGSIO IS A SET OF STRUCTURED MACROS THAT BUILD THE       *
*        ASSEMBLER INSTRUCTIONS TO PREPARE A PERSONALIZED PARM       *
*        LIST AND THE BRANCH TO IKJEGIO FOR I/O SERVICES.  THE       *
*        SET OF MACROS ARE INCLUDED; HOWEVER, THE MACRO SOURCE       *
*        IS SUPPRESSED.                                              *
*                                                                    *
*        STATUS -- VERSION NO. 01 - OS/VS2 RELEASE NO. 02            *
*        CHANGE LEVEL -- 01 - DATE 2-2-73                            *
*                                                                    *
**********************************************************************
         PRINT OFF
         MACRO
&LABEL   IKJEGSIO                                                      *
               &TYPEREQ,               TYPE SERVICE REQUESTED          *
               &FIRST=,                FIRST MESSAGE TO BE ISSUED      *
               &INST11=,               FIRST INSERT OF THE 1ST MSG     *
               &INST12=,               SECOND INSERT OF THE 1ST MSG    *
               &INST13=,               THIRD INSERT OF THE 1ST MSG     *
               &INST14=,               FOURTH INSERT OF THE 1ST MSG    *
               &INST15=,               FIFTH INSERT OF THE 1ST MSG     *
               &INST16=,               SIXTH INSERT OF THE 1ST MSG     *
               &SECOND=,               FIRST MESSAGE TO BE ISSUED      *
               &INST21=,               FIRST INSERT OF THE 2ND MSG     *
               &INST22=,               SECOND INSERT OF THE 2ND MSG    *
               &INST23=,               THIRD INSERT OF THE 2ND MSG     *
               &INST24=,               FOURTH INSERT OF THE 2ND MSG    *
               &INST25=,               FIFTH INSERT OF THE 2ND MSG     *
               &INST26=,               SIXTH INSERT OF THE 2ND MSG     *
               &ID=,                   MODULE CALLING ID NUMBER        *
               &MF=,                   TYPE MACRO TO EXPAND            *
               &DATAPTR=,              ADDRESS OF DATA LINE            *
               &DSNAME=,               ADDRESS OF OPEN DCB             *
               &SVC=,                  SVC NUMBER                      *
               &ABENDRG=,              REGISTER CONTAINING ABEND NUM   *
               &RC=,                   REGISTER CONTAINING ABEND CODE  *
               &VCONBR=            BRANCH TO VCON NOT TO TSTIO @ZA04126
.*********************************************************************
.**                                                                  *
.**                HEADER                                            */
.**DRIVER:
.**                E  MAIN DRIVER                                    */
.**                                                                  *
.*********************************************************************
         GBLB  &LISTSW                 SWITCH TO INDICATE LIST FORM
         GBLB  &STORESW                SW TO INDICATE PARM STRIPPING
         LCLC  &ADDRESS                VARIABLE TO CONTAIN THE STORE AD
         LCLC  &NUMID                  VARIABLE TO STRIP ID NUMBER
         LCLA  &IDCON                  VARIABLE TO CONTAIN THE ID NUM
         LCLA  &LENGTH                 VARIABLE TO COUNT CHARS IN PARM
&LISTSW  SETB  0                       INITIALIZE LIST SWITCH OFF
&STORESW SETB  0                       INITIALIZE STORE SWITCH OFF
&NUMID   SETC  '&ID'(4,2)              STRIP OFF THE LETTERS FROM ID NO
&IDCON   SETA  &NUMID                  TRANSFER VALUE FOR LATER USE
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,%CONT1)
.**                   HAS THE 'MF' PARAMETER BEEN CODED PROPERLY ?   */
.**                                                                  *
.*********************************************************************
         AIF   ('&MF(1)' EQ '').ERRORMF
         AIF   ('&MF(1)' EQ 'E').MFE
         AIF   ('&MF(1)' EQ 'L').MFL
.*********************************************************************
.**                                                                  *
.**                P  ISSUE AN ERROR MESSAGE                         */
.**                P  (,END)
.**                   SET CONDITION CODE = 12                        */
.**                                                                  */
.**                                                                  *
.*********************************************************************
         MNOTE 12,'SYNTAX ERROR ENCOUNTERED IN MF= PARM'
         AGO   .NOLINK
.ERRORMF MNOTE 12,'MF= PARAMETER OMITTED, BUT IS REQUIRED'
         AGO   .NOLINK
.ERORMFE MNOTE 12,'THE ADDRESS PORTION IS REQUIRED WITH THE EXECUTABLE *
               FORM'
         AGO   .NOLINK
.ERORMFA MNOTE 4,'THE ADDRESS PORTION IS INVALID WITH THE LIST FORM AND*
                IS IGNORNED'
         AGO   .IDROUT
.*********************************************************************
.**                                                                  *
.**%CONT1:
.**                P  SET THE MF TYPE SWITCHES ON                    */
.**                                                                  *
.*********************************************************************
.MFE     AIF   ('&MF(2)' EQ '').ERORMFE GO TO ERROR EXIT WITH MSG
&ADDRESS SETC  '&MF(2)'                PREPARE TO PROCESS ADDRESS
         AIF   ('&ADDRESS'(1,1) EQ '(').MFADREG TEST FOR REG NOT.
&LABEL   LA    1,&ADDRESS              ESTABLISH ADDRESSABILITY TO PARM
         AGO   .IDROUT                 CONTINUE PROCESSING NEXT OPERAND
.MFADREG ANOP                          LABEL FOR REG NOTATION OF ADDR
&LABEL   LR    1,&ADDRESS              ESTABLISH ADDRESSABILITY TO PARM
         AGO   .IDROUT                 CONTINUE PROCESSING NEXT OPERAND
.MFL     ANOP                          ROUTINE BRANCHED TO IF LIST FORM
&LISTSW  SETB  1                       TURN ON THE LIST FORM SWITCH
         DS    0H                      ALIGN TO HALFWORD BOUNDRY
         B     *+88                    BRANCH AROUND PARM LIST
         AIF   ('&MF(2)' NE '').ERORMFA  TEST FOR ADDR WITH L FORM
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,%CONT2)
.**                   IS THE USER'S ID NUMBER CODED CORRECTLY ?      */
.**                                                                  *
.*********************************************************************
.IDROUT  ANOP                          LABEL FOR 'ID=' OPERAND PROC
         AIF   ('&ID' EQ '').ERRORID   TEST FOR NULL PARM
&LENGTH  SETA  K'&ID                   COUNT LENGTH OF I.D. PARM
         AIF   (&LENGTH NE 5).ERRIDSZ  PUT OUT ERROR MESSAGE
         AIF   (&LISTSW EQ 1).IDLIST TEST IF THIS SHOULD BE DC OR MVI
         AGO   .IDE                    BRANCH TO MOVE ID INTO P.L. ROUT
.*********************************************************************
.**                                                                  *
.**                P  ISSUE A WARNING MESSAGE                        */
.**                P  (,%CONT3)
.**                   SET CONDITION CODE = 4 IF LIST FORM            */
.**                                                                  *
.*********************************************************************
.ERRIDSZ ANOP                          BRANCH HERE IF ID PARM HAS ERROR
         MNOTE 12,'THERE IS A SYNTAX ERROR IN THE ID= PARM'
         AGO   .NOLINK                 BRANCH TO RETURN TO THE USER
.ERRORID ANOP                          ROUTINE IF ID INCORRECT
         AIF   (&LISTSW EQ 1).ID4    IF THIS IS A LIST FORM SET CC=4
         MNOTE 4,'THE ID= PARM IS MISSING OR INCORRECT'
         AGO   .SELECT                 BRANCH TO CONTINUE PROCESSING
.ID4     MNOTE 0,'THE ID= PARM IS MISSING OR INCORRECT'
         AGO   .SELECT                 BRANCH TO CONTINUE PROCESSING
.*********************************************************************
.**                                                                  *
.**%CONT2:
.**                P  GENERATE THE INSTRUCTIONS NECESSARY TO
.**                   STORE THE USERS ID# IN THE P.L.                */
.**                                                                  *
.*********************************************************************
.IDE     ANOP                          ROUTINE TO MOVE ID # IN P.L.
         MVI   1(1),&IDCON             SET UP MOVE INSTRUCTION
         AGO   .SELECT                 CONTINUE PROCESSING
.IDLIST  ANOP                          ROUTINE TO CREATE 'DC'S FOR ID
&LABEL   DC    AL1(0)                  FILLER
         DC    AL1(&IDCON)             DC FOR CALLER'S ID # IN P.L.
.*********************************************************************
.**                                                                  *
.**%CONT3:
.**                D  (YES,,NO,%CONT4)
.**                   IS THIS REQUEST FOR DATA ?                     */
.**                                                                  *
.*********************************************************************
.SELECT  ANOP                          DETERMINE WHICH SERVICE REQ'D
         AIF   ('&TYPEREQ' NE 'DATA').SELECT2 TEST IF DATA REQUEST
.*********************************************************************
.**                                                                  *
.**                S  (,END)
.**                   CALL DATA: GENERATE INSTRUCIONS FOR DATA       */
.**                                                                  *
.*********************************************************************
         DATA  &DATAPTR,&DSNAME        CALL DATA REQ'T ROUTINE
         AGO   .END                    EXIT
.*********************************************************************
.**                                                                  *
.**%CONT4:
.**                D  (YES,,NO,%CONT5)
.**                   IS THIS REQUEST FOR AN SVC ERROR MESSAGE ?     */
.**                                                                  *
.*********************************************************************
.SELECT2 ANOP                          TEST FOR SVCERR REQUEST ROUT
         AIF   ('&TYPEREQ' NE 'SVCERR').SELECT3 CONTINUE IF NOT SVCERR
.*********************************************************************
.**                                                                  *
.**                S  (,END)
.**                   CALL SVCMSG: GENERATE INSTRUCTIONS FOR SVC MSG */
.**                                                                  *
.*********************************************************************
         SVCERR &SVC,&ABENDRG,&RC,&INST11 CALL SVCERR REQ'T ROUTINE
         AGO   .END                    EXIT
.*********************************************************************
.**                                                                  *
.**%CONT5:
.**                D  (YES,,NO,%CONT6)
.**                   IS THIS REQUEST FOR A MESSAGE ?                */
.**                                                                  *
.*********************************************************************
.SELECT3 ANOP                          TEST FOR MSG REQUEST ROUTINE
         AIF   ('&TYPEREQ' NE 'MSG').SELECT4 CONTINUE IF NOT MSG
.*********************************************************************
.**                                                                  *
.**                S  (,END)
.**                   CALL MSG: GENERATE INSTRUCTIONS FOR MSG        */
.**                                                                  *
.*********************************************************************
         MSG   &FIRST,&SECOND,&INST11,&INST12,&INST13,&INST14,&INST15, *
               &INST16,&INST21,&INST22,&INST23,&INST24,&INST25,&INST26
         AGO   .END                    EXIT
.*********************************************************************
.**                                                                  *
.**%CONT6:
.**                D  (YES,,NO,%CONT7)
.**                   IS THIS REQUEST FOR A PROMPTING MESSAGE ?      */
.**                                                                  *
.*********************************************************************
.SELECT4 ANOP                          TEST FOR PTGT REQUEST ROUTINE
         AIF   ('&TYPEREQ' NE 'PTGT').ERSLECT CONTINUE IF NOT MSG
.*********************************************************************
.**                                                                  *
.**                S  (,END)
.**                   CALL PTGT: GENERATE INSTRUCTIONS FOR PTGT MSG  */
.**                                                                  *
.*********************************************************************
         PTGT  &FIRST                  INVOKE THE INTERNAL SUBROUTINE
         AGO   .END                    EXIT
.*********************************************************************
.**                                                                  *
.**%CONT7:
.**                P  ISSUE AN ERROR MESSAGE                         */
.**                P  (,NOLINK)
.**                   SET CONDITION CODE TO 12                       */
.**                                                                  *
.*********************************************************************
.ERSLECT ANOP                          COME HERE IF SYNTAX IN &TYPEREQ
         MNOTE 12,'MISSING OR SYNTAX ERROR IN OPTION KEYWORD'
         AGO   .NOLINK                 BRANCH TO EXIT
.*********************************************************************
.**                                                                  *
.**END:
.**                D  (NO,,YES,NOLINK)
.**                   HAS THE LIST FORM BEEN REQUESTED ?             */
.**                                                                  *
.*********************************************************************
.END     ANOP                          ENDING ROUTINE FOR ENTIRE MACRO
         AIF   (&LISTSW EQ 1).NOLINK   BRANCH AROUND THE BRANCH & LINK
.*********************************************************************
.**                                                                  *
.**                P  GENERATE THE BRANCH & LINK INSTRUCTION TO I/O  */
.**                                                                  *
.*********************************************************************
         AIF   ('&VCONBR' NE 'YES').BALR
         L     15,@IOVCON               ADDR TO BR TO        @ZA04126
         BALR  14,15                    ENTER THE IO ROUTINE @ZA04126
         AGO      .NOLINK
.BALR    ANOP
         L     15,TSTIO                ESTABLISH THE ADDRESS TO IO
         BALR  14,15                   BRANCH TO I/O
.*********************************************************************
.**                                                                  *
.**NOLINK:
.**                R  RETURN CONTROL                                 */
.**                                                                  *
.*********************************************************************
.NOLINK  MEND                          RETURN CONTROL TO USER
         MACRO
         DATA  &DATAPTR,&DSNAME
.*********************************************************************
.**                                                                  *
.**DATA:
.**                E  DATA REQUEST ROUTINE                           */
.**                                                                  *
.*********************************************************************
         GBLB  &LISTSW
.*********************************************************************
.**                                                                  *
.**                P  SET UP INSTRUCTION NECESSARY TO STORE          */
.**                   DATA REQUEST # IN P.L.                         */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW EQ 1).LISTTYP  FIND OUT WHICH FORM E OR L
         MVI   0(1),1                  STORE TYPE REQT IN P. L. (DATA)
         AGO   .NEXTSTP                BRANCH AND CONTINUE
.LISTTYP ANOP                          LABEL FOR DC TYPE INSTRUCTION
         DC    AL2(0)                  FILLER
.*********************************************************************
.**                                                                  *
.**                D  (YES,,NO,%CONT8)
.**                   IS THE 'DATAPTR' PARAMETER NULL                */
.**                                                                  *
.*********************************************************************
.NEXTSTP ANOP                          LABEL USED TO SKIP DC TYPE INST
         AIF   ('&DATAPTR' NE '').STOREDP  TEST FOR REQD PARM DATAPTR
.*********************************************************************
.**                                                                  *
.**                P  ISSUE WARNING MESSAGE                          */
.**                                                                  *
.*********************************************************************
         MNOTE 0,'THE DATAPTR KEYWORD IS REQUIRED, UNLESS A NULL LINE  *
               HAS BEEN REQUESTED'
.*********************************************************************
.**                                                                  *
.**%CONT8:
.**                S  CALL STORE: TO INSERT DATAPTR, DSNAME
.**                   AND FILLER INTO P.L.                           */
.**                                                                  *
.*********************************************************************
.STOREDP ANOP                          BRANCH HERE IF &DATAPTR CODED
         STORE 4,&DATAPTR,4            PREPARE INST TO INSERT INTO P.L.
         STORE 8,&DSNAME,4             PREPARE INST TO INSERT INTO P.L.
         STORE 12,,72                  IF LIST FORM PREPARE DC'S ZERO
.*********************************************************************
.**                                                                  *
.**%CONT9:
.**                R  RETURN TO THE DRIVER                           */
.**                                                                  *
.*********************************************************************
         MEND
         MACRO
         SVCERR &SVC,&ABENDRG,&RC,&INST11
.*********************************************************************
.**                                                                  *
.**SVC:
.**                E  SVC BUILD PARM ROUTINE                         */
.**                                                                  *
.*********************************************************************
         GBLB  &LISTSW                 SWITCH FOR LIST FORM OF MF
         GBLB  &STORE                  SW FOR STORE TO STRIP NUM DOWN
.*********************************************************************
.**                                                                  *
.**                P  SET UP INST NECESSARY TO STORE
.**                   SVC REQUEST # IN P.L.                          */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW EQ 1).NEXTSTP  TEST FOR LIST FORM INST
         MVI   0(1),3                  STORE SVCERR REQUEST NUM IN P.L.
.*********************************************************************
.**                                                                  *
.**                D  (YES,,NO,%CONT10)
.**                   ARE THE PARAMETERS CODED CORRECTLY ?           */
.**                P  ISSUE WARNING MESSAGE IF INCORRECT             */
.**                                                                  *
.*********************************************************************
.NEXTSTP ANOP                          THIS PORTION CHECKS PARMS
         AIF   ('&ABENDRG' EQ '').CONT1  BRANCH TO CONTINUE IF NULL
         AIF   ('&ABENDRG'(1,1) EQ '(').CONT1  BRANCH TO CONTINUE IF OK
         MNOTE 12,'REGISTER NOTATION IS REQUIRED WITH THE ABENDRG PARM'
.CONT1   ANOP                          BRANCH HERE IF ABENDRG OK
         AIF   ('&RC' EQ '').CONT2     BRANCH TO CONTINUE IF NULL
         AIF   ('&RC'(1,1) EQ '(').CONT2  BRANCH TO CONTINUE IF OK
         MNOTE 12,'REGISTER NOTATION IS REQUIRED WITH THE RC PARM'
.CONT2   ANOP                          BRANCH HERE IF RC PARM OK
         AIF   ('&SVC' NE '').STORSVC  TEST FOR NULL PARM
         MNOTE 0,'THE SVC KEYWORD IS REQUIRED WITH THE SVCERR OPTION'
.*********************************************************************
.**                                                                  *
.**%CONT10:
.**                S  CALL STORE:  TO INSERT &ABENDRG, &SVC,
.**                   &RC AND FILLER INTO P.L.                       */
.**                                                                  *
.*********************************************************************
.STORSVC ANOP                          BRANCH HERE IF &SVC CODED
         STORE 2,,2                    PREPARE FILLER IF LIST FORM
         STORE 4,&ABENDRG,4            PREPARE INST TO INSERT INTO P.L.
         STORE 8,&RC,4                 PREPARE INST TO INSERT INTO P.L.
         STORE 12,,4                   PREPARE FILLER IF LIST FORM
         STORE 16,&SVC,4               PREPARE INST TO INSERT INTO P.L.
         STORE 20,,26                  PREPARE FILLER IF LIST FORM
.*********************************************************************
.**                                                                  *
.**                S  CALL INSTTYP: PROCESS TYPE INSERT              */
.**                                                                  *
.*********************************************************************
         TYPE 46,&INST11
.*********************************************************************
.**                                                                  *
.**                S  CALL STORE: PREPARE INST TO STORE
.**                   FILLER INTO P.L.                               */
.**                                                                  *
.*********************************************************************
         STORE 52,,8                   PREPARE FILLER IF LIST FORM
.*********************************************************************
.**                                                                  *
.**                S  CALL INSTWHR: PROCESS WHERE PORTION OF INSERT  */
.**                                                                  *
.*********************************************************************
         WHERE 60,&INST11
.*********************************************************************
.**                                                                  *
.**%CONT13:
.**                R  RETURN TO DRIVER                               */
.**                                                                  *
.*********************************************************************
         MEND                          END OF SVCERR ROUTINE
         MACRO
         MSG   &FIRST,&SECOND,&INST11,&INST12,&INST13,&INST14,&INST15, *
               &INST16,&INST21,&INST22,&INST23,&INST24,&INST25,&INST26
.*********************************************************************
.**                                                                  *
.**MSG:
.**                E  MSG ROUTINE                                    */
.**                                                                  *
.*********************************************************************
         GBLB  &LISTSW                 SWITCH IF LIST FORM USED
         GBLB  &STORESW                SW INDICATES TO STORE STRIP NUM
.*********************************************************************
.**                                                                  *
.**                P  SET UP INST NECESSARY TO
.**                   STORE MSG REQ # IN P.L.                        */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW EQ 1).LISTTYP  TEST WHICH FORM MACRO L OR E
         MVI   0(1),2                  MOVE CODE FOR MSG INTO P.L.
.*********************************************************************
.**                                                                  *
.**                D  (YES,,NO,%CONT14)
.**                   IS THE 'FIRST' PARAMETER NULL ?                */
.**                                                                  *
.*********************************************************************
.LISTTYP ANOP                          LABEL USED BRANCH AROUND E INST
         AIF   ('&FIRST' NE '').NEXTSTP TEST IF USER CODED FIRST PARM
.*********************************************************************
.**                                                                  *
.**                P  ISSUE AN WARNING MSG                           */
.**                                                                  *
.*********************************************************************
         MNOTE 0,'THE FIRST PARM IS REQUIRED WITH THE MSG OPTION'
.*********************************************************************
.**                                                                  *
.**%CONT14:
.**                S  CALL INSTTYP: SET UP INST TO INSERT
.**                   TYPES INTO P. L.                               */
.**                                                                  *
.*********************************************************************
.NEXTSTP ANOP                          ROUTINE TO PLACE TYPES IN LIST
         INSTTYP 2,&INST11,&INST12,&INST13,&INST14,&INST15,&INST16
         STORE 8,,8                    PREPARE FILLER IF LIST FORM
.*********************************************************************
.**                                                                  *
.**%CONT17:
.**                S  CALL STORE: STORE &FIRST INTO PARM LIST        */
.**                                                                  *
.*********************************************************************
&STORESW SETB  1                       TURN ON STRIP SWITCH FOR STORE
         STORE 16,&FIRST,4             PREPARE INST TO STORE IN LIST
.*********************************************************************
.**                                                                  *
.**                S  CALL INSTWHR: PROCESS WHERE PORTION OF INSERTS */
.**                                                                  *
.*********************************************************************
         INSTWHR 20,&INST11,&INST12,&INST13,&INST14,&INST15,&INST16
         STORE 44,,2                   PREPARE FILLER IF LIST FORM
.*********************************************************************
.**                                                                  *
.**                S  CALL INSTTYP: PROCESS TYPE INSERT OF 2ND LEV   */
.**                                                                  *
.*********************************************************************
         INSTTYP 46,&INST21,&INST22,&INST23,&INST24,&INST25,&INST26
.*********************************************************************
.**                                                                  *
.**                S  CALL STORE: PREPARE INST TO STORE
.**                   &SECOND AND FILLER INTO P.L.                   */
.**                                                                  *
.*********************************************************************
         STORE 52,,4                   PREPARE FILLER IF LIST FORM
&STORESW SETB  1                       TURN ON STRIP SWITCH FOR STORE
         STORE 56,&SECOND,4            PREPARE INST TO STORE &SECOND
.*********************************************************************
.**                                                                  *
.**                S  CALL INSTWHR: PROCESS WHERE PORTION OF INSERTS */
.**                                                                  *
.*********************************************************************
         INSTWHR 60,&INST21,&INST22,&INST23,&INST24,&INST25,&INST26
.*********************************************************************
.**                                                                  *
.**%CONT18:
.**                R  RETURN TO DRIVER                               */
.**                                                                  *
.*********************************************************************
         MEND                          END OF MESSAGE PROCESSOR ROUTINE
         MACRO
         PTGT  &FIRST
.*********************************************************************
.**                                                                  *
.**                E  PTGT ROUTINE                                   */
.**                                                                  *
.*********************************************************************
         GBLB  &LISTSW                 SWITCH FOR LIST FORM
         GBLB  &STORESW                SWITCH FOR STORE TO STRIP NUM
.*********************************************************************
.**                                                                  *
.**                P  SET NECESSARY INST TO STORE
.**                   PTGT # INTO P.L.                               */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW EQ 1).LISTTYP  TEST FOR LIST FORM
         MVI   0(1),4                  STORE CODE NUM FOR PTGT REQT
.*********************************************************************
.**                                                                  *
.**                D  (YES,,NO,%CONT19)
.**                   IS THE 'FIRST' PARAMETER NULL ?                */
.**                                                                  *
.*********************************************************************
.LISTTYP ANOP                          BRANCH TO IF LIST FORM
         AIF   ('&FIRST' NE '').STOR1ST TEST FOR NULL PARM
.*********************************************************************
.**                                                                  *
.**                P  ISSUE AN WARNING MESSAGE                       */
.**                                                                  *
.*********************************************************************
         MNOTE 0,'THE FIRST KEYWORD IS REQUIRED WITH THE PTGT OPTION'
.*********************************************************************
.**                                                                  *
.**%CONT19:
.**                S  CALL STORE: STORE PARMS AND FILLERS INTO P.L.  */
.**                                                                  *
.*********************************************************************
.STOR1ST ANOP                          ROUTINE TO PREPARE P.L.
         STORE 2,,14                   PREPARE FILLER IF LIST FORM
&STORESW SETB  1                       TURN ON STRIP SWITCH
         STORE 16,&FIRST,4             PREPARE INST TO STORE &FIRST
         STORE 20,,64                  PREPARE FILLER IF LIST FORM
.*********************************************************************
.**                                                                  *
.**%CONT20:
.**                R  RETURN TO DRIVER                               */
.**                                                                  *
.*********************************************************************
         MEND                          END OF PTGT PROCESSOR ROUTINE
         MACRO
         INSTTYP &START,&IN1,&IN2,&IN3,&IN4,&IN5,&IN6
.*********************************************************************
.**                                                                  *
.**INSTTYP:
.**                E  ROUTINE FOR INSERT TYPE PORTION                */
.**                                                                  *
.*********************************************************************
         LCLA  &DISP                   VARIABLE TO CONTAIN DISPLACEMENT
&DISP    SETA  &START                  INITIALIZE DISPLACEMENT COUNTER
.*********************************************************************
.**                                                                  *
.**                P  PREPARE EACH INSERT TO BE PROCESSED
.**                   BY TYPE PROCESSOR                              */
.**                                                                  *
.*********************************************************************
         TYPE  &DISP,&IN1              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+1                 INCREMENT DISPLACEMENT COUNTER
         TYPE  &DISP,&IN2              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+1                 INCREMENT DISPLACEMENT COUNTER
         TYPE  &DISP,&IN3              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+1                 INCREMENT DISPLACEMENT COUNTER
         TYPE  &DISP,&IN4              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+1                 INCREMENT DISPLACEMENT COUNTER
         TYPE  &DISP,&IN5              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+1                 INCREMENT DISPLACEMENT COUNTER
         TYPE  &DISP,&IN6              CALL TYPE ROUTINE TO STORE PARM
.*********************************************************************
.**                                                                  *
.**                R  RETURN TO CONTINUE PROCESSING                  */
.**                                                                  *
.*********************************************************************
         MEND                          END OR TYPE PROCESSOR OF INSERTS
         MACRO
         TYPE  &DISP,&IN               INTERNAL ROUTINE / TYPE PORTION
.*********************************************************************
.**                                                                  *
.**TYPE:           E  ROUTINE TO DETERMINE WHICH TYPE                */
.**                                                                  *
.*********************************************************************
         LCLA  &TYINST                 VARIABLE FOR CODE OF INSERT
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,NULLTYP)
.**                   IS THIS INSERT NULL ?                          */
.**                                                                  *
.*********************************************************************
         AIF   ('&IN' EQ '').NULLTYP   BRANCH TO NULL PROCESSING
.*********************************************************************
.**                                                                  *
.**                P  DETERMINE WHICH TYPE REQ'T (PDE,NUM,ETC)       */
.**                                                                  *
.*********************************************************************
         AIF   ('&IN(1)' NE 'APDE').CONT1 TEST IF APDE TYPE INSERT
&TYINST  SETA  6                       SET CODE VARIABLE / INDICATE PDE
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT1   ANOP                          BRANCHED TO IF NOT PDE
         AIF   ('&IN(1)' NE 'NUM').CONT2 TEST IF NUM TYPE INSERT
&TYINST  SETA  5                       SET CODE VARIABLE / INDICATE PDE
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT2   ANOP                          BRANCHED TO IF NOT PDE
         AIF   ('&IN(1)' NE 'REG').CONT3 TEST IF REG TYPE INSERT
&TYINST  SETA  1                       SET CODE VARIABLE / INDICATE REG
         AIF   ('&IN(2)'(1,1) EQ '(').CONT21  TEST FOR REG NOTATION
         MNOTE 12,'WITH THE REG TYPE INSERT ONLY REG NOTATION ALLOWED'
.CONT21  ANOP                          BRANCH HERE IF SYNTAX OK
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT3   ANOP                          BRANCH TO IF NOT REG TYPE
         AIF   ('&IN(1)' NE 'ADDR').CONT4 TEST IF ADDR TYPE INSERT
&TYINST  SETA  2                       SET CODE VAR / INDICATE ADDR
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT4   ANOP                          BRANCH TO IF NOT ADDR TYPE
         AIF   ('&IN(1)' NE 'ADDRCONX').CONT5 TEST ADDRCONX TYPE INSERT
&TYINST  SETA  3                       SET CODE VAR / INDICATE ADDR
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT5   ANOP                          BRANCH TO IF NOT ADDR TYPE
         AIF   ('&IN(1)' NE 'ADDRCOND').CONT6 TEST ADDRCOND TYPE INSERT
&TYINST  SETA  4                       SET CODE VAR / INDICATE ADDR
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.CONT6   ANOP                          BRANCH TO IF NOT ADDRCONX TYPE
         AIF   ('&IN(1)' NE 'VPDE').ERROR TEST VPDE TYPE INSERT
&TYINST  SETA  7                       SET CODE VAR / INDICATE ADDR
         AGO   .PROCESS                BRANCH TO STORE INTO LIST
.*********************************************************************
.**                                                                  *
.**                P  IF THERE IS A SYNTAX ERROR - ISSUE WARNING MSG */
.**                                                                  *
.*********************************************************************
.ERROR   ANOP                          COME HERE IF SYNTAX ERROR FOUND
         MNOTE 12,'THE TYPE PORTION OF AN INSERT HAS A SYNTAX ERROR'
&TYINST  SETA  0                       SET CODE TO NULL VALUE
.PROCESS ANOP                          THIS PORTION CALLS STORE
.*********************************************************************
.**                                                                  *
.**                S  (,ENDTYP)
.**                   CALL STORE: STORE CODE INTO P. L.              */
.**                                                                  *
.*********************************************************************
         STORE &DISP,&TYINST,1         CALL STORE TO PREPARE INST
         AGO   .ENDTYP                 BRANCH TO END TO RETURN
.*********************************************************************
.**                                                                  *
.**NULLTYP:
.**                S  CALL STORE: STORE FILLER ZEROS IF L FORM       */
.**                                                                  *
.*********************************************************************
.NULLTYP ANOP                          BRANCH HERE IF INSERT PARM NULL
         STORE &DISP,,1                CALL STORE FOR FILLER
.*********************************************************************
.**                                                                  *
.**ENDTYP:         R  RETURN TO INSTTYP                              */
.**                                                                  *
.*********************************************************************
.ENDTYP  ANOP                          BRANCH HERE TO EXIT ROUTINE
         MEND                          END OF TYPE ROUTINE
         MACRO
         INSTWHR &START,&IN1,&IN2,&IN3,&IN4,&IN5,&IN6
.*********************************************************************
.**                                                                  *
.**INSTWHR:
.**                E  ROUTINE FOR INSERT WHERE PORTION               */
.**                                                                  *
.*********************************************************************
         LCLA  &DISP                   VARIABLE TO CONTAIN DISPLACEMENT
&DISP    SETA  &START                  INITIALIZE DISPLACEMENT COUNTER
.*********************************************************************
.**                                                                  *
.**                P  PREPARE EACH INSERT TO BE PROCESSED
.**                   BY WHERE PROCESSOR                             */
.**                                                                  *
.*********************************************************************
         WHERE &DISP,&IN1              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+4                 INCREMENT DISPLACEMENT COUNTER
         WHERE &DISP,&IN2              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+4                 INCREMENT DISPLACEMENT COUNTER
         WHERE &DISP,&IN3              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+4                 INCREMENT DISPLACEMENT COUNTER
         WHERE &DISP,&IN4              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+4                 INCREMENT DISPLACEMENT COUNTER
         WHERE &DISP,&IN5              CALL TYPE ROUTINE TO STORE PARM
&DISP    SETA  &DISP+4                 INCREMENT DISPLACEMENT COUNTER
         WHERE &DISP,&IN6              CALL TYPE ROUTINE TO STORE PARM
.*********************************************************************
.**                                                                  *
.**                R  RETURN TO CONTINUE PROCESSING                  */
.**                                                                  *
.*********************************************************************
         MEND
         MACRO
         WHERE &DISP,&IN
.*********************************************************************
.**                                                                  *
.**WHERE:          E  ROUTINE PROCESS WHERE PORTION OF ONE INSERT    */
.**                                                                  *
.*********************************************************************
         GBLB  &STORESW                SWITCH - INDICATE STRIP NUM
         LCLC  &WHERE                  LABEL USED TO OBTAIN WHERE PARM
&WHERE   SETC  '&IN(2)'                STRIP OFF WHERE PORTION OF INST
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,NULLWHR)
.**                   IS THIS INSERT NULL ?                          */
.**                                                                  *
.*********************************************************************
         AIF   ('&IN' EQ '').NULLWHR   BRANCH TO NULL PROCESSING
.*********************************************************************
.**                                                                  *
.**                P  ISSUE A WARNING MSG IF WHERE PORTION IS NULL   */
.**                                                                  *
.*********************************************************************
         AIF   ('&WHERE' NE '').CONT   TEST FOR NULL PORTION
         MNOTE 12,'AN INSERT WHERE PORTION IS MISSING OR MISSPELLED'
.*********************************************************************
.**                                                                  *
.**                P  TURN ON THE STRIP SW IF 'NUM' IS THE TYPE      */
.**                                                                  *
.*********************************************************************
.CONT    ANOP                          THIS PORTION PREPARE TO STORE
&STORESW SETB  ('&IN(1)' EQ 'NUM')     TURN ON STRIP SWITCH IF I/O NUM
.*********************************************************************
.**                                                                  *
.**                S  (,ENDWHR)
.**                   CALL STORE: HAVE WHERE PORTION PLACED IN P.L.  */
.**                                                                  *
.*********************************************************************
         STORE &DISP,&WHERE,4          CALL STORE - STORE WHERE PORTION
         AGO   .ENDWHR                 BRANCH TO END FOR RETURN
.*********************************************************************
.**                                                                  *
.**NULLWHR:
.**                S  CALL STORE: STORE FILLER ZEROS IF L FORM       */
.**                                                                  *
.*********************************************************************
.NULLWHR ANOP                          BRANCH HERE IF INSERT PARM NULL
         STORE &DISP,,4                CALL STORE FOR FILLER
.*********************************************************************
.**                                                                  *
.**ENDWHR:         R  RETURN TO PROCESSING                           */
.**                                                                  *
.*********************************************************************
.ENDWHR  ANOP                          BRANCH HERE TO EXIT
         MEND                          END OF INSTWHR ROUTINE
         MACRO
         STORE &DISP,&PARM,&LEN        ROUT. TO PREPARE INST FOR LIST
.*********************************************************************
.**                                                                  *
.**STORE:
.**                E  STORE DATA IN PARM LIST                        */
.**                                                                  *
.*********************************************************************
         GBLB  &STORESW                SW TO INDICATE STRIPPING
         GBLB  &LISTSW                 SW TO INDICATE USER USED L FORM
         LCLC  &PARMC                  VARIABLE TO STRIP PARM DOWN
         LCLA  &PARMN                  VARIABLE TO CHANGE ATTRIBUTES
         LCLA  &LENGTH                 VARIABLE TO COUNT CHARS IN PARM
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,START)
.**                   IS THIS A LIST FORM ?                          */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW EQ 1).START    AVOID NEXT TEST IF LIST FORM
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,RETURN)
.**                   IS THIS A NULL PARM ?                          */
.**                                                                  *
.*********************************************************************
         AIF   ('&PARM' EQ '').RETURN  TEST FOR NULL PARM (ONLY E FORM)
.*********************************************************************
.**                                                                  *
.**START:          D  (YES,,NO,PROCESS)
.**                   IS STRIP SWITCH ON ?                           */
.**                                                                  *
.*********************************************************************
.START   ANOP                          BRANCH HERE IF LIST FORM
         AIF   (&STORESW NE 1).PROCESS SEE IF THERE IS NO NEED TO STRIP
         AIF   ('&PARM' EQ '').PROCESS IF NULL DO NOT PREPARE TO STRIP
.*********************************************************************
.**                                                                  *
.**                D  (NO,,YES,PROCESS)
.**                   TEST FOR REGISTER NOTATION                     */
.**                                                                  *
.*********************************************************************
         AIF   ('&PARM'(1,1) EQ '(').PROCESS TEST FOR REGISTER NOTATION
.*********************************************************************
.**                                                                  *
.**                P  ISSUE A WARNING MESSAGE IF
.**                   ERROR FOUND IN NUMBER TO BE STRIPPED           */
.**                                                                  *
.*********************************************************************
&LENGTH  SETA  K'&PARM                 COUNT PARM LENGTH FOR POSS ERROR
         AIF   (&LENGTH EQ 5).OK       TEST FOR SYNTAX ERROR BY USER
         MNOTE 12,'A SYNTAX ERROR FOUND IN A MESSAGE OR INSERT NUMBER'
.*********************************************************************
.**                                                                  *
.**OK:             P  STRIP OFF NUMBER PORTION OF PARM               */
.**                                                                  *
.*********************************************************************
.OK      ANOP                          BRANCH HERE IF NUM SEEMS CORRECT
&PARMC   SETC  '&PARM'(3,3)            STRIP OF NUM PORTION
&PARMN   SETA  &PARMC                  CHANGE ATTRIBUTE / CHAR TO ARTH
.*********************************************************************
.**                                                                  *
.**                D  (YES,,NO,BLDINST)
.**                   IS THIS THE LIST FORM WHICH REQS DC'S          */
.**                                                                  *
.*********************************************************************
         AIF   (&LISTSW NE 1).BLDINST  SEE IF PL IN DYNAMIC STORAGE
.*********************************************************************
.**                                                                  *
.**                P  (,RETURN)
.**                   BUILD DC FOR LIST FORM P.L.                    */
.**                                                                  *
.*********************************************************************
         DC    AL&LEN.(&PARMN)         PLACEMENT OF PARAMETER INTO LIST
         AGO   .RETURN                 BRANCH TO EXIT
.*********************************************************************
.**                                                                  *
.**BLDINST:        P  (,RETURN)
.**                   BUILD THE INSTURCTION NECESSARY TO STORE
.**                   INTO A DYNAMIC AREA                            */
.**                                                                  *
.*********************************************************************
.BLDINST ANOP                          BRANCH HERE FOR EXECUTABLE FORM
         AIF   (&LEN EQ 1).BLDMVI1     SEE IF ONE BYTE TYPE INST NEEDED
         LA    14,&PARMN               GET PARAMETER INTO WORK REGISTER
         ST    14,&DISP.(1)            STORE PARAMETER INTO PARM LIST
         AGO   .RETURN                 BRANCH TO EXIT
.BLDMVI1 ANOP                          BRANCH HERE IF ONE BYTE LENGTH
         MVI   &DISP.(1),&PARMN        MOVE PARM INTO PARAMETER LIST
         AGO   .RETURN                 BRANCH TO EXIT
.*********************************************************************
.**                                                                  *
.**PROCESS:        P  PREPARE DC OR INSTRUCTIONS TO BUILD P.L.       */
.**                                                                  *
.*********************************************************************
.PROCESS ANOP                          BRANCH HERE / STANDARD STORE REQ
         AIF   (&LISTSW NE 1).INSTBLD  TEST FOR EXECUTABLE FORM REQT
         AIF   ('&PARM' NE '').DCPARM  TEST FOR FILLER REQUEST
         DC    &LEN.AL1(0)             FILLER OF HEX ZEROS
         AGO   .RETURN                 BRANCH TO EXIT
.DCPARM  ANOP                          BRANCH HERE FOR PREPARATION DC'S
         AIF   ('&PARM'(1,1) NE '(').CONTDC ENSURE USER HAS NOT USED RG
         MNOTE 12,'REGISTER NOTATION IS NOT PERMITTED WITH THE LIST FOR*
               M (MF=L)'
.CONTDC  DC    AL&LEN.(&PARM)          PLACEMENT OF PARAMETER INTO LIST
         AGO   .RETURN                 BRANCH TO EXIT
.INSTBLD ANOP                          BRANCH HERE FOR DYNAMIC LIST
         AIF   (&LEN NE 1).CONTI2      TEST FOR ONE BYTE TYPE INST
         AIF   ('&PARM'(1,1) NE '(').CONTI1 TEST FOR REG NOTATION
         STC   &PARM,&LEN.(1)          STORE PARM INTO LIST
         AGO   .RETURN                 BRANCH TO EXIT
.CONTI1  ANOP                          BRANCH HERE FOR MVI TYPE INST
         MVI   &DISP.(1),&PARM         MOVE VALUE INTO LIST
         AGO   .RETURN                 BRANCH TO EXIT
.CONTI2  ANOP                          BRANCH HERE IF NOT ONE BYTE
         AIF   ('&PARM'(1,1) EQ '(').REGPARM  IF REG USE STORE INST
         LA    14,&PARM                GET PARM INTO WORK REGISTER
         ST    14,&DISP.(1)            STORE PARM INTO PARAMETER LIST
         AGO   .RETURN                 BRANCH TO EXIT
.REGPARM ANOP                          BRANCH HERE IF REG NOTATION
         ST    &PARM,&DISP.(1)         STORE PARM INTO PARAMETER LIST
.*********************************************************************
.**                                                                  *
.**RETURN:         R  RETURN TO CALLER AND CONTINUE PROCESSING       */
.**                                                                  *
.*********************************************************************
.RETURN  ANOP                          EXIT ROUTINE FOR STORE
&STORESW SETB  0                       TURN OFF STRIP SW FOR NEXT PARM
         MEND                          END OF STORE ROUTINE
         PRINT ON
./ ADD NAME=IKJEGSUB                               17OCT89 12.47.53
         MACRO
         IKJEGSUB &CSECT=IBM
         LCLA  &A,&B,&C,&D,&E,&F,&G
         LCLC  &CSNAM,&SUBCMD,&ABBR,&LDNAM,&LABEL,&LABEL1,&LABEL2,&NMBR
         LCLC  &LABEL0,&INDEX
&F       SETA  1              INDICATE FIRST SUBCOMMAND ID.
         AIF   ('&CSECT' NE 'IBM').CONT0
&CSNAM   SETC  'IKJEGSCD'     DEFINE CSECT NAME FOR IBM TABLE.
IKJEGSCD CSECT
         ENTRY IKJEGAID
         ENTRY IKJEGEID
         ENTRY IKJEGPID
         AGO   .CONT1         BRANCH TO GENERATE COMMENTS.
.CONT0   ANOP
         AIF   ('&CSECT' NE 'USER').ERROR2
&CSNAM   SETC  'IKJEGSCU'     DEFINE CSECT NAME FOR USER TABLE.
IKJEGSCU CSECT
&F       SETA  &F+128         INDICATE USER SUBCOMMAND ID.
.CONT1   ANOP
         SPACE 2
***********************************************************************
*                                                                     *
*        THE IKJEGSUB MACRO IS USED TO GENERATE THE TABLE OF IBM      *
*        SUPPLIED SUBCOMMAND PROCESSORS AND/OR THE TABLE OF USER      *
*        WRITTEN SUBCOMMAND PROCESSORS.  BOTH TABLES, EVEN THOUGH     *
*        THE USER TABLE MAY BE EMPTY, MUST BE LINKAGE EDITED WITH     *
*        IKJEGMNL.                                                    *
*                                                                     *
*        THE FORMAT OF A TABLE ENTRY IS AS FOLLOWS--                  *
*                                                                     *
*        .......................................................      *
*        .        .        .        .        .        .        .      *
*        .  NAME  . SUBCMD .  ABBR  .  ABBR  . LDMOD  . SUBCMD .      *
*        . LENGTH .  NAME  . LENGTH .  NAME  .  NAME  .   ID   .      *
*        .        .        .        .        .        .        .      *
*        .......................................................      *
*        +0       +1       +(M+1)   +(M+2)   +(M+N+2) +(M+N+10)       *
*                                                                     *
         AIF   ('&CSNAM' EQ 'IKJEGSCU').CONT2
*        STATUS -- VERSION NO. 01 - OS/VS2 RELEASE NO. 01             *
*                                                                     *
*        CHANGE LEVEL -- 04 - DATE 04/25/72                           *
*                                                                     *
.CONT2   ANOP
***********************************************************************
&A       SETA  N'&SYSLIST     DETERMINE NUMBER OF SUBCOMMANDS.
         AIF   (&A EQ 0).CONT6 IF NO SUBCOMMANDS, BRANCH TO
.*                            GENERATE END OF TABLE INDICATOR.
&B       SETA  1              SUBLIST INDEX.
.CONT3   ANOP
&C       SETA  N'&SYSLIST(&B)
         AIF   (&C NE 3).ERROR1 VERIFY THAT SUBCMD
.*                            NAME AND LOAD NAME SPECIFIED.
         AIF   ('&CSNAM' EQ 'IKJEGSCD').CONT33
&INDEX   SETC  '&F'
&LABEL0  SETC  '&CSNAM'(6,3).'&INDEX'.'00'
&LABEL1  SETC  '&CSNAM'(6,3).'&INDEX'.'01'
&LABEL2  SETC  '&CSNAM'(6,3).'&INDEX'.'02'
         AGO   .CONT37
.CONT33  ANOP
&E       SETA  K'&SYSLIST(&B,&C)
&D       SETA  &E-2
&G       SETA  3
         AIF   (&E EQ 8).CONT35
&G       SETA  2
&D       SETA  &E-1
.CONT35  ANOP
&LABEL0  SETC  '&CSNAM'(6,3).'&SYSLIST(&B,&C)'(&D,&G).'00'
&LABEL1  SETC  '&CSNAM'(6,3).'&SYSLIST(&B,&C)'(&D,&G).'01'
&LABEL2  SETC  '&CSNAM'(6,3).'&SYSLIST(&B,&C)'(&D,&G).'02'
.CONT37  ANOP
&SUBCMD  SETC  '&SYSLIST(&B,1)'
         SPACE 2
&LABEL0  EQU   *
         DC    AL1(&LABEL1-*-1) - LENGTH OF SUBCOMMAND NAME.
         DC    C'&SUBCMD' -   SUBCOMMAND NAME.
&LABEL1  EQU   *
         DC    AL1(&LABEL2-*-1) - LENGTH OF ABBREVIATION.
         AIF   (K'&SYSLIST(&B,2) EQ 0).CONT4
&ABBR    SETC  '&SYSLIST(&B,2)'
         DC    C'&ABBR' -     ABBREVIATION FOR SUBCOMMAND NAME.
.CONT4   ANOP
&LABEL2  EQU   *
&LDNAM   SETC  '&SYSLIST(&B,3)'
         DC    CL8'&LDNAM' -  LOAD MODULE NAME.
         DC    AL1(&F) -      MODULE IDENTIFICATION BYTE.
         AIF   (&B EQ &A).CONT6
&B       SETA  &B+1
&F       SETA  &F+1           INCREMENT IDENTIFICATION NUMBER.
         AGO   .CONT3
.CONT6   ANOP
         SPACE 2
         DC    AL1(255) -     END OF TABLE INDICATOR.
         AIF   ('&CSNAM' EQ 'IKJEGSCU').CONT7
         SPACE 5
SCDNXTNM EQU   10 -           LENGTH OF ABBREVIATION LENGTH FIELD,
*                             LOAD MODULE NAME LENGTH AND ID
*                             LENGTH.
SCDNXTAB EQU   1 -            LENGTH OF NAME LENGTH FIELD.
SCDTBEND EQU   255 -          END OF TABLE INDICATOR.
SCDABBRL EQU   1 -            LENGTH OF ABBREVIATION LENGTH FIELD
SCDABBR  EQU   0 -            OFFSET TO ABBREVIATION LENGTH FIELD
*                             FROM POINTER TO ABBREVIATION
*                             SUBFIELD.
SCDNAME  EQU   0 -            OFFSET TO SUBCOMMAND NAME LENGTH
*                             FIELD FROM POINTER TO SUBCOMMAND
*                             ENTRY.
SCDSCPPN EQU   1 -            OFFSET FROM BEGINNING OF ENTRY TO
*                             COMMON NAME OF SUBCOMMAND PROCESSOR.
IKJEGAID EQU   SCDAT00 -      ENTRY NAME FOR AT SUBCOMMAND.
IKJEGEID EQU   SCDEND00 -     ENTRY NAME FOR END SUBCOMMAND.
IKJEGPID EQU   SCDPCH00 -     ENTRY NAME FOR ASSIGN SUBCOMMAND.
SCDATLD  EQU   SCDAT02-SCDAT00 OFFSET TO LOAD MODULE NAME FOR
*                             AT SUBCOMMAND.
SCDENDLD EQU   SCDEND02-SCDEND00 OFFSET TO LOAD MODULE NAME FOR
*                             END SUBCOMMAND.
SCDPCHLD EQU   SCDPCH02-SCDPCH00 OFFSET TO LOAD MODULE NAME FOR
*                             ASSIGN SUBCOMMAND.
.CONT7   ANOP
         MEXIT
.ERROR1  MNOTE 12,'INVALID TABLE ENTRY'
         MEXIT
.ERROR2  MNOTE 12,'INVALID KEYWORD VALUE'
         MEXIT
         MEND
./ ADD NAME=IKJPARMA                               17OCT89 12.47.53
         MACRO
         IKJPARMA
IKJPARMA DSECT                DESCRIPTION OF ADDRESS PDE.
         SPACE
PDELDNAM DS    A              POINTER TO LOAD NAME.
PDELEN1  DS    H              LENGTH OF LOAD NAME.
PDEFLG1  DS    X              FLAGS FOR LOAD NAME.
LDNAMFLG EQU   B'10000000'    INDICATES PRESENCE OF LOAD NAME.
PDERSV1  DS    X              RESERVED.
PDECTNAM DS    A              POINTER TO ENTRY NAME.
PDELEN2  DS    H              LENGTH OF ENTRY NAME.
PDEFLG2  DS    X              FLAGS FOR ENTRY NAME.
CTNAMFLG EQU   B'10000000'    INDICATES PRESENCE OF ENTRY NAME.
PDERSV2  DS    X              RESERVED.
PDEADRPT DS    A              POINTER TO ADDRESS STRING.
PDELEN3  DS    H              LENGTH OF ADDRESS STRING.
PDEFLG3  DS    X              FLAGS FOR ADDRESS STRING.
AFLG     EQU   B'10000000'    INDICATES PRESENCE OF ADDRESS STRING.
PDERSV3  DS    X              RESERVED.
         SPACE
PDEFLG4  DS    X              ADDRESS TYPE FLAGS.
ABSADDR  EQU   B'00000000'    ABSOLUTE ADDRESS.
SYMADDR  EQU   B'10000000'    SYMBOLIC ADDRESS.
RELADDR  EQU   B'01000000'    RELATIVE ADDRESS.
GENR     EQU   B'00100000'    GENERAL REGISTER.
LFPR     EQU   B'00010000'    DOUBLE PREC. FLT. PT. REGISTER.
SFPR     EQU   B'00001000'    SINGLE PREC. FLT. PT. REGISTER.
CTONLY   EQU   B'00000100'    ENTRY NM NOT FOLLOWED BY SYM OR REL.
ABSCOMPL EQU   B'11111111'    COMPLEMENT FOR ABSOLUTE TEST.
         SPACE
PDESIGN  DS    C              EXPRESSION SIGN.
PDEINDCT DS    H              INDIRECT COUNT.
PDEEXPTR DS    A              PTR TO 1ST EXPRESSION PDE.
PDEFLG5  DS    0X             FLAG BYTE, PDE USER WORD.
PURESYM  EQU   B'10000000'    INDICATES PDE USER WORD POINTS TO SIB.
PDEUSER  DS    A              RESOLVED ADDR OR SIB POINTER.
PDESIB   EQU   X'80'          INDICATES USER WORD POINTS TO SIB.
PDECONVD EQU   X'40'          INDICATES ADDRESS IS CONVERTED.
PDEADLST EQU   X'20'          INDICATES PDE IS ONE OF A LIST.
PDEYECH  EQU   X'10'          INDICATES PDE IS USELESS.
PDEWRITE EQU   X'08'          INDICATES ADDRESS IS WRITABLE.
         SPACE
PDECHAIN DS    A              CHAIN PTR FIELD FOR ADDRESS LIST.
         SPACE
         ORG   PDECHAIN       IN ADDR RANGE, ADDR2 FOLLOWS ADDR1.
PDE2LDNA DS    A              POINTER TO LOAD NAME.
PDE2LEN1 DS    H              LENGTH OF LOAD NAME.
PDE2FLG1 DS    X              FLAGS FOR LOAD NAME.
PDE2RSV1 DS    X              RESERVED.
PDE2CTNA DS    A              POINTER TO ENTRY NAME.
PDE2LEN2 DS    H              LENGTH OF ENTRY NAME.
PDE2FLG2 DS    X              FLAGS FOR ENTRY NAME.
PDE2RSV2 DS    X              RESERVED.
PDE2ADRP DS    A              POINTER TO ADDRESS STRING.
PDE2LEN3 DS    H              LENGTH OF ADDRESS STRING.
PDE2FLG3 DS    X              FLAGS FOR ADDRESS STRING.
PDE2RSV3 DS    X              RESERVED.
         SPACE
PDE2FLG4 DS    X              ADDRESS TYPE FLAGS.
PDE2SIGN DS    C              EXPRESSION SIGN.
PDE2INDC DS    H              INDIRECT COUNT.
PDE2EXPT DS    A              PTR TO 1ST EXPRESSION PDE.
PDE2FLG5 DS    0X             FLAG BYTE, PDE USER WORD.
PDE2USER DS    A              RESOLVED ADDR OR SIB POINTER.
         SPACE
PDE2CHAI DS    A              CHAIN POINTER FOR ADDR LIST.
         MEND
./ ADD NAME=IKJPSCBX 0102-83124-87171-1546-00032-00030-00000-KK00150
         MACRO
&LAB1    IKJPSCBX
* PCF2 (THE GREEDY BUM) USES PSCBATR2 AND THE FIRST WORD OF PSCBU.WDPSC
*                                                                 WDPSC
* WE ARE THUS USING BITS 12-15 OF PSCBATR1 (AND LIKEWISE IN UADS) WDPSC
* FOR STATUS/CANCEL/OUTPUT EXIT FLAGS AS DESCRIBED BELOW.         WDPSC
*                                                                 WDPSC
*   IN ANTICIPATION OF IBM FURTHER ENCROACHING THESE AREAS, WE    WDPSC
* HAVE CHAINED A 'PSCBX' FROM THE SECOND WORD OF PSCBU, ALSO      WDPSC
* DESCRIBED BELOW.                                                WDPSC
*                                                                 WDPSC
* PSCBATR1 DS 0XL2                                                WDPSC
*          DS  X                                                  WDPSC
*          DS  X                                                  WDPSC
*              X'08' INIT FIELD DOES NOT NEED TO MATCH (CANCEL)   WDPSC
*              X'04' INIT FIELD DOES NOT NEED TO MATCH (OUTPUT)   WDPSC
*              X'02' SYS  FIELD DOES NOT NEED TO MATCH (OUTPUT)   WDPSC
*              X'01' SYS  FIELD DOES NOT NEED TO MATCH (CANCEL)   WDPSC
*                                                                 WDPSC
*        THE   PSCBX IS CREATED BY IKJEFLD AND CHAINED TO         WDPSC
*              THE PSCB VIA PSCBU+4                               WDPSC
*              IT IS FROM SUBPOOL 234  AND REMAINS THRU SESSION   WDPSC
PSCBX    DS   0XL80                                               WDPSC
PSCBXID  DS  CL8'PSCBX   '  BLOCK ID FIELD                        WDPSC
PSCBXPG  DS  XL32     PERFORM GROUP DATA FROM UADS                WDPSC
PSCBXLA  DS  XL2      LINE ADDR FROM TSB+82                       WDPSC
PSCBXTID DS  CL8      TERMID FROM TSB+104                         WDPSC
PSCBXAN  DS  CL8      ACCOUNT NAME FOR MULTI-ACCT USER            WDPSC
PSCBXRES DS  XL2      RESERVED FOR FUTURE USE (DUE TO ALIGNMENT)  WDPSC
PSCBXSUB DS  F        PTR TO SUBMIT DATA AREA (PASSED TO IKJEFF10)WDPSC
*                     (CALLED PASSWORD PRIOR TO ACF2)             WDPSC
         MEND
./ ADD NAME=IKJUPTU  0106-80267-83196-1049-00030-00018-00013-KK00150
         MACRO
&LAB1    IKJUPTU
         ORG   UPTUSER
UPTU     DS    0CL10                    WDPSC AREA                WDPSC
UPTU1    DS    X                        OUR FLAG BYTE 1           WDPSC
UPTUNF   EQU   X'80'  SUBMIT EXIT SHOULD ADD NOTIFY OPERAND       WDPSC
UPTUAF   EQU   X'40'  PAN EXIT SHOULD NOT AUDIT COMMANDS          WDPSC
UPTUAAF  EQU   X'20'  PAN WILL AUDIT ALL CMDS - IF 0,CHANGES ONLY WDPSC
UPTUPF   EQU   X'10'  LOGON EXIT WILL ADD PREFIX= TO EXEC PROD1   WDPSC
UPTUSF   EQU   X'08'  LOGON EXIT WILL ADD STEPLIB DD CARD         WDPSC
UPTUNM   EQU   X'04'  DO NOT LIST MAIL AT LOGON TIME              WDPSC
UPTUNC1  EQU   X'02'  DO NOT ADD USERCAT1 TO STEPCAT              WDPSC
UPTUDPF  EQU   X'01'  DIALOG PREFIX HAS BEEN ENCODED IN UPT       WDPSC
*                                                                 WDPSC
UPTU2    DS    X                        OUR FLAG BYTE 2           WDPSC
UPTUSPF  EQU   X'80'  LOGON WITH ISPPLIB (PANELS=)                WDPSC
UPTUSSF  EQU   X'40'  LOGON WITH ISPSLIB (SKELS=)                 WDPSC
UPTUSMF  EQU   X'20'  LOGON WITH ISPMLIB (MSGS=)                  WDPSC
UPTUSAF  EQU   X'10'  DIALOG PREFIX INCLUDES SUBAGENCY            WDPSC
*                                                                 WDPSC
UPTUDPA  DS    XL3    DIALOG PREFIX AREA FOR ISP LIB DD CARDS     WDPSC
*                                                                 WDPSC
UPTU67   DS    XL2    # OF SECONDS TO EXTEND BEFORE 322 ABENDING  WDPSC
*                     0=DEFAULT, -1=DON'T EXTEND                  WDPSC
*                                                                 WDPSC
UPTU8    DS    X      # OF USERS DEFAULT STEPCAT 0=NONE (CATTABLE)WDPSC
UPTU9    DS    X      USERS DEFAULT TIME LIMIT MINUTES            WDPSC
UPTU10   DS    X      USERS DEFAULT TIME LIMIT SECONDS            WDPSC
         ORG
         MEND
./ ADD NAME=ISPCALL                                17OCT89 12.47.53
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         X
               &DELETED=,   EP PARAMETER DELETED               01/17/84X
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    X
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    X
               &WKREG=,        WORK REGISTER (DEFAULT 1)               X
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*                                     RON MURA, TECHNICAL SERVICES
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
.* 01/17/84 MURA - SUPPORT "CALL ISPEXEC" FORMAT FOR ISPF VER2 01/17/84
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG
         LCLC  &EP             ISPLINK OR ISPEXEC              01/17/84
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&EP      SETC  'ISPLINK'       DEFAULT IS CALL TO ISPLINK      01/17/84
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE *,'*** ISPCALL "CALL ISPEXEC" FORMAT USED ***'  01/17/84
&EP      SETC  'ISPEXEC'       CALL ISPEXEC                    01/17/84
.*               LINE DELETED                                  01/17/84
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.CHKTYPE AIF   ('&EP' EQ 'ISPEXEC').ISPEXEC                    01/17/84
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 X
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LISX
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .CHKTYPE                                        01/17/84
.*************** BUILD PARAMETER LIST FOR CALL TO ISPEXEC *****01/17/84
.ISPEXEC ANOP  ,   FIRST FIND ALL DOUBLE QUOTE AND AMPERSAND   01/17/84
&S       SETA  2                     SEARCH START LOC.         01/17/84
&T       SETA  K'&LIST-2             SEARCH END LOC.           01/17/84
&U       SETA  K'&LIST-2             LENGTH OF BUFFER          01/17/84
.ISPEXLP AIF   (&S GT &T).ISPEXCD                              01/17/84
         AIF   ('&LIST'(&S,2) EQ '''').DOUBLE                  01/17/84
         AIF   ('&LIST'(&S,2) EQ '&&').DOUBLE                  01/17/84
&S       SETA  &S+1                                            01/17/84
         AGO   .ISPEXLP                                        01/17/84
.DOUBLE  ANOP ,                                                01/17/84
&S       SETA  &S+2                  SKIP DOUBLED CHAR.        01/17/84
&U       SETA  &U-1                  DEDUCT ONE FOR DOUBLE CHR 01/17/84
         AGO   .ISPEXLP                                        01/17/84
.ISPEXCD ANOP ,                                                01/17/84
         MVC   &WKAR.(4),=F'&U'      LENGTH OF BUFFER  01/17/84 ISPCALL
         LA    &WKRG,&WKAR           ADDR./ BUFF. LNTH 01/17/84 ISPCALL
         ST    &WKRG,&WKAR+4         STORE BUFF. LNTH  01/17/84 ISPCALL
         LA    &WKRG,=C&LIST         ADDR./ BUFFER     01/17/84 ISPCALL
         ST    &WKRG,&WKAR+8         STORE BUFF. ADDR. 01/17/84 ISPCALL
&T       SETA  8                     SET TO VL BIT LOC.        01/17/84
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
         LA    1,&WKAR.+4                                       ISPCALL
         L     15,=V(&EP)                                       ISPCALL
         BALR  14,15                                            ISPCALL
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
./ ADD NAME=ITCOMA1  0102-86049-89067-2143-00034-00033-00000-KK00155
         MACRO
&LAB1    ITCOMA1 &DSECT
         LCLC  &LAB2
*
*        LOCAL INTER-TASK COMMUNICATION AREA ONE:
*
         AIF   (T'&LAB1 NE 'O').LOK
&LAB2    SETC  'ITCOMA1'
         AGO   .DSCK
.LOK     ANOP
&LAB2    SETC  '&LAB1'
.DSCK    AIF   ('&DSECT' EQ 'DSECT').DSL
         DS    0A
&LAB2    DS    0XL100
         AGO   .ADSL
.DSL     ANOP
&LAB2    DSECT
.ADSL    ANOP
         DC    CL8'ITCOMA1'
         DC    CL4'V1M3'
         DC    A(ITCOML)
*              TABLE OF UP TO 4 TCB ADDRESS PAIRS FOR NEWMWILE    *KK*
SCR1     DC    2A(0)                   SCREEN 1 TCB ADDRESSES
SCR2     DC    2A(0)                   SCREEN 2 TCB ADDRESSES
SCR3     DC    2A(0)                   SCREEN 3 TCB ADDRESSES
SCR4     DC    2A(0)                   SCREEN 4 TCB ADDRESSES
         SPACE
SUBDPTR  DS    A                       PTR TO CURRENT SUBMIT DATA(TSOE)
         SPACE
CMAINROK DS    A                       PTR TO TRUE C370 MAIN PROGRAM
         SPACE
         DS    23F                     FUTURE OPTIONS
ITCOML   EQU   *-ITCOMA1
         MEND
./ ADD NAME=K                                      17OCT89 12.47.53
         MACRO
&N       K     ,                       IDENTICAL TO THE ERASE MACRO.
&N       LA    0,9                     LOAD LENGTH.
         LA    1,=X'F11140403C40400013'
         ICM   1,8,=X'03'              SET FULLSCR OPTION.
         SVC   93                      INVOKE TPUT.
         MEND
./ ADD NAME=KKPARSE  0109-77250-81119-1717-00058-00047-00047-KK00150
         MACRO
&LAB     KKPARSE &CPPL=R11,&ANS=ANS,&ECB=ECB,&PCL=PCL,&PPLA=PPLA
         MNOTE 'CPPL=&CPPL,ANS=&ANS,ECB=&ECB,PCL=&PCL,PPLA=&PPLA'
*                  DEFAULT OPERANDS :
*        KKPARSE &CPPL=R11,&ANS=ANS,&ECB=ECB,&PCL=PCL,&PPLA=PPLA
*                            CPPL IS THE REGISTER CONTAINING THE
*                                 POINTER TO THE COMMAND PROCESSOR
*                                 PARAMETER LIST.
*                            ANS  IS A FULLWORD WHERE PARSE RETURNS
*                                 THE POINTER TO HIS ANSWER (IKJPARMD).
*                            ECB  IS A FULLWORD.
*                            PCL  IS THE NAME OF THE PARSE CONTROL
*                                 LIST CSECT (IKJPARM).
*                            PPLA IS A 7 FULLWORD AREA USED FOR
*                                 THE PARSE PARAMETER LIST.
*
*        KKPARSE             USES REGS R14,R15,R1,&CPPL REG
*
         USING CPPL,&CPPL         POINTS TO CPPL (PARM LIST)
*
&LAB     LA    R1,&PPLA           R1->PPL AREA
         USING PPL,R1             TELL ASSEMBLER POINTS TO PPL AREA
*
*                         CONSTRUCT PARSE PARAMETER LIST
*                                           FOR IKJPARS :
*
         MVC   PPLUPT,CPPLUPT     UPT
         MVC   PPLECT,CPPLECT     ECT
         LA    R15,&ECB           ECB
         ST    R15,PPLECB
         L     R15,=V(&PCL.)      PCL
         ST    R15,PPLPCL
         LA    R15,&ANS           ANS
         ST    R15,PPLANS
         MVC   PPLCBUF,CPPLCBUF   CBUF
         XC    PPLUWA,PPLUWA
*
         DROP  R1
         EJECT
*        CALLTSSR EP=IKJPARS      CALL PARSE:
         CALLTSSR EP=IKJPARS      CALL PARSE:
         EJECT
*
*                        DSECTS NEEDED BY PARSE:
*
*CPPL    IKJCPPL
CPPL     IKJCPPL
*PPL     IKJPPL
PPL      IKJPPL
*
CVTMAP   DSECT
         ORG   CVTMAP+524
CVTPARS  DS    F
CVTPTR   EQU   16
*
*
&SYSECT  CSECT
         MEND
./ ADD NAME=KKPWA    0100-81119-81119-1723-00015-00015-00000-KK00150
         MACRO
&LAB     KKPWA  &ANS=ANS,&ECB=ECB,&PPLA=PPLA
         MNOTE 'ANS=&ANS,ECB=&ECB,PPLA=&PPLA'
*                            ANS  IS A FULLWORD WHERE PARSE RETURNS
*                                 THE POINTER TO HIS ANSWER (IKJPARMD).
*                            ECB  IS A FULLWORD.
*
*                            PPLA IS A 7 FULLWORD AREA USED FOR
*                                 THE PARSE PARAMETER LIST.
*
&ANS     DS    F
&ECB     DS    F
&PPLA    DS    7F
*
         MEND
./ ADD NAME=KKSCAN                                 17OCT89 12.47.53
         MACRO
&LAB     KKSCAN  &CPPL=R11,&CSOA=ACSOA,&ECB=ECB,&CSPL=ACSPL,&CSFW=CSFW,X
               &CVT='NO'
         MNOTE 'CPPL=&CPPL,CSOA=&CSOA,ECB=&ECB,CSPL=&CSPL,CSFW=&CSFW'
*                  DEFAULT OPERANDS :
*        KKSCAN CPPL=R11,ECB=ECB,CSFW=CSFW
*                            CPPL IS THE REGISTER CONTAINING THE
*                                 POINTER TO THE COMMAND PROCESSOR
*                                 PARAMETER LIST.
*                            CSOA IS A DBLWORD WHERE SCAN RETURNS
*                                 THE SCAN RESULTS
*                            ECB  IS A FULLWORD.
*                            CSFW IS A FULLWORD FLAGAREA PASSED TO SCAN
*                            CSPL IS A 6 FULLWORD AREA USED FOR
*                                 THE SCAN PARAMETER LIST.
*
*        KKSCAN              USES REGS R14,R15,R1,R0
*
         USING CPPL,&CPPL         POINTS TO CPPL (PARM LIST)
*
&LAB     LA    R1,&CSPL           R1->CSPL AREA
         USING CSPL,R1
*
*                         CONSTRUCT SCAN PARAMETER LIST
*                                           FOR IKJSCAN :
*
         MVC   CSPLUPT,CPPLUPT    UPT
         MVC   CSPLECT,CPPLECT    ECT
         LA    R15,&ECB           ECB
         ST    R15,CSPLECB         "
         L     R15,&CSFW          CSFW
         ST    R15,CSPLFLG
         LA    R15,&CSOA          ANS
         ST    R15,CSPLOA
         MVC   CSPLCBUF,CPPLCBUF  CBUF
         XC    &CSFW,&CSFW
*
         DROP  R1
         DROP  &CPPL
         EJECT
*        CALLTSSR EP=IKJSCAN      CALL SCAN:
         CALLTSSR EP=IKJSCAN      CALL SCAN:
         EJECT
*
*                        DSECTS NEEDED BY SCAN:
*
         IKJCSPL
*
         IKJCSOA
         AIF   ('&CVT' NE 'YES').SC
*
         IKJCVT
.SC      ANOP
*
&SYSECT  CSECT
         MEND
./ ADD NAME=KKSWA    0101-81131-81131-0948-00020-00020-00001-KK00150
         MACRO
&LAB     KKSWA  &CSPL=ACSPL,&CSFW=CSFW,&CSOA=ACSOA,&DSECT
         MNOTE 'CSPL=&CSPL,CSFW=&CSFW,CSOA=&CSOA'
*
*                            CSPL IS A 6 FULLWORD AREA USED FOR
*                                 THE SCAN PARAMETER LIST.
*                            CSFW IS A FULLWORD FLAG PASSED TO SCAN
*                            CSOA IS 2 FULLWORDS WHERE SCAN RETURNS
*                                 THE RESULTS OF HIS SCANNING
*
         AIF   ('&DSECT' EQ 'DSECT').DL
         AGO   .L1
.DL      ANOP
&LAB     DSECT
.L1      ANOP
&CSPL    DS    6F
&CSFW    DS    F
&CSOA    DS    2F
*
         MEND
./ ADD NAME=KPPL     0109-80017-88285-1626-00032-00027-00032-KK00155
         MACRO
&LAB1    KPPL   &DSECT
         LCLC  &LAB2
*
*               PARAMETER LIST PASSED TO KMDPARS (12 FULLWORDS):
*
         AIF   (T'&LAB1 NE 'O').LOK
&LAB2    SETC  'KPPL'
         AGO   .DSCK
.LOK     ANOP
&LAB2    SETC  '&LAB1'
.DSCK    AIF   ('&DSECT' EQ 'DSECT').DSL
         DS    0A
&LAB2    DS    0XL48
         AGO   .ADSL
.DSL     ANOP
&LAB2    DSECT
.ADSL    ANOP
CBUFPTR  DS    F                        CMDBUF TO BE PARSED
OPLSTPTR DS    F                        LIST OF OPERANDS TO FLAG
FLAGPTR  DS    F                        WHERE TO FLAG THEM
UNKNEXIT DS    F                        EXIT TO CALL IF UNKNOWN OPER
EXITPARM DS    F                        PARAMETER TO PASS EXITS (ADDR)
*                                       (R1 POINTS HERE AT EXIT ENTRY)
BPPLPASS DS    F                        BPPL PTR WHEN EXIT GETS CONTROL
WORKPASS DS    F                        OPTIONAL WORKAREA (512 BYTES)
KEYLPASS DS    F                        LIST OF KEYWORD OPERANDS
KEYWPASS DS    F                        WORKAREA FOR KEYWORD PROCESSOR
REEXPASS DS    F                        ADDRESS OF REAL UNKNOWN EXIT
BUFFLEN  DS    F                        LEN OF STR BUFF
         DS    F                        RSVD- FLAGS
         MEND
./ ADD NAME=LEAVE    0100-80120-80120-1131-00018-00018-00000-KK00150
         MACRO
&NAME    LEAVE  &EQ,&CC=
         GBLC  &LV,&SP,&SAVED(2)
&NAME    LR    2,13               SAVE CURRENT WORK/SAVE AREA
         L     13,4(13)           PICK UP LAST SAVE AREA
         STM   15,1,16(13)        STORE RETURN REGS
         AIF   ('&LV' EQ '').L1
         FREEMAIN R,LV=&LV,SP=&SP,A=(2)  FREE SAVE AREA
.L1      AIF   ('&SAVED(2)' EQ '').L2
         AIF   ('&CC' EQ '').L15       WAS CC SPECIFIED
         RETURN (&SAVED(1),&SAVED(2)),T,RC=&CC RETURN
         AGO   .L3
.L15     RETURN (&SAVED(1),&SAVED(2)),T        RETURN
         AGO   .L3
.L2      RETURN &SAVED(1),T *   RETURN TO CALLER
.L3      AIF   ('&EQ' NE 'EQ').L4
         REGISTER
.L4      MEND
./ ADD NAME=LEAVENR  0100-77291-77291-1100-00005-00005-00000-KK00150
         MACRO
&LAB     LEAVENR
&LAB     L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         RETURN (14,12),RC=(15)
         MEND
./ ADD NAME=LEAVER   0102-77256-80289-1619-00010-00008-00009-KK00150
         MACRO
&LAB     LEAVER &WR=R13,&WL=WORKLEN
&LAB     LR    R1,&WR             WORKAREA ADDR FOR FREEMAIN
         L     R0,=A(&WL)         WORKAREA LEN   "    "
         L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         LR    R11,R15            SAVE RETURN CODE
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R11            RESTORE RETURN CODE
         RETURN (14,12),RC=(15)
         MEND
./ ADD NAME=LEAVEWW  0100-82328-82328-1125-00005-00005-00000-KK00150
         MACRO
&LAB     LEAVEWW
&LAB     L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         RETURN (14,12),RC=(15)
         MEND
./ ADD NAME=LIT1                                   17OCT89 12.47.53
         MACRO
         LIT1  &NAME
&NAME    CSECT .
***********************************************************************
* PROGRAM TO COPY A LITERAL DEFINED IN THIS PROGRAM INTO A LOCATION
* DEFINED BY THE CALLING PROGRAM.  IT WAS WRITTEN TO ENABLE COBOL AND
* FORTRAN PROGRAMS TO DEFINE FULL-SCREEN 3270 MENUS.  IT CONSISTS OF
* TWO MACROS, WITH SOME USER-DEFINED CONSTANTS BETWEEN.
*
* SAMPLE CODING SEQUENCE:
*        LIT1  MENU1
*        DC    X'C3'              WCC
*        DC    X'114040'          SBA AT START OF SCREEN
*        DC    X'3C4C6000'        CLEAR THROUGH LINE 10
*        DC    X'114040'          LINE 1: TITLE
*        DC    X'1DF8',C'ADD COMMUNICATIONS LINE'
*        LIT2
*
* SAMPLE CALLING SEQUENCE:
*    CALL 'MENU1' USING MENU.
* WHERE MENU IS THE LOCATION IN THE CALLING PROGRAM WHERE THE LITERAL
* IS TO BE COPIED.
*
* KEITH NEWSOM, WDPSC, 12-77
***********************************************************************
BEGIN    B     14(0,15) .         BRANCH AROUND ID
         DC    AL1(8) .
         DC    CL8'&NAME.       ' . IDENTIFIER
         STM   14,12,12(13) .     SAVE REGISTERS
         BALR  12,0 .
         USING *,12 .
         LA    4,LIT .            SET UP "FROM" ADDRESS AND LENGTH
         L     5,LENLIT .
         L     6,0(1) .           SET UP "TO" ADDRESS AND LENGTH
         L     7,LENLIT .
         MVCL  6,4 .              MOVE LITERAL
         LM    14,12,12(13) .     RESTORE REGISTERS
         LA    15,0(0,0) .        LOAD RETURN CODE
         BR    14 .               RETURN
LIT      EQU   * .
*----------------------------------------------------------------------
* DEFINE YOUR LITERAL BETWEEN HERE . . .
*----------------------------------------------------------------------
         MEND
./ ADD NAME=LIT2                                   17OCT89 12.47.53
         MACRO
         LIT2
*----------------------------------------------------------------------
* . . . AND HERE.
*----------------------------------------------------------------------
ENDLIT   EQU   * .
LENLIT   DC    A(ENDLIT-LIT) .
         END   BEGIN .
         MEND
./ ADD NAME=LOGONXWA 0114-83138-85302-1335-00055-00029-00000-KX00150
         MACRO
&LAB     LOGONXWA &OFF=*
WDPSCPRM EQU   &OFF                                               WDPSC
*                                                                 WDPSC
*        WDPSC ADDITIONS TO IKJEFLDA PARM LIST:                   WDPSC
*                                                                 WDPSC
DESTLNG  EQU   &OFF                                               WDPSC
         DS    XL2                                                WDPSC
DESTUSID EQU   &OFF                                               WDPSC
         DS    CL8                                                WDPSC
MSGCLASS EQU   &OFF                                               WDPSC
         DS    XL2                                                WDPSC
TIMELM   EQU   &OFF                                               WDPSC
         DS    H                                                  WDPSC
TIMEM    EQU   &OFF                                               WDPSC
         DS    CL4                                                WDPSC
TIMELS   EQU   &OFF                                               WDPSC
         DS    H                                                  WDPSC
TIMES    EQU   &OFF                                               WDPSC
         DS    CL2                                                WDPSC
PROGL    EQU   &OFF                                               WDPSC
         DS    H                                                  WDPSC
PROG     EQU   &OFF                                               WDPSC
         DS    CL20                                               WDPSC
SLIBFLGS EQU   &OFF                                               WDPSC
         DS    H                                                  WDPSC
STEP     EQU   X'80'                                              WDPSC
NOSTEP   EQU   X'40'                                              WDPSC
PAL      EQU   X'20'                                              WDPSC
NOPAL    EQU   X'10'                                              WDPSC
DIALOG   EQU   X'08'                                              WDPSC
NODIALOG EQU   X'04'                                              WDPSC
VANILLA  EQU   X'02'                                              WDPSC
CHOCLATE EQU   X'01'                                              WDPSC
*                                                                 WDPSC
*              ADDED WORK AREAS FOR IKJEFLD                       WDPSC
*                                                                 WDPSC
ACF2FLG  DS    H                                                  WDPSC
WUPTPTR  DS    2H                                                 WDPSC
WLWAPTR  DS    2H                                                 WDPSC
WLWPPTR  DS    2H                                                 WDPSC
WLIPPTR  DS    2H                                                 WDPSC
WPFGPTR  DS    2H                                                 WDPSC
TIMWK1   DS    4H                                                 WDPSC
TIMWK2   DS    4H                                                 WDPSC
DESTRECK DS    H                                                  WDPSC
REGIONS  DS    CL5                                                WDPSC
DESTFORM DS    CL9                                                WDPSC
ACCTDATA DS    CL10                                               WDPSC
W1       DS    F                                                  WDPSC
W2       DS    F                                                  WDPSC
DSL      DS    H                                                  WDPSC
DSF      DS    XL20                                               WDPSC
         DS    0F                                                 WDPSC
         MEND
./ ADD NAME=MENTER                                 17OCT89 12.47.53
         MACRO
&NAME    MENTER  &REG,&EQU,&COM=,&CP=NO
&NAME    CSECT
         GBLC    &USE
         GBLC    &GBCP
         LCLC    &COMMENT
         LCLC    &BASE
         LCLA    &REGNO
&GBCP    SETC    '&CP'
&USE     SETC    'USE'
&COMMENT SETC    '&COM'
         AIF     ('&COMMENT' NE '').NOCOMM
&COMMENT SETC    '&NAME'
.NOCOMM  ANOP
         AIF     ('&EQU' EQ 'NOEQU').NOEQU
***********************************************************************
*                REGISTER EQUATES                                     *
***********************************************************************
*
R0       EQU     0
R1       EQU     1
R2       EQU     2
R3       EQU     3
R4       EQU     4
R5       EQU     5
R6       EQU     6
R7       EQU     7
R8       EQU     8
R9       EQU     9
R10      EQU     10
R11      EQU     11
R12      EQU     12
R13      EQU     13
R14      EQU     14
R15      EQU     15
.*
.NOEQU   ANOP
&REGNO   SETA    &REG
         AIF     (&REGNO GT 12).NOWAY
         AIF     (&REGNO LT 2).NOWAY
.*
.*       INITIALIZE BASE REGISTER TO USER SPECIFICATIONS
.*
&BASE    SETC    '&REG'
         AGO     .START
.NOWAY   ANOP
*
         MNOTE   0,'INCORRECT REGISTER SPECIFIED ...USING 12'
*
&BASE    SETC    '12'
.START   ANOP
***********************************************************************
*                SET UP THE SAVE AREA                                 *
***********************************************************************
*        SAVE    (14,12)
         DS      0H
         STM     14,12,12(13)          SAVE REGISTERS
         LR      &BASE,15              LOAD BASE REGISTER
         USING   &NAME,&BASE           TELL ASSEMBLER
         B       MENT&SYSNDX
         DC      CL8'&SYSDATE'
         DC      CL8'&SYSTIME'
         DC      CL8'&COMMENT'
MENT&SYSNDX DS 0H
         ST      13,SAVE+4             STORE OLD SAVE AREA
         LA      3,SAVE                AND PREPARE TO STORE NEW
         ST      3,8(13)               STORE IT
         LR      13,3                  AND LOAD R13
         AIF     ('&CP' EQ 'NOCP').NOPPL
         LA      R2,IOPLADS
         USING   IOPL,R2
         L       R3,0(1)
         ST      R3,CPPLCBUF
         L       R3,4(1)
         ST      R3,CPPLUPT
         ST      R3,IOPLUPT
         L       R3,8(1)
         ST      R3,CPPLPSCB
         L       R3,12(1)
         ST      R3,CPPLECT
         ST      R3,IOPLECT
         L       R3,ECBADS
         ST      R3,IOPLECB
         DROP    R2
.NOPPL   ANOP
*
*
*
         MEND
./ ADD NAME=MLEAVE                                 17OCT89 12.47.53
         MACRO
&NAME    MLEAVE
         GBLC    &GBCP
         LH      R15,EXITRC
         L       13,SAVE+4
*        RETURN  (14,12),T,RC=0
         L       14,12(13)          RESTORE THE REGISTERS
         LM      0,12,20(13)          RESTORE THE REGISTERS
         MVI     12(13),X'FF'          SET RETURN INDICATION
         BR      14                    RETURN
EXITRC   DC      H'0'             RETURN CODE FROM COMMAND PROCESSOR
         AIF     ('&GBCP' EQ 'NOCP').NOPPL
CPPLCBUF DS      A
CPPLUPT  DS      A
CPPLPSCB DS      A
CPPLECT  DS      A
IOPLADS  DS      4F               SPACE FOR IO PARM LIST
ECBADS   DC      F'0'             ECB FOR PUTLINE, GETLINE
PUTBLOK  PUTLINE MF=L             DEFAULT PUTLINE MACRO, LIST FORM
.NOPPL   ANOP
SAVE     DS      18F
         MEND
./ ADD NAME=MOVE     0100-88291-88291-1848-00057-00057-00000-KK00155
         MACRO
&L1      MOVE  &TO,&TL,&FROM,&FL,&PAD=
         LCLA  &NL,&UL,&VL,&RP,&RP2,&RC
         LCLC  &TO$,&TL$,&FROM$,&FL$
         LCLC  &R1(2),&R2(2),&RN1,&RN2
&RC      SETA  1
&RP      SETA  2
&RP2     SETA  &RP+1
.RPLOOP  ANOP
&RN1     SETC  '(R'.'&RP'.')'
&RN2     SETC  '(R'.'&RP2'.')'
         AIF   ('&TO' EQ '&RN1').CKP2
         AIF   ('&TL' EQ '&RN1').CKP2
         AIF   ('&FROM' EQ '&RN1').CKP2
         AIF   ('&FL' EQ '&RN1').CKP2
         AIF   ('&TO' EQ '&RN2').CKP2
         AIF   ('&TL' EQ '&RN2').CKP2
         AIF   ('&FROM' EQ '&RN2').CKP2
         AIF   ('&FL' EQ '&RN2').CKP2
&R1(&RC) SETC  'R'.'&RP'
&R2(&RC) SETC  'R'.'&RP2'
&RC      SETA  &RC+1
         AIF   (&RC EQ 3).GP
.CKP2    ANOP
&RP      SETA  &RP+2
&RP2     SETA  &RP+1
         AIF   (&RP LT 10).RPLOOP
         MNOTE 8,'NO REGISTER PAIRS FREE FOR MVCL INSTRUCTION'
         MEXIT
.GP      ANOP
&TO$     SETC  '&TO'
&TL$     SETC  '&TL'
&FROM$   SETC  '&FROM'
&FL$     SETC  '&FL'
         AIF   ('&TO'(1,1) NE '(').TLC
&TO$     SETC  '0&TO'
.TLC     AIF   ('&TL'(1,1) NE '(').FC
&TL$     SETC  '0&TL'
.FC      AIF   ('&FROM'(1,1) NE '(').FLC
&FROM$   SETC  '0&FROM'
.FLC     AIF   (T'&FL EQ 'O').OO
         AIF   ('&FL'(1,1) NE '(').OO
&FL$     SETC  '0&FL'
.OO      ANOP
&L1      STM   14,12,12(13)
         LA    &R1(1),&TO$
         LA    &R2(1),&TL$
         LA    &R1(2),&FROM$
         AIF   (T'&FL EQ 'O').UTL
         LA    &R2(2),&FL$
         AGO   .PC
.UTL     LA    &R2(2),&TL$
.PC      AIF   ('&PAD' EQ '').NPC
         ICM   &R2(2),8,=&PAD
.NPC     MVCL  &R1(1),&R1(2)
         LM    14,12,12(13)
         MEND
./ ADD NAME=MOVEOLD  0100-83115-83115-1254-00031-00031-00000-KK00150
         MACRO
&L1      MOVE  &TO,&TL,&FROM,&FL,&PAD=
         LCLA  &NL,&UL,&VL
         LCLC  &TO$,&TL$,&FROM$,&FL$
&TO$     SETC  '&TO'
&TL$     SETC  '&TL'
&FROM$   SETC  '&FROM'
&FL$     SETC  '&FL'
         AIF   ('&TO'(1,1) NE '(').TLC
&TO$     SETC  '0&TO'
.TLC     AIF   ('&TL'(1,1) NE '(').FC
&TL$     SETC  '0&TL'
.FC      AIF   ('&FROM'(1,1) NE '(').FLC
&FROM$   SETC  '0&FROM'
.FLC     AIF   (T'&FL EQ 'O').OO
         AIF   ('&FL'(1,1) NE '(').OO
&FL$     SETC  '0&FL'
.OO      ANOP
&L1      STM   14,12,12(13)
         LA    R2,&TO$
         LA    R3,&TL$
         LA    R4,&FROM$
         AIF   (T'&FL EQ 'O').UTL
         LA    R5,&FL$
         AGO   .PC
.UTL     LA    R5,&TL$
.PC      AIF   ('&PAD' EQ '').NPC
         ICM   R5,8,=&PAD
.NPC     MVCL  R2,R4
         LM    14,12,12(13)
         MEND
./ ADD NAME=MVINC    0114-87138-87141-1700-00049-00039-00000-GS00150
         MACRO
&NAME    MVINC &TO,&TL,&FROM,&TABLE=OLD
         LCLC  &TO$,&TL$,&FROM$
         GBLC  &TBL$
         GBLA  &PREVIDX
&TO$     SETC  '&TO'
&TL$     SETC  '&TL'
&FROM$   SETC  '&FROM'
.TCHK    AIF   ('&TO'(1,1) NE '(').TLCHK
&TO$     SETC  '0&TO'
.TLCHK   AIF   ('&TL'(1,1) NE '(').FCHK
&TL$     SETC  '0&TL'
.FCHK    AIF   ('&FROM'(1,1) NE '(').START$
&FROM$   SETC  '0&FROM'
.START$  ANOP
&SI      SETA  &SYSNDX
&NAME    LA    R1,&TL$
         LA    R14,&TO$
         B     NEXT&SI
         LCLA  &I &IDX
         AIF   (&PREVIDX NE 0).PIDX
&PREVIDX SETA  0001
.PIDX    ANOP
&IDX     SETA  &PREVIDX
.NEWCHK  AIF   ('&TABLE' NE 'NEW').DFNCHK
&IDX     SETA  &SI
         AGO   .INIT$
.DFNCHK  AIF   ('&TBL$' EQ 'DFND').TRNSLT
.INIT$   ANOP
&I       SETA  255
TBL&IDX  DS    0XL256
.DFNT    PRINT NOGEN
.DLP     DC    AL1(&I)
&I       SETA  &I-1
         AIF   (&I GE 0).DLP
         PRINT GEN
.TRNSLT  ANOP
&TBL$    SETC  'DFND'
&PREVIDX SETA  &IDX
NEXT&SI  LA    R15,TBL&IDX+256
         SR    R15,R1
         BCTR  R1,0
         EX    R1,COPY&SI
         EX    R1,TRAN&SI
         B     QUIT&SI
COPY&SI  MVC   0(0,R14),0(R15)
TRAN&SI  TR    0(0,R14),&FROM$
QUIT&SI  EQU   *
         MEND
./ ADD NAME=NAMETAB  0100-83013-83013-1107-00047-00047-00000-KK00150
         MACRO
&PTL     NAMETAB &LL,&NL,&ENTRIES=0,&DSECT=DEF
         LCLA  &LC,&LN
         LCLC  &SS
*              TABLE OF PREFIX OR QUALIFIER NAMES -
*                UP TO 8 BYTES EACH, PRECEDED BY HALFWORD LENGTHS
         AIF   ('&DSECT' EQ 'YES').CD
         AIF   (T'&LL EQ 'O').ND
         AIF   ('&LL'(1,1) EQ '(').ND
.CD      AIF   ('&DSECT' EQ 'NO').ND
&PTL     DSECT
         AGO   .L1
.ND      ANOP
&PTL     DS    0H
.L1      ANOP
         AIF   (T'&LL EQ 'O').MBST
         AIF   ('&LL'(1,1) EQ '(').MBTP
&LL      DS    H                  LENGTH OF NAME - IF ZERO TABLE END
&NL      DS    CL8                NAME - UP TO 8 BYTES
         MEXIT
.MBTP    AIF   (N'&LL EQ 1).STRNG
&LC      SETA  &LC+1
&LN      SETA  K'&LL(&LC)
         DC    H'&LN',CL8'&LL(&LC)'
         AIF   (&LC LT N'&LL).MBTP
         DC    H'0',CL8' '        END OF TABLE
         MEXIT
.MBST    DS    &ENTRIES.H,&ENTRIES.CL8
         MEXIT
.STRNG   ANOP
&LC      SETA  2
&LN      SETA  0
.NC      AIF   ('&LL'(&LC,1) EQ ',').BE
         AIF   ('&LL'(&LC,1) EQ ')').BE
&LN      SETA  &LN+1
&LC      SETA  &LC+1
         AGO   .NC
.BE      ANOP
&SS      SETC  '&LL'(&LC-&LN,&LN)
         DC    H'&LN',CL8'&SS'
         AIF   ('&LL'(&LC,1) EQ ')').TE
&LN      SETA  0
&LC      SETA  &LC+1
         AGO   .NC
.TE      ANOP
         DC    H'0',CL8' '        END OF TABLE
         MEND
./ ADD NAME=OACPRTPM                               17OCT89 12.47.53
         MACRO
         OACPRTPM &DUMMY,&PRINT=
.*
.*       SETS  VALUES OF &OACPRT AND &OACPRTS DEPENDING ON
.*       VALUES OF &OACPRTG, &OACPRTF, AND &PRINT
.*
.*       DOES  A PUSH PRINT AND A PRINT ON,GEN OR A
.*       PRINT OFF,NOGEN DEPENDING ON THE CALCULATED VALUE
.*       OF &OACPRT
.*
         COPY  OACGBLS
&OACPRT  SETC  '&OACPRTG'
         AIF   (&OACPRTF).L4
         AIF   ('&PRINT' EQ '').L1DEF
         AIF   ('&PRINT' NE 'ALL' ).L1
&OACPRT  SETC  'ALL'
         AGO   .L4
.L1      AIF   ('&PRINT' NE 'SHORT').L2
.L1DEF   ANOP                     DEFAULT PRINT VALUE
&OACPRT  SETC  'SHORT'
         AGO   .L4
.L2      AIF   ('&PRINT' EQ 'NONE').L3
         MNOTE 4,'UNRECOGNIZABLE PRINT VALUE ''NONE'' USED'
.L3      ANOP
&OACPRT  SETC  'NONE'
.L4      ANOP
         PUSH  PRINT
         AIF   ('&OACPRT' NE 'ALL').L5
&OACPRTS SETA  99
         AGO   .L8
.L5      AIF   ('&OACPRT' NE 'SHORT').L6
&OACPRTS SETA  1
         AGO   .L8
.L6      AIF   ('&OACPRT' EQ 'NONE').L7
         MNOTE 4,'UNRECOGNIZABLE VALUE OF &OACPRTG ''NONE'' SET'
&OACPRTG SETC  'NONE'
&OACPRT  SETC  '&OACPRTG'
.L7      ANOP
         PRINT OFF,GEN
&OACPRTS SETA  1
.L8      MEND
./ ADD NAME=OACREGS                                17OCT89 12.47.53
         MACRO
         OACREGS &DUMMY,&PRINT=
         COPY  OACGBLS
         OACPRTPM PRINT=&PRINT
         AIF   ('&OACPRT' NE 'SHORT').L1
         PRINT OFF,GEN
.L1      ANOP
         SPACE &OACPRTS
******************************************
R0       EQU   0                   <-----
R1       EQU   1                   <-----
R2       EQU   2                   <-----
R3       EQU   3                   <-----
R4       EQU   4                   <-----
R5       EQU   5                   <-----      =============
R6       EQU   6                   <-----      =  GENERAL  =
R7       EQU   7                   <-----      =  PURPOSE  =
R8       EQU   8                   <-----      = REGISTERS =
R9       EQU   9                   <-----      =============
R10      EQU   10                  <-----
R11      EQU   11                  <-----
R12      EQU   12                  <-----
R13      EQU   13                  <-----
R14      EQU   14                  <-----
R15      EQU   15                  <-----
         SPACE
F0       EQU   0                   <-----    ==================
F2       EQU   2                   <-----    = FLOATING POINT =
F4       EQU   4                   <-----    =   REGISTERS    =
F6       EQU   6                   <-----    ==================
         POP   PRINT
         MEND
./ ADD NAME=OPER     0105-88281-88285-1135-00054-00054-00000-KX00155
         MACRO
&OPLB    OPER  &ON,&F,&EXIT=,&BLANK=,&MINLEN=,&KEYFLD=,&SUBFLD=,      XX
               &SUBLIST=,&POSITIONAL=,&ASIS=
         GBLC  &XPARM
         LCLA  &I,&L,&J
&OPLB    DS    0F                       FULLWORD BNDRY
         DC    &F                       FLAG LOC (SET IF OPER FOUND):
*                                       BYTE1=FLAG BIT,BYTE2=FLAG BYTE
         AIF   ('&EXIT&SUBFLD' EQ '').S1
&I       SETA  &I+8
.S1      AIF   ('&BLANK' EQ '').S2
&I       SETA  &I+4
.S2      AIF   ('&KEYFLD' EQ '').S2B
&I       SETA  &I+2
.S2B     AIF   ('&SUBFLD' EQ '').S2C
&I       SETA  &I+1
         AIF   ('&SUBLIST' EQ '').S2C
         MNOTE 'SUBFLD AND SUBLIST ARE MUTUALLY EXCLUSIVE'
.S2C     AIF   ('&POSITIONAL' EQ '').S2D
&J       SETA  &J+8
.S2D     AIF   ('&SUBLIST' EQ '').S2E
&J       SETA  &J+4
.S2E     AIF   ('&ASIS' EQ '').SS
&J       SETA  &J+2
.SS      DC    AL1(&I*16+&J)   BYTE1= OPTIONS (80 = TAKE EXIT)
*              (40 = OVERLAY WITH BLANKS,20 = KEYWORD OPERAND)
*              (10 = SUBFIELD AREA OFFSET PROVIDED FOR SUBEX )
*              (08 = POSITIONAL OPER,04 = SUBLIST,02 = ASIS  )
         DC    AL1(0&MINLEN)     BYTE2= MINIMUM LENGTH OF OPER TO MATCH
         AIF   ('&EXIT' EQ '').S3A
         DC    A(&EXIT)                 EXIT ADDR
         AGO   .S4
.S3A     AIF   ('&SUBFLD' EQ '').S3
         DC    A(X&SYSNDX)              EXTERNAL EXIT TRANSFER
         AGO   .S4
.S3      AIF   ('&KEYFLD' EQ '').SL    NO EXIT
         DC    A(&KEYFLD)         ADDRESS OF KEYFLD ->
*        HFWD  FIELD LEN, HFWD DATA LEN, DATA FIELD
         AGO   .S4
.SL      AIF   ('&SUBLIST' EQ '').SN    NO EXIT
         DC    A(&SUBLIST)        ADDRESS OF SUBLIST ->
*        ANOTHER OPERLIST DESCRIBING SUBLIST ONLY OPERANDS
         AGO   .S4
.SN      DC    A(0)                     NO EXIT
.S4      ANOP
&L       SETA  K'&ON                    NAME LENGTH
         DC    H'&L'                    OPERAND LENGTH (SUBFLD EXCL)
         DC    C'&ON '                  OPERAND NAME
         SPACE
         AIF   ('&SUBFLD' EQ '').SX
X&SYSNDX DC    V(SUBEX)
         DC    A(&SUBFLD-&XPARM)
         SPACE
.SX      MEND
./ ADD NAME=OPERLIST 0100-80035-80035-1625-00011-00011-00000-KK00150
         MACRO
&OPNM    OPERLIST
         LCLA  &I
&OPNM    DC    F'0'                     LIST OF KNOWN OPERANDS
.CU      ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&SYSLIST).MEX
         DC    A(&SYSLIST(&I))
         AGO   .CU
.MEX     DC    F'0'                     END OF LIST
         MEND
./ ADD NAME=OPEROLD  0114-80035-82302-1204-00041-00017-00000-KK00150
         MACRO
&OPLB    OPER  &ON,&F,&EXIT=,&BLANK=,&MINLEN=,&KEYFLD=,&SUBFLD=
         GBLC  &XPARM
         LCLA  &I
         LCLA  &L
&OPLB    DS    0F                       FULLWORD BNDRY
         DC    &F                       FLAG LOC (SET IF OPER FOUND):
*                                       BYTE1=FLAG BIT,BYTE2=FLAG BYTE
         AIF   ('&EXIT&SUBFLD' EQ '').S1
&I       SETA  &I+8
.S1      AIF   ('&BLANK' EQ '').S2
&I       SETA  &I+4
.S2      AIF   ('&KEYFLD' EQ '').S2B
&I       SETA  &I+2
.S2B     AIF   ('&SUBFLD' EQ '').SS
&I       SETA  &I+1
.SS      DC    AL1(&I*16)        BYTE1= OPTIONS (80 = TAKE EXIT)
*              (40 = OVERLAY WITH BLANKS,20 = KEYWORD OPERAND)
*              (10 = SUBFIELD AREA OFFSET PROVIDED FOR SUBEX )
         DC    AL1(0&MINLEN)     BYTE2= MINIMUM LENGTH OF OPER TO MATCH
         AIF   ('&EXIT' EQ '').S3A
         DC    A(&EXIT)                 EXIT ADDR
         AGO   .S4
.S3A     AIF   ('&SUBFLD' EQ '').S3
         DC    A(X&SYSNDX)              EXTERNAL EXIT TRANSFER
         AGO   .S4
.S3      AIF   ('&KEYFLD' EQ '').SN    NO EXIT
         DC    A(&KEYFLD)         ADDRESS OF KEYFLD ->
*        HFWD  FIELD LEN, HFWD DATA LEN, DATA FIELD
         AGO   .S4
.SN      DC    A(0)                     NO EXIT
.S4      ANOP
&L       SETA  K'&ON                    NAME LENGTH
         DC    H'&L'                    OPERAND LENGTH (SUBFLD EXCL)
         DC    C'&ON'                   OPERAND NAME
         SPACE
         AIF   ('&SUBFLD' EQ '').SX
X&SYSNDX DC    V(SUBEX)
         DC    A(&SUBFLD-&XPARM)
         SPACE
.SX      MEND
./ ADD NAME=PARSEKMD 0107-88285-88286-1048-00088-00095-00000-KX00155
         MACRO
&ML      PARSEKMD &CBUF=CPPLCBUF,&OPLIST=OPLIST,&FLAG=FLAG,            X
               &EXIT=,&PARM=,&KPPL=KPPL,                               X
               &KEYLIST=,&KEYWORK=,&BUFF=,&BUFFLEN=,                   X
               &WORK=,&OPFLG=,&UNKFLG=
         GBLC  &XPARM
         LCLC  &EXNM,&FS,&BUFF$,&BUFFL$
&XPARM   SETC  '&PARM'
         AIF   ('&UNKFLG' EQ '').SU
&EXNM    SETC  'UNKX&SYSNDX'
         AGO   .SUOK
.SU      ANOP
&EXNM    SETC  '&EXIT'
.SUOK    ANOP
*
         MNOTE '*           &&CBUF=&CBUF,&&OPLIST=&OPLIST,&&FLAG=&FLAG'
         MNOTE '*           &&EXIT=&EXNM,&&PARM=&PARM,&&KPPL=&KPPL'
         MNOTE '*           &&KEYLIST=&KEYLIST,&&KEYWORK=&KEYWORK'
         MNOTE '*           &&WORK=&WORK,&&OPFLG=&OPFLG'
*
*       FIRST BUILD KOMAND-PROCESSING PARAMETER LIST:
*
&ML      XC    &KPPL.(40),&KPPL         CLEAR BUFFER PROC PARMLIST
         AIF   ('&BUFF' EQ '').CB
&BUFF$   SETC  '&BUFF'
         AIF   ('&BUFF'(1,1) NE '(').BOK
&BUFF$   SETC  '0&BUFF'
.BOK     LA    R15,&BUFF$
         ST    R15,CBUFPTR
&BUFFL$  SETC  '&BUFFLEN'
         AIF   ('&BUFFLEN'(1,1) NE '(').BLOK
&BUFFL$  SETC  '0&BUFFLEN'
.BLOK    LA    R15,&BUFFL$
         ST    R15,BUFFLEN
         MVC   KEYWPASS,=X'FFFFFFFF'    FLAG V2 PARMLIST (12 WD KPPL)
         AGO   .SB
.CB      L     R15,&CBUF                R15 -> CBUF
         ST    R15,CBUFPTR              BUFFER PTR
.SB      LA    R15,&OPLIST              OPERAND LIST
         ST    R15,OPLSTPTR             PASS IT
         LA    R15,&FLAG                FLAG AREA
         ST    R15,FLAGPTR              PASS IT
         AIF   ('&EXNM' EQ '').SK
         LA    R15,&EXNM                EXIT FOR OPS NOT IN MY LIST
         AIF   ('&KEYLIST' EQ '').S1A
         ST    R15,REEXPASS             PASS IT
         AGO   .SK
.S1A     ST    R15,UNKNEXIT             PASS IT
.SK      AIF   ('&KEYLIST' EQ '').S1B
         LA    R15,&KEYLIST       LIST OF KEYWORDS
         ST    R15,KEYLPASS       PASS IT
         L     R15,=V(KEYEX)      KEYWORD EXIT
         ST    R15,UNKNEXIT       PASS IT
.S1B     AIF   ('&KEYWORK' EQ '').S1
         AIF   ('&BUFF' EQ '').S1
         LA    R15,&KEYWORK       SAVEAREA AND FLAGAREA FOR KEYWD EXIT
         ST    R15,KEYWPASS       PASS IT
.S1      AIF   ('&PARM' EQ '').S2
         LA    R15,&PARM                WORK AREA FOR EXIT
         ST    R15,EXITPARM             PASS IT TO THE EXIT
.S2      AIF   ('&WORK' EQ '').S3
         LA    R15,&WORK                WORK AREA FOR KMDPARS
         ST    R15,WORKPASS             LENGTH PTR RETURN AREA
.S3      AIF   ('&OPFLG' EQ '').S4
         XC    &OPFLG(1),&OPFLG         CLEAR OPERANDS PRESENT FLAG
.S4      ANOP
*
*       THEN CALL KMDPARS (PARSE SUBROUTINE):
*
         LA    R1,KPPL                  R1 -> BUFFER PROC PARMLIST
         CALL  KMDPARS                  PROCESS AN OPERAND
         AIF   ('&OPFLG' EQ '').S5
         LTR   R15,R15                  ANYTHING FOUND?
         BNZ   B&SYSNDX                 YES
         OI    &OPFLG,X'FF'
.S5      AIF   ('&UNKFLG' EQ '').SU2
         B     B&SYSNDX
         SPACE
UNKX&SYSNDX L  R1,0(,R1)                R1->PARMAREA PASSED
&FS      SETC  '&UNKFLG-&PARM'.'(R1)'
         OI    &FS,X'FF'                SET FLAG FOR UNKN OPS
         SLR   R15,R15                  SAY OK TO CONTINUE
         BR    R14
         SPACE
.SU2     ANOP
B&SYSNDX DS    0H
         ANOP
         MEND
./ ADD NAME=PARSEOLD 0108-80035-82302-1522-00057-00044-00000-KK00150
         MACRO
&ML      PARSEKMD &CBUF=CPPLCBUF,&OPLIST=OPLIST,&FLAG=FLAG,            X
               &EXIT=,&PARM=,                                          X
               &KEYLIST=,&KEYWORK=,                                    X
               &WORK=,&OPFLG=
         GBLC  &XPARM
&XPARM   SETC  '&PARM'
*
         MNOTE '*           &&CBUF=&CBUF,&&OPLIST=&OPLIST,&&FLAG=&FLAG'
         MNOTE '*           &&EXIT=&EXIT,&&PARM=&PARM'
         MNOTE '*           &&KEYLIST=&KEYLIST,&&KEYWORK=&KEYWORK'
         MNOTE '*           &&WORK=&WORK,&&OPFLG=&OPFLG'
*
*       FIRST BUILD KOMAND-PROCESSING PARAMETER LIST:
*
&ML      XC    KPPL(40),KPPL            CLEAR BUFFER PROC PARMLIST
         L     R15,&CBUF                R15 -> CBUF
         ST    R15,CBUFPTR              BUFFER PTR
         LA    R15,&OPLIST              OPERAND LIST
         ST    R15,OPLSTPTR             PASS IT
         LA    R15,&FLAG                FLAG AREA
         ST    R15,FLAGPTR              PASS IT
         AIF   ('&EXIT' EQ '').SK
         LA    R15,&EXIT                EXIT FOR OPS NOT IN MY LIST
         AIF   ('&KEYLIST' EQ '').S1A
         ST    R15,REEXPASS             PASS IT
         AGO   .SK
.S1A     ST    R15,UNKNEXIT             PASS IT
.SK      AIF   ('&KEYLIST' EQ '').S1B
         LA    R15,&KEYLIST       LIST OF KEYWORDS
         ST    R15,KEYLPASS       PASS IT
         L     R15,=V(KEYEX)      KEYWORD EXIT
         ST    R15,UNKNEXIT       PASS IT
.S1B     AIF   ('&KEYWORK' EQ '').S1
         LA    R15,&KEYWORK       SAVEAREA AND FLAGAREA FOR KEYWD EXIT
         ST    R15,KEYWPASS       PASS IT
.S1      AIF   ('&PARM' EQ '').S2
         LA    R15,&PARM                WORK AREA FOR EXIT
         ST    R15,EXITPARM             PASS IT TO THE EXIT
.S2      AIF   ('&WORK' EQ '').S3
         LA    R15,&WORK                WORK AREA FOR KMDPARS
         ST    R15,WORKPASS             LENGTH PTR RETURN AREA
.S3      AIF   ('&OPFLG' EQ '').S4
         XC    &OPFLG(1),&OPFLG         CLEAR OPERANDS PRESENT FLAG
.S4      ANOP
*
*       THEN CALL KMDPARS (PARSE SUBROUTINE):
*
         LA    R1,KPPL                  R1 -> BUFFER PROC PARMLIST
         CALL  KMDPARS                  PROCESS AN OPERAND
         AIF   ('&OPFLG' EQ '').S5
         LTR   R15,R15                  ANYTHING FOUND?
         BNZ   B&SYSNDX                 YES
         OI    &OPFLG,X'FF'
B&SYSNDX DS    0H
.S5      ANOP
         MEND
./ ADD NAME=PARSKMD  0106-84233-84233-1521-00094-00077-00000-KK00150
         MACRO
&ML      PARSKMD &CBUF=CPPLCBUF,&OPLIST=OPLIST,&FLAG=FLAG,             X
               &EXIT=,&PARM=,                                          X
               &KEYLIST=,&KEYWORK=,                                    X
               &WORK=,&OPFLG=,&UNKFLG=
         GBLC  &XPARM
         LCLC  &EXNM,&FS
&XPARM   SETC  '&PARM'
         AIF   ('&UNKFLG' EQ '').SU
&EXNM    SETC  'UNKX&SYSNDX'
         AGO   .SUOK
.SU      ANOP
&EXNM    SETC  '&EXIT'
.SUOK    ANOP
*
*        PARSEKMD          CALLS KMDPARS PARSE SYSTEM
*
* OPERAND  DEFAULT    USAGE
*
* CBUF=    CPPLCBUF   ADDR OF COMMAND BUFFER (IKJPARS STD FMT)
* OPLIST=  OPLIST     LIST OF OPERAND DESCRIPTORS
* FLAG=    FLAG       AREA TO FLAG OPERANDS PRESENT
* EXIT=               ADDR OF EXIT TO CALL IF UNKN OPS PRESENT
* PARM=               ADDR TO PASS TO EXITS (R1 POINTS TO KPPL WORD
*                         CONTAINING THIS ADDR AT ENTRY TO EXITS)
* KEYLIST=            ADDR OF LIST OF KEYWORDS TO BE
*                         PROCESSED BY STD KEYEX EXIT RTN
* KEYWORK=            ADDR OF 20 WORD WORK AREA FOR KEYWORD EXITS
* WORK=               ADDR OF 600 BYTE WORKAREA FOR KMDPARS USE
* OPFLG=              ADDR OF FLAG TO SET IF OPERANDS PRESENT
* UNKFLG=             ADDR OF FLAG TO SET IF UNKN OPS PRESENT
*                         (EXCLUSIVE WITH EXIT=)
*
         EJECT
*
         MNOTE '*           &&CBUF=&CBUF,&&OPLIST=&OPLIST,&&FLAG=&FLAG'
         MNOTE '*           &&EXIT=&EXNM,&&PARM=&PARM'
         MNOTE '*           &&KEYLIST=&KEYLIST,&&KEYWORK=&KEYWORK'
         MNOTE '*           &&WORK=&WORK,&&OPFLG=&OPFLG'
*
*       FIRST BUILD KOMAND-PROCESSING PARAMETER LIST:
*
&ML      XC    KPPL(40),KPPL            CLEAR BUFFER PROC PARMLIST
         L     R15,&CBUF                R15 -> CBUF
         ST    R15,CBUFPTR              BUFFER PTR
         LA    R15,&OPLIST              OPERAND LIST
         ST    R15,OPLSTPTR             PASS IT
         LA    R15,&FLAG                FLAG AREA
         ST    R15,FLAGPTR              PASS IT
         AIF   ('&EXNM' EQ '').SK
         LA    R15,&EXNM                EXIT FOR OPS NOT IN MY LIST
         AIF   ('&KEYLIST' EQ '').S1A
         ST    R15,REEXPASS             PASS IT
         AGO   .SK
.S1A     ST    R15,UNKNEXIT             PASS IT
.SK      AIF   ('&KEYLIST' EQ '').S1B
         LA    R15,&KEYLIST       LIST OF KEYWORDS
         ST    R15,KEYLPASS       PASS IT
         L     R15,=V(KEYEX)      KEYWORD EXIT
         ST    R15,UNKNEXIT       PASS IT
.S1B     AIF   ('&KEYWORK' EQ '').S1
         LA    R15,&KEYWORK       SAVEAREA AND FLAGAREA FOR KEYWD EXIT
         ST    R15,KEYWPASS       PASS IT
.S1      AIF   ('&PARM' EQ '').S2
         LA    R15,&PARM                WORK AREA FOR EXIT
         ST    R15,EXITPARM             PASS IT TO THE EXIT
.S2      AIF   ('&WORK' EQ '').S3
         LA    R15,&WORK                WORK AREA FOR KMDPARS
         ST    R15,WORKPASS             LENGTH PTR RETURN AREA
.S3      AIF   ('&OPFLG' EQ '').S4
         XC    &OPFLG(1),&OPFLG         CLEAR OPERANDS PRESENT FLAG
.S4      ANOP
*
*       THEN CALL KMDPARS (PARSE SUBROUTINE):
*
         LA    R1,KPPL                  R1 -> BUFFER PROC PARMLIST
         CALL  KMDPARS                  PROCESS AN OPERAND
         AIF   ('&OPFLG' EQ '').S5
         LTR   R15,R15                  ANYTHING FOUND?
         BNZ   B&SYSNDX                 YES
         OI    &OPFLG,X'FF'
.S5      AIF   ('&UNKFLG' EQ '').SU2
         B     B&SYSNDX
         SPACE
UNKX&SYSNDX L  R1,0(,R1)                R1->PARMAREA PASSED
&FS      SETC  '&UNKFLG-&PARM'.'(R1)'
         OI    &FS,X'FF'                SET FLAG FOR UNKN OPS
         SLR   R15,R15                  SAY OK TO CONTINUE
         BR    R14
         SPACE
.SU2     ANOP
B&SYSNDX DS    0H
         ANOP
         MEND
./ ADD NAME=PRIME                                  17OCT89 12.47.53
         MACRO
&NAME    PRIME &REG,&SAVE,&CONS,&NTER,&EQU
.* THIS IS THE PRIME MACRO
.* ITS FUNCTION IS
.* 1.  ESTABLISH ADDRESSABILITY SAVE REGISTERS ACT AS ENTRY
.* 2.  GENERATE REG SAVE AREA OR DO GETMAIN
.* 3.  PROVIDE EXIT USED BY TERME MACRO
.* 4.  PROVIDE ADDIT SAVE AREAS FOR SUBROUTINES SO ONLY 1 BAL REG
         GBLA  &PERFD,&PERFL
         GBLC  &PERFR,&LAB,&SNTRY
         GBLB  &EQFLG,&PRIM
         LCLB  &REENT
         LCLA  &CNT,&LOOP,&INC
         LCLC  &BASE(3),&SCSECT,&PNAME
         AIF   (&PRIM).DUMB
&PRIM    SETB  1
&LAB     SETC  'ICC'.'&SYSNDX'
&SNTRY   SETC  '&LAB'.'A'
&SCSECT  SETC  '&SYSECT'
         AIF   (T'&NAME EQ 'O').CSCT0
         AIF   ('&SCSECT' NE '').CSCT1
&NAME    CSECT
&SCSECT  SETC  '&NAME'
         AGO   .CSCT0
.DUMB    MNOTE 8,'ONLY ONE PRIME MAY BE DEFINED'
         MEXIT
.CSCT1   MNOTE 'A CSECT IS ALREADY DEFINED.'
&PNAME   SETC  '&NAME'
.CSCT0   ANOP
         USING &SCSECT,R15
.*        AIF   ('&EQU' EQ '').A1
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
.A1      AIF   ('&CONS' EQ '').A2
&PNAME   SAVE  (14,12),,&CONS.-&SYSDATE.-&SYSTIME
         AGO   .A3
.A2      ANOP
&PNAME   SAVE  (14,12),,&SCSECT.-&SYSDATE.-&SYSTIME
.A3       ANOP
         LA    14,&LAB
         ST    14,&SNTRY
         B     &SNTRY+4
         AIF   ('&SAVE' NE '').A4
&REENT   SETB  1
.A4      AIF   ('&REG' EQ '').A6
         AIF   (N'&REG GT 3).BAD3
         AIF   (N'&REG LT 2).B1
&CNT     SETA  N'&REG
&LOOP    SETA  1
&INC     SETA  &LOOP+1
.A5      AIF   (T'&REG(&LOOP) NE 'N').BAD1
&BASE(&LOOP) SETC '&REG'(&INC,K'&REG(&LOOP))
&INC     SETA  &INC+K'&REG(&LOOP)+1
&LOOP    SETA  &LOOP+1
         AIF   (&LOOP LE &CNT).A5
         AGO   .A7
.B1      ANOP
&BASE(1) SETC  '&REG'
         AGO   .B2
.A6      ANOP
&BASE(1) SETC  '12'
.B2      ANOP
&CNT     SETA  1
.A7      ANOP
&SNTRY   DS    F
          DROP 15
         AIF   (&CNT NE 3).A8
         LA    &BASE(3),2048                     ESTABLISH ADDRESSAB
         LA    &BASE(2),2048(&BASE(3),15)        WITH THREE
         LA    &BASE(3),2048(&BASE(3),&BASE(2))  BASE REGS
         LR    &BASE(1),15
         USING &SCSECT,&BASE(1),&BASE(2),&BASE(3)
         AGO   .A10
.A8      AIF   (&CNT NE 2).A9
         LA    &BASE(2),2048                     ESTABLISH ADRESSAB
         LA    &BASE(2),2048(&BASE(2),15)        WITH TWO
         LR    &BASE(1),15                       BASE REGS
         USING &SCSECT,&BASE(1),&BASE(2)
         AGO   .A10
.A9      AIF   (&CNT NE 1).BAD4
         LR    &BASE(1),15                       ESTABLISH ADRESSAB
         USING &SCSECT,&BASE(1)
.A10     AIF   (&REENT).A11
         LA    15,&SAVE
         AGO   .A14
.A11     AIF   ('&NTER' NE '').A12
&PERFL   SETA  0
&CNT     SETA  72
         AGO   .A13
.A12     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  72+&NTER*4
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A13     ANOP
         GETMAIN R,LV=&CNT
         LR    15,1
.A14     ANOP
         LM    0,1,20(13)
         ST    13,4(15)                          CHAIN THE
         ST    15,8(13)                          SAVE AREAS
         LR    13,15
         L     15,&SNTRY
          BR   15
         AIF   (&REENT).A20
&LAB.X   L     13,4(13)
         AGO   .A21
.A20     ANOP
&LAB.X   LR    1,13
         L     13,4(13)
         LR    11,15
         FREEMAIN R,LV=&CNT,A=(1)
         LR    15,11
.A21     ANOP
         RETURN (14,12),T,RC=(15)
         AIF   (&REENT).A24
         AIF   ('&NTER' NE '').A22
&CNT     SETA  18
         AGO   .A23
.A22     AIF   (T'&NTER NE 'N').BAD5
&PERFL   SETA  &NTER
&PERFR   SETC  '14'
&PERFD   SETA  0
&CNT     SETA  &NTER+18
* GENERATED SAVE AREA EXTENDED FOR USE BY NTER AND XIT MACROS
.A23     ANOP
&SAVE    DC    &CNT.F'0'
.A24     ANOP
         LTORG
&LAB     DS    0H
         MEXIT
.BAD1    MNOTE 8,'FIRST PARAMETER/S MUST BE SELF DEFINING'
         MEXIT
.BAD3    ANOP
.BAD4    MNOTE 8,'THIS MACRO WILL HANDLE 1-3 BASE REGS'
         MEXIT
.BAD5    MNOTE 8,'THIRD PARAMETER MUST BE SELF DEFINING'
         MEND
./ ADD NAME=PROLOG                                 17OCT89 12.47.53
         MACRO
&NAME    PROLOG &REG,&CODE,&ID,&CLEAR=YES,&REGS=YES,&FPR=F,            X
               &GPR=R,&PARM=,&RENT=,&RREGS=(14,12),&SAVE=,&SP=0,       X
               &TYPE=,&USE=NO,&USING=12,&LIST=
.*
.*       GLOBAL VARIABLES DESCRIPTION
.*
.*       &REGSSW   BOOLEAN FLAG TO SIGNAL WHETHER REGISTER EQUATES
.*                    HAVE BEEN GENERATED IN A PREVIOUS EXPANSION
.*
.*       &REGSNM   CHARACTER STRING THAT CONTAINS THE EQUATE NAME
.*                    PREFIX FOR THE GENERAL REGISTERS (SEE &RN
.*                    VALUE DESCRIPTION BELOW)
.*
.*       &USNGSW   BOOLEAN FLAG TO SIGNAL WHETHER THE  XXUSE  MACRO
.*                    IS TO BE USED IN-PLACE OF THE USING STATEMENT
.*
         GBLB  &REGSSW,&USNGSW
         GBLC  &REGSNM
         LCLA  &A,&B,&C
         LCLC  &I,&N,&R,&RN,&X,&CSECT
*  PROLOG  MACRO -- ASSEMBLY PROLOG  01/14/78 MARK GOTO
         AIF   ('&LIST' EQ 'YES').COMNT
*          (CODE LIST=YES FOR FURTHER DESCRIPTION)
*
         AGO   .NOCOMNT
.COMNT   ANOP
*    &REG,&CODE,&ID  CONSULT OPERANDS DESCRIPTION OF OS/VS2 SAVE MACRO
*                    (SUPERVISOR SERVICES AND MACRO INSTR. GC28-0683)
*
*        *NOTE* = SPECIFYING &CODE=T INSURES THAT THE CONTENTS OF
*                 R14 AND R15 ARE SAVED
*        *NOTE* = IF THE LENGTH OF &ID IS > 8, THEN CURRENT DATE
*                 AND TIME WILL BE APPENDED TO THE ID STRING
*        *NOTE* = IF &RENT= PARAMETER IS SPECIFIED, THEN THE &REG
*                 AND &CODE PARAMETERS ARE IGNORED
*
*
*    ,&CLEAR=  YES/NO OPTION TO GENERATE CODE TO ZERO GETMAINED AREA
*
*        OPERATION = GENERATE CODE TO ZERO GETMAINED WORK AREA:
*
*          ORG *-2                          RE-ORIGIN OVER SVC
*          LR  3,0                          SAVE DSECT LENGTH
*          SVC 10                           ISSUE GETMAIN SVC
*          LR  2,1                          COPY DSECT AREA ADDR
*          SLR 14,14                        ZERO SOURCE ADDR
*          LR  15,14                        ZERO SOURCE LENGTH
*          MVCL 2,14                        ZERO DSECT AREA
*
*
*    ,&REGS=   YES/NO OPTION TO GENERATE REGISTER REGISTER EQUATES
*
*        OPERATION = GENERATE/SUPPRESS REGISTER EQUATES
*
*
*    ,&FPR=    PREFIX FOR GENERATING FLOATING POINT REGISTER EQUATES
*
*        OPERATION = GENERATE REGISTER EQUATES FOR 0,2,4,6
*
*
*    ,&GPR=    PREFIX FOR GENERATING GENERAL REGISTER EQUATES
*
*        OPERATION = GENERATE REGISTER EQUATES FOR 0 THRU 15
*
*
*    ,&PARM=   PARM ADDR REG.
*                 OR
*              (PARM ADDR REG. , LABEL)
*
*        OPERATION = LOAD DESIGNATED PARM REG. AND PROVIDE USING
*                    STMT. AS FOLLOWS:
*
*          LR  "PARM REG.",1
*                 OR
*          LR  "PARM REG.",1
*          USING "LABEL","PARM REG."
*
*        *NOTE* = "PARM REG." MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
*    ,&RENT=   DSECT ADDR REG.
*                 OR
*              (DSECT ADDR REG. , DSECT NAME , DSECT SIZE)
*
*        OPERATION = ISSUE GETMAIN, CONNECT GETMAIN AREA AS NEW SAVE
*                    AREA, LOAD DESIGNATED DSECT ADDR REG. AND PROVIDE
*                    USING STMT. AS FOLLOWS:
*
*          GETMAIN R,LV=72,...
*              .
*          LR  "DSECT REG.",1
*                          OR
*          GETMAIN R,LV="DSECT SIZE",...
*              .
*          LR  "DSECT REG.",1
*          USING "DSECT NAME","DSECT REG."
*
*        *NOTE* = "DSECT REG." MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*        *NOTE* = WHEN THIS PARAMETER IS SPECIFIED, &REG, &CODE AND
*                 &SAVE= PARAMETERS ARE IGNORED
*
*
*    ,&RREGS=  (FIRST REG. , LAST REG.)
*
*        OPERATION = SPECIFY REGS. TO BE SAVED AS FOLLOWS:
*
*          STM "FIRST REG.","LAST REG.",12(13)
*
*        *NOTE* = THIS PARAMETER IS IGNORED EXCEPT WHEN &RENT= IS
*                 SPECIFIED
*
*
*    ,&SAVE=   (TEMP REG. , SAVE AREA)
*                OR
*    ,&SAVE=   SAVE AREA
*
*        OPERATION = GENERATE CODE TO CONNECT SAVE AREAS AS FOLLOWS:
*
*          LA  "TEMP REG.","SAVE AREA"
*          ST  13,4(,"TEMP REG.")
*          ST  "TEMP REG.",8(,13)
*          LR  13,"TEMP REG."
*                OR
*          LR  15,13
*          LA  14,"SAVE AREA"
*          ST  13,4(,14)
*          ST  14,8(,13)
*          LR  13,14
*          LM  14,15,12(15)
*
*        *NOTE* = SPECIFYING THIS PARAMETER CAUSES THAT THE
*                 CONTENTS OF R14 AND R15 ARE SAVED
*        *NOTE* = "TEMP REG." MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*        *NOTE* = IF &RENT= IS SPECIFIED, THEN THIS PARAMETER IS
*                 IGNORED
*
*
*    ,&SP=     SUB-POOL NUMBER FOR RE-ENTRANT GETMAIN
*
*        OPERATION = SPECIFY THE GETMAIN SUB-POOL OPERAND:
*
*          GETMAIN R,LV=...,SP=...
*
*        *NOTE* = THIS PARAMETER DEFAULTS TO "SP=0" (IE. SUB-POOL
*                 NUMBER ZERO)
*
*
*    ,&TYPE=   'CSECT'   OR   ALIGNMENT SPECIFICATION
*
*        OPERATION = GENERATE 'CSECT', OR 'DS' ALIGNMENT STATEMENT
*                    WITH &NAME (IF &NAME IS OMITTED, THEN &SYSECT)
*                    PARAMETER IN THE NAME FIELD.  EXAMPLE:
*
*                    "&NAME" CSECT
*                                      OR
*                    "&NAME" DS  "ANY VALID OPERAND"
*
*
*    ,&USE=    YES/NO OPTION TO GENERATE  XXUSE  MACROS IN PLACE OF
*              USING STATEMENTS
*
*        OPERATION = GENERATE  XXUSE  MACROS -- USING STATEMENTS WITH
*                    MEMORY (THE COMPLEMENTARY MACRO IS  XXDRP ;
*                    WHICH MAY BE USED WITH NO OPERAND TO DROP ALL
*                    ACTIVE BASE REGISTERS)
*
*
*    ,&USING=  BASE REG.   OR   ,&USING=  (BASE REG. , LABEL)
*
*        OPERATION = GENERATE THE FOLLOWING CODE:
*
*          LR  "BASE REG.",15
*          USING "&NAME / &SYSECT","BASE REG."
*                          OR
*          LR  "BASE REG.",15
*          USING "LABEL","BASE REG."
*
*        *NOTE* = "BASE REG." MUST A DECIMAL CONSTANT OR AN
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A
*                 DECIMAL REGISTER NUMBER
*
*
.NOCOMNT AIF   ('&REGS' EQ 'NO').REGSSW
         AIF   (&REGSSW OR '&REGS' NE 'YES').NOREGS
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
&GPR.0   EQU   0
&GPR.1   EQU   1
&GPR.2   EQU   2
&GPR.3   EQU   3
&GPR.4   EQU   4
&GPR.5   EQU   5
&GPR.6   EQU   6
&GPR.7   EQU   7
&GPR.8   EQU   8
&GPR.9   EQU   9
&GPR.10  EQU   10
&GPR.11  EQU   11
&GPR.12  EQU   12
&GPR.13  EQU   13
&GPR.14  EQU   14
&GPR.15  EQU   15
&FPR.0   EQU   0
&FPR.2   EQU   2
&FPR.4   EQU   4
&FPR.6   EQU   6
         SPACE 1
.REGSSW  ANOP
&REGSNM  SETC  '&GPR'                      .SAVE GPR PREFIX
&REGSSW  SETB  1                           .SET FLAG TO AVOID
         AGO   .START                      . DUPLICATES IF RE-INVOKED
.NOREGS  MNOTE *,'REGS SUPRESSED'
.START   ANOP
&RN      SETC  '&REGSNM'
&USNGSW  SETB  (&USNGSW OR '&USE' EQ 'YES')
&N       SETC  '&NAME'                     .SAVE LABEL NAME ON MACRO
&CSECT   SETC  '&SYSECT'                   .SAVE CSECT NAME
         AIF   ('&TYPE' EQ '').NOTYPE      .SKIP IF TYPE OMITTED
&N       SETC  ''                          .SET NAME FIELD TO NULL
         AIF   ('&TYPE' NE 'CSECT').DSTYPE .MUST BE DS ALIGNMENT OPRND
&CSECT   SETC  '&NAME'                     .SET CSECT NAME
&NAME    CSECT
         AGO   .NOTYPE
.DSTYPE  ANOP
&NAME    DS    &TYPE
.NOTYPE  AIF   ('&ID' EQ '').NULLID        .SKIP IF ID OMITTED
&I       SETC  '&ID'
         AIF   ('&ID' EQ '*').NAMEID       .IF ID REQUESTED, GO BUILD
         AIF   (K'&I LE 8).SPLITID         .IF SIMPLE PGM NAME, GO USE
         AGO   .EXPANID                    .OTHERWISE, GO EXPAND
.NAMEID  AIF   ('&NAME' EQ '').CSECTID     .IF NO NAME, TRY CSECT ID
&I       SETC  '&NAME'
         AGO   .EXPANID                    .GO EXPAND NAME ID
.CSECTID AIF   ('&CSECT' EQ '').E4         .IF NO CSECT NAME, ERROR
&I       SETC  '&CSECT'
.EXPANID ANOP                              .APPEND ID INFORMATION
&I       SETC  '&I_&SYSDATE_'.'&SYSTIME'(1,2).':'.'&SYSTIME'(4,2)
.SPLITID ANOP
&A       SETA  ((K'&I+2)/2)*2+4            .COMPUTE BRANCH LENGTH
&N       B     &A.(0,15)                    BRANCH AROUND ID
&A       SETA  K'&I                        .SET IDENTIFIER LENGTH
         DC    AL1(&A)                      LENGTH OF IDENTIFIER
.SPLITLP AIF   (&A GT 24).SPLITUP
&X       SETC  '&I'(&B+1,&A)               .ISOLATE REMAINDER OF ID
         DC    CL&A'&X'                          IDENTIFIER
         AGO   .REGS
.SPLITUP ANOP
&X       SETC  '&I'(&B+1,24)               .SUBSTRING IDENTIFIER
         DC    CL24'&X'                          IDENTIFIER
&B       SETA  &B+24                       .INCREMENT POSITION
&A       SETA  &A-24                       .DECREMENT LENGTH
         AGO   .SPLITLP                    .GO SPLIT ID FURTHER
.NULLID  ANOP
&N       DS    0H
.REGS    AIF   ('&RENT' EQ '').REGSB       .SKIP IF NOT RE-ENTRANT
&A       SETA  &RREGS(1)                   .SET FIRST RENT REG. NO.
         AIF   (&A EQ 14).REGSA            .SKIP IF VALID
         MNOTE *,'RENT. REG(1) SET TO 14'
&A       SETA  14                          .CORRECT ERROR
.REGSA   ANOP
&B       SETA  &RREGS(2)                   .SET LAST RENT REG. NO.
         AIF   (&B GE 3 AND &B LE 12).REGSI     .SKIP IF VALID
         MNOTE *,'RENT. REG(2) SET TO 3'
&B       SETA  3                           .CORRECT ERROR
         AGO   .REGSI
.REGSB   AIF   ('&REG' EQ '').E1           .ERROR IF REGS OMITTED
         AIF   ('&CODE' NE '' AND '&CODE' NE 'T').E2 .INVALID CODE
&R       SETC  '&REG(1)'                   .GET REG. EXPRESSION
         AIF   (T'&REG(1) EQ 'N').REGSC    .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,K'&RN) NE '&RN').E3 .SKIP IF NOT REG. EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' LT '0' OR '&R' GT '15').E3 .ERROR IF INVALID
.REGSC   ANOP
&A       SETA  &R                          .SET FIRST SAVE REG. NO.
         AIF   (&A LT 0 OR &A GT 15).E3    .ERROR IF BAD REG. NO.
&B       SETA  &A                          .ASSUME LAST = FIRST
         AIF   (N'&REG EQ 1).REGSF         .SKIP IF ONLY ONE REG.
&R       SETC  '&REG(2)'                   .GET REG. EXPRESSION
         AIF   (T'&REG(2) EQ 'N').REGSE    .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,K'&RN) NE '&RN').E3 .ERROR IF NOT REG. EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' LT '0' OR '&R' GT '15').E3 .ERROR IF INVALID
.REGSE   ANOP
&B       SETA  &R                          .SET LAST SAVE REG. NO.
         AIF   (&B LT 0 OR &B GT 15).E3    .ERROR IF BAD REG. NO.
.REGSF   AIF   ('&SAVE' NE '').REGSG       .TREAT AS CODE 'T' IF SAVE
         AIF   ('&CODE' NE 'T').REGSI      .SKIP IF NOT CODE 'T'
.REGSG   AIF   (&A GE 14 OR &A LE 2).REGSH .SKIP IF R14 THRU R2
         STM   14,15,12(13)                 SAVE LINKAGE REGS
         AGO   .REGSI
.REGSH   ANOP
&A       SETA  14                          .SET FIRST SAVE REG. NO.
         AIF   (&B NE 14).REGSI            .INSURE THAT R15 IS SAVED
&B       SETA  15                          .SET LAST SAVE REG. NO.
.REGSI   ANOP
&C       SETA  &A*4+20                     .COMPUTE DISPLACEMENT
         AIF   (&C LE 75).REGSJ            .SKIP IF NOT TOO LARGE
&C       SETA  &C-64                       .READJUST
.REGSJ   AIF   (&A EQ &B).REGSK            .SKIP IF SAME REG. NO.
         STM   &A,&B,&C.(13)                SAVE REGISTERS
         AGO   .USING
.REGSK   ST    &A,&C.(,13)                  SAVE A REGISTER
.USING   AIF   ('&USING' EQ '').RENT       .ERROR IF OMITTED
&R       SETC  '&USING(1)'                 .GET REG. EXPRESSION
         AIF   ('&R' EQ '').USINGA         .SKIP IF NULL REG. NO.
         AIF   (T'&USING(1) EQ 'N').USINGB .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,K'&RN) NE '&RN').USINGA       .ERROR IF NOT EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' GE '0' AND '&R' LE '15').USINGB .SKIP IF VALID
.USINGA  ANOP
&R       SETC  '15'                        .SET DEFAULT REG.
.USINGB  AIF   (N'&USING EQ 1).USINGC      .SKIP IF NO SECOND OPERAND
&N       SETC  '&USING(2)'                 .GET ADDRESSIBILITY NAME
         AIF   ('&N' EQ '*').USINGH        .SKIP IF USING *,R
         AIF   ('&N' NE '').USINGE         .SKIP IF NON-BLANK LABEL
.USINGC  AIF   ('&NAME' EQ '').USINGD      .SKIP IF NO LABEL ON MACRO
&N       SETC  '&NAME'                     .SET ADDRESSIBILITY NAME
         AGO   .USINGE                     .GO GENERATE USING
.USINGD  AIF   ('&CSECT' EQ '').USINGH     .SKIP IF NO CSECT NAME
&N       SETC  '&CSECT'                    .SET ADDRESSIBILITY NAME
.USINGE  AIF   ('&R' EQ '15').USINGF       .SKIP IF DEFAULT REG.
         LR    &R,15                        LOAD BASE REGISTER
.USINGF  AIF   (&USNGSW).USINGG            .GENERATE SPECIAL USING
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY
         AGO   .RENT
.USINGG  ANOP
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY
         AGO   .RENT
.USINGH  ANOP
         BALR  &R,0                         GET BASE ADDRESS
         AIF   (&USNGSW).USINGI            .GENERATE SPECIAL USING
         USING *,&RN.&R                     DEFINE ADDRESSIBILITY
         AGO   .RENT
.USINGI  ANOP
         XXUSE *,&RN.&R                     DEFINE ADDRESSIBILITY
.RENT    AIF   ('&RENT' EQ '').SAVE        .SKIP IF NOT RE-ENTRANT
&R       SETC  '&RENT(1)'                  .GET REG. EXPRESSION
&N       SETC  ''                          .SET NULL DSECT NAME
&X       SETC  '72'                        .SET DEFAULT DSECT SIZE
&A       SETA  0                           .INDICATE NO LENGTH FOUND
         AIF   ('&R' EQ '').RENTB          .SKIP IF NULL REG. NO.
.*REPLCD AIF   (T'&RENT(1) EQ 'N').RENTC   .SKIP IF VALID REG. NO.
         AIF   (T'&R EQ 'N').RENTC         .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,1) NE '(').RENTA    .SKIP IF NOT PARENTHESIS
         AIF   (N'&RENT NE 2).RENTB        .SKIP IF WRONG NO. OF OPRNDS
&R       SETC  '&R'(2,K'&R-2)              .GET REG. NO.
&X       SETC  '&RENT(2)'
&A       SETA  2                           .SET POS. WHERE LEN. FOUND
         AGO   .GETMAIN
.RENTA   AIF   ('&R'(1,K'&RN) NE '&RN').RENTB   .SKIP IF NOT REG. EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' GE '0' AND '&R' LE '15').RENTC  .SKIP IF VALID
.RENTB   ANOP
&R       SETC  '13'                        .SET DEFAULT DSECT REG.
.RENTC   AIF   (N'&RENT LT 2).GETMAIN      .SKIP IF NO DESCT NAME
&N       SETC  '&RENT(2)'                  .SAVE DSECT NAME
.RENTD   AIF   (N'&RENT LT 3).GETMAIN      .SKIP IF NO DSECT SIZE
&X       SETC  '&RENT(3)'                  .SET DSECT LENGTH
&A       SETA  3                           .SET POS. WHERE LEN. FOUND
.GETMAIN GETMAIN R,LV=&X,SP=&SP
         AIF   ('&CLEAR' NE 'YES').CONNECT .SKIP IF NO CLEAR
         AIF   ('&X' EQ '72' OR &A EQ 0).XC     .GO USE XC
         AIF   (T'&RENT(&A) NE 'N').MVCL   .GO USE MVCL
         AIF   (&RENT(&A) LE 256).MVCL     .GO USE MVCL
.XC      XC    0(&X,1),0(1)                 ZERO SAVE AREA
         AGO   .CONNECT                    .GO CONNECT SAVE AREAS
.MVCL    ORG   *-2                          RE-ORIGIN OVER SVC
         LR    3,0                          SAVE DSECT LENGTH
         SVC   10                           ISSUE GETMAIN SVC
         LR    2,1                          COPY DSECT AREA ADDR
         SLR   14,14                        ZERO SOURCE ADDR
         LR    15,14                        ZERO COUNT
         MVCL  2,14                         ZERO DSECT AREA
.CONNECT ST    13,4(,1)                     SET OLD SAVE AREA ADDR
         ST    1,8(,13)                     SET NEW SAVE AREA ADDR
         LR    2,13                         COPY OLD SAVE AREA ADDR
         LR    13,1                         RESET SAVE AREA PTR
         LM    14,3,12(2)                   RESTORE DESTROYED REGS.
         AIF   ('&R' EQ '13').RENTE        .SKIP IF DEFAULT REG.
         LR    &R,13                        SET UP DSECT BASE
.RENTE   AIF   ('&N' EQ '').PARM           .SKIP IF NO DSECT NAME
         AIF   (&USNGSW).RENTF             .GENERATE SPECIAL USING
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY
         AGO   .PARM
.RENTF   ANOP
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY
         AGO   .PARM
.SAVE    AIF   ('&SAVE' EQ '').PARM        .SKIP IF SAVE AREA OMITTED
&X       SETC  ''                          .SET FLAG FOR SAVE RESTORE
&A       SETA  1                           .SET POS. OF SAVE LABEL
         AIF   (N'&SAVE LT 2).SAVEA        .SKIP IF REG. OMITTED
&R       SETC  '&SAVE(1)'                  .GET REG. EXPRESSION
&A       SETA  2                           .SET POS. OF SAVE LABEL
         AIF   ('&R' EQ '').SAVEA          .SKIP IF NULL REG. NO.
         AIF   (T'&SAVE(1) EQ 'N').SAVEB   .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,K'&RN) NE '&RN').SAVEA   .SKIP IF NOT EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' GE '0' AND '&R' LE '15').SAVEB  .SKIP IF VALID
.SAVEA   ANOP
&R       SETC  '14'
         LR    15,13                        SAVE OLD SAVE AREA ADDR
&X       SETC  '15'                        .INDICATE REGS. RESTORE
.SAVEB   ANOP
&N       SETC  '&SAVE(&A)'                 .GET SAVE AREA LABEL
         LA    &R,&N                        GET NEW SAVE AREA ADDR
         ST    13,4(,&R)                    SET OLD SAVE AREA ADDR
         ST    &R,8(,13)                    SET NEW SAVE AREA ADDR
         LR    13,&R                        RESET SAVE AREA PTR
         AIF   ('&X' EQ '').PARM           .SKIP REGS. RESTORE
         LM    &R,&X,12(&X)                 RESTORE DESTROYED REGISTERS
.PARM    AIF   ('&PARM' EQ '').MEND        .SKIP IF PARM OMITTED
&R       SETC  '&PARM(1)'                  .GET REG. EXPRESSION
&N       SETC  ''                          .SET DEFAULT PARM NAME
         AIF   ('&R' EQ '').PARMA          .SKIP IF NULL REG. NO.
         AIF   (T'&PARM(1) EQ 'N').PARMB   .SKIP IF VALID REG. NO.
         AIF   ('&R'(1,K'&RN) NE '&RN').PARMA   .SKIP IF NOT EQU
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.
         AIF   ('&R' GE '0' AND '&R' LE '15').PARMB  .SKIP IF VALID
.PARMA   ANOP
&R       SETC  '1'                         .SET DEFAULT REG.
.PARMB   AIF   (N'&PARM EQ 1).PARMC        .SKIP IF NO SECOND OPERAND
&N       SETC  '&PARM(2)'                  .GET PARM DSECT NAME
.PARMC   AIF   ('&R' EQ '1').PARMD         .SKIP IF DEFAULT REG.
         LR    &R,1                         LOAD PARM REGISTER
.PARMD   AIF   ('&N' EQ '').MEND           .SKIP IF NO PARM DSECT NAME
         AIF   (&USNGSW).PARME             .GENERATE SPECIAL USING
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY
         AGO   .MEND
.PARME   ANOP
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY
.MEND    MEXIT
.E1      IHBERMAC 18,360                   .REG PARAMETER MISSING
         MEXIT
.E2      IHBERMAC 37,360,&CODE             .INVALID CODE SPECIFIED
         MEXIT
.E3      IHBERMAC 36,360,&REG              .INVALID REGS. SPECIFIED
         MEXIT
.E4      IHBERMAC 78,360                   .CSECT NAME NULL
         AGO   .REGS
         MEND
./ ADD NAME=REGEQU   0102-77040-78145-1620-00019-00016-00003-KK00150
         MACRO
         REGEQU
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
./ ADD NAME=REGISTER 0102-77040-78145-1620-00019-00016-00003-KK00150
         MACRO
         REGISTER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
./ ADD NAME=REGS     0100-86169-86169-0950-00010-00010-00000-KX00150
         MACRO
         REGS  &A
         LCLA  &N
         SPACE 2
*        GENERATE REGISTER NAMES
.B       ANOP
R&N      EQU   &N
&N       SETA  &N+1
         AIF   (&N NE 16).B
         MEND
./ ADD NAME=REQU                                   17OCT89 12.47.53
         MACRO
&LABEL   REQU &RF,&RL
.* MACRO TO GENERATE EQUATES FOR REGISTERS: 0(R0) TO 15(R15).
.* IF NO OPERAND IS SPECIFIED 0-15 ASSUMED.
.* TWO OPERANDS MAY BE CODED FOR A SERIES OF REGISTERS.
.* ERROR CHECKING IS FOR LESS THAN 0 OR GREATER THAN 15.
.* IN CASE OF ERROR CONDITION, 0-15 ARE GENERATED WITH MNOTE.
         LCLA  &NUM,&MAX
         AIF   (T'&RF EQ 'O').ALL
         AIF   ('&RF' LT '0' OR '&RL' GT '15').OPERR
         AIF   ('&RL' LT '&RF').OPERR
         AIF   (N'&SYSLIST NE 2).OPERR
&NUM     SETA &RF
&MAX     SETA  &RL
         AGO   .GEN
.OPERR   ANOP
         MNOTE 0,'IMPROPER REGISTER SPECIFICATION. R0-R15 GENERATED.'
.ALL     ANOP
&NUM     SETA  0
&MAX     SETA  15
.GEN     ANOP
*****   EQUATES FOR REGISTERS   *****
.LOOP    ANOP
R&NUM    EQU   &NUM
         AIF   ('&NUM' EQ '&MAX').END
&NUM     SETA  &NUM+1
         AGO   .LOOP
.END     ANOP
         SPACE 2
MASK80   EQU   X'80'
MASK40   EQU   X'40'
MASK20   EQU   X'20'
MASK10   EQU   X'10'
MASK08   EQU   X'08'
MASK04   EQU   X'04'
MASK02   EQU   X'02'
MASK01   EQU   X'01'
         EJECT
         MEXIT
         MEND
./ ADD NAME=SASCOMU  0101-82111-82111-1727-00008-00008-00001-DN00150
       MACRO
&NAME  SASCOMU
.*  THIS MACRO DEFINES A USER WORK AREA WITHIN THE SAS COMMUNICATIONS
.*  BLOCK.
WHEADER DS  CL1
*   VALUE OF X'DC' INDICATES PREVIOUS PASSAGE THROUGH G78SEPH1
.*
        MEND
./ ADD NAME=SETOF                                  17OCT89 12.47.53
         MACRO
&LABEL   SETOF &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N,&O,&P
         LCLA  &NU
         LCLC  &VALUE
         LCLC  &#
&#       SETC  '#'
         AIF   ('&A' NE '').OK
         MNOTE 5,'MISSING OPERANDS - NO GEN'
         MEXIT
.OK      AIF   ('&LABEL' EQ '').B1
&LABEL   EQU   *
.B1      ANOP
&NU      SETA  N'&SYSLIST
.CHK     AIF   ('&NU' NE '0').LOOP
         SPACE
         MEXIT
.LOOP    ANOP
&VALUE   SETC  '&SYSLIST(&NU)'
         NI    &SYSLIST(&NU),255-&VALUE&#
&NU      SETA  &NU-1
         AGO   .CHK
         MEND
./ ADD NAME=SETON                                  17OCT89 12.47.53
         MACRO
&LABEL   SETON &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N,&O,&P
         LCLA  &NU
         LCLC  &VALUE
         LCLC  &#
&#       SETC  '#'
         AIF   ('&A' NE '').OK
         MNOTE 5,'MISSING OPERANDS - NO GEN'
         MEXIT
.OK      AIF   ('&LABEL' EQ '').B1
&LABEL   EQU   *
.B1      ANOP
&NU      SETA  N'&SYSLIST
.CHK     AIF   ('&NU' NE '0').LOOP
         SPACE
         MEXIT
.LOOP    ANOP
&VALUE   SETC  '&SYSLIST(&NU)'
         OI    &SYSLIST(&NU),&VALUE&#
&NU      SETA  &NU-1
         AGO   .CHK
         MEND
./ ADD NAME=SETPARSE                               17OCT89 12.47.53
         MACRO
         SETPARSE &PCL=,&ECB=MYECB,&ANS=MYANS
         GBLC  &SETBEF
         GBLC  &USE
         AIF   ('&USE' EQ '').ERROR
         MVC   PPLUPT(4),CPPLUPT       MOVE IN UPT
         MVC   PPLCBUF(4),CPPLCBUF     MOVE IN CBUF/
         MVC   PPLECT(4),CPPLECT       MOVE IN ECT
         L     R1,=V(&PCL)             GET ADDRESS OF PARM CONTROL
         ST    R1,PPLPCL               MOVE IT IN
         LA    R1,&ECB                 GET ADDRESS OF AN ECB
         ST    R1,PPLECB               MOVE IT IN
         XC    PPLUWA(4),PPLUWA        ZERO OUT THE WORK AREA
         LA    R1,&ANS                 GET ADDRESS OF AN ANSWER
*                                      PLACE
         ST    R1,PPLANS
         LA    R1,PPL                  FINALLY, LOAD R1 WITH A(PPL)
         AIF   ('&SETBEF' EQ 'SET').NOSYM
&SETBEF   SETC  'SET'
         B     SETP&SYSNDX
MYECB    DC    F'0'
MYANS    DC    F'0'
PPL      DS    0D                       START OF PARM CONTROL LIST
PPLUPT   DS    A
PPLECT   DS    A
PPLECB   DS    A
PPLPCL   DS    A
PPLANS   DS    A
PPLCBUF  DS    A
PPLUWA   DS    A
SETP&SYSNDX DS  0H
.NOSYM   ANOP
         MEND
.ERROR   ANOP
         MNOTE  12,'ERROR, MENTER MACRO MUST BE USED PREVIOSLY'
         MEND
./ ADD NAME=SETRET   0100-88281-88281-1408-00010-00010-00000-GS00155
         MACRO
&LBL     SETRET &L=RETURN,&RC=0
.**********************************************************************
.***   SETRET INITIALIZES REG 15 WITH A RETURN CODE AND BRANCHES    ***
.***   TO A SPECIFIED LABEL OR THE DEFAULT LABEL OF RETURN.  IF     ***
.***   NO RETURN CODE IS SPECIFIED, A RETURN CODE OF 0 IS SET.      ***
.**********************************************************************
&LBL     LA    15,&RC
         B     &L
         MEND
./ ADD NAME=SIOSCVT                                17OCT89 12.47.53
         MACRO
         SIOSCVT
**
**       S. I. O. COMMUNICATIONS VECTOR TABLE
**
         IEZBITS
**
SIOSCVT  DSECT
**
SIOSSVCP DC    A(0)                    ADDRESS OF SIOSSVC
SIOSRELP DC    A(0)                    ADDRESS OF SIOSREL
SIOSJISP DC    A(0)                    ADDRESS OF SIOSJIS
**
SIOSFLAG DC    XL4'0'                  PROCESSING FLAGS
SIOSSHFG EQU   BIT0                    SIO IS UP (AVAILABLE)
SIOS19FG EQU   BIT1                    SIO IS SHUTDOWN (WILL ONLY
*                                      BE SET IF CAN NOT BACKOUT SVC19)
SIOS64FG EQU   BIT2                    SIO IS SHUTDOWN (WILL ONLY
*                                      BE SET IF CAN NOT BACKOUT SVC64)
SIOSCVTL EQU   *-SIOSCVT               LENGTH OF SIO CVT
         MEND
./ ADD NAME=SIOSDVT                                17OCT89 12.47.53
         MACRO
         SIOSDVT &TYPE=TABLE,                                          X
               &P=DVT,                                                 X
               &DEVICE=,                                               X
               &BUFNUM=5,                                              X
               &BLKSIZE=
         GBLB  &FRST
         GBLA  &COUNT
         LCLA  &FTYPE
         LCLA  &BLKX
         LCLC  &DEVID
         AIF   (&FRST).DSCTX
&FRST    SETB  1
***********************************************************************
**                                                                   **
**                         VERSION 4.0                               **
**                                                                   **
**         SEQUENTIAL I/O OPTIMIZER DEVICE TABLE DEFINITION          **
**                                                                   **
**          (C) COPYRIGHT 1987  SYSTEM CONNECTIONS, INC.             **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' EQ 'DSECT').DSCT
SIOSDVTM CSECT                         TABLE START
         AGO   .GENT
.DSCT    ANOP
SIOSDVTM DSECT                         TABLE START
.GENT    ANOP
         SPACE 2
&P.SID   DC    CL8'SIOSDVTM'
&P.SNE   DC    H'0'                    NUMBER OF DEFINITIONS
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').DSCTX
**
**       TABLE DEFINITION
**
&P.STYP  DC    H'0'                    DEVICE TYPE
&P.SBLK  DC    H'0'                    BLOCK SIZE
&P.SBUF  DC    H'0'                    NUMBER OF BUFFERS
&P.SELL  EQU   *-&P.STYP               ENTRY LENGTH
         MEXIT
.DSCTX   ANOP
**
**       TABLE DEFINITION
**
&BLKX    SETA  &BLKSIZE
         AIF   ('&BLKX' LT '32761').BLKOKX
         MNOTE 16,'BLOCK SIZE=&BLKSIZE IS TOO LARGE'
         MEXIT
.BLKOKX  ANOP
&DEVID   SETC  '2009'
         AIF   ('&DEVICE' EQ '3330').DEVOK
&DEVID   SETC  '200A'
         AIF   ('&DEVICE' EQ '3340').DEVOK
&DEVID   SETC  '200B'
         AIF   ('&DEVICE' EQ '3350').DEVOK
&DEVID   SETC  '200C'
         AIF   ('&DEVICE' EQ '3375').DEVOK
&DEVID   SETC  '200E'
         AIF   ('&DEVICE' EQ '3380').DEVOK
&DEVID   SETC  '8003'
         AIF   ('&DEVICE' EQ '3420').DEVOK
&DEVID   SETC  '8080'
         AIF   ('&DEVICE' EQ '3480').DEVOK
         MNOTE 16,'INCORRECT DEVICE TYPE'
         MEXIT
.DEVOK   ANOP
         AIF   ('&DEVICE' NE '3330').NOT3330
         DC    X'200D'
         DC    AL2(&BLKSIZE)
         DC    AL2(&BUFNUM)
         ORG   &P.SNE
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
.NOT3330 ANOP
         DC    X'&DEVID'
         DC    AL2(&BLKSIZE)
         DC    AL2(&BUFNUM)
         ORG   &P.SNE
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIOSDVTM                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**       DEFINE THE MAXIMUM BLOCKSIZE FOR ALL THE DEVICES            **
**                                                                   **
**       IF YOU HAVE MULTIPLE DISK DEVICES, PLEASE MAKE SURE         **
**       THAT THE BLOCKSIZE YOU SELECT IS COMPATIBLE FOR ALL         **
**       THESE DEVICES.. FOR EXAMPLE IF YOU HAVE 3350'S AND          **
**       AND 3380'S SELECT A BLOCKSIZE OF ABOUT 6K OR 9K.            **
**       USING THE DEFAULT BLOCKSIZES IN THIS CASE MAY CAUSE         **
**       PROBLEMS FOR THE SORTOUT (SORT) FILES AND THE SYSUT2        **
**       (IEBGENER) FILES SINCE THE MAX BLOCKSIZE FOR A 3350         **
**       IS 19069.                                                   **
**                                                                   **
***********************************************************************
         SIOSDVT DEVICE=3330,BLKSIZE=13030,BUFNUM=5
         SIOSDVT DEVICE=3340,BLKSIZE=08368,BUFNUM=5
         SIOSDVT DEVICE=3350,BLKSIZE=19069,BUFNUM=5
         SIOSDVT DEVICE=3375,BLKSIZE=17600,BUFNUM=5
         SIOSDVT DEVICE=3380,BLKSIZE=23476,BUFNUM=5
         SIOSDVT DEVICE=3420,BLKSIZE=32760,BUFNUM=5
         SIOSDVT DEVICE=3480,BLKSIZE=32760,BUFNUM=5
         END
./ ADD NAME=SIOSMFR                                17OCT89 12.47.53
**
**       SIO SMF RECORD DESCRIPTION
**
SSRECRD  DC    0F'0'
SSRJOBN  DC    CL8' '                  JOB NAME
SSRSTEP  DC    CL8' '                  STEP NAME
SSRPROG  DC    CL8' '                  PROGRAM NAME
SSRDDNM  DC    CL8' '                  DDNAME
SSRDSNM  DC    CL44' '                 DATA SET NAME
SSRVOLS  DC    CL6' '                  VOLUME SERIAL NUMBER
**
SSRNBLK  DC    XL2'00'                 NEW BLOCK SIZE
**
SSRDATE  DC    0XL4'00'                RECORD DATE
SSRXIOT  DC    XL4'00'                 I/O TIME SAVED
**
SSRTIME  DC    0XL4'00'                RECORD TIME
SSRXEXC  DC    XL4'00'                 EXCP'S SAVED
**
SSRJOBD  DC    0XL4'00'                READER DATE
SSRXTRK  DC    XL4'00'                 TRACKS SAVED
**
SSRJOBT  DC    0XL4'00'                READER TIME
         DC    XL4'00'
**
SSREXCP  DC    XL4'00'                 EXCP COUNT
SSRDTYP  DC    XL4'00'                 DEVICE TYPE
SSRTRKS  DC    XL4'00'                 TRACKS/BLOCS USED
SSRIOTM  DC    XL4'00'                 I/O TIME
**
SSRUSEC  DC    XL2'00'                 USE COUNT
SSRCUAN  DC    XL2'00'                 DEV ADDRESS
SSRLREC  DC    XL2'00'                 RECORD LENGTH
SSRBLKS  DC    XL2'00'                 BLOCK SIZE
SSRECFM  DC    XL2'00'                 RECORD FORMAT
SSRDSRG  DC    XL2'00'                 DATA SET ORGANIZATION
**
SSRTYPE  DC    XL1'00'                 RECORD TYPE
**
SSRLENG  EQU   *-SSRECRD
./ ADD NAME=SIOSRL01                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**                BYPASS ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIOSTBR MODE=BYPASS,PROGRAM=GIMSMP
***********************************************************************
**                                                                   **
**                SELECT ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIOSTBR MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87            TEMP DATA SETS
         END
./ ADD NAME=SIOSRL02 0100-87309-87309-2012-00014-00014-00000-UTRTS10
***********************************************************************
**                                                                   **
**                BYPASS ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIOSTBR MODE=BYPASS,PROGRAM=GIMSMP
***********************************************************************
**                                                                   **
**                SELECT ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIOSTBR MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87            TEMP DATA SETS
         END
./ ADD NAME=SIOSTBL                                17OCT89 12.47.53
         MACRO
         SIOSTBL &TYPE=TABLE,                                          X
               &P=SIO,                                                 X
               &OFFSET=0,                                              X
               &MODE=,                                                 X
               &DSNAME=,                                               X
               &BLKSIZE=0,                                             X
               &BUFNUM=5,                                              X
               &PREFIX=,                                               X
               &DDNAME=,                                               X
               &DPREFIX=,                                              X
               &JOB=,                                                  X
               &JPREFIX=,                                              X
               &VOLSER=,                                               X
               &VPREFIX=,                                              X
               &STEP=,                                                 X
               &SPREFIX=,                                              X
               &DEVADDR=,                                              X
               &PROGRAM=,                                              X
               &PPREFIX=,                                              X
               &ACCMETH=QSAM,                                          X
               &ACTION=YES
         GBLB  &FRSTX
         GBLA  &COUNT
         LCLA  &FTYPE
         LCLA  &BLKV
         LCLA  &MODEG
         LCLC  &FIELD
         AIF   ('&TYPE' EQ 'DSECT').MODEOK
         AIF   ('&MODE' NE '').MODEF
         MNOTE 16,'MODE PARAMETER IS MISSING'
         MEXIT
.MODEF   ANOP
&MODEG   SETA  128                     ASSUME SELECT
         AIF   ('&MODE' EQ 'SELECT').MODEOK
&MODEG   SETA  64                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'EXEMPT').MODEOK
&MODEG   SETA  32                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'BYPASS').MODEOK
         MNOTE 16,'MODE=&MODE IS INVALID'
         MEXIT
.MODEOK  ANOP
         AIF   ('&ACCMETH' EQ 'QSAM').ISQSAM
         AIF   ('&ACCMETH' EQ 'BSAM').ISBSAM
         MNOTE 16,'ACCMETH=&ACCMETH IS INVALID'
         MEXIT
.ISBSAM  ANOP
&MODEG   SETA  &MODEG+2                SET CORRECT MODE
.ISQSAM  ANOP
         AIF   ('&ACTION' EQ 'YES').ISACTN
&MODEG   SETA  &MODEG+1                SET CORRECT MODE
.ISACTN  ANOP
&BLKV    SETA  &BLKSIZE
         AIF   ('&BLKV' LT '32761').BLKOK
         MNOTE 16,'BLOCK SIZE=&BLKSIZE IS TOO LARGE'
         MEXIT
.BLKOK   ANOP
         AIF   (&FRSTX).DSCTX
&FRSTX   SETB  1
***********************************************************************
**                                                                   **
**                         VERSION 4.0                               **
**                                                                   **
**          SEQUENTIAL I/O OPTIMIZER TABLE DEFINITION                **
**                                                                   **
**          (C) COPYRIGHT 1987 SYSTEM CONNECTIONS, INC.              **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' EQ 'DSECT').DSCT
&P.STBLM CSECT                         TABLE START
         AGO   .GENT
.DSCT    ANOP
&P.STBLM DSECT                         TABLE START
.GENT    ANOP
         SPACE 2
&P.SID   DC    CL8'SIOSTBLM'
&P.SNE   DC    AL2(0)                  NUMBER OF DEFINITIONS
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').DSCTX
**
**       TABLE DEFINITION
**
&P.SEL   DC    AL2(0)                  DEFINITION LENGTH
&P.BLK   DC    AL2(0)                  BLOCK SIZE
&P.BUF   DC    AL1(0)                  NUMBER OF BUFFERS
&P.SF1   DC    XL1'0'                  FLAG BYTE 1
&P.SSL   EQU   X'80'                   SELECT ENTRY
&P.SEX   EQU   X'40'                   EXEMPT ENTRY
&P.SIG   EQU   X'20'                   IGNORE ENTRY
&P.SBA   EQU   X'02'                   BSAM ENTRY
&P.SAC   EQU   X'01'                   ACTION FLAG
&P.STL   DC    AL1(0)                  DATA LENGTH
&P.STO   DC    AL1(0)                  DATA OFFSET
&P.SF2   DC    XL1'0'                  FLAG BYTE 2
&P.SNM   EQU   X'01'                   DSNAME FIELD
&P.SNP   EQU   X'02'                   DSNAME PREFIX
&P.SDM   EQU   X'03'                   DDNAME FIELD
&P.SDP   EQU   X'04'                   DDNAME PREFIX
&P.SJM   EQU   X'05'                   JOB NAME FIELD
&P.SJP   EQU   X'06'                   JOB NAME PREFIX
&P.SSM   EQU   X'07'                   STEP NAME FIELD
&P.SSP   EQU   X'08'                   STEP NAME PREFIX
&P.SVM   EQU   X'09'                   VOLUME SERIAL
&P.SVP   EQU   X'0A'                   VOLUME SERIAL PREFIX
&P.SPM   EQU   X'0B'                   PROGRAM NAME
&P.SPP   EQU   X'0C'                   PROGRAM NAME PREFIX
&P.SDV   EQU   X'0D'                   DEVICE DEVADDR
&P.SDT   EQU   *                       START OF DATA
         MEXIT
.DSCTX   ANOP
**
**       TABLE DEFINITION
**
         DC    AL2(N&SYSNDX-*)
         DC    AL2(&BLKSIZE)
         DC    AL1(&BUFNUM)
         DC    AL1(&MODEG)
         AIF   ('&DSNAME' EQ '').NODSN
&FIELD   SETC  '&DSNAME'
&FTYPE   SETA  1
         DC    AL1(L'A&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
A&SYSNDX DC    CL44'&FIELD'
.NODSN   ANOP
         AIF   ('&PREFIX' EQ '').NOPFX
&FIELD   SETC  '&PREFIX'
&FTYPE   SETA  2
         DC    AL1(L'B&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
B&SYSNDX DC    C'&FIELD'
.NOPFX   ANOP
         AIF   ('&DDNAME' EQ '').NODD
&FIELD   SETC  '&DDNAME'
&FTYPE   SETA  3
         DC    AL1(L'C&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
C&SYSNDX DC    CL8'&FIELD'
.NODD    ANOP
         AIF   ('&DPREFIX' EQ '').NODDPX
&FIELD   SETC  '&DPREFIX'
&FTYPE   SETA  4
         DC    AL1(L'D&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
D&SYSNDX DC    C'&FIELD'
.NODDPX  ANOP
         AIF   ('&JOB' EQ '').NOJOB
&FIELD   SETC  '&JOB'
&FTYPE   SETA  5
         DC    AL1(L'E&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
E&SYSNDX DC    CL8'&FIELD'
.NOJOB   ANOP
         AIF   ('&JPREFIX' EQ '').NOJPX
&FIELD   SETC  '&JPREFIX'
&FTYPE   SETA  6
         DC    AL1(L'F&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
F&SYSNDX DC    C'&FIELD'
.NOJPX   ANOP
         AIF   ('&STEP' EQ '').NOSTEP
&FIELD   SETC  '&STEP'
&FTYPE   SETA  7
         DC    AL1(L'G&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
G&SYSNDX DC    CL8'&FIELD'
.NOSTEP  ANOP
         AIF   ('&SPREFIX' EQ '').NOSTEPX
&FIELD   SETC  '&SPREFIX'
&FTYPE   SETA  8
         DC    AL1(L'H&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
H&SYSNDX DC    C'&FIELD'
.NOSTEPX ANOP
         AIF   ('&VOLSER' EQ '').NOVSN
&FIELD   SETC  '&VOLSER'
&FTYPE   SETA  9
         DC    AL1(L'I&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
I&SYSNDX DC    CL6'&FIELD'
.NOVSN   ANOP
         AIF   ('&VPREFIX' EQ '').NOVSNP
&FIELD   SETC  '&VPREFIX'
&FTYPE   SETA  10
         DC    AL1(L'J&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
J&SYSNDX DC    C'&FIELD'
.NOVSNP  ANOP
         AIF   ('&PROGRAM' EQ '').NOPGM
&FIELD   SETC  '&PROGRAM'
&FTYPE   SETA  11
         DC    AL1(L'K&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
K&SYSNDX DC    CL8'&FIELD'
.NOPGM   ANOP
         AIF   ('&PPREFIX' EQ '').NOPGMX
&FIELD   SETC  '&PPREFIX'
&FTYPE   SETA  12
         DC    AL1(L'L&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
L&SYSNDX DC    C'&FIELD'
.NOPGMX  ANOP
         AIF   ('&DEVADDR' EQ '').NOUNIT
&FIELD   SETC  '&DEVADDR'
&FTYPE   SETA  13
         DC    AL1(L'M&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
M&SYSNDX DC    CL3'&FIELD'
.NOUNIT  ANOP
N&SYSNDX EQU   *
         ORG   &P.SNE
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIOSTBR                                17OCT89 12.47.53
         MACRO
         SIOSTBR &TYPE=TABLE,                                          X
               &P=REL,                                                 X
               &OFFSET=0,                                              X
               &MODE=,                                                 X
               &DSNAME=,                                               X
               &PREFIX=,                                               X
               &DDNAME=,                                               X
               &DPREFIX=,                                              X
               &JOB=,                                                  X
               &JPREFIX=,                                              X
               &VOLSER=,                                               X
               &VPREFIX=,                                              X
               &STEP=,                                                 X
               &SPREFIX=,                                              X
               &DEVADDR=,                                              X
               &PROGRAM=,                                              X
               &PPREFIX=
         GBLB  &FRSTX
         GBLA  &COUNT
         LCLA  &FTYPE
         LCLA  &BLKV
         LCLA  &MODEG
         LCLC  &FIELD
         AIF   ('&TYPE' EQ 'DSECT').MODEOK
         AIF   ('&MODE' NE '').MODEF
         MNOTE 16,'MODE PARAMETER IS MISSING'
         MEXIT
.MODEF   ANOP
&MODEG   SETA  128                     ASSUME SELECT
         AIF   ('&MODE' EQ 'SELECT').MODEOK
&MODEG   SETA  32                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'BYPASS').MODEOK
         MNOTE 16,'MODE=&MODE IS INVALID'
         MEXIT
.MODEOK  ANOP
         AIF   (&FRSTX).DSCTX
&FRSTX   SETB  1
***********************************************************************
**                                                                   **
**                         VERSION 4.0                               **
**                                                                   **
**          SEQUENTIAL I/O OPTIMIZER TABLE DEFINITION                **
**                                                                   **
**          (C) COPYRIGHT 1987 SYSTEM CONNECTIONS, INC.              **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' EQ 'DSECT').DSCT
&P.STBLM CSECT                         TABLE START
         AGO   .GENT
.DSCT    ANOP
&P.STBLM DSECT                         TABLE START
.GENT    ANOP
         SPACE 2
&P.SID   DC    CL8'SIOSTREL'
&P.SNE   DC    AL2(0)                  NUMBER OF DEFINITIONS
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').DSCTX
**
**       TABLE DEFINITION
**
&P.SEL   DC    AL2(0)                  DEFINITION LENGTH
&P.SF1   DC    XL1'0'                  FLAG BYTE 1
&P.SSL   EQU   X'80'                   SELECT ENTRY
&P.SIG   EQU   X'20'                   IGNORE ENTRY
&P.STL   DC    AL1(0)                  DATA LENGTH
&P.STO   DC    AL1(0)                  DATA OFFSET
&P.SF2   DC    XL1'0'                  FLAG BYTE 2
&P.SNM   EQU   X'01'                   DSNAME FIELD
&P.SNP   EQU   X'02'                   DSNAME PREFIX
&P.SDM   EQU   X'03'                   DDNAME FIELD
&P.SDP   EQU   X'04'                   DDNAME PREFIX
&P.SJM   EQU   X'05'                   JOB NAME FIELD
&P.SJP   EQU   X'06'                   JOB NAME PREFIX
&P.SSM   EQU   X'07'                   STEP NAME FIELD
&P.SSP   EQU   X'08'                   STEP NAME PREFIX
&P.SVM   EQU   X'09'                   VOLUME SERIAL
&P.SVP   EQU   X'0A'                   VOLUME SERIAL PREFIX
&P.SPM   EQU   X'0B'                   PROGRAM NAME
&P.SPP   EQU   X'0C'                   PROGRAM NAME PREFIX
&P.SDV   EQU   X'0D'                   DEVICE DEVADDR
&P.SDT   EQU   *                       START OF DATA
         MEXIT
.DSCTX   ANOP
**
**       TABLE DEFINITION
**
         DC    AL2(N&SYSNDX-*)
         DC    AL1(&MODEG)
         AIF   ('&DSNAME' EQ '').NODSN
&FIELD   SETC  '&DSNAME'
&FTYPE   SETA  1
         DC    AL1(L'A&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
A&SYSNDX DC    CL44'&FIELD'
.NODSN   ANOP
         AIF   ('&PREFIX' EQ '').NOPFX
&FIELD   SETC  '&PREFIX'
&FTYPE   SETA  2
         DC    AL1(L'B&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
B&SYSNDX DC    C'&FIELD'
.NOPFX   ANOP
         AIF   ('&DDNAME' EQ '').NODD
&FIELD   SETC  '&DDNAME'
&FTYPE   SETA  3
         DC    AL1(L'C&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
C&SYSNDX DC    CL8'&FIELD'
.NODD    ANOP
         AIF   ('&DPREFIX' EQ '').NODDPX
&FIELD   SETC  '&DPREFIX'
&FTYPE   SETA  4
         DC    AL1(L'D&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
D&SYSNDX DC    C'&FIELD'
.NODDPX  ANOP
         AIF   ('&JOB' EQ '').NOJOB
&FIELD   SETC  '&JOB'
&FTYPE   SETA  5
         DC    AL1(L'E&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
E&SYSNDX DC    CL8'&FIELD'
.NOJOB   ANOP
         AIF   ('&JPREFIX' EQ '').NOJPX
&FIELD   SETC  '&JPREFIX'
&FTYPE   SETA  6
         DC    AL1(L'F&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
F&SYSNDX DC    C'&FIELD'
.NOJPX   ANOP
         AIF   ('&STEP' EQ '').NOSTEP
&FIELD   SETC  '&STEP'
&FTYPE   SETA  7
         DC    AL1(L'G&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
G&SYSNDX DC    CL8'&FIELD'
.NOSTEP  ANOP
         AIF   ('&SPREFIX' EQ '').NOSTEPX
&FIELD   SETC  '&SPREFIX'
&FTYPE   SETA  8
         DC    AL1(L'H&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
H&SYSNDX DC    C'&FIELD'
.NOSTEPX ANOP
         AIF   ('&VOLSER' EQ '').NOVSN
&FIELD   SETC  '&VOLSER'
&FTYPE   SETA  9
         DC    AL1(L'I&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
I&SYSNDX DC    CL6'&FIELD'
.NOVSN   ANOP
         AIF   ('&VPREFIX' EQ '').NOVSNP
&FIELD   SETC  '&VPREFIX'
&FTYPE   SETA  10
         DC    AL1(L'J&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
J&SYSNDX DC    C'&FIELD'
.NOVSNP  ANOP
         AIF   ('&PROGRAM' EQ '').NOPGM
&FIELD   SETC  '&PROGRAM'
&FTYPE   SETA  11
         DC    AL1(L'K&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
K&SYSNDX DC    CL8'&FIELD'
.NOPGM   ANOP
         AIF   ('&PPREFIX' EQ '').NOPGMX
&FIELD   SETC  '&PPREFIX'
&FTYPE   SETA  12
         DC    AL1(L'L&SYSNDX-1)
         DC    AL1(&OFFSET)
         DC    AL1(&FTYPE)
L&SYSNDX DC    C'&FIELD'
.NOPGMX  ANOP
         AIF   ('&DEVADDR' EQ '').NOUNIT
&FIELD   SETC  '&DEVADDR'
&FTYPE   SETA  13
         DC    AL1(L'M&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
M&SYSNDX DC    CL3'&FIELD'
.NOUNIT  ANOP
N&SYSNDX EQU   *
         ORG   &P.SNE
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIOSTB01                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**     THESE PROGRAMS WILL BE BYPASSED DURING SIO PROCESSING         **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=BYPASS,PROGRAM=IEWL
         SIOSTBL MODE=BYPASS,PROGRAM=HEWL
         SIOSTBL MODE=BYPASS,PROGRAM=HMASMP
         SIOSTBL MODE=BYPASS,PROGRAM=GIMSMP
         SIOSTBL MODE=BYPASS,PROGRAM=IEHINITT
         SIOSTBL MODE=BYPASS,PROGRAM=TMSTPNIT
         SIOSTBL MODE=BYPASS,PROGRAM=ICNRTNDF
***********************************************************************
**                                                                   **
**                     EXEMPT ENTRIES                                **
**                                                                   **
***********************************************************************
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       BECAUSE REBLOCKING WILL CREATE PROBLEMS
**       FOR THE ASSEMBLER, LINKAGE-EDITOR AND SMP.
**
         SIOSTBL MODE=EXEMPT,DDNAME=SYSPUNCH
         SIOSTBL MODE=EXEMPT,DDNAME=SYSGO
         SIOSTBL MODE=EXEMPT,DDNAME=SYSLIN
         SIOSTBL MODE=EXEMPT,DDNAME=DVDCODE
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       FOR DISOSS
**
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRKSS
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRK
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRKJB
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (QSAM)                             **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87,           TEMP DATA SETS                  X
               ACCMETH=QSAM            INDICATE QSAM
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (BSAM)                             **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=SELECT,          SELECT THESE FILES:             X
               PROGRAM=IEBGENER,       IEBGENER USES BSAM              X
               DDNAME=SYSUT1,          INPUT FILE ONLY                 X
               ACCMETH=BSAM            INDICATE BSAM USED
         END
./ ADD NAME=SIOSTB02 0100-87309-87309-1622-00055-00055-00000-UTRTS10
***********************************************************************
**                                                                   **
**     THESE PROGRAMS WILL BE BYPASSED DURING SIO PROCESSING         **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=BYPASS,PROGRAM=IEWL
         SIOSTBL MODE=BYPASS,PROGRAM=HEWL
         SIOSTBL MODE=BYPASS,PROGRAM=HMASMP
         SIOSTBL MODE=BYPASS,PROGRAM=GIMSMP
         SIOSTBL MODE=BYPASS,PROGRAM=IEHINITT
         SIOSTBL MODE=BYPASS,PROGRAM=TMSTPNIT
         SIOSTBL MODE=BYPASS,PROGRAM=ICNRTNDF
***********************************************************************
**                                                                   **
**                     EXEMPT ENTRIES                                **
**                                                                   **
***********************************************************************
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       BECAUSE REBLOCKING WILL CREATE PROBLEMS
**       FOR THE ASSEMBLER, LINKAGE-EDITOR AND SMP.
**
         SIOSTBL MODE=EXEMPT,DDNAME=SYSPUNCH
         SIOSTBL MODE=EXEMPT,DDNAME=SYSGO
         SIOSTBL MODE=EXEMPT,DDNAME=SYSLIN
         SIOSTBL MODE=EXEMPT,DDNAME=DVDCODE
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       FOR DISOSS
**
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRKSS
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRK
         SIOSTBL MODE=EXEMPT,DDNAME=DSVWRKJB
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (QSAM)                             **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87,           TEMP DATA SETS                  X
               ACCMETH=QSAM            INDICATE QSAM
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (BSAM)                             **
**                                                                   **
***********************************************************************
         SIOSTBL MODE=SELECT,          SELECT THESE FILES:             X
               PROGRAM=IEBGENER,       IEBGENER USES BSAM              X
               DDNAME=SYSUT1,          INPUT FILE ONLY                 X
               ACCMETH=BSAM            INDICATE BSAM USED
         SIOSTBL MODE=SELECT,          SELECT THESE FILES:             X
               PROGRAM=IEBGENER,       IEBGENER USES BSAM              X
               DDNAME=SYSUT2,          INPUT FILE ONLY                 X
               ACCMETH=BSAM            INDICATE BSAM USED
         END
./ ADD NAME=SIO5DVBF                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**       DEFINE THE MAXIMUM BLOCKSIZE FOR ALL THE DEVICES            **
**                                                                   **
**       IF YOU HAVE MULTIPLE DISK DEVICES, PLEASE MAKE SURE         **
**       THAT THE BLOCKSIZE YOU SELECT IS COMPATIBLE FOR ALL         **
**       THESE DEVICES.. FOR EXAMPLE IF YOU HAVE 3350'S AND          **
**       AND 3380'S SELECT A BLOCKSIZE OF ABOUT 6K OR 9K.            **
**       USING THE DEFAULT BLOCKSIZES IN THIS CASE MAY CAUSE         **
**       PROBLEMS FOR THE SORTOUT (SORT) FILES AND THE SYSUT2        **
**       (IEBGENER) FILES SINCE THE MAX BLOCKSIZE FOR A 3350         **
**       IS 19069.                                                   **
**                                                                   **
***********************************************************************
         SIO5DVT DEVICE=3330,BLKSIZE=13030
         SIO5DVT DEVICE=3340,BLKSIZE=08368
         SIO5DVT DEVICE=3350,BLKSIZE=19069
         SIO5DVT DEVICE=3375,BLKSIZE=17600
         SIO5DVT DEVICE=3380,BLKSIZE=23476
         SIO5DVT DEVICE=3420,BLKSIZE=32760
         SIO5DVT DEVICE=3480,BLKSIZE=32760
         END
./ ADD NAME=SIO5DVT                                17OCT89 12.47.53
         MACRO
         SIO5DVT &TYPE=TABLE,                                          X
               &P=DVT,                                                 X
               &DEVICE=,                                               X
               &BUFNUM=(NUMBER,0),                                     X
               &BLKSIZE=
         GBLB  &FIRST
         GBLA  &COUNT
         LCLA  &BLKSZ
         LCLA  &BNUMB
         LCLA  &BXTYP
         LCLC  &BTYPE
         LCLC  &DEVID
         AIF   (&FIRST).NOTFST
***********************************************************************
**                                                                   **
**                         VERSION 5.0                               **
**                                                                   **
**         SEQUENTIAL I/O OPTIMIZER DEVICE TABLE DEFINITION          **
**                                                                   **
**          (C) COPYRIGHT 1987  SYSTEM CONNECTIONS, INC.             **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
.NOTFST  ANOP
         AIF   ('&TYPE' NE 'DSECT').DSCTX
SIO5DVTD DSECT                         TABLE START
         SPACE 2
         SIO5MID TYPE=NOLB,P=&P
         SPACE 2
&P.SNE   DC    H'0'                    NUMBER OF DEFINITIONS
**
**       TABLE DEFINITION
**
&P.STYP  DC    XL2'00'                 DEVICE TYPE
&P.SBLK  DC    AL2(0)                  MAX BLOCK SIZE
&P.BUTY  DC    AL1(0)                  BUFFER UNIT TYPE
&P.NBUF  EQU   X'01'                   NUMBER OF BUFFERS DEF
&P.SPAC  EQU   X'02'                   BUFFER SPACE DEFINED
&P.TRAK  EQU   X'03'                   TRACK SIZE DEF BUF #
&P.SBUF  DC    AL3(0)                  BUFFER UNIT COUNT
&P.SELL  EQU   *-&P.STYP               ENTRY LENGTH
         MEXIT
.DSCTX   ANOP
         AIF   (&FIRST).DOENTR
&FIRST   SETB  1
SIO5DVTM CSECT                         TABLE START
         SPACE 2
         SIO5MID
         SPACE 2
&P.NOD   DC    H'0'                    NUMBER OF DEFINITIONS
         SPACE 2
.DOENTR  ANOP
**
**       TABLE DEFINITION
**
&BTYPE   SETC  '&BUFNUM(1)'
         AIF   ('&BTYPE' EQ 'NUMBER').BTYPNB
         AIF   ('&BTYPE' EQ 'SPACE').BTYPSP
         AIF   ('&BTYPE' EQ 'TRACKS').BTYPTR
         MNOTE 16,'BUFFER TYPE=&BTYPE IS INVALID'
         MEXIT
.BTYPNB  ANOP
&BXTYP   SETA  1
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' LT '256').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPSP  ANOP
&BXTYP   SETA  2
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' GT '7').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPTR  ANOP
&BXTYP   SETA  3
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' LT '256').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPOK  ANOP
&BLKSZ   SETA  &BLKSIZE
         AIF   ('&BLKSZ' LT '32761').BLKOKX
         MNOTE 16,'BLOCK SIZE=&BLKSIZE IS TOO LARGE'
         MEXIT
.BLKOKX  ANOP
&DEVID   SETC  '2009'
         AIF   ('&DEVICE' EQ '3330').DEVOK
&DEVID   SETC  '200A'
         AIF   ('&DEVICE' EQ '3340').DEVOK
&DEVID   SETC  '200B'
         AIF   ('&DEVICE' EQ '3350').DEVOK
&DEVID   SETC  '200C'
         AIF   ('&DEVICE' EQ '3375').DEVOK
&DEVID   SETC  '200E'
         AIF   ('&DEVICE' EQ '3380').DEVOK
&DEVID   SETC  '8003'
         AIF   ('&DEVICE' EQ '3420').DEVOK
&DEVID   SETC  '8080'
         AIF   ('&DEVICE' EQ '3480').DEVOK
         MNOTE 16,'INCORRECT DEVICE TYPE'
         MEXIT
.DEVOK   ANOP
         AIF   ('&DEVICE' NE '3330').NOT3330
         DC    X'200D'
         DC    AL2(&BLKSIZE)
         DC    AL1(&BXTYP)
         DC    AL3(&BNUMB)
         ORG   &P.NOD
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
.NOT3330 ANOP
         DC    X'&DEVID'
         DC    AL2(&BLKSIZE)
         DC    AL1(&BXTYP)
         DC    AL3(&BNUMB)
         ORG   &P.NOD
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIO5DV01                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**       DEFINE THE MAXIMUM BLOCKSIZE FOR ALL THE DEVICES            **
**                                                                   **
**       IF YOU HAVE MULTIPLE DISK DEVICES, PLEASE MAKE SURE         **
**       THAT THE BLOCKSIZE YOU SELECT IS COMPATIBLE FOR ALL         **
**       THESE DEVICES.. FOR EXAMPLE IF YOU HAVE 3350'S AND          **
**       AND 3380'S SELECT A BLOCKSIZE OF ABOUT 6K OR 9K.            **
**       USING THE DEFAULT BLOCKSIZES IN THIS CASE MAY CAUSE         **
**       PROBLEMS FOR THE SORTOUT (SORT) FILES AND THE SYSUT2        **
**       (IEBGENER) FILES SINCE THE MAX BLOCKSIZE FOR A 3350         **
**       IS 19069.                                                   **
**                                                                   **
***********************************************************************
         SIO5DVT DEVICE=3330,BLKSIZE=13030,BUFNUM=(SPACE,65536)
         SIO5DVT DEVICE=3340,BLKSIZE=08368,BUFNUM=(SPACE,65536)
         SIO5DVT DEVICE=3350,BLKSIZE=19069,BUFNUM=(TRACKS,3)
         SIO5DVT DEVICE=3375,BLKSIZE=17600,BUFNUM=(TRACKS,3)
         SIO5DVT DEVICE=3380,BLKSIZE=23476,BUFNUM=(TRACKS,2)
         SIO5DVT DEVICE=3420,BLKSIZE=32760,BUFNUM=(NUMBER,5)
         SIO5DVT DEVICE=3480,BLKSIZE=32760,BUFNUM=(NUMBER,5)
         END
./ ADD NAME=SIO5EXT                                17OCT89 12.47.53
         TITLE '"SIOSEXT" SEQUENTIAL I/O OPTIMIZER USER EXIT MODULE'
**
**       REGISTER DEFINITIONS
**
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         DCBD  DSORG=PS
         EJECT
DEXJOBD  DSECT
**
**       JOB DEFINITIONS PASSED TO THE EXIT
**
DEXJOBN  DC    CL8' '                  JOB NAME
         DC    CL1' '
DEXSTEP  DC    CL8' '                  STEP NAME
         DC    CL1' '
DEXDDNM  DC    CL8' '                  DDNAME
         DC    CL1' '
DEXPGMN  DC    CL8' '                  PROGRAM NAME
         DC    CL1' '
DEXVOLS  DC    CL6' '                  VOLUME SERIAL NUMBER
         DC    CL1' '
DEXBLKF  DC    CL3'   '                BLOCK SIZE DEFINED (EXT/PGM)
         DC    CL1' '
         DC    CL7' '
DEXOBLK  DC    CL5' '                  OLD BLOCK SIZE
         DC    CL1' '
         DC    CL7' '
DEXNBLK  DC    CL5' '                  NEW BLOCK SIZE
         DC    CL1' '
DEXATYP  DC    CL6' '                  OPEN TYPE (INPUT/OUTPUT)
         DC    CL1' '
DEXDSNM  DC    CL44' '                 DATA SET NAME
         EJECT
SIO5EXT  CSECT
**
**       THE SIO5MID MACRO MUST BE USED FOR THE RELEASE 5 USER
**       EXIT EVEN IF THE NAME IS CHANGED FROM SIO5DEX TO OTHER
**       NAME.  THE CSECT NAME MUST BE THE SAME AS THE ONE CODED
**       IN THE GLOBAL TABLE. DIFFERENCE IN THESE NAMES WILL
**       PREVENT SIO INITIALIZATION.
**
         SIO5MID
**
**       EPA FROM SIO IS HERE
**
         USING *,R15
**
**       REGISTER ONE POINTS TO THE EXIT PARAMETER LIST.
**
         SPACE 2
**
**       THIS IS A DUMMY SIO USER EXIT
**
         SLR   R15,R15                  CLEAR THE RETURN CODE
         BR    R14                      AND RETURN TO SIO
         END
./ ADD NAME=SIO5GBBF                               17OCT89 12.47.53
         SIO5GBL MODE=MIXMOD,          MODE OF OPERATION               X
               SYSUFX=BF,              SYSTEM TABLE SUFFIX             X
               RLSUFX=BF,              RELEASE TABLE SUFFIX            X
               DVSUFX=BF,              DEVICE TABLE SUFFIX             X
               BLKMSG=NO,              OPTIMIZATION MSG (WTO/WTL/NO)   X
               BUFMSG=WTO,             BUFF CHANGE MSG(WTO/WTL/NO)     X
               TSOMSG=NO,              MESSAGES TO TSO USERS? (YES/NO) X
               SETBLK=NO,              REBLOCK DATASETS? (YES/NO)      X
               PGMDCB=NO,              DO HARD CODED DCB'S? (YES/NO)   X
               SMFREC=0,               "0" NO SMF RECORDS BUILT        X
               RPG=NO,                 CODE YES IF USING RPG           X
               SORTIN=(SORTIN),        SORT IN DD NAME LIST            X
               EXITNM=SIO5EXT,         USER EXIT NAME                  X
               SETBUF=YES              CHANGE THE BUFFERS (YES/NO)
         END
./ ADD NAME=SIO5GBL                                17OCT89 12.47.53
         MACRO
         SIO5GBL &TYPE=TABLE,                                          X
               &P=GBT,                                                 X
               &MODE=MIXMOD,                                           X
               &SYSUFX=01,                                             X
               &RLSUFX=01,                                             X
               &DVSUFX=01,                                             X
               &BLKMSG=WTO,                                            X
               &BUFMSG=NO,                                             X
               &TSOMSG=NO,                                             X
               &SETBLK=NO,                                             X
               &PGMDCB=NO,                                             X
               &SMFREC=0,                                              X
               &RPG=NO,                                                X
               &SORTIN=(SORTIN),                                       X
               &EXITNM=SIO5EXT,                                        X
               &SETBUF=NO
         LCLA  &INDEX,&COUNT,&VALUE
         LCLC  &TEXT
***********************************************************************
**                                                                   **
**                         VERSION 5.0                               **
**                                                                   **
**         SEQUENTIAL I/O OPTIMIZER GLOBAL DEFAULTS DEFINITION       **
**                                                                   **
**          (C) COPYRIGHT 1987  SYSTEM CONNECTIONS, INC.             **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').CSCT
SIO5GBLD DSECT                         TABLE START
         SPACE 2
         SIO5MID TYPE=NOLB,P=&P
         SPACE 2
&P.TBSZ  DC    A(0)                    TABLE SIZE
         SPACE 2
**
**       TABLE DEFINITION
**
&P.SVCP  DC    A(0,0)                  ADR/LEN OF SIO5SVC
&P.RELP  DC    A(0,0)                  ADR/LEN OF SIO5REL
&P.JISP  DC    A(0,0)                  ADR/LEN OF SIO5JIS
&P.DCBE  DC    A(0,0)                  ADR/LEN OF SIO5DEX
&P.TBLM  DC    A(0,0)                  ADR/LEN OF SIO5TBXX
&P.DVTM  DC    A(0,0)                  ADR/LEN OF SIO5DVXX
&P.RELT  DC    A(0,0)                  ADR/LEN OF SIO5RLXX
&P.UEXI  DC    A(0,0)                  ADR/LEN OF USER EXIT
**
&P.SC19  DC    A(0)                    SVC 19 ADDRESS
&P.SC64  DC    A(0)                    SVC 64 ADDRESS
**
&P.VSAM  DC    A(0,0)                  ADR/LEN OF SIO5VSM
**
&P.SMFR  DC    AL1(0)                  SMF RECORD ID
**
&P.RUNT  DC    CL1' '                  RUN TYPE (T/A)
**
         DC    AL2(0)
**
&P.SYSP  DC    A(0)                    SYSPRINT DCB LOC
**
&P.OPTC  DC    F'0'                    OPTIMIZED COUNT
**
&P.USEX  DC    CL8' '                  NAME OF USER EXIT
**
&P.GBFX  DC    CL2'00'                 GLOBAL TABLE SUFFIX
&P.SYFX  DC    CL2'00'                 SYSTEM TABLE SUFFIX
&P.RLFX  DC    CL2'00'                 RELEASE TABLE SUFFIX
&P.DVFX  DC    CL2'00'                 DEVICE TABLE SUFFIX
**
&P.PRFL  DC    XL1'0'                  PROCESSING FLAGS
&P.PROU  EQU   X'00'                   SIO IS SHUTDOWN
&P.PRUP  EQU   X'80'                   SIO IS AVAILABLE
**
&P.MDFL  DC    XL1'00'                 MODE OF OPER FLAG
&P.MDSL  EQU   X'01'                   SELECT MODE
&P.MDEX  EQU   X'02'                   EXEMPT MODE
&P.MDMX  EQU   X'03'                   MIXMOD MODE
**
&P.OPMG  DC    XL1'00'                 OPTIMIZATION MESSAGE
&P.OPOM  EQU   X'01'                   ISSUE WTO MESSAGE
&P.OPLM  EQU   X'02'                   ISSUE WTL MESSAGE
&P.OPNO  EQU   X'03'                   NO MESSAGE GENERATED
**
&P.BFMG  DC    XL1'00'                 BUFFER CHANGE MESSAGE
&P.BFOM  EQU   X'01'                   ISSUE WTO MESSAGE
&P.BFLM  EQU   X'02'                   ISSUE WTL MESSAGE
&P.BFNO  EQU   X'03'                   NO MESSAGE GENERATED
**
&P.TSMG  DC    XL1'00'                 TSO MESSAGE
&P.TSYE  EQU   X'01'                   ISSUE WTO MESSAGE
&P.TSNO  EQU   X'02'                   DO NOT ISSUE MESSAGE
**
&P.ACTF  DC    XL1'00'                 SETBLK (REBLOCK) FLAG
&P.ACTY  EQU   X'01'                   DO THE REBLOCKING
&P.ACTN  EQU   X'02'                   DO NOT REBLOCK FILES
**
&P.DCBF  DC    XL1'00'                 HARD CODED DCB FLAG
&P.DCBY  EQU   X'01'                   DO THE REBLOCKING
&P.DCBN  EQU   X'02'                   DO NOT REBLOCK FILES
**
&P.BUFF  DC    XL1'00'                 BUFFER (REBUFFER) FLAG
&P.BUFY  EQU   X'01'                   CHANGE THE # OF BUFFERS
&P.BUFN  EQU   X'02'                   DO NOT CHANGE # BUFFERS
**
&P.RPGF  DC    XL1'00'                 R.P.G. PROGRAMS FLAG
&P.RPGY  EQU   X'01'                   SHOP HAS RPG PROGRAMS
&P.RPGN  EQU   X'02'                   SHOP DOES NOT RUN RPG
**
&P.SIDD  DS    0CL256                  BEGINNING OF DDNAMES
         MEXIT
.CSCT    ANOP
SIO5GBLT CSECT                         TABLE START
         SPACE 2
         SIO5MID
         SPACE 2
         DC    A(0)                    TABLE SIZE
         SPACE 2
         DC    A(0,0)                  ADR/LEN OF SIO5SVC
         DC    A(0,0)                  ADR/LEN OF SIO5REL
         DC    A(0,0)                  ADR/LEN OF SIO5JIS
         DC    A(0,0)                  ADR/LEN OF SIO5DEX
         DC    A(0,0)                  ADR/LEN OF SIO5TBXX
         DC    A(0,0)                  ADR/LEN OF SIO5DVXX
         DC    A(0,0)                  ADR/LEN OF SIO5RLXX
         DC    A(0,0)                  ADR/LEN OF USER EXIT
         DC    A(0)                    SVC 19 ADDRESS
         DC    A(0)                    SVC 64 ADDRESS
**
         DC    A(0,0)                  ADR/LEN OF SIO5VSM
**
         DC    AL1(&SMFREC)            SMF RECORD ID
         DC    AL3(0)
**
         DC    A(0)                    SYSPRINT DCB LOC
**
         DC    F'0'                    OPTIMIZED COUNT
**
         DC    CL8'&EXITNM'            USER EXIT NAME
**
         DC    CL2'  '                 GLOBAL TABLE SUFFIX
         DC    CL2'&SYSUFX'            SYSTEM TABLE SUFFIX
         DC    CL2'&RLSUFX'            RELEASE TABLE SUFFIX
         DC    CL2'&DVSUFX'            DEVICE TABLE SUFFIX
**
         DC    XL1'0'                  PROCESSING FLAGS
**
&VALUE   SETA  1
         AIF   ('&MODE' EQ 'SELECT').MODEOK
&VALUE   SETA  2
         AIF   ('&MODE' EQ 'EXEMPT').MODEOK
&VALUE   SETA  3
         AIF   ('&MODE' EQ 'MIXMOD').MODEOK
         MNOTE 16,'MODE=&MODE IS INVALID'
         MEXIT
.MODEOK  ANOP
         DC    AL1(&VALUE)             MODE OF OPERATION
**
&VALUE   SETA  1
         AIF   ('&BLKMSG' EQ 'WTO').OPTMSOK
&VALUE   SETA  2
         AIF   ('&BLKMSG' EQ 'WTL').OPTMSOK
&VALUE   SETA  3
         AIF   ('&BLKMSG' EQ 'NO').OPTMSOK
         MNOTE 16,'BLKMSG=&BLKMSG IS INVALID'
         MEXIT
.OPTMSOK ANOP
         DC    AL1(&VALUE)             OPTIMIZATION MESSAGE
**
&VALUE   SETA  1
         AIF   ('&BUFMSG' EQ 'WTO').BUFMSOK
&VALUE   SETA  2
         AIF   ('&BUFMSG' EQ 'WTL').BUFMSOK
&VALUE   SETA  3
         AIF   ('&BUFMSG' EQ 'NO').BUFMSOK
         MNOTE 16,'BUFMSG=&BUFMSG IS INVALID'
         MEXIT
.BUFMSOK ANOP
         DC    AL1(&VALUE)             BUFFER SET MESSAGE
**
&VALUE   SETA  1
         AIF   ('&TSOMSG' EQ 'YES').TSOMSOK
&VALUE   SETA  2
         AIF   ('&TSOMSG' EQ 'NO').TSOMSOK
         MNOTE 16,'TSOMSG=&TSOMSG IS INVALID'
         MEXIT
.TSOMSOK ANOP
         DC    AL1(&VALUE)             TSO MESSAGE FLAG
**
&VALUE   SETA  1
         AIF   ('&SETBLK' EQ 'YES').ACTNOK
&VALUE   SETA  2
         AIF   ('&SETBLK' EQ 'NO').ACTNOK
         MNOTE 16,'SETBLK=&SETBLK IS INVALID'
         MEXIT
.ACTNOK  ANOP
         DC    AL1(&VALUE)             SETBLK (REBLOCK) FLAG
**
&VALUE   SETA  1
         AIF   ('&PGMDCB' EQ 'YES').HDCBSOK
&VALUE   SETA  2
         AIF   ('&PGMDCB' EQ 'NO').HDCBSOK
         MNOTE 16,'PGMDCB=&PGMDCB IS INVALID'
         MEXIT
.HDCBSOK ANOP
         DC    AL1(&VALUE)             OPTIMIZE HARD CODE DCB'S
**
&VALUE   SETA  1
         AIF   ('&SETBUF' EQ 'YES').BUFFOK
&VALUE   SETA  2
         AIF   ('&SETBUF' EQ 'NO').BUFFOK
         MNOTE 16,'SETBUF=&SETBUF IS INVALID'
         MEXIT
.BUFFOK  ANOP
         DC    AL1(&VALUE)             CHANGE # OF BUFFERS FLAG
**
&VALUE   SETA  1
         AIF   ('&RPG' EQ 'YES').RPGOK
&VALUE   SETA  2
         AIF   ('&RPG' EQ 'NO').RPGOK
         MNOTE 16,'RPG=&RPG IS INVALID'
         MEXIT
.RPGOK   ANOP
         DC    AL1(&VALUE)             RPG PROGRAMS FLAG
**
         AIF   ('&SORTIN' EQ '').NOSRTIN
&COUNT   SETA  N'&SORTIN
         AIF   ('&COUNT' EQ '0').NOSRTIN
         AIF   ('&COUNT' LT '33').OKSRTIN
         MNOTE 16,'TOO MANY SORTIN DD NAMES'
         MEXIT
.OKSRTIN ANOP
&INDEX   SETA  1
.LOOP    ANOP
&TEXT    SETC  '&SORTIN(&INDEX)'
         DC    CL8'&TEXT'
         AIF   ('&INDEX' EQ '&COUNT').NOSRTIN
&INDEX   SETA  &INDEX+1
         AGO   .LOOP
.NOSRTIN ANOP
&COUNT   SETA  32-N'&SORTIN
         DC    &COUNT.CL8' '
         MEND
./ ADD NAME=SIO5GB01                               17OCT89 12.47.53
         SIO5GBL MODE=MIXMOD,          MODE OF OPERATION               X
               SYSUFX=01,              SYSTEM TABLE SUFFIX             X
               RLSUFX=01,              RELEASE TABLE SUFFIX            X
               DVSUFX=01,              DEVICE TABLE SUFFIX             X
               BLKMSG=WTO,             OPTIMIZATION MSG (WTO/WTL/NO)   X
               BUFMSG=WTO,             BUFF CHANGE MSG(WTO/WTL/NO)     X
               TSOMSG=NO,              MESSAGES TO TSO USERS? (YES/NO) X
               SETBLK=YES,             REBLOCK DATASETS? (YES/NO)      X
               PGMDCB=NO,              DO HARD CODED DCB'S? (YES/NO)   X
               SMFREC=0,               "0" NO SMF RECORDS BUILT        X
               RPG=NO,                 CODE YES IF USING RPG           X
               SORTIN=(SORTIN),        SORT IN DD NAME LIST            X
               EXITNM=SIO5EXT,         USER EXIT NAME                  X
               SETBUF=YES              CHANGE THE BUFFERS (YES/NO)
         END
./ ADD NAME=SIO5MID                                17OCT89 12.47.53
         MACRO
         SIO5MID &TYPE=TEXT,&P=MID
         AIF   ('&TYPE' EQ 'TEXT').NODSCT
         AIF   ('&TYPE' EQ 'NOLB').NOLBL
SIO5MIDD DSECT
.NOLBL   ANOP
**
**       MODULE IDENTIFICATION AREA
**
&P.NAME  DC    CL8' '
         DC    CL2' '
&P.RELN  DC    CL8' '
         DC    CL2' '
&P.DATE  DC    CL8' '
         DC    CL2' '
&P.TIME  DC    CL8' '
         DC    CL2' '
         AIF   ('&TYPE' EQ 'NOLB').DONE
&P.LENG  EQU   *-&P.NAME
.DONE    ANOP
         MEXIT
.NODSCT  ANOP
**
**       MODULE IDENTIFICATION AREA
**
         DC    CL8'&SYSECT'
         DC    CL2' '
         DC    CL8'VER 5.0'
         DC    CL2' '
         DC    CL8'&SYSDATE'
         DC    CL2' '
         DC    CL8'&SYSTIME'
         DC    CL2' '
         MEND
./ ADD NAME=SIO5RLBF                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**                BYPASS ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIO5TBR MODE=BYPASS,PPREFIX=GIM
***********************************************************************
**                                                                   **
**                SELECT ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIO5TBR MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87            TEMP DATA SETS
         END
./ ADD NAME=SIO5RL01                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**                BYPASS ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIO5TBR MODE=BYPASS,PROGRAM=GIMSMP
***********************************************************************
**                                                                   **
**                SELECT ENTRIES FOR SPACE RELEASE                   **
**                                                                   **
***********************************************************************
         SIO5TBR MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS87            TEMP DATA SETS
         END
./ ADD NAME=SIO5SMFD                               17OCT89 12.47.53
JISSMFD  DSECT
**
**       S.I.O. SMF RECORD LAYOYT
**
JISLLBB  DC    F'0'                    LL-BB
JISSYSI  DC    XL1'00'                 SYSTEM TYPE
JISSMFI  DC    XL1'00'                 RECORD NUMBER
JISTIME  DC    XL4'00'                 SMF RECORD TIME (BIN)
JISDATE  DC    XL4'00'                 SMF RECORD DATE (0YYDDDF)
JISSSID  DC    CL4' '                  SUB SYSTEM ID
JISJOBN  DC    CL8' '                  JOB NAME
JISSTEP  DC    CL8' '                  STEP NAME
JISDDNM  DC    CL8' '                  DDNAME
JISPGMN  DC    CL8' '                  PROGRAM NAME
JISVOLS  DC    CL6' '                  VOLUME SERIAL NUMBER
JISBLKF  DC    CL3'PGM'                WERE BLOCK SIZE FOUND
JISOBLK  DC    CL5' '                  OLD BLOCK SIZE
JISNBLK  DC    CL5' '                  NEW BLOCK SIZE
JISATYP  DC    CL6' '                  ACCESS TYPE
JISDSNM  DC    CL44' '                 DATA SET NAME
JISSMFL  EQU   *-JISSMFD               RECORD LENGTH
./ ADD NAME=SIO5TBBF                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**     THESE PROGRAMS WILL BE BYPASSED DURING SIO PROCESSING         **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=BYPASS,PPREFIX=GIM
         SIO5TBL MODE=BYPASS,PPREFIX=DFH
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (QSAM REBUFFERING ONLY)            **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=A,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=B,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=C,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=D,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=E,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=F,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=G,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=H,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=I,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=J,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=K,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=L,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=M,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=N,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=O,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=P,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Q,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=R,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=S,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=T,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=U,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=V,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=W,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=X,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Y,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Z,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               SETBLK=NO,              NO REBLOCKING ALLOWED           X
               BUFNUM=(SPACE,131072),  ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (VSAM REBUFFERING ONLY)            **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=A,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=B,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=C,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=D,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=E,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=F,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=G,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=H,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=I,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=J,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=K,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=L,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=M,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=N,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=O,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=P,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Q,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=R,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=S,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=T,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=U,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=V,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=W,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=X,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Y,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=Z,               DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ,DIR),        VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,131072),  BUFSP=131072                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         END
./ ADD NAME=SIO5TBL                                17OCT89 12.47.53
         MACRO
         SIO5TBL &TYPE=TABLE,                                          X
               &P=SYT,                                                 X
               &MACRF=(SEQ),                                           X
               &AMP=,                                                  X
               &OFFSET=0,                                              X
               &MODE=,                                                 X
               &DSNAME=,                                               X
               &BLKSIZE=0,                                             X
               &BUFNUM=(NUMBER,0),                                     X
               &SETBUF=NO,                                             X
               &PREFIX=,                                               X
               &DDNAME=,                                               X
               &DPREFIX=,                                              X
               &JOB=,                                                  X
               &JPREFIX=,                                              X
               &VOLSER=,                                               X
               &VPREFIX=,                                              X
               &STEP=,                                                 X
               &SPREFIX=,                                              X
               &DEVADDR=,                                              X
               &PROGRAM=,                                              X
               &PPREFIX=,                                              X
               &ACCMETH=QSAM,                                          X
               &SETBLK=YES
         GBLB  &FRSTX
         GBLA  &COUNT
         LCLA  &BXTYP
         LCLA  &VSTYP,&VSCNT,&INDEX
         LCLA  &BNUMB
         LCLA  &FTYPE
         LCLA  &BLKVL
         LCLA  &MODEG
         LCLA  &AOFST
         LCLC  &FIELD
         LCLC  &BTYPE
         LCLC  &ATEXT
         AIF   ('&TYPE' EQ 'DSECT').GENTBL
         AIF   ('&MODE' NE '').MODEF
         MNOTE 16,'MODE PARAMETER IS MISSING'
         MEXIT
.MODEF   ANOP
&MODEG   SETA  128                     ASSUME SELECT
         AIF   ('&MODE' EQ 'SELECT').MODEOK
&MODEG   SETA  64                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'EXEMPT').MODEOK
&MODEG   SETA  32                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'BYPASS').MODEOK
         MNOTE 16,'MODE=&MODE IS INVALID'
         MEXIT
.MODEOK  ANOP
         AIF   ('&ACCMETH' EQ 'QSAM').ISQSAM
         AIF   ('&ACCMETH' EQ 'BSAM').ISBSAM
         AIF   ('&ACCMETH' EQ 'VSAM').ISVSAM
         MNOTE 16,'ACCMETH=&ACCMETH IS INVALID'
         MEXIT
.ISVSAM  ANOP
&MODEG   SETA  &MODEG+8                SET CORRECT MODE
         AGO   .XMODEOK
.ISBSAM  ANOP
&MODEG   SETA  &MODEG+2                SET CORRECT MODE
.ISQSAM  ANOP
.XMODEOK ANOP
         AIF   ('&SETBLK' EQ 'YES').ISACTN
&MODEG   SETA  &MODEG+1                SET CORRECT MODE
.ISACTN  ANOP
         AIF   ('&SETBUF' EQ 'YES').ISBUFA
&MODEG   SETA  &MODEG+4                SET CORRECT MODE
.ISBUFA  ANOP
&BLKVL   SETA  &BLKSIZE
         AIF   ('&BLKVL' LT '32761').BLKOK
         MNOTE 16,'BLOCK SIZE=&BLKSIZE IS TOO LARGE'
         MEXIT
.BLKOK   ANOP
&BTYPE   SETC  '&BUFNUM(1)'
         AIF   ('&ACCMETH' EQ 'VSAM').BTYPVS
         AIF   ('&BTYPE' EQ 'NUMBER').BTYPNB
         AIF   ('&BTYPE' EQ 'SPACE').BTYPSP
         AIF   ('&BTYPE' EQ 'TRACKS').BTYPTR
         MNOTE 16,'BUFFER TYPE=&BTYPE IS INVALID'
         MEXIT
.BTYPNB  ANOP
&BXTYP   SETA  1
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' LT '31').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPSP  ANOP
&BXTYP   SETA  2
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' GT '7').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPTR  ANOP
&BXTYP   SETA  3
&BNUMB   SETA  &BUFNUM(2)
         AIF   ('&BNUMB' LT '256').BTYPOK
         MNOTE 16,'BUFFER UNITS=&BNUMB IS INVALID'
         MEXIT
.BTYPVS  ANOP
         AIF   ('&BTYPE' EQ 'SPACE').VSPACE
&BXTYP   SETA  &BUFNUM(1)
.VSPACE  ANOP
&BNUMB   SETA  &BUFNUM(2)
&VSCNT   SETA  N'&MACRF
         AIF   ('&VSCNT' EQ '0').VSAMEE
         AIF   ('&VSCNT' LT '3').OKVSIN
.VSAMEE  ANOP
         MNOTE 16,'INVALID OR MISSING VSAM MACRF'
         MEXIT
.OKVSIN  ANOP
&INDEX   SETA  1
.LOOP    ANOP
         AIF   ('&MACRF(&INDEX)' EQ 'SEQ').ISSEQX
         AIF   ('&MACRF(&INDEX)' NE 'DIR').VSAMEE
&VSTYP   SETA  &VSTYP+64
         AGO   .CKENDVS
.ISSEQX  ANOP
&VSTYP   SETA  &VSTYP+128
.CKENDVS ANOP
         AIF   ('&INDEX' EQ '&VSCNT').CKAMPP
&INDEX   SETA  &INDEX+1
         AGO   .LOOP
.CKAMPP  ANOP
         AIF   ('&AMP' EQ '').BTYPOK
         AIF   ('&AMP' EQ 'OVERRIDE').AMPOVER
         MNOTE 16,'AMP=&AMP IS INVALID'
         MEXIT
.AMPOVER ANOP
&VSTYP   SETA  &VSTYP+32
.BTYPOK  ANOP
.GENTBL  ANOP
         AIF   (&FRSTX).ONETIME
***********************************************************************
**                                                                   **
**                         VERSION 5.0                               **
**                                                                   **
**          SEQUENTIAL I/O OPTIMIZER TABLE DEFINITION                **
**                                                                   **
**          (C) COPYRIGHT 1987 SYSTEM CONNECTIONS, INC.              **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').XDSCT
&P.5TBLD DSECT                         TABLE START
         SPACE 2
         SIO5MID TYPE=NOLB,P=&P
         SPACE 2
&P.SNE   DC    AL2(0)                  NUMBER OF DEFINITIONS
         SPACE 2
**
**       TABLE DEFINITION
**
&P.SEL   DC    AL2(0)                  DEFINITION LENGTH
&P.BLK   DC    AL2(0)                  BLOCK SIZE
**
         ORG   &P.BLK
         DC    XL1'00'                 OPTION BYTE ONE
&P.VOP   DC    XL1'00'                 VSAM OPTIONS
&P.VSQ   EQU   X'80'                   SEQUENTIAL PROCESSING
&P.VDI   EQU   X'40'                   DIRECT PROCESSING
&P.VAO   EQU   X'20'                   AMP OVERRIDE ON
**
&P.UTY   DC    XL1'0'                  BUFFER UNIT TYPE
*                           OR         VALUE FOR "BUFNI"
&P.NBF   EQU   X'01'                   NUMBER OF BUFFERS DEF
&P.SPC   EQU   X'02'                   BUFFER SPACE DEFINED
&P.TRK   EQU   X'03'                   TRACK SIZE DEF BUF #
&P.BUF   DC    AL3(0)                  BUFFER UNIT COUNT
*                           OR         VALUE FOR "BUFND"
*                           OR         VALUE FOR "BUFSP"
**
&P.SF1   DC    XL1'0'                  FLAG BYTE 1
&P.SSL   EQU   X'80'                   SELECT ENTRY
&P.SEX   EQU   X'40'                   EXEMPT ENTRY
&P.SIG   EQU   X'20'                   IGNORE ENTRY
&P.SVS   EQU   X'08'                   VSAM ENTRY
&P.SNF   EQU   X'04'                   DO NOT CHANGE BUFFERS
&P.SBA   EQU   X'02'                   BSAM ENTRY
&P.SAC   EQU   X'01'                   DO NOT CHANGE BLOCKSIZE
**
&P.STL   DC    AL1(0)                  DATA LENGTH
&P.STO   DC    AL1(0)                  DATA OFFSET
&P.SF2   DC    XL1'0'                  FLAG BYTE 2
&P.SNM   EQU   X'01'                   DSNAME FIELD
&P.SNP   EQU   X'02'                   DSNAME PREFIX
&P.SDM   EQU   X'03'                   DDNAME FIELD
&P.SDP   EQU   X'04'                   DDNAME PREFIX
&P.SJM   EQU   X'05'                   JOB NAME FIELD
&P.SJP   EQU   X'06'                   JOB NAME PREFIX
&P.SSM   EQU   X'07'                   STEP NAME FIELD
&P.SSP   EQU   X'08'                   STEP NAME PREFIX
&P.SVM   EQU   X'09'                   VOLUME SERIAL
&P.SVP   EQU   X'0A'                   VOLUME SERIAL PREFIX
&P.SPM   EQU   X'0B'                   PROGRAM NAME
&P.SPP   EQU   X'0C'                   PROGRAM NAME PREFIX
&P.SDV   EQU   X'0D'                   DEVICE DEVADDR
&P.SDT   EQU   *                       START OF DATA
         MEXIT
.XDSCT   ANOP
&FRSTX   SETB  1
SIO5TBLM CSECT                         TABLE START
         SPACE 2
         SIO5MID
         SPACE 2
&P.SND   DC    AL2(0)                  NUMBER OF DEFINITIONS
         SPACE 2
.ONETIME ANOP
**
**       TABLE DEFINITION
**
         DC    AL2(N&SYSNDX-*)
         AIF   ('&ACCMETH' EQ 'VSAM').XVSBLK
         DC    AL2(&BLKSIZE)
         AGO   .ZBLKOK
.XVSBLK  ANOP
         DC    AL1(0)
         DC    AL1(&VSTYP)
.ZBLKOK  ANOP
         DC    AL1(&BXTYP)
         DC    AL3(&BNUMB)
         DC    AL1(&MODEG)
         AIF   ('&DSNAME' EQ '').NODSN
&FIELD   SETC  '&DSNAME'
&FTYPE   SETA  1
         DC    AL1(L'A&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
A&SYSNDX DC    CL44'&FIELD'
.NODSN   ANOP
         AIF   ('&PREFIX' EQ '').NOPFX
&FIELD   SETC  '&PREFIX'
&FTYPE   SETA  2
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD1
&FIELD   SETC  '&PREFIX(1)'
&AOFST   SETA  &PREFIX(2)
.SFIELD1 ANOP
         DC    AL1(L'B&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
B&SYSNDX DC    C'&FIELD'
.NOPFX   ANOP
         AIF   ('&DDNAME' EQ '').NODD
&FIELD   SETC  '&DDNAME'
&FTYPE   SETA  3
         DC    AL1(L'C&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
C&SYSNDX DC    CL8'&FIELD'
.NODD    ANOP
         AIF   ('&DPREFIX' EQ '').NODDPX
&FIELD   SETC  '&DPREFIX'
&FTYPE   SETA  4
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD2
&FIELD   SETC  '&DPREFIX(1)'
&AOFST   SETA  &DPREFIX(2)
.SFIELD2 ANOP
         DC    AL1(L'D&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
D&SYSNDX DC    C'&FIELD'
.NODDPX  ANOP
         AIF   ('&JOB' EQ '').NOJOB
&FIELD   SETC  '&JOB'
&FTYPE   SETA  5
         DC    AL1(L'E&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
E&SYSNDX DC    CL8'&FIELD'
.NOJOB   ANOP
         AIF   ('&JPREFIX' EQ '').NOJPX
&FIELD   SETC  '&JPREFIX'
&FTYPE   SETA  6
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD3
&FIELD   SETC  '&JPREFIX(1)'
&AOFST   SETA  &JPREFIX(2)
.SFIELD3 ANOP
         DC    AL1(L'F&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
F&SYSNDX DC    C'&FIELD'
.NOJPX   ANOP
         AIF   ('&STEP' EQ '').NOSTEP
&FIELD   SETC  '&STEP'
&FTYPE   SETA  7
         DC    AL1(L'G&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
G&SYSNDX DC    CL8'&FIELD'
.NOSTEP  ANOP
         AIF   ('&SPREFIX' EQ '').NOSTEPX
&FIELD   SETC  '&SPREFIX'
&FTYPE   SETA  8
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD4
&FIELD   SETC  '&SPREFIX(1)'
&AOFST   SETA  &SPREFIX(2)
.SFIELD4 ANOP
         DC    AL1(L'H&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
H&SYSNDX DC    C'&FIELD'
.NOSTEPX ANOP
         AIF   ('&VOLSER' EQ '').NOVSN
&FIELD   SETC  '&VOLSER'
&FTYPE   SETA  9
         DC    AL1(L'I&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
I&SYSNDX DC    CL6'&FIELD'
.NOVSN   ANOP
         AIF   ('&VPREFIX' EQ '').NOVSNP
&FIELD   SETC  '&VPREFIX'
&FTYPE   SETA  10
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD5
&FIELD   SETC  '&VPREFIX(1)'
&AOFST   SETA  &VPREFIX(2)
.SFIELD5 ANOP
         DC    AL1(L'J&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
J&SYSNDX DC    C'&FIELD'
.NOVSNP  ANOP
         AIF   ('&PROGRAM' EQ '').NOPGM
&FIELD   SETC  '&PROGRAM'
&FTYPE   SETA  11
         DC    AL1(L'K&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
K&SYSNDX DC    CL8'&FIELD'
.NOPGM   ANOP
         AIF   ('&PPREFIX' EQ '').NOPGMX
&FIELD   SETC  '&PPREFIX'
&FTYPE   SETA  12
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD6
&FIELD   SETC  '&PPREFIX(1)'
&AOFST   SETA  &PPREFIX(2)
.SFIELD6 ANOP
         DC    AL1(L'L&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
L&SYSNDX DC    C'&FIELD'
.NOPGMX  ANOP
         AIF   ('&DEVADDR' EQ '').NOUNIT
&FIELD   SETC  '&DEVADDR'
&FTYPE   SETA  13
         DC    AL1(L'M&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
M&SYSNDX DC    CL3'&FIELD'
.NOUNIT  ANOP
N&SYSNDX EQU   *
         ORG   &P.SND
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIO5TBR                                17OCT89 12.47.53
         MACRO
         SIO5TBR &TYPE=TABLE,                                          X
               &P=REL,                                                 X
               &OFFSET=0,                                              X
               &MODE=,                                                 X
               &DSNAME=,                                               X
               &PREFIX=,                                               X
               &DDNAME=,                                               X
               &DPREFIX=,                                              X
               &JOB=,                                                  X
               &JPREFIX=,                                              X
               &VOLSER=,                                               X
               &VPREFIX=,                                              X
               &STEP=,                                                 X
               &SPREFIX=,                                              X
               &DEVADDR=,                                              X
               &PROGRAM=,                                              X
               &PPREFIX=
         GBLB  &FRSTX
         GBLA  &COUNT
         LCLA  &FTYPE
         LCLA  &MODEG
         LCLA  &AOFST
         LCLC  &FIELD
         LCLC  &ATEXT
         AIF   ('&TYPE' EQ 'DSECT').GENTBL
         AIF   ('&MODE' NE '').MODEF
         MNOTE 16,'MODE PARAMETER IS MISSING'
         MEXIT
.MODEF   ANOP
&MODEG   SETA  128                     ASSUME SELECT
         AIF   ('&MODE' EQ 'SELECT').MODEOK
&MODEG   SETA  64                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'EXEMPT').MODEOK
&MODEG   SETA  32                      ASSUME EXEMPT
         AIF   ('&MODE' EQ 'BYPASS').MODEOK
         MNOTE 16,'MODE=&MODE IS INVALID'
         MEXIT
.MODEOK  ANOP
.GENTBL  ANOP
         AIF   (&FRSTX).DOTBL
***********************************************************************
**                                                                   **
**                         VERSION 5.0                               **
**                                                                   **
**          SEQUENTIAL I/O OPTIMIZER TABLE DEFINITION                **
**                                                                   **
**          (C) COPYRIGHT 1987 SYSTEM CONNECTIONS, INC.              **
**                                                                   **
**                                                                   **
***********************************************************************
         SPACE 2
         AIF   ('&TYPE' NE 'DSECT').DSCTX
&P.5RELD DSECT                         TABLE START
         SPACE 2
         SIO5MID TYPE=NOLB,P=&P
         SPACE 2
&P.SNE   DC    AL2(0)                  NUMBER OF DEFINITIONS
         SPACE 2
**
**       TABLE DEFINITION
**
&P.SEL   DC    AL2(0)                  DEFINITION LENGTH
**
&P.SF1   DC    XL1'0'                  FLAG BYTE 1
&P.SSL   EQU   X'80'                   SELECT ENTRY
&P.SEX   EQU   X'40'                   EXEMPT ENTRY
&P.SIG   EQU   X'20'                   BYPASS ENTRY
**
&P.STL   DC    AL1(0)                  DATA LENGTH
&P.STO   DC    AL1(0)                  DATA OFFSET
&P.SF2   DC    XL1'0'                  FLAG BYTE 2
&P.SNM   EQU   X'01'                   DSNAME FIELD
&P.SNP   EQU   X'02'                   DSNAME PREFIX
&P.SDM   EQU   X'03'                   DDNAME FIELD
&P.SDP   EQU   X'04'                   DDNAME PREFIX
&P.SJM   EQU   X'05'                   JOB NAME FIELD
&P.SJP   EQU   X'06'                   JOB NAME PREFIX
&P.SSM   EQU   X'07'                   STEP NAME FIELD
&P.SSP   EQU   X'08'                   STEP NAME PREFIX
&P.SVM   EQU   X'09'                   VOLUME SERIAL
&P.SVP   EQU   X'0A'                   VOLUME SERIAL PREFIX
&P.SPM   EQU   X'0B'                   PROGRAM NAME
&P.SPP   EQU   X'0C'                   PROGRAM NAME PREFIX
&P.SDV   EQU   X'0D'                   DEVICE DEVADDR
&P.SDT   EQU   *                       START OF DATA
         MEXIT
.DSCTX   ANOP
&FRSTX   SETB  1
SIO5RLTM CSECT                         TABLE START
         SPACE 2
         SIO5MID
         SPACE 2
&P.SND   DC    AL2(0)                  NUMBER OF DEFINITIONS
.DOTBL   ANOP
         SPACE 2
**
**       TABLE DEFINITION
**
         DC    AL2(N&SYSNDX-*)
         DC    AL1(&MODEG)
         AIF   ('&DSNAME' EQ '').NODSN
&FIELD   SETC  '&DSNAME'
&FTYPE   SETA  1
         DC    AL1(L'A&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
A&SYSNDX DC    CL44'&FIELD'
.NODSN   ANOP
         AIF   ('&PREFIX' EQ '').NOPFX
&FIELD   SETC  '&PREFIX'
&FTYPE   SETA  2
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD1
&FIELD   SETC  '&PREFIX(1)'
&AOFST   SETA  &PREFIX(2)
.SFIELD1 ANOP
         DC    AL1(L'B&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
B&SYSNDX DC    C'&FIELD'
.NOPFX   ANOP
         AIF   ('&DDNAME' EQ '').NODD
&FIELD   SETC  '&DDNAME'
&FTYPE   SETA  3
         DC    AL1(L'C&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
C&SYSNDX DC    CL8'&FIELD'
.NODD    ANOP
         AIF   ('&DPREFIX' EQ '').NODDPX
&FIELD   SETC  '&DPREFIX'
&FTYPE   SETA  4
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD2
&FIELD   SETC  '&DPREFIX(1)'
&AOFST   SETA  &DPREFIX(2)
.SFIELD2 ANOP
         DC    AL1(L'D&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
D&SYSNDX DC    C'&FIELD'
.NODDPX  ANOP
         AIF   ('&JOB' EQ '').NOJOB
&FIELD   SETC  '&JOB'
&FTYPE   SETA  5
         DC    AL1(L'E&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
E&SYSNDX DC    CL8'&FIELD'
.NOJOB   ANOP
         AIF   ('&JPREFIX' EQ '').NOJPX
&FIELD   SETC  '&JPREFIX'
&FTYPE   SETA  6
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD3
&FIELD   SETC  '&JPREFIX(1)'
&AOFST   SETA  &JPREFIX(2)
.SFIELD3 ANOP
         DC    AL1(L'F&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
F&SYSNDX DC    C'&FIELD'
.NOJPX   ANOP
         AIF   ('&STEP' EQ '').NOSTEP
&FIELD   SETC  '&STEP'
&FTYPE   SETA  7
         DC    AL1(L'G&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
G&SYSNDX DC    CL8'&FIELD'
.NOSTEP  ANOP
         AIF   ('&SPREFIX' EQ '').NOSTEPX
&FIELD   SETC  '&SPREFIX'
&FTYPE   SETA  8
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD4
&FIELD   SETC  '&SPREFIX(1)'
&AOFST   SETA  &SPREFIX(2)
.SFIELD4 ANOP
         DC    AL1(L'H&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
H&SYSNDX DC    C'&FIELD'
.NOSTEPX ANOP
         AIF   ('&VOLSER' EQ '').NOVSN
&FIELD   SETC  '&VOLSER'
&FTYPE   SETA  9
         DC    AL1(L'I&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
I&SYSNDX DC    CL6'&FIELD'
.NOVSN   ANOP
         AIF   ('&VPREFIX' EQ '').NOVSNP
&FIELD   SETC  '&VPREFIX'
&FTYPE   SETA  10
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD5
&FIELD   SETC  '&VPREFIX(1)'
&AOFST   SETA  &VPREFIX(2)
.SFIELD5 ANOP
         DC    AL1(L'J&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
J&SYSNDX DC    C'&FIELD'
.NOVSNP  ANOP
         AIF   ('&PROGRAM' EQ '').NOPGM
&FIELD   SETC  '&PROGRAM'
&FTYPE   SETA  11
         DC    AL1(L'K&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
K&SYSNDX DC    CL8'&FIELD'
.NOPGM   ANOP
         AIF   ('&PPREFIX' EQ '').NOPGMX
&FIELD   SETC  '&PPREFIX'
&FTYPE   SETA  12
&AOFST   SETA  &OFFSET
         AIF   ('&FIELD'(1,1) NE '(').SFIELD6
&FIELD   SETC  '&PPREFIX(1)'
&AOFST   SETA  &PPREFIX(2)
.SFIELD6 ANOP
         DC    AL1(L'L&SYSNDX-1)
         DC    AL1(&AOFST)
         DC    AL1(&FTYPE)
L&SYSNDX DC    C'&FIELD'
.NOPGMX  ANOP
         AIF   ('&DEVADDR' EQ '').NOUNIT
&FIELD   SETC  '&DEVADDR'
&FTYPE   SETA  13
         DC    AL1(L'M&SYSNDX-1)
         DC    AL1(0)
         DC    AL1(&FTYPE)
M&SYSNDX DC    CL3'&FIELD'
.NOUNIT  ANOP
N&SYSNDX EQU   *
         ORG   &P.SND
&COUNT   SETA  &COUNT+1
         DC    AL2(&COUNT)
         ORG   ,
         SPACE 2
         MEND
./ ADD NAME=SIO5TB01                               17OCT89 12.47.53
***********************************************************************
**                                                                   **
**     THESE PROGRAMS WILL BE BYPASSED DURING SIO PROCESSING         **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=BYPASS,PROGRAM=IEWL
         SIO5TBL MODE=BYPASS,PROGRAM=HEWL
         SIO5TBL MODE=BYPASS,PROGRAM=HMASMP
         SIO5TBL MODE=BYPASS,PPREFIX=GIM
         SIO5TBL MODE=BYPASS,PPREFIX=DFH
         SIO5TBL MODE=BYPASS,PROGRAM=IEHINITT
         SIO5TBL MODE=BYPASS,PROGRAM=TMSTPNIT
         SIO5TBL MODE=BYPASS,PROGRAM=ICNRTNDF
***********************************************************************
**                                                                   **
**                     EXEMPT ENTRIES                                **
**                                                                   **
***********************************************************************
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       BECAUSE REBLOCKING WILL CREATE PROBLEMS
**       FOR THE ASSEMBLER, LINKAGE-EDITOR AND SMP.
**
         SIO5TBL MODE=EXEMPT,DDNAME=SYSPUNCH
         SIO5TBL MODE=EXEMPT,DDNAME=SYSGO
         SIO5TBL MODE=EXEMPT,DDNAME=SYSLIN
         SIO5TBL MODE=EXEMPT,DDNAME=DVDCODE
**
**       THE FOLLOWING EXEMPT ENTRIES MUST BE USED
**       FOR DISOSS
**
         SIO5TBL MODE=EXEMPT,DDNAME=DSVWRKSS
         SIO5TBL MODE=EXEMPT,DDNAME=DSVWRK
         SIO5TBL MODE=EXEMPT,DDNAME=DSVWRKJB
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (QSAM)                             **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=SYS88,           TEMP DATA SETS                  X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               BUFNUM=(NUMBER,5),      ALLOCATE BUFFERS                X
               ACCMETH=QSAM            INDICATE QSAM
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (BSAM)                             **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PROGRAM=IEBGENER,       IEBGENER USES BSAM              X
               DDNAME=SYSUT1,          INPUT FILE ONLY                 X
               ACCMETH=BSAM            INDICATE BSAM USED
***********************************************************************
**                                                                   **
**                SELECT ENTRIES  (VSAM)                             **
**                                                                   **
**       PLEASE NOTE THAT THE VSAM ENTRIES ARE USED FOR BUFFERING    **
**       PURPOSES ONLY.                                              **
**                                                                   **
***********************************************************************
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=XXXXX,           DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(DIR),            VSAM MACRF SELECTED             X
               BUFNUM=(6,3),           BUFNI=6, BUFND=3                X
               ACCMETH=VSAM            INDICATE VSAM USED
**
         SIO5TBL MODE=SELECT,          SELECT THESE FILES:             X
               PREFIX=XXXXX,           DATA SET NAME PREFIX            X
               SETBUF=YES,             CHANGE THE BUFFERS              X
               MACRF=(SEQ),            VSAM MACRF SELECTED             X
               AMP=OVERRIDE,           FORCE THE BUFFERING             X
               BUFNUM=(SPACE,128000),  BUFSP=128000                    X
               ACCMETH=VSAM            INDICATE VSAM USED
         END
./ ADD NAME=SUBDATA  0101-86049-89243-1518-00047-00047-00001-KK00155
         MACRO
&LAB1    SUBDATA &DSECT
         LCLC  &LAB2
*
*               OPERAND DATA PASSED TO IKJEFF10 VIA ITCOMA1:
*
         AIF   (T'&LAB1 NE 'O').LOK
&LAB2    SETC  'SUBDATA'
         AGO   .DSCK
.LOK     ANOP
&LAB2    SETC  '&LAB1'
.DSCK    AIF   ('&DSECT' EQ 'DSECT').DSL
         DS    0A
&LAB2    DS    0XL100
         AGO   .ADSL
.DSL     ANOP
&LAB2    DSECT
.ADSL    ANOP
NOTIFY   DS    AL2                NOTIFY/NONOTIFY FLAG
JES      DS    AL2                JES CMD FLAG
*
LONE     DS    2A                 PTR/FLG CLASS
LTWO     DS    2A                 PTR/FLG MSGCLASS
DESCR    DS    2A                 PTR/FLG PROG NAME FLD
AN       DS    2A                 PTR/FLG ACCOUNT NO.
BSA      DS    2A                 PTR/FLG BIN-SUB-AGY
SN       DS    2A                 PTR/FLG SYSTEM NO.
NUID     DS    2A                 PTR/FLG NOTIFY UID.
P        DS    2A                 PTR/FLG PRTY
*
CLFLD    DS    H
CLASS    DS    X
MCFLD    DS    H
MSGCLASS DS    X
PNFLD    DS    H
PROGNAME DS    XL20
ANFLD    DS    H
ACCOUNT  DS    XL20
BFLD     DS    H
BIN      DS    XL4
SFLD     DS    H
SYS      DS    XL4
NFLD     DS    H
NOTIUID  DS    XL8
PFLD     DS    H
PRTY     DS    XL8
         MEND
./ ADD NAME=SVLNK                                  17OCT89 12.47.53
         MACRO
&NAME    SVLNK &REG1,&REG2,&REG3,&IND,&LV=
         GBLC  &EQU
         GBLC  &GM
         GBLC  &CSECT
         LCLC  &REGA
         AIF   (T'&NAME EQ 'O').NOCSECT
         AIF   ('&NAME' EQ '*').NOCSECT
&CSECT   SETC  '&NAME'
&CSECT   CSECT 
         AGO   .RST
.NOCSECT ANOP
&CSECT   SETC  '*'
         CSECT 
.RST     ANOP
         AIF   ('&EQU' EQ 'ON').NRQ
&EQU     SETC  'ON'
*
***REGISTER EQUATES***
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.NRQ     ANOP
*
*******************  PROGRAM INITIALIZATION  *************************
*
         AIF   (T'&REG1 EQ 'O').NOREG
&REGA    SETC  '&REG1'
         AIF   (T'&REG2 EQ 'O').ONEREG
         USING &CSECT,&REGA,&REG2 
         AGO   .BYUSE
.NOREG   ANOP
&REGA    SETC  'R3'
.ONEREG  USING &CSECT,&REGA 
.BYUSE   ANOP
         STM   R14,R12,12(R13) :       STORE REGS IN HIGH SAVE AREA
         LR    &REGA,R15 
         AIF   (T'&REG2 EQ 'O').NOLA
         LA    &REG2,4095(&REGA) 
         LA    &REG2,1(&REG2) 
.NOLA    ANOP
         AIF   (T'&REG3 EQ 'O').NOLNK
         L     &REG3,0(R1) 
.NOLNK   ANOP
         AIF (T'&IND EQ 'O').GETIT
         CNOP  0,4
         BAL   R15,SAVEAREA+72 
SAVEAREA DS    18F 
         ST    R15,8(R13)
         ST    R13,SAVEAREA+4
         LR    R13,R15
         AGO   .NOSAVE
.GETIT   ANOP
         AIF   (T'&LV EQ 'O').GMD
&GM      SETC  '&LV'
         AGO   .DGM
.GMD     ANOP
&GM      SETC  '72'
.DGM     ANOP
*
***GET MAIN STORAGE FOR SAVE AREA***
*
         AIF   ('&GM' LT '4096').LA
         GETMAIN R,LV=&GM 
         AGO   .EGM
.LA      ANOP
         LA    R0,&GM                  GET &GM BYTES
         GETMAIN R,LV=(0)
.EGM     ANOP
*
***SET UP SAVE AREA POINTERS***
*
         ST    R1,8(R13) 
         ST    R13,4(R1) 
         LR    R13,R1 
         AIF   (T'&REG3  NE 'O').NOSAVE
         L     R1,4(R13) 
         L     R1,24(R1) 
.NOSAVE  ANOP
         AIF   ('&CSECT' EQ '*').EXIT
         B     *+12
         DC    CL8'&CSECT'  
.EXIT    ANOP
*
*********************  END INITIALIZATION  ***************************
*
         MEND
./ ADD NAME=TCOMTAB                                17OCT89 12.47.53
         MACRO
         TCOMTAB
TCOMTAB  DSECT
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS MACRO MAPS THE TEST COMMAND PROCESSOR COMMUNICATION     *
*        TABLE (TCOMTAB) USED BY ALL SUBCOMMAND PROCESSORS AND        *
*        SERVICE ROUTINES WHICH MAKE UP THE TSO/TEST COMMAND.         *
*                                                                     *
* STATUS -- VERSION NO. 01 - OS/VS2 RELEASE NO. 01                    *
*                                                                     *
* CHANGE LEVEL -- 04 - DATE 04/25/72                                  *
*                                                                     *
***********************************************************************
         SPACE 1
ECBPP    DS    A -            PP DISPATCHABILITY ECB.
         SPACE 1
ECBLIST  EQU   * -            BEGINNING OF ECBLIST FOR WAIT.
ECBTST   DS    A -            PTR TO TEST DISPATCHABILITY ECB.
ECBTERM  DS    A -            PTR TO PP TERMINATION ECB.
ECBTMPS  DS    A -            PTR TO STAI ECB.
ECBTMPA  DS    A -            PTR TO ATTENTION ECB.
         SPACE 1
ECBLOG   DS    A -            PTR TO STOP/MODIFY ECB.
         SPACE 1
TSTTCB   DS    0A -           PTR TO THE TEST TCB.
TSTTCBR  DS    X -            RESERVED.
TSTTCBP  DS    AL3 -          PTR TO THE TEST TCB.
         SPACE 1
PPTCB    DS    0A -           PTR TO THE PROBLEM PROGRAM TCB.
PPTCBR   DS    X -            RESERVED.
PPTCBP   DS    AL3 -          PTR TO THE PROBLEM PROGRAM TCB.
         DS    D -            *** RESERVED SPACE ***
         SPACE 1
OUTBUF   DS    A -            PTR TO GENERAL OUTPUT BUFFER.
OUTBUFRL EQU   256 -          LENGTH OF OUTPUT BUFFER.
BLDLAREA DS    0A -           ADDRESS OF BLDL ENTRY USED BY
*                             IKJEGINT AND IKJEGLDR.
CONAREA  DS    A -            PTR TO OUTPUT AREA USED BY CONVERT RTN.
CONAREAL EQU   72 -           LENGTH OF CONVERT WORK AREA.
WORKAREA DS    A -            PTR TO GENERAL WORK AREA.
REGSAVEL EQU   72*6 -         LENGTH OF 6 REGISTER SAVE AREAS.
REGSAVE1 DS    A -            PTR TO SAVE AREA FOR MAINLINE.
REGSAVE2 DS    A -            PTR TO SAVE AREA FOR COMMANDS.
REGSAVE3 DS    A -            PTR TO SAVE AREA FOR VALIDITY CHECKERS.
REGSAVE4 DS    A -            PTR TO SAVE AREA FOR IKJEGCVT.
REGSAVE5 DS    A -            PTR TO SAVE AREA FOR IKJEGIO.
REGSAVE6 DS    A -            PTR TO SAVE AREA FOR IKJEGSRH.
@TSTPARM DS    A -            *** RESERVED SPACE ***
         SPACE 1
TPLPTR   DS    A -            PTR TO TPL
TMPLL    DS    H -            LINE LENGTH
TSTMTASK DS    X -            BREAKPOINT SYNCHRONIZATION BYTE.
TSTESTRC DS    X -            ESATE ERROR RETURN CODE
TSTWHR   DS    A -            PTR TO COMMAND LIB DCB.
         SPACE 1
PARMLIST DS    0F -           PARM LIST FOR CALLING SERVICE ROUTINES
TSTUPT   DS    A -            PTR TO UPT
TSTECT   DS    A -            PTR TO ECT
TSTCPECB DS    A -            PTR TO CP ECB
         SPACE 1
TSTANSPL DS    A -            ANSWER PLACE FOR PARSE SERVICE ROUTINE.
         SPACE 1
ABENTAB  DS    A -            PTR TO ABEND VECTOR TABLE(RESIDENT).
ABENTAB1 DS    A -            PTR TO ABEND VECTOR TABLE(TRANSIENT).
TSTRETRY DS    0A -           ENTRY POINT OF TRANSIENT RETRY ROUTINE.
TSTRTYCD DS    X -            SUBCOMMAND ID.
TSTRTYPT DS    AL3 -          PTR TO A STAE RETRY ROUTINE WITHIN ANY
*                             TEST MODULE THAT HAS ISSUED A STAE
         SPACE 1
INBUF    DS    A -            PTR TO BUFFER CONTAINING SUBCMD
TSTIODSN DS    A -            HEAD OF DSNAME CHAIN FOR IKJEGIO 'PRINT'.
TSTIO    DS    A -            ENTRY POINT OF GET ROUTINE IKJEGIO.
         DS    A -            RESERVED
         DS    A -            RESERVED
TSTCONVT DS    A -            ENTRY POINT OF IKJEGCVT.
TSTADDR  DS    A -            ENTRY POINT OF ADDRESS BUILD SUBROUTINE.
TSTSTAE  DS    A -            ENTRY POINT OF STAE EXIT RTN. (IKJEGSTA).
         SPACE 2
TSTFLGS1 DS    X -            TEST FLAGS, BYTE 1.
         SPACE 1
PCHLSTVL EQU   B'10000000' -  PATCH LIST SWITCH.
S9GINTSW EQU   B'01000000' -  INTERNAL SW FOR SVC97          @ZA05687
TSTPRINT EQU   B'00100000' -  PRINT SWITCH.
TSTFIRST EQU   B'00010000' -  FIRST TIME SWITCH.
RANGESW  EQU   B'00001000' -  INDICATES PDE IS FOR ADDRESS RANGE.
TSTBUILD EQU   B'00000100' -  'AT' SWITCH FOR DEFER CHECK.
ENDSW    EQU   B'00000010' -  INDICATES 'END' TO MAINLINE.
RUNSW    EQU   B'00000001' -  INDICATES 'RUN' TO MAINLINE.
         SPACE 2
TSTFLGS2 DS    X -            TEST FLAGS, BYTE 2.
         SPACE 1
TSTLDF   EQU   B'10000000' -  IKJEGLDF TASK-SWITCH INDICATOR.
TSTXCTL  EQU   B'01000000' -  STAE XCTL INDICATOR.
TOFFDEF  EQU   B'00100000' -  NO ACTIVE BREAKPOINTS.
TTESTTCB EQU   B'00010000' -  RUNNING UNDER TEST TASK.
TADDROUT EQU   B'00001000' -  LOAD MODULE FOUND UNDER TCB.
TWHRLOAD EQU   B'00000100' -  VALID LOAD MODULE CHECK.
TSTQUAL  EQU   B'00000010' -  QUALIFICATION IS IN PROCESS
TMYIOMSG EQU   B'00000001' -  IKJEGIO MESSAGE SWITCH.
         SPACE 2
TSTFLGS3 DS    X -            TEST FLAGS, BYTE 3.
         SPACE 1
TSTGOSW  EQU   B'10000000' -  SPECIAL BREAKPOINT TYPE SWITCH.
TSTSTAI  EQU   B'01000000' -  PROBLEM PROGRAM ABEND INDICATOR.
SYMMESG  EQU   B'00100000' -  SYM 'NO DIAGNOSTIC' SWITCH.
TCSECTCK EQU   B'00010000' -  CSECT ONLY DEFER QUEUE CLEAR.
TDUPNAME EQU   B'00001000' -  DEFER QUEUE DUPLICATE NAME BIT.
TSTLINK  EQU   B'00000100' -  SUB-CMD 'LINK FAILED' INDICATOR.
TSTHELP  EQU   B'00000010' -  INDICATES THAT 'HELP' IS ATTACHED.
NOPARMS  EQU   B'00000001' -  INDICATES NO PARAMETERS WITH COMMAND.
         SPACE 2
TSTFLGS4 DS    X              TEST FLAGS, BYTE 4.
         SPACE 1
TSTA     EQU   B'10000000' -  TEST'S INPUT IS NOT FROM A STACK.
TSTB     EQU   B'01000000' -  A STACKED TERMINAL ELEM. IS PRESENT.
TSTFLUSH EQU   B'00100000' -  FORCE TCLEARQ AND POSSIBLE STACK FLUSH.
TSTRERTN EQU   B'00010000' -  A RETRY IS IN PROCESS
TSTESTAE EQU   B'00001000' -  ESTAE IS INVOKING I/O FOR MESSAGE
TSTSVCAB EQU   B'00000100' -  SVC ABEND IS IN PROCESS
TSTPERC  EQU   B'00000010' -  THIS RETRY ROUTINE WAS PERCOLLATED TO
TSTVALCK EQU   B'00000001' -  INDICATES PARSE VALIDITY CHECK IN PROCESS
         SPACE 2
BREAKTAB DS    A -            PTR TO FIRST BREAK ELEMENT.
DEFERTAB DS    A -            PTR TO DEFER'ED CMD LIST
PPLOAD   DS    A -            PTR TO CURRENT BASE FOR RELATIVES.
PPTEMP   DS    A -            TEMPORARY BASE FOR RELATIVES.
SUBCHAIN DS    A -            PTR TO BREAKPOINT SUBCOMMAND CHAIN.
         SPACE 1
TSTGO    DS    0X -           RESUME ADDRESS AFTER BREAKPOINT.
TSTGOPSW DS    F -            SECOND WORD OF RBOPSW FIELD.
TSTGOWCF DS    X -            WAIT COUNT FROM RBWCF FIELD.
TSTRSVD2 DS    X -            RESERVED BYTE.
TSTSVC   DS    H -            AN SVC 97 INSTRUCTION (0A61).
PPRB     DS    A -            CURRENT PROBLEM PROGRAM RB ADDRESS.
         SPACE 1
TSTIODCB DS    A -            PTR TO OPEN PRINT DCB.
CALLPARM DS    A -            HEAD OF CHAIN FOR PARMS BUILT BY 'CALL'.
         DS    A -            *** RESERVED SPACE ***
INTSTDDN DS    0CL8           DDNAME FOR DATA SET SPECIFIED ON THE TEST
*                             COMMAND - USED BY IKJEGINT AND IKJEGLDR.
TSTCURLD DS    CL8 -          CURRENTLY QUALIFIED LOAD NAME.
TERMDD   DS    0CL8 -         DDNAME FOR TERMINAL USED BY OS LOADER.
TSTCURCT DS    CL8 -          CURRENTLY QUALIFIED CSECT NAME.
TSTSYMBA DS    A -            CURRENTLY QUALIFIED SYMBOLIC ADDR BASE.
TSTTRN   DS    A -            HEAD OF SAVE INFORMATION CHAIN
SICHAIN  DS    A -            HEAD OF SYMBOL INFORMATION CHAIN
TSTSYMWK DS    A -            PTR TO SYMBOL PROCESSING WORK AREA.
SYMTABLE DS    A -            PTR TO IN-CORE SYMBOL TABLE.
         SPACE 1
PPEXIT   DS    0A -           BREAKPOINT & EXIT SVC'S FOR PP TERM.
PPEXIT1  DS    H -            AN SVC 97 INSTRUCTION (0A61).
PPEXIT2  DS    H -            AN SVC 3 INSTRUCTION (0A03).
TSTDCB   DS    A -            HEAD OF OVLY DCB CHAIN.
OPCODTAB DS    A -            PTR TO TABLE OF VALID OPERATION CODES.
TSTOPCD2 DS    F -            PTR TO TABLE FOR TWO BYTE S/370
*                             OPERATION CODES.
         DS    A -            RESERVED
         DS    A -            RESERVED
TSTHTCB  DS    A -            POINTER TO THE HELP TCB
TSTAQUAL DS    0X -           AUTOMATIC QUALIFICATION CONTROL
TSTAQLDM DS    CL8 -          EBCDIC LOAD MODULE NAME.
TSTAQEP  DS    A -            ENTRY POINT OF LOAD MODULE.
TSTRSTRT DS    A -            RESTART ADDRESS FOR STAE PROCESSING
TSTSRHRT DS    A -            ADDRESS OF RESIDENT ADDRESS VALIDITY
*                             CHECK ROUTINE.
         SPACE 1
TSTSTAX  DS    5F -           STAX PARAMETER LIST.
TSTDSECB DS    F -            TEST DISPATCHABILITY ECB.
TSTMNLWK DS    10F -          WORK AREA FOR EXCULSIVE USE OF MNL
TSTSTAEL DS    4F -           STAE PARAMETER LIST FOR MNL ONLY.
TSTOIPRM DS    0CL84          IO PARAMETER BLOCK
TSTIOPRM DS    CL84           IO PARAMETER BLOCK USE BY SIO
TSTSVCM1 DS    F              SVC FIRST LEVEL MESSAGE NO.
TSTSVCM2 DS    F              SVC SECOND LEVEL MESSAGE NO.
TSTSVCNQ DS    CL6'ECBTST'    CHARACTER CONSTANT FOR SVC ENQUE
R11SAVE  DS    F              SAVE AREA FOR R11 FOR SVC97    @ZA00889
         DS    8D             *** RESERVED SPACE ***
TCOMLEN  EQU   ((*-TCOMTAB+7)/8)*8 - TCOMTAB LENGTH.
         MEND
./ ADD NAME=TERME                                  17OCT89 12.47.53
         MACRO
&NAME    TERME &PASS,&RC=O
         GBLC  &LAB
         GBLB  &PRIM
&NAME    DS    0H
         AIF   (&PRIM).TSTRC
         MNOTE 8,'PRIME MUST OCCUR BEFORE TERME'
         MEXIT
.TSTRC   AIF   ('&RC' EQ '(15)').RET
         AIF   ('&RC' EQ 'O').RET
         AIF   ('&RC'(1,1) EQ '(').ISAREG
         LA    15,&RC
         AGO   .RET
.ISAREG  ANOP
         LR    15,&RC
.RET     AIF   ('&PASS' NE 'PASS').NOPASS
         L     14,4(13)
         STM   0,1,20(14)
.NOPASS  ANOP
         B     &LAB.X
         MEND
./ ADD NAME=TLINE                                  17OCT89 12.47.53
         MACRO
         TLINE  &LIT
         B   X&SYSNDX
LIT&SYSNDX DC   C&LIT
X&SYSNDX DS    0H
         TPUT  LIT&SYSNDX,L'LIT&SYSNDX
         MEND
./ ADD NAME=TPT      0100-82189-82189-1212-00011-00011-00000-$A1238
         MACRO
&NAME    TPT   &A,&B
         LCLC  &X
&X       SETC  'L'''
&NAME    LA    1,&A
         AIF   ('&B' NE '').GENB
         LA    0,&X&A
         AGO   .TPT
.GENB    LA    0,&B
.TPT     TPUT  (1),(0),R
         MEND
./ ADD NAME=TRANTAB  0102-87166-87204-1452-00026-00018-00000-KK00155
         MACRO
&NAME    TRANTAB
.*
.*       TRANTAB (FV,TV),(FV2,TV2)....
.*     FV = FROM VALUE
.*     TV = TO VALUE
.* ALL UNDEFINED CHARACTERS ARE NOT TRANSLATED
.*
         LCLA  &I &N
&I       SETA  0
&NAME    DS    0XL256
.DFNT    PRINT NOGEN
.DLP     DC    AL1(&I)
&I       SETA  &I+1
         AIF   (&I LE 255).DLP
         PRINT GEN
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 0).X
.NE      AIF   (T'&SYSLIST(&N,1) EQ 'O').NO
         ORG   &NAME+&SYSLIST(&N,1)
         DC    &SYSLIST(&N,2)
.NO      ANOP
&N       SETA  &N-1
         AIF   (&N GE 1).NE
.X       ORG
         MEND
./ ADD NAME=UNDERTSO                               17OCT89 12.47.53
         MACRO
&LOCLBL  UNDERTSO &YES=,&NO=
.*                                                                   *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
.*                                                                   *.
.*  ISYES          IDENTIFY WHETHER OR NOT TSO     IS BEING USED     *.
.*                                                                   *.
.*                 TAKES A BRANCH DEPENDING ON WHICH PARMS ARE       *.
.*                 SPECIFIED                                         *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
         AIF   ('&YES' EQ '' AND '&NO' EQ '').ERR
&LOCLBL  DS    0H                      REG &R --> REAL CVT
         L     R14,X'224'              GET ASCB ADDRESS
         L     R14,60(R14)             GET ASCBTSB FIELD
         LTR   R14,R14                 IF ZERO, NOT UNDER TSO
         AIF   ('&YES' EQ '').GEN370
         BNZ   &YES
.GEN370  ANOP
         AIF   ('&NO' EQ '').EXIT
         BZ    &NO
         AGO   .EXIT
.EXIT    ANOP
         MEXIT
.ERR     MNOTE 8,'*** UNDERTSO ERROR, MUST SPECIFY YES OR NO '
         MEND
./ ADD NAME=UPUT     0105-86237-86240-1703-00294-00288-00000-GS00150
         MACRO
&NAME    UPUT  &BFF,&SIZE,&EDIT,&WAIT,&HOLD,&BRKI,&INFOR,&HELP=,&CPPL=,*
               &IOPL=,&MF=I,&@ENTRY=UPUT                       RTS02A1
         LCLA  &OPT,&POPT                                      RTS02A1
         LCLA  &RET
         LCLB  &E,&W,&H,&B,&P
         LCLC  &PARM,&S,&L,&CPIOPL                             RTS02A1
&S       SETC  '&SYSNDX'                                       RTS02A1
&L       SETC  'L'''                                           RTS02A1
         AIF   ('&MF' EQ 'L').MFL                              RTS02A1
         AIF   ('&MF(1)' EQ 'E').MFE                           RTS02A1
         AIF   ('&MF(1)' EQ 'I' OR '&MF' EQ '').MFI            RTS02A1
         IHBERMAC 35,0,&MF                                     RTS02A1
         MEXIT                                                 RTS02A1
.MFI     ANOP                                                  RTS02A1
         AIF   ('&BFF' EQ '*' OR '&BFF' EQ '').ERROR1
         AIF   ('&BFF'(1,1) EQ '(' AND ('&SIZE' EQ '' OR '&SIZE' EQ '*'*
               )).ERROR1                                       RTS02A1
.MFE     AIF   ('&CPPL' EQ '' AND '&IOPL' EQ '' AND '&@ENTRY' EQ 'UPUT'*
               ).GETCPPL                    GET CPPL     WDPSC-8/26/86
         AIF   ('&CPPL' NE '' AND '&IOPL' NE '').ERROR2        RTS02A1
&CPIOPL  SETC  '&CPPL&IOPL'                                    RTS02A1
&PARM    SETC  '&EDIT'
.CKPARM  ANOP
&RET     SETA  &RET+1
         AIF   ('&PARM' EQ '').RET           NULL, TRY NEXT
         AIF   ('&PARM' EQ 'EDIT').EDI       SET EDIT
         AIF   ('&PARM' EQ 'ASIS').ASI       SET ASIS
         AIF   ('&PARM' EQ 'CONTROL').CON    SET CONTROL
         AIF   ('&PARM' EQ 'WAIT').WAI       SET WAIT
         AIF   ('&PARM' EQ 'NOWAIT').NOW     SET NOWAIT
         AIF   ('&PARM' EQ 'HOLD').HOL       SET HOLD
         AIF   ('&PARM' EQ 'NOHOLD').NOH     SET NOHOLD
         AIF   ('&PARM' EQ 'NOBREAK').NOB    SET NOBREAK
         AIF   ('&PARM' EQ 'BREAKIN').BRE    SET BREAKIN
         AIF   ('&@ENTRY' NE 'UPUT').ERROR3                    RTS02A1
         AIF   ('&PARM' EQ 'DATA').DATA                        RTS02A1
         AIF   ('&PARM' EQ 'INFOR').INFOR                      RTS02A1
         AGO   .ERROR3
.GETCPPL ANOP                                            WDPSC-8/26/86
         L     R15,4(R13)           GET OLD SAVE AREA    WDPSC-8/26/86
         L     R0,24(R15)           AND LOAD THE CPPL    WDPSC-8/26/86
&CPIOPL  SETC  '(0)'
&PARM    SETC  '&EDIT'
         AGO   .CKPARM
.*
.*  CHECK REGISTER NOTATION IS USED
.*
.CHKREG  SPACE 1
         AIF   ('&MF' EQ 'L').GENLIST                          RTS02A1
         AIF   ('&MF' NE 'I' AND '&MF' NE '').GENE             RTS02A1
.EXPAND  CNOP  0,4
         AIF   ('&@ENTRY' EQ 'UPUT').PUTNAME                   RTS02A1
&NAME    B     UPR&S              BRANCH AROUND PROMPT PARMS   RTS02A1
         AGO   .GENI                                           RTS02A1
.PUTNAME ANOP                                                  RTS02A1
         AIF   ('&NAME' EQ '').NONAME                          RTS02A1
&NAME    EQU   *                                               RTS02A1
.NONAME  AIF   ('&SIZE' EQ '' OR '&SIZE' EQ '*').NOSIZER       RTS02A1
         AIF   ('&SIZE'(1,1) NE '(').NOSIZER                   RTS02A1
         STH   &SIZE(1),UPT&S+2   STORE MESSAGE LENGTH         RTS02A1
.NOSIZER AIF   ('&BFF'(1,1) NE '(').NOBFFR                     RTS02A1
         STCM  &BFF(1),7,UPT&S+5  STORE BUFFER ADDRESS         RTS02A1
.NOBFFR  ANOP                                                  RTS02A1
         AIF   (&POPT NE X'06' AND &POPT NE X'04').NOH2R       RTS02A1
         AIF   ('&HELP(1)'(1,1) NE '(').NOH1R                  RTS02A1
         ST    &HELP(1),UPT&S+12  STORE HELP ADDRESS           RTS02A1
.NOH1R   AIF   ('&HELP(2)' EQ '').NOH2R                        RTS02A1
         AIF   ('&HELP(2)'(1,1) NE '(').NOH2R                  RTS02A1
         ST    &HELP(2),UPT&S+8   STORE HELP LENGTH            RTS02A1
.NOH2R   AIF   ('&@ENTRY' NE 'UPUT').GEND                      RTS02A1
         BAL   1,UPU&S            BRANCH AROUND CONSTANTS      RTS02A1
UPT&S    EQU   *                                               RTS02A1
         AGO   .GENI                                           RTS02A1
.GENLIST AIF   ('&@ENTRY' EQ 'UPUT').PUTLIST                   RTS02A1
         DS    0A                 ALIGN TO FULLWORD            RTS02A1
&NAME    EQU   *-4                PROMPT LIST HAS ORIGIN 4     RTS02A1
         AGO   .GENI                                           RTS02A1
.PUTLIST ANOP                                                  RTS02A1
&NAME    DS    0A                                              RTS02A1
.GENI    DC    AL2(&POPT)               PUTLINE OPTIONS        RTS02A1
         AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').DUMSIZE         RTS02A1
         AIF   ('&SIZE' NE '' AND '&SIZE' NE '*').GOTSIZE      RTS02A1
         DC    AL2(&L&BFF)        BUFFER LENGTH                RTS02A1
         AGO   .JSIZE                                          RTS02A1
.GOTSIZE AIF   ('&SIZE'(1,1) NE '(').OKSIZE                    RTS02A1
.DUMSIZE DC    AL2(0)             BUFFER SIZE                  RTS02A1
         AGO   .JSIZE                                          RTS02A1
.OKSIZE  ANOP                                                  RTS02A1
         DC    AL2(&SIZE)               BUFFER SIZE
.JSIZE   DC    AL1(&OPT)                OPTIONS
         AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').DUMBFF          RTS02A1
         AIF   ('&BFF'(1,1) NE '(').OKBFF                      RTS02A1
.DUMBFF  DC    AL3(0)             BUFFER ADDR                  RTS02A1
         AGO   .JBFF                                           RTS02A1
.OKBFF   ANOP                                                  RTS02A1
         DC    AL3(&BFF)                BUFFER ADDR
.JBFF    AIF   ('&HELP' EQ '' AND ('&MF' EQ 'I' OR '&MF' EQ '')).SKPHEL*
               P                                               RTS02A1
         AIF   ('&HELP' EQ '').RHNOLEN                         RTS02A1
         AIF   ('&HELP(1)'(1,1) EQ '(').RHMSG                  RTS02A1
         AIF   ('&HELP(2)' EQ '').HGLEN                        RTS02A1
         AIF   ('&HELP(2)'(1,1) EQ '(').HNOLEN                 RTS02A1
         DC    AL4(&HELP(2))      LENGTH OF HELP MESSAGE       RTS02A1
         AGO   .JHLEN                                          RTS02A1
.HGLEN  DC    AL4(&L&HELP(1))    LENGTH OF HELP MESSAGE        RTS02A1
         AGO   .JHLEN                                          RTS02A1
.HNOLEN DC    AL4(0)             LENGTH OF HELP MESSAGE        RTS02A1
.JHLEN   DC    AL4(&HELP(1))      HELP MESSAGE ADDR            RTS02A1
         AGO   .SKPHELP                                        RTS02A1
.RHMSG   AIF   ('&HELP(2)'(1,1) EQ '(').RHNOLEN                RTS02A1
         DC    AL4(&HELP(2))      LENGTH OF HELP MESSAGE       RTS02A1
         AGO   .JRHLEN                                         RTS02A1
.RHNOLEN DC    AL4(0)             LENGTH OF HELP MESSAGE       RTS02A1
.JRHLEN  DC    AL4(0)             ADDRESS OF HELP MESSAGE      RTS02A1
.SKPHELP ANOP                                                  RTS02A1
         AIF   ('&MF' EQ 'L').GEND                             RTS02A1
         AIF   ('&@ENTRY' EQ 'UPUT').PUTCPPL                   RTS02A1
UPR&S    EQU   *                                               RTS02A1
         AGO   .NONAME                                         RTS02A1
.PUTCPPL ANOP                                                  RTS02A1
UPU&S    EQU   *                                               RTS02A1
.LDCPPL  AIF   ('&CPIOPL' EQ '(0)').CPPL0                      RTS02A1
         AIF   ('&CPIOPL'(1,1) EQ '(').CPPLR                   RTS02A1
         LA    0,&CPIOPL        LOAD CPPL ADDRESS              RTS02A1
         AGO   .CPPL0                                          RTS02A1
.CPPLR   LA    0,0&CPIOPL     LOAD CPPL ADDRESS                RTS02A1
         AIF   ('&IOPL' NE '').IOPLFF                          RTS02A1
         AGO   .CPPLO                                          RTS02A1
.CPPL0   AIF   ('&IOPL' NE '').IOPLFF                          RTS02A1
         ICM   0,8,=X'00'         INDICATE CPPL PASSED         RTS02A1
         AGO   .CPPLO                                          RTS02A1
.IOPLFF  ICM   0,8,=X'FF'         INDICATE IOPL PASSED         RTS02A1
.CPPLO   ANOP                                                  RTS02A1
         L     15,=V(UKJUPUT)                                  RTS02A1
         BALR  14,15              CALL UPUT ROUTINE            RTS02A1
.GEND    SPACE 1
         MEXIT
.EDI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
         AGO   .RET
.ASI     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
&OPT     SETA  &OPT+1                   SET EDIT=ASIS
         AGO   .RET
.CON     ANOP
         AIF   (&E).ERROR2              DUP OPTION
&E       SETB  1                        EDIT OPTION SPECFIED
         MNOTE 0,'UPUT001 WARNING -- EFFECTS OF CONTROL OPTION ARE DEVI*
               CE DEPENDENT'
&OPT     SETA  &OPT+2                   SET EDIT=CONTROL
         AGO   .RET
.WAI     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
         AGO   .RET
.NOW     ANOP
         AIF   (&W).ERROR2              DUP OPTION
&W       SETB  1                        WAIT OPTION SPECIFIED
&OPT     SETA  &OPT+X'10'               SET WAIT=NOWAIT
         AGO   .RET
.HOL     ANOP
         AIF   (&H).ERROR2              DUP OPTION
&H       SETB  1                        HOLD OPTION SPECIFIED
&OPT     SETA  &OPT+X'08'               SET HOLD=HOLD
         AGO   .RET
.NOH     ANOP
         AIF   (&H).ERROR2              DUP OPTION
&H       SETB  1                        HOLD OPTION SPECIFIED
         AGO   .RET
.BRE     ANOP
         AIF   (&B).ERROR2              DUP OPTION
&B       SETB  1                        BREAK OPTION SPECIFIED
&OPT     SETA  &OPT+X'04'               SET BREAKIN
         AGO   .RET
.NOB     ANOP
         AIF   (&B).ERROR2              DUP OPTION
&B       SETB  1                        BREAK OPTION SPECIFIED
         AGO   .RET
.INFOR   ANOP                                                  RTS02A1
         AIF   (&P).ERROR2              DUP OPTION             RTS02A1
&P       SETB  1                   PUTLINE OPTION SPECIFIED    RTS02A1
         AGO   .RET
.DATA    ANOP                                                  RTS02A1
         AIF   (&P).ERROR2              DUP OPTION             RTS02A1
&P       SETB  1                  PUTLINE OPTION SPECIFIED     RTS02A1
&POPT    SETA  X'30'              SET SINGLE DATA LINE         RTS02A1
.RET     ANOP
&PARM    SETC  '&WAIT'
         AIF   ('&RET' EQ '1').CKPARM   CHECK WAIT
&PARM    SETC  '&HOLD'
         AIF   ('&RET' EQ '2').CKPARM   CHECK HOLD
&PARM    SETC  '&BRKI'
         AIF   ('&RET' EQ '3').CKPARM   CHECK BRKI
&PARM    SETC  '&INFOR'                                        RTS02A1
         AIF   ('&RET' EQ '4').CKPARM   CHECK INFOR/DATA       RTS02A1
         AIF   (&POPT NE 0).NOHELP                             RTS02A1
         AIF   ('&HELP' EQ '').ONELEV                          RTS02A1
&POPT    SETA  X'04'                                           RTS02A1
         AIF   ('&@ENTRY' NE 'UPUT').CHKREG                    RTS02A1
&POPT    SETA  &POPT+X'02'                                     RTS02A1
         AGO   .CHKREG                                         RTS02A1
.ONELEV  AIF   (NOT &P AND '&MF(1)' EQ 'E').NOHELP             RTS02A1
&POPT    SETA  X'10'                                           RTS02A1
         AIF   ('&@ENTRY' NE 'UPUT').CHKREG                    RTS02A1
&POPT    SETA  &POPT+X'20'      CHANGED DEFAULT FROM INFO      RTS02A1
         AGO   .CHKREG          (12) TO DATA (30)      8/28/86 **WDPSC
.NOHELP  AIF   ('&HELP' EQ '').CHKREG                          RTS02A1
         MNOTE 4,'HELP IGNORED WHEN DATA OPTION USED'          RTS02A1
         AGO   .CHKREG                  DONE WITH OPTIONS
.MFL     ANOP                                                  RTS02A1
         AIF   ('&BFF' EQ '').BFFOK                            RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').LREG                       RTS02A1
.BFFOK   AIF   ('&SIZE' EQ '').SIZEOK                          RTS02A1
         AIF   ('&SIZE'(1,1) EQ '(').LREG                      RTS02A1
.SIZEOK  AIF   ('&HELP' EQ '').HELPOK                          RTS02A1
         AIF   ('&HELP(1)' EQ '').HELP1OK                      RTS02A1
         AIF   ('&HELP(1)'(1,1) EQ '(').LREG                   RTS02A1
.HELP1OK AIF   ('&HELP(2)' EQ '').HELPOK                       RTS02A1
         AIF   ('&HELP(2)'(1,1) NE '(').HELPOK                 RTS02A1
.LREG    IHBERMAC 69                                           RTS02A1
         MEXIT                                                 RTS02A1
.HELPOK  AIF   ('&CPPL' EQ '' AND '&IOPL' EQ '').NOCPPL        RTS02A1
         MNOTE 4,'UPUT002 CPPL/IOPL OPERAND IGNORED WHEN MF=L' RTS02A1
.NOCPPL  ANOP                                                  RTS02A1
&PARM    SETC  '&EDIT'                                         RTS02A1
         AGO   .CKPARM                                         RTS02A1
.GENE    ANOP                                                  RTS02A1
&NAME    DS    0H                                              RTS02A1
         AIF   ('&@ENTRY' EQ 'UPUT').PUTEX                     RTS02A1
         AIF   ('&MF(2)'(1,1) EQ '(').PRER                     RTS02A1
         LA    1,4+&MF(2)         ADDR PROMPT PARM LIST        RTS02A1
         AGO   .JEX                                            RTS02A1
.PRER    LA    1,4&MF(2)          ADDR PROMPT PARM LIST        RTS02A1
         AGO   .JEX                                            RTS02A1
.PUTEX   ANOP                                                  RTS02A1
         AIF   ('&BFF' NE '(1)').NER1                          RTS02A1
         LR    15,1               COPY MSG ADDR TO 15          RTS02A1
.NER1    IHBINNRA &MF(2)                                       RTS02A1
.JEX     AIF   (&POPT EQ 0).ENOPOPT                            RTS02A1
         MVI   1(1),&POPT         STORE PUTLINE OPTIONS        RTS02A1
.ENOPOPT AIF   ('&SIZE' EQ '').ENOSIZ                          RTS02A1
         AIF   ('&SIZE' EQ '*').STARSIZ                        RTS02A1
         AIF   ('&SIZE'(1,1) EQ '(').RSIZ                      RTS02A1
         LA    14,&SIZE                                        RTS02A1
         STH   14,2(1)           STORE MSG LENGTH IN PARM LIST RTS02A1
         AGO   .ENOSIZ                                         RTS02A1
.RSIZ    STH   &SIZE(1),2(1)     STORE MSG LENGTH IN PARM LIST RTS02A1
         AGO   .ENOSIZ                                         RTS02A1
.STARSIZ AIF   ('&BFF' EQ '*' OR '&BFF' EQ '').ENOSIZ          RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').ENOSIZ                     RTS02A1
         LA    14,&L&BFF          GET LENGTH OF MESSAGE        RTS02A1
         STH   14,2(1)            STORE IN PARM LIST           RTS02A1
.ENOSIZ  AIF   (NOT (&E OR &W OR &H OR &B)).ENOPT              RTS02A1
         MVI   4(1),&OPT          STORE TPUT OPTIONS           RTS02A1
.ENOPT   AIF   ('&BFF' EQ '' OR '&BFF' EQ '*').ENOBFF          RTS02A1
         AIF   ('&BFF'(1,1) EQ '(').ERBFF                      RTS02A1
         LA    14,&BFF                                         RTS02A1
         STCM  14,7,5(1)        STORE BUFFER ADDR IN PARM LIST RTS02A1
         AGO   .ENOBFF                                         RTS02A1
.ERBFF   AIF   ('&BFF' NE '(1)').ERN1                          RTS02A1
         STCM  15,7,5(1)        STORE MSG ADDRESS IN PARM LIST RTS02A1
         AGO   .ENOBFF                                         RTS02A1
.ERN1    STCM  &BFF(1),7,5(1)   STORE MSG ADDRESS IN PARM LIST RTS02A1
.ENOBFF  AIF   ('&HELP' EQ '').ENOHELP                         RTS02A1
         AIF   ('&HELP(1)' EQ '').EGHELP                       RTS02A1
         AIF   ('&HELP(1)'(1,1) EQ '(').ERH1                   RTS02A1
         LA    14,&HELP(1)                                     RTS02A1
.EGHELP  ST    14,12(1)           STORE HELP MSG ADDR          RTS02A1
         AGO   .EHELP2                                         RTS02A1
.ERH1    ST    &HELP(1),12(1)     STORE HELP MSG ADDR          RTS02A1
.EHELP2  AIF   ('&HELP(2)' EQ '').EDH2                         RTS02A1
         AIF   ('&HELP(2)'(1,1) EQ '(').ERH2                   RTS02A1
         LA    14,&HELP(2)                                     RTS02A1
         ST    14,8(1)            STORE HELP MSG LENGTH        RTS02A1
         AGO   .ENOHELP                                        RTS02A1
.ERH2    ST    &HELP(2),8(1)      STORE HELP MSG LENGTH        RTS02A1
         AGO   .ENOHELP                                        RTS02A1
.EDH2    AIF   ('&HELP(1)' EQ '').ENOHELP                      RTS02A1
         AIF   ('&HELP(1)'(1,1) EQ '(').ENOHELP                RTS02A1
         LA    14,&L&HELP(1)                                   RTS02A1
         ST    14,8(1)            STORE HELP MSG LENGTH        RTS02A1
.ENOHELP AIF   ('&@ENTRY' NE 'UPUT').GEND                      RTS02A1
         AGO   .LDCPPL                                         RTS02A1
.RERR    IHBERMAC 192
         MEXIT
.ERROR1  IHBERMAC 24
         MEXIT
.ERROR2  IHBERMAC 54,,,
         MEXIT
.ERROR3  IHBERMAC 49,,&PARM
         MEND
./ ADD NAME=USERADDS 0122-82147-85175-1001-00026-00010-00017-DN00150
         NAME  ZWAP,0A     DONTSWAP                    WDPSC
         NAME  TPJOBS,0B                               WDPSC
         NAME  WHERE,0C                                WDPSC
         NAME  SQA,0D                                  WDPSC
         NAME  SWAP,0E                                 WDPSC
         NAME  SETS,0F                                 WDPSC
         NAME  VTOCIX,0G                               WDPSC
         NAME  DQE,0H                                  WDPSC
*        USED        ,0I   NUCMAP                      WDPSC
         NAME  ACF2CVT,0J                              WDPSC
         NAME  RATE,0K                                 WDPSC
         NAME  LIST,0L                                 WDPSC
         NAME  TCZAP,0M                                WDPSC
         NAME  VTOCOS,0N                               WDPSC
         NAME  ACFSVC,0O                               WDPSC
         NAME  MPMCSA,0P                               WDPSC
         NAME  CTCCSA,0Q                               WDPSC
         NAME  PUBLIC,0R                               WDPSC
         NAME  SMF,0S                                  WDPSC
         NAME  PQE,0T                                  WDPSC
         NAME  TBUF,0U                                 WDPSC
         NAME  XPJOBS,0X                               WDPSC
         NAME  QUJOBS,05                               WDPSC
         NAME  CSP,0W                                  WDPSC
         NAME  RMFMEM,0Y                               WDPSC
         NAME  ADDLIB,1A                               WDPSC
./ ADD NAME=USERSVC  0140-84089-89255-1303-00053-00041-00000-CG00155
***********************************************************************
*        -------------  S V C   T A B L E   --------------            *
*                                                                     *
*        SPECIFY THE NUMBER  TYPE  LOCK_HELD AUTHORIZATION            *
*                                                                     *
*   WHEN SPECIFYING THE SVC NUMBER, NUMBERS ENDING IN ZERO SHOULD BE  *
*   RESERVED FOR TYPES 1,2 AND 6.                                     *
***********************************************************************
      SVCTABLE SVC-255-T1,                                             C
               SVC-254-T1,                                             C
               SVC-253-T3,              RESOLVE/XA SVC (REL. 2)        C
               SVC-252-T3,              CICS/STABILIZE                 C
               SVC-251-T3,              UCC-11 (ARTS)                  C
               SVC-250-T1,              CA-DISPATCH PROD SVC (JET)     C
               SVC-249-T3,              FAVER QUICAT - SP ONLY         C
               SVC-248-T3,              ACF2                           C
               SVC-247-T3,              ACF2                           C
               SVC-246-T3-(L1),         ADABAS V4 PROD                 C
               SVC-245-T3,              PACE ACCOUNT LOAD              C
               SVC-244-T2,              IMS                            C
               SVC-243-T3,              COMPLETE                       C
               SVC-242-T3,              QUICKFETCH                     C
               SVC-241-T3,              VTAM PRINTER SUPPORT SYSTEM    C
               SVC-240-T1,                                             C
               SVC-239-T2,              CICS 1.7                       C
               SVC-238-T4,              UCC-1 (TMS)                    C
               SVC-237-T4,              UCC-1 (TMS)                    C
               SVC-236-T3,              JES2/TSO INTERFACE             C
               SVC-235-T4,              RESOLVE/XA SVC (REL. 3)        C
               SVC-234-T4,              SPF INTERACTIVE COMPRESS       C
               SVC-233-T3,              ADABAS/ACF2 INTERFACE  V4      C
               SVC-232-T3,              ADABAS V5 TEST                 C
               SVC-231-T3,              DMS ALLOCATION SVC             C
               SVC-230-T1,              3270 SUPEROPTIMIZER/TSO (KK)   C
               SVC-229-T4,              UCC-7                          C
               SVC-228-T4,              IMS/VS 1.3                     C
               SVC-227-T2,              CICS 2.1                       C
               SVC-226-T2,              IMS/VS 1.3                     C
               SVC-225-T5,                                             C
               SVC-224-T5,                                             C
               SVC-223-T5,                                             C
               SVC-222-T6,              CICS 1.7                       C
               SVC-221-T6,              CICS 2.1   VTAM FAST PATH      C
               SVC-220-T4,              IMS 2.1                        C
               SVC-219-T2,              IMS 2.1                        C
               SVC-218-T3,              NETVIEW LOGSVC (R2)            C
               SVC-217-T3,              FOCUS SUPPORT                  C
               SVC-216-T3,              ADABAS V5 - ACF2               C
               SVC-215-T3,              THE MONITOR                    C
               SVC-214-T3,                                             C
               SVC-213-T3,                                             C
               SVC-212-T3,                                             C
               SVC-211-T3               IOF RELEASE 6
./ ADD NAME=VOLMAC   0100-82299-82299-1420-00122-00122-00000-DN00150
        MACRO
        VOLMAC  &VOLUME,&USE=YES,&UNIT=3350
        GBLA   &COUNTY,&COUNTN
        LCLA   &NEXT,&NOW,&LOOP
        LCLC   &USES,&BIT1,&BIT2,&BIT3
.* THE MACRO VOLMAC IS USED TO CONSTRUCT A TABLE OF VOLUMES
.* THAT ARE ALLOCATED IN RESPONSE TO A USESR'S REQUEST FOR DEFAULT
.* VOLUME.
.* THE LIST IS IN TWO PARTS THAT ARE INTERMINGLED. EACH PART IS
.* CHAINED SEPARATELY AND A SEQUENTIAL SEARCH CAN SCAN ENTRIES.
.* PART ONE IS ELIGIBLE VOLUMES WHERE ALLOCATION WILL TAKE PLACE.
.* PART TWO IS ELIGIBLE VOLUMES BUT ALLOCATION WILL CONVERT TO PART
.* ONE.
&USES    SETC  '&USE'
       AIF (N'&VOLUME EQ 0).LASTUP
       AIF (&SYSNDX GT 1).DOIT
VOLTABLE CSECT
         DC  A(VOLY1)
         DC  A(VOLN1)
.DOIT  ANOP
       AIF ('&USES' EQ 'Y').TRY1
       AIF ('&USES' EQ 'YES').TRY1
       AIF ('&USES' EQ 'NO').TRY2
       AIF ('&USES' EQ 'N').TRY2
       MNOTE 4,'USES IS INVALID. VOLUMES ARE CONSIDERED INELIGIBLE'
       AGO   .TRY2
.TRY1  ANOP
&USES   SETC  'Y'
&COUNTY SETA &COUNTY+1
&NEXT   SETA &COUNTY+1
&NOW    SETA &COUNTY
&BIT1   SETC '1'
       AGO   .OK
.TRY2   ANOP
&USES  SETC  'N'
&COUNTN SETA &COUNTN+1
&NEXT   SETA &COUNTN+1
&NOW    SETA &COUNTN
&BIT1   SETC '0'
.OK      ANOP
       AIF ('&UNIT' EQ '').M1
       AIF ('&UNIT' EQ '3350').M1
       AIF ('&UNIT' EQ '3330-1').M2
       AIF ('&UNIT' EQ '3380').M4
       AIF ('&UNIT' EQ '3330').M2
       AIF ('&UNIT' EQ '3330-11').M2
       MNOTE 4,'INVALID UNIT TYPE, ASSUME 3350'
       AGO  .M1
.M2    ANOP
&BIT2  SETC '0'
&BIT3  SETC '0'
       AGO   .M3
.M4    ANOP
&BIT2  SETC '0'
&BIT3  SETC '1'
       AGO   .M3
.M1    ANOP
&BIT2  SETC '1'
&BIT3  SETC '0'
.M3    ANOP
       AIF (N'&VOLUME GT 1).MULTI
       AIF ('&VOLUME'(1,1) EQ '(').MULTI
       AIF (K'&VOLUME GT 6).DROP
       SPACE 1
VOL&USES&NOW DC  A(VOL&USES&NEXT)
             DC  CL6'&VOLUME'
             DC  B'&BIT1.&BIT2.&BIT3.00000',XL1'0'
        MEXIT
.MULTI  ANOP
&LOOP   SETA  1
.AGAIN  ANOP
        AIF  (&LOOP GT  N'&VOLUME).END
        AIF  (K'&VOLUME(&LOOP) GT 6).DROPM
        SPACE 1
VOL&USES&NOW DC  A(VOL&USES&NEXT)
             DC  CL6'&VOLUME(&LOOP)'
             DC  B'&BIT1.&BIT2.&BIT3.00000',XL1'0'
&NEXT   SETA &NEXT+1
&NOW    SETA &NOW+1
&LOOP   SETA &LOOP+1
        AGO  .AGAIN
.END    ANOP
        AIF  ('&USES' EQ 'N').ENDN
&COUNTY SETA &NOW-1
        MEXIT
.ENDN   ANOP
&COUNTN SETA &NOW-1
        MEXIT
.DROP   ANOP
        MNOTE 8,'&VOLUME IS LONGER THAN 6 CHARACTERS'
        AIF   ('&USES' EQ 'N').DROPN
&COUNTY SETA  &COUNTY-1
        MEXIT
.DROPN  ANOP
&COUNTN SETA  &COUNTN-1
        MEXIT
.DROPM  ANOP
        MNOTE 8,'&VOLUME(&LOOP) IS LONGER THAN 6 CHARACTERS'
&LOOP   SETA  &LOOP+1
        AGO   .AGAIN
.LASTUP ANOP
&COUNTY SETA  &COUNTY+1
&COUNTN SETA  &COUNTN+1
        AIF  (&COUNTY EQ 1).LASTA
        SPACE 1
VOLY&COUNTY DC A(VOLY1)
         DC   CL6' '
         DC   XL6'8000'
         AGO  .LASTB
.LASTA   ANOP
VOLY1    DC   A(0),CL6' ',XL6'0'
.LASTB   ANOP
        AIF  (&COUNTN EQ 1).LASTC
        SPACE 1
VOLN&COUNTN DC A(VOLN1)
         DC   CL6' '
         DC   XL6'0'
         MEXIT
.LASTC   ANOP
VOLN1    DC   A(0),CL6' ',XL6'0'
         MEXIT
        MEND
./ ADD NAME=VPSACFID 0106-84257-84257-1759-00023-00016-00016-KK00150
         MACRO
&USER    VPSACFID &ACFUID=NONE
         LCLA  &L
         LCLC  &AS
*
* VPSACFID MACRO ALLOWS VPS COMMAND SECURITY BASED ON ACF2 UID FIELD.
*        IN ORDER TO USE THIS MACRO, TURN ON UOPTION 0 AND MAKE THE
*        LABEL ON THIS INSTRUCTION EQUAL THE USER VALUE ON THE VPSEC
*        MACRO FOR THE USER/GROUP YOU WISH TO MODIFY
*
         ORG   U&USER
         DC    A(&USER)
         ORG
VPSACF   CSECT
&AS      SETC  '&ACFUID'
         AIF   ('&ACFUID '(1,1) EQ '''').QOK
&AS      SETC  '''&ACFUID'''
.QOK     ANOP
&L       SETA  K'&AS-2
&USER    DC    AL2(&L)
         DC    CL24&AS
&SYSECT  CSECT
         MEND
./ ADD NAME=WTP      0115-84299-84310-1032-00094-00004-00092-KK00150
         MACRO
&LAB1    WTP   &TXT,&LEN,&MF=I
         LCLC  &GT,&T,&CL,&P
         LCLA  &TL
&P       SETC  '()'(1,1)
&T       SETC  T'&TXT
.*       MNOTE 'MESSAGE TYPE IS &T'
         AIF   (T'&TXT EQ 'U').MP
         AIF   (T'&TXT EQ 'N').MP
.* NO MESSAGE PRESENT
&GT      SETC  (125)' '
&TL      SETA  125
         AGO   .NQL
.* MESSAGE PRESENT
.MP      ANOP
&GT      SETC  '&TXT'
         AIF   ('&TXT'(1,1) NE '''').NQL
&GT      SETC  '&TXT'(2,K'&TXT-2)
.* CHECK LENGTH:
.NQL     ANOP
&T       SETC  T'&LEN
.*       MNOTE 'LENGTH TYPE IS &T'
         AIF   (T'&LEN NE 'O').NLC
.* NO LENGTH PRESENT
         AIF   (T'&TXT EQ 'O').DL
         AIF   (T'&TXT EQ 'U').UL
&TL      SETA  L'&TXT
         AGO   .LOK
.UL      ANOP
&TL      SETA  K'&GT
         AGO   .NLL
.* NO LENGTH AND NO MESSAGE: DEFAULT LEN = 125
.DL      ANOP
&TL      SETA  125
         AGO   .NLL
.* LENGTH PRESENT
.NLC     AIF   (T'&LEN EQ 'N').SDL
         AIF   (T'&LEN EQ 'U').NLL
.* LENGTH NOT SELF DEFINING
&TL      SETA  L'&LEN
         AGO   .LOK
.* LENGTH SELF DEFINING
.SDL     ANOP
         AIF   ('&LEN'(1,1) EQ '&P').NLL
&TL      SETA  &LEN
.LOK     ANOP
.* FORCE MESSAGE LENGTH:
&GT      SETC  '&GT'.(125)' '
&GT      SETC  '&GT'(1,&TL)
.NLL     ANOP
         AIF   ('&MF(1)' EQ 'L').WTPL
         AIF   ('&MF(1)' EQ 'E').WTPE
&LAB1    WTO   &TXT,ROUTCDE=(11)
         MEXIT
.WTPL    ANOP
&LAB1    WTO   '&GT',ROUTCDE=(11),MF=L
         MEXIT
.WTPE    ANOP
         AIF   (T'&TXT NE 'O').YM
         AIF   (N'&MF EQ 2).MFOK
         MNOTE 8,'WTP MF TYPE INVALID'
         MEXIT
.MFOK    ANOP
&LAB1    WTO   MF=&MF
         MEXIT
.YM      ANOP
&LAB1    DS    0H
         LH    1,&MF(2)
         SH    1,=H'6'
         MVI   &MF(2).+4,C' '
C&SYSNDX MVC   &MF(2).+5(0),&MF(2).+4   CLEAR WTO BUFF
         EX    1,C&SYSNDX
         AIF   (T'&LEN EQ 'U').UL2
         AIF   (T'&LEN EQ 'O').AIL
         AIF   ('&LEN'(1,1) NE '&P').AIL
&P       SETC  '&LEN'(2,K'&LEN-2)
&CL      SETC  '0'
         BCTR  &P,0
         EX    &P,M&SYSNDX
         LA    &P,1(&P)
         AGO   .CT
.AIL     ANOP
&CL      SETC  '&TL'
         AGO   .CT
.UL2     ANOP
&CL      SETC  '&LEN'
.CT      AIF   (T'&TXT EQ 'C').CC
M&SYSNDX MVC   &MF(2).+4(&CL),=C'&GT'
         AGO   .EWTO
.CC      ANOP
M&SYSNDX MVC   &MF(2).+4(&CL),&TXT
.EWTO    ANOP
         WTO   MF=&MF
         MEND
./ ADD NAME=WTPO     0100-81189-81189-1634-00004-00004-00000-KK00150
         MACRO
&LAB1    WTP   &TXT
&LAB1    WTO   &TXT,ROUTCDE=(11)
         MEND
