         EJECT
*                                                                   KK1
*KK START OF WDPSC VIRTUAL TERMINAL MOD - 1ST 20 TERMS ANY REMOTE:  KK1
*        NEXT 20  FOR LOCAL SNA TERMINALS                           KK1
*                                                                   KK1
* THIS CODE IS INSERTED INTO UCC7 MODULE SASSLGON DIRECTLY BEFORE   KK1
*  LABEL INVID (INVALID TERMINAL WTO) TO ALLOW ANY TERMINAL TO      KK1
*  CONNECT TO UCC7. AS CODED YOU MUST DEFINE FIRST 20 TERMINALS     KK1
*  AS DUMMY REMOTES AND NEXT 20 AS DUMMY LOCAL SNA TERMINALS.       KK1
*  OPERATION OF OTHER VTAM TERMINALS DEFINED IS NOT AFFECTED.       KK1
*                                                                   KK1
***NOTE- ADD LABEL 'RESTART' TO INSTRUCTION LIKE THE FOLLOWING      KK1
*        INSTRUCTION BUT ABOVE LABEL 'SCAN' IN SASSLGON:            KK1
*                                                                   KK1
         L     R1,VTMIDTA         POINT TO LOGON ID TABLE           KK1
         LH    R3,=H'21'          LOAD NUMBER OF ENTRIES            KK1
         BCTR  R3,0               REDUCE FOR ONE EXTRA              KK1
         CLI   0(R4),C'L'         LOCAL TERM?                       KK1
         BNE   SCAN2              NO->START SCAN                    KK1
         AH    R1,=H'240'         SKIP 1ST 20 TERMINALS             KK1
SCAN2    DS    0H                                                   KK1
         LA    R1,12(,R1)         POINT TO NEXT TABLE ENTRY         KK1
         L     R6,8(,R1)          ADDRESS OF UCC7 TERM DEFINITION   KK1
         TM    TRMVFLG,TRMINCON   CONNECT IN PROCESS...             KK1
         BNZ   CSCAN                   BIF - SIMLOGON DONE          KK1
         TM    TRMVFLG,TRMCONN    ALREADY CONNECTED...              KK1
         BNZ   CSCAN                   BIF - CONNECTED(REJECT LOGON)KK1
         MVC   0(8,R1),0(R4)      MAKE TABLE ENTRY MATCH USERS ID   KK1
         MVC   TRMVTMID(8),0(R4)  MAKE TERM VTAMID MATCH            KK1
         L     R2,TRMADDRC+4      R2->NIB                           KK1
         USING ISTDNIB,R2                                           KK1
         MVC   NIBSYM,0(R4)       VTAMID TO NIB                     KK1
         B     RESTART            START THE NORMAL SCAN AGAIN       KK1
CSCAN    BCT   R3,SCAN2           CONTINUE SCAN                     KK1
*                                                                   KK1
*KK END OF WDPSC VIRTUAL TERMINAL MOD                               KK1
*                                                                   KK1
         EJECT
