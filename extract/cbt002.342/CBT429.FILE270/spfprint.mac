SPFPRINT TITLE 'SPF DSPRINT OPTIONS INTERCEPT   '
*
*    11/07/85 - V1M1 MOD TO IGNORE ISPF OPTION 6
*
SPFPRINT CSECT
         SAVE  (14,12),,SPFPRINT-V1M1
         LR    R12,R15            R12->SPFPRINT
         USING SPFPRINT,R12
         L     R14,540            R14->TCB
         L     R14,132(R14)       R14->MOTHER
         L     R15,0(R14)         R15->CURRENT MOTHER RB
         L     R2,12(R15)         R2->CURRENT MOTHER CDE
         CLC   8(8,R2),=CL8'ISRPTC' ISPF OPTION 6?
         BE    XCTL               DON'T MESS WITH!
         L     R15,28(R15)        R15->PREVIOUS RB
         CR    R14,R15            TCB = RB?
         BE    XCTL               SORRY, NOT ISPF
         L     R14,12(R15)        R14->CDE
         CLC   8(8,R14),=CL8'ISPTASK' SPF CALLING?
         BE    TRUENTRY           YUP.
XCTL     L     R15,=V(VPSPRINT)   R15->REAL DSPRINT
         L     R14,12(,R13)
         LM    0,12,20(R13)
         BR    R15                GO TO REAL DSPRINT
         EJECT
TRUENTRY EQU   *
         LR    R5,R1              R5-> PARM LIST
         L     R0,=A(WORKLEN)     R0=LENGTH OF WORKAREA
         GETMAIN R,LV=(0)        GET SOME ROOM TO MANUVER
         USING WORKAREA,R13
         ST    R13,4(,R1)         HIS SAVEAREA IN MINE
         ST    R1,8(,R13)         MY SAVEAREA IN HIS
         LR    R13,R1             R13->MY SAVEAREA/WORKAREA
         REGEQU
*
*     CALLING SEQUENCE (ISPF CALL TO DSPRINT IS INTERCEPTED.):
*
*        CALL  'DSPRINT'    -
*              CBUF
*              UPT
*              PSCB
*              ECT
*
         EJECT
*
*              GET ANY DSPRINT OPTIONS:
*
         MVI   VA,C' '            A BLANK
         MVC   VA+1(71),VA        SMEAR
         MVC   VL(4),VLP          SET DATA AREA LENGTHS
         CALL  ISPLINK,(=CL8'VCOPY',NL,VL,VA,=CL8'MOVE'),VL,MF=(E,CL)
         LR    R1,R5
         LTR   R15,R15            RC=0?
         BNZ   NOPARMS            NO.
         LR    R10,R5             R10->CPPL
         USING CPPL,R10
         EJECT
*
*              ADD ANY DSPRINT OPTIONS TO THE COMMAND STRING:
*
*              SET UP:
         MVI   BLANKS,C' '        GET A SPACE CHAR
         MVC   BLANKS+1(255),BLANKS  SPREAD IT AROUND
         MVC   CMD,BLANKS         INIT BUFFER
         L     R6,CPPLCBUF        R6 -> CMD BUFFER
         MVC   CMD,0(R6)          PUT IT IN OUR CMD AREA
*        LENGTH LOOP:
         LA    R1,200             R1=INDEX TO LAST CMD BUF SPOT
         LA    R15,CMD+199        R15->CMD BUF END
CLOOP    CLI   0(R15),C' '        BLANK?
         BNE   CEND               NOPE.
         BCTR  R15,R0             YUP.
         BCT   R1,CLOOP           YUP.
CEND     STH   R1,CMD             PASS IT
*
         MVC   PARML(16),CPPL     COPY PARM LIST
         LA    R3,CMD             R3 -> NEW CMD BUFF
         ST    R3,PARML           PASS NEW ADDR
         LA    R1,PARML           R1->NEW PARM LIST
*
*
*        CALL  DSPRINT:
*
NOPARMS  L     R15,=V(VPSPRINT)   R15 -> DSPRINT (REAL ONE)
         BALR  R14,R15            CALL
         EJECT
*
*        RETURN TO WHOEVER CALLED US:
*
RETURN   DS    0H
RET      LEAVER
         TITLE 'WORKAREAS'
*
*              STATIC WORKAREA:
*
NL       DC    C'(DSPRTOPT)'
VLP      DC    F'72'
         LTORG
*
*
*              DYNAMIC WORKAREAS:
*
*
WORKAREA DSECT
SAVEAREA DS    18F
PARML    DS    3F
BLANKS   DS    CL256
VL       DS    F'72'
CMD      DS    CL128
VA       DS    0H
OPTAREA  DS    CL72
CL       CALL  ,(0,0,0,0,0),MF=L
WORKLEN  EQU   *-WORKAREA
         EJECT
         IKJCPPL
         END
