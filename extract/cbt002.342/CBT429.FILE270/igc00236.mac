         TITLE 'SHOW/$/LOGON  SVC'
*
*        THIS MODULE REQUIRES ASMH - IT CAN RUN ABOVE 16M ADDRESS
*
*        THIS WAS SVC 242 BUT DAN CHANGED IT TO SVC 236
*        IT IS USED BY THE LOGON EXIT MODULE LOGJCLX TO GET AUTHORIZED
*        TO PERFORM SOME OF OUR LOCAL EXTENSIONS...
*        IT MAY BE CALLED ONLY BY PROGRAMS RESIDING OUTSIDE OF THE
*        PRIVATE (AND PRIVATE-EXTENDED) AREAS TO TURN ON/OFF JSCBAUTH
*        OR TO ISSUE A SYSTEM COMMAND VIA SVC 34 ($ CMD DOES THAT)
*
IGC00236 START
         USING *,R6
IGC00236 AMODE ANY
IGC00236 RMODE ANY
         B     STARTSVC
         DC    AL1(22)
         DC    C'IGC00236-WDPSC/TSO SVC'
*
*                         CHECK SYSTEM LEVEL FIRST:
*
STARTSVC L     R11,16             R11->CVT
         USING CVT,R11
         L     R2,28(5)           R2->CALLERS RB
         L     R2,20(R2)          R2->CALLERS NEXT INSTRUCTION
         N     R2,=X'7FFFFFFF'    KILL XA BIT
*
XACHK    TM    CVTDCB,CVTMVSE     XA SYSTEM?
         BNO   NOTXA              NOPE.
         LA    R8,XA              R8->XA CODE
         O     R8,=X'80000000'    AMODE=31
         BSM   0,R8               GO TO XA CODE
*
*        NON-XA SYSTEM: VERIFY CALLED FROM OUTSIDE PRIVATE AREA:
*
NOTXA    DS    0H
         L     R10,CVTGDA         R10->GDA IN SQA
         C     R2,16(R10)         BELOW PRIVATE?
         BL    CALLEROK           YES
         L     R9,16(R10)         R9->PRIVATE AREA
         A     R9,20(R10)         R9->END OF PRIVATE AREA
         CR    R9,R2              ABOVE PRIVATE?
         BL    CALLEROK           YES
         B     AUTHBEND           NO->KILL HIM
*
*
*        XA SYSTEM: VERIFY CALLED FROM OUTSIDE PRIVATE AREA:
*
XA       DS    0H
         L     R10,CVTGDA         R10->GDA IN SQA
         L     R9,160(R10)        R9->PRIVATE AREA
         LA    R9,0(,R9)          CLEAR XA BIT
         CR    R2,R9              BELOW PRIVATE?
         BL    CALLEROK           YES
         A     R9,164(R10)        R9->END OF PRIVATE AREA
         CR    R2,R9              ABOVE PRIVATE?
         BL    AUTHBEND           NO->KILL HIM
*
         L     R9,168(R10)        R9->PRIVATE AREA EXTENDED
         LA    R9,0(,R9)          CLEAR XA BIT
         CR    R2,R9              BELOW PRIVATE EXTENDED?
         BL    CALLEROK           YES
         A     R9,172(R10)        R9->END OF PRIVATE AREA EXTENDED
         CR    R2,R9              ABOVE PRIVATE EXTENDED?
         BL    AUTHBEND           NO->KILL HIM
         EJECT
*
*        CALLER IS NOT IN PRIVATE AREAS - CHECK FUNCTION REQUEST:
*
CALLEROK DS    0H
         C     R0,=F'1'           1=PASS CMD TO SYSTEM?
         BE    JARED              YUP
         L     R8,180(R4)         R8->JSCB
         LTR   R0,R0              RESET AUTH REQUEST?
         BZ    AUTHOFF            YUP.
         C     R0,=F'-1'          OK?
         BE    AUTHON             YUP->SKIP PREAUTH CHECK
         LR    R2,R0              GET TEST ADDRESS
         L     R0,=F'-1'          DON'T LOOP
         B     XACHK              GO SEE IF OK
*
*   TURN AUTHORIZATION ON:
*
AUTHON   OI    236(R8),X'01'      SET JSCBAUTH BIT TO ON.
         B     RETURN             EXIT
*
*    TURN AUTHORIZATION OFF:
*
AUTHOFF  NI    236(R8),X'FE'      SET JSCBAUTH TO OFF.
*
RETURN   SLR   R15,R15            R15=0
         BR    R14                EXIT SVC
*
*
*
AUTHBEND ABEND X'FEC',,,SYSTEM    GIVE SYSTEM ABEND
         DS    0D
         EJECT
         PRINT     ON,GEN,NODATA
JARED    DS        0H
         L         2,0(1)              POINT AT CHAR VAR STRING
         LH        3,0(2)              GET LENGTH
         LTR       3,3                 ANY PARM
         BNP       AUTHBEND
*
         LR    R8,R1              SAVE R1
         LA    R0,WORKLEN         R0=LENGTH OF DESIRED WORKAREA
         GETMAIN R,LV=(0)
         LR    R12,R1             R12->WORKAREA
         USING WORKAREA,R12
         XC    WORKAREA(WORKLEN),WORKAREA      CLEAR THE AREA
         LR    R1,R8              RESTORE R1
*
         BCTR      3,0
         EX        3,MVC
MVC      MVC       TEXT(0),2(2)        COPY TEXT TO SVC 34 PARM LIST
         LA        3,5(3)              INCREMENT TO TRUE PARM LENGTH
         STC       3,LENGTH            SAVE LENGTH IN SVC 34 PARM LIST
         SPACE     1
         STCTL     4,4,CR4             SAVE PRIMARY ASID
         XC        CR4(2),CR4          CLEAR HIGH TWO BYTES
         OI        CR4+2,X'80'         MAKE ASID INTO TJID
         L         0,CR4               SVC 34 PARAMETER SOURCE PARM
         SPACE     1
         MGCR      COMMAND             QUEUE COMMAND
         SPACE     1
*
         LR    R8,R1              SAVE R1
         LA    R0,WORKLEN         R0=LENGTH OF DESIRED WORKAREA
         LA    R1,WORKAREA
         FREEMAIN R,LV=(0),A=(1)
         LR    R1,R8              RESTORE R1
*
         SVC   3   EXIT                DIRECT TERMINATION
         EJECT
         LTORG
         REGISTER
         SPACE     3
WORKAREA DSECT
CR4      DC        A(0)
COMMAND  DC        X'00'               SVC 34 PARAMETER LIST
LENGTH   DC        AL1(L'TEXT),2X'00'
TEXT     DC        CL100'$PI '
WORKLEN  EQU   *-WORKAREA
         CVT  DSECT=YES
         END  IGC00236
