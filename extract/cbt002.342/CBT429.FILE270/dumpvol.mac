//D0100150 JOB (2100,56-D),'DZEIG 0684 T',CLASS=H,MSGCLASS=T
//X EXEC ASMHCL,LIBRARY='WDPSC.SYSLIBRY',LNDSP=SHR,
//    LIB2='SYS1.AMODGEN',LIB3='WIZRD150.DN00150.FILE'
DUMPVOL  CSECT
         STM   R14,R12,12(R13)            SAVE REGISTERS
         LR    R11,R13
         BALR  R12,0                      LOAD BASE REGISTER
         USING *,R12
         LA    R13,SAVE                   SAVE AREA
         ST    R13,8(0,R11)               AND BACK CHAIN
         ST    R11,4(0,R13)
         XR    R4,R4                      ANALYZE PARAMETER
         L     R3,0(,R1)
         CH    R4,0(,R3)                  IF LENGTH IS ZERO
         BE    NOPARM                         NO PARAMETER
         LA    R5,2(,R3)
         LH    R4,0(,R3)
DSNCHECK EQU   *
         CLC   0(4,R5),=C'DSN='
         BNE   DAYCHECK
         LR    R6,R4
         BCTR  R6,0
         EX    R6,MVCJOB          MVC  JOBLIB(0),0(R5)
         LA    R7,JOBLIB
         LA    R6,52
COMMAR   CLI   0(R7),C','
         BE    COMMA
         LA    R7,1(,R7)
         BCT   R6,COMMAR
         B     UPSCAN
COMMA    MVI   0(R7),C' '
DAYCHECK CLC   0(4,R5),=C'DAY='
         BE    DAYPARM
UPSCAN   EQU   *
         LA    R5,1(,R5)
         BCT   R4,DSNCHECK
         B     NOPARM
         SPACE 4
DAYPARM  EQU   *
         CLC   4(3,R5),=C'SUN'
         BNE   UPSCAN
SETPARM  MVI   JOB1+4,C'5'               DUANE 2/24/84
         MVI   SETUP1+17,C'5'            DUANE 2/24/84
         MVC   TAPDSN0B,VLTWDPSC
         MVC   TAPDSN1B,VLTWDPSC
         MVC   TAPDSN2B,VLTWDPSC
         MVC   TAPDSN3B,VLTWDPSC
         MVC   TAPDSN4B,VLTWDPSC
         MVC   TAPRET1B,=C'14'           DUANE 2/24/84
         MVC   TAPRET2B,=C'14'           DUANE 2/24/84
         MVC   TAPRET3B,=C'14'           DUANE 2/24/84
         MVC   TAPRET4B,=C'14'           DUANE 2/24/84
         B     UPSCAN
NOPARM   EQU   *
         DEVTYPE SERIALS,DEVAREA,DEVTAB    CHECK FOR INPUT DDNAME
         LTR   R15,R15
         BNZ   FINIT                      FINISH IF NOT PRESENT
         OPEN  (INDCB)
         TM    INDCB+48,X'10'             DID OPEN FAIL
         BZ    FINIT                      YES
         LA    R4,VOLTABLE            POINT TO TABLE
         SR    R11,R11                ZERO THE COUNT
         SPACE 3
**********************************************************************
*                                                                    *
*      READ INPUT FILE AND BUILD VOLUME TABLE                        *
READING  GET   INDCB                      GET INPUT FILE
*        CLI   71(R1),C' '
*        BNE   ACQUIRE
*        CLI   6(R1),C' '
*        BE    READING
ACQUIRE  EQU   *
         MVC   0(6,R4),0(R1)         ACQUIRE VOLUME SERIAL NUMBER
         LA    R4,10(,R4)            POINT TO NEXT TABLE ENTRY
         LA    R11,1(,R11)           ADD TO COUNT
         B     READING
*                                                                    *
**********************************************************************
         SPACE 3
EOF      EQU   *
         LTR   R11,R11
         BNP   VOLTABLE+100
         ST    R11,VOLCOUNT
        EJECT
**********************************************************************
*                                                                    *
*    SET UP IOS SCAN OR UCB SCAN                                     *
*  GIVES EACH DASD UCB BACK IN SUCESSION                             *
         L     R4,CVTPTR
         USING CVT,R4
LOOKIT   LA    R1,UCBPARM
         L     R15,CVTUCBSC
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ALLOVER
         L     R6,UCBLOCAT
         USING UCBCMSEG,R6
         SPACE 4
**********************************************************************
*                                                                    *
*     LOOP THROUGH VOLUME TABLE LOOKING FOR MATCH TO UCB             *
         LA    R5,VOLTABLE
CLCCLC   CLC   UCBVOLI,0(R5)
         BE    PUBLIC
         LA    R5,10(,R5)
         CLC   0(2,R5),=X'EEEE'
         BNE   CLCCLC
         B     LOOKIT
PUBLIC   EQU   *
         MVC   6(3,R5),UCBNAME
         B     LOOKIT
* END OF VOLUME TABLE LOOP                                           *
**********************************************************************
* END OF UCB SCAN                                                    *
**********************************************************************
         SPACE 4
**********************************************************************
*                                                                    *
ALLOVER  EQU   *
         L     R11,VOLCOUNT
         L     R10,VOLCOUNT
         LA    R5,VOLTABLE
         ST    R11,VOLCOUNT
* ALLOCATE AND OPEN THE OUTPUT DATA SET
         DEVTYPE INTR,DEVAREA         CHECK FOR DDNAME JCL
         LTR   R15,R15                IS IT PRESENT?
         BNZ   FINIT                  NO
         OPEN  (JCL,OUTPUT)           INTERNAL READER DATA SET
         TM    JCL+48,X'10'         DID IT OPEN
         BZ    FINIT
         PUT   JCL,JOB1
*//W6601150 JOB (2100,56-D),'DZEIG 0684 T',CLASS=C
         CLC   JOBLIB(8),=CL8' '
         BE    SETUPS
         PUT   JCL,JOB3
         SPACE 4
SETUPS   EQU   *
         L     R11,VOLCOUNT
         LA    R5,VOLTABLE
         PUT   JCL,SETUP1
         PUT   JCL,MESSAGE1
MESSAGES EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BE    MESSAGEX
         MVC   VOLSER0A,0(R5)
         PUT   JCL,MESSAGE2
MESSAGEX EQU   *
         LA    R5,10(,R5)
         BCT   R11,MESSAGES
         SPACE 4
         L     R11,VOLCOUNT
         LA    R5,VOLTABLE
EXECUTE  EQU   *
         PUT   JCL,EXEC3
*//*
         AP    STEP##,=P'1'       ADD 1 TO STEP COUNT
         UNPK  STEPNAME,STEP##    UNPACK THE NUMBER
         OI    STEPNAME+4,X'F0'   MAKE NUMBER USEABLE
         PUT   JCL,EXEC1
*//STEPNAME EXEC PGM=FDR,PARM=A     DUMP MULTIPLE VOLUMES
         PUT   JCL,SLPRINT            DUANE 2/24/84
*//SYSPRINT DD SYSOUT=*               DUANE 2/24/84
LEVER1   EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BNE   LEVER2
         LA    R5,10(,R5)
         BCT   R11,LEVER1
         B     SLISTSET
LEVER2   EQU   *
         MVC   VOLSER1,0(R5)
         MVC   VOLSER1A,0(R5)
         PUT   JCL,DISK1
*//DISK1 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=??????
         PUT   JCL,TAPE1
*//TAPE1  DD UNIT=TAPE16,DISP=(,KEEP),DSN=
         PUT   JCL,SYSPRIN1
*//SYSPRIN1 DD SYSOUT=*
         LA    R5,10(,R5)
         BCT   R11,SECONDD
         B     SLISTSET
SECONDD  EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BNE   LEVER3
         LA    R5,10(,R5)
         BCT   R11,SECONDD
         B     SLISTSET
LEVER3   EQU   *
         MVC   VOLSER2,0(R5)
         MVC   VOLSER2A,0(R5)
         PUT   JCL,DISK2
*//DISK2 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=######
         PUT   JCL,TAPE2
*//TAPE2  DD UNIT=TAPE16,DISP=(,KEEP),DSN=
         PUT   JCL,SYSPRIN2
*//SYSPRIN2 DD SYSOUT=*
         LA    R5,10(,R5)
         BCT   R11,THREEDD
         B     SLISTSET
         SPACE 4
THREEDD  EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BNE   LEVER8
         LA    R5,10(,R5)
         BCT   R11,THREEDD
         B     SLISTSET
LEVER8   EQU   *
         MVC   VOLSER3,0(R5)
         MVC   VOLSER3A,0(R5)
         PUT   JCL,DISK3
*//DISK3 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=######
         PUT   JCL,TAPE3
*//TAPE3  DD UNIT=TAPE16,DISP=(,KEEP),DSN=
         PUT   JCL,SYSPRIN3
*//SYSPRIN3 DD SYSOUT=*
         LA    R5,10(,R5)
         BCT   R11,FOURDD
         B     SLISTSET
         SPACE 4
FOURDD   EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BNE   LEVER10
         LA    R5,10(,R5)
         BCT   R11,FOURDD
         B     SLISTSET
LEVER10  EQU   *
         MVC   VOLSER4,0(R5)
         MVC   VOLSER4A,0(R5)
         PUT   JCL,DISK4
*//DISK4 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=######
         PUT   JCL,TAPE4
*//TAPE4  DD UNIT=TAPE16,DISP=(,KEEP),DSN=
         PUT   JCL,SYSPRIN4
*//SYSPRIN4 DD SYSOUT=*
         LA    R5,10(,R5)
         BCT   R11,EXECUTE
         EJECT
SLISTSET EQU   *
         PUT   JCL,EXEC3
*//*
         PUT   JCL,EXEC2
*//SLIST  EXEC PGM=SLIST,PARM='NOXTNT'
         PUT   JCL,SLPRINT            DUANE 2/29/84
*//SYSPRINT DD SYSOUT=*               DUANE 2/29/84
         LA    R5,VOLTABLE
         L     R11,VOLCOUNT
SLISTP   EQU   *
         CLC   6(3,R5),=X'EEEEEE'
         BE    LEVER4
         MVC   SLISTV1,0(R5)
         MVC   SLISTV2,0(R5)
         PUT   JCL,SLISTDD
*//VVOLSER  DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=VOLSER
LEVER4   EQU   *
         LA    R5,10(,R5)
         BCT   R11,SLISTP
*
         CLOSE (INDCB)
         CLOSE (JCL)
FINIT    EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
SAVE     DC    18F'0'
MVCJOB   MVC   JOBLIB(0),0(R5)
BLNKPTR  DC    A(BLNKPARM)
BLNKPARM DC    H'0',C'  '
VLTWDPSC DC    C'VLTWDPSC'
         DS    0D
DEVAREA  DC    6F'0'          AREA FOR DEVTYPE MACRO
SERIALS  DC    C'SERIALS  '
INTR     DC    C'DUMPJCL '
         PRINT NOGEN
INDCB    DCB   DDNAME=SERIALS,DSORG=PS,MACRF=(GL),EODAD=EOF
JCL      DCB   DDNAME=DUMPJCL,DSORG=PS,MACRF=(PM),LRECL=80
UCBPARM  DC    A(IOSSCAN)
         DC    A(DASD20)
         DC    X'80',AL3(UCBLOCAT)
UCBLOCAT DC    A(0)
DASD20   DC    X'20'   DEVICE CLASS FOR DASD
PASSONE  DC    X'00'
         DS    0D
IOSSCAN  DC    25F'0'  100-BYTE UCB SCAN WORK AREA
         SPACE 4
JOB1   DC  CL80'//W6601150 JOB (2100,56-D),''DZEIG 0684 T'',CLASS=C,TIME
               E=30'
JOB3     DC    CL80'//JOBLIB DD DISP=SHR, '
JOBLIB   EQU   JOB3+21,48
EXEC1    DC    CL80'//A12345  EXEC PGM=FDR,PARM=A,REGION=1600K,COND=EVEX
               N         '
STEPNAME EQU   EXEC1+3,5
DISK1    DC    CL80'//DISK1 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER='
VOLSER1  EQU   DISK1+42,6
TAPE1    DC    CL80'//TAPE1 DD UNIT=TAPE16,DISP=(,KEEP),DSN=SOFTBKUP.V1X
               23456,LABEL=RETPD=30'
VOLSER1A EQU   TAPE1+50,6
TAPDSN1B EQU   TAPE1+40,8
TAPRET1B EQU   TAPE1+69,2                DUANE 2/24/84
DISK2    DC    CL80'//DISK2 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER='
VOLSER2  EQU   DISK2+42,6
TAPE2    DC    CL80'//TAPE2 DD UNIT=TAPE16,DISP=(,KEEP),DSN=SOFTBKUP.V1X
               23456,LABEL=RETPD=30'     DUANE 2/24/84
VOLSER2A EQU   TAPE2+50,6
TAPDSN2B EQU   TAPE2+40,8
TAPRET2B EQU   TAPE2+69,2                DUANE 2/24/84
DISK3    DC    CL80'//DISK3 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER='
VOLSER3  EQU   DISK3+42,6
TAPE3    DC    CL80'//TAPE3 DD UNIT=TAPE16,DISP=(,KEEP),DSN=SOFTBKUP.V1X
               23456,LABEL=RETPD=30'      DUANE 2/24/84
VOLSER3A EQU   TAPE3+50,6
TAPDSN3B EQU   TAPE3+40,8
TAPRET3B EQU   TAPE3+69,2                 DUANE 2/24/84
DISK4    DC    CL80'//DISK4 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER='
VOLSER4  EQU   DISK4+42,6
TAPE4    DC    CL80'//TAPE4 DD UNIT=TAPE16,DISP=(,KEEP),DSN=SOFTBKUP.V1X
               23456,LABEL=RETPD=30'      DUANE 2/24/84
VOLSER4A EQU   TAPE4+50,6
TAPDSN4B EQU   TAPE4+40,8
TAPRET4B EQU   TAPE4+69,2                 DUANE 2/24/84
         SPACE 6
EXEC2    DC    CL80'//SLIST EXEC PGM=SLIST,PARM=NOXTNT,REGION=1600K,CONX
               D=EVEN'
EXEC3    DC    CL80'//* '
SLPRINT  DC    CL80'//SYSPRINT DD SYSOUT=*'
SYSPRIN1 DC    CL80'//SYSPRIN1 DD SYSOUT=*'
SYSPRIN2 DC    CL80'//SYSPRIN2 DD SYSOUT=*'
SYSPRIN3 DC    CL80'//SYSPRIN3 DD SYSOUT=*'
SYSPRIN4 DC    CL80'//SYSPRIN4 DD SYSOUT=*'
SLISTDD  DC    CL80'//V123456 DD UNIT=SYSALLDA,DISP=SHR,VOL=SER=123456'
SLISTV1  EQU   SLISTDD+3,6
SLISTV2  EQU   SLISTDD+44,6
SETUP1   DC    CL80'/*SETUP        W6601150,2100-56-D,C880K,04 '
MESSAGE1 DC    CL80'/*MESSAGE I/O CONTROL,2240,06.00,PD,WDPSC '
MESSAGE2 DC    CL80'/*MESSAGE FDR     ,SOFTBKUP.V??????   -      -T-O-4-
               -LIBRARY            '
VOLSER0A EQU MESSAGE2+29,6
TAPDSN0B EQU MESSAGE2+19,8
         SPACE 4
STEP##   DC    PL3'0'
VOLCOUNT DC    F'0'
         LTORG
         DS    0F
VOLTABLE DC    2000X'EEEEEEEE'
         PRINT OFF
         CVT   DSECT=YES
         IEFUCBOB
         REGISTER  ,
         PRINT ON
         END
//LKED.SYSIN DD *
  NAME DUMPVOL(R)
//SYSLIBRY DD DSN=WDPSC.SHARED.LOAD,DISP=SHR
