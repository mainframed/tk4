*          DATA SET 764PASDSCB AT LEVEL 001 AS OF 06/03/80
         SPACE 3
********************************************************************
*        SAVE REGISTERS AND ESTABLISH REGISTER 12 AS BASE REGISTER *
********************************************************************
         SPACE 3
PASSDSCB START
         SAVE  (14,12),,*
         LR    12,15
         USING PASSDSCB,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
********************************************************************
*        INITIALIZATION AND ESTABLISHING DATA AREAS                *
*        CONTAINING OUR PARAMETERS                                 *
********************************************************************
         SPACE 3
STARTIT  DS    0D
         LR    R11,R1                 SAVE REG 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   INDSN,0(R9)            PUT PARAMETER #1 INTO INDSN
         L     R9,4(R11)
         MVC   INVOL,0(R9)            PUT PARAMETER #2 INTO INVOL
         EJECT
         SPACE 3
********************************************************************
*        MAIN LINE LOGIC                                           *
********************************************************************
         SPACE 3
         MVC   VALIDSW(3),YESS        MOVE 'YES' TO VALIDSW
         CLC   INVOL,ALLNINES
         BE    LOKAYT
         MVC   VOLSER,INVOL
         B     OBTANE
LOKAYT   LOCATE CAMLIST1
         SPACE 2
         C     R15,=F'0'              COMPARE REG 15 TO BE = 0
         BE    RC0                    IF REG 15=0, BRANCH TO RC0
         MVC   VALIDSW(3),NOPE        MOVE 'NO ' TO VALIDSW
         MVC   ERRMSG(30),MESS1       MOVE 'DATASET NOT CATALOGED'
*                                       TO ERRMSG
         B     SHUTDOWN               BRANCH TO SHUTDOWN
RC0      CLC   LOCAREA(2),HEX0001     VERIFY THAT THE NUMBER OF
*                                        VOLUMES COUNT IS 1
         BE    MATCH                  IF YES, BRANCH TO MATCH
         MVC   VALIDSW(3),NOPE        MOVE 'NO ' TO VALIDSW
         MVC   ERRMSG(30),MESS2       MOVE 'FILE IS ON MORE THAN
*                                       1 VOLUME' TO ERRMSG
         B     SHUTDOWN               BRANCH TO SHUTDOWN
MATCH    MVC   VOLSER(6),LOCAREA+6
         CLI   LOCAREA+4,X'80'        COMPARE LOCAREA+4 TO BE EQUAL
*                                       TO HEXADECIMAL 80  (THIS
*                                       INDICATES THAT THE DATASET
*                                       IS ON TAPE)
         BNE   OBTANE                 BRANCH TO OBTANE
         MVC   VALIDSW(3),NOPE        MOVE 'NO ' TO VALIDSW
         MVC   ERRMSG(30),MESS6
         SPACE 2
OBTANE   OBTAIN CAMLIST2
         SPACE 2
         C     R15,=F'0'              COMPARE REG 15 TO BE = 0
         BE    RCODE0                 IF REG 15=0, BRANCH TO RCODE0
         C     R15,=F'4'              COMPARE REG 15 TO BE = 4
         BE    RCODE4                 IF REG 15=4, BRANCH TO RCODE4
         C     R15,=F'8'              COMPARE REG 15 TO BE = 8
         BE    RCODE8                 IF REG 15=8, BRANCH TO RCODE8
         MVC   ERRMSG(30),MESS5       MOVE 'OBTAIN MACRO RETURNED
*                                       RC 12/16' TO ERRMSG
         B     MOVENOPE
RCODE4   MVC   ERRMSG(30),MESS3       MOVE 'REQUIRED VOLUME NOT
*                                       MOUNTED' TO ERRMSG
         B     MOVENOPE
RCODE8   MVC   ERRMSG(30),MESS4       MOVE 'FORMAT-1 DSCB NOT FOUND
*                                       ON VOL' TO ERRMSG
MOVENOPE MVC   VALIDSW(3),NOPE        MOVE 'NO ' TO VALIDSW
         B     SHUTDOWN               BRANCH TO SHUTDOWN
RCODE0   B     SHUTDOWN
         EJECT
         SPACE 3
********************************************************************
*        LOAD PARAMETERS 3 - 5 AND BRANCH BACK TO CALLING PROGRAM  *
********************************************************************
         SPACE 3
SHUTDOWN L     R9,8(R11)
         MVC   0(3,R9),VALIDSW        MOVE VALIDSW TO PARAMETER #3
         L     R9,12(R11)
         MVC   0(30,R9),ERRMSG        MOVE ERRMSG TO PARAMETER #4
         L     R9,16(R11)
         MVC   0(140,R9),OBTAREA      MOVE OBTAREA TO PARAMETER #5
         L     R13,4(R13)
         RETURN (14,12)
         EJECT
         SPACE 3
********************************************************************
*        DATA AREAS                                                *
********************************************************************
         SPACE 3
         DS    0F
ALLNINES DC    CL6'999999'
INVOL    DS    CL6
INDSN    DS    CL44
CAMLIST1 CAMLST NAME,INDSN,,LOCAREA
LOCAREA  DS    0D
         DS    265C
         DS    0D
CAMLIST2 CAMLST SEARCH,INDSN,VOLSER,OBTAREA
OBTAREA  DS    0D
         DS    140C
         DS    0D
VALIDSW  DS    CL3
ERRMSG   DS    CL30
VOLSER   DS    CL6
HEX0001  DC    X'0001'
YESS     DC    C'YES'
NOPE     DC    C'NO '
MESS1    DC    C'DATASET NOT CATALOGED         '
MESS2    DC    C'FILE IS ON MORE THAN 1 VOLUME '
MESS3    DC    C'REQUIRED VOLUME NOT MOUNTED   '
MESS4    DC    C'FORMAT-1 DSCB NOT FOUND ON VOL'
MESS5    DC    C'OBTAIN MACRO RETURNED RC 12/16'
MESS6    DC    C'DATASET ON TAPE               '
         END
