CHKTODAY CSECT
*
*        C H K T O D A Y                     (CHKTODAY)
*
*        THIS PROGRAM CHECKS TO SEE IF A FILE HAS BEEN CREATED TODAY.
*        THE FILE NAME IS PASSED TO THE PROGRAM AS A PARM.  IF THE
*        FILE NAME CAN'T BE FOUND A RC OF 8 IS ISSUED.  IF IT IS FOUND,
*        IT COMPARES THE DS1CREDT OF THE FORMAT 1 DSCB TO TODAY'S
*        DATE.  IF THEY ARE EQUAL IT ISSUES A RC 0 TO INDICATE THE FILE
*        WAS CREATED TODAY. IF NOT A RC 4 IS ISSUED.  THE DATE HAS
*        TO BE CONVERTED FROM PACKED DECIMAL (YYDDDF) FORMAT TO
*        A BINARY FORMAT (YY0DDD) USED BY THE VTOC.
*
*                             WRITTEN BY HOWARD GLASTETTER    4-3-86
R0       EQU   0
R1       EQU   1              ADDR OF ADDR OF DSN GIVEN AS A PARM
R2       EQU   2
R3       EQU   3              ADDR OF DSN
R4       EQU   4
R5       EQU   5
R6       EQU   6              EXEC MOVE LENGTH
R7       EQU   7              POINT FOR EXEC MOVE
R8       EQU   8
R9       EQU   9
R10      EQU   10             BINARY CONV REG
R11      EQU   11             BASING
R12      EQU   12             WORK (IN SETTING UP PROGRAM LINKAGE)
R13      EQU   13             LINKAGE POINTER
R14      EQU   14             RETURN REGISTER
R15      EQU   15             RETURN CODE
         PRINT ON,NOGEN
*
BEGIN    SAVE  (14,12)        HOUSEKEEPING
         BALR  11,0
         USING *,11
         ST    R13,REGSAVE+4
         LR    R12,R13
         LA    R13,REGSAVE
         ST    R13,8(R12)     ____________
         EJECT
*                             GET DATASET NAME AND VOLSER
*
         L     R3,0(R1)       SAVE DSN ADDR
         LA    R7,DSNAME
         LH    R6,0(R3)       LENGTH IN R6
         BCT   R6,*+4         REDUCE LENGTH FOR EXEC MOVE
         EX    R6,MOVEDSN     MOVE THE DATASET NAME TO THE FIELD
*
         LOCATE FINDVOL       FIND THE VOLSER OF THE DATASET
         LTR   R15,R15        WAS THE VOLUME FOUND?
         BNZ   NOTATALL       NO, THE FILE DOESN'T EXIST - CC4 ANYWAY
         MVC   VOLNUM,LOCAREA+6  YEP, PLUG IN VOLNUM
*
         OBTAIN DSCBFILE      READ DSCB OF THE DATASET
*
GETDATE  TIME  DEC            TODAY'S DATE WILL RETURN IN R1
         ST    R1,TODAY
*
         MVO   PACKEDYR,TODAY+1(1)             CONVERT YY
         MVC   CONVERT+6(2),PACKEDYR
         CVB   R10,CONVERT
         STH   R10,BINARYYR
*
         MVC   CONVERT+6(2),TODAY+2            CONVERT DDD
         CVB   R10,CONVERT
         STH   R10,BINARYDA
*
         CLC   WORKAREA+9(3),BINARYYR+1       FILE CREATED TODAY?
         BE    SETCC0                          YEP
         MVC   REGSAVE+16(4),=F'4'             NOPE, FORCE RC 4
         B     RETURN
NOTATALL EQU   *
         MVC   REGSAVE+16(4),=F'8'  DATASET NOT FOUND, FORCE RC 8
         B     RETURN
SETCC0   EQU   *
         MVC   REGSAVE+16(4),=F'0'             FORCE RC 0
RETURN   EQU   *
         L     R15,REGSAVE+16                  PLACE RC IN R15
         L     R13,REGSAVE+4                   RETURN HOUSEKEEPING
         L     R14,12(R13)
         LM    R2,R12,28(R13)
         L     R0,20(R13)
         BCR   15,R14
*
MOVEDSN  MVC   0(0,R7),2(R3)                   LENGTH IN R6
         EJECT
*
*           AREA BELOW IS USED BY THE LOCATE AND OBTAIN MACROS
*
DSCBFILE CAMLST SEARCH,DSNAME,VOLNUM,WORKAREA
DSNAME   DC    CL44'                                            '
VOLNUM   DC    CL6'      '
WORKAREA DS    CL140
FINDVOL  CAMLST NAME,DSNAME,,LOCAREA
         DS    0D
LOCAREA  DS    CL265
*
REGSAVE  DS    9D
CONVERT  DC    D'0'
TODAY    DC    F'0'
BINARYYR DC    H'0'
BINARYDA DC    H'0'
PACKEDYR DC    PL2'0'
         END
*
