//WTO00150 JOB (2100,56-D),'LLUEB 0679 T',CLASS=H,MSGCLASS=T
//JOBCAT DD DSN=SYS1.SOFTCAT,DISP=SHR
//       DD DSN=SYS1.USERCAT1,DISP=SHR
//A  EXEC ASMFCL,LIBRARY='WDPSC.PROGLIB',REGION=1000K,
//      LIB3='SYS1.AMODGEN',LIB2='WDPSC.MACLIB',
//      OPTION2='LOAD,MAP'
//*
//*               LL00150.MISC.DATA(WTORCOND)
//*                  WTOR SETS RETURN CODE
//*
//ASM.SYSIN DD *
WTORCOND ENTERNR
*
*   THIS PROGRAM TAKES UP TO 38 CHARACTERS FROM A PARM FIELD AND
*   PLACES THEM IN A WTOR AFTER THE PHRASE 'REPLY Y OR N' TO MAKE
*   A QUESTION THAT OPERATIONS MUST REPLY TO.
*
*   THE REPLY GIVEN IS ANALYZED AND A RETURN CODE IS SET BASED ON IT.
*    0 - REPLY WAS Y   8 - REPLY WAS N    15 - PARAMETER SIZE ERROR
*   THE RETURN CODE CAN THEN BE TESTED IN A SUCCEEDING JOBSTEP.
*
         SPACE 3
*   FIND THE PARAMETER AND CHECK IT FOR LENGTH
         SLR   R3,R3
         L     R2,0(0,R1)              POINT TO SUPPLIED PARM FIELD
         ICM   R3,3,0(R2)              LENGTH OF PARM SUPPLIED
         BZ    BADPARM                  NO PARM
         CH    R3,=H'38'
         BH    BADPARM
         SPACE 3
*   SET UP AND ISSUE THE WTOR
         BCTR  R3,0                    SET UP FOR EX USAGE
         EX    R3,MVCWTOR
WTOR     WTOR  'REPLY Y OR N.                                        ',X
               REPLY,1,ECB1,ROUTCDE=(1,2)
         WAIT  ECB=ECB1 WAIT FOR THE REPLY
         OI    REPLY,X'40'             MAKE REPLY UPPERCASE
         CLI   REPLY,C'Y'              YES
         BE    ZERORC
         CLI   REPLY,C'N'              NO
         BE    EIGHTRC
         SLR   R4,R4                   CLEAR OUT THE ECB
         ST    R4,ECB1
         B     WTOR                    BAD REPLY  TRY AGAIN!
         SPACE 3
*   RETURN WITH PROPER RETURN CODE SET
BADPARM  LA    R15,15
         B     EXIT
EIGHTRC  LA    R15,8
         B     EXIT
ZERORC   SLR   R15,R15
EXIT     LEAVENR
         EJECT
MVCWTOR  MVC   WTOR+31(0),2(R2)    MOVE PARM TO WTOR
REPLY    DC    C' '
ECB1     DC    F'0'
         LTORG
         DC    F'0'
         SPACE 3
         DC    C'WTORCOND  '
         DC    C'L. LUEBCKE   4/28/83   COND CODE SET BY WTOR RESPONSE'
         END
//LKED.SYSLMOD DD DSN=WDPSC.PROGLIB,DISP=SHR
//LKED.SYSLIB DD DSN=WDPSC.PROGLIB,DISP=SHR
//LKED.SYSIN DD *
   NAME WTORCOND(R)
