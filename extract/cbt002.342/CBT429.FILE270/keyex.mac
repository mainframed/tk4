*          DATA SET 761KKKEYEX AT LEVEL 001 AS OF 08/30/82
* WRITTEN BY KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
         REGEQU
         EJECT
KEYEX    CSECT
         SAVE  (14,12),,*
         LR    R12,R15            R12->KEYEX
         USING KEYEX,R12
         LR    R11,R1             R11->KEYSECT
         USING KEYSECT,R11
         LR    R9,R13             R9->OLD SAVE
         L     R13,KEYWP          R13->NEW SAVE
         USING KEYWORK,R13
         ST    R9,SAVE+4
         ST    R13,8(,R9)
         L     R10,BPPLP          R10->BPPL
         USING BPPL,R10
         SLR   R1,R1
         SLR   R4,R4
         L     R9,OPERPTR         R9->OPERAND
         L     R8,OPLENPTR        R8->OPER LEN
         LH    R8,0(,R8)          R8=OPER LEN
         BCTR  R8,R0              R8-1 FOR EX
         EX    R8,KTRT            ANY = PRESENT?
         BZ    KEYCTX             NO -> CALL UNKNEXIT
         BC    11,KEYXX           NO DATA
         LR    R6,R1              R6->'='
         SR    R6,R9              R6=KEYWD LENGTH
         L     R5,KEYOL           R5->LIST OF KEYWORDS
         EJECT
KSL      LA    R5,4(R5)           R5->NEXT ENTRY
         ICM   R4,7,1(R5)         R4->OPER DESCRIPT
         BZ    KEYCTX             NOT FOUND->CALL TRUE EXIT
         TM    2(R4),X'20'        KEYWORD DESC?
         BNO   KSL
         LH    R3,8(,R4)          R3=OPER DESC LEN FLD
         CR    R6,R3              TOO SHORT?
         BH    KSL                YES
         LR    R3,R6
         BCTR  R3,R0              R3-1
         EX    R3,KCLC            NO->COMPARE THEM
         BNE   KSL                NO MATCH->TRY AGAIN
         L     R3,4(,R4)          R3->KEY AREA
         LH    R2,0(,R3)          R2=LENGTH OF AREA
         BCTR  R2,R0              R2-1 FOR EX
         BCTR  R2,R0              R2-1 FOR MVI:
         MVI   4(R3),C' '         A BLANK
         EX    R2,KMVC1           FILL IT
         LA    R8,0(R8,R9)        R8->END OF OPER
         SR    R8,R1              R8=LENGTH OF DATA
         STH   R8,2(,R3)          PASS DATA LEN
         BCTR  R8,R0              R8-1
         EX    R8,KMVC2           MOVE THE DATA
         SLR   R5,R5
         IC    R5,1(,R4)          R5=FLAG BYTE OFFSET
         LA    R5,KEYFLG(R5)      R5->FLAG BYTE
         OC    0(1,R5),0(R4)      SET THE FLAG
         EJECT
KEYXX    SLR   R15,R15            RC=0
KEYX2    L     13,SAVE+4
         RETURN (14,12),RC=(15)
         BR    R14
         SPACE 3
KEYCTX   DS    0H
         LR    R1,R11             R1->PARM LIST
         L     R15,TUX            R15->TRUE EXIT
         LTR   R15,R15            ANY EXIT?
         BZ    KEYXX              NO
         BALR  R14,R15            CALL IT
         B     KEYX2
         SPACE 3
KTRT     TRT   0(0,R9),EQTAB
EQTAB    DC    XL256'00'
         ORG   EQTAB+C'='
         DC    X'04'
         ORG
KCLC     CLC   0(0,R9),10(R4)     MATCH?
KMVC1    MVC   5(0,R3),4(R3)      BLANK FILLER
KMVC2    MVC   4(0,R3),1(R1)      DATA MOVE
         EJECT
KEYWORK  DSECT
SAVE     DS    18F
KEYFLG   DS    F
KEYSECT  DSECT
XP1      DS    F                  UNKN EXIT PARM
BPPLP    DS    F                  PTR TO BPPL
WORKP    DS    F                  PTR TO KMDPARS WORKAREA
*
KEYOL    DS    F                  PTR TO KEYWD OPLIST
KEYWP    DS    F                  PTR TO KEY WORKAREA
TUX      DS    F                  PTR TO TRUE UNKN EXIT
         BPPL  DSECT
         END
