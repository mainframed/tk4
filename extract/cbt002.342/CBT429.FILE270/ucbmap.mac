         TITLE 'UCBMAP - DISPLAY DEVICE CHARACTERISTICS'
*********************************************************************
*  MODIFIED    -    05/04/84 BY KERMIT KISER FOR DYADICS (*KK1)     *
*  MODIFIED    -    08/14/86 BY KERMIT KISER FOR IOSVSUCB(*KK2)     *
*********************************************************************
*  NAME        -    UCBMAP                                          *
*                                                                   *
*  FUNCTION    -    LIST ATTRIBUTES OF A DEVICE                     *
*                                                                   *
*  RESTRICTION -    16 CHANNEL SUPPORT                              *
*                                                                   *
*  AUTHOR      -    C. LYONS                                        *
*                                                                   *
*  DATE        -    01/10/80                                        *
*                                                                   *
*  SYNTAX      -    UCBMAP CUU(XYZ) VOL(VOLSER)                     *
*                                                                   *
*  ATTRIB      -    RENT                                            *
*                                                                   *
*  VERSION     -    1.0                                             *
*                                                                   *
*  ERRORS      -    CHANNEL NUMBER TOO HIGH                         *
*                   INVALID CUU (NOT SYSGENED)                      *
*                   CUU LENGTH INCORRECT                            *
*                   VOLSER NOT FOUND                                *
*                                                                   *
*  PRIVILEGES  -    PROBLEM PROGRAM, V=V                            *
*                                                                   *
*  SYSTEM      -    MVS 3.8 AND MVS/SE2                             *
*                                                                   *
*  INPUT       -    R1 -> CPPL                                      *
*                                                                   *
*  OUTPUT      -            DEVICE MAP FOR CUU                      *
*                                                                   *
*                    STATUS         :  ONLINE                       *
*                                      ALLOCATED                    *
*                                      READY                        *
*                                      NOT RESERVED                 *
*                                      NOT BUSY                     *
*                                      STORAGE                      *
*                                      SHARED                       *
*                                      021 OPEN DATASETS            *
*                                                                   *
*                    UNIT NAMES     :  SYSTSO  SYSDA  3350          *
*                                                                   *
*                    CHANNEL PATHS  :  2 PRI ONLINE  CPU 0          *
*                                      3 SEC OFFLINE                *
*                                                                   *
*                    DEVICE TYPE    :  DIRECT ACCESS  - 3070500D    *
*                                                                   *
*                    VOLSER         :  TSO001                       *
*                                                                   *
*                    UCB ADDRESS    :  0000F214                     *
*                                                                   *
*  MACROS      -     FULLSAVE,FULLRTRN,GETMAIN,FREEMAIN,CALLTSSR,   *
*                    TPUT,PRTLINE,IKJRLSA,IKJPARM,IKJENDP,IKJIDENT  *
*                                                                   *
*  DSECTS      -     WORK,CVT,IOCOM,IOX,IKJCPPL,IKJPPL,IEFUCBOB,    *
*                    JESCT,EDT,CSD,LCH,PDL                          *
*                                                                   *
*  REGISTERS   -     R12 = BASE, R4 -> WORK AREA, R7 -> UCB         *
*                                                                   *
*  LOGIC       -     1. PARSE CUU AND VOL OPERANDS                  *
*                    2. IF CUU PRESENT USE IT, ELSE USE VOL         *
*                    3. SEARCH UCB TABLE, IF VOL BEING USED         *
*                    4. IF CUU BEING USED:                          *
*                       CONVERT CUU TO BINARY FORMAT                *
*                       VALIDATE CUU                                *
*                       GIVEN VALID CUU, FIND UCB ADDRESS           *
*                    5. DISPLAY DEVICE CHARACTERISTICS:             *
*                                                                   *
*                       STATUS     -  DIRECTLY FROM UCB             *
*                       UNIT NAMES -  FROM ELIGIBLE DEVICE TABLE    *
*                       CHAN PATHS -  FROM LCH AND TCH PATH TABLE   *
*                       DEVICE TYPE-  DIRECTLY FROM UCB             *
*                       VOLSER     -  DIRECTLY FROM UCB             *
*                       UCB ADDR   -  FROM UCB LOOKUP TABLE         *
*                                                                   *
*  INSTALL     -     ASSEMBLE UCBMAP                                *
*                    LINK UCBMAP RENT REUS REFR                     *
*                    PLACE UCBMAP IN LINKLIST LIB OR PLPA           *
*                                                                   *
*********************************************************************
         MACRO
&LABEL   PRTLINE
&LABEL   LA    R1,PRTLINE
         LA    R0,60
         TPUT  (1),(0),R
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         MEND
UCBMAP   CSECT
         FULLSAVE RENT=YES
         REGS  R
         LR    R3,R1                   SAVE CPPL ADDR
         USING CPPL,R3
         GETMAIN R,LV=WORKLEN          GET WORK SPACE
         LR    R4,R1
         USING WORK,R4
*
*        BUILD PPL AND INVOKE PARSE
*
         LA    R5,PARSPARM
         USING PPL,R5
         XC    PARSE(PARSELEN),PARSE
         MVC   PPLUPT,CPPLUPT          UPT
         MVC   PPLECT,CPPLECT          ECT
         MVC   PPLCBUF,CPPLCBUF        CIB
         DROP  R3
         LA    R1,PARSECB
         ST    R1,PPLECB               ECB
         LA    R1,PARSANS
         ST    R1,PPLANS               PDL
         L     R1,=A(PCL)
         ST    R1,PPLPCL               PCL
         CALLTSSR EP=IKJPARS,MF=(E,(R5))
         L     R5,PARSANS
         LTR   R5,R5
         BP    PARSEOK
         LA    R1,=CL40'UCBMAP TERMINATED - PARSE ERROR'
         LA    R0,40
         TPUT  (1),(0),R               PRINT ERROR IF NECESSARY
         B     EXIT
         USING PDL,R5
*
*        IF CUU SPECIFIED, THEN USE IT;
*           ELSE IF VOLSER SPECIFIED, THEN USE IT;
*                ELSE ERROR.
*
PARSEOK  TM    CUUFLG,CUUFOUND         WAS CUU SPECIFIED
         BO    USECUU
         TM    VOLFLG,VOLFOUND
         BO    USEVOL
         LA    R1,=CL40'UCBMAP TERMINATED - NO OPERAND'
         LA    R0,40
         TPUT  (1),(0),R
         B     EXIT
USEVOL   L     R6,VOLPTR               A(VOLSER)
         LH    R8,VOLLEN               L'VOLSER
         BCTR  R8,0
         L     R1,CVTPTR               CVT
         USING CVT,R1
*KK2     L     R2,CVTILK2              USB LOOKUP TABLE
         SR    R3,R3                   FOR ALTERNATE PATH COMPARISON
*CBLOOP  LH    R7,0(0,R2)              GET UCB ADDRESS            CG
*KK2     LA    R2,0(0,R2)                                         CG
*KK2 UCBLOOP SLR R7,R7                                            CG
         XC    USCNWORK,USCNWORK                     KK2
         MVI   USCNTYPE,X'00' ALL UCBS               KK2
         MVC   USCNCALL,CVTUCBSC-CVTMAP(R1)          KK2
         DROP  R1
UCBLOOP  L     R15,USCNCALL                          KK2
         CALL  (15),(USCNWORK,USCNTYPE,USCNADDR),VL,MF=(E,USCNPARM) KK2
         LTR   R15,R15            FOUND ANY?         KK2
         BNZ   BADVOL                                KK2
         ICM   R7,15,USCNADDR      GET UCB ADDRESS   KK2
*KK2     ICM   R7,3,0(R2)              GET UCB ADDRESS            CG
*        LTR   R7,R7                                              CG
*KK2     C     R7,=X'00000000'
*KK2     BE    UCBNEXT
*KK2     C     R7,=X'0000FFFF'
*KK2     BE    BADVOL
*        BZ    UCBNEXT                                            CG
*        BM    BADVOL                  IF TABLE DONE, THEN BAD VOLSER
         USING UCBCMSEG,R7
         TM    UCBDVCLS,UCB3TAPE+UCB3DACC    TAPE OR DISK?
         BZ    UCBNEXT
*KK2     CR    R7,R3                   ALTERNATE PATH?
*KK2     BNH   UCBNEXT
*KK2     LR    R3,R7                   FOR NEXT COMPARISON
         TM    CUUFLG,CUUFOUND    CUU?                            KK2
         BNO   CKVOL                                              KK2
         CLC   DEVSAV,13(R7)      CUU MATCH?                      KK2
         BE    VOLHIT             YUP                             KK2
CKVOL    EQU   *                                                  KK2
         EX    R8,COMPVOL
         BE    VOLHIT                  IF OUR VOLUME, THEN EXIT LOOP
         B     UCBLOOP
*KK2 UCBNEXT  LA    R2,2(0,R2)
UCBNEXT  EQU   *                                                  KK2
         B     UCBLOOP
BADVOL   LA    R1,=CL40'UCBMAP TERMINATED - VOLSER NOT FOUND'
         LA    R0,40
         TPUT  (1),(0),R
         B     EXIT
VOLHIT   ST    R7,UCBADDR              SAVE UCB ADDRESS
         B     UCBSCAN
COMPVOL  CLC   0(*-*,R6),UCBVOLI
         DROP  R7
*
*        CONVERT CUU TO BINARY
*
USECUU   L     R1,CUUPTR               A(CUU)
         LH    R2,CUULEN               L'CUU
         DROP  R5
         LA    R15,3                   FOR COMPARISON
         CR    R2,R15
         BE    GOODLEN                 CUU MUST BE 3 CHARS
         LA    R1,=CL40'UCBMAP TERMINATED - CUU NOT 3 CHARS'
         LA    R0,40
         TPUT  (1),(0),R
         B     EXIT
GOODLEN  TRT   0(3,R1),HEXCHK          INSURE VALID HEXADECIMAL
         BZ    HEXOK
         LA    R1,=CL40'UCBMAP TERMINATED - CUU NOT HEX'
         LA    R0,40
         TPUT  (1),(0),R
         B     EXIT
HEXOK    MVC   DEVSAV,0(R1)            SAVE DEVICE ADDR
         B     USEVOL             KK2
         AGO   .KK2
         TR    DEVSAV,TRTAB
         PACK  DEVBIN,DEVSAV           CONVERT CUU TO BINARY
         L     R1,DEVBIN
         SRL   R1,4
         ST    R1,DEVBIN
*
*        GIVEN CUU, FIND UCB ADDRESS
*
         L     R2,CVTPTR               CVT
         USING CVT,R2
         L     R3,CVTIXAVL             IOSCOM
         USING IOCOM,R3
         L     R5,IOCOMEX              IOSCOM EXT
         USING IOX,R5
         L     R6,DEVBIN               CUU
         CLM   R6,2,IOXHICH            CHANNEL # OK?
         BNH   CHANOK
         LA    R1,=CL40'UCBMAP TERMINATED - CUU CHAN # TOO HIGH'
         LA    R0,40
         TPUT  (1),(0),R
         B     EXIT
*
*        DETERMINE IF DEVICE ADDRESS VALID
*
CHANOK   LR    R14,R6                  GET CUU IN EVEN/ODD PAIR
         SRDL  R14,3
         SRL   R15,29
         L     R7,IOCUCBBM             GET A(UCB BIT MAP)
         AR    R7,R14                  GET OFFSET INTO BIT MAP
         LA    R1,X'80'
         SRL   R1,0(R15)               SETUP MASK
         IC    R15,0(R7)               GET BIT MAP BYTE
         NR    R1,R15                  VALID UNIT?
         BNZ   UNITOK
         LA    R1,=CL40'UCBMAP TERMINATED - CUU INVALID'
         LA    R0,40
         TPUT  (1),(0),R               PRINT ERROR, IF NECESSARY
         B     EXIT
*
*        LOCATE UCB ADDRESS USING CVTILK1 & CVTILK2
*
UNITOK   SRDL  R14,5                   ISOLATE CHANNEL AND CU
         SRL   R15,28
         L     R8,CVTILK1
         IC    R1,0(R14,R8)            GET CHANNEL CONSTANT
         ALR   R15,R1
         ALR   R15,R15                 2 * (CHANNEL CONSTANT + CU)
         LA    R7,15
         NR    R7,R6                   ISOLATE DEVICE
         AH    R7,0(R15,R8)
         ALR   R7,R7                   GET LOOKUP TABLE OFFSET
         L     R8,CVTILK2
         LH    R7,0(R7,R8)             GET UCB ADDRESS
         N     R7,=A(X'0000FFFF')      CLEAN IT
         ST    R7,UCBADDR
.KK2     ANOP
*
*        DISPLAY DEVICE STATUS INFORMATION
*
         USING UCBCMSEG,R7
UCBSCAN  TPUT  CLEAR,17,FULLSCR        CLEAR SCREEN
         MVC   PRTLINE,HEADING         PRINT HEADING
         MVC   PRTCUU,UCBNAME
         LA    R1,PRTLINE
         LA    R0,60
         TPUT  (1),(0),R
         L     R1,CONSTANT                      *KK1
         LA    R1,HYPHENS-CBASE(,R1)            *KK1
         LA    R0,60                            *KK1
         TPUT  (1),(0),R                        *KK1
*KK1     TPUT  HYPHENS,60                       *KK1
         L     R15,CONSTANT
         MVC   PRTLINE,STATHEAD-CBASE(R15)
*
*        ONLINE, OFFLINE OR CHANGING STATUS
*
         TM    UCBSTAT,UCBCHGS         DEVICE CHANGING STATUS?
         BO    STATCHNG
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE?
         BO    ONLINE
         MVC   DEVSTAT,=CL30'OFFLINE'
         B     PRTSTAT
STATCHNG MVC   DEVSTAT,=CL30'CHANGING STATUS'
         B     PRTSTAT
ONLINE   MVC   DEVSTAT,=CL30'ONLINE'
PRTSTAT  PRTLINE
*
*        ALLOCATED OR NOT ALLOCATED
*
         TM    UCBSTAT,UCBALOC
         BO    ALLOC
         MVC   DEVSTAT,=CL30'NOT ALLOCATED'
         B     PRTALLOC
ALLOC    MVC   DEVSTAT,=CL30'ALLOCATED'
PRTALLOC PRTLINE
*
*        READY OR NOT READY
*
         TM    UCBFLA,UCBNRY
         BO    NOTREADY
         MVC   DEVSTAT,=CL30'READY'
         B     PRTREADY
NOTREADY MVC   DEVSTAT,=CL30'NOT READY'
PRTREADY PRTLINE
         B     NOCANDO            KK2
*
*        BUSY OR NOT BUSY
*
         TM    UCBFLA,UCBBSY
         BO    BUSY
         MVC   DEVSTAT,=CL30'NOT BUSY'
         B     PRTBUSY
BUSY     MVC   DEVSTAT,=CL30'BUSY'
PRTBUSY  PRTLINE
*
*        FOR DASD DEVICES, DISPLAY RESERVE, MOUNT, USE, SHARE STATUS
*                    AND # OF CURRENT USERS
*
         TM    UCBTBYT3,UCB3DACC       DASD DEVICE?
         BZ    NOTDASD                 NO, OMIT DASD STATUS
         CLI   UCBVOLI,0               ANYTHING MOUNTED?
         BE    NOTDASD                 NO, OMIT DASD STATUS
*
*        RESERVED, RESERVE PENDING, OR NOT RESERVED
*
         TM    UCBFL4,UCBRESVP         RESERVE PENDING
         BO    RESPEND
         CLI   UCBSQC,0                RESERVE COUNT = 0?
         BE    NOTRES
         MVC   DEVSTAT,=CL30'RESERVED'
         B     PRTRES
RESPEND  MVC   DEVSTAT,=CL30'RESERVE PENDING'
         B     PRTRES
NOTRES   MVC   DEVSTAT,=CL30'NOT RESERVED'
PRTRES   PRTLINE
NOCANDO  EQU   *                  KK2
*
*        SHARED OR NOT SHARED
*
         TM    UCBTBYT2,UCBRR        SHARED?      AXC
         BO    SHARED                             AXC
         MVC   DEVSTAT,=CL30'NOT SHARED'
         B     PRTSHARE
SHARED   MVC   DEVSTAT,=CL30'SHARED'
PRTSHARE PRTLINE
*
*        PRIVATE, PUBLIC, OR STORAGE
*
         TM    UCBSTAB,UCBBPRV         PRIVATE
         BO    PRIVATE
         TM    UCBSTAB,UCBPUB          PUBLIC
         BO    PUBLIC
         MVC   DEVSTAT,=CL30'USE=STORAGE'  STORAGE
         B     PRTUSE
PRIVATE  MVC   DEVSTAT,=CL30'USE=PRIVATE'
         B     PRTUSE
PUBLIC   MVC   DEVSTAT,=CL30'USE=PUBLIC'
PRTUSE   PRTLINE
*
*        PERMANENTLY RESIDENT, RESERVED OR REMOVABLE
*
         TM    UCBSTAT,UCBPRES
         BO    PERM
         TM    UCBSTAT,UCBRESV
         BO    RESV
         MVC   DEVSTAT,=CL30'MOUNT=REMOVABLE'
         B     PRTMOUNT
PERM     MVC   DEVSTAT,=CL30'MOUNT=PERMANENTLY RESIDENT'
         B     PRTMOUNT
RESV     MVC   DEVSTAT,=CL30'MOUNT=RESERVED'
PRTMOUNT PRTLINE
*
*        NUMBER OF CURRENT USERS
*
         SR    R1,R1
         IC    R1,UCBUSER              # OF CURRENT USERS
         CVD   R1,DBLWORD
         MVC   MASK,=X'F0212020'
         ED    MASK,DBLWORD+6
         MVC   DEVSTAT(3),MASK+1
         MVC   DEVSTAT+4(26),=CL26'OPEN DATASETS'
         PRTLINE
         DROP  R7
         B     ALSONOGO           KK2
*
*        DISPLAY UNIT NAMES ASSOICATED WITH THIS DEVICE
*
NOTDASD  TPUT  BLANK,1
         L     R15,CONSTANT            FOR ADDRESSABILITY
         MVC   PRTLINE,UNITHEAD-CBASE(R15)  UNITNAME HEADING
         MVC   DEVSTAT,BLANK-CBASE(R15)
         L     R2,CVTPTR               CVT
         USING CVT,R2
         L     R2,CVTJESCT             JESCT
         USING JESCT,R2
         L     R2,JESEDT               EDT
         USING EDTHEAD,R2
         L     R1,LOOKPTR              LOOKUP SECTION
         DROP  R2
         USING LOOKSEC,R1
         L     R3,LOOK#ENT             # UNIT NAME ENTRIES
         L     R2,LOOKLENT             L'ENTRY
         ST    R2,ENTRYLEN             SAVE
         LA    R5,LOOKUNIT             ->UNITNAME
         USING LOOKUNIT,R5
         SR    R0,R0                   SETUP PRINT LINE CONTROL
         STH   R0,NAMECNT
         LA    R15,DEVSTAT
         ST    R15,LINEPTR
UPUT     L     R6,LOOK#GEN             # GENERICS WITHIN UNIT
         L     R7,LOOKGPTR             ->FIRST GENERIC ENTRY
         DROP  R5
         USING GENSEC,R7
UGEN     L     R8,GEN#GRP              # GROUPS WITHIN GENERIC
         L     R9,GENGPTR              ->FIRST GROUP POINTER
         DROP  R7
         USING GPTRSEC,R9
UGRP     L     R10,GPTRGRP             ->GROUP SECTION
         DROP  R9
         USING GRPSEC,R10
         L     R11,GRP#UCB             # UCBS WITHIN GROUP
         L     R14,GRPOUCB             ->FIRST UCB OFFSET
         DROP  R10
UCB      LH    R15,0(0,R14)            UCB OFFSET
         SLL   R15,1                   X 2
         L     R1,CVTPTR               CVT
         USING CVT,R1
         L     R1,CVTILK2              UCB LOOKUP TABLE
         LH    R1,0(R15,R1)            UCB ADDRESS
         USING UCBCMSEG,R1
         L     R2,UCBADDR              OUR UCB
         CLC   UCBNAME,13(R2)          OUR NAME = THIS UCB
         DROP  R1
         BNE   NEXTUCB
         LH    R0,NAMECNT              COUNT OF DISPLAY NAMES
         L     R15,LINEPTR             CURRENT LINE PTR
         C     R0,=F'3'                LINE FILLED?
         BL    SPACEOK
         PRTLINE                       YES, PRINT IT
         SR    R0,R0                   COUNT=0
         LA    R15,DEVSTAT             NEW LINE POINTER
SPACEOK  AH    R0,=H'1'                BUMP COUNT
         STH   R0,NAMECNT              SAVE IT
         MVC   0(8,R15),0(R5)          MOVE UNIT NAME
         LA    R15,10(0,R15)           BUMP LINE POINTER
         ST    R15,LINEPTR             SAVE IT
NEXTUCB  LA    R14,2(0,R14)            NEXT UCB
         BCT   R11,UCB
         LA    R9,4(0,R9)              NEXT GROUP ENTRY
         BCT   R8,UGRP
         LA    R7,12(0,R7)             NEXT GENERIC ENTRY
         BCT   R6,UGEN
         L     R2,ENTRYLEN
         LA    R5,0(R2,R5)             NEXT ENTRY
         BCT   R3,UPUT
         PRTLINE                       PRINT UNFILLED LINE
         TPUT  BLANK,1
*
*        DISPLAY DEVICE CHANNEL PATHS
*
         L     R15,CONSTANT            FOR ADDRESSABILITY
         MVC   PRTLINE,CHANHEAD-CBASE(R15)
         L     R7,UCBADDR              GET UCB ADDRESS
         USING UCBCMSEG,R7
         MVC   DEVSTAT,BLANK-CBASE(R15)      CPU 0 PATHS
         MVC   DEVSTAT+14(5),=C'CPU 0'
         MVC   DEVSTAT+2(3),=C'PRI'
         SR    R1,R1                   TCH PATH INDEX
         TM    UCBCHM,UCBPPA           PRIMARY ONLINE?
         BZ    PRION
         MVC   DEVSTAT+6(7),=C'OFFLINE'
         BAL   R10,GETCHAN#
         B     TRYALT
PRION    MVC   DEVSTAT+6(6),=C'ONLINE'
         BAL   R10,GETCHAN#
TRYALT   TM    UCBFL5,UCBALTPH         ALTERNATE PATH?
         BZ    TRYMP
         MVC   DEVSTAT+2(3),=C'SEC'
         LA    R1,2                    INDEX IN TCH PATH TABLE
         TM    UCBCHM,UCBSPA           SECONDARY ONLINE?
         BZ    SECON
         MVC   DEVSTAT+6(7),=C'OFFLINE'
         BAL   R10,GETCHAN#
         B     TRYMP
SECON    MVC   DEVSTAT+6(6),=C'ONLINE'
         BAL   R10,GETCHAN#
TRYMP    L     R1,CVTPTR               CVT
         USING CVT,R1
         L     R1,CVTCSD               CSD
         USING CSD,R1
         TM    CSDCPUAL,CPU1+X'20'     MP? OR DYADIC - *KK1
         BZ    DEVTYPE                 DEAL WITH CPU 1
         TM    CSDCPUAL,CPU1         MP?               *KK1
         BO    SCPU1                 YES               *KK1
         MVC   DEVSTAT+14(5),=C'CPU 2'                 *KK1
         B     *+10                  YES               *KK1
SCPU1    EQU   *                                       *KK1
         MVC   DEVSTAT+14(5),=C'CPU 1'
         DROP  R1
         SR    R1,R1                   TCH PATH INDEX
         MVC   DEVSTAT+2(3),=C'PRI'    PRIMARY PATH
         TM    UCBCHM,UCBPPB           PRIMARY ONLINE?
         BZ    MPPRION
         MVC   DEVSTAT+6(7),=C'OFFLINE'
         BAL   R10,GETCHAN#
         B     SECMP
MPPRION  MVC   DEVSTAT+6(6),=C'ONLINE'
         BAL   R10,GETCHAN#
SECMP    TM    UCBFL5,UCBALTPH         ALTERNATE PATH?
         BZ    DEVTYPE
         MVC   DEVSTAT+2(3),=C'SEC'
         LA    R1,2                    TCH PATH INDEX
         TM    UCBCHM,UCBSPB           ALTERNATE ONLINE?
         BZ    MPALTON
         MVC   DEVSTAT+6(7),=C'OFFLINE'
         BAL   R10,GETCHAN#
         B     DEVTYPE
MPALTON  MVC   DEVSTAT+6(6),=C'ONLINE'
         BAL   R10,GETCHAN#
         B     DEVTYPE
*
*        GET CHANNEL NUMBER FROM TCH PATH TABLE
*
GETCHAN# L     R5,CVTPTR               CVT
         USING CVT,R5
         L     R5,CVTILCH              LCH
         USING LCH,R5
         SR    R6,R6
         IC    R6,UCBLCI               LCH INDEX
         SLL   R6,5
         LA    R5,0(R6,R5)             LCH ENTRY
         L     R6,LCHTCH               TCH PATH TABLE
         DROP  R5
         LA    R6,0(R1,R6)             TCH PATH TABLE ENTRY
         MVC   DEVSTAT(1),0(R6)
         TR    DEVSTAT(1),HEX          CHANNEL NUMBER
         PRTLINE                       PRINT TO TERMINAL
         BR    R10
ALSONOGO EQU   *                  KK2
*
*        DISPLAY DEVTYPE FIELD
*
DEVTYPE  TPUT  BLANK,1
         L     R15,CONSTANT            FOR ADDRESSABILITY
         MVC   PRTLINE,DEVTHEAD-CBASE(R15)  SETUP HEADING
         TM    UCBDVCLS,UCB3TAPE       DETERMINE DEVICE CLASS
         BNO   TRYCOMM
         MVC   DEVSTAT(17),=CL17'TAPE          -  '
*
*        TRY TO FIND TAPE DENSITY
*
         TM    UCBTBYT2,UCBDUDN2       1600/6250?
         BZ    TRYDN1
         MVC   DEVSTAT+5(9),=C'1600/6250'
         B     TRANTYPE
TRYDN1   TM    UCBTBYT2,UCBDUDN1       800/1600?
         BZ    TRY1600
         MVC   DEVSTAT+6(8),=C'800/1600'
         B     TRANTYPE
TRY1600  TM    UCBTBYT1,UCBD1600       1600?
         BZ    TRY6250
         MVC   DEVSTAT+10(4),=C'1600'
TRY6250  TM    UCBTBYT1,UCBD6250       6250?
         BZ    TRANTYPE
         MVC   DEVSTAT+10(4),=C'6250'
TRYCOMM  TM    UCBDVCLS,UCB3COMM
         BNO   TRYCTC
         MVC   DEVSTAT(17),=CL17'COMMUNICATION -  '
         B     TRANTYPE
TRYCTC   TM    UCBDVCLS,UCB3CTC
         BNO   TRYDASD
         MVC   DEVSTAT(17),=CL17'CTC           -  '
         B     TRANTYPE
TRYDASD  TM    UCBDVCLS,UCB3DACC
         BNO   TRYDISP
         MVC   DEVSTAT(17),=CL17'DASD          -  '
*
*        DETERMINE DASD MODEL
*
         CLI   UCBTBYT4,X'0D'          3330-11?
         BE    MOD11
         CLI   UCBTBYT4,X'0B'          3350?
         BNE   TRANTYPE
         MVC   DEVSTAT+10(4),=C'3350'
         B     TRANTYPE
MOD11    MVC   DEVSTAT+7(7),=C'3330-11'
         B     TRANTYPE
TRYDISP  TM    UCBDVCLS,UCB3DISP
         BNO   TRYUNIT
         MVC   DEVSTAT(17),=CL17'DISPLAY       -  '
         B     TRANTYPE
TRYUNIT  TM    UCBDVCLS,UCB3UREC
         BNO   TRYOCR
         MVC   DEVSTAT(17),=CL17'UNIT RECORD   -  '
         B     TRANTYPE
TRYOCR   TM    UCBDVCLS,UCB3CHAR
         BNO   UNKNOWN
         MVC   DEVSTAT(17),=CL17'CHARACTER RDR -  '
         B     TRANTYPE
UNKNOWN  MVC   DEVSTAT(17),=CL17'UNKNOWN CLASS -  '
TRANTYPE UNPK  DEVSTAT+17(9),UCBTYP(5)
         TR    DEVSTAT+17(9),HEX-C'0'  DISPLAY DEVICE TYPE
         MVI   DEVSTAT+25,C' '
         PRTLINE
         TPUT  BLANK,1
*
*        FOR TAPE AND DASD, DISPLAY VOLUME SERIAL NUMBER
*
         L     R15,CONSTANT
         MVC   PRTLINE,VOLSHEAD-CBASE(R15)
         TM    UCBDVCLS,UCB3TAPE+UCB3DACC
         BZ    DISADDR
         CLI   UCBVOLI,0
         BNE   MOVEVOL
         MVC   DEVSTAT,=CL30'NONE'
         B     PRTVOL
MOVEVOL  MVC   DEVSTAT(6),UCBVOLI
PRTVOL   PRTLINE
         TPUT  BLANK,1
*
*        DISPLAY UCB ADDRESS
*
DISADDR  L     R15,CONSTANT
         MVC   PRTLINE,UCBAHEAD-CBASE(R15)
         UNPK  DEVSTAT(9),UCBADDR(5)
         TR    DEVSTAT(8),HEX-C'0'
         MVI   DEVSTAT+8,C' '
         PRTLINE
         TPUT  BLANK,1
         TITLE 'EXIT CONVENTIONS'
EXIT     IKJRLSA PARSANS               RELEASE PARSE WORK AREA
         FREEMAIN R,LV=WORKLEN,A=(R4)  RELEASE WORK AREA
         FULLRTRN RENT=YES             RETURN
*
*        LITERAL POOL
*
         LTORG
         TITLE 'PARSE PCL, CONSTANTS AND DSECTS'
*
*        PARSE PARAMETER CONTROL LIST
*
PCL      IKJPARM  DSECT=PDL
X        IKJKEYWD
         IKJNAME  'VOL',SUBFLD=VOLFLD
Y        IKJKEYWD
         IKJNAME  'CUU',SUBFLD=CUUFLD
VOLFLD   IKJSUBF
VOL      IKJIDENT 'VOLSER',FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6
CUUFLD   IKJSUBF
CUU      IKJIDENT 'DEVICE ADDRESS',FIRST=NONATNUM,OTHER=NONATNUM,      $
               MAXLNTH=3
         IKJENDP
*
*        CONSTANTS
*
CLEAR    DC   X'27F540115D7E1140403C40400011404013'
HEX      DC   C'0123456789ABCDEF'
HEXCHK   DC   256X'FF'
         ORG  HEXCHK+C'A'
         DC   X'000000000000'          ABCEDF ARE OK
         ORG  HEXCHK+C'0'
         DC   X'00000000000000000000'  0123456789 ARE OK
         ORG
TRTAB    DC   (C'A')X'0',X'FAFBFCFDFEFF',41X'0',C'0123456789',6X'0'
*
CONSTANT DC   A(HEADING)               BASE LOCATOR FOR CONSTANTS
*
CBASE    EQU  *
HEADING  DC   CL60'          DEVICE MAP FOR XXX'
BLANK    DC   CL30' '
HYPHENS  DC   CL60'          ------ --- --- ---'
VOLSHEAD DC   CL60'VOLUME SERIAL     -  '
UCBAHEAD DC   CL60'UCB ADDRESS       -  '
STATHEAD DC   CL60'STATUS            -  '
UNITHEAD DC   CL60'UNIT NAMES        -  '
CHANHEAD DC   CL60'CHANNEL PATHS     -  '
DEVTHEAD DC   CL60'DEVICE TYPE       -  '
*
*        DSECTS
*
WORK     DSECT                         WORK AREA
PARSE    EQU   *
PARSPARM DS    7A                      PPL
PARSECB  DS    F                       PARSE ECB
PARSANS  DS    A                       PDL POINTER
PARSELEN EQU   *-PARSE
DBLWORD  DS    D                       WORK
ENTRYLEN DS    F                       L' UNIT NAME ENTRY
LINEPTR  DS    A                       PRINT LINE CONTROL
DEVBIN   DS    F                       CUU IN BINARY
UCBADDR  DS    F                       OUR UCB ADDR
NAMECNT  DS    H                       PRINT LINE CONTROL
DEVSAV   DS    CL3                     CUU IN EBCDIC
MASK     DS    XL4                     EDIT MASK
PRTLINE  DS    CL60                    PRINT LINE
         ORG   PRTLINE+22
DEVSTAT  DS    CL30
         ORG   PRTLINE+25
PRTCUU   DS    CL3
         ORG
         DS    0D                 KK2
USCNWORK DS    XL100              KK2
USCNTYPE DS    X                  KK2
USCNADDR DS    A                  KK2
USCNCALL DS    A                  KK2
USCNPARM DS    3A                 KK2
WORKLEN  EQU   *-WORK
*
EDT      DSECT                         EDT
EDTHEAD  EQU    *
LOOKPTR  DS     A                      ->LOOKUP SECTION
         ORG    EDT
LOOKSEC  EQU    *                      LOOKUP SECTION
LOOK#ENT DS     F                      # ENTRIES
LOOKLENT DS     F                      L'ENTRY
LOOKUNIT DS     CL8                    UNIT NAME
LOOKVAL  DS     F                      LOOKUP VALUE
LOOKMPTR DS     A                      ->GROUP MASK ENTRY
LOOK#GEN DS     F                      # GENERICS
LOOKGPTR DS     A                      ->GENERIC SECTION
         ORG    EDT
GENSEC   EQU    *                      GENERIC SECTION
GENDEV   DS     XL4                    GENERIC DEVICE TYPE
GEN#GRP  DS     F                      # GROUPS
GENGPTR  DS     A                      ->FIRST GROUP POINTER
         ORG    EDT
GPTRSEC  EQU    *                      GROUP POINTER SECTION
GPTRGRP  DS     A                      ->FIRST GROUP ENTRY
         ORG    EDT
GRPSEC   EQU    *                      GROUP SECTION ENTRY
GRPID    DS     F                      ID
GRP#UCB  DS     F                      # UCBS IN THIS GROUP
GRPOUCB  DS     A                      ->FIRST UCB ENTRY
*
PDL      DSECT
         ORG   CUU                     PDL CONTINUATION
CUUPTR   DS    A                       ->CUU
CUULEN   DS    H                       L'CUU
CUUFLG   DS    X
CUUFOUND EQU   X'80'
         ORG   VOL
VOLPTR   DS    A                       ->VOL SER
VOLLEN   DS    H                       L'VOLSER
VOLFLG   DS    X
VOLFOUND EQU   X'80'
*
LCH      DSECT
         ORG   LCH+16                  LOGICAL CHANNEL TABLE ENTRY
LCHTCH   DS    A                       TCH PATH TABLE ADDRESS
*
CSD      DSECT
         ORG   CSD+8                   COMMON SYSTEM DATA AREA
CSDCPUAL DS    XL2                     CPUS AVAILABLE
CPU1     EQU   X'40'                   MP INDICATION
*
*        PRINT NOGEN
         CVT   DSECT=YES,PREFIX=NO     CVT
         IECDIOCM                      IOSCOM
         IECDIOCX                      IOSCOM EXT
         IKJCPPL                       CPPL
         IKJPPL                        PPL
         IEFUCBOB                      UCB
         IEFJESCT                      JESCT
         END
