*          DATA SET 761KKNWAIT AT LEVEL 002 AS OF 08/30/82
*
* WRITTEN BY KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
NEWWAIT  ENTERR
         L     R11,0(R1)          R11->PARM FIELD
         LH    R10,0(R11)         R10= PARM LENGTH
         LA    R11,1(R11)         R11=R11+1
         SLR   R9,R9              R9=0
         SLR   R8,R8              R8=0
LOOP1    LA    R11,1(R11)         R11=R11+1
         CLI   0(R11),C' '        SPACE?
         BNE   DECODE             NO->ASSUME DATA
         BCT   R10,LOOP1          YES
         B     NOVAL
         EJECT
DECODE   CLI   0(R11),C' '        SPACE?
         BE    MBY100             YES
         CLI   0(R11),C'.'        PERIOD?
         BE    SKIPSET            YES
         SLR   R2,R2              R2=0
         TRT   0(1,R11),TABLE     GET VALUE
         MH    R9,=H'10'          R9=R9*10
         AR    R9,R2              R9=R9+R2
         CH    R8,=H'1'           R8=1?
         BH    DEDONE             HIGH=DONE
         BE    LASTDIG            EQUAL->LAST DIGIT NEXT
SKIPPED  LA    R11,1(R11)         R11=R11+1
         BCT   R10,DECODE         R10=R10-1 >0 -> LOOP FOR NEXT DIGIT
         B     MBY100             ASSUME DONE
*
SKIPSET  LTR   R8,R8              R8=0?
         BNZ   ERRP               NO->OOPS
         LA    R8,1               R8=1
         B     SKIPPED
LASTDIG  LA    R8,2               R8=2
         B     SKIPPED
ERRP     EQU   *
MBY100   CH    R8,=H'2'           TENTH DIGIT PRESENT ONLY?
         BE    MBY10              YES
         MH    R9,=H'10'          R9=R9*10
MBY10    MH    R9,=H'10'          R9=R9*10
*
DEDONE   ST    R9,BINTVL
         EJECT
*
         MVC   POSTME(POSTLEN),POSTMAP
*
         STIMER REAL,POSTME,BINTVL=BINTVL
*
         WAIT  ECB=POSTME+MYECB-POSTMAP
*
         B     EXIT
         SPACE 3
         DS    0F
POSTMAP  STM   14,12,12(R13)
         POST  MYECB-POSTMAP(,R15)
         LM    14,12,12(R13)
         BR    R14
MYECB    DC    F'0'
POSTLEN  EQU   *-POSTMAP
         EJECT
NOVAL    EQU   *
EXIT     SLR   R15,R15            R15=0
         LEAVER
*
TABLE    DC    256X'00'
         ORG   TABLE+X'F0'
         DC    X'00010203040506070809'
         ORG
         LTORG
*
*   WORKING STORAGE AREA - DYNAMIC
*
WORKAREA DSECT
SAVEAREA DS    18F
POSTME   DS    XL(POSTLEN)
BINTVL   DS    F
WORKLEN  EQU   *-WORKAREA
         END
