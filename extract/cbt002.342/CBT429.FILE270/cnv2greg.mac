       ID DIVISION.
       PROGRAM-ID.  CNV2GREG.
      *****************************************************************
      *  THIS IS A SUBROUTINE WHICH WILL RETURN A FORMATTED           *
      *  GREGORIAN DATE GIVEN A STANDARD DATE, JULIAN DATE OR         *
      *  SERIAL DATE                                                  *
      *                                                               *
      *  HOW TO USE THIS SUBROUTINE                                   *
      *  ..........................                                   *
      *                                                               *
      *     1.  LOAD TYPE-OF-INPUT-DATE WITH 'STA', 'JUL', OR         *
      *         'SER' DEPENDING UPON WHETHER THE DATE TO BE           *
      *         CONVERTED IS STANDARD (MMDDYY), JULIAN, OR            *
      *         SERIAL                                                *
      *                                                               *
      *     2.  MOVE YOUR DATE TO BE CONVERTED TO INPUT-DATE          *
      *         (INPUT-DATE IS 6 BYTES IN LENGTH.  IF YOUR            *
      *         DATE TO BE CONVERTED IS JULIAN OR SERIAL, LEFT        *
      *         JUSTIFY IT IN INPUT-DATE)                             *
      *                                                               *
      *     3.  CALL 'CNV2GREG' USING CNV2GREG-I-O-AREA.              *
      *                                                               *
      *     4.  IF VALID-INPUT-DATE IS EQUAL TO 'YES'                 *
      *            THEN YOUR DATE WAS VALID AND THE FORMATTED         *
      *            DATE IS IN FORMATTED-DATE                          *
      *         ELSE                                                  *
      *            YOUR DATE TO BE CONVERTED IS INVALID.              *
      *                                                               *
      *   01  CNV2GREG-I-O-AREA.                                      *
      *       05  INPUT-FIELDS.                                       *
      *           10  TYPE-OF-INPUT-DATE            PIC X(03).        *
      *           10  INPUT-DATE                    PIC X(06).        *
      *       05  OUTPUT-FIELDS.                                      *
      *           10  VALID-INPUT-DATE              PIC X(03).        *
      *           10  FORMATTED-DATE                PIC X(18).        *
      *                                                               *
      *  FORMAT OF FORMATTED-DATE                                     *
      *  ........................                                     *
      *                                                               *
      *        18 BYTE FIELD = MMMMMMMMM DD, 19YY                     *
      *                                                               *
      *           WHERE                                               *
      *                                                               *
      *                 MMMMMMMMM IS 'JANUARY  '                      *
      *                              'FEBRUARY '                      *
      *                              'MARCH    '                      *
      *                              ETC                              *
      *                                                               *
      *                 DD        IS DAY OF MONTH (ZERO SUPPRESSED)   *
      *                                                               *
      *                 YY        IS YEAR                             *
      *****************************************************************
           EJECT
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.

       77  MONTH-NOW-IN-ALPHA                    PIC X(9).

       01  BREAKDOWN-STANDARD-DATE.
           05  MONTH                             PIC X(02).
           05  DAYY                              PIC 9(02).
           05  YEAR                              PIC X(02).

       01  WORK-INPUT-DATE.
           05  BYTES-1-5                         PIC X(05).
           05  FILLER                            PIC X(01).

       01  WORK-FORMATTED-DATE.
           05  WORK-ALPHA-MONTH                  PIC X(09).
           05  FILLER            VALUE SPACE     PIC X(01).
           05  WORK-DAY-OF-MONTH                 PIC Z9.
           05  FILLER            VALUE ', '      PIC X(02).
           05  FILLER            VALUE '19'      PIC X(02).
           05  WORK-YEAR                         PIC X(02).

           EJECT
      *
      *****************************************************************
      *                                                               *
      *         J T O S C O N V   L I N K A G E   A R E A             *
      *                                                               *
      *****************************************************************
      *
       01  JTOSCONV-I-O-AREA.
           05  FILLER                        PIC X(08).
           05  JTOSCONV-STANDARD-DATE        PIC X(06).
           05  JTOSCONV-JULIAN-DATE          PIC X(05).
           05  JTOSCONV-BAD-DATE             PIC X(03).

           EJECT
      *
      *****************************************************************
      *                                                               *
      *         S T O J C O N V   L I N K A G E   A R E A             *
      *                                                               *
      *****************************************************************
      *
       01  STOJCONV-I-O-AREA.
           05  FILLER                        PIC X(08).
           05  STOJCONV-STANDARD-DATE        PIC X(06).
           05  STOJCONV-JULIAN-DATE          PIC X(05).
           05  STOJCONV-SERIAL-DATE          PIC X(05).
           05  STOJCONV-BAD-DATE             PIC X(03).

           EJECT
      *
      *****************************************************************
      *                                                               *
      *         S E R L C O N V   L I N K A G E   A R E A             *
      *                                                               *
      *****************************************************************
      *
       01  SERLCONV-I-O-AREA.
           05  FILLER                        PIC X(08).
           05  SERLCONV-SERIAL-DATE          PIC X(05).
           05  SERLCONV-STANDARD-DATE        PIC X(06).
           05  SERLCONV-VALID-INPUT          PIC X(03).
           EJECT
       LINKAGE SECTION.

       01  CNV2GREG-I-O-AREA.
           05  INPUT-FIELDS.
               10  TYPE-OF-INPUT-DATE            PIC X(03).
               10  INPUT-DATE                    PIC X(06).
           05  OUTPUT-FIELDS.
               10  VALID-INPUT-DATE              PIC X(03).
               10  FORMATTED-DATE                PIC X(18).
           EJECT
       PROCEDURE DIVISION USING CNV2GREG-I-O-AREA.

           MOVE SPACES TO FORMATTED-DATE OF CNV2GREG-I-O-AREA.

      *         START CASE STATEMENT

                IF TYPE-OF-INPUT-DATE OF CNV2GREG-I-O-AREA = 'STA'
                       PERFORM 1000-PROCESS-STA-DATE
                          THRU 1999-EXIT-STA-DATE
           ELSE
                IF TYPE-OF-INPUT-DATE OF CNV2GREG-I-O-AREA = 'JUL'
                       PERFORM 2000-PROCESS-JUL-DATE
                          THRU 2999-EXIT-JUL-DATE
           ELSE
                IF TYPE-OF-INPUT-DATE OF CNV2GREG-I-O-AREA = 'SER'
                       PERFORM 3000-PROCESS-SER-DATE
                          THRU 3999-EXIT-SER-DATE
           ELSE
                MOVE 'NO ' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA.

      *         END CASE STATEMENT

           GO TO 9999-GOBACK.
           EJECT
       1000-PROCESS-STA-DATE.

           MOVE INPUT-DATE OF CNV2GREG-I-O-AREA
             TO STOJCONV-STANDARD-DATE.

           CALL 'STOJCONV' USING STOJCONV-I-O-AREA.

           IF STOJCONV-BAD-DATE = 'YES'
                MOVE 'NO ' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA
                GO TO 1999-EXIT-STA-DATE.

           MOVE INPUT-DATE OF CNV2GREG-I-O-AREA
             TO BREAKDOWN-STANDARD-DATE.

           PERFORM 4000-CONVERT-MONTH-TO-ALPHA
              THRU 4999-EXIT-CONVERSION.

           MOVE MONTH-NOW-IN-ALPHA
             TO WORK-ALPHA-MONTH.
           MOVE DAYY OF BREAKDOWN-STANDARD-DATE
             TO WORK-DAY-OF-MONTH.
           MOVE YEAR OF BREAKDOWN-STANDARD-DATE
             TO WORK-YEAR.

           MOVE WORK-FORMATTED-DATE
             TO FORMATTED-DATE OF CNV2GREG-I-O-AREA.

           MOVE 'YES' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA.

       1999-EXIT-STA-DATE.
           EXIT.
           EJECT
       2000-PROCESS-JUL-DATE.

           MOVE INPUT-DATE OF CNV2GREG-I-O-AREA
             TO WORK-INPUT-DATE.

           MOVE BYTES-1-5 OF WORK-INPUT-DATE
             TO JTOSCONV-JULIAN-DATE.

           CALL 'JTOSCONV' USING JTOSCONV-I-O-AREA.

           IF JTOSCONV-BAD-DATE = 'YES'
                MOVE 'NO ' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA
                GO TO 2999-EXIT-JUL-DATE.

           MOVE JTOSCONV-STANDARD-DATE
             TO BREAKDOWN-STANDARD-DATE.

           PERFORM 4000-CONVERT-MONTH-TO-ALPHA
              THRU 4999-EXIT-CONVERSION.

           MOVE MONTH-NOW-IN-ALPHA
             TO WORK-ALPHA-MONTH.
           MOVE DAYY OF BREAKDOWN-STANDARD-DATE
             TO WORK-DAY-OF-MONTH.
           MOVE YEAR OF BREAKDOWN-STANDARD-DATE
             TO WORK-YEAR.

           MOVE WORK-FORMATTED-DATE
             TO FORMATTED-DATE OF CNV2GREG-I-O-AREA.

           MOVE 'YES' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA.

       2999-EXIT-JUL-DATE.
           EXIT.
           EJECT
       3000-PROCESS-SER-DATE.

           MOVE INPUT-DATE OF CNV2GREG-I-O-AREA
             TO WORK-INPUT-DATE.

           MOVE BYTES-1-5 OF WORK-INPUT-DATE
             TO SERLCONV-SERIAL-DATE.

           CALL 'SERLCONV' USING SERLCONV-I-O-AREA.

           IF SERLCONV-VALID-INPUT = 'YES'
                NEXT SENTENCE
           ELSE
                MOVE 'NO ' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA
                GO TO 3999-EXIT-SER-DATE.

           MOVE SERLCONV-STANDARD-DATE
             TO BREAKDOWN-STANDARD-DATE.

           PERFORM 4000-CONVERT-MONTH-TO-ALPHA
              THRU 4999-EXIT-CONVERSION.

           MOVE MONTH-NOW-IN-ALPHA
             TO WORK-ALPHA-MONTH.
           MOVE DAYY OF BREAKDOWN-STANDARD-DATE
             TO WORK-DAY-OF-MONTH.
           MOVE YEAR OF BREAKDOWN-STANDARD-DATE
             TO WORK-YEAR.

           MOVE WORK-FORMATTED-DATE
             TO FORMATTED-DATE OF CNV2GREG-I-O-AREA.

           MOVE 'YES' TO VALID-INPUT-DATE OF CNV2GREG-I-O-AREA.

       3999-EXIT-SER-DATE.
           EXIT.
           EJECT
       4000-CONVERT-MONTH-TO-ALPHA.

      *       START CASE STATEMENT

              IF MONTH OF BREAKDOWN-STANDARD-DATE = '01'
                    MOVE 'JANUARY  ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '02'
                    MOVE 'FEBRUARY ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '03'
                    MOVE 'MARCH    ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '04'
                    MOVE 'APRIL    ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '05'
                    MOVE 'MAY      ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '06'
                    MOVE 'JUNE     ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '07'
                    MOVE 'JULY     ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '08'
                    MOVE 'AUGUST   ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '09'
                    MOVE 'SEPTEMBER' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '10'
                    MOVE 'OCTOBER  ' TO MONTH-NOW-IN-ALPHA
           ELSE
              IF MONTH OF BREAKDOWN-STANDARD-DATE = '11'
                    MOVE 'NOVEMBER ' TO MONTH-NOW-IN-ALPHA
           ELSE
              MOVE 'DECEMBER ' TO MONTH-NOW-IN-ALPHA.

      *       END CASE STATEMENT

       4999-EXIT-CONVERSION.
           EXIT.
           EJECT
       9999-GOBACK.

           GOBACK.
