*          DATA SET 764NEXTGEN AT LEVEL 004 AS OF 10/23/81
********************
*                  *
*  N E X T G E N   *
*                  *
********************
*
*
*  --------------------------------------------------------------------
*  COMMAND     OPERANDS
*  --------------------------------------------------------------------
*  NEXTGEN     DSN('DSNAME')
*  --------------------------------------------------------------------
*
*  OPERANDS -
*
*    REQUIRED -
*
*        DSN('DSNAME')         'DSNAME' IS THE DSNAME OF THE GENERATION
*                              DATA GROUP (BASE NAME ONLY)
*
*  EXPLANATION -
*
*    THIS COMMAND WILL RETURN TWO CLIST VARIABLES &CURGEN AND &NXTGEN
*    WHERE &CURGEN CONTAINS THE ABSOLUTE GENERATION NUMBER OF THE +0
*    GENERATION AND &NXTGEN CONTAINS THE ABSOLUTE GENERATION NUMBER
*    OF THE +1 GENERATION
*
*
*  EXAMPLE -
*
*    SUPPOSE THAT AGNCY999.MY.GDG(+0) IS AGNCY999.MY.GDG.G0497V00
*
*    THEN IF ONE WOULD ENTER THE FOLLOWING COMMAND:
*
*            NEXTGEN DSN('AGNCY999.MY.GDG')
*
*    THIS COMMAND WOULD RETURN THE FOLLOWING IN THE TWO CLIST VARIABLES
*
*            CURGEN = G0497V00
*
*            NXTGEN = G0498V00
*
*            R E T U R N   C O D E S
*            -----------------------
*
*
*        &LASTCC         MEANING
*        -------         --------------------------
*
*          0             SUCCESSFUL COMPLETION
*          4             PARSE ERROR
*          8             MISSING DSNAME PARAMETER
*          12            GDG BASE NOT FOUND
         EJECT
*
*        REGISTER       ASSIGNMENT
*        --------       -------------------------------------------
*
*           3           NOT USED
*           4           HOLDS THE RETURN CODE
*           5           NOT USED
*           6           WORK REGISTER
*           7           WORK REGISTER
*           8           WORK REGISTER
*           9           NOT USED
*           10          WORK REGISTER
*           11          SAVE PARM POINTER
*           12          BASE REGISTER
*           13          ADDRESS OF SAVE AREA
*           14          RETURN ADDRESS
*           15          ENTRY POINT ADDRESS
         EJECT
NEXTGEN  ENTERR
         EJECT
********************************************************************
*                                                                  *
*      M A I N   L I N E   L O G I C   F O R   N E X T G E N       *
*                                                                  *
********************************************************************
*
         LR    R11,R1              SAVE THE PARM POINTER
         LA    R4,0                R4 ==> 0
         MVC   CURGEN,=CL8' '      MOVE SPACES TO CURGEN
         MVC   NXTGEN,=CL8' '      MOVE SPACES TO NXTGEN
         BAL   R14,DOPARSE         PERFORM THE PARSE SERVICE ROUTINE
         CLC   PROCESSW,=C'YES'    WAS PARSE SUCCESFUL ?
         BNE   INITL               IF NOT, BRANCH TO INITL
         BAL   R14,LOADFLD         PERFORM ROUTINE TO LOAD THE PARSED
*                                    FIELD INTO DATA FIELD GDGNAME
         CLC   PROCESSW,=C'YES'    IS EVERTHING STILL HONKEY DOREY ?
         BNE   INITL               IF NOT, BRANCH TO INITL
         BAL   R14,MAJORTN         PERFORM ROUTINE TO DO THE LOCATE
*                                    AND THEN GENERATE THE VALUES OF
*                                    CLIST VARS &CURGEN AND &NXTGEN
INITL    BAL   R14,INITLIST        BUILD PARMLIST FOR IKJUPDT
         BAL   R14,SETCURGN        SET CLIST VARIABLE &CURGEN
         BAL   R14,SETNXTGN        SET CLIST VARIABLE &NXTGEN
         B     SHUTDOWN            BRANCH TO SHUTDOWN
         EJECT
********************************************************************
*                                                                  *
*        P A R S E   I N P U T   C O M M A N D   B U F F E R       *
*                                                                  *
********************************************************************
*
DOPARSE  ST    R14,SAVEIT1          SAVE REG 14 CONTENTS IN SAVEIT1
         KKPARSE
         LTR   R15,R15              RC = 0 ? (I.E PARSE GO OK ?)
         BNZ   PARSERR              IF NOT, BRANCH TO PARSERR
         MVC   PROCESSW,=C'YES'     MOVE 'YES' TO PROCESSW
         L     R10,ANS
         USING IKJPARMD,R10
         B     EXITPARS
PARSERR  MVC   PROCESSW,=C'NO '     MOVE 'NO ' TO PROCESSW
         LA    R4,4                 SET REG 4 = 4
EXITPARS L     R14,SAVEIT1
         BR    14
         EJECT
********************************************************************
*                                                                  *
*        THIS ROUTINE PROCESSES THE PARSED FIELD, AND THEN         *
*        LOADS THE DATA FIELD GDGNAME                              *
*                                                                  *
********************************************************************
*
LOADFLD  ST    R14,SAVEIT2             SAVE REG 14 CONTENTS IN SAVEIT2
         MVC   PROCESSW,=CL3'YES'      MOVE 'YES' TO PROCESSW
         TM    DSNSTRNG+6,X'80'        IS DSNAME PRESENT ?
         BNO   NODSNAME                IF NOT, BRANCH TO NODSNAME
         MVC   GDGNAME,=CL44' '        MOVE SPACES TO GDGNAME
         LA    R1,GDGNAME              R1 ==> ADDRESS OF GDGNAME
         L     R8,DSNSTRNG             R8 ==> ADDRESS OF THE DSNAME
         LH    R7,DSNSTRNG+4           R7 ==> LENGTH OF THE DSNAME
         S     R7,=F'1'                SUBTRACT 1 FOR EX
         SPACE 1
MOVEDSN  MVC   0(0,R1),0(R8)           MOVE THE DSNAME TO GDGNAME
         EX    R7,MOVEDSN
         SPACE 1
         MVC   INDSNLEN(2),DSNSTRNG+4  INDSNLEN ==> LENGTH OF DSNAME
         B     EXITLOAD                BRANCH TO EXITLOAD
NODSNAME MVC   PROCESSW,=CL3'NO '      MOVE 'NO ' TO PROCESSW
         LA    R4,8                    SET REG 4 = 8
EXITLOAD L     R14,SAVEIT2
         BR    R14
         EJECT
*
********************************************************************
*                                                                  *
*     THIS ROUTINE IS RESPONSIBLE FOR DOING THE LOCATE AND THEN    *
*     PARSING THE DSNAME IN ORDER TO GENERATE THE CLIST VARIABLES  *
*     &CURGEN AND NXTGEN                                           *
*                                                                  *
********************************************************************
*
MAJORTN  ST    R14,SAVEIT3            SAVE REG 14 IN SAVEIT3
         BAL   R14,ADDSUFFX           PERFORM ROUTINE TO SUFFIX THE
*                                     DSNAME WITH (+0)
         MVC   CAMLIST1,PATCAML1
         LA    R1,GDGNAME
         ST    R1,CAMLIST1+4
         LA    R1,LOCAREA
         ST    R1,CAMLIST1+12
         LOCATE CAMLIST1
         SPACE 2
         C     R15,=F'0'              COMPARE REG 15 TO BE = 0
         BE    RC0                    IF REG 15=0, BRANCH TO RC0
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
         LA    R4,12                  R4==> 12
         B     EXITMAJR               BRANCH TO EXITMAJR
RC0      LA    R6,GDGNAME             R6==> ADDRESS OF GDGNAME
         A     R6,=F'43'              R6==> ADDRESS OF LOW ORDER OF
*                                         GDGNAME
ABSLOOP  CLI   0(R6),C' '             HAVE WE FOUND A SIGNIFICANT CHAR?
         BNE   FOUND                  IF SO, BRANCH TO FOUND
         S     R6,=F'1'               DECREMENT R6 BY 1
         B     ABSLOOP                BRANCH TO ABSLOOP
FOUND    S     R6,=F'7'               SET R6 TO START OF ABS GEN NO
         MVC   CURGEN(8),0(R6)        MOVE GXXXXV00 TO CURGEN
         BAL   R14,ADDONE             PERFORM ROUTINE TO ADD 1 TO XXXX
EXITMAJR L     R14,SAVEIT3
         BR    R14
         EJECT
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR ADDING 1 TO THE           *
*        GENERATION NUMBER FOUND IN DATA FIELD CURGEN AND          *
*        STORING THE RESULT IN NXTGEN                              *
*                                                                  *
*        E.G.                                                      *
*                                                                  *
*             IF CURGEN IS G0497V00 COMING IN TO THIS ROUTINE,     *
*             THEN THIS ROUTINE WILL LOAD DATA FIELD NXTGEN        *
*             WITH G0498V00                                        *
*                                                                  *
********************************************************************
*
ADDONE   ST    R14,SAVEIT4            SAVE REG 14 IN SAVEIT4
         MVC   NXTGEN,CURGEN
         PACK  PACKGEN(3),CURGEN+1(4)
         AP    PACKGEN,PACK1
         UNPK  GENNO,PACKGEN
         OI    GENNO+3,X'F0'
         MVC   NXTGEN+1(4),GENNO
         L     R14,SAVEIT4
         BR    R14
         EJECT
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR SUFFIXING THE DATA        *
*        FIELD GDGNAME WITH (+0)                                   *
*                                                                  *
*        E.G.                                                      *
*             IF GDGNAME COMING IN TO THIS ROUTINE IS              *
*             AGNCY999.MY.GDG, THEN AFTER THIS ROUTINE IS          *
*             COMPLETED, GDGNAME WILL CONTAIN AGNCY999.MY.GDG(+0)  *
*                                                                  *
********************************************************************
*
ADDSUFFX ST    R14,SAVEIT5            SAVE REG 14 IN SAVEIT5
         LA    R6,GDGNAME             R6==> ADDRESS OF GDGNAME
         A     R6,=F'43'              R6==> ADDRESS OF LOW ORDER OF
*                                         GDGNAME
SFXLOOP  CLI   0(R6),C' '             HAVE WE FOUND A SIGNIFICANT CHAR?
         BNE   SIGNIF                 IF SO, BRANCH TO SIGNIF
         S     R6,=F'1'               DECREMENT R6 BY 1
         B     SFXLOOP                BRANCH TO SFXLOOP
SIGNIF   A     R6,=F'1'
         MVC   0(4,R6),=CL4'(+0)'     SUFFIX DSNAME WITH (+0)
         L     R14,SAVEIT5
         BR    R14
         EJECT
********************************************************************
*                                                                  *
*        BUILD UPDTLIST (IKJUPDT PARM LIST)                        *
*                                                                  *
********************************************************************
*
INITLIST ST    R14,SAVEIT6
         MVC   UPDTUPT,CPPLUPT          UPT
         MVC   UPDTECT,CPPLECT          ECT
         LA    R9,ECB
         ST    R9,UPDTECB               ECB
         LA    R8,UPLIST
         ST    R8,UPDTUPL               UPLIST
         L     R14,SAVEIT6
         BR    14
         EJECT
********************************************************************
*                                                                  *
*        BUILD UPDATE PARM LIST FOR &CURGEN AND CALL IKJUPDT       *
*                                                                  *
********************************************************************
*
SETCURGN ST    R14,SAVEIT7
         LA    R9,=C'CURGEN'
         ST    R9,LOCPTR
         MVC   LOCLEN,=F'6'
         LA    R5,CURGEN
         ST    R5,VALPTR
         MVC   VALLEN,=F'8'
         LA    R1,UPDTLIST
         CALL  IKJUPDT
         L     R14,SAVEIT7
         BR    14
         EJECT
********************************************************************
*                                                                  *
*        BUILD UPDATE PARM LIST FOR &NXTGEN AND CALL IKJUPDT       *
*                                                                  *
********************************************************************
*
SETNXTGN ST    R14,SAVEIT8
         LA    R9,=C'NXTGEN'
         ST    R9,LOCPTR
         MVC   LOCLEN,=F'6'
         LA    R5,NXTGEN
         ST    R5,VALPTR
         MVC   VALLEN,=F'8'
         LA    R1,UPDTLIST
         CALL  IKJUPDT
         L     R14,SAVEIT8
         BR    14
         EJECT
********************************************************************
*                                                                  *
*        E N D   O F   J O B   R O U T I N E                       *
*                                                                  *
********************************************************************
*
SHUTDOWN LR   R15,R4
         LEAVER
         EJECT
         SPACE 3
********************************************************************
*                                                                  *
*        D A T A   A R E A   C O N S T A N T S                     *
*                                                                  *
********************************************************************
*
CURGEN   DS    CL8
NXTGEN   DS    CL8
PATCAML1 CAMLST  NAME,PATDSN,,PATLOC
PATDSN   DS    CL44
PATLOC   DS    CL256
PACK1    DC    PL1'1'
         LTORG
         EJECT
         SPACE 3
********************************************************************
*                                                                  *
*        D Y N A M I C   W O R K   A R E A                         *
*                                                                  *
********************************************************************
*
WORKAREA DSECT
SAVEAREA DS    18F
ANS      DS    F
CAMLIST1 CAMLST NAME,GDGNAME,,LOCAREA
LOCAREA  DS    0D
         DS    265C
ECB      DS    F
         DS    0D
GENNO    DS    CL4
GDGNAME  DS    CL44
PACKGEN  DS    PL3
INDSNLEN DS    H
PPLA     DS    7F
PROCESSW DS    CL3
SAVEIT1  DS    F
SAVEIT2  DS    F
SAVEIT3  DS    F
SAVEIT4  DS    F
SAVEIT5  DS    F
SAVEIT6  DS    F
SAVEIT7  DS    F
SAVEIT8  DS    F
*
UPDTLIST DS    0F
UPDTUPT  DS    F
UPDTECT  DS    F
UPDTECB  DS    F
UPDTUPL  DS    F
*
UPLIST   DS    0F
LOCPTR   DS    AL4
LOCLEN   DS    FL4
VALPTR   DS    AL4
VALLEN   DS    FL4
*
WORKLEN  EQU   *-WORKAREA
NEXTGEN CSECT
         EJECT
********************************************************************
*                                                                  *
*                   P A R S E   M A C R O S                        *
*                                                                  *
********************************************************************
*
PCL      IKJPARM
DSNAME   IKJKEYWD
         IKJNAME 'DSNAME',SUBFLD=DSNAM
DSNAM    IKJSUBF
DSNSTRNG IKJPOSIT DSNAME,USID
         IKJENDP
         END
