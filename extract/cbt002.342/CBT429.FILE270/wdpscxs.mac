         TITLE 'WDPSCXS - SPF EXIT,LOGOFF MODULE'
*
*  V2M2 - MODIFIED 11/9/87 TO USE IBM STACK MACRO (TSOE STACK CHG)
*
*  V2M1 - MODIFIED 2/9/87 TO ACCEPT SPFEXEC TYPE ENVIRONMENT
*
         EJECT
WDPSCXS  ENTERR LEVEL=V2M2
         EJECT
*
*              GET CMD PTR AND LENGTH:
*
         LR    R11,R1             SAVE PARM PTR
         L     R10,0(,R1)              R10->PARM AREA              *KK1
         LH    R9,0(,R10)              R9=PARM LEN                 *KK1
         LTR   R9,R9              ANY PARM?
         BZ    EXIT               NO -> GET OUT NOW
         BCTR  R9,R0               R9-1 FOR EX                     *KK1
         SPACE
*
*    OBTAIN THE COMMAND AREA AND COPY THE COMMAND IN:
*
GETMAIN  LA    R0,GETLEN          R0= LENGTH NEEDED
         O     R0,=X'4E000000'    SUBPOOL=78
         GETMAIN R,LV=(0)         ASK SYSTEM FOR IT
         LR    R5,R1              R5 -> LSD AREA
         USING LSD,R5             TELL ASSEMBLER WE HAVE IT
         MVI   COMMAND,C' '       BLANK
         MVC   COMMAND+1(255),COMMAND  SMEAR IT AROUND
         SPACE
MCMD     MVC   COMMAND(0),2(R10)  FOR EX                           *KK1
         EX    R9,MCMD            SAVE THE CMD STRING              *KK1
         EJECT
*
*    HE  WANTS TO ISSUE A CMD SO LETS STACK IT FOR HIM
*
         L     R7,540             R7->MY TCB
         L     R8,132(,R7)        R8->MY MAMA
         L     R7,112(,R8)        R7->FSA
         L     R8,8(,R7)          R8->NEXT SAVE AREA
         CLC   72(8,R8),=CL8'NEWISPF'  IS THIS OUR FRONTEND?
         BE    GETOLD CPPL        YES WE ARE OK
         ICM   R8,15,8(R8)        R8->NEXT SA                      V2M1
         BZ    DEAD                                                V2M1
         CLC   72(8,R8),=CL8'NEWISPF' ECT AVAIL?                   V2M1
         BE    GETOLD CPPL        YUP                              V2M1
DEAD     WTP   'TELL TECH SUPPORT ISPF FRONTEND INACTIVE'
         LA    R15,20             RC=REAL BAD!
         B     EXIT               GET OUT NOW
GETOLD   LA    R8,80(,R8)         R8->NEWISPF SAVED CPPL
         L     R7,12(,R8)         R7->ECT!
         LA    R6,ECB             R6 -> ECB        BLOCK.
*
*    BUILD THE LSD DESCRIBING THE COMMAND:
*
         LA    R4,COMMAND         R4 -> COMMAND
         ST    R4,LSDADATA        SAVE IT
         ST    R4,LSDANEXT        "
         MVC   LSDRCLEN,=H'256'   RECLEN
         MVC   LSDTOTLN,=H'256'   TOTAL LEN
         EJECT
*
*    NOW THAT  WE HAVE BUILT IT,LETS PUT IT ON THE STACK
*
         STACK PARM=STACKL,UPT=(R8),ECT=(R7),ECB=(R6),              XXXX
               STORAGE=LSD,MF=(E,IOPL)
         EJECT
*
*
*    DONE HERE -> RETURN TO SPFPMD MAIN MOD (PMD)
*
EXIT     LEAVER
         EJECT
*
*    CONSTANTS AND WORKAREAS
*
KVBLOCK  DC    AL1(255)
         DC    AL1(2)
         DC    C'XC'
         DC    F'0'               END FLAG?
         LTORG
         SPACE 3
WORKAREA DSECT
SAVEAREA DS    18F                SAVEAREA
COMSAVE  DS    CL8                COM REF
PARML    DS    3F                 PARM LIST FOR CKVGET
IOPL     DS    4F                 IOPL
ECB      DS    F                  ECB
STACKL   STACK MF=L               STACK LIST
WORKLEN  EQU   *-WORKAREA
         EJECT
LSD      IKJLSD                   LSD
COMMAND  DS    CL256              COMMAND
GETLEN   EQU   *-LSD
         END
