*          DATA SET 761KKGETSC AT LEVEL 001 AS OF 10/13/80
         TITLE 'GETSCRN - GENERAL PURPOSE 3270 SCREEN INPUT RTN'
GETSCRN  ENTERR
         LR    R11,R1                   R11-> PARM LIST
*
         EJECT
*                                       VERSION 1 MOD 0
***********************************************************************
*
*
*              CALL FORMAT:
*
*
*        CALL  'GETSCRN' USING DDNAME, MENUNAME, LENGTH, MENUAREA.
*
*
*              DDNAME =  8 BYTE DDNAME FOR MENU (LOAD MODULE)
*              MENUNAME = 8 BYTE NAME OF MENU (LOAD MODULE)
*              LENGTH = 2 BYTE COMP FLD TO RECEIVE MENU LENGTH
*              MENUAREA = AREA TO RECEIVE MENU BUFFER
*                    (MUST BE LARGE ENOUGH FOR ANY MENU REQUESTED)
*
*
*              RETURN CODES:  99 = BAD PARMS
*                          OTHER = DDNAME NOT ALLOCATED OR
*                                  DDNAME DID NOT OPEN  OR
*                                  MENU NOT FOUND ,ETC.
*
*
*
*
*
***********************************************************************
         EJECT
***********************************************************************
*
*              MAINLINE CONTROL LOGIC:
*
***********************************************************************
*
         BAL   R14,INIT                 CHECK PARM LIST OK
         LTR   R15,R15
         BNZ   RETURN
*
*
         BAL   R14,DDCHEK               GO CHECK DD PRESENT AND OPEN
         LTR   R15,R15
         BNZ   RETURN
*
         BAL   R14,FINDMENU             GO FIND MENU AND LENGTH
         LTR   R15,R15
         BNZ   RETURN
*
*
         BAL   R14,MOVEMENU             GO GIVE HIM THE OUTPUT BUFFER
*
*
         BAL   R14,TERM                 CLOSE THE MENU FILE
         EJECT
RETURN   LEAVER
         SPACE 3
         EJECT
INIT     ST    R14,L1LS
         SPACE
*
         TM    0(R11),X'80'
         BNZ   PARMERR
         L     R10,0(,R11)              R10->DDNAME
         MVC   DDNAME,0(R10)            SAVE
*
         TM    4(R11),X'80'
         BNZ   PARMERR
         L     R9,4(,R11)               R9 ->MENUNAME
         MVC   MENUNAME,0(R9)           SAVE
*
         TM    8(R11),X'80'
         BZ    IX
         SPACE
PARMERR  LA    R15,99                   RC=99 IF BAD PARM LIST
         SPACE
IX       L     R14,L1LS
         BR    R14                      RET
         EJECT
DDCHEK   ST    R14,L1LS
         SPACE
         MVC   DCB(DCBLEN),DCBPAT       INIT DCB
         MVC   DCB+40(8),DDNAME         GET DDNAME
*
         DEVTYPE  DDNAME,DEVAREA,DEVTAB  DD HERE?
         LTR   R15,R15                  FOUND IT?
         BNZ   DDX                      NO
         SPACE 3
         OI    OPENL,X'80'              FIX VL FLAG
DDNOPEN  OPEN  (DCB,INPUT),MF=(E,OPENL)    OPEN FOR INPUT
         SPACE 2
DDX      L     R14,L1LS
         BR    R14                      YES
         EJECT
FINDMENU ST    R14,L1LS
*
         LOAD  EPLOC=MENUNAME,DCB=DCB        LOAD MENU FROM MENU LIB
         LA    R1,0(,R1)                CLEAR HI BYTE
         SLL   R1,3                     MULT BY 8
         ST    R1,MODLEN                SAVE MODULE LENGTH
         ST    R0,MODLOC                SAVE MODULE LOCATION
         LR    R3,R0                    R3->MODULE
         L     R2,=A(MENUPAT)           R2->MENU PAT
         USING MENUPAT,R2
         MVC   MENULIKE(48),MENUPAT     INIT MENU COMPARE
         DROP  R2
         MVC   MENULIKE+5(8),MENUNAME   GET NAME OK
         CLC   MENULIKE(24),0(R3)       LIT1 THERE?
         BNE   NOTLIT1                  NO->OTHER TYPE MENU
*
LLOOP    SH    R1,=H'4'                 GET OFFSET TO LENLIT
         L     R5,0(R1,R3)              LOAD THE LENGTH           WDPSC
         C     R5,=F'0'                 OK?                       WDPSC
         BE    LLOOP                    NO->LINKEDITOR PADDED TO DBLWD
         LR    R1,R5                    R1=CORRECT MENU LENGTH    WDPSC
         LA    R3,LIT-BEGIN(,R3)        R3->LIT MENU
*
NOTLIT1  STH   R1,MENULEN               SAVE MENU LEN
         ST    R3,MENULOC               SAVE MENU LOC
*
         L     R14,L1LS
         BR    R14                      RET
         EJECT
MOVEMENU ST    R14,L1LS
*
         L     R4,MENULOC               R4->FROM ADDR
         LH    R5,MENULEN               R5= MENU LEN
         L     R6,12(,R11)              R6->MENU AREA
         LR    R7,R5                    R7= MENU LEN
         MVCL  R6,R4                    GIVE IT TO HIM
*
         L     R8,8(,R11)               R8->LENGTH FIELD
         LH    R5,MENULEN               R5= MENU LEN
         STH   R5,0(,R8)                GIVE HIM THE LENGTH
*
         L     R14,L1LS
         BR    R14                      RET
         EJECT
TERM     ST    R14,L1LS
*
         CLOSE (DCB),MF=(E,OPENL)       CLOSE AUDIT FILE
         SPACE
         L     R14,L1LS
         BR    R14                      EXIT
         EJECT
DCBPAT   DCB   DSORG=PO,MACRF=R
DCBLEN   EQU   *-DCBPAT
         LTORG
         TITLE 'WORKAREA - WORK AREA FOR GETSCRN-RTN'
WORKAREA DSECT
         SPACE
SAVEAREA DS    18F                      SAVE AREA
L1LS     DS    F                        LEVEL 1 LINK REG SAVE
L2LS     DS    F
L3LS     DS    F
DDNAME   DC    CL8' '
MENUNAME DC    CL8' '
MENULEN  DS    F
MENULOC  DS    F
MODLEN   DS    F
MODLOC   DS    F
MENULIKE DS    XL52
DEVAREA  DS    6F
OPENL    OPEN  (DCB,OUTPUT),MF=L
         SPACE 3
DCB      DCB   DSORG=PO,MACRF=R
         SPACE 3
WORKLEN  EQU   *-WORKAREA
         EJECT
         LIT1  MENUPAT
         LIT2
         END
