*          DATA SET 761KKDSTAT AT LEVEL 001 AS OF 10/13/80
         TITLE 'DATASTAT COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
* DATASTAT 'SWC.L.DATA'                                               *
*   "   MISC.CLIST                                                    *
*   "   'SWC.L.DATA(MEM)'                                             *
*   "   MISC.CLIST(CLMEM)                                             *
*                                                                     *
*  GIVES A RETURN CODE AS FOLLOWS:                                    *
*        0     = DATASET IS A PDS       (PO)                          *
*        1     = DATASET IS SEQUENTIAL  (PS)                          *
*        2     = DATASET IS OTHER DASD  (DA,ETC.)                     *
*        3     = DATASET IS NON-DASD    (TAPE,ETC.)                   *
*        4     = DATASET IS NOT CATALOGUED                            *
*        8     = DATASET IS NOT ON PACK (NOT FOUND IN VTOC)           *
*                                                                     *
***********************************************************************
*
* REGISTER USEAGE
*
*        R2-R7 WORK REGISTERS
*        R8    BASE REGISTER FOR PDES
*        R10   BASE REGISTER FOR PPL
*        R11   BASE REGISTER FOR CPPL
*        R12   BASE REGISTER FOR PROGRAM
*        R13   BASE REGISTER FOR WORKAREA
*        R14   LINK REGISTER
DATASTAT ENTER (14,12),12,(,WORKL1)
         EJECT
*        SET UP ADDRESSABILITY
*        COMMAND PROCCESSOR PARM LIST,PARSE PARM LIST,DYNAMIC ALLOC
*        PARM LIST,WORKAREA. (CPPL,PPL,DAPL,W.A.)
         USING WORK1,R13
         LR    R11,R1
         USING CPPL,R11
         LA    R10,PPLAREA
         USING PPL,R10
         SPACE
*
*        PREPARE PARSE  CONTROL BLOCKS
*        TRANSFER INFO FROM CPPL TO PPL
         L     R5,CPPLCBUF    COMMAND BUFFER
         ST    R5,PPLCBUF
         L     R5,CPPLUPT    UPT
         ST    R5,PPLUPT
         L     R5,CPPLECT    ECT
         ST    R5,PPLECT
         LA    R5,PARSECB    MOVE OUR ECB
         ST    R5,PPLECB
         L     R5,=A(PARMS)
         ST    R5,PPLPCL
         LA    R5,ANSAREA
         ST    R5,PPLANS
         XC    PPLUWA(4),PPLUWA
         XC    PARSECB(4),PARSECB
         SPACE
*
*        PARSE COMMAND BUFFER
*
         LR    R1,R10
         DROP  R10
         LINK  EP=IKJPARS
         SPACE
*
*        CHECK RETURN CODE
*
         LTR   R15,R15
         BNZ   PARSERR
         SPACE
*
*        SET UP PDL ADDRESSABILITY
*
         L     R8,ANSAREA
         USING IKJPARMD,R8
         EJECT
*
*
*              ISSUE LOCATE AND OBTAIN  FOR DSN
*
*
*              FIRST BUILD 44 BYTE DSN:
*
         MVI   DSNAME,C' '              A BLANK
         MVC   DSNAME+1(43),DSNAME      SPREAD IT AROUND
         LA    R1,DSNAME                R1->DSN AREA
         TM    DSTRING+6,X'40'          QUOTED?
         BNZ   YESQ                     YES
         L     R2,CPPLUPT               R2->UPT
         SLR   15,15                    R15=0
         IC    R15,23(,R2)              R15=PREFIX LEN
         LTR   R15,R15                  ANY?
         BZ    YESQ                     NO
         MVC   DSNAME(7),16(R2)         MOVE PREFIX
         IC    R0,=C'.'                 LOAD A PERIOD
         STC   R0,0(R15,R1)             INSERT IT
         LA    R1,1(R15,R1)             POINT PAST IT
YESQ     L     R4,DSTRING               R4->DSN AS ENTERED
         LH    R3,DSTRING+4             R3=LEN OF ENTERED
         BCTR  R3,R0                    -1 FOR EX
         SPACE
MOVEDSN3 MVC   0(0,R1),0(R4)
         EX    R3,MOVEDSN3              CONCAT IT
*
*              THEN BUILD PARM LISTS:
*
         MVC   CAML1,CAML1R             PATTERN                   WDPSC
         MVC   CAML2,CAML2R             PATTERN                   WDPSC
         LA    R1,DSNAME                R1->DSNAME                WDPSC
         ST    R1,CAML1+4               PASS IT                   WDPSC
         ST    R1,CAML2+4                "   "                    WDPSC
         LA    R1,CAMA1                 R1->WORK AREA 1           WDPSC
         ST    R1,CAML1+12              PASS                      WDPSC
         LA    R1,CAMA2                 R1->WORK AREA 2           WDPSC
         ST    R1,CAML2+12              PASS                      WDPSC
         LA    R1,CAMA1+6               R1->VOL SER AREA          WDPSC
         ST    R1,CAML2+8               PASS                      WDPSC
         EJECT                                                    WDPSC
*
*              NEXT DO LOCATE TO GET VOLSER:
*
         LOCATE  CAML1                  SEARCH CAT
*
         LTR   R15,R15                  FOUND?
         BNZ   RC4                      NO
         CLC   CAMA1(2),=X'0001'        ONE VOL?
         BNE   RC3                      NO
         CLI   CAMA1+4,X'20'            DASD?
         BNE   RC3                      NO
*
*              NOW DO OBTAIN TO SEE IF  THERE
*
         OBTAIN  CAML2                 SEARCH VTOC
*
         LTR   R15,R15                  FOUND OK?
         BNZ   RC8                      NO
         TM    CAMA2+82-44,X'02'        PO?
         BO    RC0                      YES
         TM    CAMA2+82-44,X'40'        PS?
         BO    RC1                      YES
*
RC2      LA    R15,2                    RC=2
         B     EXIT
RC1      LA    R15,1                    RC=1
         B     EXIT
RC0      SLR   R15,R15                  RC=0
         B     EXIT
RC3      LA    R15,3                    RC=3
         B     EXIT
RC4      LA    R15,4                    RC=4
         B     EXIT
RC8      LA    R15,8                    RC=8
         B     EXIT
         TITLE 'EXITS...BOTH GOOD AND BAD'
EXIT     EQU   *
         LEAVE EQ
         TITLE 'DATASTAT -- ERROR PROCESSING'
*
*        AN ERROR OCCURED IN PARSE
*
PARSERR  EQU   *
         TPUT  BADPARS,11
         B     RC4
         TITLE 'DATASTAT -- CONSTANTS, STORAGE, AND DSECTS'
*
CAML1R   CAMLST   NAME,DSNAMER,,CAMA1R
CAML2R   CAMLST   SEARCH,DSNAMER,CAMA1R+6,CAMA2R
CAMA1R   EQU   *                        DUMMY                     WDPSC
CAMA2R   EQU   *                        DUMMY                     WDPSC
DSNAMER  EQU   *                        DUMMY                     WDPSC
*
BADPARS  DC    CL11'PARSE ERROR'
         LTORG                                                    WDPSC
         EJECT                                                    WDPSC
WORK1    DSECT
         DS    18F
PPLAREA  DS    7F
ANSAREA  DS    F
PARSECB  DS    F
DSNAME   DC    CL44' '
CAML1    CAMLST   NAME,DSNAME,,CAMA1
CAMA1    DS    0D
         DC    265X'00'
CAML2    CAMLST   SEARCH,DSNAME,CAMA1+6,CAMA2
CAMA2    DS    0D
         DC    140X'00'
WORKL1   EQU   *-WORK1
DATASTAT CSECT                                                    WDPSC
         EJECT                                                    WDPSC
PARMS    IKJPARM
DSTRING  IKJPOSIT DSNAME,PROMPT='DATASET'
         IKJENDP
         EJECT                                                    WDPSC
         IKJCPPL
         SPACE 2
         IKJPPL
         END
