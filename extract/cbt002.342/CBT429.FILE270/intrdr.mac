**************
*   INTRDR   *
**************
*
*        THIS PROGRAM IS A SUBROUTINE WHICH WILL ACCEPT A DDNAME
*        PASSED TO IT IN THE PARAMETER LIST AND THEN DYNAMICALLY
*        ALLOCATE THE INTERNAL READER TO THAT DDNAME
*
********************************************************************
*
*        TO CALL THIS PROGRAM FROM A CLIST, DO THE FOLLOWING:
*
*           CALL 'WDPSC.SHARED.LOAD(INTRDR)' '&DDNAME'
*
*                   WHERE &DDNAME CONTAINS THE DDNAME
*
********************************************************************
*
*        TO CALL THIS SUBROUTINE FROM A PROGRAM, DO THE FOLLOWING:
*
*           MOVE THE LENGTH OF YOUR DDNAME TO DDNAME-LENGTH
*                INTRDR-I-O-AREA.
*           MOVE YOUR DDNAME TO DDNAME OF INTRDR-I-O-AREA.
*
*           CALL 'INTRDR' USING INTRDR-I-O-AREA.
*
*                WHERE:
*
*                      01  INTRDR-I-O-AREA.
*                          05  DDNAME-LENGTH    PIC S9(4) COMP.
*                          05  DDNAME           PIC X(8).
*
*********************************************************************
*
*         RETURN CODES        MEANING
*         ------------        -----------------------------------
*
*              0              ALLOCATION WAS SUCCESSFUL
*              4              ALLOCATION ERROR
*              8              INVALID DDNAME PASSED TO THIS PROGRAM
*
         EJECT
INTRDR   CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
*
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
********************************************************************
*        SAVE REGISTER 1 CONTENTS IN REGISTER 11 AND PLACE         *
*        PARAMETER INTO IOAREA                                     *
********************************************************************
*
STARTIT  DS    0D
         LR    R11,R1
         L     R9,0(R11)
         MVC   IOAREA,0(R9)
         EJECT
         SPACE 3
*
********************************************************************
*        LOAD THE LENGTH OF THE DDNAME AND THE DDNAME INTO THE     *
*        DYNAMIC UNALLOCATION AND DYNAMIC ALLOCATION WORK AREAS    *
********************************************************************
*
         LA    R4,0                 R4==> 0
         LH    R7,INDDNLN           R7==> LENGTH OF DDNAME
         CH    R7,=H'0'             LENGTH NOT GREATER THAN 0 ?
         BNH   NOGOOD               IF SO, BRANCH TO NOGOOD
         CH    R7,=H'8'             LENGTH GREATER THAN 8 ?
         BH    NOGOOD               IF SO, BRANCH TO NOGOOD
         MVC   DD1DDNLN,INDDNLN     DD1DDNLN==> LENGTH OF DDNAME
         MVC   FREEUT1A,INDDNLN     FREEUT1A==> LENGTH OF DDNAME
         MVC   DD1DDNAM,INDDNAM     DD1DDNAM==> DDNAME
         MVC   FREEUT1B,INDDNAM     FREEUT1B==> DDNAME
         B     DODYNAM              BRANCH TO DODYNAM
NOGOOD   LA    R4,8                 R4==> 8
         B     SHUTDOWN             BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*        DO DYNAMIC ALLOCATION                                     *
********************************************************************
*
DODYNAM  LA    R1,FREEPARM
         DYNALLOC                      FREE DDNAME
         LTR   R15,R15                 TEST REGISTER 15 FOR ZERO
         BZ    DYNALLOK                IF ZERO, BRANCH TO DYNALLOK
         CLC   ERCODE,=XL2'0438'       COMPARE ERROR REASON CODE
*                                         AGAINST HEXADECIMAL 0438
         BE    DYNALLOK                IF EQUAL, BRANCH TO DYNALLOK
         LA    R4,4                    R4==> 4
         B     SHUTDOWN                BRANCH TO SHUTDOWN
DYNALLOK LA    R1,DD1PARM
         DYNALLOC                      ALLOC F(DDNAME) DA(DSN) SHR
         LTR   R15,R15                 TEST REGISTER 15 FOR ZERO
         BZ    SHUTDOWN                IF ZERO, BRANCH TO SHUTDOWN
         LA    R4,4                    R4==> 4
         B     SHUTDOWN                BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*        E N D   O F   J O B   P R O C E S S I N G                 *
********************************************************************
*
SHUTDOWN LR    R15,R4
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        DATA STRUCTURE FOR FREEING OF DDNAME                      *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
FREEPARM DC    X'80'         TURN ON HIGH ORDER BIT
         DC    AL3(FREERB)   ADDRESS OF REQUEST BLOCK
FREERB   DC    X'14'         S99RBLN (LENGTH OF REQUEST BLOCK)
         DC    X'02'         S99VERB (REQUEST FOR UNALLOCATION)
         DC    X'2000'       S99FLAG1 (DO NOT MOUNT VOLUMES)
ERCODE   DC    X'0000'       S99ERROR (ERROR REASON CODE)
         DC    X'0000'       S99INFO (INFORMATION REASON CODE)
         DC    A(FREETUPL)   S99TXTPP (ADDR OF POINTER TO TEXT UNIT)
         DC    X'00000000'   RESERVED
         DC    X'00000000'   S99FLAG2
FREETUPL DC    A(FREEUNT1)   ADDRESS OF TEXT UNIT #1
         DC    X'80'         TURN ON HIGH ORDER BIT
         DC    AL3(FREEUNT2) ADDRESS OF TEXT UNIT #2
FREEUNT1 DC    X'0001'
         DC    X'0001'
FREEUT1A DS    BL2           LENGTH OF DDNAME
FREEUT1B DS    CL8           DDNAME
FREEUNT2 DC    X'0007'
         DC    X'0000'       REMOVE PERM ALLOC ATTR
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        DATA STRUCTURE FOR DYNAMIC ALLOCATION                     *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
DD1PARM  DC    X'80'         TURN ON HIGH ORDER BIT
         DC    AL3(DD1RB)    ADDRESS OF REQUEST BLOCK
DD1RB    DC    X'14'         S99RBLN (LENGTH OF REQUEST BLOCK)
         DC    X'01'         S99VERB (REQUEST FOR DSNAME ALLOC)
         DC    X'2000'       S99FLAG1 (DO NOT MOUNT VOLUMES)
         DC    X'0000'       S99ERROR (ERROR REASON CODE)
         DC    X'0000'       S99INFO (INFORMATION REASON CODE)
         DC    A(DD1TUPL)    S99TXTPP (ADDR OF POINTERS TO TEXT UNITS
         DC    X'00000000'   RESERVED
         DC    X'00000000'   S99FLAG2
DD1TUPL  DC    A(DD1UNIT1)   ADDRESS OF TEXT UNIT #1
         DC    A(DD1UNIT2)   ADDRESS OF TEXT UNIT #2
         DC    A(DD1UNIT3)   ADDRESS OF TEXT UNIT #3
         DC    X'80'         TURN ON HIGH ORDER BIT
         DC    AL3(DD1UNIT4) ADDRESS OF TEXT UNIT #4
DD1UNIT1 DC    X'0001'
         DC    X'0001'
DD1DDNLN DS    XL2           LENGTH OF DDNAME
DD1DDNAM DS    CL8           DDNAME
DD1UNIT2 DC    X'0018'
         DC    X'0001'
         DC    X'0001'
         DC    C'A'          SYSOUT=(A
DD1UNIT3 DC    X'0019'
         DC    X'0001'
         DC    X'0006'
         DC    CL6'INTRDR'   SYSOUT=(A,INTRDR)
DD1UNIT4 DC    X'0052'
         DC    X'0000'       PERMANENTLY ALLOCATED ATTRIBUTE
         EJECT
         SPACE 3
********************************************************************
*        DATA AREAS                                                *
********************************************************************
         SPACE 3
         DS    0D
IOAREA   DS    0CL10
INDDNLN  DS    CL2
INDDNAM  DS    CL8
         END
