*   PROGRAM WHICH DOES A WRITE-TO-OPERATOR FROM DATA IN PARM FIELD
*   KEITH NEWSOM, WDPSC, 2-20-86
*
WTOPARM  CSECT
         USING *,R15
         B     GO
         DC    X'08',CL8'WTOPARM'
GO       STM   14,12,12(13)
         LR    12,15
         USING WTOPARM,R12
         SPACE 2
         L     R2,0(R1)        R2 <-- ADDRESS OF PARM FIELD
         LH    R3,0(R2)        R3 <-- LENGTH OF PARM FIELD
         LTR   R3,R3           TEST FOR ZERO LENGTH
         BZ    RETURN            BRANCH IF ZERO
         SPACE 2
         BCTR  R3,0            DECREMENT MESSAGE LENGTH
         EX    R3,MOVE
         WTO   MF=(E,WTOMSG)
         SPACE 2
RETURN   LM    R14,R12,12(R13)  RETURN
         MVI   12(R13),X'FF'
         SR    R15,R15
         BR    R14
         SPACE 2
MOVE     MVC   MESSAGE(1),2(R2)
         EJECT
WTOMSG   DS    0F
         DC    Y(LWTOMSG)
         DC    B'1000000000000000'   MCS FLAGS
MESSAGE  DC    CL100' '
LWTOMSG  EQU   *-WTOMSG
         DC    B'0000000000000000'   DESCRIPTOR CODES
         DC    B'1100000000000000'   ROUTING CODES
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
