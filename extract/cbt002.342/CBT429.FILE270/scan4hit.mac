         PRINT ON,NOGEN
         SPACE 3
*        THIS PROGRAM IS A SUBROUTINE WHICH WILL SCAN A RECORD OF UP
*        TO 80 BYTES IN LENGTH FOR AN ARGUMENT OF UP TO 80 BYTES IN
*        LENGTH AND INFORM THE INVOKER OF WHETHER A MATCH WAS FOUND.
*
*        TO CALL THIS PROGRAM, DO THE FOLLOWING :
*
*           MOVE YOUR ARGUMENT TO ARGUMENT OF SCAN4HIT-I-O-AREA.
*           MOVE THE LENGTH OF YOUR ARGUMENT
*             TO ARGUMENT-LENGTH OF SCAN4HIT-I-O-AREA.
*           MOVE YOUR RECORD TO BE SCANNED
*             TO TABLE-ELEMENT OF SCAN4HIT-I-O-AREA.
*           MOVE THE LENGTH OF YOUR RECORD TO BE SCANNED
*             TO TABLE-ELEMENT-LENGTH OF SCAN4HIT-I-O-AREA.
*
*           CALL 'SCAN4HIT' USING SCAN4HIT-I-O-AREA.
*
*           AFTER THE CALL, HIT-SWITCH OF SCAN4HIT-I-O-AREA WILL
*           CONTAIN EITHER 'YES' OR 'NO '.  'YES' MEANS A MATCH WAS
*           FOUND AND 'NO ' MEANS A MATCH WAS NOT FOUND.
*
*                WHERE:
*
*                      01  SCAN4HIT-I-O-AREA.
*                          05  ARGUMENT              PIC X(80).
*                          05  ARGUMENT-LENGTH       PIC S9(4) COMP.
*                          05  TABLE-ELEMENT         PIC X(80).
*                          05  TABLE-ELEMENT-LENGTH  PIC S9(4) COMP.
*                          05  HIT-SWITCH            PIC X(3).
*
*
*         RETURN CODES        MEANING
*         -----------------   -----------------------------------
*
*                 0           MATCH WAS FOUND
*                 4           NO MATCH WAS FOUND
*                 8           INVALID PARAMETERS PASSED TO PROGRAM
         EJECT
         SPACE 3
*
**********************************************
*                                            *
*        I N I T I A L I Z A T I O N         *
*                                            *
**********************************************
*
SCAN4HIT CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        SAVE REGISTER 1 IN REGISTER 11 AND LOAD PARAMETER FIELD   *
*        INTO IOAREA                                               *
*                                                                  *
********************************************************************
*
         SPACE 3
STARTIT  DS    0D
         LR    R11,R1              SAVE REGISTER 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   IOAREA,0(R9)
*                                        BINARY ZAROES
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        M A I N   L I N E   L O G I C                             *
*                                                                  *
********************************************************************
*
         LA    R10,0
         MVC   ARGLEN,INARGLEN
         MVC   TBLLEN,INTBLLEN
         LH    R8,ARGLEN
         LH    R9,TBLLEN
         C     R8,=F'1'
         BL    NOGOOD
         C     R8,=F'80'
         BH    NOGOOD
         C     R9,=F'1'
         BL    NOGOOD
         C     R9,=F'80'
         BH    NOGOOD
         CR    R8,R9
         BH    NOGOOD
         LA    R6,ARGUMENT
         LA    R7,TBLELMNT
         S     R8,=F'1'
         AR    R9,R7
         SR    R9,R8
         S     R9,=F'1'
LOOP     CR    R7,R9
         BH    NOHIT
         EX    R8,DOCOMPR
CHEK     BE    HIT
         A     R7,=F'1'
         B     LOOP
NOHIT    MVC   HITSW,=CL3'NO '
         LA    R10,4
         B     SHUTDOWN
NOGOOD   MVC   HITSW,=CL3'   '
         LA    R10,8
         B     SHUTDOWN
HIT      MVC   HITSW,=CL3'YES'
         B     SHUTDOWN
DOCOMPR  CLC   0(0,R6),0(R7)
         B     CHEK
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        E N D  O F   J O B   P R O C E S S I N G                  *
*                                                                  *
********************************************************************
*
SHUTDOWN LR    R15,R10
         L     R9,0(R11)
         MVC   0(167,R9),IOAREA
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        BREAKDOWN OF IOAREA                                       *
*                                                                  *
********************************************************************
*
IOAREA   DS    0CL167
ARGUMENT DS    CL80
INARGLEN DS    CL2
TBLELMNT DS    CL80
INTBLLEN DS    CL2
HITSW    DS    CL3
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        D A T A   A R E A S                                       *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
ARGLEN   DS    H
TBLLEN   DS    H
         END
