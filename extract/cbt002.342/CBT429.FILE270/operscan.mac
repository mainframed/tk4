*
*   @OK - ADJUSTED TO INVOKE REGISTER EQUATE MACRO SUPPLIED IN
*          FILE270 .MACLIB                        10/26/98
*
*          DATA SET 761KKOPERS AT LEVEL 002 AS OF 02/13/80
*
* WRITTEN BY KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
*
*              DOCUMENTATION FOR KMDPARS AND OPERSCAN SUBRTNS:
*
*
*
*               PARAMETER LIST PASSED TO KMDPARS:
*
*         KPPL     DS 10F :
*         CBUFPTR  DS    F              CMDBUF TO BE PARSED
*         OPLSTPTR DS    F              LIST OF OPERANDS TO FLAG
*         FLAGPTR  DS    F              WHERE TO FLAG THEM
*         UNKNEXIT DS    F              EXIT TO CALL IF UNKNOWN OPER
*         EXITPARM DS    F              PARAMETER TO PASS EXITS (ADDR)
*                                       (R1 POINTS HERE AT EXIT ENTRY)
*         BPPLPASS DS    F              BPPL PTR WHEN EXIT GETS CONTROL
*         WORKPASS DS    F              OPTIONAL WORKAREA (512 BYTES)
*                  DS    3F             RESERVED
*
*
*               PARAMETER LIST PASSED TO OPERSCAN SUBROUTINE:
*
*         BPPL     DS    10F :
*         BUFFPTR  DS    F              BUFFER PTR (CBUF)
*         LENPTR   DS    F              LENGTH PTR (CBUF LEN)
*         STARTPTR DS    F              START SEARCH PTR
*         OPERPTR  DS    F              NEXT OPER LOC
*         OPLENPTR DS    F              NEXT OPER LEN PTR
*         SUBPTR   DS    F              SUBFIELD PTR
*         SUBLENPT DS    F              SUBFIELD LEN PTR
*         WORKPTR  DS    F              OPTIONAL WORKAREA (350 BYTES)
*                  DS    2F             RESERVED
*
*         OPLEN    DS    H              POINTED TO BY OPLENPTR
*         SUBLEN   DS    H              POINTED TO BY SUBLENPT
*
*          DATA SET 761KKOPSCN AT LEVEL 001 AS OF 04/27/79
         TITLE 'OPERSCAN - PARSE ROUTINE FOR INTERCEPT COMMANDS'
OPERSCAN CSECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING OPERSCAN,R12
         LR    R11,R1                   R11 -> PARM LIST
         USING BPPL,R11
         SLR   R15,R15                  R15 = 0
         LR    R10,R13                  SAVE SAVE
         L     R13,WORKPTR              SEE IF A FREE ONE THERE
         LTR   R13,R13                  ANY?
         BNZ   FASTCALL                 YES -> SMART CALLER
         LA    R0,WORKLEN               NO -> GET SOME
         GETMAIN R,LV=(0)
         LR    R13,R1                   SAVE IT
         USING WORKAREA,R13
FASTCALL ST    R10,SAVEAREA+4           SAVE HIS SAVE PTR
         LA    R13,SAVEAREA             MAKE SURE MINE IS OK
         USING SAVEAREA,R13
         ST    R13,8(,R10)              GIVE HIM MY SAVE AREA
*
         EJECT
         BAL   R14,VALCHECK             CAN WE LOOK FOR AN OPERAND?
         LTR   R15,R15                  R15 STILL 0?
         BNZ   OPEREX                   NO CAN DO
*
         BAL   R14,FINDOPER             LOOK FOR OPERAND
         LTR   R15,R15                  R15 STILL 0?
         BNZ   OPEREX                   NO CAN DO
*
         BAL   R14,FINDEND              FIND THE END OF THE OPERAND
         EJECT
OPEREX   LR    R1,R13                   R1 -> WORKAREA
         LA    R0,WORKLEN               R0 = LENGTH OF AREA
         L     R13,4(,R13)              R13 -> OLD SAVE AREA
         C     R1,WORKPTR               IS WORKAREA MINE?
         BE    FASTEX                   NO -> DONT FREE
         LR    R11,R15                  SAVE RC
         FREEMAIN R,LV=(0),A=(1)        FREE
         LR    R15,R11                  GET RC BACK
FASTEX   RETURN (14,12),RC=(15)         EXIT OPERSCAN
         REGEQU                                                     @OK
         EJECT
VALCHECK ST    R14,L1LS
*
         ST    R15,OPERPTR              NEXT OPER PTR = 0
         ST    R15,SUBPTR               SUBFLD PTR = 0
*
         L     R10,BUFFPTR              R10 -> BUFFER
         L     R9,LENPTR                R9 ->  BUFF LEN
         AH    R10,0(R9)                R10 -> BUFFEND+1
         S     R10,STARTPTR             R10 = LEN TO SCAN
         BP    SAVELEN                  SAVE IF > 0
*
         LA    R15,4                    RC = 4 -> NO OPER FOUND
         BR    R14
*
SAVELEN  ST    R10,SCANLEN              SAVE LENGTH TO SCAN
         BR    R14                      EXIT
         EJECT
FINDOPER ST    R14,L1LS
*
         SLR   R1,R1                    R1=0
*
         L     R10,STARTPTR             R10 -> START OF SEARCH
         L     R9,SCANLEN               R9 = LEN TO SEARCH
         BCTR  R9,R0                    R9 = R9-1 FOR EX
         EX    R9,TRT1                  LOOK FOR AN OPERAND
         BZ    NOOPER                   NONE FOUND?
*              WE HAVE ONE! ; POINT TO  IT:
         ST    R1,OPERPTR               RETURN OPER LOCATION
         L     R8,BUFFPTR               R8 -> BUFF
         L     R7,LENPTR                R7 -> BUFF LEN
         AH    R8,0(R7)                 R8 -> BUFFEND+1
         SR    R8,R1                    R8 = NEW SCAN LENGTH
         ST    R8,SCANLEN               SAVE IT
         BR    R14
NOOPER   LA    R15,4
         L     R14,L1LS
         BR    R14
TRT1     TRT   0(0,R10),TRANTAB1        EXECUTED SCAN
         TITLE 'OPERSCAN    - FINDEND ROUTINES'
FINDEND  DS    0H
         ST    R14,L1LS           SAVE R14
*
FINDINIT L     R9,OPERPTR         R9 -> START OF OPER
         A     R9,SCANLEN         R9 -> END OF BUFF +1
         LR    R8,R9              R8 -> "
         BCTR  R8,R0              R8 = R8-1
         L     R7,OPERPTR         R7 -> OPER START
         LA    R5,NORMODE         R5 -> NORMMODE
         SLR   R4,R4              R4 = 0 = PAREN DEPTH
         L     R3,SCANLEN         R3 =  LEN TO SCAN
         BCTR  R3,R0              R3=LENGTH FOR EXECUTE
         SLR   R2,R2              R2=0
         SLR   R1,R1              R1=0
         L     R15,=F'-1'         R15 = -1
         MVC   TRTTABLE,TT
*
SCANLOOP DS    0H
         BAL   R14,SCANLINE       LINK TO PARSE SCAN
         LTR   R15,R15            R15=0?
         BM    SCANLOOP           CONTINUE IF NEGATIVE
*
FINDEXIT L     R14,L1LS           RESTORE R14
         BR    R14                EXIT
*
*
SCANLINE DS    0H
*
TRTEX    EX    R3,TRT             EXECUTE SCAN
         BZ    0(,R5)             END OF LINE - NOTHING FOUND
         BC    4,0(R2,R5)         NOT END OF LINE - SOMETHING FOUND
         BC    2,4(R2,R5)         END OF LINE - SOMETHING FOUND
         DS    0F
TRT      TRT   0(0,R7),TRTTABLE   TRANSLATE INSTRUCTION (EXECUTED)
         EJECT
         DS    0F
TT       DC    256X'0'            TRANS TABLE PATTERN
         ORG   TT+107             COMMA
         DC    X'04'
         ORG   TT+77              LEFT PAREN
         DC    X'0C'
         ORG   TT+93              RIGHT PAREN
         DC    X'14'
         ORG   TT+125             QUOTE
         DC    X'1C'
         ORG   TT+64              BLANK
         DC    X'24'
         ORG
*
*   BRANCH TABLES
*
NORMODE  DS    0H
         B     NORMEND                       END OF LINE
         B     NORMCOM            COMMA
         B     NMCOMEL            COMMA      END OF LINE
         B     NORMLP             LEFT PAREN
         B     NMLPEL             LEFT PAREN END OF LINE
         B     NORMRP             RITE PAREN
         B     NMRPEL             RITE PAREN END OF LINE
         B     NORMQ              QUOTE
         B     NORMQEL            QUOTE      END OF LINE
         B     NORMB              BLANK
         B     NORMBEL            BLANK      END OF LINE
*
PARNMODE DS    0H
         B     PMEND                       END OF LINE
         B     PMCOM              COMMA
         B     PMCOMEL            COMMA      END OF LINE
         B     PMLP               LEFT PAREN
         B     PMLPEL             LEFT PAREN END OF LINE
         B     PMRP               RITE PAREN
         B     PMRPEL             RITE PAREN END OF LINE
         B     PMQ                QUOTE
         B     PMQEL              QUOTE      END OF LINE
         B     PMB                BLANK
         B     PMBEL              BLANK      END OF LINE
*
QMODE    DS    0H
         B     QMEND                       END OF LINE
         B     QMQ                QUOTE
         B     QMQEL              QUOTE      END OF LINE
         EJECT
*
*   COMMON SCAN ROUTINES
*
*
COMSEX1  DS    0H                 COMMON SCAN EXIT
         LA    R7,1(,R1)          R7 -> NEXT SCAN POSITION
         LR    R3,R8              R3 -> END OF LINE
         SR    R3,R7              R3 =  EX LENGTH FOR SCAN
         BM    SCANERR            ERROR IF NO LEN
         BR    R14                EXIT SCANLINE
*
STORLEN1 EQU   *                  STORE OPER LENGTH
         ST    R1,STARTPTR        SAVE DELIM PTR
         S     R1,OPERPTR         R1 = OPERLEN
         L     R10,OPLENPTR       R10 -> OPER LEN FIELD
         STH   R1,0(R10)          SAVE IT
         BR    R14
*
STORLEN2 EQU   *                  STORE SUBFIELD LENGTH
         ST    R1,STARTPTR        SAVE DELIM PTR
         S     R1,SUBPTR          R1 = SUBFLD LENGTH
         L     R10,SUBLENPT       R10 -> SUBLEN FIELD
         STH   R1,0(R10)          SAVE LEN
         BR    R14
*
*
SCANERR  EQU   *
         LR    R1,R9              R1 -> BUFFEND+1
         LTR   R4,R4              IN PARENS?
         BZ    SENM               NO
         LA    R15,12             RC=12 ERROR
         B     STORLEN2
SENM     LA    R15,8              RC= 8 ERROR
         B     STORLEN1
         EJECT
NORMEND  EQU   *
         LR    R1,R9              R1 -> BUFFEND+1
         SLR   R15,R15            RC=0 -> SCAN COMPLETED
         B     STORLEN1           EXIT
*
NMCOMEL  EQU   *                  NORMAL MODE,COMMA,EOL
NORMCOM  EQU   *                  NORMAL MODE,COMMA
         SLR   R15,R15            RC=0 -> SCAN COMPLETED
         B     STORLEN1           EXIT
*
NMLPEL   B     NORMRP             NORMAL MODE,LEFT PAREN,EOL
NORMLP   DS    0H                 NORMAL MODE,LEFT PAREN
         LA    R5,PARNMODE        R5 -> PAREN MODE (MODE SWITCH)
         LA    R4,1(,R4)          R4 = R4+1 (PAREN DEPTH = 1)
         ST    R1,SUBPTR          SAVE START OF SUBFIELD
         LR    R7,R1              R7 -> LEFT PAREN
         S     R7,OPERPTR         R7 = OPERAND LENGTH
         L     R10,OPLENPTR       R10 -> OPER LEN AREA
         STH   R7,0(R10)          SAVE OPER LENGTH
         B     COMSEX1            GO TO COMMON EXIT
*
NMRPEL   EQU   *                  NORMAL MODE,RITE PAREN,EOL
NORMRP   EQU   *                  NORMAL MODE,RITE PAREN
         SLR   R15,R15            RC=0 -> SCAN COMPLETE
         B     STORLEN1           EXIT
*
NORMQEL  EQU   SCANERR            NORMAL MODE,QUOTE,EOL
NORMQ    DS    0H                 NORMAL MODE,QUOTE
         LA    R5,QMODE           CHANGE TO QUOTE MODE
         XC    TRTTABLE,TRTTABLE  CLEAR TRANSLATE TABLE
         MVI   TRTTABLE+125,X'04' QUOTE POSITION IN TABLE
         B     COMSEX1            GO TO COMMON EXIT
*
NORMBEL  EQU   NORMCOM            NORMAL MODE,BLANK,EOL
NORMB    EQU   NORMCOM            NORMAL MODE,BLANK
         EJECT
*
PMEND    EQU   *                  PAREN MODE,EOL (NO VALID DELIM)
         LR    R1,R9              R1 -> BUFFEND+1
         SLR   R15,R15            RC=0 -> SCAN COMPLETED
         B     STORLEN2           EXIT
*
PMCOMEL  EQU   PMEND              PAREN MODE,COMMA,EOL
PMCOM    EQU   COMSEX1            PAREN MODE,COMMA
*
PMLPEL   EQU   PMEND              PAREN MODE,LEFT PAREN,EOL
PMLP     DS    0H                 PAREN MODE,LEFT PAREN
         LA    R4,1(,R4)          R4=R4+1 (PAREN DEPTH)
         B     COMSEX1            GO TO COMMON EXIT
*
PMRPEL   EQU   PMEND              PAREN MODE,RITE PAREN,EOL
PMRP     EQU   *                  PAREN MODE,RITE PAREN
         BCT   R4,COMSEX1         R4=R4-1 (EXIT IF STILL NESTED)
         SLR   R15,R15            RC=0 -> SCAN COMPLETE FLAG
         LA    R1,1(R1)           R1=R1+1 POINT PAST DELIMITER
         B     STORLEN2           GO TO SAVE SUBFLD LEN
*
PMQEL    EQU   SCANERR            PAREN MODE,QUOTE,EOL
PMQ      EQU   NORMQ              PAREN MODE,QUOTE
*
PMBEL    EQU   PMEND              PAREN MODE,BLANK,EOL
PMB      EQU   COMSEX1            PAREN MODE,BLANK
*
QMEND    EQU   SCANERR            QUOTE MODE,EOL (INVALID DELIM)
*
QMQ      DS    0H                 QUOTE MODE,QUOTE
         CLI   1(R1),C''''        DOUBLE QUOTE?
         BE    IGQ                YES ->SKIP IT
         MVC   TRTTABLE,TT        NO -> RESET MODE
         LTR   R4,R4              R4=0? (MODE=NORM)
         BZ    QNS                YES -> QUOTE TO NORM SWITCH
         LA    R5,PARNMODE        NO -> PAREN MODE SWITCH
         B     COMSEX1            GO TO COMMON EXIT
QNS      LA    R5,NORMODE         NORMAL MODE SWITCH
         B     COMSEX1            GO TO COMMON EXIT
IGQ      LA    R1,1(,R1)          R1=R1+1 (IGNORE QUOTES)
         B     COMSEX1            GO TO COMMON EXIT
*
QMQEL    EQU   *                  QUOTE MODE,QUOTE,EOL
         LTR   R4,R4              IN PARENS?
         BZ    NORMEND            NO
         B     PMEND              YES
         TITLE 'OPERSCAN    - WORKAREA AND CONSTANTS'
STATWORK EQU   *
*
TRANTAB1 DC    256X'04'                 OPER SCAN TABLE
         ORG   TRANTAB1+C' '            BLANK
         DC    X'00'
         ORG   TRANTAB1+C','            COMMA
         DC    X'00'
         ORG   TRANTAB1+C')'            RIGHT PAREN
         DC    X'00'
         ORG
         LTORG
         DC    80X'00'                  ZAP AREA
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
L4LS     DS    F
SCANLEN  DS    F
*
TRTTABLE DS    XL256
WORKLEN  EQU   *-WORKAREA
         TITLE 'PARAMETER LIST PASSED TO OPERSCAN SUBROUTINE'
BPPL     DSECT
BUFFPTR  DS    F                        BUFFER PTR
LENPTR   DS    F                        LENGTH PTR
STARTPTR DS    F                        START SEARCH PTR
OPERPTR  DS    F                        NEXT OPER LOC
OPLENPTR DS    F                        NEXT OPER LEN PTR
SUBPTR   DS    F                        SUBFIELD PTR
SUBLENPT DS    F                        SUBFIELD LEN PTF
WORKPTR  DS    F                        WORKAREA MAY BE PASSED HERE
         DS    F
         DS    F
         END
