*
*  4/4/86 - MODIFIED (V2M4) TO DO A WAIT AFTER ATTACH
*
NEWMWILE ENTERR LEVEL=V2M4
         EJECT
         LR    R11,R1                   R11-> PARM LIST
         L     R10,0(,R11)              R10-> PARM FIELD
         SLR   R9,R9                    R9 = 0
         SLR   R8,R8                    R8 = 0
         IC    R9,2(,R10)               R9 = CALL TYPE (0,1,2)
         IC    R8,TT-240(R9)            R8 = BRANCH TAB OFFSET
         B     BTAB(R8)                 PICK AN ENTRY POINT
         SPACE 3
BTAB     B     E0                       BTAB = BRANCH TABLE
         B     E1
         B     E2
         SPACE 3
RET      LEAVER
         EJECT
E1       LOAD  EP=ITCOMA1               R0-> INTER TASK COM AREA 1
         SPACE
         LR    R7,R0                   R7-> "                     WDPSC
         LA    R6,4                    MAX 4 COPIES RUNNING        *KK*
         LA    R5,16(,R7)              R5->START OF TCB TABLE      *KK*
E1L1     CLC   0(4,R5),540(0)          IS OUR TCB?                 *KK*
         BNE   CNS                     NOPE -> TRY NEXT            *KK*
         L     R2,4(,R5)             R2-> SUB TCB               WDPSC
         USING TCB,R2                                           WDPSC
         CLC   0(4,R5),TCBOTC        OURS?                      WDPSC
         DROP  R2                                               WDPSC
         BE    RET                   YUP=QUIT WHILE WE ARE AHEADWDPSC
         B     E1ATT                 REUSE IT                   WDPSC
CNS      LA    R5,8(,R5)               R5->NEXT TABLE SLOT         *KK*
         BCT   R6,E1L1                 GO CHECK IT                 *KK*
         LA    R6,4                    MAX 4 COPIES RUNNING        *KK*
         LA    R5,16(,R7)              R5->START OF TCB TABLE      *KK*
E1L2     CLC   0(4,R5),=F'0'           IS EMPTY SLOT?              *KK*
         BE    E1ATT                   NO->WE CAN USE IT THEN      *KK*
         LA    R5,8(,R5)               R5->NEXT TABLE SLOT         *KK*
         BCT   R6,E1L2                 GO CHECK IT                 *KK*
         B     RET                     NO ROOM IN TABLE           WDPSC
         EJECT                                                     *KK*
E1ATT    LA    R1,PARML                 R1 -> PARM LIST
         LA    R2,CT0                                            V2M4
         ST    R2,PARML                                          V2M4
         LA    R2,MYECB                                          V2M4
         ST    R2,PARML+4                                        V2M4
         OI    PARML+4,X'80'                                     V2M4
         XC    MYECB,MYECB                                       V2M4
         SPACE
         ATTACH EP=NEWMWILE
         SPACE
         ST    R1,4(,R5)                SAVE SUBTASK ADDR
         MVC   0(4,R5),540(0)           SAVE OUR TCB ADDR          *KK*
         WAIT  ECB=MYECB                                         V2M4
         B     RET                      GET OUT
         EJECT
E2       LOAD  EP=ITCOMA1               R0-> INTER TASK COM AREA 1
         SPACE
         LR    R7,R0                   R7-> "                     WDPSC
         LA    R6,4                    MAX 4 COPIES RUNNING        *KK*
         LA    R5,16(,R7)              R5->START OF TCB TABLE      *KK*
E2L1     CLC   0(4,R5),540(0)          IS OUR TCB?                 *KK*
         BE    E2DET                   WE ARE         ACTIVE!      *KK*
         LA    R5,8(,R5)               R5->NEXT TABLE SLOT         *KK*
         BCT   R6,E2L1                 GO CHECK IT                 *KK*
         B     RET                     NOT ACTIVE
E2DET    LA    R1,4(,R5)               R1->TCB ADDR               WDPSC
         SPACE
         L     R2,0(,R1)             R2->SUB TCB                WDPSC
         USING TCB,R2                                           WDPSC
         CLC   0(4,R5),TCBOTC        OURS?                      WDPSC
         DROP  R2                                               WDPSC
         BNE   RET                   NOPE-> NO MORE 23E ABEND!  WDPSC
         SPACE
         DETACH (R1)
         SPACE
         XC    0(8,R5),0(R5)           CLEAR TCB ADDR             WDPSC
         B     RET                      GET OUT
         EJECT
E0       EQU   *
         TM    0(R11),X'80'       2 PARMS?                       V2M4
         BO    SETWAIT            NOPE.                          V2M4
         L     R1,4(R11)                                         V2M4
         POST  (R1)                                              V2M4
SETWAIT  MVC   WAITCNT(2),=H'144'      INIT MAX WAIT COUNT 12 HRS
E0L      XC    MYECB,MYECB              MAKE SURE ECB IS CLEAR!!!!
         SPACE
         STIMER REAL,POSTME,BINTVL=WAITTIME
         SPACE
         WAIT  ECB=MYECB
         SPACE
         LH    R7,WAITCNT               R7=WAIT CNT
         BCT   R7,DOITAGIN              TOO MANY?
         SPACE
ABENDME  ABEND 77,DUMP                  YES->KILL ME
         SPACE
DOITAGIN STH   R7,WAITCNT               SAVE THE CNT
         B     E0L                      START OVER
         SPACE 2
POSTME   DS    0H                       ASYNC EXIT
         USING POSTME,R15
         STM   14,12,12(R13)
         LR    R11,R13                 SAVE R13                   WDPSC
         L     R13,540                 R13->TCB                   WDPSC
         L     R13,112(,R13)           R13->1ST SAVE AREA         WDPSC
         L     R13,8(,R13)             R13->NEWMWILE SAVE AREA    WDPSC
         SPACE 2
         POST  MYECB                    POST THE ECB
         SPACE 2
         LR    R13,R11                 RESORE TO CP R13           WDPSC
         LM    14,12,12(R13)
         BR    R14                      RET TO MAIN
         EJECT
*
*              WORK AREAS:
*
CT0      DC    C'CT0'
TT       DC    X'0004080C1014181C2024'
WAITTIME DC    F'30000'                 5 MIN?
         SPACE 3
WORKAREA DSECT
SAVEAREA DS    18F
PARML    DS    2A
MYECB    DC    F'0'
WAITCNT  DC    H'144'                   12 HRS MAX
WORKLEN  EQU   *-WORKAREA
         IKJTCB
         END
