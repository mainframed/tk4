         PRINT ON,NOGEN
         SPACE 3
*
*        OUTDATED    OUTDATED    OUTDATED    OUTDATED  !!!!
*        --------    --------    --------    --------
*
*        THIS PROGRAM IS NO LONGER TO BE USED.  IT HAS BEEN REPLACED
*        BY THE PROGRAM FILSPACE AND THEN THE PGM FILEATTR AND THE
*        COMMAND FILEINFO INVOKES FILSPACE IN ORDER TO PRODUCE THE
*        USED EXTENT NUMBER AND THE NUMBER OF USED TRACKS
*
*
*
*        THIS PROGRAM IS A SUBROUTINE MEANT TO BE CALLED FROM A CLIST
*        WHICH ACCEPTS A DATASET NAME PASSED TO IT AND THEN PASSES
*        BACK TO THE INVOKER THE NUMBER OF USED EXTENTS THAT HAS
*        BEEN REACHED BY THE DATASET
*
*        TO CALL THIS PROGRAM FROM A CLIST, DO THE FOLLOWING:
*
*              CALL 'WDPSC.SHARED.LOAD(EXTENTNO)' '&DSN'
*
*                     WHERE &DSN CONTAINS THE DATASET NAME
*
*        HOWEVER; THIS PROGRAM CAN BE CALLED FROM A COBOL PROGRAM
*
*        TO CALL THIS PROGRAM FROM A COBOL PROGRAM, DO THE FOLLOWING:
*
*           MOVE YOUR DSNAME TO DSNAME OF EXTENTNO-I-O-AREA.
*           MOVE THE LENGTH OF YOUR DSNAME TO DSNAME-LENGTH
*               OF EXTENTNO-I-O-AREA.
*
*           CALL 'EXTENTNO' USING EXTENTNO-I-O-AREA.
*
*                WHERE:
*
*                      01  EXTENTNO-I-O-AREA.
*                          05  DSNAME-LENGTH      PIC S9(3) COMP.
*                          05  DSNAME             PIC X(44).
*
*
*           AFTER THE CALL, RETURN CODE WILL CONTAIN:
*
*               .  THE NUMBER OF USED EXTENTS
*             OR
*               .  0091 IF THE DATASET IS NOT CATALOGUED
*             OR
*               .  0092 IF THE DATASET IS CONTAINED ON MORE THAN
*                           ONE VOLUME
*             OR
*               .  0093 IF THE DATASET IS ON TAPE
*             OR
*               .  0094 IF THE REQUIRED VOLUME IS NOT MOUNTED
*             OR
*               .  0095 IF THE DATASET IS NOT FOUND ON VOLUME
*                           INDICATED BY THE CATALOGUE
*             OR
*               .  0096 IF THE OBTAIN MACRO RETURNED WITH
*                           RC = 12, 16, OR 20
*             OR
*               .  0097 IF AN ERROR IN VOL2DEVT WAS ENCOUNTERED
*             OR
*               .  0099 IF THE DSNAME PASSED TO THIS PROGRAM
*                           IS INVALID
         EJECT
         SPACE 3
*
**********************************************
*                                            *
*        I N I T I A L I Z A T I O N         *
*                                            *
**********************************************
*
EXTENTNO CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        SAVE REGISTER 1 IN REGISTER 11 AND LOAD PARAMETER FIELD   *
*        INTO PARMAREA                                             *
*                                                                  *
********************************************************************
*
         SPACE 3
STARTIT  DS    0D
         LR    R11,R1              SAVE REGISTER 1 CONTENTS IN REG 11
         L     R9,0(R11)
         MVC   PARMAREA,0(R9)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        M A I N   L I N E   L O G I C                             *
*                                                                  *
********************************************************************
*
         MVC   PROCESSW,=CL3'YES'  MOVE 'YES' TO PROCESSW
*
         BAL   R14,LOKATE          PERFORM ROUTINE TO DO LOCATE
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   GETOUT              IF SO, BRANCH TO GETOUT
         BAL   R14,OBTANE          PERFORM ROUTINE TO DO OBTAIN
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   GETOUT              IF SO, BRANCH TO GETOUT
         BAL   R14,EYEDSCB1        PERFORM ROUTINE TO PROCESS THE DSCB1
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   GETOUT              IF SO, BRANCH TO GETOUT
         CLC   PROCDCB3,=CL3'YES'  DO WE NEED TO PROCESS THE DSCB3 ?
         BNE   GETOUT              IF NOT, BRANCH TO GETOUT
         BAL   R14,EYEDSCB3        PERFORM ROUTINE TO PROCESS THE DSCB3
GETOUT   B     SHUTDOWN            BRANCH TO SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR DOING A LOCATE ON THE     *
*        DATASET NAME                                              *
*                                                                  *
*        IF THE LOCATE MACRO ISSUES A RETURN CODE OF 0 AND         *
*        THE DATASET IS CONTAINED ON ONLY ONE VOLUME AND THE       *
*        DATASET IS NOT ON TAPE, THEN THIS ROUTINE MOVES 'YES'     *
*        TO PROCESSW ELSE IT MOVES 'NO ' TO PROCESSW               *
*                                                                  *
********************************************************************
*
LOKATE   ST    R14,SAVEIT1
         LH    R8,PARMLEN             R8==> LENGTH OF PARM DSNAME
LOCTEST1 CH    R8,=H'3'               IS THE LENGTH OF THE PARM
*                                       DATASET NAME GT 3 ?
         BH    LOCTEST2               IF SO, BRANCH TO LOCTEST2
         LA    R10,99                 R10==> 99
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
         B     EXITLOC                BRANCH TO EXITLOC
LOCTEST2 CH    R8,=H'44'              IS THE LENGTH OF THE PARM
*                                       DATASET NAME LE 44 ?
         BNH   LOADCAM                IF SO, BRANCH TO LOADCAM
         LA    R10,99                 R10==> 99
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
         B     EXITLOC                BRANCH TO EXITLOC
LOADCAM  MVI   CAMDSN,C' '            A BLANK
         MVC   CAMDSN+1(43),CAMDSN    SPREAD IT AROUND
         LA    R1,CAMDSN              R1 ==> ADDRESS OF CAMDSN
         LA    R8,PARMDSN             R8 ==> ADDRESS OF PARMDSN
         LH    R7,PARMLEN             R7 ==> LENGTH OF THE DSN
         S     R7,=F'1'               SUBTRACT 1 FOR EX
         SPACE 1
MOVEDSN  MVC   0(0,R1),0(R8)          MOVE THE PARM DSN TO CAMDSN
         EX    R7,MOVEDSN
         SPACE 1
LOKAYT   LOCATE CAMLIST1
         SPACE 2
         C     R15,=F'0'              IS DATASET CATALOGUED ?
         BE    CATLGD                 IF SO, BRANCH TO CATLGD
         LA    R10,91                 R10==> 91
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
         B     EXITLOC                BRANCH TO EXITLOC
CATLGD   CLC   LOCAREA(2),=XL2'0001'  VERIFY THAT THE NUMBER OF
*                                        VOLUMES COUNT IS 1
         BE    VOLCNT1                IF YES, BRANCH TO VOLCNT1
         LA    R10,92                 R10==> 92
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
         B     EXITLOC                BRANCH TO EXITLOC
VOLCNT1  MVC   OBTVOL,LOCAREA+6       LOAD OBTVOL FROM CAMLST WORKAREA
         CLI   LOCAREA+4,X'80'        COMPARE LOCAREA+4 TO BE EQUAL
*                                       TO HEXADECIMAL 80  (THIS
*                                       INDICATES THAT THE DATASET
*                                       IS ON TAPE)
         BNE   EXITLOC                IF NOT; BRANCH TO EXITLOC
         LA    R10,93                 R10==> 93
         MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
EXITLOC  L     R14,SAVEIT1
         BR    14                     RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR DOING AN OBTAIN ON THE    *
*        DATASET NAME                                              *
*                                                                  *
*        IF THE OBTAIN MACRO ISSUES A RETURN CODE OF 0, THEN       *
*        THIS ROUTINE MOVES 'YES' TO PROCESSW ELSE IT MOVES        *
*        'NO ' TO PROCESSW                                         *
*                                                                  *
********************************************************************
*
OBTANE   ST    R14,SAVEIT2
OBTAAN   OBTAIN CAMLIST2
         SPACE 2
         C     R15,=F'0'              COMPARE REG 15 TO BE = 0
         BE    EXITOBT                IF REG 15=0, BRANCH TO EXITOBT
         C     R15,=F'4'              COMPARE REG 15 TO BE = 4
         BE    RCODE4                 IF REG 15=4, BRANCH TO RCODE4
         C     R15,=F'8'              COMPARE REG 15 TO BE = 8
         BE    RCODE8                 IF REG 15=8, BRANCH TO RCODE8
         LA    R10,96                 R10==> 96
         B     MOVENOPE               BRANCH TO MOVENOPE
RCODE4   LA    R10,94                 R10==> 94
         B     MOVENOPE               BRANCH TO MOVENOPE
RCODE8   LA    R10,95                 R10==> 95
MOVENOPE MVC   PROCESSW,=CL3'NO '     MOVE 'NO ' TO PROCESSW
EXITOBT  L     R14,SAVEIT2
         BR    14                     RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR PROCESSING THE DSCB1.     *
*                                                                  *
*        IF AN ERROR IS ENCOUNTERED, THIS ROUTINE DOES THE         *
*        FOLLOWING:                                                *
*                                                                  *
*             .  MOVES 'NO ' TO PROCESSW                           *
*             .  MOVES APPROPRIATE 9X VALUE TO REGISTER 10         *
*                   WHICH IDENTIFIES THE REASON FOR THE ERROR      *
*                                                                  *
*        IF NO ERROR IS ENCOUNTERED AND THIS ROUTINE CALCULATES    *
*        THAT THE USED EXTENTS DOES NOT EXCEED 3 (HENCE THERE      *
*        IS NO DSCB3), THEN THIS ROUTINE DOES THE FOLLOWING:       *
*                                                                  *
*             .  MOVES 'NO ' TO PROCDCB3                           *
*             .  MOVES APPROPRIATE VALUE TO REGISTER 10            *
*                   (I.E 1, 2 OR 3)                                *
*                                                                  *
*        IF NO ERROR IS ENCOUNTERED AND THIS ROUTINE CALCULATES    *
*        THAT THE USED EXTENTS DOES EXCEED 3 AND THAT IT NEEDS TO  *
*        PROCESS THE DSCB3, THEN THIS ROUTINE DOES THE FOLLOWING:  *
*                                                                  *
*             .  MOVES 'YES' TO PROCDCB3                           *
*             .  MOVES 3 TO REGISTER 10                            *
*                   (TO BE ADDED TO IN EYEDSCB3)                   *
*                                                                  *
********************************************************************
*
EYEDSCB1 ST    R14,SAVEIT3
*
         MVC   PROCDCB3,=CL3'NO '  MOVE 'NO ' TO PROCDCB3
         LA    R10,0               R10 ==> 0
         ZAP   RUNTOT,PACKED0      RUNTOT = 0
*
         MVC   DS1LSTAR,OBTAREA+54   ---
         MVC   HALFWORD(2),DS1LSTAR    ---
         LH    R5,HALFWORD               ---- LSTRTRKS IS IN PACKED
         CVD   R5,CVDOPRND              ---   DECIMAL AND CONTAINS
         MVC   LSTRTRKS(3),CVDOPRND+5 ---     TOTAL NO OF USED TRACKS
         MVC   VOLSER,OBTVOL
*
         CLI   OBTAREA+61,X'00'    IS 1ST EXTENT DESC INSIGNIFICANT ?
         BE    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         LA    R10,1               R10 ==> 1
         MVC   BGNCCHH(4),OBTAREA+63
         MVC   ENDCCHH(4),OBTAREA+67
         BAL   R14,CALCTRKS        PERFORM ROUTINE TO CALCULATE
*                                  NUMBER OF TRACKS USED IN THIS EXTENT
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   EXITDCB1            IF SO, BRANCH TO EXITDCB1
         AP    RUNTOT,TOTRACKS     RUNTOT = TOTAL NUMBER OF USED TRACKS
*                                      TO THIS POINT
         CP    LSTRTRKS,RUNTOT     DOES THIS EXTENT PUT US PAST THE
*                                      DS1LSTAR VALUE ?
         BL    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         CLI   OBTAREA+71,X'00'    IS 2ND EXTENT DESC INSIGNIFICANT ?
         BE    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         LA    R10,2               R10 ==> 2
         MVC   BGNCCHH(4),OBTAREA+73
         MVC   ENDCCHH(4),OBTAREA+77
         BAL   R14,CALCTRKS        PERFORM ROUTINE TO CALCULATE
*                                  NUMBER OF TRACKS USED IN THIS EXTENT
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   EXITDCB1            IF SO, BRANCH TO EXITDCB1
         AP    RUNTOT,TOTRACKS     RUNTOT = TOTAL NUMBER OF USED TRACKS
*                                      TO THIS POINT
         CP    LSTRTRKS,RUNTOT     DOES THIS EXTENT PUT US PAST THE
*                                      DS1LSTAR VALUE ?
         BL    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         CLI   OBTAREA+81,X'00'    IS 3RD EXTENT DESC INSIGNIFICANT ?
         BE    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         LA    R10,3               R10 ==> 3
         MVC   BGNCCHH(4),OBTAREA+83
         MVC   ENDCCHH(4),OBTAREA+87
         BAL   R14,CALCTRKS        PERFORM ROUTINE TO CALCULATE
*                                  NUMBER OF TRACKS USED IN THIS EXTENT
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   EXITDCB1            IF SO, BRANCH TO EXITDCB1
         AP    RUNTOT,TOTRACKS     RUNTOT = TOTAL NUMBER OF USED TRACKS
*                                      TO THIS POINT
         CP    LSTRTRKS,RUNTOT     DOES THIS EXTENT PUT US PAST THE
*                                      DS1LSTAR VALUE ?
         BL    EXITDCB1            IF SO, BRANCH TO EXITDCB1
         MVC   PROCDCB3,=CL3'YES'  MOVE 'YES' TO PROCDCB3
         MVC   CCHHR,OBTAREA+91    MOVE CCHHR PTR FOR DSCB3 TO CCHHR
EXITDCB1 L     R14,SAVEIT3
         BR    14                  RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        THIS ROUTINE IS RESPONSIBLE FOR PROCESSING THE DSCB3      *
*        AND THEN GENERATING THE EXTENT NUMBER WHICH IS TO BE      *
*        CONTAINED IN REGISTER 10.                                 *
*                                                                  *
********************************************************************
*
EYEDSCB3 ST    R14,SAVEIT4
DODSCB3  OBTAIN CAMLIST3
         SPACE 2
         C     R15,=F'0'              COMPARE REG 15 TO BE = 0
         BE    COUNTEM                IF REG 15=0, BRANCH TO COUNTEM
         C     R15,=F'4'              COMPARE REG 15 TO BE = 4
         BE    DSCB3RC4               IF REG 15=4, BRANCH TO DSCB3RC4
         C     R15,=F'8'              COMPARE REG 15 TO BE = 8
         BE    DSCB3RC8               IF REG 15=8, BRANCH TO DSCB3RC8
         LA    R10,96                 R10==> 96
         B     EXITDCB3               BRANCH TO EXITDCB3
DSCB3RC4 LA    R10,94                 R10==> 94
         B     EXITDCB3               BRANCH TO EXITDCB3
DSCB3RC8 LA    R10,95                 R10==> 95
         B     EXITDCB3               BRANCH TO EXITDCB3
COUNTEM  LA    R5,DCB3AREA            R5 ==> ADDRESS OF DSCB3
         A     R5,=F'4'               OFFSET TO DS3EXTNT
         LA    R6,1
LOOP3A   C     R6,=F'4'
         BH    NXTGROUP
         CLI   0(R5),X'00'
         BE    EXITDCB3
         A     R10,=F'1'
         MVC   BGNCCHH(4),2(R5)
         MVC   ENDCCHH(4),6(R5)
         BAL   R14,CALCTRKS        PERFORM ROUTINE TO CALCULATE
*                                  NUMBER OF TRACKS USED IN THIS EXTENT
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   EXITDCB3            IF SO, BRANCH TO EXITDCB3
         AP    RUNTOT,TOTRACKS     RUNTOT = TOTAL NUMBER OF USED TRACKS
*                                      TO THIS POINT
         CP    LSTRTRKS,RUNTOT     DOES THIS EXTENT PUT US PAST THE
*                                      DS1LSTAR VALUE ?
         BL    EXITDCB3            IF SO, BRANCH TO EXITDCB3
         A     R6,=F'1'
         A     R5,=F'10'
         B     LOOP3A
NXTGROUP A     R5,=F'1'            OFFSET TO DS3ADEXT
         LA    R6,1
LOOP3B   C     R6,=F'9'
         BH    EXITDCB3
         CLI   0(R5),X'00'
         BE    EXITDCB3
         A     R10,=F'1'
         MVC   BGNCCHH(4),2(R5)
         MVC   ENDCCHH(4),6(R5)
         BAL   R14,CALCTRKS        PERFORM ROUTINE TO CALCULATE
*                                  NUMBER OF TRACKS USED IN THIS EXTENT
         CLC   PROCESSW,=CL3'YES'  WERE ANY ERRORS ENCOUNTERED ?
         BNE   EXITDCB3            IF SO, BRANCH TO EXITDCB3
         AP    RUNTOT,TOTRACKS     RUNTOT = TOTAL NUMBER OF USED TRACKS
*                                      TO THIS POINT
         CP    LSTRTRKS,RUNTOT     DOES THIS EXTENT PUT US PAST THE
*                                      DS1LSTAR VALUE ?
         BL    EXITDCB3            IF SO, BRANCH TO EXITDCB3
         A     R6,=F'1'
         A     R5,=F'10'
         B     LOOP3B
EXITDCB3 L     R14,SAVEIT4
         BR    14                  RETURN
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS ROUTINE CALCULATES THE NUMBER OF TRACKS GIVEN           *
*        A BEGINNING CCHH, AN ENDING CCHH AND THE VOLUME SERIAL       *
*        NUMBER                                                       *
*                                                                     *
*        BEFORE PERFORMING THIS ROUTINE, DO THE FOLLOWING:            *
*                   MOVE THE BEGINNING CCHH TO BGNCCHH                *
*                   MOVE THE ENDING CCHH TO ENDCCHH                   *
*                   MOVE THE VOL SER TO VOLSER                        *
*                                                                     *
*        AFTER PERFORMING THIS ROUTINE, THE NUMBER OF USED            *
*        TRACKS WILL BE IN TOTRACKS                                   *
*                                                                     *
***********************************************************************
*
CALCTRKS ST    R14,SAVEIT5        STORE R14 CONTENTS IN SAVEIT5
         LH    R7,BGNCCHH              ----
         CVD   R7,CVDOPRND               ----- PACK BEGINNING CYL
         MVC   BEGINCYL(3),CVDOPRND+5  ----
         LH    R7,ENDCCHH              ----
         CVD   R7,CVDOPRND               ----- PACK ENDING CYL
         MVC   ENDCYL(3),CVDOPRND+5    ----
         LH    R7,BGNCCHH+2            ----
         CVD   R7,CVDOPRND               ----- PACK BEGINNING TRK
         MVC   BEGINTRK(3),CVDOPRND+5  ----
         LH    R7,ENDCCHH+2            ----
         CVD   R7,CVDOPRND               ----- PACK ENDING TRK
         MVC   ENDTRK(3),CVDOPRND+5    ----
         ZAP   CYLDIFF,ENDCYL          CYLDIFF = ENDING CYL #
         SP    CYLDIFF,BEGINCYL        CYLDIFF = ENDING CYL - BEGIN CYL
         BAL   14,DETRKCYL             THIS ROUTINE EXAMINES VOL SER
*                                          TO DETERMINE THE # OF TRACKS
*                                          PER CYLINDER AND PLACES THE
*                                          ANSWER IN TRKPRCYL
         CP    ENDTRK,BEGINTRK         IF ENDING TRK # > BEGINNING TRK
         BH    ENDISGT                    # THEN BRANCH TO ENDISGT
         ZAP   TRKDIFF,ENDTRK          TRKDIFF = ENDING TRACK #
         SP    TRKDIFF,BEGINTRK        TRKDIFF = ENDING TRK - BEGIN TRK
         AP    TRKDIFF,PACKED1         ADD 1 TO TRKDIFF
         B     DOCALC                  BRANCH TO DOCALC
ENDISGT  SP    CYLDIFF,PACKED1         SUBTRACT 1 FROM CYLDIFF
         ZAP   REMANTRK,TRKPRCYL       REMANTRK = TRKPRCYL
         SP    REMANTRK,BEGINTRK       REMANTRK = TRKPRCYL - BEGINTRK
         ZAP   TRKDIFF,REMANTRK        TRKDIFF = REMANTRK
         AP    TRKDIFF,ENDTRK          TRKDIFF = REMANTRK + ENDTRK
         AP    TRKDIFF,PACKED1         ADD 1 TO TRKDIFF
DOCALC   ZAP   TOTRACKS,TRKPRCYL       TOTRACKS = TRKPRCYL
         MP    TOTRACKS,CYLDIFF        TOTRACKS = TRKPRCYL X CYLDIFF
         AP    TOTRACKS,TRKDIFF        ADD TRKDIFF TO TOTRACKS
         L     R14,SAVEIT5
         BR    R14                     RETURN
         EJECT
         SPACE 3
***********************************************************************
*                                                                     *
*        THIS ROUTINE EXAMINES VOL SER TO DETERMINE THE DEVICE TYPE   *
*        IN ORDER TO DETERMINE THE NUMBER OF TRACKS PER CYLINDER      *
*                                                                     *
*        BEFORE PERFORMING THIS ROUTINE, MOVE THE VOLUME SERIAL       *
*        NUMBER TO VOLSER                                             *
*                                                                     *
*        AFTER PERFORMING THIS ROUTINE, THE NUMBER OF TRACKS PER      *
*        CYLINDER IS IN TRKPRCYL                                      *
*                                                                     *
*        IF AN ERROR IS ENCOUNTERED IN THIS ROUTINE, IT WILL          *
*        MOVE 'NO ' TO PROCESSW AND LOAD REGISTER 10 WITH 97          *
*                                                                     *
***********************************************************************
*
DETRKCYL ST    R14,SAVEIT6        STORE R14 CONTENTS IN SAVEIT6
         MVC   INVOL,VOLSER       MOVE VOL SER TO INVOL
         CALL  VOL2DEVT,(DEVTAREA),VL
         C     R15,=F'0'          WAS VOL2DEVT SUCCESSFUL ?
         BE    EYEDEVT            IF SO, BRANCH TO EYEDEVT
         MVC   PROCESSW,=CL3'NO ' MOVE 'NO ' TO PROCESSW
         LA    R10,97             R10 ==> 97
         B     EXITDETR           BRANCH TO EXITDETR
EYEDEVT  CLC   OUTDEVT(4),=CL4'3330'
         BE    D3330
         CLC   OUTDEVT(4),=CL4'3350'
         BE    D3350
         CLC   OUTDEVT(4),=CL4'3380'
         BE    D3380
         MVC   PROCESSW,=CL3'NO ' MOVE 'NO ' TO PROCESSW
         LA    R10,97             R10 ==> 97
         B     EXITDETR           BRANCH TO EXITDETR
D3330    ZAP   TRKPRCYL,PACKED19  MOVE 19 TO TRKPRCYL
         B     EXITDETR           BRANCH TO EXITDETR
D3350    ZAP   TRKPRCYL,PACKED30  MOVE 30 TO TRKPRCYL
         B     EXITDETR           BRANCH TO EXITDETR
D3380    ZAP   TRKPRCYL,PACKED15  MOVE 15 TO TRKPRCYL
EXITDETR L     R14,SAVEIT6
         BR    R14                RETURN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        E N D  O F   J O B   P R O C E S S I N G                  *
*                                                                  *
********************************************************************
*
SHUTDOWN LR    R15,R10
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        BREAKDOWN OF PARMAREA                                     *
*                                                                  *
********************************************************************
*
         DS    0F
PARMAREA DS    0CL46
PARMLEN  DS    CL2
PARMDSN  DS    CL44
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        V O L 2 D E V T   L I N K A G E   A R E A                 *
*                                                                  *
********************************************************************
*
DEVTAREA DS    0CL16
INVOL    DS    CL6
OUTDEVT  DS    CL8
OUTLEN   DS    CL2
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        D A T A   A R E A S                                       *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
SAVEIT1  DS    F
SAVEIT2  DS    F
SAVEIT3  DS    F
SAVEIT4  DS    F
SAVEIT5  DS    F
SAVEIT6  DS    F
         DS    0F
CAMDSN   DS    CL44
CAMLIST1 CAMLST NAME,CAMDSN,,LOCAREA
LOCAREA  DS    0D
         DS    265C
         DS    0D
CAMLIST2 CAMLST SEARCH,CAMDSN,OBTVOL,OBTAREA
OBTAREA  DS    0D
         DS    140C
         DS    0D
OBTVOL   DS    CL6
         DS    0D
CAMLIST3 CAMLST SEEK,CCHHR,OBTVOL,DCB3AREA
DCB3AREA DS    0D
         DS    140C
PROCESSW DS    CL3
PROCDCB3 DS    CL3
CCHHR    DS    CL5
BEGINCYL DS    PL3
BEGINTRK DS    PL3
BGNCCHH  DS    F
CVDOPRND DS    D
CYLDIFF  DS    PL3
DS1LSTAR DS    CL3
ENDCCHH  DS    F
ENDCYL   DS    PL3
ENDTRK   DS    PL3
HALFWORD DS    H
LSTRTRKS DS    PL3
PACKED0  DC    PL2'0'
PACKED1  DC    PL2'1'
PACKED15 DC    PL2'15'
PACKED19 DC    PL2'19'
PACKED30 DC    PL2'30'
REMANTRK DS    PL3
RUNTOT   DS    PL6
TOTRACKS DS    PL6
TRKDIFF  DS    PL3
TRKPRCYL DS    PL3
VOLSER   DS    CL6
         END
