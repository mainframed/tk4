*
**************
*  PASSDEST  *
**************
*
*        THIS IS A PROGRAM TO LOCATE THE DEFAULT DESTINATION FOUND IN
*        THE PSCB (TSO PROTECTED STEP CONTROL BLOCK) AND THEN VPUT IT
*        TO THE SHARED VARIABLE POOL WITH VARIABLE NAME &DEFDEST
*
         EJECT
         SPACE 3
*
**********************************************
*                                            *
*        I N I T I A L I Z A T I O N         *
*                                            *
**********************************************
*
PASSDEST CSECT
         SAVE  (14,12)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         B     STARTIT
         SPACE
SAVEAREA DC    18F'0'
         SPACE 3
********************************************************************
*        EQUATE RESISTERS 0 THRU 15 TO MORE MEANINGFUL NAMES       *
********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        M A I N   L I N E   L O G I C                             *
*                                                                  *
********************************************************************
*
STARTIT  CALL  ISPLINK,(VDEFINE,SPFDEST,THEDEST,CHAR,DESTLEN),VL
         L     R9,16            R9 ==> ADDRESS OF THE CVT
         L     R6,0(R9)         R6 ==> ADDRESS OF A DOUBLE WORD
*                                        POINTING TO THE TCB
         LA    R5,TCBDWRD       R5 ==> ADDRESS OF DOUBLE WORD TCBDWRD
         MVC   0(8,R5),0(R6)    MOVE TCB ADDRESSES TO TCBDWRD
         L     R8,TCBDWRD+4     R8 ==> ADDRESS OF THE TCB
         L     R7,180(R8)       R7 ==> ADDRESS OF THE JSCB
         L     R6,264(R7)       R6 ==> ADDRESS OF THE PSCB
         MVC   THEDEST,40(R6)   MOVE DEFAULT DESTIN TO THEDEST
         CLI   THEDEST,X'00'    IS FIRST BYTE OF PRINT DEST = X'00'?
         BNE   DOVPUT           IF NOT, BRANCH TO DOVPUT
         MVC   THEDEST,=CL8'LOCAL   '
DOVPUT   CALL  ISPLINK,(VPUT,SPFDEST,SHARED),VL
         B     SHUTDOWN
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        E N D  O F   J O B   P R O C E S S I N G                  *
*                                                                  *
********************************************************************
*
SHUTDOWN LA    R15,0               SET RC = 0
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         SPACE 3
*
********************************************************************
*                                                                  *
*        D A T A   A R E A S                                       *
*                                                                  *
********************************************************************
*
         SPACE 3
         DS    0F
TCBDWRD  DS    D
THEDEST  DS    CL8
VDEFINE  DC    CL7'VDEFINE'
VPUT     DC    CL4'VPUT'
CHAR     DC    CL4'CHAR'
DESTLEN  DC    F'8'
SPFDEST  DC    CL9'(DEFDEST)'
SHARED   DC    CL6'SHARED'
         END
