XJ22SHOW TITLE     'TSO STATUS/CANCEL EXIT'
***********************************************************************
*                                                                     *
*        TSO REQUESTS FOR JOB OWNERSHIP VERIFICATION COMES THRU HERE. *
*                                                                     *
*        FOR STATUS...                                                *
*          A REQUEST FOR STATUS FOR A SPECIAL LENGTH USERID WILL BE   *
*          TREATED AS  THE SHOW COMMAND. INFORMATION IN THE SJB AND   *
*          SSOB WILL BE USED TO SELECT PARTICULAR JQE'S WHICH WILL    *
*          BE COPIED INTO THE SSOB? AREA. THE ROUTINE WILL BE EXITED  *
*          WITH RETURN CODE 16, WHEREUPON THE SHOW COMMAND CODE WILL  *
*          INTERPRET THE JQE'S.                                       *
*                                                                     *
*        INPUT PARAMETERS                                             *
*                                                                     *
*        MAINTENANCE HISTORY:                                         *
*          JET-D1, CHANGES FOR MVS/XA JES2 VERSION 2.1.5.  JET 12/19/86
*                                                                     *
***********************************************************************
         EJECT                                                   JET-D1
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - RETURN SINGLE JQE IN R1 MATCHING REQUEST           *
*        RC = 12 - MUTIPLE JQES FOUND; CALL AGAIN; R1JQE = JQE ADDR   *
*                - MUTIPLE JQES FOUND; ALL RETURNED; R1JQE = 0        *
*        RC = 16 - ALL PROCESSING DONE BY EXIT                        *
*        RC = 20 - JOB NOT FOUND                                      *
*        RC = 24 - JOBNAME/JOB NUMBER MISMATCH                        *
*        RC = 28 - DUPLICATE JOBS                                     *
*        RC = 32 - STATUS BUFFER TOO SMALL                            *
*        RC = 36 - JOB ON OUTPUT QUEUE (USE "CANCEL JOBNAME,PURGE")   *
*        RC = 40 - INVALID CANCEL REQUEST (PERMISSION DENIED)         *
*                                                                     *
***********************************************************************
         SPACE     1
         EJECT
         COPY  $HASPGBL
         EJECT
XJ22SHOW $MODULE   SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                X
               ENVIRON=JES2,                                           X
               TITLE='TSO STATUS/CANCEL',                              X
               RPL,                    MVS RPL DSECT             JET-D1X
               SSOB,                   SUBSYSTEM OUTPUT BLOCK          X
               $BUFFER,                HASP BUFFER                     X
               $CAT,                   CLASS ATTRIBUTE TABLE           X
               $HASPEQU,               HASP EQUATES                    X
               $HCT,                   HASP CONTROL TABLE              X
               $JCT,                   JOB CONTROL TABLE               X
               $JOE,                   JOB OUTPUT ELEMENT              X
               $JOT,                   JOB OUTPUT TABLE                X
               $JQE,                   JOB QUEUE ENTRY                 X
               $MIT,                   MODULE IDENTIFICATION TABLE     X
               $PCE,                   PROCESSOR CONTROL ELEMENT       X
               $QSE,                   SYSTEM DEFINITION               X
               $RDRWORK,               READER PCE WORK AREA            X
               $SCAT,                  SYSOUT CLASS ATTRIBUTE TABLE    X
               $SJB,                   SUBSYSTEM JOB BLOCK             X
               $TQE,                   TIMMER QUEUE ELEMENT      JET-D1X
               $TRP,                   TRACE PARAMETER LIST            X
               $XECB,                  EXTENDED EVENT CONTROL BLOCK    X
               $XEQWORK,               PCE COMMAND WORK AREA           X
               $USERCBS                USER CONTROL BLOCKS
         SPACE     3
*        SPECIAL EQUATES
QUENEXT  EQU       JQENEXT-JQE                                   JET-D1
         TITLE     '&TITLEID -- ENTRY CONVENTIONS'
***********************************************************************
*                                                                     *
*        MAIN ENTRY POINT OF THE EXIT                                 *
*                                                                     *
***********************************************************************
         SPACE     1
X22SHOW  $ENTRY    BASE=R12
         SPACE     1
         $SAVE     TRACE=YES           SAVE CALLER'S REGISTERS
         LR        R12,R15             LOAD BASE REGISTER
         L         R8,0(,R1)           POINT AT SUBSYSTEM JOB BLOCK
         USING     SJB,R8              ANNOUNCE ADDRESSIBILITY   JET-D1
         SPACE     1
         $UCT      R9,TROUBLE=EXITRC00
         SPACE     3
         MEMDUMP   ID=7,DATA=SJB,LEN=SJBSIZE,NAME=T22-SJB        JET-D1
         SPACE     3
*        STATUS OR CANCEL?
*
         CLI       SJBTFUNC+1,SSOBSTAT STATUS REQUEST
         BNE       EXITRC00            NO
         TITLE     '&TITLEID -- TSO STATUS REQUEST'
***********************************************************************
*                                                                     *
* THE HIGH-ORDER BIT IN SJBTULEN (SSCSULEN) IS SET FOR REQUESTS       *
* COMING FROM COMMAND  SHOW . AFTER FINDING THAT THIS IS OUR COMMAND, *
* FURTHER CHECKING IS DONE TO DEFINE WHAT KIND OF INFORMATION IS      *
* BEING SOUGHT.                                                       *
*                                                                     *
*    SJBTULEN                                                         *
* SS22SHOW   1... ....   SHOW COMMAND REQUEST                         *
* SS22RVRS   .1.. ....   REVERSE GENERIC REQUEST                      *
* SS22LENG   .... XXXX   LENGTH OF GENERIC REQUEST, FORWARD OR REVERSE*
*                                                                     *
*   SJBTFLGS             FROM SSCSFLGS                                *
* SSCSUSID  1... ....    USER ID IN SJBTJNAM                          *
*                                                                     *
***********************************************************************
         TM    SJBTULEN,X'80'      IS THIS A SHOW REQUEST?
         BZ    EXITRC00            PHONY BIT NOT ON
         SPACE 4
         XC    SJBFWORD,SJBFWORD
         SLR   R15,R15            GET LENGTH LOADED
         ICM   R15,1,SJBTULEN             AND PREPARED
         N     R15,=F'15'                      FOR LATER
         ST    R15,SJBDWORK       SAVE USER ID LENGTH
         TM    SJBTULEN,X'C0'       REVERSE FLAG ON
         BO    XJ22A300                  YES
         TM    SJBTFLGS,SSCSUSID    SPECIFIC USER ID
         BZ    XJ22A100                  NO
         LTR   R15,R15             ANY LENGTH TO THE USERID
         BZ    XJ22SERC              SEARCH ON USERID, ALL FIELDS ZERO
         LA    R15,4
         ST    R15,SJBFWORD        STORE INDEX IN WORK AREA
         B     XJ22SERC
XJ22A100 EQU   *
         LTR   R15,R15             ANY LENGTH TO THE JOBNAME
         BZ    XJ22A200            NO, USE JOBID ONLY
         LA    R15,12
         ST    R15,SJBFWORD        STORE INDEX IN WORK AREA
         B     XJ22SERC
XJ22A200 EQU   *
         LA    R15,8               JOB ID SEARCH
         ST    R15,SJBFWORD        STORE INDEX IN WORK AREA
         B     XJ22SERC
XJ22A300 EQU   *
         TM    SJBTFLGS,SSCSUSID   USER ID REQUEST
         BO    EXITRC00
         LTR   R15,R15             USERID LENGTH
         BZ    EXITRC00                HAS TO BE
         LA    R15,16            REVERSE GENERIC JOBNAME
         ST    R15,SJBFWORD
         B     XJ22SERC
         SPACE 5
XJ22SERC EQU   *
*       SEARCH  JOB QUEUE
         SLR   R15,R15                     PREPARE
         ST    R15,XEQWORK                    TO SCAN
         LA    R1,$JQHEADS-$JQHEADL-QUENEXT(R15) ACTIVE QUEUES
         SPACE
         L     R7,SJBTAREA          LOCATION OF AREA TO FILL IN
         LA    R6,JQEBLEN           INDEX LENGTH                 JET-D1
         CH    R6,SJBTDIMP          JQE LENGTH TO SIZE
         BNL   EXITRC00   GET OUT, PROBABLY DESERVES A MESSAGE
         LA    R2,0(R6,R7)            ALSO
         ST    R7,SJBDWORK+4          SAVE FOR TRACING
         ST    R6,0(,R7)           SET LENGTH OF INFORMATION ENTRY
         AH    R7,SJBTDIMP              ADD LENGTH OR AREA
         SR    R7,R6                DECREASE LIMIT BY INDEX LENGTH
         SR    R7,R6                         TWICE
         LR    R4,R6                LOAD FOR MOVE
         BCTR  R4,0                      DECREASE BY 1
         L     R3,SJBFWORD         BRANCH
         B     XJ22SCN
         SPACE 4
XJ22SCN0 EQU   *
         L     R15,XEQWORK         RELOAD INDEX INTO QUEUE HEADERS
         LA    R15,$JQHEADL(,R15)  ADD TO INDEX INT0 QUEUE HEADERS
         CL    R15,=A($JQTYPES*$JQHEADL) ANOTHER QUEUE?
         BH    XJQUEND
         ST    R15,XEQWORK         SAVE NEW QUEUE HEAD OFFSET
         LA    R1,$JQHEADS-$JQHEADL-QUENEXT(R15) PREPARE TO SCAN
         SPACE 1
         USING JQE,R1                                            JET-D1
XJ22SCN  EQU   *
         L     R1,JQENEXT          GET OFFSET OF NEXT JQE
         N     R1,$ZEROFFF         END OF THIS QUEUE
         BZ    XJ22SCN0            FIND NEXT QUEUE OF JQE'S
         AL    R1,$JOBQPTR         CONVERT OFFSET TO ABSOLUTE ADDRESS
         CLI   JQETYPE,$FREE       IF JQE IS FREE,
         BE    XJ22SCN                    THEN SKIP IT
         MEMDUMP   ID=7,DATA=JQE,LEN=JQEBLEN,NAME=T22-JQEA       JET-D1
         B     XJ22C944(R3)
XJ22C944 EQU   *
         B     XJ22C100           0 USER ID/ ACF2 REQUEST
         B     XJ22C200           4 USE TSO TIOT NAME AS USERID
         B     XJ22C300           8 JOB ID ONLY
         B     XJ22C400          12 GENERIC JOB NAME SEARCH
         B     XJ22C500          16 REVERSE GENERIC SEARCH
         SPACE 3
XJ22C100 EQU   *           USER ID TO  JQEOWNER MATCH
         CLC   SJBTJOBN,JQEOWNER
         BNE   XJ22SCN
         B     XJ22MOVE           FOUND ONE
         SPACE 3
XJ22C200 EQU   *      SEARCH BASED ON OS KNOWN TSO NAME
         CLC   SJBTJOBN(2),JQEJNAME
         BNE   XJ22SCN
         CLC   SJBTJOBN+2(5),JQEJNAME+3
         BNE   XJ22SCN
         B     XJ22MOVE
         SPACE 3
XJ22C300 EQU   *          SEARCH ON JOB ID/NUMBER
         CLC   SJBTJOBI+2(2),JQEJOBNO
         BNE   XJ22SCN
         B     XJ22MOVE
         SPACE 3
XJ22C400 EQU   *          GENERIC JOB NAME SEARCH
         L     R15,SJBDWORK
         BCTR  R15,0
         EX    R15,XJ22CLC2         CLC  SJBTJOBN,JQEJNAME
         BNE   XJ22SCN
         B     XJ22MOVE             YES, GOOD WORK
XJ22CLC2 CLC   SJBTJOBN(0),JQEJNAME
         SPACE 3
XJ22C500 EQU   *
         LA    R15,8
         L     R5,SJBDWORK          GET THE LENGTH
         SR    R15,R5               GET THE OFFSET IN THE JQE
         LA    R15,JQEJNAME(R15)    POINT INTO JOBNAME
         BCTR  R5,0
         EX    R5,XJ22CLC3    CLC   SJBTJOBN,0(R15)
         BNE   XJ22SCN
         B     XJ22MOVE
XJ22CLC3 CLC   SJBTJOBN(0),0(R15)
         SPACE 3
XJ22MOVE EQU   *
         MEMDUMP   ID=7,DATA=JQE,LEN=JQEBLEN,NAME=T22-JQEM       JET-D1
**
*COPY THE JQE INTO THE AREA POINTED TO BY THE SSOB AND $SJB
*
         EX   R4,XJMOVEIT
************* MVC  0(0,R2),JQE       MOVE THE JQE
         BXH  R2,R6,XJQUEND            OUTPUT AREA IS FULL
         B    XJ22SCN
XJMOVEIT MVC  0(0,R2),JQE       MOVE THE JQE
         SPACE 3
XJQUEND  EQU  *
*        S    R2,SJBDWORK+4    END - BEGINNING = LENGTH
*        STH  R2,SJBTDIMR      SEND LENGTH BACK TO REQUESTOR
         L    R15,$QSE1        LOCATION OF SYSTEM AFFINITY IDS
         LH   R1,=AL2(QSELEN)  LENGTH OF QSE ENTRY
         L    R2,SJBDWORK+4    BEGINNING OF RETURN AREA
         LA   R3,7
         USING QSE,R15                                           JET-D1
XJQSEEND EQU  *
         MVC  16(L'QSESID+2,R2),QSESID   COPY NAME OF SYSTEM
         AR   R15,R1                ADD LENGTH OF QSE
         LA   R2,6(,R2)             ADD LENGTH OF QSEID
         BCT  R3,XJQSEEND           DO IT SEVEN TIMES
         SLR  R1,R1                    SIGNAL END OF QUEUE
         B    EXITRC16                   AND MULTIPLE REQUEST
         EJECT
         TITLE     '&TITLEID -- EXIT'
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - RETURN SINGLE JQE IN R1 MATCHING REQUEST           *
*        RC = 12 - MUTIPLE JQES FOUND; CALL AGAIN; R1JQE = JQE ADDR   *
*                - MUTIPLE JQES FOUND; ALL RETURNED; R1JQE = 0        *
*        RC = 16 - ALL PROCESSING DONE BY EXIT                        *
*        RC = 20 - JOB NOT FOUND                                      *
*        RC = 24 - JOBNAME/JOB NUMBER MISMATCH                        *
*        RC = 28 - DUPLICATE JOBS                                     *
*        RC = 32 - STATUS BUFFER TOO SMALL                            *
*        RC = 36 - JOB ON OUTPUT QUEUE (USE "CANCEL JOBNAME,PURGE")   *
*        RC = 40 - INVALID CANCEL REQUEST (PERMISSION DENIED)         *
*                                                                     *
***********************************************************************
         SPACE     1
EXITRC00 EQU       *
         MEMDUMP   ID=7,DATA=SJB,LEN=SJBSIZE,NAME=T22-R00       JET-D1
        $RETURN    RC=0,TRACE=YES      RETURN TO JES2
         SPACE     3
EXITRC16 EQU       *
         REGTRACE  ID=3,NAME=T22EXIT
         MEMDUMP   ID=7,DATA=SJB,LEN=SJBSIZE,NAME=T22-R16
         L         R5,SJBTAREA
         MEMDUMP   ID=7,DATA=(R5),LEN=SJBTDIMP,NAME=T22-T16
        $RETURN    RC=16,TRACE=YES     RETURN TO JES2
         SPACE     5
         LTORG     ,
         EJECT
        $MODEND    ,
         END       ,
