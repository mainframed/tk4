*          DATA SET 761KKMSTAT AT LEVEL 001 AS OF 10/13/80
*          DATA SET SW2MEMSTAT AT LEVEL 001 AS OF 07/01/74
         MACRO
&LAB     PUTIT &MESS
         LCLC  &L
&L       SETC  'L'''
&LAB     TPUT  M&SYSNDX,&L.M&SYSNDX
         B     N&SYSNDX
M&SYSNDX DC    C&MESS
N&SYSNDX DS    0H
         MEND
*                        MEMSTAT
* THE MEMSTAT PROGRAM WELL GO OUT TO A LIBRARY AND LOOK FOR A PITICULAR
* MEMBER
MEMSTAT  CSECT
PARMLIST IKJPARM
NAME     IKJPOSIT DSNAME,LIST,PROMPT='ENTER LIBRARY(MEMER) TO CHECK',  X
               HELP='LIBRARY(MEMBER)'
PRTL     IKJKEYWD  DEFAULT='PRINT'
         IKJNAME  'PRINT'
         IKJNAME  'NOPRINT'
         IKJENDP
MEMBERS  ENTER (14,12),11,(,WORKLEN)
         LR    R2,R1
         USING CPPL,R2             GET CONTROL
         USING WORKAREA,R13        SAVE REGS
         L     R7,CPPLUPT       *KK*
         MVC   USID(8),16(R7)   *KK*
         L     R7,PPLIST
         ST    R7,PPLPCL
         SR    R7,R7
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         MVC   PPLUPT,CPPLUPT
         LA    R1,PPLUPT
         LINK  EP=IKJPARS
         LTR   R15,R15
         BNZ   PARERR
PARSOK   EQU   *
         MVC   HOLDDSN,HELDDSN
         L    R3,PPLANS
         LA    R3,8(R3)
         USING PDES,R3
         L    R4,POINTDSN
         LH    R5,DSNLEN
         BCTR   R5,R0
         LA     R6,HOLDDSN
         EX     R5,MOVIT
         TM    DSNFLG,X'40'
         BNO   APPENDID
         TM    MEMFLG,X'80'
         BNO   NOMEM
ALLOCIT  EQU   *
         LH   R6,DSNLEN
         STH  R6,HOLDDSNL
         LA    R6,HOLDDSNL
         ST    R6,DA08PDSN
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    R1,DA08CD
         ST    R1,DAPLDAPB
         LA    R1,DAPLUPT
         LINK  EP=IKJDAIR
         LTR   R15,R15
         BNZ   DAIRERR
DAIRDONE EQU   *
         MVC   LIBDIR+40(8),DA08DDN
         L     R4,POINTMEM
         LH    R5,MEMLEN
         BCTR   R5,0
         LA     R6,HOLDMEM
         EX     R5,MOVIT
         OPEN  (LIBDIR)
STARTRDR EQU   *
         FIND  LIBDIR,HOLDMEM,D
         LTR   R15,R15
         BNZ   NOTFOUND
FOUNDIT  EQU   *
         TM     KEYNUM,X'01'
         BNO   ENDITALL
         MVC   MSGFOUD+8(8),HOLDMEM
         LA    R9,MSGFOUD
         TPUT  (R9),23
         B     ENDITALL
NOTFOUND EQU   *
         LA   R9,8
         TM     KEYNUM,X'01'
         BNO   BADRCOD
         MVC   NOTFOUD+8(8),HOLDMEM
         LA    R9,NOTFOUD
         TPUT  (R9),33
         LA   R9,8
         B    BADRCOD
PARERR   PUTIT 'PARSE ERROR'
         B     ENDITALL
DAIRERR  PUTIT 'LIBRARY  NOT FOUND '
         B     ENDITALL
*ODEALLC  PUTIT  'FILE HAS NOT BEEN DEALLOCATED DO A FREEALL'
NODEALLC  B       LEAVEIT
APPENDID EQU   *
         MVC   HOLDDSN(7),USID          *KK*
         SLR   R1,R1                    *KK*
         IC    R1,USID+7                *KK*
         IC    R0,PERIOD                *KK*
         STC   R0,HOLDDSN(R1)           *KK*
         LA    R6,HOLDDSN+1(R1)         *KK*
         L     R4,POINTDSN
         LH    R5,DSNLEN
         BCTR  R5,R0
         EX    R5,MOVIT
         LH     R5,DSNLEN
         LA     R5,1(R5,R1)             *KK*
        STH    R5,DSNLEN
         B     ALLOCIT
NOMEM    EQU   *
         PUTIT 'NO MEMBER ENTERED'
ENDITALL EQU   *
         SR     R9,R9
BADRCOD EQU    *
         CLOSE (LIBDIR)
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA   R1,DA18CD
         ST   R1,DAPLDAPB
         LA    R1,HOLDDSNL
         ST    R1,DA18PDSN
         LA    R1,DAPLUPT
         LINK    EP=IKJDAIR
         LTR   R15,R15
         BNZ   NODEALLC
LEAVEIT    EQU   *
         LR   R15,R9
         LEAVE EQ
LIBDIR   DCB   DSORG=PO,DDNAME=BLANK,BLKSIZE=256,KEYLEN=0,MACRF=R,     X
               RECFM=F,EODAD=ENDITALL
MSGFOUD  DC    CL23'MEMBER           FOUND'
NOTFOUD  DC    CL33'MEMBER           NOT IN DATA SET'
HELDDSN  DC    CL44' '
         DS    0D
HOLDMEM   DC  CL8' '
MOVIT    MVC  0(0,R6),0(R4)
PPLIST     DC  V(PARMLIST)
USID       DC  CL8' '
PERIOD   DC    C'.'
         DS    0D
DA08CD   DC    X'0008'
DA08FLG  DC    H'0'
DA08DARC DC    H'0'
DA08CTRC DC    H'0'
DA08PDSN DC    F'0'
DA08DDN  DC    CL8' '
DA08UNIT DC    CL8' '
DA08SER  DC    CL8' '
DA08BLK  DC    F'0'
DA08PQTY DC    F'0'
DA08SQTY DC    F'0'
DA08DQTY DC    F'0'
DA08MNM  DC    CL8' '
DA08PSWD DC    CL8' '
DA08DSP1 DC    X'08'
DA08DSP2 DC    X'08'
DA08DSP3 DC    X'08'
DA08CTL  DC    X'00'
         DC    X'000000'
DA08DSO  DC    X'02'
         DC     2F'0'
*
*
         DS    0D
DA18CD   DC    X'0018'
DA18FLG  DC    H'0'
DA18DARC DC    H'0'
DA18CTRC DC    H'0'
DA18PDSN DC    A(0)
DA18DDN  DC    CL8' '
DA18MNM  DC    CL8' '
DA18SCLS DC    CL2' '
DA18DPSC DC    X'08'
DA18CTL  DC    X'0'
DA18JBNM DC    CL8' '
*
*        PARSE PARM LIST
*
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(PPLECBS)
PPLPCL   DC    A(0)
         DC    A(PPLANS)
PPLCBUF  DC    A(0)
PPLWA    DC    A(0)
PPLECBS  DC    A(0)
PPLANS   DC    A(0)
*
*        DAIR  PARM LIST
*
DAPLUPT  DC    A(0)
DAPLECT  DC    A(0)
DAPLECB  DC    A(DAPLECBS)
DAPLPSCB DC    A(0)
DAPLDAPB DC   A(0)
DAPLECBS DC    A(0)
*
*        WORK  AREA
*
WORKAREA DSECT
SAVEAREA DS    18F
HOLDDSNL     DS    H
HOLDDSN       DS   CL44
WORKLEN  EQU   *-WORKAREA
*
*        PDE ENTRIES
*
PDES     DSECT
POINTDSN DS    F
DSNLEN   DS    H
DSNFLG   DS    XL1
         DS    XL1
POINTMEM DS    F
MEMLEN   DS    H
MEMFLG   DS    XL1
         DS    XL1
         DS    3F
            DS   BL1
KEYNUM       DS    BL1
PDELEN   EQU   *-PDES
*
*         CPPL  PARM LIST PASSED WHEN CALLED
*
         IKJCPPL
*
*
         END
