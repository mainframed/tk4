SMF64EXT CSECT
*
*
*        THE PURPOSE OF THIS PROGRAM IS EXTRACT AND REFORMAT SMF 64
*        RECORD TYPES.  THESE RECORDS SUMMARIZE ACTIVITY THAT OCCURRED
*        ON VSAM FILES DURING THE PROCESS OF JOB EXECUTIONS.  THIS
*        DATA SHOULD BE USEFUL DETERMINING WHAT VSAM FILES ARE POORLY
*        ALLOCATED.  IT SHOULD ALSO BE VERY USEFUL IN IDENTIFYING WHAT
*        VSAM FILES SHOULD RESIDE ON CACHED VOLUMES.
*
*
*        WRITTEN 02-14-85 BY HOWARD H. GLASTETTER
*
R0       EQU   0
R1       EQU   1         POINTER TO LATEST INPUT RECORD
R2       EQU   2
R3       EQU   3         WORK POINTER TO INPUT RECORD
R4       EQU   4         WORK AREA FOR RR DIVIDE
R5       EQU   5         *
R6       EQU   6         *
R7       EQU   7
R8       EQU   8         WORK FOR TIME CONVERSION
R9       EQU   9         *
R10      EQU   10        SUBROUTINE LINKAGE
R11      EQU   11
R12      EQU   12        BASE
R13      EQU   13        SYSTEM LINKAGE
R14      EQU   14
R15      EQU   15
*
BEGIN    SAVE  (14,12)       PROGRAM LINKAGE AND INITIATION
         BALR  12,0
         USING *,12
         ST    R13,REGSAVE+4
         LA    R11,REGSAVE
         ST    R11,8(R13)
         LR    R13,R11
*
         PRINT ON,NOGEN
         OPEN  (FILEIN,,FILEOUT,(OUTPUT),PRINTOUT,(OUTPUT))
*
         EJECT
LOOP10   EQU   *
         GET   FILEIN
         AP    INCOUNT,=X'1C'
         LR    R3,R1            POINT R3 TO THE INPUT RECORD
         CLI   5(R3),X'40'      CHECK FOR RECORD TYPE 64
         BE    LOOP10A
         AP    BYPASSES,=X'1C'
         B     LOOP10
*
LOOP10A  EQU   *
         MVC   BTIME,6(R3)           CONVERT BI TIME TO HHMM CH
         BAL   R10,TIME40            *   * START TIME *
         MVC   SMF64TME,HHMMSS       *   SS WILL BE DROPPED
*
         MVC   SMF64DTE,10(R3)
         MVC   SMF64JBN,18(R3)       JOB NAME
*
         MVC   BTIME,26(R3)          CONVERT BI TIME TO HHMM CH
         BAL   R10,TIME40            *   * END TIME *
         MVC   SMF64RST,HHMMSS       *   SS WILL BE DROPPED
*
         MVC   SMF64RSD,30(R3)       JOB START DAY
         MVC   SMF64DNM,88(R3)       VSAM DSN
         MVC   SMF64VSR,148(R3)      1ST OCCURANCE OF VOLSER
*
         MVC   HALFWORD,138(R3)      POINT PAST EXTENTS
         AH    R3,HALFWORD           *
         AH    R3,=H'140'            * ALSO INITIAL SEGMENT
         SR    R4,R4                 FIND NUMBER OF EXTENTS
         LH    R5,HALFWORD           *
         LH    R6,=H'26'             *
         DR    R4,R6                 *
         STH   R5,SMF64XTN           PLACE THE NUMBER IN OUTPUT RECORD
*
         MVC   SMF64NLR,12(R3)       # OF RECORDS
         MVC   SMF64NFS,32(R3)       # UNUSED CI
         MVC   SMF64NCS,36(R3)       TOTAL CONTROL INTERVAL SPLITS
         MVC   SMF64NAS,40(R3)       TOTAL CONTROL AREA SPLITS
         MVC   SMF64DDE(32),60(R3)   MOVE LAST 8 FIELDS
*
WRITE20  EQU   *
         PUT   FILEOUT,RCRDOUT
         AP    OUTCOUNT,=X'1C'
         B     LOOP10
*
EOF30    EQU   *
         ED    INRECS,INCOUNT
         ED    OUTRECS,OUTCOUNT
         ED    BYPRECS,BYPASSES
         PUT   PRINTOUT,PRINTREC
         CLOSE (FILEIN,,FILEOUT,,PRINTOUT)
         L     R13,REGSAVE+4
         RETURN (14,12),RC=0
         EJECT
*
*****************************************************************
*  CALCULATE HOURS, MINUTES, SECONDS    *  S U B R O U T I N E  *
*****************************************************************
*
TIME40   EQU   *
         L     R9,BTIME                REG 9  = STORE CLOCK TIME
         SLR   R8,R8                   PREP REG 8 FOR WORK
         D     R8,=F'100'              CONVERT 100S OF SECS TO SECS
         CVD   R9,DOUBLE               CONVERT SECONDS TO PACKED
         MVC   WORKTIME(6),DOUBLE+2    MOVE SECONDS TO WORK AREA
         DP    WORKTIME,=PL3'3600'     CALCULATE HOURS
*                                    QQQQQSRRRRRS  Q=QUOTIENT R=REMAIN
*                                    000HHCSSSSSC  H=HOURS S=SECONDS
         UNPK  HHMMSS(2),WORKTIME+1(2)   CURRENT HOUR
         OI    HHMMSS+1,X'F0'          NORMALIZE THE SIGN
*
         MVC   WORKTIME(3),=XL3'0'     ZERO OUT TOP HALF
         DP    WORKTIME,=PL2'60'       CALCULATE MINUTES
*                                    QQQQQQQSRRRS  Q=QUOTIENT R=REMAIN
*                                    00000MMCSSSC  H=HOURS S=SECONDS
         UNPK  HHMMSS+2(2),WORKTIME+2(2) THE MINUTES
         OI    HHMMSS+3,X'F0'          NORMALIZE THE SIGN
*
         UNPK  HHMMSS+4(2),WORKTIME+4(2) MOVES REMAINING SECONDS
         OI    HHMMSS+5,X'F0'          NORMALIZE THE SIGN
*
         BR    R10                     RETURN TO MAINLINE
*
         EJECT
REGSAVE  DS    9D
DOUBLE   DC    D'0'
BTIME    DC    F'0'
WORKTIME DC    PL6'0'
HHMMSS   DC    CL6'0'
HALFWORD DC    H'0'
INCOUNT  DC    PL5'0'
OUTCOUNT DC    PL5'0'
BYPASSES DC    PL5'0'
*
FILEIN   DCB   DDNAME=FILEIN,MACRF=(GL),EODAD=EOF30,DSORG=PS,BFTEK=A
FILEOUT  DCB   DSORG=PS,LRECL=124,DDNAME=FILEOUT,MACRF=(PM)
PRINTOUT DCB   DSORG=PS,LRECL=80,DDNAME=PRINTOUT,MACRF=(PM)
*
PRINTREC DS    0CL80    OUTPUT RECORD AREA
         DC    CL8'        '
INRECS   DC    XL12'402020206B2020206B202020'
         DC    CL12' INPUT      '
OUTRECS  DC    XL12'402020206B2020206B202020'
         DC    CL13' OUTPUT      '
BYPRECS  DC    XL12'402020206B2020206B202020'
         DC    CL11' BYPASSED  '
*
         DS    0F
RCRDOUT  DS    0CL124    OUTPUT RECORD AREA
SMF64TME DS    CL4       JOB END TIME
SMF64DTE DS    PL4       DATE 00YYDDDF SMF CREATED
SMF64JBN DS    CL8       JOBNAME
SMF64RST DS    CL4       JOB START TIME
SMF64RSD DS    PL4       JOB START  00YYDDDF
SMF64DNM DS    CL44      VSAM DSN
SMF64NLR DS    F         NUMBER OF RECORDS IN COMPONANT
SMF64NFS DS    F         NUMBER OF UNUSED CONTROL INTERVALS
SMF64NCS DS    F         TOTAL NUMBER OF CONTROL INTERVAL SPLITS
SMF64NAS DS    F         TOTAL NUMBER OF CONTROL AREA SPLITS
SMF64DDE DS    F         NUMBER OF RECORDS DELETED BY THIS JOB
SMF64DIN DS    F         NUMBER OF RECORDS INSERTED BY THIS JOB
SMF64DUP DS    F         NUMBER OF RECORDS UPDATED BY THIS JOB
SMF64DRE DS    F         NUMBER OF RECORDS RETRIEVED BY THIS JOB
SMF64DFS DS    F         CHANGE IN UNUSED CONTROL INTERVALS (+ OR -)
SMF64DCS DS    F         CONTROL INTERVALS SPLIT BY THIS JOB
SMF64DAS DS    F         CONTROL AREAS SPLIT BY THIS JOB
SMF64DEP DS    F         EXCPS DONE BY THIS JOB
SMF64XTN DS    H         NUMBER OF FILE EXTENTS
SMF64VSR DS    CL6       VOLSER
         END
