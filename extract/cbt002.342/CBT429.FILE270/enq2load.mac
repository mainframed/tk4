//DNE00150 JOB (2100,56-D),'DPARS 0682 T',CLASS=H
//* FROM WIZRD150.DN00150.FILE(ENQ2LOAD)
//A EXEC ASMHCL,OPTION1='RENT,TERM', IBRARY='WDPSC.CMDLIB',
//   OPTION2=RENT
ENQ2LOAD CSECT
***********************************************************************
*
*        ENQ2LOAD -- SECOND LOAD OF IEESA03D
*
*        DOES ALL I/O FOR FIRST LOAD
*
*        ON ENTRY:
*                 R13 = ADDRESS OF WORK-AREA OF IEESA03D
*                 R15 = ENTRY POINT
*
*        CSECT  NAME: ENQ2LOAD
*        MODULE NAME: ENQ2LOAD
*        ATTRIBUTES : REENTRANT,REUSABLE
*
*        AUTHOR      : ALAN M. WOOLF
*        DATE        : NOVEMBER 9, 1971
*        INSTALLATION: SIKORSKY AIRCRAFT, DIV. OF UNITED AIRCRAFT COPR.
*
***********************************************************************
         B     14(0,15)           BRANCH AROUND ID
         DC    AL1(8)
         DC    CL8'ENQ2LOAD  '
         LR    R12,R15
         USING ENQ2LOAD,R12
         USING WKDSECT,R13
         SPACE 2
         MVI   WTOLIST+2,X'01'    MCSFLAG = QREG0
         LH    R3,MSGCODE         GET MESSAGE ERROR CODE, IF ANY
         LTR   R3,R3              ANY ERRORS ?
         BZ    FORMAT             NO...
         L     R5,MSGTABLE(R3)    YES...GET DISPLACEMENT OF MESSAGE
         LA    R5,0(R12,R5)       ADD BASE FOR ABSOLUTE ADDRESS
         LH    R4,0(,R5)          LENGTH OF MESSAGE
         BCTR  R4,0               SET FOR EXECUTE
         EX    R4,GETMSG
         LA    R4,5(,R4)          RESET LENGTH+4 FOR CONTROL BYTES
         STH   R4,WTOLIST         PUT LENGTH IN CONTROL FIELD
         BAL   R11,IOCS           OUTPUT MESSAGE
         CH    R3,=H'32'          "LOST DATA" ERROR CODE ?
         BNE   CLEANUP            NO...ALL FINISHED THEN.
         EJECT
***********************************************************************
*
*        FORMAT DATA AND WRITE IT OUT
*
***********************************************************************
FORMAT   EQU   *
         LA    R3,36              SET MESSAGE CODE
         LA    R9,2               LOOP COUNTER
         TM    SWTCH+1,X'0F'      ONLY MAJOR QCB NAMES?
         BZ    LOOP1              NO..OK AS IS.
         LA    R3,44              YES..SET FOR CORRECT HEADER MSG.
LOOP1    L     R5,MSGTABLE(R3)    DISPLACEMENT OF MESSAGE
         LA    R5,0(R12,R5)       ADD BASE FOR ABSOLUTE ADDRESS
         LH    R4,0(,R5)          LENGTH
         BCTR  R4,0               SET FOR EXECUTE
         EX    R4,GETMSG          MOVE MESSAGE TO BUFFER
         LA    R4,5(,R4)          CORRECT LENGTH
         STH   R4,WTOLIST         SET IN MESSAGE
         BAL   R11,IOCS           DO OUTPUT
         LA    R3,4(,R3)          NEXT MESSAGE CODE
         BCT   R9,LOOP1           DO NEXT MESSAGE
*
         LH    R3,TBLSIZE         NO. OF ENTRIES IN TABLE
         L     R5,CORE            START OF TABLE
         LA    R6,BUFFER          START OF OUTPUT BUFFER
*
         TM    SWTCH+1,X'0F'      ONLY MAJOR QCB NAMES ?
         BO    MAJONLY            YES..
*
LOOP2    MVI   BUFFER,C' '
         MVC   BUFFER+1(64),BUFFER  BLANK-OUT BUFFER
         LA    R4,24              SET NORMAL WTO LENGTH
         SR    R9,R9
         IC    R9,0(,R5)          GET FIRST CHAR IN ENTRY
         CH    R9,=H'91'          LESS THAN X'5B'...CHAR '$' ?
         BL    SETNAME            YES..MUST BE MINOR QCB NAME ENTRY.
GETOWNER MVC   2(8,R6),0(R5)      JOBNAME/USERID
         MVC   11(6,R6),=CL6'- SHR '   ASSUME SHR'D ENQ
         CLI   8(R5),C'S'         IS IT SO ?
         BE    PRINT
         MVC   13(4,R6),=CL4'EXCL'  NO, ENQ EXCLUSIVE
         CLI   8(R5),C'E'         IS IT SO
         BE    PRINT
         MVC   13(4,R6),=CL4'RSVS'  NO,  RESERVE SHR
         CLI   8(R5),C'R'         IS IT SO
         BE    PRINT
         MVC   13(4,R6),=CL4'RSVX'  NO,  RESERVE EXCL
PRINT    STH   R4,WTOLIST         SET MESSAGE LENGTH
         BAL   R11,IOCS           OUTPUT BUFFER
         LA    R5,9(,R5)          NEXT ENTRY IN TABLE
         BCT   R3,LOOP2           DO NEXT ENTRY, IF ANY
         B     CLEANUP            FINISHED...
*
SETNAME  SR    R11,R11
         IC    R11,1(,R5)         LENGTH OF MINOR NAME
         LR    R4,R11
         LA    R4,24(,R4)         TOTAL MESSAGE LENGTH
         BCTR  R11,0              SET FOR EXECUTE INST
         EX    R11,GETNAME        MOVE NAME INTO BUFFER
         SR    R3,R9              DELETE NO. OF TABLE ENTRIES
         SR    R8,R8              (NO. OF ENTRIES) X (SIZE OF ENTRY)
         M     R8,=F'9'           PLUS CURRENT POINTER EQUAL
         AR    R5,R9              NEXT TABLE ENTRY
         B     GETOWNER           GET JOBNAME/USERID, ETC.
         SPACE 2
***********************************************************************
*
*        PRINT ONLY MAJOR QCB NAMES HERE
*
***********************************************************************
LOOP3    EQU   *
MAJONLY  LA    R4,17              SIZE OF MESSAGE
         STH   R4,WTOLIST         PLACE IN CONTROL FIELD
         MVI   BUFFER,C' '
         MVC   BUFFER+1(64),BUFFER BLANK-OUT BUFFER
         MVC   5(8,R6),0(R5)      GET MAJOR NAME FROM TABLE
         BAL   R11,IOCS           WRITE IT OUT
         LA    R5,8(,R5)          NEXT ENTRY IN TABLE,IF ANY
         BCT   R3,LOOP3           GET IT, IF IT EXISTS
         B     CLEANUP            FINISHED...
         EJECT
***********************************************************************
*
*        RELEASE ALL CORE AND EXIT
*
***********************************************************************
CLEANUP  EQU   *
         L     R1,CORE            START OF CORE TABLE
         LH    R0,CORESIZE        SIZE OF TABLE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
         LH    R0,WKSIZE          SIZE OF WORK-AREA
         LR    R1,R13             START OF WORK-AREA
         L     R14,SAVA           SET RETURN ADDRESS
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
         SR    R15,R15
         BR    R14                RETURN
         EJECT
***********************************************************************
*
*        IOCS -- I/O CONTROL SECTION
*
*        ON ENTRY:
*                 R4  = MESSAGE LENGTH+4 (INCLUDES WTO CONTROL BYTES)
*                 R7  = TJID IN HIGH ORDER BYTES, IF IT EXISTS
*                 R10 = CONSOL ID FOR MCS
*                 R11 = RETURN ADDRESS
*
*        NOTE: OUTPUT BUFFER IS SETUP PRIOR TO ENTRY
*
***********************************************************************
IOCS     DS    0H
         SR   R7,R7
         SPACE 2
         SH    R4,=H'4'           STRIP OFF LENGTH OF CONTROL BYTES
         LA    R1,BUFFER          START OF MESSAGE
         OR    R4,R7              PUT TJID INTO LENGTH REGISTER
         TPUT  (1),(4),R          OUTPUT MESSAGE TO TSO USER
         BR    R11                RETURN
         EJECT
***********************************************************************
*
*        CONSTANTS, TABLES, ETC.
*
***********************************************************************
MSGTABLE DS    0F                 MSGCODE
         DC    A(*)                 0 - NOT USED
         DC    A(MSG1-ENQ2LOAD)     4
         DC    A(MSG2-ENQ2LOAD)     8
         DC    A(MSG3-ENQ2LOAD)   12
         DC    A(MSG4-ENQ2LOAD)   16
         DC    A(MSG5-ENQ2LOAD)   20
         DC    A(MSG6-ENQ2LOAD)   24
         DC    A(MSG7-ENQ2LOAD)   28
         DC    A(MSG8-ENQ2LOAD)   32
         DC    A(MSG9-ENQ2LOAD)   36
         DC    A(MSG10-ENQ2LOAD)  40
         DC    A(MSG11-ENQ2LOAD)  44
         DC    A(MSG12-ENQ2LOAD)  48
*
MSG1     DC    H'32',C'*** MAJOR NAME QCB NOT FOUND ***'
MSG2     DC    H'37',C'*** ERROR IN MINOR QCB CHAIN ***'
MSG3     DC    H'27',C'*** QEL CHAIN ERROR ***'
MSG4     DC    H'46',C'*** NO FREE SQA CORE AVAILABLE...TRY AGAIN ***'
MSG5     DC    H'42',C'*** INVALID MAJOR/MINOR NAME SPECIFIED ***'
MSG6     DC    H'31',C'*** NO MINOR NAME SPECIFIED ***'
MSG7     DC    H'32',C'*** MINOR NAME QCB NOT FOUND ***'
MSG8     DC    H'54',C'*** LOST DATA...MAXIMUM NUMBER OF ENTRIES EXCEEDX
               ED ***'
MSG9     DC    H'30',C'  JOB/USER   MODE   MINOR NAME'
MSG10    DC    H'31',C' ------------------------------'
MSG11    DC    H'16',C'  MAJOR QCB NAME'
MSG12    DC    H'17',C' ----------------'
         SPACE 2
         DS    0F
SUB255   DC    X'FF000000'
GETMSG   MVC   BUFFER(*-*),2(R5)
GETNAME  MVC   BUFFER+20(*-*),2(R5)
         SPACE 2
WKDSECT  DSECT
SAVA     DS    18F                SAVE-AREA
XSASAVE  DS    F                  EXTENDED SAVE-AREA PTR
CORE     DS    F                  POINTER TO FREE CORE
MAJLGTH  DS    H                  MODIFIED LENGTH OF MAJOR NAME
MINLGTH  DS    H                  MODFIED LENGTH OF MINOR NAME
SWTCH    DS    H                  PROGRAM SWITCHES
WKSIZE   DS    H                  SIZE OF WORK-AREA
CORESIZE DS    H                  SIZE OF FREE CORE
MSGCODE  DS    H                  I/O MESSAGE CODE
TBLSIZE  DS    H                  NUMBER OF TABLE ENTRIES
MAJNAM   DS    CL8                MAJOR NAME
MINNAME  DS    CL255              MINOR NAME
WTOLIST  DS    F                  WTO CONTROL BYTES
BUFFER   DS    CL65               WTO MESSAGE BUFFER
XCTLLIST DS    0F                 XCTL CONTROL INFO
XCTLEP   DS    F                  ADDRESS OF EP PARAMETER
XCTLDCB  DS    F                  DCB ADDRESS PARAMETER
XCTLNAME DS    CL8                ENTRY POINT NAME
LWKDSECT EQU   *-WKDSECT
         SPACE 2
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         END
//LKED.SYSIN DD *
 NAME ENQ2LOAD(R)
