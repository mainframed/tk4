*          DATA SET 761KKIFCAT AT LEVEL 001 AS OF 10/13/80
         TITLE 'IFCAT -- COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
* IFCAT 'SWC.L.DATA'                                                  *
*   "   MISC.CLIST                                                    *
*   "   'SWC.L.DATA(MEM)'                                             *
*   "   MISC.CLIST(CLMEM)                                             *
*                                                                     *
***********************************************************************
*
* REGISTER USEAGE
*
*        R2-R7 WORK REGISTERS
*        R8    BASE REGISTER FOR PDES
*        R9    BASE REGISTER FOR DAPL
*        R10   BASE REGISTER FOR PPL
*        R11   BASE REGISTER FOR CPPL
*        R12   BASE REGISTER FOR PROGRAM
*        R13   BASE REGISTER FOR WORKAREA
*        R14   LINK REGISTER
IFCT    ENTER (14,12),12,(,WORKL1)
         SPACE 3
*        SET UP ADDRESSABILITY
*        COMMAND PROCCESSOR PARM LIST,PARSE PARM LIST,DYNAMIC ALLOC
*        PARM LIST,WORKAREA. (CPPL,PPL,DAPL,W.A.)
         USING WORK1,R13
         LR    R11,R1
         USING CPPL,R11
         LA    R10,PPLAREA
         USING PPL,R10
         LA    R9,DAPLAREA
         USING DAPL,R9
         SPACE 3
*
*        PREPARE PARSE AND DAIR CONTROL BLOCKS
*        TRANSFER INFO FROM CPPL TO PPL AND DAPL
         L     R5,CPPLCBUF    COMMAND BUFFER
         ST    R5,PPLCBUF
         L     R5,CPPLUPT    UPT
         ST    R5,PPLUPT
         ST    R5,DAPLUPT
         L     R5,CPPLECT    ECT
         ST    R5,PPLECT
         ST    R5,DAPLECT
         LA    R5,PARSECB    MOVE OUR ECB
         ST    R5,PPLECB
         ST    R5,DAPLECB
         L     R5,CPPLPSCB   PSCB
         ST    R5,DAPLPSCB
         L     R5,=A(PARMS)
         ST    R5,PPLPCL
         LA    R5,ANSAREA
         ST    R5,PPLANS
         XC    PPLUWA(4),PPLUWA
         XC    PARSECB(4),PARSECB
         SPACE 3
*
*        PARSE COMMAND BUFFER
*
         LR    R1,R10
         DROP  R10
         LINK  EP=IKJPARS
         SPACE 3
*
*        CHECK RETURN CODE
*
         LTR   R15,R15
         BNZ   PARSERR
         SPACE 3
*
*        SET UP PDL ADDRESSABILITY
*
         L     R8,ANSAREA
         USING IKJPARMD,R8
         SPACE 3
*         CLI   DSTRING,0
*         BE    NEITHER
         SPACE 3
*
*
*        COMPLETE DAIR CONTROL BLOCKS
*
         MVC   DAPB(16),DAPB04
         LA    R5,DAPB
         ST    R5,DAPLDAPB
         LA    R4,DSN
         ST    R4,DAPB+8
         MVI   0(R4),C' '
         MVC   1(45,R4),0(R4)
         L     R5,DSTRING
         LH    R3,DSTRING+4
         STH   R3,0(R4)
         LA    R4,2(R4)
         BCTR  R3,0
         EX    R3,MOVEDSN3
*
*        IF DATA SET WAS NOT IN QUOTES, ADD USERID TO FRONT
*
         TM    DSTRING+6,B'01000000'
         BNZ   QUOTED01
         OI    DAPB+12,B'00100000'
QUOTED01 EQU   *
         SPACE 3
*
*        CREATE DD CARD FOR DATA SET WITH DAIR
*
         LA    R1,DAPL
         LINK  EP=IKJEFD00
         SPACE 3
*
*        CHECK RETURN CODE FROM DAIR
*
         LTR   R15,R15
         BNZ   ERROR4
         LA    R5,DAPB
         TM    2(R5),X'06'
         BZ    ERROR4
         B     EXIT
         SPACE 3
MOVEDSN3 MVC   0(0,R4),0(R5)
         SPACE 3
NEITHER  EQU   *
* NEITHER KEYWORD IS SPECIFIED, GIVE ERROR, SET COND.CODES ,BYE
         TPUT  NOOP,21
         TITLE 'EXITS...BOTH GOOD AND BAD'
ERROR4   LEAVE CC=4
EXIT     EQU   *
         LEAVE EQ
         TITLE 'IFCT -- ERROR PROCESSING'
*
*        AN ERROR OCCURED IN PARSE
*
PARSERR  EQU   *
         TPUT  BADPARS,11
         B     ERROR4
         SPACE 3
*
*        AN ERROR OCCURED IN DAIR.
*
         SPACE 3
         TITLE 'IFCT -- CONSTANTS, STORAGE, AND DSECTS'
NOOP     DC    CL21'NO OPERANDS SPECIFIED'
BADPARS  DC    CL11'PARSE ERROR'
BADAIR   DC    CL24'DYNAMIC ALLOCATION ERROR'
         SPACE 2
DAPB04   DS    0F
         DC    X'00040000'
         DC    F'0'
         DC    A(0)           DSNAME BUFFER ADDRESS
         DC    4XL1'00'
PARMS    IKJPARM
DSTRING  IKJPOSIT DSNAME,PROMPT='DATASET'
         IKJENDP
WORK1    DSECT
         DS    18F
PPLAREA  DS    7F
ANSAREA  DS    F
PARSECB  DS    F
DAPLAREA DS    5F
         DS    0F
DAPB     DS    CL16
         DS   F
DSN      DS   CL46
WORKL1   EQU   *-WORK1
         IKJCPPL
         IKJPPL
         IKJDAPL
         END
