*          DATA SET 761KKILLR  AT LEVEL 001 AS OF 07/13/81
         TITLE 'LOGON STIMER KILL INTERCEPT MODULE'
LOGKILLM ENTERR LEVEL=V2M1
         EJECT
*
*              MAYBE WAIT A FEW SECONDS TO READ SOME MESSAGES:
*
         LR    R10,R1                   SAVE PARM PTR
         L     R8,548                   R8->ASCB
         L     R7,108(R8)               R7->ASXB
         L     R6,20(R7)                R6->LWA
         TM    59(R6),X'01'             INITIAL LOGON?
         BO    GOFAST                   YES->GO ON QUICKLY
         STIMER WAIT,BINTVL=FIVE       WAIT FIVE SECONDS
         B     GOFAST                   THEN GO FAST
FIVE     DC    F'500'                   5.00 SECONDS
         EJECT
*
*              ATTACH THE KILLER:
*
GOFAST   ATTACH EP=LOGKILLR             ATTACH HIM
         ST    R1,TCBADD                SAVE HIS ADDRESS
         EJECT
*
*              CALL IKJEFLE:
*
         LR    R1,R10                   R1->FLE PARMS
         CALL  IKJEFLE
*
         EJECT
*
*              NOW STOP THE KILLER BEFORE IT IS TOO LATE:
*
         DETACH TCBADD
         EJECT
*
*              NOW RETURN TO IKJEFLC:
*
         LEAVER
         EJECT
         LTORG
WORKAREA DSECT
SAVEAREA DS    18F
TCBADD   DS    F
DUMMYECB DS    F
WORKLEN  EQU   *-WORKAREA
         TITLE 'LOGON STIMER CANCEL ROUTINE'
LOGKILLR ENTERR  R=NO,LEVEL=V2M1
         EJECT
*
*              SETUP FOR FIVE MINUTE WAIT:
*
         LA    R11,ABENDME
         STIMER REAL,(R11),BINTVL=WAITTIME  ISSUE CONTRACT ON USER
*
*        WAIT  UNTIL SOMETHING HAPPENS:
*
         XC    DUMMYECB,DUMMYECB
         WAIT  ECB=DUMMYECB
*
*              THEN ABEND THIS ADDRESS  SPACE:
*
ABENDME  ABEND 99,,STEP,SYSTEM          KILL THIS ADDR SPACE
*
WAITTIME DC    F'30000'                 300 SECS = 5 MINUTES
         LTORG
         END
