      *******************************
       ID DIVISION.
      *******************************

       PROGRAM-ID. PFKEYCHG.
       AUTHOR.     GARY DUFFIELD.
       REMARKS.
                   THIS PROGRAM SAVES THE USERS PFKEY ASSIGNMENTS AND
        CHANGES THEM TO COMMUNICATE WITH DIALOG MANAGER THROUGH '&ZCMD'.
           EJECT
      *******************************
       ENVIRONMENT DIVISION.
      *******************************

       CONFIGURATION SECTION.

       INPUT-OUTPUT SECTION.

           EJECT
      *******************************
       DATA DIVISION.
      *******************************

       FILE SECTION.

           EJECT

       WORKING-STORAGE SECTION.

       01  SWITCHES.

           05  PARM-ERROR               PIC XXX        VALUE 'NO '.
               88  PARM-OKAY                           VALUE 'NO '.
               88  PARM-NOT-OKAY                       VALUE 'YES'.

           05  CHANGE-TYPE              PIC 9          VALUE 0.
               88  CHANGE-TO-KEY-NUMBER                VALUE 1.
               88  CHANGE-LISTED-VALUES                VALUE 2.
               88  CHANGE-ALL-VALUES                   VALUE 3.

       01  MISC-WORK-FIELDS.

           05  SAVE-CODE                PIC 9(4)       VALUE 0.
           05  SWITCH-CODE              PIC 9(4)       VALUE 0.
           05  SAVE-SWITCH              PIC XX         VALUE SPACE.
           05  ZPFKEY-NUMBER            PIC XX         VALUE SPACE.
           05  PREFIXED-PFKEY.
               10  FILLER               PIC X          VALUE '>'.
               10  WORK-PFKEY           PIC X(43)      VALUE SPACE.

       01  DIALOG-MANAGER-CONTROLS.

           05  CHAR                   PIC X(4)       VALUE 'CHAR'.
           05  LEN                    PIC S9(8) COMP VALUE +0.
           05  PROFILE                PIC X(7)       VALUE 'PROFILE'.
           05  VDEFINE                PIC X(7)       VALUE 'VDEFINE'.
           05  VGET                   PIC X(4)       VALUE 'VGET'.
           05  VPUT                   PIC X(4)       VALUE 'VPUT'.
           05  ZPFKEYS                PIC X(7)       VALUE '(ZKEYS)'.
           05  ZPFSW                  PIC X(7)       VALUE '(ZPFSW)'.

       01  TABLES.

           05  PARM-TABLE.
               10  PARM-FIELD OCCURS 15 TIMES
                   INDEXED BY INX-01   PIC X(8).

           05  HOLD-TABLE.
               10  HOLD-CHARACTER OCCURS 8 TIMES
                   INDEXED BY INX-02   PIC X.

           05  PARM-VALUES-TABLE.
              10  FILLER               PIC X(48)   VALUE
                 'RCHANGE CURSOR  DOWN    END     RFIND   HELP    '.
              10  FILLER               PIC X(48)   VALUE
                 'LEFT    NOP     PRINT   PRINT-HIRETURN  RIGHT   '.
              10  FILLER               PIC X(24)   VALUE
                 'SPLIT   SWAP    UP      '.

           05  PARM-VALUE-TABLE REDEFINES PARM-VALUES-TABLE.
               10  PARM-VALUE OCCURS 15 TIMES
                   INDEXED BY INX-03   PIC X(8).

           05  SAVE-KEY-NAMES.
              10  FILLER               PIC X(43)   VALUE
                 '(ZKEY01,ZKEY02,ZKEY03,ZKEY04,ZKEY05,ZKEY06,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY07,ZKEY08,ZKEY09,ZKEY10,ZKEY11,ZKEY12,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY13,ZKEY14,ZKEY15,ZKEY16,ZKEY17,ZKEY18,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY19,ZKEY20,ZKEY21,ZKEY22,ZKEY23,ZKEY24)'.

           05  SPF-KEY-NAMES.
               10  FILLER               PIC X(37)   VALUE
                  '(ZPF01,ZPF02,ZPF03,ZPF04,ZPF05,ZPF06,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF07,ZPF08,ZPF09,ZPF10,ZPF11,ZPF12,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF13,ZPF14,ZPF15,ZPF16,ZPF17,ZPF18,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF19,ZPF20,ZPF21,ZPF22,ZPF23,ZPF24)'.

           05  NUMBERS-FOR-12-KEY.
               10  FILLER               PIC X(516)  VALUE SPACE.
               10  FILLER               PIC X(43)   VALUE
                  '>K01                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K02                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K03                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K04                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K05                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K06                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K07                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K08                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K09                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K10                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K11                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K12                                       '.

           05  NUMBERS-FOR-24-KEY.
               10  FILLER               PIC X(43)   VALUE
                  '>K01                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K02                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K03                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K04                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K05                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K06                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K07                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K08                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K09                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K10                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K11                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K12                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K13                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K14                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K15                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K16                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K17                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K18                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K19                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K20                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K21                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K22                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K23                                       '.
               10  FILLER               PIC X(43)   VALUE
                  '>K24                                       '.

           05  PFKEY-VALUES.
               10  PFKEY OCCURS 24 TIMES INDEXED BY INX-04.
                   15  PFKEY-PREF        PIC X.
                   15  PFKEY-REST        PIC X(42).
               EJECT
      *************************************
       LINKAGE SECTION.
      *************************************

       01  PARM-FIELD-IN.
           05  PARM-LENGTH               PIC S9(4) COMP.
           05  PARM-IN.
               10  PARM-CHARACTER OCCURS 134 TIMES
                   INDEXED BY INX-05         PIC X.
           EJECT
      ****************************************
       PROCEDURE DIVISION USING PARM-FIELD-IN.
      ****************************************
       CONTROL-MODULE.
      *************************************

           PERFORM 0000-INITIALIZE-MODULE.
           IF PARM-OKAY
              PERFORM 1000-CHANGE-MODULE
              PERFORM 2000-TERMINATE-MODULE.
           MOVE SAVE-CODE TO RETURN-CODE.
           STOP RUN.
           EJECT
      *************************************
       0000-INITIALIZE-MODULE.
      *************************************

           MOVE +2 TO LEN.
           CALL 'ISPLINK' USING VDEFINE
                                ZPFSW    SAVE-SWITCH   CHAR LEN.
           MOVE RETURN-CODE TO SAVE-CODE.
           MOVE SPACE TO PARM-TABLE
                         HOLD-TABLE.
           PERFORM 0010-PROCESS-PARM-MODULE.
           IF PARM-OKAY
              PERFORM 0020-INITIALIZE-KEYS-MODULE.

      *************************************
       0010-PROCESS-PARM-MODULE.
      *************************************

           IF PARM-LENGTH = 0
              OR PARM-IN = SPACE
              MOVE 3 TO CHANGE-TYPE
           ELSE
              IF PARM-LENGTH > 134
                 OR PARM-LENGTH = 1
                 MOVE 'YES' TO PARM-ERROR
                 MOVE 8 TO SAVE-CODE
              ELSE
                 SET INX-01 TO 1
                 SET INX-05 TO 1
                 PERFORM 0011-PARSE-PARM-MODULE
                    UNTIL INX-05 NOT < PARM-LENGTH
                    OR PARM-NOT-OKAY
                    OR INX-01 > 15
                 IF INX-01 > 15
                    MOVE 'YES' TO PARM-ERROR
                    MOVE 8 TO SAVE-CODE
                 ELSE
                    IF PARM-OKAY
                       PERFORM 0013-EDIT-PARM-VALUE-MODULE.

      *************************************
       0011-PARSE-PARM-MODULE.
      *************************************

           SET INX-02 TO 1.
           PERFORM 0012-MOVE-PARMCHAR-MODULE
              VARYING INX-05 FROM INX-05 BY 1
              UNTIL PARM-CHARACTER (INX-05) = ','
              OR INX-05 > PARM-LENGTH
              OR INX-02 > 9.
           IF INX-02 > 9
              MOVE 'YES' TO PARM-ERROR
              MOVE 8 TO SAVE-CODE
           ELSE
              IF HOLD-TABLE = SPACE
                 MOVE 'YES' TO PARM-ERROR
                 MOVE 8 TO SAVE-CODE
              ELSE
                 SET INX-05 UP BY 1
                 MOVE HOLD-TABLE TO PARM-FIELD (INX-01)
                 MOVE SPACE TO HOLD-TABLE
                 SET INX-01 UP BY 1.

      *************************************
       0012-MOVE-PARMCHAR-MODULE.
      *************************************

           MOVE PARM-CHARACTER (INX-05) TO HOLD-CHARACTER (INX-02).
           SET INX-02 UP BY 1.

      *************************************
       0013-EDIT-PARM-VALUE-MODULE.
      *************************************

           PERFORM 0014-LOOP-MODULE
              VARYING INX-01 FROM 1 BY 1
              UNTIL INX-01 > 15
              OR PARM-FIELD (INX-01) = 'KEYNO   '
           IF PARM-FIELD (INX-01) = 'KEYNO   '
              IF (INX-01 NOT = 1)
                 OR (PARM-FIELD (INX-01 + 1) NOT = SPACE)
                 MOVE 'YES' TO PARM-ERROR
                 MOVE 8 TO SAVE-CODE
              ELSE
                 MOVE 1 TO CHANGE-TYPE
           ELSE
              MOVE 2 TO CHANGE-TYPE
              PERFORM 0015-CHECK-PARMLIST-MODULE
                 VARYING INX-01 FROM 1 BY 1
                 UNTIL PARM-FIELD (INX-01) = SPACE
                 OR PARM-NOT-OKAY
                 OR INX-01 > 15.

      *************************************
       0014-LOOP-MODULE.
      *************************************

           EXIT.

      *************************************
       0015-CHECK-PARMLIST-MODULE.
      *************************************

           PERFORM 0014-LOOP-MODULE
              VARYING INX-03 FROM 1 BY 1
              UNTIL PARM-FIELD (INX-01) = PARM-VALUE (INX-03)
              OR INX-03 > 15.
           IF INX-03 > 15
              MOVE 'YES' TO PARM-ERROR
              MOVE 8 TO SAVE-CODE.

      *************************************
       0020-INITIALIZE-KEYS-MODULE.
      *************************************

           MOVE +43 TO LEN.
           CALL 'ISPLINK' USING VDEFINE
                                SAVE-KEY-NAMES PFKEY-VALUES CHAR LEN.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VDEFINE
                                SPF-KEY-NAMES  PFKEY-VALUES CHAR LEN.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           MOVE +2 TO LEN.
           CALL 'ISPLINK' USING VDEFINE
                                ZPFKEYS  ZPFKEY-NUMBER CHAR LEN.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VGET SPF-KEY-NAMES PROFILE.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VGET ZPFSW.
           MOVE RETURN-CODE TO SWITCH-CODE.
           IF SWITCH-CODE = 8
              OR SAVE-SWITCH = SPACE
              MOVE 'ON' TO SAVE-SWITCH
              CALL 'ISPLINK' USING VPUT SAVE-KEY-NAMES
              CALL 'ISPLINK' USING VPUT ZPFSW
           ELSE
              MOVE 4 TO SAVE-CODE.
           CALL 'ISPLINK' USING VGET ZPFKEYS PROFILE.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           EJECT
      *************************************
       1000-CHANGE-MODULE.
      *************************************

           IF CHANGE-TO-KEY-NUMBER
              PERFORM 1010-KEYNO-CHANGE-MODULE
           ELSE
              IF CHANGE-LISTED-VALUES
                 PERFORM 1020-LISTED-VALUES-MODULE
              ELSE
                 PERFORM 1030-ALL-VALUES-MODULE.
           EJECT
      *************************************
       1010-KEYNO-CHANGE-MODULE.
      *************************************

           IF ZPFKEY-NUMBER = '12'
              MOVE NUMBERS-FOR-12-KEY TO PFKEY-VALUES
           ELSE
              MOVE NUMBERS-FOR-24-KEY TO PFKEY-VALUES.
           EJECT
      *************************************
       1020-LISTED-VALUES-MODULE.
      *************************************

           PERFORM 1021-MODIFY-VALUES-MODULE
              VARYING INX-04 FROM 1 BY 1
              UNTIL INX-04 > 24.

      *************************************
       1021-MODIFY-VALUES-MODULE.
      *************************************

           IF PFKEY-PREF (INX-04) NOT = '>' AND ':'
              PERFORM 0014-LOOP-MODULE
                 VARYING INX-01 FROM 1 BY 1
                 UNTIL PFKEY (INX-04) = PARM-FIELD (INX-01)
                 OR INX-01 > 15
              IF PFKEY (INX-04) = PARM-FIELD (INX-01)
                 MOVE PFKEY (INX-04) TO WORK-PFKEY
                 MOVE PREFIXED-PFKEY TO PFKEY (INX-04)
                 MOVE SPACE TO WORK-PFKEY.
           EJECT
      *************************************
       1030-ALL-VALUES-MODULE.
      *************************************

           PERFORM 1031-MODIFY-VALUES-MODULE
              VARYING INX-04 FROM 1 BY 1
              UNTIL INX-04 > 24.

      *************************************
       1031-MODIFY-VALUES-MODULE.
      *************************************

           IF PFKEY-PREF (INX-04) NOT = '>' AND ':'
              MOVE PFKEY (INX-04) TO WORK-PFKEY
              MOVE PREFIXED-PFKEY TO PFKEY (INX-04)
              MOVE SPACE TO WORK-PFKEY.
           EJECT
      *************************************
       2000-TERMINATE-MODULE.
      *************************************

           CALL 'ISPLINK' USING VPUT SPF-KEY-NAMES PROFILE.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
