*          DATA SET 761KKGETMY AT LEVEL 002 AS OF 10/04/83
GETMY    TITLE ' - CP TO RETURN USER INFO TO A CLIST'
*                                 WRITTEN BY KERMIT KISER
*              V1M1 - MODIFIED NOV 1982 TO ADD TERMID OPERAND
*              V1M2 - MODIFIED MAR 1983 TO ADD SYSID  OPERAND
*              V1M3 - MODIFIED APR 1983 TO ADD ACFUID  OPERAND
*              V1M4 - MODIFIED DEC 1983 TO USE NEW SETVAR SUBROUTINE
*
*  GETMY IS A TSO COMMAND WHICH RETURNS INFORMATION
*        REQUESTED BY A CLIST IN CLIST VARIABLES.
*        THE INFORMATION RETURNED IS INSTALLATION DEPENDENT
*        AND THE CODE WILL NEED TO BE MODIFIED TO RETURN THE DATA
*        WHICH IS USEFUL FOR YOUR INSTALLATION.
*
*//*
*//* A TSO COMMAND USES SETVAR  AS FOLLOWS:
*//*
*//*     LINK  EP=SETVAR ,(UPT,ECT,ECB,UPLIST)                 V1M4
*//*
*//*          UPT = USER PROFILE TABLE       (FROM CPPL)
*//*          ECT = ENVIRONMENT CONTROL TABLE   "   "
*//*          ECB = COMMAND PROCESSOR ECB
*//*          UPLIST = A FOUR WORD BLOCK AS BELOW:
*//*
*//*
*//*  UPLIST  DS  0XL16
*//*  LOCPTR  DS  AL4         PTR TO VARIABLE NAME
*//*  LOCLEN  DS  F           LENGTH OF VARIABLE NAME
*//*  VALPTR  DS  A           PTR TO DATA VALUE
*//*  VALLEN  DS  F           LENGTH OF DATA VALUE
*//*
*//* EXAMPLE : SET &VAR = &STR(MYDATA)
*//*    WOULD NEED THE FOLLOWING CONTROL BLOCK PASSED TO SETVAR :
*//*
*//*  UPLIST  DS  0XL16
*//*  LOCPTR  DC  A(=C'VAR')    PTR TO VARIABLE NAME
*//*  LOCLEN  DC  F'3'          LENGTH OF VARIABLE NAME
*//*  VALPTR  DC  A(=C'MYDATA') PTR TO DATA VALUE
*//*  VALLEN  DC  F'6'          LENGTH OF DATA VALUE
*//*
GETMY    ENTERR LEVEL=V1M4
         LR    R11,R1                   R11->CPPL
         USING CPPL,R11
         EJECT
*
*              GETMY CONTROL ROUTINE:
*
SA       BAL   R14,PB                   PARSE THE INPUT BUFFER
*
S1       BAL   R14,INITLIST             BUILD PARMLIST FOR IKJUPDT
*
S2       BAL   R14,SETPROG              SET CLIST VAR PROG
*
S3       BAL   R14,SETSYS               SET CLIST VAR SYS
*
S4       BAL   R14,SETACCT              SET CLIST VAR ACCT
*
S5       BAL   R14,SETTERM              SET CLIST VAR TERMID
*
S6       BAL   R14,SETSYSID             SET CLIST VAR SYSID
*
S7       BAL   R14,SETACFID             SET CLIST VAR ACFUID
         EJECT
         LEAVER
         EJECT
PB       ST    R14,L1LS
         PARSEKMD WORK=FASTAREA
         SPACE 2
         L     R14,L1LS
         BR    R14
         EJECT
*
*              BUILD UPDTLIST (IKJUPDT PARM LIST)
INITLIST ST    R14,L1LS
         MVC   UPDTUPT,CPPLUPT          UPT
         MVC   UPDTECT,CPPLECT          ECT
         LA    R9,ECB
         ST    R9,UPDTECB               ECB
         LA    R8,UPLIST
         ST    R8,UPDTUPL               UPLIST
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR PROG AND CALL UPDT:
SETPROG  ST    R14,L1LS
         TM    FLAG,X'80'               PROG REQUESTED?
         BNOR  R14                      NO
         BAL   R14,FINDPROG             LOCATE SYS AND PROG
         LA    R9,=C'PROG'
         ST    R9,LOCPTR                VARIABLE NAME
         MVC   LOCLEN,=F'4'             VAR NAME LEN
         ST    R5,VALPTR                VALUE LOCATION
         MVC   VALLEN,=F'20'            VALUE LENGTH
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR SYS AND CALL UPDT:
SETSYS   ST    R14,L1LS
         TM    FLAG,X'20'               SYS REQUESTED?
         BNOR  R14                      NO
         BAL   R14,FINDPROG             LOCATE SYS AND PROG
         LA    R9,=C'SYS'
         ST    R9,LOCPTR                VARIABLE NAME
         MVC   LOCLEN,=F'3'             VAR NAME LEN
         LA    R7,3(,R7)                R7->SYS NO IN USE NOW
         ST    R7,VALPTR                VALUE LOCATION
         MVC   VALLEN,=F'2'             VALUE LENGTH
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR ACCT AND CALL UPDT:
SETACCT  ST    R14,L1LS
         TM    FLAG,X'40'               ACCT REQUESTED?
         BNOR  R14                      NO
         BAL   R14,FINDPROG             LOCATE SYS AND PROG
         LA    R9,=C'ACCT'
         ST    R9,LOCPTR                VARIABLE NAME
         MVC   LOCLEN,=F'4'             VAR NAME LEN
         LA    R6,25(,R5)               R6->ACCT NO IN USE
         MVC   ACCTVAL(9),0(R6)         GET ACCT
         MVI   ACCTVAL+4,C','           FOR FUN
         LA    R6,ACCTVAL
         ST    R6,VALPTR                ACCT VALUE PTR
         MVC   VALLEN,=F'9'             ACCT VALUE LEN
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR TERMID & CALL UPDT:
SETTERM  ST    R14,L1LS
         TM    FLAG,X'10'             TERMID REQUESTED?
         BNOR  R14                      NO
         L     R10,CPPLPSCB       R10->PSCB
         L     R10,68(,R10)       R10->PSCBX
         LA    R9,=C'TERMID'
         ST    R9,LOCPTR                VARIABLE NAME
         MVC   LOCLEN,=F'6'             VAR NAME LEN
         LA    R6,42(,R10)        R6->TERMID FIELD
         ST    R6,VALPTR          TERMID VALUE PTR
         MVC   VALLEN,=F'8'       TERMID VALUE LEN
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR SYSID & CALL UPDT:
SETSYSID ST    R14,L1LS
         TM    FLAG,X'08'             SYSID REQUESTED?
         BNOR  R14                      NO
         LA    R9,=C'SYSID'       VARIABLE NAME
         ST    R9,LOCPTR          R9->  VARIABLE NAME
         MVC   LOCLEN,=F'5'             VAR NAME LEN
*              FIND THE SYSTEM ID :
         L     R7,CVTPTR
         USING CVT,R7
         L     R6,CVTSMCA
         USING SMCABASE,R6
         L     R6,SMCASID
         STCM  R6,15,SYSTEMG
         DROP  R6,R7
         LA    R6,SYSTEMG         R6->SYSID FIELD
         ST    R6,VALPTR          SYSID VALUE PTR
         MVC   VALLEN,=F'4'       SYSID VALUE LEN
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
         L     R14,L1LS
         BR    R14
         EJECT
*              BUILD UPLIST (UPDATE PARM LIST) FOR ACFUID & CALL UPDT:
SETACFID ST    R14,L1LS
         TM    FLAG,X'04'            ACFUID REQUESTED?
         BNOR  R14                      NO
         LA    R9,=C'ACFUID'      VARIABLE NAME
         ST    R9,LOCPTR          R9->  VARIABLE NAME
         MVC   LOCLEN,=F'6'             VAR NAME LEN
*              FIND THE ACFUID    :
         LA    R7,ACFFLD
         ST    R7,ACFPTR
         MVC   ACFFLD(2),=H'24'   SET FIELD LENGTH
         LA    R1,ACFPTR
         L     R15,=V(GETMYUID)         CALL LINDA'S ACF2 FINDER
         LTR   R15,R15                 DO WE HAVE IT?
         BZ    ACFX                    NO-> CANNOT DO THIS THEN
         BALR  R14,R15                  CALL LINDA'S ACF2 FINDER
*              PASS THE ACFUID    :
         LA    R6,ACFFLD+2              R6->ACFUID FIELD
         ST    R6,VALPTR                ACFUID VALUE PTR
         MVC   VALLEN,=F'24'            ACFUID VALUE LEN
         LA    R1,UPDTLIST              R1->IKJUPDT PARM LIST
         LINK  EP=SETVAR                DO THE UPDATE              V1M4
ACFX     L     R14,L1LS
         BR    R14
         EJECT
*              FIND PROG AND SYS:
FINDPROG L     R5,540                   R5->TCB
         L     R5,180(,R5)              R5->JCSB
         L     R5,260(,R5)              R5->JCT
         LA    R7,24(,R5)               R7->JOBNAME
         ICM   R5,7,56(R5)              R5->ACT
         LA    R5,24(,R5)               R5-> PROG NAME FIELD
         BR    R14
         EJECT
STATWORK EQU   *
         SPACE
OPLIST   OPERLIST OPER1,OPER2,OPER3,OPER4,OPER5,OPER6
OPER1    OPER  PROG,X'8000'
OPER2    OPER  ACCT,X'4000'
OPER3    OPER  SYS,X'2000'
OPER4    OPER  TERMID,X'1000'
OPER5    OPER  SYSID,X'0800'
OPER6    OPER  ACFUID,X'0400'
         LTORG
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
*
UPDTLIST DS    0F
UPDTUPT  DS    F
UPDTECT  DS    F
UPDTECB  DS    F
UPDTUPL  DS    F
*
UPLIST   DS    0F
LOCPTR   DS    AL4
LOCLEN   DS    FL4
VALPTR   DS    AL4
VALLEN   DS    FL4
*
SYSTEMG  DC    C'    '
ECB      DS    F
ACCTVAL  DS    CL9
         EJECT
*
ACFPTR   DS    F
ACFFLD   DS    H,CL24
*
FLAG     DS    F
         SPACE
         KPPL
FASTAREA DS    XL512
         SPACE 2
WORKLEN  EQU   *-WORKAREA
         EJECT
         IKJCPPL
         EJECT
         PRINT  OFF
         CVT     DSECT=YES
         IEESMCA ,
         END
