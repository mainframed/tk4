         TITLE 'SCXSCAN - PCFSCAN REPLACEMENT'
*                                                                 WDPSC
*        MODIFICATION LOG:                                        WDPSC
*                                                                 WDPSC
*           09/30/85 - UPDATE TO ALLOW %X CLISTS                  WDPSC
*                                                                 WDPSC
*        NOTE: THIS PROGRAM IS LINKED AS THE ENTRY POINT FOR      WDPSC
*              IBM MODULE IKJSCAN TO PROVIDE AN X CMD FUNCTION    WDPSC
*              LIKE THE PCF X FUNCTION                            WDPSC
*                                                                 WDPSC
*        NOTE: IF YOU ARE RUNNING ACF2 COMMAND LIMITING LIKE      WDPSC
*              WE DO, ADD SYSPARM(NNN) TO ASSEMBLY WHERE          WDPSC
*              NNN IS YOUR ACF2 COMMAND VALIDATION SVC            WDPSC
*                                                                 WDPSC
*10/04/85  NOTE: IF YOU ARE ASSEMBLING FOR A TSO/E R2.1 (XA)
*                SYSTEM, ADD SYSPARM(XATSOE) TO THE ASSEMBLER
*                OPTIONS AND USE ASSEMBLER H WITH XA MACROS...
*                THEN LINK THIS INTO IKJTSLAR CHANGING THE IKJSCAN
*                ENTRY POINT TO NAME IBMSCAN
*
         AIF   ('&SYSPARM' NE 'XATSOE').NXA
IKJSCAN  AMODE ANY
.NXA     ANOP
IKJSCAN  ENTERR LEVEL=V1M1
         SPACE 3
         BAL   R14,SCAN           FIND COMMAND NAME
         LTR   R15,R15            OK?
         BNZ   EXIT               BAD NEWS->EXIT
         BAL   R14,XCMD           PROCESS IF 'X' REQUESTED
*
         BAL   R14,CLEANUP        FIX ECT,CSOA,ETC.
*
EXIT     LEAVER
         EJECT
SCAN     ST    R14,L1LS
         LR    R11,R1             R11->CSPL
         USING CSPL,R11
         L     R9,CSPLOA          R9->CSOA
         USING CSOA,R9
         L     R8,CSPLECT         R8->ECT
         USING ECT,R8
         LA    R7,CPPLA           R7->CPPL
         USING CPPL,R7
         XC    XFLAG(3),XFLAG     CLEAR FLAGS
         SPACE 2
CSCAN    L     R15,=V(IBMSCAN)    R15->IKJSCAN
         BALR  R14,R15            CALL IBM SCAN
         ST    R15,SCANRC         SAVE HIS RC
         LTR   R15,R15            0?
         BNZ   SCANX              BAD NEWS
         L     R6,CSOACNM         R6->CMDNAME
         LTR   R6,R6              ANY CMD?
         BNZ   XCHK               YES
NOCMD    OI    NOCMDF,X'FF'       SAY NONE
         B     SCANX              GET OUT
XCHK     CLI   0(R6),C'X'         X?
         BNE   OCMD               NO
         CLI   CSOALNM+1,1        LEN=1?
         BNE   OCMD
*  MOD TO ALLOW X CMD OR %X CLIST:
         CLI   CSOAFLG,X'04'      X CLIST?
         BE    OCMD               YUP.
         CLI   CSOAFLG,X'40'      X BY ITSELF?
         BE    OCMD               YUP->CAN'T HELP HIM
         SPACE
HAVEX    OI    XFLAG,X'FF'        FLAG EXIT REQUESTED
         MVI   0(R6),C' '         ERASE 'X' CMD
         L     R5,CSPLCBUF        R5->CBUF
         XC    2(2,R5),2(R5)      SET OFFSET TO 0
         B     CSCAN              LOOK AGAIN
*
OCMD     CLI   CSOAFLG,X'20'      OK CMD?
         BH    GOOD               YES
         CLI   CSOAFLG,X'04'      CLIST?
         BNE   NOCMD              NO->BAD
         OI    CLISTF,X'FF'       YES->FLAG CLIST
GOOD     LH    R5,CSOALNM         R5=LENGTH
         LTR   R5,R5              OK?
         BZ    NOCMD              NO
         STH   R5,CMDLEN          YES SAVE LEN
         BCTR  R5,R0              R5-1
         MVC   CMDNAME,=CL8' '    CLR
MVCMD    MVC   CMDNAME(0),0(R6)   MOVE NAME
         EX    R5,MVCMD           WITH LENGTH
SCANX    L     R14,L1LS
         BR    R14
         EJECT
XCMD     ST    R14,L1LS
         TM    XFLAG,X'FF'        X?
         BZR   R14                NO
         TM    NOCMDF,X'FF'       ANY REAL CMD PRESENT?
         BNZR  R14                NO
         CLC   CMDNAME,=CL8'TIME' ?
         BNE   CKEXEC             NO
         MVC   CMDNAME,=CL8'IKJEFT25'  USE REAL NAME
         B     ATTCMD             SKIP AUTH CHECK
CKEXEC   TM    CLISTF,X'FF'       IMPLICIT CLIST?
         BZ    ACF2BLDL           NO->ISSUE AUTH CHECK
UEXEC    L     R5,CSPLCBUF        R5->CMD BUFF
         XC    2(2,R5),2(R5)      CLEAR OFFSET
         MVC   CMDNAME,=CL8'EXEC' USE EXEC CMD
         B     ATTCMD             GO ATTACH
ACF2BLDL EQU   *                  ZAP TO ACF2 SVC HERE:
         AIF   ('&SYSPARM' EQ 'XATSOE').XA1
         AIF   ('&SYSPARM' NE '').ACF1
.XA1     MVC   BLDLIST(4),=X'0001000E'  1 ENTRY, 14 BYTES
         AGO   .NACF1                                             WDPSC
.ACF1    ANOP                                                     WDPSC
         MVC   BLDLIST(4),=X'00010008'  1 ENTRY, 8 BYTES (ACF2 CMD LIM)
.NACF1   ANOP                                                     WDPSC
         MVC   BLDNAME,CMDNAME    GET CHECK NAME
         BLDL  0,BLDLIST          SEARCH FOR IT
         AIF   ('&SYSPARM' EQ 'XATSOE').NACF2
         AIF   ('&SYSPARM' EQ '').NACF2
         ORG   *-2
         SVC   &SYSPARM WDPSC=247 SUBSTITUTE ACF2 CMD LIMIT SVC
.NACF2   ANOP                                                     WDPSC
         LTR   R15,R15            FOUND IT?
         BNZ   UEXEC              NO->TRY IT AS A CLIST
ATTCMD   BAL   R14,ATTACH         YES->ATTACH THE COMMAND
*
XX       L     R14,L1LS
         BR    R14
         EJECT
CLEANUP  ST    R14,L1LS
         TM    XFLAG,X'FF'        WAS X REQ?
         BZ    NOXC               NO
         XC    CSOACNM(6),CSOACNM YES->CLEAR CMD INDICATORS
         MVI   CSOAFLG,CSOANOC    ETC
         CLC   WASCMD,=CL8'TEST'  UNDER TEST?
         BNE   XR                 NO
         MVI   CSOALNM+1,1        TEST MUST HAVE CMD
         MVI   ECTSCMD,C'W'       SO SET UP PSUEDO 'W' CMD
         MVI   CSOAFLG,CSOAVNP    NO PARMS
         LA    R1,ECTSCMD         R1->W
         ST    R1,CSOACNM         PASS
XR       SLR   R15,R15            RC=0
         BR    R14                CONTINUE
NOXC     L     R15,SCANRC         RESTORE SCAN RC
         BR    R14
         EJECT
ATTACH   ST    R14,L2LS
         MVC   IOPLSTK(12),CSPL   INIT IOPL
         MVC   WASCMD,ECTPCMD     SAVE CMD NAME
         MVC   ECTSCMD,=CL8' '    CLEAR SUB CMD
         MVC   ECTPCMD,CMDNAME    GET NEW PRIME
         NI    ECTSWS,255-ECTNOPD-ECTATRM
         TM    CSOAFLG,CSOAVNP    OPS?
         BZ    CE                 YES
         OI    ECTSWS,ECTNOPD     NO
         SPACE
CE       XC    ECB,ECB            CLEAR AN ECB
         XC    STXL(SL),STXL      CLR STAX PARM LIST
         SPACE
         STAX  STAXEX,USADDR=ECB,REPLACE=NO,MF=(E,STXL)
         EJECT
         MVC   CPPLCBUF,CSPLCBUF
         MVC   CPPLUPT,CSPLUPT
         MVC   CPPLECT,CSPLECT
         L     R1,540             R1->TCB
         L     R1,180(,R1)        R1->JSCB
         L     R1,264(,R1)        R1->PSCB
         ST    R1,CPPLPSCB        PASS TO CMD
         LA    R1,CPPL            R1->CPPL
         MVC   ATTL(AE-ATTP),ATTP  INIT ATTACH LIST
         LA    R15,ATTL           R15->ATTACH PARM LIST
         ATTACH EPLOC=CMDNAME,ECB=ECB,MF=(E,(1)),               XXXXXXXX
               SF=(E,(15)),ESTAI=(STAIX,ECB)
         ST    R1,TCBA            SAVE TCB ADDR
         LTR   R15,R15            OK?
         BZ    WAITIT             YES->WAIT TIL DONE
         BAL   R14,ATTERR         NO->CALL ERROR SUB
         B     ATTX               EXIT
         EJECT
WAITIT   WAIT  ECB=ECB
         DETACH TCBA,STAE=YES
         STAX
         MVC   ECTRTCD(3),ECB+1   GET CODE
         TM    ECB+4,3            ERROR END?
         BZ    NOERR
         BAL   R14,CMDERR         CALL CMD ERROR SUBRTN
NOERR    MVC   ECTSCMD,ECTPCMD    MOVE EXIT CMD TO SUBCMD
         MVC   ECTPCMD,WASCMD     RESTORE MAJOR CMD NAME
         EJECT
*
*              ISSUE MODE MSG IF  USER WISHES:
*                                 (NOT NEEDED - 6/17/82)
*              ACCOUNT,EDIT,PEEK AND TEST HANDLE MODE MSGS THEMSELVES,
*              EIS61 MAY NEED REWRITING IN EISATTN.
*
*        L     R6,CSPLUPT         R6->UPT
*        USING UPT,R6
*        TM    UPTSWS,UPTMODE     MODE MSG DESIRED?
*        BZ    ATTX               NO!
*        DROP  R6
*        MVC   MH(4),=X'000C0000' MSG LEN,OFFSET
*        PUTLINE PARM=PUTL,TERMPUT=EDIT,MF=(E,IOPLSTK),
*              OUTPUT=(MH,TERM,SINGLE,DATA)
*
*        EXIT FROM ATTACH ROUTINE:
*
ATTX     L     R14,L2LS
         BR    R14
         EJECT
*
*              ISUUE MESSAGE IF ERROR IN ATTACH:
*
ATTERR   ST    R14,L3LS
         ST    R15,ATTRC
         MVC   MSGA(CMDERR2+2-CMDERRM),CMDERRM MOVE MSG TO WORK
         LA    R10,MSGA
         USING CMDERRM,R10
         MVC   CMDERR1(8),=CL8'ATTACH'
         UNPK  CMDERR2(3),ATTRC+3(2)
         TR    CMDERR2(2),TRANS-240
         TPUT  (R10),CMDERR2+2-CMDERRM
         DROP  R10
         L     R14,L3LS
         BR    R14
         EJECT
*
*              ISSUE MSG IF ABEND OR ATTENTION OCCURED:
*
CMDERR   ST    R14,L3LS
         ST    R15,SCANRC
         MVC   MSGA(DETMSGE-DETMSG),DETMSG  MOVE MSG TO WORK
         LA    R10,MSGA
         USING DETMSG,R10
         LA    R1,DETMSG1
         ST    R1,DETMSG+4
         TM    ECB+4,2            ATTN?
         BZ    DETABND            NO
         MVC   DETMSGA(12),=CL12'ATTENTION.' COMPLETE MSG
         B     DETPUTL
DETABND  MVC   DETMSGA(6),=CL6'ABEND'
         UNPK  DETMSGA+6(7),ECB+1(4)
         TR    DETMSGA+6(6),TRANS-240
         MVI   DETMSGA+12,C'.'
DETPUTL  MVC   DETMSGB(8),ECTPCMD MOVE IN CMD NAME
         DROP  R10
         PUTLINE PARM=PUTL,TERMPUT=EDIT,MF=(E,IOPLSTK),                X
               OUTPUT=(MSGA,TERM,SINGLE,INFOR)
         L     R14,L3LS
         BR    R14
         EJECT
STAXEX   L     R1,8(R1)           CP ECB ADDR
         OI    4(R1),2            SET ATTN SWCH
         POST  (R1),4             POST CP DONE
         BR    R14
*
STAIX    LA    R3,12
         USING  *,R15
         CR    R0,R3              SDWA PRESENT?
         BE    XSET               NO USE R2
         USING  SDWA,R1
         L     R2,SDWAPARM
         SETRP RC=16
XSET     OI    4(R2),1            SET ABEND FLG
         LA    R15,16             CONTINUE ABEND
         BR    R14
         DROP  R15,R1
         EJECT
*
*              CONSTANT AREAS:
*
TRANS    DC    C'0123456789ABCDEF '
ATTP     ATTACH SHSPV=78,SZERO=NO,SF=L
AE       EQU   *
         SPACE 2
         LTORG
         SPACE 2
DETMSG   DC    A(1,DETMSG1)
DETMSG1  DC    Y(DETMSGE-DETMSG1,0),C'SCXSCAN COMMAND '''
DETMSGB  DC    C'XXXXXXXX'' TERMINATED DUE TO '
DETMSGA  DC    C'XXXXXXXXXXX  '
DETMSGE  EQU   *
CMDERRM  DC    C'TSO MAIN COMMAND SYSTEM ERROR - SCXSCAN '
CMDERR1  DC    C'XXXXXXXX ERROR CODE '
CMDERR2  DC    C'XX '
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
SCANRC   DS    F
ATTRC    DS    F
XFLAG    DS    X
NOCMDF   DS    X
CLISTF   DS    X
CMDLEN   DS    H
CMDNAME  DS    CL8
MH       DS    F
WASCMD   DS    CL8
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
BLDLIST  DS    H
         DS    H
BLDNAME  DS    CL8
         DS    F
ECB      DS    D                  ECB AND FLAGS
CPPLA    DS    4F
STXL     STAX  MF=L
SL       EQU   *-STXL
ATTL     DS    CL(AE-ATTP)
TCBA     DS    F
MSGA     DS    CL100
IOPLSTK  DS    4F
PUTL     DS    3F
WORKLEN  EQU   *-WORKAREA
         EJECT
         IKJECT
         IKJUPT
         IKJCPPL
         IKJCSPL
         IKJCSOA
         IHASDWA
         END
