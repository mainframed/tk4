*
*   @OK - ADJUSTED TO INVOKE COMPATIBLE LEVEL OF COMMAND PARSE MACRO
*         10/26/98
*
*          DATA SET 761KKSUPRN AT LEVEL 001 AS OF 08/30/82
*
*  AUTHOR - KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*
SUPRNAME ENTERR LEVEL=V3M0
         LR    R11,R1             R11->PARM LIST
         SLR   R15,R15            R15=0
         EJECT
*
*        MAINLINE ROUTINE:
*
         BAL   R14,INIT           OPEN CONTROL AND MESSAGE FILES
         BXH   R15,R15,MX         STOP IF FATAL ERROR
*
         BAL   R14,PROCNTL        PROCESS ALL REQUESTS
*
EODAD    LR    R11,R15            SAVE RC
         BAL   R14,EOD            CLOSE UP ALL OPEN FILES
*
         BAL   R14,SZAP           INVOKE SUPERZAP IF REQUESTED
*
         BAL   R14,TERM           DO ANY NEEDED CLEANUP
*
         LR    R15,R11            RESTORE RC
*
         EJECT
*
*        CODE  TO LEAVE SUPRNAME:
*
MX       LEAVER
         EJECT
*                                 INITIALIZE FILES:
*
INIT     ST    R14,L1LS
*
         OPEN  (CTLCD,INPUT,MSGDCB,OUTPUT,UT1,OUTPUT)
         TM    MSGDCB+48,X'10'    MSG FILE OK?
         BO    MOK                YES
         ABEND 88                 NO
MOK      LA    R1,OKM             R1->SUPRNAME IDENT MSG
         BAL   R14,PUTMSGV        PRINT
         B     CIF
OKM      DC    AL2(L'OKMD)
OKMD     DC    C'1 *** SUPRNAME - DSCB UTILITY - WDPSC VERSION 3.0 '
CIF      TM    CTLCD+48,X'10'     INPUT FILE OPEN?
         BO    IOK                YES
         LA    R1,IOEM            R1->ERR1 MSG
         BAL   R14,PUTMSGV        TELL
         LA    R15,44             RC=44
         B     IX
IOEM     DC    AL2(L'IOEMD)
IOEMD    DC    C'2CONTROL FILE CAN NOT BE OPENED. SUPRNAME TERMINATED.'
IOK      OI    CTLF,X'FF'
         OI    MSGF,X'FF'
         TM    UT1+48,X'10'       SYSUT1 OPEN?
         BNO   GETSER             NO->MAY BE OK
         OI    UT1F,X'FF'
*
GETSER   RDJFCB (VTOCDCB)
         BXH   R15,R15,IX         EXIT IF CAN'T DO
         MVC   VOLSER(6),JFCBA+118      GET VOLSER
*
IX       L     R14,L1LS
         BR    R14                EXIT
         EJECT
CTLCD    DCB   DDNAME=SYSIN,DSORG=PS,EODAD=EODAD,MACRF=(GM),LRECL=80
MSGDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FA
UT1      DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(PM),LRECL=80,RECFM=F
DCBEXITS DC    0F'0',X'87',AL3(JFCBA)
JFCBA    DS    0F,176X
VTOCDCB  DCB   DSORG=PSU,DDNAME=SYSLIB,EXLST=DCBEXITS,                 X
               MACRF=E,RECFM=U,KEYLEN=44
         EJECT
*
*              SUBRTN TO PROCESS  CONTROL CARD REQUESTS:
*
*
PROCNTL  ST    R14,L1LS
*
PROLOOP  BAL   R14,READPARS       GET CONTROL CARD
         BXH   R15,R15,PX         EXIT IF CAN'T DO
*
         BAL   R14,PENDCLR        DO ANY REQUESTS COMPLETE
         BXH   R15,R15,PX         EXIT IF CAN'T DO
*
         B     PROLOOP
*
PX       L     R14,L1LS
         BR    R14                EXIT
*
         EJECT
*
*              SUBRTN TO CLOSE ANY OPEN FILES:
*
*
EOD      ST    R14,L1LS
*
         TM    CTLF,X'FF'         CTL FILE OPEN?
         BZ    NDCK               NO
         CLOSE (CTLCD)
*
NDCK     TM    VDCBF,X'FF'        VTOC OPEN?
         BZ    UTCK               NO
         CLOSE (VTOCDCB)
*
UTCK     TM    UT1F,X'FF'         UT1  OPEN?
         BZ    EODX               NO
         CLOSE (UT1)
*
*
EODX     L     R14,L1LS
         BR    R14                EXIT
         EJECT
*
*              SUBRTN TO CALL SUPERZAP IF REQUESTED:
*
*
SZAP     ST    R14,L1LS
*
         TM    ZAPF,X'FF'         ZAP NEEDED?
         BZR   R14                NO
*
         LINK  EP=AMASPZAP,PARAM=(OPTS,DDNAMES),VL=1
*
SZX      L     R14,L1LS
         BR    R14                EXIT
*
*              SUBRTN TO CLEANUP ANYTHING NEEDED:
*
TERM     ST    R14,L1LS
*
         LA    R1,TMSG
         BAL   R14,PUTMSGV
*
         TM    MSGF,X'FF'         MSG FILE OPEN?
         BZ    TERMX              NO
         CLOSE (MSGDCB)
*
TERMX    L     R14,L1LS
         BR    R14
*
TMSG     DC    AL2(L'TMSGD)
TMSGD    DC    C'1*** SUPRNAME PROCESSING COMPLETED. ***'
         EJECT
*
*              SUBRTN TO READ A CARD AND PARSE IT:
*
READPARS ST    R14,L2LS
*
         MVC   CMD(133),=CL133' ' CLEAR
         LA    R1,CMDAREA+4
         BAL   R14,PUTMSG
*
         GET   CTLCD,CMDAREA+5
*
         MVC   CMDAREA(4),=H'72,0'
         LA    R14,CMDAREA        R14->CMD
         ST    R14,CMDPTR         PASS TO PARSE
*
         LA    R1,CMDAREA+4
         BAL   R14,PUTMSG
         EJECT
         PARSKMD CBUF=CMDPTR,EXIT=UNKNEX,FLAG=NFLAG,PARM=UNKNFLAG,     X
               WORK=FASTAREA,KEYLIST=KEYLIST,KEYWORK=KEYWORK        @OK
*
         L     R14,L2LS
         BR    R14
         SPACE 3
UNKNEX   L     R1,0(,R1)
         OI    0(R1),X'FF'
         SLR   R15,R15
         BR    R14
         EJECT
PENDCLR  ST    R14,L2LS
*
         CLI   CMDAREA+4,C'*'     COMMENT CARD?
         BNE   NEWCHK             NO
         LA    R1,CMDAREA+4       R1->CARD
         BAL   R14,PUTUT1         WRITE IT
         B     PCX
NEWCHK   TM    NFLAG,X'FF'        NEW REQUEST?
         BNZ   CLRCHK             YES
         TM    KEYFLG,X'FF'       JUST KEYWORDS?
         BZ    CLRCHK             NO->UNKN OP MAYBE
         B     CHECKDO
PCX      MVI   UNKNFLAG,X'00'     RESET UNKNS
         L     R14,L2LS
         BR    R14
CLRCHK   TM    FLAG,X'FF'         ANY PENDING?
         BZ    CHECKDO            NO->WE ARE OK
         LA    R1,LERR1           R1->ERROR MSG
         BAL   R14,PUTMSGV
         LA    R15,100
         MVI   ZAPF,X'00'         SAY NO SZAP
         B     PCX
LERR1    DC    AL2(L'LERR1M)
LERR1M   DC    C' NEW OPERATION FOUND BEFORE PREVIOUS COMPLETED.   -----
                   SUPRNAME TERMINATED.'
*
CHECKDO  OC    FLAG(4),NFLAG      SAVE FLAGS
         XC    NFLAG(4),NFLAG     CLR  FLAGS
         OC    KEYFLGS(4),KEYFLG  SAVE KEYWD FLAGS
         XC    KEYFLG(4),KEYFLG   CLEAR FLAG
         TM    FLAG,X'80'         RENAME OP?
         BZ    CD2                NO
         BAL   R14,RENAME         DO IT
         B     PCX
CD2      TM    FLAG,X'40'         CCHHR  OP?
         BZ    CD3                NO
         BAL   R14,CCHHR          DO IT
         B     PCX
CD3      TM    FLAG,X'30'         ABSDUMP OP?
         BZ    CD5                NO
         BAL   R14,ABSDUMP        DO IT
         B     PCX
CD5      TM    FLAG,X'08'         DELETE  OP?
         BZ    CD4                NO
         BAL   R14,DELETE         DO IT
         B     PCX
CD4      LA    R1,CMDAREA+4       R1->CARD
         BAL   R14,PUTUT1         WRITE IT
         B     PCX
         EJECT
         EJECT
ABSDUMP  EQU   *
CCHHR    ST    R14,L3LS
         TM    UNKNFLAG,X'FF'     UNKNOWN OPERANDS PRESENT?
         BZ    MINE
         MVI   UNKNFLAG,X'00'     RESET FLAG
         B     JUSTRITE           WRITE THE RECORD TO ZAP ASIS
MINE     TM    KEYFLGS,X'80'      OLDNAME GIVEN?
         BNOR  R14                NO->WAIT FOR IT
         BAL   R14,GETCCHHR       GET CCHHR FOR IT
         BXH   R15,R15,CX         DSCB FOUND?
         UNPK  AAC(11),AAH(6)     UNPACK THE CCHHR
         TR    AAC(10),HTCTAB     MAKE IT CHARACTER
         MVC   CMD(80),=CL133' '  BLANK FILL
         SLR   R3,R3              R3=0
         IC    R3,FLAG            R3=OPCODE
         SRL   R3,4               R3=OP#
         BCTR  R3,R0              R3-1 OFFSET
         MH    R3,=H'10'          R3*10
         LA    R3,OPTAB(R3)       R3->OP NAME
         MVC   CMD+1(10),0(R3)    FTN NAME (CCHHR,ABSDUMPT,ABSDUMP)
         MVC   CMDAREA+15(10),AAC PUT IN ADDR OF DSCB
         TM    FLAG,X'30'         ABSDUMP?
         BZ    JUSTRITE           NO
         MVC   CMDAREA+27(10),AAC PUT IN ADDR OF DSCB
JUSTRITE LA    R1,CMDAREA+4
         BAL   R14,PUTUT1         WRITE TO SZAP
         LA    R1,CMDAREA+4
         BAL   R14,PUTMSG         WRITE TO PRINT
         XC    FLAG,FLAG          CLEAR THE FLAGS
         XC    KEYFLGS,KEYFLGS    CLEAR THE FLAGS
CX       L     R14,L3LS
         BR    R14
*
OPTAB    DC    CL10'ABSDUMPT'
         DC    CL10'ABSDUMP'
         DC    CL10'DUMMY'
         DC    CL10'CCHHR'
         EJECT
*
RENAME   ST    R14,L4LS
         TM    KEYFLGS,X'C0'      OLDNAME AND NEWNAME
         BNOR  R14                NO->WAIT FOR
         BAL   R14,GETCCHHR       FIND THE DSCB
         BXH   R15,R15,RX         FOUND?
*
         BAL   R14,OPENVTOC       OVEN THE VTOC
         BXH   R15,R15,RX         OK?
*
         BAL   R14,CHDSCB         MODIFY THE DSCB
         BXH   R15,R15,RX         OK?
*
         LA    R1,RNM             R1->RENAME OK MSG
         BAL   R14,PUTMSGV        WRITE TO PRINT
         XC    FLAG,FLAG          CLEAR THE FLAGS
         XC    KEYFLGS,KEYFLGS    CLEAR THE FLAGS
RX       L     R14,L4LS
         BR    R14
*
RNM      DC    AL2(L'RNMD)
RNMD     DC    C' RENAME COMPLETED.'
         EJECT
*
GETCCHHR ST    R14,L5LS
         OBTAIN ODLST             GET CCHHR FOR IT
         LTR   R15,R15            DSCB FOUND?
         BNZ   DSCBNF             NO->TELL AND STOP
         XC    MBBCCHHR,MBBCCHHR
         MVC   MBBCCHHR+3(5),AAH  SAVE CCHHR
GAX      L     R14,L5LS
         BR    R14
*
DSCBNF   LA    R1,NFERR           R1->ERR MSG
         BAL   R14,PUTMSGV        WRITE TO USER
         LA    R15,200            SAY BAD DEAL
         MVI   ZAPF,X'00'         SAY NO CAN SZAP
         B     GAX                TERMINATE
NFERR    DC    AL2(L'NFERRM)
NFERRM   DC    C' DSCB NOT FOUND - SUPRNAME TERMINATED.'
         EJECT
*
*
OPENVTOC ST    R14,L5LS
         TM    VTOCDCB+48,X'10'   VTOC OPEN?
         BOR   R14                YES
*
         RDJFCB (VTOCDCB)
         BXH   R15,R15,VOERR      EXIT IF CAN'T DO
         MVC   VOLSER(6),JFCBA+118      GET VOLSER
         MVI   JFCBA,X'04'        VTOC
         MVC   JFCBA+1(43),JFCBA  "
         OPEN  (VTOCDCB,(INOUT)),TYPE=J
         TM    VTOCDCB+48,X'10'   OK?
         BNO   VOERR              NO
*
         L     R1,540             R1->TCB
         L     R1,12(,R1)         R1->TIOT
         LH    R14,VTOCDCB+40     R14=TIOT OFFSET
         LA    R1,16(R1,R14)      R1->TIOT UCB FLD
         L     R1,0(,R1)          R1->UCB FOR VTOC
         MVC   SDT(4),16(R1)      SAVE DEVTYPE FOR SCRATCH
*
OVX      L     R14,L5LS
         BR    R14
VOERR    LA    R1,VOEM
         BAL   R14,PUTMSGV
         LA    R15,99
         B     OVX
VOEM     DC    AL2(L'VOEMM)
VOEMM    DC    C' CANNOT OPEN VTOC'
*
         EJECT
CHDSCB   ST    R14,L5LS
*
         ENQ   (SYSVTOC,VOLSER,E,6,SYSTEMS),RET=HAVE
*
         XDAP  VECB1,RI,VTOCDCB,DSCBA,140,,MBBCCHHR,MF=E
*
         WAIT  ECB=VECB1
*
         CLC   OLDNAME+4(44),DSCBA
         BNE   RE1
         MVC   DSCBA(44),NEWNAME+4
         EJECT
         XDAP  VECB2,WI,VTOCDCB,DSCBA,140,,MBBCCHHR,MF=E
*
         WAIT  ECB=VECB2
*
         DEQ   (SYSVTOC,VOLSER,6,SYSTEMS),RET=HAVE
*
         CLI   VECB2,X'7F'        IO OK?
         BE    CDX                YES
         DC    H'0',C'EXCP OUTPUT ERROR'
*
CDX      L     R14,L5LS
         BR    R14
*
RE1      LA    R1,DRE1
         BAL   R14,PUTMSGV
         LA    R15,77
         B     CDX
DRE1     DC    AL2(L'DREM)
DREM     DC    C' DSCB IO ERROR'
         DS    0F
         EJECT
         XDAP  VECB1,RI,MF=L
         XDAP  VECB2,WI,MF=L
         EJECT
DELETE   ST    R14,L3LS
         TM    KEYFLGS,X'80'      OLDNAME
         BNOR  R14                NO->WAIT FOR
         SPACE
*              CREATE A TEMPORARY DATASET NAME:
         TIME
         ST    R0,TOD1            TIME
         ST    R1,DATE1           DATE
         UNPK TOD2(9),TOD1(5)
         UNPK DATE2(8),DATE1(4)
         MVC   TOD3(7),TOD2
         MVC   DATE3(7),DATE2+1
         MVC   NEWNAME+4(44),SDSN
         MVC   NEWNAME+2(2),=AL2(SDL)
         OI    KEYFLGS,X'40'      SAY NEWNAME READY
         SPACE
         LA    R1,SMSG
         BAL   R14,PUTMSGV        WRITE FOR TRACE
         SPACE
         BAL   R14,RENAME         RENAME DS TO TEMPNAME
         BXH   R15,R15,DX         OK?
         EJECT
         SLR   R0,R0
         SCRATCH TEMPDSN
         BXH   R15,R15,BADSCR
         LA    R1,SOKM            R1->SCRATCH OK MESSAGE
         BAL   R14,PUTMSGV        TELL EM
         B     DXOK
BADSCR   LA    R1,SNOK            R1->BAD SCR MSG
         BAL   R14,PUTMSGV
         B     DX
SOKM     DC    AL2(L'SOKMD)
SOKMD    DC    C' DATASET SCRATCHED.'
SNOK     DC    AL2(L'SNOKD)
SNOKD    DC    C' ERROR: DATASET NOT SCRATCHED. SUPRNAME TERMINATED.'
DXOK     XC    FLAG,FLAG          CLEAR THE FLAGS
         XC    KEYFLGS,KEYFLGS    CLEAR THE FLAGS
DX       L     R14,L3LS
         BR    R14
*
TEMPDSN  CAMLST SCRATCH,SDSN,,SVOL,,OVRD
TOD1     DS    F
         DS    H
TOD2     DS    D
         DS    H
DATE1    DS    F
DATE2    DS    D
SMSG     DC    AL2(44+L'SMD)
SMD      DC    C' TEMPNAME IS ->'
SDSN     DC    C'WIZRD150.SUPRNAME.SCRATCH.D'
DATE3    DC    CL7' '
         DC    C'.T'
TOD3     DC    CL7' '
SDL      EQU   *-SDSN
         DC    CL(44-SDL)' '
         EJECT
PUTMSG   ST    R14,PMLS
         LR    R2,R1
         PUT   MSGDCB,(R2)
         SLR   R15,R15
         L     R14,PMLS
         BR    R14
         SPACE
PUTMSGV  ST    R14,PVLS
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(132),OUTLINE
         LH    R14,0(R1)          R14=MSG LEN
         LA    R15,2(,R1)         R15->MSG DATA
         BCTR  R14,R0             R14-1 FOR EX
MM       MVC   OUTLINE(0),0(R15)
         EX    R14,MM
         PUT   MSGDCB,OUTLINE
         SLR   R15,R15
         L     R14,PVLS
         BR    R14
         SPACE 2
PUTUT1   ST    R14,PULS
*
         TM    UT1F,X'FF'         OPEN?
         BO    UOK                YES
         LA    R1,UT1ERR          R1->ERR MSG
         BAL   R14,PUTMSGV
         LA    R15,66
         B     PUX
UT1ERR   DC    AL2(L'UT1ERRD)
UT1ERRD  DC    C' ERROR: SYSUT1 DATASET NOT OPEN. SUPRNAME TERMINATED.'
UOK      OI    ZAPF,X'FF'         SAY ZAP CALL NEEDED
         LR    R2,R1
         PUT   UT1,(R2)           WRITE ZAP RECORD
         SLR   R15,R15
PUX      L     R14,PULS
         BR    R14
         EJECT
*
*              STATIC WORK AREA:
*
SYSVTOC  DC    CL8'SYSVTOC'
HTCTAB   EQU   *-240
         DC    C'0123456789ABCDEF'
ODLST    CAMLST SEARCH,OLDNAME+4,VOLSER,ODAREA
ODAREA   DS    XL140
AAH      EQU   ODAREA+96
AAC      DC    CL20' '
*
L88      DC    H'88'
HTC      DC    CL4'HTC'
*
SVOL     DC    H'1'
SDT      DC    XL4'00'
VOLSER   DS    CL6
SCODE    DC    H'0'
*
PENDFLG  DS    F                  PENDING OP FLAG
CMDPTR   DS    A
FLAG     DC    F'0'
NFLAG    DC    F'0'
OPFLG    DC    F'0'
KEYWORK  DS    0F
KEYSAVE  DS    18F
KEYFLG   DC    F'0'
KEYFLGS  DC    F'0'
OLDNAME  DC    H'44',H'0',CL44' '
NEWNAME  DC    H'44',H'0',CL44' '
HEXNAME  DC    CL88' '
OPTS     DC    H'0'
DDNAMES  DC    H'48'              6 NAME LIST
         DC    3XL8'00'           3 DUMMY ENTS
         DC    CL8'SYSLIB',CL8'SYSUT1',CL8'SYSPRINT'
         EJECT
OPLIST   OPERLIST OP1,OP2,OP3,OP4,OP5,OP6
OP1      OPER  RENAME,X'8000'
OP2      OPER  CCHHR,X'4000'
OP3      OPER  ABSDUMP,X'2000'
OP4      OPER  ABSDUMPT,X'1000'
OP5      OPER  DELETE,X'0800'
OP6      OPER  SCRATCH,X'0800'
*
KEYLIST  OPERLIST KEY1,KEY2
KEY1     OPER  DSNAME,X'8000',KEYFLD=OLDNAME
KEY2     OPER  NEWNAME,X'4000',KEYFLD=NEWNAME
         LTORG
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
L4LS     DS    F
L5LS     DS    F
L6LS     DS    F
L7LS     DS    F
PVLS     DS    F
PMLS     DS    F
PULS     DS    F
*
VDCBF    DS    X
CTLF     DS    X
MSGF     DS    X
UT1F     DS    X
ZAPF     DS    X
UNKNFLAG DS    X
*
MBBCCHHR DS    XL8
*
CMDAREA  DS    F,CL256            CMD BUF AREA
CMD      EQU   CMDAREA+4
*
FASTAREA DS    XL600              PARSE WORKAREAS
DSCBA    DS    200X
OUTLINE  DS    CL133
         EJECT
         KPPL
*
WORKLEN  EQU   *-WORKAREA
         BPPL  DSECT
         END
