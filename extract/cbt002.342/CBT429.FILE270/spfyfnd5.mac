       ID DIVISION.
       PROGRAM-ID.  SPFYFND5.

      ****************************************************************
      *                                                              *
      *   THIS PROGRAM SUPERCEDES SPFYFND4.                          *
      *                                                              *
      ****************************************************************
           EJECT
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.

           SELECT  INPUT-FILE          ASSIGN UT-S-CMDOUT1.
           SELECT  OUTPUT-FILE         ASSIGN UT-S-OUTFILE.
           EJECT
       DATA DIVISION.

       FILE SECTION.

       FD  INPUT-FILE
           LABEL RECORDS ARE   STANDARD
           RECORDING MODE IS   F
           RECORD CONTAINS     80 CHARACTERS
           BLOCK CONTAINS      0 RECORDS
           DATA RECORDS IS     INPUT-REC.

       01  INPUT-REC                              PIC X(80).
           EJECT

       FD  OUTPUT-FILE
           LABEL RECORDS ARE   STANDARD
           RECORDING MODE IS   F
           RECORD CONTAINS     80 CHARACTERS
           BLOCK CONTAINS      0 RECORDS
           DATA RECORDS IS     OUTPUT-REC.

       01  OUTPUT-REC                              PIC X(80).
           EJECT
       WORKING-STORAGE SECTION.

       77  TIME-TO-QUIT     VALUE 'NO '          PIC X(3).

       01  INPUT-REC-WORK.
           05  FILLER                             PIC X(09).
           05  BYTES-10-79.
               10  BYTES-10-18                    PIC X(09).
               10  FILLER                         PIC X(61).
           05  FILLER                             PIC X(01).

       01  SCAN4HIT-I-O-AREA.
           05  ARGUMENT                           PIC X(80).
           05  ARGUMENT-LENGTH                    PIC S9(4) COMP.
           05  TABLE-ELEMENT                      PIC X(80).
           05  TABLE-ELEMENT-LENGTH               PIC S9(4) COMP.
           05  HIT-SWITCH                         PIC X(03).


           EJECT
      *
      *    I S P L I N K   P A R A M E T E R S
      *
       01  VDEFINE               VALUE 'VDEFINE'   PIC X(7).
       01  CHAR                  VALUE 'CHAR'      PIC X(4).
       01  SHARED                VALUE 'SHARED'    PIC X(6).
       01  VGET                  VALUE 'VGET'      PIC X(4).
       01  LENGTH-FIELD                            PIC S9(8) COMP.
           EJECT
      *
      *    S P F   S Y M B O L I C   N A M E S
      *
       01  SPF-SYMBOLIC-NAMES.
           05  SPFYSRCH VALUE '(SPFYSRCH)'         PIC X(10).
           05  SPFYSLEN VALUE '(SPFYSLEN)'         PIC X(10).
           EJECT
      *
      *    C O B O L   V A R I A B L E   N A M E S
      *

       01  COBOL-VARIABLE-NAMES.
           05  SPIFFY-SEARCH-ARGUMENT              PIC X(70).
           05  SPIFFY-ARGUMENT-LENGTH              PIC 9(2).
           EJECT
       PROCEDURE DIVISION.

           PERFORM 1000-INITIALIZATION
              THRU 1999-EXIT-INITIALIZATION.

           PERFORM 2000-MAIN-PROCESS
              THRU 2999-EXIT-MAIN-PROCESS
                  UNTIL
                      TIME-TO-QUIT = 'YES'.

           CLOSE INPUT-FILE
                OUTPUT-FILE.

           STOP RUN.

           EJECT
       1000-INITIALIZATION.

           MOVE +70 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE
               SPFYSRCH
               SPIFFY-SEARCH-ARGUMENT
               CHAR
               LENGTH-FIELD.

           MOVE +02 TO LENGTH-FIELD.
           CALL 'ISPLINK'
             USING
               VDEFINE
               SPFYSLEN
               SPIFFY-ARGUMENT-LENGTH
               CHAR
               LENGTH-FIELD.

           CALL 'ISPLINK'
                USING VGET SPFYSRCH SHARED.

           CALL 'ISPLINK'
                USING VGET SPFYSLEN SHARED.

           OPEN INPUT INPUT-FILE
               OUTPUT OUTPUT-FILE.

       1500-INIT-READS.

           PERFORM 3000-READ
              THRU 3999-EXIT-READ.

           IF TIME-TO-QUIT = 'YES'
              CLOSE INPUT-FILE
                    OUTPUT-FILE
              STOP RUN.

           MOVE 'ATTRIB :' TO ARGUMENT OF SCAN4HIT-I-O-AREA.
           MOVE +8 TO ARGUMENT-LENGTH OF SCAN4HIT-I-O-AREA.
           MOVE INPUT-REC TO TABLE-ELEMENT OF SCAN4HIT-I-O-AREA.
           MOVE +80 TO TABLE-ELEMENT-LENGTH  OF SCAN4HIT-I-O-AREA.

           CALL 'SCAN4HIT' USING SCAN4HIT-I-O-AREA.

           IF HIT-SWITCH OF SCAN4HIT-I-O-AREA = 'YES'
               GO TO 1500-INIT-READS.

           MOVE INPUT-REC TO INPUT-REC-WORK.

           IF BYTES-10-18 OF INPUT-REC-WORK = 'PF STATS:'
              PERFORM 3000-READ
                 THRU 3999-EXIT-READ
           ELSE
               GO TO 1500-INIT-READS.

           MOVE SPIFFY-SEARCH-ARGUMENT
             TO ARGUMENT OF SCAN4HIT-I-O-AREA.
           MOVE SPIFFY-ARGUMENT-LENGTH
             TO ARGUMENT-LENGTH OF SCAN4HIT-I-O-AREA.
           MOVE +80
             TO TABLE-ELEMENT-LENGTH OF SCAN4HIT-I-O-AREA.

       1999-EXIT-INITIALIZATION.
           EXIT.
           EJECT
       2000-MAIN-PROCESS.

           MOVE INPUT-REC TO INPUT-REC-WORK.
           MOVE BYTES-10-79 OF INPUT-REC-WORK
             TO TABLE-ELEMENT OF SCAN4HIT-I-O-AREA.

           CALL 'SCAN4HIT' USING SCAN4HIT-I-O-AREA.

           IF HIT-SWITCH OF SCAN4HIT-I-O-AREA = 'YES'
                MOVE TABLE-ELEMENT OF SCAN4HIT-I-O-AREA TO OUTPUT-REC
                WRITE OUTPUT-REC.

           PERFORM 3000-READ
              THRU 3999-EXIT-READ.

       2999-EXIT-MAIN-PROCESS.
           EXIT.
           EJECT
       3000-READ.

           READ INPUT-FILE
              AT END
                MOVE 'YES' TO TIME-TO-QUIT.

       3999-EXIT-READ.
           EXIT.
