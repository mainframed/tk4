//DNO00155 JOB (2100,56-D),'DPARS 0682 T',CLASS=H,MSGCLASS=T
//* FROM WDPSC.DN00155.FILE(CATBYVOL)
//D EXEC ASMHCL,LIBRARY='WDPSC.TSAEXAM',OPTION1=TERM,
//  LNDSP=SHR
    TITLE 'CATALOG VERIFICATION FOR NON-VSAM'
*
* INPUT IS AN AMS CARD IMAGE TO UNCATALOG A DATA SET.
* COLUMN 73-74 CONTAINS THE LAST TWO BYTES OF UNIT TYPE
* COLUMN 75-80 CONTAINS THE VOLUME SERIAL NUMBER
*
* INPUT IS IN VOLUME SERIAL NUMBER SEQUENCE
* CARDS FOR TAPE ARE NOT  WRITTEN TO OUTPUT
* CARDS FOR DASD ARE CHECKED FOR A VALID VOLUME.
*   IF THE VOLUME EXISTS, IT IS CHECKED FOR THE DATA SET.
*   IF THE VOLUME DOES NOT CONTAIN THE DATA SET, THE RECORD IS
*   WRITTEN FOR IDCAMS PROCESSING.
*
CATBYVOL CSECT
         SAVE  (14,12),,CATBYVOL
         REGISTER
         BALR  R12,0
         USING *,R12
         B     GO
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
GO       CNOP  0,4
         ST    R13,SAVE+4
         LA    R11,SAVE
         ST    R11,8(,R13)
         LR    R13,R11
* PARM FIELD ANALYSIS
         L     R2,0(,R1)
         LH    R3,0(,R2)
         LTR   R3,R3
         BZ    OPENS
         OI    DEFLG,X'80'
OPENS    EQU   *
         OPEN  (PRINT,OUTPUT)
         TM    PRINT+48,X'10'
         BO    PR
         ABEND 12
PR       OPEN  (SYSIN,INPUT)
         TM    SYSIN+48,X'10'
         BZ    ERROR4              NO SYSIN DATA SET
         OPEN  (AMSDCB,OUTPUT)
PROCESS  GET   SYSIN,AMSCARD
         CLI   AMSDEVT,X'20'      IF NOT A DASD DEVICE TYPE, IT IS
         BNE   PROCESS            WILL BE PROCESSED ELSEWHERE
         CLC   AMSDSN(9),=C'SYSCTLG.V'
         BE    PROCESS             DO NOT DELETE CVOL ENTRIES
         MVC   DYNDSN,AMSDSN
         MVC   UNNDSN,AMSDSN
         MVC   DYNVOL,AMSVOL
         LA    R1,DYNAMIC
         SVC   99                  DYNAMIC ALLOCATION
         LTR   R15,R15             TEST RETURN
         BZ    UNALLOC             DATA SET EXISTS ON VOLUME, CONTINUE
         CLC   DYNERROR,=X'0210'    DATA SET ALLOCATED
         BE    PROCESS
         CLC   DYNERROR,=X'0218'    VOLUME NOT MOUNTED
         BE    KEEPAMS
         CLC   DYNERROR,=X'0220'    VOLUME NOT AVAILABLE
         BE    KEEPAMS
         CLC   DYNERROR,=X'0440'    DATA SET NOT FOUND
         BE    KEEPAMS
         B     ERROR8
UNALLOC  EQU  *
         LA    R1,UNDONE
         SVC   99
         LTR   R15,R15
         BZ    PROCESS
         B     ERRORC
*
         AGO   .SKIPIT
         CLC   WORKDEVT(8),AMSDEVT SAME VOLUME AS LAST TIME
         BE    VOLTHERE
         MVC   WORKDEVT(8),AMSDEVT
         L     R8,16               GET CVT ADDRESS
         L     R8,40(,R8)          POINTER TO UCB ADDRESSES
         SR    R7,R7
FINDVOL  ICM   R7,3,0(R8)          UCB ADDRESS
         BZ    UPUCB
         CLM   R7,3,=X'FFFF'       END OF ADDRESS LIST
         BE    NOTHERE
         CLC   28(6,R7),WORKVOL    IS THIS THE VOLUME
         BE    DSNCHECK
UPUCB    LA    R8,2(,R8)           NEXT UCB ADDRESS
         B     FINDVOL
DSNCHECK ST    R7,WORKUCB          KEEP ADDRESS OF UCB
         B     PROCESSB
NOTHERE  XC    WORKUCB,WORKUCB     MARK VOLUME SERIAL INVALID
VOLTHERE CLC   WORKUCB,=F'0000'
         BE    KEEPAMS             KEEP THE UNCATALOG RECORD
         SPACE 4
PROCESSB EQU   *
* VTOC VERIFY BY USE OF DF/DS FACILITY
         L     R7,WORKUCB
         XC    BFLHSP(6),BFLHSP         ZERO THE BUFFER LIST
         XC    BFLEARG,BFLEARG        ZERO THE BUFFER LIST
CVPLON   CVAFDIR ACCESS=READ,BRANCH=(YES,PGM),UCB=(R7),                X
               DSN=AMSDSN,BUFLIST=EMPTY,                               X
               MAPRCDS=NO,IXRCDS=NOKEEP,IOAREA=NOKEEP
         PRINT NOGEN
         ORG   CVPLON+4
         ICVAFPL DSECT=NO    MAP THE PARAMETER LIST
         ORG
         PRINT GEN
         C     R15,=F'4'           TEST RETURN CODE
         BH    ERROR7        TELL ABOUT ERROR AND ABEND FOR ANALYSIS
         CLI   CVSTAT,X'01'   DATA SET NOT FOUND
         BNE   PROCESS        SKIP ALL ERRORS FOR NOW
.SKIPIT  ANOP
KEEPAMS  EQU   *
         PUT   AMSDCB,AMSCARD
         B     PROCESS
OUTOF    EQU   *
         CLOSE (PRINT,,AMSDCB,,SYSIN)
         L     R13,SAVE+4
         RETURN  (14,12),RC=0   ,,,,,,,,,,,,,,,,,S
ERROR8   ABEND 567,DUMP
ERRORC   ABEND 566,DUMP
ERROR7   ABEND 568,DUMP
ERROR4   PUT   PRINT,NOSYSIN
         LA    R15,62
         B     OUTOF
SAVE     DS    18F
LINES    DC    PL5'77'
         PRINT NOGEN
PRINT    DCB   DDNAME=SYSPRINT,RECFM=FA,BLKSIZE=133,LRECL=133,DSORG=PS,X
               MACRF=(PM)
AMSDCB   DCB   DDNAME=AMSCARD,RECFM=FB,MACRF=PM,LRECL=80,DSORG=PS
SYSIN    DCB      DDNAME=SYSIN,RECFM=FB,LRECL=80,DSORG=PS,             X
               MACRF=(GM),EODAD=OUTOF
         PRINT GEN
OUT      DC    136C' '
NOSYSIN  DC    CL130'  NO SYSIN DATA SET  '
AMSCARD  DS    0CL80
         DS    CL15
AMSDSN   DS    CL44
         DS    CL13
AMSDEVT  DS    CL2
AMSVOL   DS    CL6
*
DEFLG    DC    X'00'
         DC    C'DYNAMIC'
         DS    0F
DYNAMIC  DC    XL1'80'              HIGH ORDER BIT ON
         DC    AL3(DYNBLOCK)        POINT TO REQUEST BLOCK
DYNBLOCK DC    X'14'                LENGTH OF BLOCK
         DC    X'01'                VERB CODE = DSNAME ALLOCATION
DYNFLAGS DC    X'6000'              FLAGS
*                DO NOT USE AN EXISTING ALLOCATION TO SATISFY THIS
*                DO NOT MOUNT VOLUMES
DYNERROR DC    X'0000'
DYNINFO  DC    X'0000'
DYNTEXTP DC    A(DYNPTRS)
         DC    A(0)
DYNFLAG2 DC    A(0)
*                 POINTERS TO ________ TEXT UNIT
DYNPTRS  DC    A(DYNTU02)     DSNAME
         DC    A(DYNTU04)     STATUS
         DC    A(DYNTU10)     VOLUME
         DC    A(DYNTU15)     UNIT
         DC    XL1'80',AL3(DYNTU1C) FREE ON CLOSE
*
DYNTU02  DC    X'0002',X'0001',X'002C'
DYNDSN   DC    CL44' '
DYNTU04  DC    X'0004',X'0001',X'0001',X'08'
DYNTU10  DC    X'0010',X'0001',X'0006'
DYNVOL   DC    CL6' '
DYNTU15  DC    X'0015',X'0001',X'0008',C'SYSALLDA'
DYNTU1C  DC    X'001C',X'0000'
**
         DC    C'UNDONE'
         DS    0F
UNDONE   DC    XL1'80'              HIGH ORDER BIT ON
         DC    AL3(UNNBLOCK)        POINT TO REQUEST BLOCK
UNNBLOCK DC    X'14'                LENGTH OF BLOCK
         DC    X'02'                VERB CODE = UNALLOCATION
UNNFLAGS DC    X'6000'              FLAGS
*                DO NOT USE AN EXISTING ALLOCATION TO SATISFY THIS
*                DO NOT MOUNT VOLUMES
UNNERROR DC    X'0000'
UNNINFO  DC    X'0000'
UNNTEXTP DC    A(UNNPTRS)
         DC    A(0)
UNNFLAG2 DC    A(0)
*                 POINTERS TO ________ TEXT UNIT
UNNPTRS  DC    A(UNNTU02)     DSNAME
         DC    A(UNNTU05)     DISPOSITION
         DC    XL1'80',AL3(UNNTU07) FREE ON CLOSE
UNNTU02  DC    X'0002',X'0001',X'002C'
UNNDSN   DC    CL44' '
UNNTU05  DC    X'0005',X'0001',X'0001',X'08'
UNNTU07  DC    X'0007',X'0000'
*
*
  AGO .A
EMPTY    ICVAFBFL  DSECT=NO    THE BUFFER LIST FOR CVAFDIR
         ORG   BFLHNOE         NUMBER OF ENTRIES
         DC    X'0104'
         ORG   BFLELTH
         DC    AL1(96)         LENGTH OF DSCB DATA PORTION
         ORG   BFLEBUF
         DC    A(STUFF)
         ORG
STUFF    DC    XL180'0'        THE BUFFER
.A    ANOP
WORKDEVT DC    XL2'00'          DEVICETYPE FOR ADDITIONAL CHECK
WORKVOL  DC    CL6' '           VOLUME SERIAL IN PROCESS
WORKUCB  DC    F'0'             ADDRESS OF UCB
         END
//LKED.SYSIN DD *
  SETCODE AC(1)
  NAME CATBYVOL(R)
