*
*  08/12/86 - V2M0 MOD TO ALLOW PARMS TO BE PASSED:
*
*             PARM='INIT'   MEANS DO AN STFSMODE ON,INITIAL=YES
*             PARM='ON'     MEANS DO AN STLINENO MODE=ON
*             PARM='OFF'    MEANS DO AN STLINENO MODE=OFF,LINE=0
*  (DEFAULT)  PARM='NORMAL' MEANS DO AN STLINENO MODE=OFF,LINE=0;
*                           THEN TPUT(FSCR) RESET; STLINENO MODE=ON
*
CLRSPFIO START
         SAVE  (14,12),,CLRIO-V2M0
         LR    R12,R15                                            WDPSC
         USING CLRSPFIO,R12
         L     R11,0(R1)          R11->PARM
         CLC   0(2,R11),=X'0000'  ANY PARM?
         BNE   GOTPARM            YUP.
         LA    R11,=C'  NORMAL'   DEFAULT PARM
GOTPARM  EQU   *
*
         CLC   2(2,R11),=C'INIT'  FSMODE INIT?
         BNE   CKOFF              NOOP.
         STFSMODE ON,INITIAL=YES
         B     XIT
CKOFF    EQU   *
*
         CLC   2(2,R11),=C'ON'    FSMODE ON?
         BE    ON                 YUP.
         SLR   R2,R2                                              WDPSC
         STLINENO MODE=OFF,LINE=(R2)                              WDPSC
         CH    R15,=H'8'               0=VTAM?                    WDPSC
         BE    TCAM                                               WDPSC
         CLC   2(2,R11),=C'OFF'   FSMODE OFF ONLY?
         BE    XIT                YUP.
         EJECT
         TPUT  =X'C3',1,FULLSCR,HOLD                              WDPSC
*
*        ICM   R0,15,=X'13000000'                                 WDPSC
*        SLR   R1,R1                                              WDPSC
*        ICM   R15,15,=C'    '                                    WDPSC
*        SVC   94                                                 WDPSC
*        SLR   R2,R2                                              WDPSC
*
ON       STLINENO MODE=ON,LINE=2                                  WDPSC
         CH    R15,=H'8'               0=VTAM?                    WDPSC
         BNE   VTAM                                               WDPSC
*
TCAM     L     R9,548             R9->ASCB
         L     R8,108(,R9)        R8->ASXB
         L     R7,20(,R8)         R7->LWA
         L     R6,152(,R7)        R6->TXC
         LTR   R6,R6              ANY TXC PRESENT?
         BZ    XIT                NO
         NI    0(R6),X'F7'        SAY NO LINE IO OCCURED
VTAM     EQU   *
*
XIT      RETURN (14,12),RC=0
         REGEQU
         END
