//ASM00150 JOB (2100,56-D),'LLUEB 0679 T',CLASS=H,MSGCLASS=T
//JOBCAT DD DSN=SYS1.SOFTCAT,DISP=SHR
//       DD DSN=SYS1.USERCAT1,DISP=SHR
//A  EXEC ASMFCL,LIBRARY='SYS1.MLPALIB',REGION=1000K,
//      LIB3='SYS1.AMODGEN',LIB2='WDPSC.MACLIB',
//      OPTION2='LOAD,MAP'
//ASM.SYSIN DD *
TIMECOND ENTERNR
         L     R2,0(0,R1)              POINT TO SUPPLIED PARM FIELD
         ICM   R3,3,0(R2)              LENGTH OF PARM SUPPLIED
NOPARM   BZ    DAYOFWK                 NO PARM = DAY OF WEEK (DEFAULT)
         CLI   2(R2),C' '              DAY OF WEEK?
         BE    DAYOFWK
         SPACE 1
*
**   INVOKE THE DATE ROUTINE FOR THE OTHER TYPES
         LA    R1,PARMTD               POINT TO PARM LIST
         CALL  TIMEDATE
*
         SPACE 1
         CLI   2(R2),C'D'              DAY OF MONTH?
         BE    DAYOFMTH
         CLI   2(R2),C'Y'              YEAR?
         BE    YEAR
         CLI   2(R2),C'H'              HOUR OF DAY? - 24 HOUR TIME
         BE    HOUR
         CLI   2(R2),C'M'              MONTH OF YEAR?
         BE    MNTHOFYR
         CLI   2(R2),C'J'              JULIAN DAY OF YEAR?
         BE    JULIAN
         B     PARMERR                 BAD PARM ON EXEC CARD
         EJECT
*   THIS SECTION DETERMINES THE DAY OF THE WEEK  -  1 TO 7
DAYOFWK  DS    0H
*   INVOKE THE DATE ROUTINE FOR DAY OF WEEK
GETDATE  LA    R1,PARML
         CALL  DATE
         L     R8,DATEAR
         LA    R9,WKLST
CKDLOOP  CLC   0(2,R9),=C'XX'           END?
         BE    BADMESS
         CLC   0(2,R9),DATEAR
         BE    GDAYCNT
         LA    R9,2(R9)
         B     CKDLOOP
GDAYCNT  S     R9,=A(WKLST)
         SRL   R9,1
         LR    R15,R9
         B     EXIT
         EJECT
*   THIS SECTION DETERMINES THE MONTH OF THE YEAR - 1 THRU 12
MNTHOFYR DS    0H
         MVC   CHARS+2(2),STDATEAR+2     PICK UP CHARACTER MONTH
         B     BINARY
         SPACE 3
*   THIS SECTION DETERMINES THE DAY OF THE MONTH - 1 THRU 31
DAYOFMTH DS    0H
         MVC   CHARS+2(2),STDATEAR+4     PICK UP DAY OF MONTH
         B     BINARY
         SPACE 3
*   THIS SECTION DETERMINES THE YEAR  -  LAST TWO DIGITS
YEAR     DS    0H
         MVC   CHARS+2(2),STDATEAR       PICK UP YEAR
         B     BINARY
         SPACE 3
*   THIS SECTION DETERMINES THE JULIAN DATE
JULIAN   DS    0H
         MVC   CHARS+1(3),JDATEAR+2      PICK UP JULIAN DATE
         B     BINARY
         SPACE 3
*   THIS SECTION DETERMINES THE HOUR
HOUR     DS    0H
         MVC   CHARS+2(2),TIMEAR        PICK UP HOUR
         B     BINARY
         EJECT
*
*     CONVERT THE CHARACTER FIELDS TO BINARY FOR THE CONDITION CODE
*
BINARY   DS    0H
         PACK  DOUBLE+5(3),CHARS(4)
         CVB   R15,DOUBLE
         SPACE 2
EXIT     LEAVENR
         EJECT
         LTORG
         SPACE 3
PARMTD   DC    A(TIMEAR),A(JDATEAR),X'80',AL3(STDATEAR)
PARML    DC    X'80',AL3(DATEAR)
DATEAR   DS    0CL28         USED FOR DATE CALL
TIMEAR   DS    CL8           USED FOR TIMEDATE CALL    HHMMSSHH
JDATEAR  DS    CL5                                     YYDDD
STDATEAR DS    CL6                                     YYMMDD
         DS    CL9           EXTRA FOR DATE CALL
WKLST    DC    C'SUMOTUWETHFRSAXX'
CHARS    DC    C'0000'
DOUBLE   DS    8X'00'
BADMESS  DC    X'00',C'THERE ARE ONLY 7 DAYS IN A WEEK, SORRY.'
PARMERR  DC    X'00',C'EXEC CARD PARM NOT H, D, Y, J, M, BLANK OR NONE'
         END
//LKED.SYSLMOD DD DSN=WDPSC.SYSLIBRY,DISP=SHR
//LKED.SYSLIB DD DSN=WDPSC.SYSLIBRY,DISP=SHR
//LKED.SYSIN DD *
   INCLUDE SYSLIB(DATE)
   INCLUDE SYSLIB(TIMEDATE)
   NAME TIMECOND(R)
