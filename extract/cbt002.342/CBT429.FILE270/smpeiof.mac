SMPEIOF TITLE 'SMPE INTERCEPT TO ROUTE JOB STATUS CHECKS TO IOF'
*
*  UPDATED 2/10/88 TO CALL REGULAR ST CMD IF NO OPERANDS(NEW USER PROB)
*
*  WRITTEN 4/21/87 BY KERMIT KISER - INSTALLATION CODE WSD (WDPSC)
*
*  THIS PROGRAM CAN BE NAMED STATUS AND PLACED IN THE SMPE DIALOG
*   LOAD LIBRARY TO REROUTE THE STATUS COMMAND CALLS TO A CLIST
*
         EJECT
SMPEIOF  ENTERR LEVEL=V1M1,CLEAR=YES
         EJECT
*
         LR    R11,R1             R11->CPPL
         USING CPPL,R11
         L     R9,CPPLECT
         USING ECT,R9
         TM    ECTSWS,ECTNOPD     OPERANDS?
         BNO   USECLIST
         LOAD  EP=ST
         LR    R15,R0
         LR    R1,R11
         BALR  R14,R15
         DELETE EP=ST
         B     RETURN
USECLIST EQU   *
*
*     BUILD NEW SELECT CMD STRING AND PASS TO ISPF FOR STATUS CLIST
*
         L     R1,0(R11)          R1-> CBUF
         MVI   VA,C' '            A BLANK
         MVC   VA+1(99),VA        SMEAR
         MVC   CMDSEL,=C'CMD(%'   GET SET
         MVI   RP,C')'            A PAREN
         LH    R2,0(R1)           R2=BUF LENGTH
         SH    R2,=H'5'           R2=CMD LEN
MOVCMD   MVC   VA(0),4(R1)        EXED CMD MOVE
         EX    R2,MOVCMD          MOVE TO NEW BUF
         CALL  ISPLINK,(=CL8'SELECT',=F'106',CMDSEL),                XXX
               VL,MF=(E,CL)
         EJECT
*
*     SEE IF A STATUS MESSAGE WAS PASSED BACK FOR PUTLINE OUTPUT
*
         CALL  ISPLINK,(=CL8'VCOPY',=C'KSMPSTAT',VL,VP),             XXX
               VL,MF=(E,CL)
         LTR   R15,R15
         BNZ   OOPS               BAD RC
         EJECT
*
*   A STATUS MESSAGE WAS PASSED - WRITE VIA PUTLINE OUTPUT
*
         L     R1,VP              R1->OUTPUT MSG
         L     R2,VL              R2=OUT MSG LEN
         BCTR  R2,R0              R2-1 FOR EX
MOVMSG   MVC   PLD(0),0(R1)
         EX    R2,MOVMSG
         LA    R2,5(R2)           R2+5 FOR PUTLINE
         STH   R2,PLBUF
         USING CPPL,R11
         L     R2,CPPLUPT
         L     R3,CPPLECT
         PUTLINE PARM=PLB,UPT=(R2),ECT=(R3),                          XX
               ECB=MYECB,OUTPUT=(PLBUF,DATA),MF=(E,IOPL)
         EJECT
*
*        RETURN TO WHOEVER CALLED US:
*
RETURN   DS    0H
         SLR   R15,R15
         B     RET
OOPS     DS    0H
         LA    R15,8
RET      LEAVER
         TITLE 'WORKAREAS'
         LTORG
*
*              DYNAMIC WORKAREAS:
*
WORKAREA DSECT
SAVEAREA DS    18F
VL       DS    F
VP       DS    F
CMDSEL   DS    CL5'CMD(%'
VA       DS    CL100
RP       DS    C')'
CL       CALL  ,(0,0,0,0,0,0,0),MF=L
IOPL     DS    4F
PLB      PUTLINE MF=L
MYECB    DS    F
PLBUF    DS    H,H
PLD      DS    CL200
WORKLEN  EQU   *-WORKAREA
         IKJCPPL
         IKJECT
         END
