SHOWNEW  TITLE ' - CP TO REPLACE JTIP SHOW COMMAND.'
*
* UPDATED FOR JES 2.1.5 06/05/87 (VERSION V2M0)
*
* WRITTEN BY KERMIT KISER - APRIL, 1985
*
*   COMMAND FORMATS:     SSCSFLGS:   SSCSULEN: SSCSJOBN: SSCSJOBI:
*
*  1. SHOW {UID(ACF2ID)}     X'80'       X'80'       ID     BLANK
*
*  2. SHOW JOB(XXXXXXXX)     X'00'       X'8N'  JOBNAME     BLANK
*                                    N=LENGTH OF JOBNAME SUPPLIED
*
*  3. SHOW JOB(NNNN)         X'00'       X'80'    BLANK   F'NNNN'
*
*  4. SHOW SYS{(NN)}         X'80'       X'87' ID+TIOT/N    BLANK
*                          SSCSJOBN= UID WITH SYS # OR TIOT SYS
*
*  5. SHOW LIKE(XXXXXXXX)    X'00'       X'CN'  JOBNAME     BLANK
*                                    N=LENGTH OF JOBNAME SUPPLIED
*
*
*
SHOW     ENTERR LEVEL=V2M0,CLEAR=YES
         LR    R11,R1                   R11->CPPL
         USING CPPL,R11
         EJECT
*
*              SHOWT CONTROL ROUTINE:
*
SA       BAL   R14,PB                   PARSE THE INPUT BUFFER
*
S1       BAL   R14,INIT                 SET UP SSOB
*
S2       BAL   R14,CALLSS               PASS REQUEST TO SUBSYSTEM
*
S3       BAL   R14,SHOWEM               EXPLAIN TO THE GUY
         EJECT
         LEAVER
         EJECT
PB       ST    R14,L1LS
         MVC   JOBNAME(8),=CL8' '
         PARSKMD WORK=FASTAREA,PARM=WORKAREA,KEYWORK=KEYWAREA,       XXX
               UNKFLG=UFLG
         SPACE 2
         L     R14,L1LS
         BR    R14
         EJECT
INIT     ST    R14,L1LS
         L     R1,8(,R11)         R1->PSCB
         MVC   MPSCB,0(R1)        MAKE A COPY
         BAL   R14,MODPSCB        MODIFY ?
         SPACE
* BUILD SUBSYS REQUEST PARMLIST
         LA    R10,SSOBAREA       R10->SSOB
         USING SSOB,R10
         ST    R10,SSRPL          SET UP PARM LIST
         OI    SSRPL,X'80'        "
         SPACE
* BUILD SSOB HERE
         MVC   SSOBID,=C'SSOB'    ID
         MVC   SSOBLEN,=H'20'     LENGTH
         MVC   SSOBFUNC,=AL2(SSOBSTAT)
         LA    R1,SSCSBGN         R1->SSOB EXTENSION
         ST    R1,SSOBINDV        LINK EM
         SPACE
* SSOB CANCEL/STATUS EXTENSION - INIT
         MVC   SSCSLEN,=X'0028'
         MVC   SSCSJOBN,=CL8' '
         MVC   SSCSJOBI,=CL8' '
         TM    FLAG,X'50'         USER ENTERED JOB()?
         BZ    SHOWALL            NOPE.
         TM    FLAG,X'10'         USER ENTERED LIKE()?
         BZ    CKJOB              NOPE.
         OI    SSCSULEN,X'C0'        SAY LIKE                    WDPSC
         B     MVCN                  GO MOVE NAME                WDPSC
CKJOB    CLI   JOBNAME,C'0'       ID?
         BNL   JOBID              YUP.
MVCN     MVC   SSCSJOBN,JOBNAME   GET JOBNAME
         OC    SSCSULEN(1),JOBAREA+1 SET LENGTH                  WDPSC
         B     SHOWFLAG
JOBID    LH    R1,JOBAREA         R1=LENGTH OF ID
         BCTR  R1,R0              R1-1
PAKID    PACK  PDW(8),JOBNAME(0)  PACKS ID
         EX    R1,PAKID           DO IT FOR REAL
         UNPK  SSCSJOBI,PDW       MAKE CHAR
         OI    SSCSJOBI+7,X'F0'   IS THIS NEEDED?
         MVC   SSCSJOBI(3),=C'JOB' PREFIX
         B     SHOWFLAG
SHOWALL  MVC   SSCSJOBN(7),MPSCB  GET USERID
         TM    FLAG,X'08'         USER ENTERED SYSTEM(NN)?
         BZ    SETID              NOPE.
         MVC   SSCSJOBN+2(2),SYSNAME UPDATE SYSTEM #             WDPSC
         MVI   SSCSULEN,X'87'     SET LEN
SETID    OI    SSCSFLGS,SSCSUSID  SAY USERID PRESENT
SHOWFLAG DS    0H
         OI    SSCSULEN,X'80'     SAY SHOW CALLING
         MVC   SSCSDIMP,=H'15500' PASS RESPONSE AREA LENGTH
         SPACE 2
         L     R14,L1LS
         BR    R14
         EJECT
MODPSCB  ST    R14,L2LS
         LH    R3,OPAREA          R3=LENGTH OF OPER PASSWORD
         LTR   R3,R3              ANY GIVEN?
         BZ    CKOPER             NOPE.
         CLC   OPAREA+2(5),=CL5'BINGO' IS HE OK?
         BNE   CKOPER                NO SUCH
         OI    MPSCB+16,X'80'     HE IS OPER!
CKOPER   TM    MPSCB+16,X'80'     IS HE OPER?
         BNO   USESYS             NO->RESTRICT HIM
         LH    R3,SAVEUSER        R3=LENGTH OF USERID SPECIFIED
         LTR   R3,R3              ANY GIVEN?
         BZ    USESYS             NOPE.
         MVC   MPSCB(7),=CL7' '    CLEAR USERID FIELD
SETUID   MVC   MPSCB(0),SAVEUSER+2 COPY USERID FIELD
         BCTR  R3,R0              R3-1 FOR EX
         EX    R3,SETUID          SHOVE IT
         B     MPX
USESYS   L     R1,540                R1->TCB
         L     R1,12(,R1)            R1->TIOT
         MVC   MPSCB+2(2),3(R1)      UPDATE SYSID
         SPACE 2
MPX      L     R14,L2LS
         BR    R14
         EJECT
CALLSS   ST    R14,L1LS
         SPACE
         L     R0,=F'-1'          R0=-1
         SVC   236                SET JSCB AUTH BIT ON
         SPACE
         MVC   MODELIST,SUP       INIT MODE PLIST
         MODESET MF=(E,MODELIST)  SET SUPER STATE
         SPACE
         LA    R1,SSRPL           R1->SUBSYS REQ PARM LIST
         IEFSSREQ
         ST    R15,SSRC           SAVE RETURN CODE
         SPACE
         MVC   MODELIST,PROB      INIT MODE PLIST
         MODESET MF=(E,MODELIST)  SET PROBLEM STATE
         SPACE
         SLR   R0,R0              R0=0
         SVC   236                SET JSCB AUTH BIT OFF
         EJECT
         L     R15,SSRC           RESTORE RETURN CODE
         LTR   R15,R15            GOOD REQUEST?
         BZ    GOODREQ            YUP.
         CH    R15,=H'20'         RC VALID?
         BL    RCOK               Y
         LA    R15,20             BAD ENOUGH
RCOK     L     R1,MSGTAB1(R15)    R1-> ERROR MSG
         BAL   R14,PUTLINE        TELL M
         B     SSX
GOODREQ  ICM   R15,15,SSOBRETN    R15=SS RETCODE
         BZ    SSX                OK?
         CH    R15,=H'28'         RC VALID?
         BNH   RC2OK              Y
         LA    R15,32             RESPONSE UNKNOWN
RC2OK    L     R1,MSGTAB2(R15)    R1->ERRRR MSG
         BAL   R14,PUTLINE        TELL ER
         SPACE 2
SSX      L     R14,L1LS
         BR    R14
         SPACE
MSGTAB1  DS    F
         DC    A(RC1M4)
         DC    A(RC1M8)
         DC    A(RC1M12)
         DC    A(RC1M16)
         DC    A(RC1M20)
RC1M4    DC    AL2(L'RC1IS4),H'0'
RC1IS4   DC    C'SUBSYSTEM DOES NOT SUPPORT THIS FUNCTION'
RC1M8    DC    AL2(L'RC1IS8),H'0'
RC1IS8   DC    C'SUBSYSTEM EXISTS, BUT IS NOT UP'
RC1M12   DC    AL2(L'RC1IS12),H'0'
RC1IS12  DC    C'SUBSYSTEM DOES NOT EXIST'
RC1M16   DC    AL2(L'RC1IS16),H'0'
RC1IS16  DC    C'FUNCTION NOT COMPLETED-DISASTROUS ERROR'
RC1M20   DC    AL2(L'RC1IS20),H'0'
RC1IS20  DC    C'LOGICAL ERROR (BAD SSOB FORMAT,INCORRECT LENGTH,...)'
         SPACE
MSGTAB2  DS    F
         DC    A(RC2M4)
         DC    A(RC2M8)
         DC    A(RC2M12)
         DC    A(RC2M16)
         DC    A(RC2M20)
         DC    A(RC2M24)
         DC    A(RC2M28)
         DC    A(RC2M32)
RC2M4    DC    AL2(L'RC2IS4),H'0'
RC2IS4   DC    C'JOB NAME NOT FOUND'
RC2M8    DC    AL2(L'RC2IS8),H'0'
RC2IS8   DC    C'INVALID JOBNAME/JOB ID COMBINATION'
RC2M12   DC    AL2(L'RC2IS12),H'0'
RC2IS12  DC    C'JOB NOT CANCELED- DUPLICATE JOBNAMES AND NO ID'
RC2M16   DC    AL2(L'RC2IS16),H'0'
RC2IS16  DC    C'STATUS ARRAY TOO SMALL'
RC2M20   DC    AL2(L'RC2IS20),H'0'
RC2IS20  DC    C'JOB NOT CANCELLED-JOB ON OUTPUT QUEUE'
RC2M24   DC    AL2(L'RC2IS20),H'0'
RC2IS24  DC    C'JOBID WITH INVALID SYNTAX FOR SUBSYSTEM'
RC2M28   DC    AL2(L'RC2IS20),H'0'
RC2IS28  DC    C'INVALID CANCEL REQUEST - CANNOT CANCEL ACTIVE TSU/STC'
RC2M32   DC    AL2(L'RC2IS20),H'0'
RC2IS32  DC    C'UNKNOWN ERROR IN SUBSYSTEM PROCESSING'
         EJECT
SHOWEM   ST    R14,L1LS
         SPACE
         LA    R9,SSCSARAY        R9->JQE AREA
         L     R1,0(R9)           R1=PREFIX LENGTH
         LA    R2,SA2             R2->FORMAT WORKAREA
         ST    R2,FJMPL           SAVE
         LA    R2,16(R9)          R9->QSE AREA
         ST    R2,FJMPL+8         SAVE ADDR
         AR    R9,R1              R9->1ST JQE
         USING JQE,R9
JQELOOP  EQU   *
         CLC   0(4,R9),=XL4'00'   END?
         BE    JQEXIT             YEP.
         ST    R9,FJMPL+4         PASS JQE
         LA    R1,FJMPL           R1->FJM PARMLIST
         CALL  FJM                FORMAT OUTPUT LINE
         LA    R1,STATLEN         R1->OUTPUT LINE
         BAL   R14,PUTLINE        TELL IT ALL...
         LA    R9,JQEBLEN(R9)     R9->NEXT JQE
         B     JQELOOP            CHK IT.
         SPACE
JQEXIT   L     R14,L1LS
         BR    R14
         EJECT
PUTLINE  ST    R14,L3LS
         SPACE
         LR    R2,R1              R2->OUTPUT MESSAGE
         L     R3,CPPLUPT         R3->UPT
         L     R4,CPPLECT         R4->ECT
         PUTLINE PARM=PUTLST,UPT=(R3),ECT=(R4),ECB=LOCLECB,       XXXXXX
               OUTPUT=((2),DATA),MF=(E,IOPL)
         SPACE
         CH    R15,=H'4'          NORMAL RETURN?
         BNH   PLX                YES
         DS    H'0'
         DC    C'BAD PUTLINE ERROR OCCURED'
PLX      L     R14,L3LS
         BR    R14
         EJECT
STATWORK EQU   *
         SPACE
OPLIST   OPERLIST OPER1,OPER2,OPER3,OPER4,OPER5
OPER1    OPER  UID,X'8000',MINLEN=1,BLANK=YES,SUBFLD=SAVEUSER
OPER2    OPER  JOB,X'4000',MINLEN=1,BLANK=YES,SUBFLD=JOBAREA
OPER3    OPER  OPPW,X'2000',MINLEN=3,BLANK=YES,SUBFLD=OPAREA
OPER4    OPER  LIKE,X'1000',MINLEN=1,BLANK=YES,SUBFLD=JOBAREA
OPER5    OPER  SYSTEM,X'0800',MINLEN=1,BLANK=YES,SUBFLD=SYSAREA
         SPACE
*
SUP      MODESET MODE=SUP,MF=L
*
PROB     MODESET MODE=PROB,MF=L
*
         SPACE
         LTORG
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
*
MODELIST MODESET MODE=SUP,MF=L
*
IOPL     DS    4F
LOCLECB  DS    F
PUTLST   PUTLINE MF=L
*
FJMPL    DS    4F                 FJM PARM LIST: FJMWA,JQE,QSE TAB
*
* FJM WORK AREA:
SA2      DS    18F
JQEPTR   DS    F
$QSE1    DS    F
NODEPTR  DS    F
PDW      DS    D
DDW      DS    D
STATLEN  DS    H
         DS    H
STATLINE DS    0CL512
         SPACE
FASTAREA DS    XL512
         KPPL
KEYWAREA DS    20F
*
SAVEUSER DS    H                  LENGTH OF UID
         DS    CL8
JOBAREA  DS    H                  LENGTH OF JOB NAME
JOBNAME  DS    CL8
SYSAREA  DS    H                  LENGTH OF JOB NAME
SYSNAME  DS    CL8
OPAREA   DS    H                  LENGTH OF PASSWD
OPNAME   DS    CL8
MPSCB    DS    XL72
MCPPL    DS    4F
*
FLAG     DS    F
UFLG     DS    X
         SPACE 2
SSRC     DS    F
SSRPL    DS    F
SSOBAREA DS    XL16384            SUBSYS AREAS
         SPACE 2
WORKLEN  EQU   *-WORKAREA
         EJECT
         IEFJSSOB CS,CONTIG=YES
         IEFJESCT
FF       EQU   X'FF'
         $JQE
         CVT   DSECT=YES
         EJECT
         IKJCPPL
         EJECT
         IKJECT
         END
