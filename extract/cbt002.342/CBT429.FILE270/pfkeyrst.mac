      *******************************
       ID DIVISION.
      *******************************

       PROGRAM-ID. PFKEYRST.
       AUTHOR.     GARY DUFFIELD.
       REMARKS.
                   THIS PROGRAM RESTORES THE USERS PFKEY ASSIGNMENTS
           FROM TEMPORARY 'SAVE' VARIABLES IN HIS SHARED VARIABLE POOL.
           EJECT
      *******************************
       ENVIRONMENT DIVISION.
      *******************************

       CONFIGURATION SECTION.

       INPUT-OUTPUT SECTION.

           EJECT
      *******************************
       DATA DIVISION.
      *******************************

       FILE SECTION.

           EJECT

       WORKING-STORAGE SECTION.

       01  MISC-WORK-FIELDS.

           05  SAVE-CODE                PIC 9(4)       VALUE 0.
           05  SAVE-SWITCH              PIC XX         VALUE SPACE.

       01  DIALOG-MANAGER-CONTROLS.

           05  CHAR                     PIC X(4)       VALUE 'CHAR'.
           05  LEN                      PIC S9(8) COMP VALUE +0.
           05  PROFILE                  PIC X(7)       VALUE 'PROFILE'.
           05  VDEFINE                  PIC X(7)       VALUE 'VDEFINE'.
           05  VGET                     PIC X(4)       VALUE 'VGET'.
           05  VPUT                     PIC X(4)       VALUE 'VPUT'.
           05  ZPFSW                    PIC X(7)       VALUE '(ZPFSW)'.

       01  TABLES.

           05  SAVE-KEY-NAMES.
              10  FILLER               PIC X(43)   VALUE
                 '(ZKEY01,ZKEY02,ZKEY03,ZKEY04,ZKEY05,ZKEY06,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY07,ZKEY08,ZKEY09,ZKEY10,ZKEY11,ZKEY12,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY13,ZKEY14,ZKEY15,ZKEY16,ZKEY17,ZKEY18,'.
              10  FILLER               PIC X(42)   VALUE
                 'ZKEY19,ZKEY20,ZKEY21,ZKEY22,ZKEY23,ZKEY24)'.

           05  SPF-KEY-NAMES.
               10  FILLER               PIC X(37)   VALUE
                  '(ZPF01,ZPF02,ZPF03,ZPF04,ZPF05,ZPF06,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF07,ZPF08,ZPF09,ZPF10,ZPF11,ZPF12,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF13,ZPF14,ZPF15,ZPF16,ZPF17,ZPF18,'.
               10  FILLER               PIC X(36)   VALUE
                  'ZPF19,ZPF20,ZPF21,ZPF22,ZPF23,ZPF24)'.

           05  PFKEY-VALUES.
               10  PFKEY OCCURS 24 TIMES PIC X(43).
               EJECT
      *************************************
       PROCEDURE DIVISION.
      *************************************
       PROCESS-MODULE.
      *************************************

           MOVE +2 TO LEN.
           CALL 'ISPLINK' USING VDEFINE
                                ZPFSW          SAVE-SWITCH  CHAR LEN.
           MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VGET ZPFSW.
           IF SAVE-SWITCH = 'ON'
              MOVE SPACE TO SAVE-SWITCH
              CALL 'ISPLINK' USING VPUT ZPFSW
              PERFORM 1000-RESTORE-MODULE
           ELSE
              MOVE 4 TO RETURN-CODE.
           STOP RUN.

      *************************************
       1000-RESTORE-MODULE.
      *************************************

           MOVE +43 TO LEN.
           CALL 'ISPLINK' USING VDEFINE
                                SAVE-KEY-NAMES PFKEY-VALUES CHAR LEN.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VDEFINE
                                SPF-KEY-NAMES  PFKEY-VALUES CHAR LEN.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VGET SAVE-KEY-NAMES.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           CALL 'ISPLINK' USING VPUT SPF-KEY-NAMES PROFILE.
           IF RETURN-CODE > SAVE-CODE
              MOVE RETURN-CODE TO SAVE-CODE.
           MOVE SAVE-CODE TO RETURN-CODE.
