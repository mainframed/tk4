         TITLE 'PROGRAM TO SET COND CODES FOR PROCEDURES,ETC'
*
* IF THE PARM FIELD CONTAINS JUST A NUMBER, IT IS RETURNED AS COND CODE
*
* IF JUST CHARS, THE FIRST IS CONVERTED TO DECIMAL AND RETURNED.
*
* IF A NUMBER FOLLOWED BY CHARS IS PRESENT, THE NUMBER IS USED AS AN
*    INDEX INTO THE CHAR STRING AND THAT CHAR IS RETURNED.
*    (A BLANK OR COMMA IMMEDIATELY FOLLOWING THE NUMBER IS IGNORED)
*
* IF PARM IS NULL OR CHAR REQUESTED IS NOT PRESENT, CODE IS ZERO
*
* LEADING BLANKS ARE IGNORED
*
* EXAMPLES:
*
*  EXEC PGM=SETRC,PARM='99'    SETS COND CODE = 99
*
*  EXEC PGM=SETRC,PARM='A'     SETS COND CODE = 193
*
*  EXEC PGM=SETRC,PARM='AB'    SETS COND CODE = 193
*
*  EXEC PGM=SETRC,PARM='2,AB'  SETS COND CODE = 194
*
*  EXEC PGM=SETRC,PARM='3 ABC' SETS COND CODE = 195
*
*  EXEC PGM=SETRC,PARM='3ABC'  SETS COND CODE = 195
*
*  EXEC PGM=SETRC,PARM=''      SETS COND CODE = 0
*
*  EXEC PGM=SETRC,PARM='3,AB'  SETS COND CODE = 0
*
*  EXEC PGM=SETRC,PARM=' $'    SETS COND CODE = 91
         EJECT
SETRC    ENTERR LEVEL=V2M1
         L     R11,0(R1)          R11->PARM FIELD
         LH    R10,0(R11)         R10= PARM LENGTH
         LA    R11,1(R11)         R11=R11+1
         SLR   R9,R9              R9=0
         SLR   R8,R8              R8=0
LOOP1    LA    R11,1(R11)         R11=R11+1
         CLI   0(R11),C' '        SPACE?
         BNE   DECODE             NO->ASSUME DATA
         BCT   R10,LOOP1          YES
         B     NOVAL
         EJECT
DECODE   CLI   0(R11),C' '        SPACE?
         BE    DELIM              YES
         CLI   0(R11),C','        COMMA?
         BE    DELIM              YES
         TM    0(R11),X'F0'       NUMERIC?
         BNO   DATA               NO->  TERMINATE SCAN
         SLR   R2,R2              R2=0
         TRT   0(1,R11),TABLE     GET VALUE
         MH    R9,=H'10'          R9=R9*10
         AR    R9,R2              R9=R9+R2
SKIPPED  LA    R11,1(R11)         R11=R11+1
         BCT   R10,DECODE         R10=R10-1 >0 -> LOOP FOR NEXT DIGIT
NRC      LR    R15,R9             R15 = RC FOR MODULE RETURN
         B QUIT                                                  WDPSC
*
DELIM    LA    R11,1(,R11)        R11+1 - SKIP DELIM
         SH    R10,=H'1'          LENGTH OF REMAINING PARM FLD
DATA     LTR   R10,R10            ANY CHARS LEFT?
         BNP   NRC                NOPE.                          WDPSC
         CR    R9,R10             CHAR REQUESTED PRESENT?        WDPSC
         BH    NOVAL                 NOT THERE                   WDPSC
         LTR   R9,R9                       R9 STILL ZERO? (INDEX)
         BZ    ICIT                        YUP-NO INDEX GIVEN
         BCTR  R9,R0              CONVERT INDEX TO OFFSET
ICIT     SLR   R15,R15            ZERO RC                        WDPSC
         IC    R15,0(R9,R11)         R15=CHAR REQUESTED          WDPSC
         B     LEAVER                ALL DONE                    WDPSC
*
NOVAL    EQU   *
EXIT     SLR   R15,R15            R15=0
QUIT     EQU   *
LEAVER   LEAVER
*
TABLE    DC    256X'00'
         ORG   TABLE+X'F0'
         DC    X'00010203040506070809'
         ORG
         LTORG
*
*   WORKING STORAGE AREA - DYNAMIC
*
WORKAREA DSECT
SAVEAREA DS    18F
WORKLEN  EQU   *-WORKAREA
         END
