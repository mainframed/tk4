         PRINT NOGEN
         TITLE 'LIBCALL - CALL A LPA/LNKLST OR TASKLIB MODULE'
**********************************************************************
***                                                                ***
***                      LIBCALL COMMAND                           ***
***                                                                ***
***  GENERAL FUNCTION:                                             ***
***                                                                ***
***     TRANSFER CONTROL TO MODULES IN JPA/LPA/LINKLIST WITH       ***
***     A 'CALL' TYPE PARAMETER LIST.  THIS ALLOWS THE USER TO     ***
***     INVOKE PROGRAMS WITHOUT HAVING TO KNOW THE NAME OF THE     ***
***     LIBRARIES THEY ARE IN.                                CAH84198A
***                                                           CAH84198A
***                                                                ***
***     THE USE OF THE 'STEPLIB' OPERAND WILL CAUSE THE USE   CAH84198A
***     OF A CONCATENATION OF LIBRARY NAMES AS A TASKLIB,     CAH84198A
***     AHEAD OF THE USE OF JPA/LPA/LINKLIST MODULES.  THE    CAH84198A
***     LIBRARIES WILL BE ALLOCATED AND CONCATENATED BEFORE   CAH84198A
***     TRANSFER OF CONTROL TO THE MODULE.  CONTROL WILL BE   CAH84198A
***     TRANSFERRED WITH THE ATTACH SVC (SVC 42).  AFTER      CAH84198A
***     MODULE EXECUTION, CONTROL WILL RETURN TO THIS COMMAND CAH84198A
***     PROCESSOR, WHERE THE PROGRAM WILL BE DETACHED, AND    CAH84198A
***     THE LIBRARIES WILL BE DECONCATENATED, AND MARKED NOT  CAH84198A
***     IN USE.                                               CAH84198A
***                                                           CAH84198A
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  FORMAT:                                                       ***
***                                                                ***
***  LIBCALL  NAME  'PARMS'  CAPS  SYSLIBS                    CAH84160A
***  LCALL                   ASIS  STEPLIB('DSN1' 'DSN2' ...) CAH84160A
***                                TASKLIB('DSN1' 'DSN2' ...) CAH84198A
***                                    LIB('DSN1' 'DSN2' ...) CAH84160A
***                                 CONCAT('DSN1' 'DSN2' ...) CAH84160A
***                                                                ***
***    ALIAS:  LCALL                                               ***
***                                                                ***
***    REQUIRED:                                                   ***
***      1.  NAME - LIBCALL WILL PROMPT FOR NAME IF NOT SUPPLIED.  ***
***                                                                ***
***    DEFAULTS:                                                   ***
***      1.  SYSLIBS - SYSTEM LIBRARIES AND PROGRAM AREAS.         ***
***      2.  CAPS - IF A 'PARM' IS USED, SHIFT TO UPPER CASE. CAH84160A
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  RETURN CODES -                                                ***
***       RETURN CODE IS THE CODE FROM THE EXECUTED MODULE.        ***
***                                                                ***
***  REGISTER USAGE                                                ***
***       R0-R2  LINKAGE CONVENTIONS, MACROS                       ***
***          R3  (NOT USED)                                        ***
***          R4  WORK, TEMPORARY BASE OF PDE                       ***
***       R5-R6  WORK                                              ***
***       R7-R8  (NOT USED)                                        ***
***          R9  BASE OF PARM DESCRIPTOR ELEMENT LIST (PDL)        ***
***         R10  BAL REGISTER                                      ***
***         R11  (NOT USED)                                   CAH84198A
***         R12  PROGRAM BASE REGISTER                             ***
***         R13  LINKAGE CONVENTIONS, BASE OF AREA IN SP-000       ***
***     R14-R15  LINKAGE CONVENTIONS                               ***
***                                                                ***
***  NON-IBM MACROS USED:                                          ***
***     GTEDADAT  - CREATE CONTROL BLOCKS FOR DYNALLOC/DAIRFAIL    ***
***     GTEDASET  - LINK CONTROL BLOCKS TOGETHER                   ***
***     GTEDAALC  - EXECUTE DYNALLOC/DAIRFAIL                      ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  REUS,RENT,REFR  ALIAS(LCALL)         ***
***                                                                ***
***  CULPRIT:                                                      ***
***     CHUCK HOFFMAN                                              ***
***     USER SERVICES CENTER                                       ***
***     BUREAU OF SYSTEMS OPERATIONS (BSO)                         ***
***     OFFICE OF MANAGEMENT INFORMATION SYSTEMS (OMIS)            ***
***     EXECUTIVE OFFICE FOR ADMINISTRATION AND FINANCE (A&F)      ***
***     COMMONWEALTH OF MASSACHUSETTS                              ***
***     ONE ASHBURTON PLACE, ROOM 1619                             ***
***     BOSTON, MA  02108                                          ***
***                                 (617) 727-5725                 ***
***  ALIAS:                                           TS05103 CAH84284A
***     CHUCK HOFFMAN                                 TS05103 CAH84284A
***     GTE LABORATORIES COMPUTER CENTER              TS05103 CAH84284A
***     40 SYLVAN ROAD                                TS05103 CAH84284A
***     WALTHAM, MA  02254-1168                       TS05103 CAH84284A
***                                 (617) 466-2131    TS05103 CAH84284A
***                                 (V/N) 679-2131    TS05103 CAH84284A
***                                                   TS05103 CAH84284A
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***                                                                ***
***  1.  A.  ADD CAPS/ASIS OPERAND.                           CAH84160A
***      B.  CHANGE PARM OPERAND FROM CAPS TO ASIS.           CAH84160A
***      C.  IF CAPS IS SELECTED, TRANSLATE PARM TO UPPER     CAH84160A
***          CASE.                                            CAH84160A
***                                                           CAH84160A
***  2.  A.  CHANGE FROM LINK MACRO TO ATTACH MACRO FOR       CAH84198A
***          EXECUTION OF COMMAND WITH 'LIB' OPERAND.         CAH84198A
***      B.  CHANGE FROM XCTL MACRO TO ATTACH MACRO FOR       CAH84198A
***          EXECUTION WITHOUT 'LIB' OPERAND.                 CAH84198A
***      C.  ADD 'TASKLIB' ALIAS FOR 'LIB' OPERAND.           CAH84198A
***      D.  CHANGE WORDING OF PROMPT FOR PROGRAM NAME.       CAH84198A
***      E.  CHANGE COMMENTS REFERRING TO 'SYSLIST' OPERAND   CAH84198A
***          TO REFER TO 'SYSLIBS' OPERAND.                   CAH84198A
***      F.  PUT PARAMETERS INTO SP-000 INSTEAD OF SP-001.    CAH84198A
***      G.  SHARE SP-078 WITH THE ATTACHED TASK.             CAH84198A
***                                                           CAH84198A
***  3.  A.  ADD ALTERNATE PROGRAMMER ID INFORMATION. TS05103 CAH84284A
***      B.  FIX LOSS OF RETURN CODE DURING WAIT      TS05103 CAH84284A
***          AFTER ATTACH.                            TS05103 CAH84284A
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***   PROLOGUE                                                     ***
***                                                                ***
**********************************************************************
*
LIBCALL  CSECT
         STM   R14,R12,12(R13)          SAVE INTO CALLERS S.A.
         B     BASE-LIBCALL(0,R15)      BRANCH TO AROUND EYECATCHER
         DC    AL1(L'NAME)                LENGTH OF NAME
NAME     DC    C'LIBCALL'                 MODULE NAME
         DC    C' &SYSDATE &SYSTIME '     DD.MM.YY HH.MM
BASE     LR    RBASE,R15                RBASE IS BASE REGISTER
         USING LIBCALL,RBASE              TELL ASSEMBLER
         GETMAIN  R,LV=WORK0LEN,SP=0    GET AREA FOR MYSAVE AND WORK
         ST    R13,4(0,R1)              CALLERS S.A. ADDR TO MY S.A.
         ST    R1,8(0,R13)              MY S.A. ADDR TO CALLERS S.A.
         LM    R15,R1,16(R13)           RESTORE REGS USED BY GETMAIN
         L     R13,8(0,R13)             R13 POINTS TO MY S.A.
         USING WORK0,R13                  TELL ASSEMBLER
*
         USING PARMPDL,R9               ADDRESSABILITY OF PDE LIST
*
         ST    R1,CPPLPTR               SAVE CPPL POINTER
*
         MVC   SWITCH1(1),INIT1         INITIALIZE SWITCHES
*
         B     MAINLINE
*
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
*
MAINLINE BAL   RBAL,PPLSETUP            SET UP PARSE PARM LIST
         BAL   RBAL,PARSE               PARSE THE INPUT PARAMETERS
         BAL   RBAL,MOVEDATA            MOVE THE PARM DATA
         BAL   RBAL,LIBS                PARSE THE LIBS PARAMETER
         BAL   RBAL,CONCAT              CONCATENATE THE LIBRARIES
         BAL   RBAL,XFER                TRANSFER CONTROL
         BAL   RBAL,DECONCAT            DECONCATENATE THE LIBRARIES
*
         B     ENDING                   BRANCH TO ENDING
         EJECT
**********************************************************************
***                                                                ***
***   EPILOGUE                                                     ***
***                                                                ***
**********************************************************************
ENDING   LA       R2,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R2,0(0,R2)              R4 POINTS TO PDL
         IKJRLSA  (R2)                    FREE STORAGE OF PDL
*
         L        R14,COMPCODE            R14 TEMPORARILY HAS COMP CODE
         LR       R15,R13                 R15 HAS MY SAVE AREA ADDRESS
         L        R13,4(0,R13)            R13 RESTORE, PNT TO CALLER SA
         FREEMAIN R,LV=WORK0LEN,SP=0,A=(R15)  FREE MYSAVE,COMPCODE,ETC
         LM       R0,R12,20(R13)          R0-R12 RESTORE FROM CALLER SA
         LR       R15,R14                 R15 GETS COMP CODE
         L        R14,12(0,R13)           R14 RESTORE FROM CALLERS S.A.
         MVI      12(R13),X'FF'           SIGNAL MODULE COMPLETE
         BR       R14                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST                             ***
***                                                                ***
**********************************************************************
PPLSETUP ST    RBAL,PPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         MVC   MYPPL+(PPLUPT-PPL)(4),CPPLUPT              UPT  (CPPL)
         MVC   MYPPL+(PPLECT-PPL)(4),CPPLECT              ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,MYPPL+(PPLECB-PPL)                      ECB  (MINE)
         MVC   MYPPL+(PPLPCL-PPL)(4),VPARMPCL             PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,MYPPL+(PPLANS-PPL)                      ANS  (MINE)
         MVC   MYPPL+(PPLCBUF-PPL)(4),CPPLCBUF            CBUF (CPPL)
         XC    MYPPL+(PPLUWA-PPL)(4),MYPPL+(PPLUWA-PPL)   UWA  (MINE)
         DROP  R4
*
PPEND    L     RBAL,PPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
**********************************************************************
PARSE    ST    RBAL,PARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     RBAL,PARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***    MOVE THE PARM DATA FOR PROGRAM NAME AND PROB PROGRAM PARM   ***
***                                                                ***
**********************************************************************
MOVEDATA ST    RBAL,MORBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R4,PARMNAME              R4 BASE OF PDE FOR PARM NAME
         USING PPDE,R4                    TELL THE ASSEMBLER
*
         MVC   MYNAME(8),BLANKS         CLEAR THE NAME FIELD
         L     R5,PPDEADR               R5 POINTS TO PARM NAME
         LH    R6,PPDELTH               R6 IS LENGTH OF NAME
         BCTR  R6,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   MYNAME(0),0(R5)            MOVE THE NAME
         EX    R6,*-6                   EXECUTE THE MOVE
*
         LA    R4,PARMSTNG              R4 BASE OF PDE FOR PARM STRING
*
         LA    R5,MYPARM                ADDRESS OF PARM (LENGTH+CHAR)
         ST    R5,MYPARML                 INTO PARM LIST
         OI    MYPARML+00,X'80'           MARK END OF LIST
         XC    MYPARM(102),MYPARM       CLEAR MYPLEN AND MYPCHAR
         TM    PPDEFLG,X'80'            IF NO CHARACTER STRING PRESENT
         BZ    MOEND                      BRANCH OUT
         L     R5,PPDEADR               R5 POINTS TO PARM CHARS
         LH    R6,PPDELTH               R6 IS LENGTH OF CHARS
         LTR   R6,R6                    IF CHAR STRING NOT POSITIVE LEN
         BNP   MOEND                      BRANCH OUT
         STH   R6,MYPLEN                SAVE TO PASS TO PROGRAM
         BCTR  R6,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   MYPCHAR(0),0(R5)           MOVE THE CHARS
         EX    R6,*-6                   EXECUTE THE MOVE
         CLC   PARMCASE(2),H1           IF 'CAPS' NOT USED    CAH84160A
         BNE   MOEND                      BRANCH TO ENDING    CAH84160A
         B     *+10                     BRANCH AROUND TRANSLT CAH84160A
         TR    MYPCHAR(0),CAPTABL         TRANSL TO UPPER CSE CAH84160A
         EX    R6,*-6                   EXECUTE THE TRANSLATE CAH84160A
*
         DROP  R4                       END ADDRESSABILITY OF PDE
*
MOEND    L     RBAL,MORBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***    GET LIBRARY NAMES FROM THE LIB PARAMETER, IF ANY            ***
***                                                                ***
**********************************************************************
LIBS     ST    RBAL,LIRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    CCNUM,CCNUM              SET DSN COUNT TO ZERO
         CLC   PARMLIB(2),H2            IF SYSLIBS PARM USED  CAH84198A
         BE    LINOLIB                    SET FLAG AND BYPASS PROC.
*
         LA    R4,LIBNAMES              POINT TO FIRST DSN PDE
         USING PDSN,R4                  ADDRESSABILITY
*
         MVC   CCKEY(2),H1              SET CONCATENATION TEXT KEY
         XC    CCNUM(2),CCNUM           SET CONCATENATION TEXT NUMB.
         LA    R0,CCDATA                POINT TO FIRST TEXT LEN
         ST    R0,CCNEXTAD              SET THE ADDRESS
*
         MVC   XYZ(XYZLEN),ABC          INITIALIZE DATA FOR ALLOC
         GTEDASET XYZ,CPPLPTR=CPPLPTR   LINK DATA TOGETHER
*
LINAMES  CLC   PDSNDADR(4),F0           IF NO ADDRESS
         BE    LIEND                      BRANCH OUT
*
         BAL   RBAL,CCTEXT              BUILD 1 CONCATENATION T.U.
*
         CLC   PDSNNEXT(4),FF000000     IF END OF CHAIN
         BE    LIEND                      BRANCH OUT
         L     R4,PDSNNEXT              ELSE LOAD THE NEXT ADDRESS
         B     LINAMES                    AND LOOP BACK UP
*
LINOLIB  OI    SWITCH1,X'80'            TURN ON NOLIB SWITCH
         B     LIEND                    GO TO ENDING
*
LIEND    L     RBAL,LIRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***    ADD ONE DDNAME INTO TEXT UNIT FOR CONCATENATION             ***
***                                                                ***
**********************************************************************
CCTEXT   ST    RBAL,CCRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LH    R1,PDSNDLTH              R1 HAS LENGTH OF DSN
         BCTR  R1,0                       MINUS 1 FOR EXEC
         L     R2,PDSNDADR              R2 POINTS TO DSN
         MVC   XYZDSNAM(44),BLANKS      INITIALIZE T.U. DSNAME FIELD
         B     *+10                     BRANCH AROUND MOVE
         MVC   XYZDSNAM(0),0(R2)          MOVE NAME TO TEXT UNIT
         EX    R1,*-6                   EXECUTE THE MOVE
*
         GTEDAALC XYZ,VERB=AL           ALLOCATE THE DATASET
         CLC   XYZS99RC(4),F0           IF R/C NOT ZERO
         BNE   CCERROR                    BRANCH TO ERROR
*
         L     R1,CCNEXTAD              R1 POINTS TO NEXT BYTE OF T.U.
         XR    R2,R2                    CLEAR R2 FOR INSERT
         ICM   R2,3,XYZRTDDN-2            R2 IS LENGTH OF NEXT NAME
         LA    R2,2(0,R2)                 PLUS 2 FOR LENGTH FIELD
         BCTR  R2,0                       MINUS ONE FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   0(0,R1),XYZRTDDN-2         MOVE LENGTH AND DDNAME
         EX    R2,*-6                   EXECUTE THE MOVE
         LA    R1,1(R2,R1)              POINT TO NEXT FREE BYTE OF TU.
         ST    R1,CCNEXTAD                SAVE THE ADDRESS
         LH    R1,CCNUM                 INCREMENT THE
         LA    R1,1(0,R1)                 NUMBER OF
         STH   R1,CCNUM                     DDNAMES IN TEXT UNIT
         B     CCEND                    BRANCH TO END
*
CCERROR  LA    R15,8(0,0)               SET AN ERROR CODE
         ST    R15,RETCDE                 SAVE IT
         B     CCEND                    BRANCH TO END
*
CCEND    L     RBAL,CCRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***      CONCATENATE THE LIBRARY FILES                             ***
***                                                                ***
**********************************************************************
CONCAT   ST    RBAL,CORBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         TM    SWITCH1,X'80'            IF SYSLIBS PARM USED  CAH84198A
         BO    COEND                      BRANCH TO ENDING
*
         CLC   CCNUM(2),H0              IF LIB USED, BUT NO NAMES
         BNE   *+12                       THEN
         OI    SWITCH1,X'80'                TURN ON 'NOLIB' SWITCH
         B     COEND                        BRANCH TO ENDING
*
         CLC   CCNUM(2),H1              IF LESS THAN 2 NAMES
         BH    COCC                       THEN
         OI    SWITCH1,X'40'                SET '<2' SWITCH
         B     CODCB                        SKIP CONCATENATION
*
COCC     LA    R1,CCTUPL                R1 POINTER TO CONCAT. TUPL
         ST    R1,XYZRB+(S99TXTPP-S99RB)  SAVE IN REQUEST BLOCK
         LA    R1,CCTEXTUT              R1 POINTER TO CONCAT TEXT UNIT
         ST    R1,CCTUPL                  SAVE IN CONCAT TUPL
         OI    CCTUPL,X'80'             MARK END OF LIST
         GTEDAALC XYZ,VERB=CC           CONCATENATE
         CLC   XYZS99RC(4),F0           IF R/C NOT ZERO
         BNE   COERROR                    BRANCH TO END
*
CODCB    MVC   LIBSDCB(LIBSDCBL),PODCB  INITIALIZE THE DCB
*
         LA    R5,LIBSDCB               ADDRESSABILITY OF
         USING IHADCB,R5                  INPUT DCB
         MVC   DCBDDNAM(8),BLANKS       INITIALIZE DDNAME
         XR    R6,R6                    CLEAR R6 FOR INSERT
         ICM   R6,3,CCDATA              R6 HAS LEN OF CONCAT DDNAME
         BCTR  R6,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND THE MOVE
         MVC   DCBDDNAM(0),CCDATA+2       MOVE THE DDNAME TO DCB
         EX    R6,*-6                   EXECUTE THE MOVE
         B     COEND                    BRANCH TO ENDING
*
COERROR  LA    R15,8(0,0)               SET INTERNAL CODE TO 8
         ST    R15,RETCDE               SAVE IT
         B     COEND                    BRANCH TO ENDING
*
COEND    L     RBAL,CORBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***      TRANSFER CONTROL TO THE PROBLEM PROGRAM MODULE            ***
***                                                                ***
**********************************************************************
XFER     ST    RBAL,XFRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R5,MYNAME                R5 PNTS TO MODULE NAM CAH84198A
         LA    R6,ATTECB                R6 PNTS TO ECB        CAH84198A
*                                                             CAH84198A
         LH    R0,CCNUM                 R0 IS NO OF LIB NAMES CAH84198A
         CH    R0,H0                    IF NONE               CAH84198A
         BE    XFXCTL                     NO TASKLIB          CAH84198A
         B     XFLINK                   ELSE USE TASKLIB      CAH84198A
*                                                             CAH84198A
XFXCTL   MVC   ATCHL(ATCHLLTH),ATCHD    INITIALIZE CTRL LIST  CAH84198A
         XC    ATTECB,ATTECB            CLEAR ECB             CAH84198A
         LA    R1,MYPARML               R1 PNTS TO PARM ADDR  CAH84198A
         ATTACH EPLOC=(R5),ECB=(R6),SF=(E,ATCHL),                      X
               SHSPV=78                                       CAH84198A
         ST    R1,ATTTCBAD              SAVE THE TCB ADDRESS  CAH84198A
         WAIT  1,ECB=ATTECB             WAIT ON THE TASK      CAH84198A
         MVC   COMPCODE(4),ATTECB       SAVE ECB R/C  TS05103 CAH84284A
         DETACH ATTTCBAD                DETACH THE TASK       CAH84198A
         B     XFEND                                          CAH84198A
*                                                             CAH84198A
XFLINK   LA    R2,LIBSDCB               R2 HAS TASKLIB ADDR   CAH84198A
         MVC   OPENL(8),OPEND           INIT OPEN PARM LIST   CAH84198A
         OPEN  (LIBSDCB,(INPUT)),MF=(E,OPENL)  OPEN TASKLIB   CAH84198A
         MVC   ATCHL(ATCHLLTH),ATCHD    INITIALIZE CTRL LIST  CAH84198A
         XC    ATTECB,ATTECB            CLEAR ECB             CAH84198A
         LA    R1,MYPARML               R1 PNTS TO PARM ADDR  CAH84198A
         ATTACH EPLOC=(R5),TASKLIB=(R2),ECB=(R6),SF=(E,ATCHL),         X
               SHSPV=78                                       CAH84198A
         ST    R1,ATTTCBAD              SAVE THE TCB ADDRESS  CAH84198A
         WAIT  1,ECB=ATTECB             WAIT ON THE TASK      CAH84198A
         MVC   COMPCODE(4),ATTECB       SAVE ECB R/C  TS05103 CAH84284A
         DETACH ATTTCBAD                DETACH THE TASK       CAH84198A
         MVC   CLOSEL(8),CLOSED         INIT CLOSE PARM LIST  CAH84198A
         CLOSE (LIBSDCB),MF=(E,CLOSEL)  CLOSE THE FILES       CAH84198A
         B     XFEND                                          CAH84198A
*
XFEND    L     RBAL,XFRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***      DECONCATENATE THE LIBRARIES                               ***
***                                                                ***
**********************************************************************
DECONCAT ST    RBAL,DERBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*                                                             CAH84198A
         LH    R0,CCNUM                 R0 IS NO OF LIB NAMES CAH84198A
         CH    R0,H0                    IF NONE               CAH84198A
         BE    DEEND                      NO DECONCAT         CAH84198A
*
         TM    SWITCH1,X'40'            CHECK FOR '<2' SWITCH
         BO    DEEND                      IF '<2' BYPASS DECONCAT.
*
         LA    R1,DCTEXTUT              R1 ADDR OF DECONCAT T.U.
         ST    R1,DCTUPL                STORE IN T.U. POINTER LIST
         OI    DCTUPL,X'80'             MARK END OF LIST
         MVC   DCKEY(2),H1              SET KEY TO X'0001'
         MVC   DCNUM(2),H1              SET # TO X'0001'
         MVC   DCDATA(12),CCDATA        TAKE DDNAME AND LEN FROM CONCAT
         LA    R1,DCTUPL                R1 POINTER TO DECONCAT. TUPL
         ST    R1,XYZRB+(S99TXTPP-S99RB)  SAVE IN REQUEST BLOCK
         GTEDAALC XYZ,VERB=DC           DECONCATENATE
         CLC   XYZS99RC(4),F0           IF R/C NOT ZERO
         BNE   DEERROR                    BRANCH TO ERROR
         B     DEEND
*
DEERROR  LA    R15,8(0,0)               SET INTERNAL CODE TO 8
         ST    R15,RETCDE               SAVE IT
         B     DEEND                    BRANCH TO ENDING
*
DEEND    L     RBAL,DERBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
CONSTDTA DS    0D                       AREA FOR DATA CONSTANTS
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
DZIPS    DS    0D                       DOUBLE WORD OF ZEROES
         DC    2F'0'
BLANKS   DC    CL80' '                  10 DOUBLE WORDS OF BLANKS
FF000000 DC    XL4'FF000000'            IKJPARS END OF LIST INDICATOR
F0       DC    F'0'                     CONSTANT
F4       DC    F'4'                     CONSTANT
F8       DC    F'8'                     CONSTANT
F12      DC    F'12'                    CONSTANT
H0       DC    H'0'                     CONSTANT
H1       DC    H'1'                     CONSTANT
H2       DC    H'2'                     CONSTANT
*
INIT1    DC    B'00000000'              INITIAL SETTINGS FOR SWITCH1
*                1.......               X'80' - NO USE OF LIB PARM
*                .1......               X'40' - FEWER THAN 2 DSN'S
*
ABC      GTEDADAT DSNAM=X,STATS=SHR,CNVRT=YES,RTDDN=X
*
OPEND    OPEN  (,(INPUT)),MF=L
CLOSED   CLOSE (),MF=L
*
PODCB    DCB  DSORG=PO,MACRF=R
*
ATCHD    ATTACH SF=L                                          CAH84198A
ATCHDLTH EQU   *-ATCHD                                        CAH84198A
*
*                                                             CAH84198A
*                                                             CAH84160A
CAPTABL  DC    XL16'000102030405060708090A0B0C0D0E0F'         CAH84160A
         DC    XL16'101112131415161718191A1B1C1D1E1F'         CAH84160A
         DC    XL16'202122232425262728292A2B2C2D2E2F'         CAH84160A
         DC    XL16'303132333435363738393A3B3C3D3E3F'         CAH84160A
         DC    XL16'404142434445464748494A4B4C4D4E4F'         CAH84160A
         DC    XL16'505152535455565758595A5B5C5D5E5F'         CAH84160A
         DC    XL16'606162636465666768696A6B6C6D6E6F'         CAH84160A
         DC    XL16'707172737475767778797A7B7C7D7E7F'         CAH84160A
         DC    X'80',C'ABCDEFGHI',X'8A8B8C8D8E8F'             CAH84160A
         DC    X'90',C'JKLMNOPQR',X'9A9B9C9D9E9F'             CAH84160A
         DC    X'A0A1',C'STUVWXYZ',X'AAABACADAEAF'            CAH84160A
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'         CAH84160A
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'         CAH84160A
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'         CAH84160A
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'         CAH84160A
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'         CAH84160A
*
CONSTEND DS    0D
CONSTLEN EQU   *-CONSTDTA               TOTAL LENGTH OF CONSTANTS
*
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
*                      ==> CHANGE WORDING OF PROMPT=, BELOW   CAH84198A
PARMNAME IKJIDENT 'PROGRAM NAME',                                      X
               UPPERCASE,                                              X
               MAXLNTH=8,                                              X
               FIRST=ALPHA,                                            X
               OTHER=ALPHANUM,                                         X
               PROMPT='NAME OF PROGRAM YOU ARE TRYING TO EXECUTE',     X
               HELP='1-8 CHARACTER NAME OF LOAD LIBRARY MEMBER'
*
PARMSTNG IKJPOSIT QSTRING,                                             X
               ASIS                                           CAH84160A
*
PARMCASE IKJKEYWD DEFAULT='CAPS'                              CAH84160A
         IKJNAME  'CAPS'                                      CAH84160A
         IKJNAME  'ASIS'                                      CAH84160A
*                                                             CAH84160A
PARMLIB  IKJKEYWD DEFAULT='SYSLIBS'
         IKJNAME  'LIB',SUBFLD=LIBSUBF,                                X
               ALIAS=('STEPLIB','CONCAT','TASKLIB')           CAH84198A
         IKJNAME  'SYSLIBS'
*
LIBSUBF  IKJSUBF
LIBNAMES IKJPOSIT  DSNAME,                                             X
               LIST,                                                   X
               USID
*
         IKJENDP
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA IN SUBPOOL 000                                     ***
***                                                                ***
**********************************************************************
WORK0    DSECT                          AREA-0 FOR VARIABLES
MYSAVE   DS    18F                      REGISTER SAVE AREA
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
*
PPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
MORBALSV DS    F                        RETURN ADDRESS SAVE AREA
LIRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CCRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CORBALSV DS    F                        RETURN ADDRESS SAVE AREA
XFRBALSV DS    F                        RETURN ADDRESS SAVE AREA
DERBALSV DS    F                        RETURN ADDRESS SAVE AREA
*
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
ATTECB   DS    F                        ECB FOR ATTACH        CAH84198A
ATTTCBAD DS    F                        TCB ADDR FROM ATTACH  CAH84198A
MYANS    DS    F                        POINTER TO THE PDL
*
MYNAME   DS    CL8                      NAME OF PROGRAM TO LINK TO
*
SWITCH1  DS    B'00000000'              SWITCH AREA 1
*                1.......               X'80' - NO USE OF LIB PARM
*                .1......               X'40' - FEWER THAN 2 DSN'S
*
ATCHL    ATTACH SF=L                                          CAH84198A
ATCHLLTH EQU   *-ATCHL                                        CAH84198A
*
*                                                             CAH84198A
*                                                             CAH84198A
*
XYZ      GTEDADAT MAP=ONLY,DSNAM=X,STATS=SHR,CNVRT=YES,RTDDN=X
*
OPENL    OPEN  (,(INPUT)),MF=L
CLOSEL   CLOSE (),MF=L
*
DCTUPL   DS    F                        DECONCAT TEXT UNIT PTR LIST
*
DCTEXTUT DS    0H                       DECONCATENATION TEXT UNIT
DCKEY    DS    H                          X'0001'
DCNUM    DS    H                          X'0001'
DCDATA   DS    CL12                       AREA FOR 1 12-BYTE FIELD
*
CCNEXTAD DS    F                        POINTER INTO CONCATEN T.U.
*
CCTUPL   DS    F                        CONCAT TEXT UNIT PTR LIST
*
CCTEXTUT DS    0H                       CONCATENATION TEXT UNIT
CCKEY    DS    H                          X'0001'
CCNUM    DS    H                          NUMBER OF DDNAMES
CCDATA   DS    CL3072                     AREA FOR 256 12-BYTE FIELDS
*
         DS    0D
MYPARML  DS    A                        ADDRESS OF MYPARM
*
MYPARM   DS    0D                       PARM TO PASS TO PGM 'MYNAME'
MYPLEN   DS    H                          LENGTH
MYPCHAR  DS    CL100                      DATA
*
LIBSDCB  DCB  DSORG=PO,MACRF=R
LIBSDCBL EQU  *-LIBSDCB
*
WORK0END DS    0D
WORK0LEN EQU   *-WORK0                  TOTAL LENGTH OF WORK-1 AREA
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING MACROS                                               ***
***                                                                ***
**********************************************************************
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         PRINT GEN
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         PRINT NOGEN
         IEFZB4D0
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES
         IHADCB DSORG=PO,DEVD=DA
         EJECT
**********************************************************************
***                                                                ***
***   OTHER DSECTS                                                 ***
***                                                                ***
**********************************************************************
PDSN     DSECT                          PDE FOR DSNAME
PDSNDADR DS    AL4                        ADDRESS OF DSN
PDSNDLTH DS    H                          LENGTH OF DSN
PDSNDFLG DS    BL1                        FLAG BITS
         DS    XL1                        RESERVED
PDSNMADR DS    AL4                        ADDRESS OF MEMBER NAME
PDSNMLTH DS    H                          LENGTH OF MEMBER NAME
PDSNMFLG DS    BL1                        FLAG BITS
         DS    XL1                        RESERVED
PDSNPADR DS    AL4                        ADDRESS OF PASSWORD
PDSNPLTH DS    H                          LENGTH OF PASSWORD
PDSNPFLG DS    BL1                        FLAG BITS
         DS    XL1                        RESERVED
PDSNNEXT DS    AL4                        NEXT PDE ADDRESS IN LIST
         SPACE 3
PPDE     DSECT                          PDE FOR IKJIDENT OR IKJPOSIT
PPDEADR  DS    AL4                        ADDRESS
PPDELTH  DS    H                          LENGTH
PPDEFLG  DS    BL1                        FLAG BITS
         DS    XL1                        RESERVED
         EJECT
**********************************************************************
***                                                                ***
***   EQUATES                                                      ***
***                                                                ***
**********************************************************************
RBASE    EQU   12                       BASE REGISTER
RBAL     EQU   10                       BAL REGISTER
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
