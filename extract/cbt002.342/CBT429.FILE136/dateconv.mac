DCV      TITLE 'CONVERT EBCDIC JULIAN  DATE TO GREGORIAN'
DATECONV CSECT
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        FIXED POINT REGISTERS                                        *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0        *USED BY O.S.
R1       EQU   1        *USED BY O.S. // ADDRESS OF PARAMETER LIST
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13       *USED BY O.S. // SAVE-AREA ADDRESS
R14      EQU   14       *USED BY O.S. // RETURN ADDRESS
R15      EQU   15       *USED BY O.S. // ENTRY-PT ADDR, RETURN CODE
         SPACE 1
***********************************************************************
*                                                                     *
*        FLOATING POINT REGISTERS                                     *
*                                                                     *
***********************************************************************
         SPACE 1
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
         EJECT
         SAVE  (14,12),T,DATECONV_&SYSDATE._&SYSTIME
         LR    R12,R15
         USING DATECONV,R12
         LR    R2,R1
         GETMAIN RU,LV=DCVLEN
         SPACE 1
         LR    R3,R1               SAVE STORAGE ADDRESS
         LR    R0,R1               GET ADDRESS
         LA    R1,DCVLEN           SET LENGTH FOR CLEAR
         SR    R15,R15             PAD WITH ZEROS
         MVCL  R0,R14              CLEAR BUFFER TO ZEROS
         SPACE 1
         LR    R1,R3               POINT TO WORK AREA
         ST    R13,4(,R1)          CHAIN
         ST    R1,8(,R13)               SAVE
         LR    R13,R1                        AREA
         USING WORKAREA,R13        ADDRESS THE STORAGE
         SPACE 1
         LR    R1,R2               RETSORE PARM ADDRESS
         MVC   WORKID,=CL8'DCVWORK'  IDENTIFY WORK AREA
         L     R1,0(R1)              POINT TO PARM FIELD
         CLC   0(2,R1),=H'0'         PARM EMPTY
         BE    CALLTIME              YES, USE TODAY'S DATE
         LH    R2,0(R1)              LOAD LENGTH
         CH    R2,=H'5'              CHECK THE LENGTH
         BE    CHEKZONE              OK, CHECK THE ZONES
         OI    FLAG,BADPARM          INDICATE BAD PARM
         B     CALLTIME              SET IT UP
CHEKZONE DS    0H
         MVZ   ZONING(5),2(R1)       CHECK AT LEAST FOR ZONED
         CLC   ZONING,=C'00000'      DID IT CHECK OUT
         BE    PACKER                PACK AWAY
         OI    FLAG,BADPARM          INDICATE BAD PARM
         B     CALLTIME              SET IT UP
PACKER   DS    0H
         PACK  DOUBLE(6),2(5,R1)     PACK IT FOR SHIPMENT
         MVC   JULDATE,DOUBLE+2      MOVE IT TO PARMLIST
CALLTIME DS    0H
         MVC   PRTDCB(DCBLEN),MODDCB SET UP DATA CONTROL BLOCK
         SPACE 1
         OI    OPENLIST,X'80'        INDICATE LAST DCB
         OPEN  (PRTDCB,OUTPUT),MF=(E,OPENLIST)
         SPACE 1
         TM    FLAG,BADPARM          BAD PARM FIELD?
         BO    PRTMSG                YES, PRINT ERROR MSG
         LA    R3,JULDATE            SET UP
         LA    R4,HMDWORK                  PARMS
         USING HMDAREA,R4            ADDRESSIBILITY
         SPACE 1
         CALL  HMDCHRON,((R3),(R4)),VL,MF=(E,CALLEX)
         SPACE 1
PRTMSG   DS    0H
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
         TM    FLAG,BADPARM          RECOVERING FROM BAD PARM?
         BZ    PRTTIME               NO, DO THE TIME
         MVC   PRTLINE(L'BP),BP      SHOW BAD PARM
         B     PUTLINE                 TO THE USER
         SPACE 1
PRTTIME  DS    0H
         MVC   DAYNAME,HMDDAYNM
         MVC   DAYYEAR,HMDDAYYR
         MVC   GREGDATE,HMDENGDT
         SPACE 1
PUTLINE  DS    0H
         PUT   PRTDCB,PRTLINE
         SPACE 1
         OI    OPENLIST,X'80'        INDICATE LAST DCB
         CLOSE PRTDCB,MF=(E,OPENLIST)
         SPACE 1
         LA    R15,0                 ASSUME ALL OK
         TM    FLAG,BADPARM          WAS THE PARM BAD
         BZ    EOJTIME               NO, JUST END
         LA    R15,16                SET RETURN CODE
EOJTIME  DS    0H
         LR    R8,R15                SAVE RETURN CODE
         LR    R1,R13                GET SAVE AREA ADDRESS
         L     R13,4(,R1)            RETSORE PREVIOUS SA ADDRESS
         FREEMAIN RU,LV=DCVLEN,A=(1)
         LR    R15,R8                LOAD RETURN CODE
         RETURN (14,12),T,RC=(15)    RETURN TO CALLER
         EJECT
MODDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=80,BLKSIZE=3200,         X
               MACRF=PM,RECFM=FB
DCBLEN   EQU   *-MODDCB
         SPACE 1
BP       DC    C'PARM CONSISTS OF A VALID JULIAN DATE OR NOTHING'
         EJECT
***
*** DSECTS
***
WORKAREA DSECT                       RESUME R13 DSECT
SAVEAREA DS    18F                   SAVE AREA
WORKID   DS    CL8                   WORK AREA ID
ZONING   DS    CL5                   CHECK FOR ZONE DECIMAL
         SPACE 1
FLAG     DS    C
BADPARM  EQU   X'80'                 INDICATE BAD PARM FIELD
         SPACE 1
DOUBLE   DS    D                     HMDCHRON WORK AREA
HMDWORK  DS    CL72
         SPACE 1
CALLEX   CALL  ,(0,0),MF=L           LIST FORM CALL MACRO
         SPACE 1
OPENLIST OPEN  (,),MF=L              OPEN PARAMETER LIST
         SPACE 1
JULDATE  DS    F                     JULIAN DATE AREA
         SPACE 2
PRTDCB   DC    CL(DCBLEN)' '         AREA TO COPY DCB
         SPACE 1
PRTLINE  DS    CL80                  PRINT LINE
         ORG   PRTLINE
         DS    CL2
DAYNAME  DS    CL9
         DS    CL2
DAYYEAR  DS    CL11
         DS    CL2
GREGDATE DS    CL8
         ORG   ,
DCVLEN   EQU   *-WORKAREA            LENGTH OF WORKAREA
         SPACE 2
HMDAREA  HMDTIME ,                   HMDCHRON MAP
         END     DATECONV
