         TITLE 'APLCAMS : IDCAMS PRE-PROCESSOR'
******************************************************************
* APLCAMS: FRONT END TO IDCAMS.
* THIS PROGRAM WILL INVOKE IDCAMS, MAKING MINOR CHANGES TO CERTAIN
* SYSIN DD CARDS UNDER CERTAIN CONDITIONS.  IF A VOLALLOC DD STATEMENT
* IS PRESENT, THEN ANY STRING OF XXXXXX WILL BE CHANGED TO THE
* VOLSER ASSOCIATED WITH THE VOLALLOC DD STATEMENT.
* THIS FRONT-END IS DESCRIBED IN THE IDCAMS REFERENCE BOOK OR
* MVS/EXTENDED ARCHITECTURE INTERGRATED CATALOG ADMINISTRATION:
* ACCESS METHOD SERVICES REFERENCE APPENDIX D.
* STUART BUZZO APL 12-24-86
* APF NOTE: THIS PROGRAM IS LINKED WITH AC(1) BECAUSE IDCAMS IS.
* THE SOLE PURPOSE OF THIS PROGRAM IS TO MOVE THE VOLSER SPECIFICATION
* OF A VSAM DATA SET FROM THE SYSIN CONTROL CARDS TO JCL CARDS.
* THIS IS CONSIDERED USEFULL FOR TWO PURPOSES:
* 1) IT MAKES THE VOLSER SPECIFICATION MORE OBVIOUS: EASIER TO
*    FIND AND CHANGE OR OVERRIDE.
* 2) ALLOWS NON-SPECIFIC VOLUME REQUESTS (WORK PACKS) FOR VSAM DATA
*    SETS.
*
*   SAMPLE INVOCATION  WOULD BE AS FOLLOWS:
*   //JS55       EXEC PGM=APLCAMS,PARM='ANY VALID IDCAMS PARMS'
*   //SYSPRINT   DD   SYSOUT=*
*   //VOLALLOC   DD   UNIT=SYSDA,SPACE=(TRK,0),VOL=SER=APLPR7
*   //SYSIN      DD   *
*     DEFINE CLUSTER NAME(......
*     .....................
*     VOLUME(XXXXXX) .......
*     .....................
*
*   IN THE ABOVE SAMPLE, THE XXXXXX WILL BE CHANGED TO APLPR7
*=================================================================
*   WRITTEN BY:
*
*   STUART BUZZO
*   AMERICAN PRESIDENT LINES, LTD.
*   THREE WATERS PARK DRIVE
*   SUITE 115
*   SAN MATEO, CA  94403
*   (415) 570-2325
*=================================================================
******************************************************************
         EJECT
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
******************************************************************
* SMALL DSECT OF PARM LISTS PASSED TO IOROUTINE FROM IDCAMS
******************************************************************
PRMAREA  DSECT
USRADR   DS    AL4
IOFLAGAD DS    AL4
IOINFOAD DS    AL4
******************************************************************
* MAIN ENTRY POINT, FRONT END TO IDCAMS
******************************************************************
APLCAMS  CSECT
         STM   14,12,12(13)            SAVE CALLER'S REGISTERS
         LR    12,15                   LOAD OUR BASE REGISTER
         USING APLCAMS,12              ASSIGN BASE REGISTER
         LA    14,SAVEARE1             GET OUR SAVE AREA ADDRESSS
         ST    13,4(14)                STORE CALLER'S SA ADDR IN OURS
         ST    14,8(13)                STORE OUR SA ADDR IN CALLER'S
         LR    13,14                   KEEP OUR SA ADDR
         B     STARTIT
         DC    C'APLCAMS: AMERICAN PRESIDENT LINES FRONT END TO IDCAMS'
STARTIT  L     R2,0(R1)                GET PARM LIST FROM R1 POINTER
         ST    R2,OPTADR               STORE PARM ADR IN OPTION ADR
         MVI   OPTADR,X'00'            RESET VL BIT FROM OS PARM LIST
*******************************************************************
* DETERMINE VOLSER FROM VOLALLOC DD CARD
* PRIME DCB AND OPENLIST FOR SYSIN IN DYNAMIC AREA
*******************************************************************
         RDJFCB (VOLALLOC)
         C     R15,=F'0'               WAS RDJFCB SUCCESSFUL?
         BNE   BADALLOC                NO, GO AND WTO ABOUT IT
         MVC   USERDAT1,JFCB+118       MOVE VOLSER TO PARM AREA
*******************************************************************
* INVOKE IDCAMS IN FULL STATUS
*******************************************************************
         LA    R1,ARGLIST
         LINK  EP=IDCAMS
         L     13,SAVEARE1+4           RELOAD CALLER'S SAVEARA ADDR
         RETURN (14,12),RC=(15)        RELOAD TO OS, PASS ON IDCAMS RC
*******************************************************************
* NO DDCARD FOR VOLALLOC, SO INVOKE IDCAMS WITHOUT AND
* INTERCEPTION OF SYSIN CARDS
*******************************************************************
BADALLOC WTO   'APLCAMS: VOLALLOC DD STATEMENT MISSING'
         WTO   'APLCAMS: IDCAMS WILL BE INVOKED WITHOUT VOLSER SUBSTITUX
               TIONS'
         LA    R1,ARGLIST
         MVI   OPTADR,X'80'            MARK OPTIONS AS LAST PARM
         LINK  EP=IDCAMS               INVOKE IDCAMS WITH NO IOROUTINE
         L     13,SAVEARE1+4           RELOAD CALLER'S SAVEARA ADDR
         RETURN (14,12),RC=(15)        RELOAD TO OS,PASSING IDCAMS RC
*******************************************************************
* DC'S AND DCBS FOR APLCAMS CSECT
* 1) GENERAL JUNK FOR APLCAMS CSECT
*******************************************************************
SAVEARE1 DS    18F                     18 FULLWORD SAVE AREA
VOLALLOC DCB   DDNAME=VOLALLOC,DSORG=PS,MACRF=(GM),EXLST=EXONE
DDSYSIN  DC    CL10'DDSYSIN   '
         DS    0F                      FORCE FULLWORD ALIGNMENT
EXONE    DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
*******************************************************************
* 2) PARM LIST TO PASS TO IDCAMS WHEN APLCAMS INVOKES IT.
*******************************************************************
         DS    0F                      FORCE FULLWORD ALIGNMENT
ARGLIST  DS    0XL16
OPTADR   DS    AL4
DDNADR   DC    AL4(DDNLIST)
PGNADR   DC    AL4(PGNLIST)
IOLADR   DC    AL4(IOLIST)
******************************************************************
* 3) DUMMY PARMS FOR IDCAMS WHEN APLCAMS INVOKES IT.
******************************************************************
DDNLIST  DC  XL2'0000'             NO OVERRIDING DDNAMES
PGNLIST  DC  XL2'0000'
******************************************************************
* 4) IOLIST (SYSIN ONLY) FOR IDCAMS WHEN APLCAMS INVOKES IT.
******************************************************************
         DS  0F                    FORCE FULL WORD ALIGNMENT
IOLIST   DS  0XL16
IOLLEN   DC  F'1'
IODDN1AD DC  AL4(DDSYSIN)
IORNTADR DC  AL4(APLSYSIN)
USRD1ADR DC  AL4(USERDAT1)
USERDAT1 DS  CL6
         LTORG
******************************************************************
*    CSECT APLSYSIN (IOROUTINE FOR DDNAME SYSIN TO BE INVOKED
*    BY IDCAMS FOR OPEN,GET,CLOSE)
******************************************************************
APLSYSIN CSECT
         STM   14,12,12(13)            SAVE CALLER'S REGISTERS
         LR    12,15                   LOAD OUR BASE REGISTER
         USING APLSYSIN,12             ASSIGN BASE REGISTER
         LA    14,SAVEARE2             GET OUR SAVE AREA ADDRESSS
         ST    13,4(14)                STORE CALLER'S SA ADDR IN OURS
         ST    14,8(13)                STORE OUR SA ADDR IN CALLER'S
         LR    13,14                   KEEP OUR SA ADDR
         LR    R9,R1                   GET PARM ADDRESS IN R9
         USING PRMAREA,R9              MAP PARM AREA
         L     R7,USRADR               ADDRESS USER DATA FIELD
         MVC   VOLSER2,0(R7)           GET VOLSER FROM USER DATA FIELD
         L     R7,IOFLAGAD             DETERMINE WHY IDCAMS INVOKED US
         CLC   0(1,R7),=X'00'          IS IT AN OPEN?
         BE    OPENIT                  YES, GO DO IT.
         CLC   0(1,R7),=X'04'          IS IT A CLOSE?
         BE    CLOSEIT                 YES, GO DO IT?
         CLC   0(1,R7),=X'08'          IS IT A GET?
         BE    GETIT                   YES, GO DO IT.
         WTO   'APLCAMS: UNEXPECTED REQUEST FROM IDCAMS'
         ABEND 99,DUMP
OPENIT   EQU   *
         L     R7,IOINFOAD
         CLC   0(8,R7),=CL8'SYSIN   '   IS THE OPEN FOR DDNAME SYSIN?
         BE    OPENIT2                  YES, BRANCH TO CONTINUE
         WTO   'APLCAMS: UNEXPECTED DDNAME REQUEST FROM IDCAMS'
         ABEND 88,DUMP
GETIT    GET   SYSINDCB,INREC
         LA    R5,INREC                SET POINTER TO SYSIN RECORD
         L     R6,=F'78'               SET DECREMENTING COUNTER
CHECKIT  CLC   0(6,R5),=CL6'XXXXXX'    IS THIS THE ONE?
         BE    SWITCH                  YES, GO MAKE SUBSTITUION
         A     R5,=F'1'                INCREMENT POINTER
         BCT   R6,CHECKIT              ARE WE AT END OF RECORD?
* FALL THROUGH WHEN AT END OF RECORD
         L     R5,IOINFOAD
         MVC   0(4,R5),RECADR          PRIME PARM LIST FOR RETURN
         MVC   4(4,R5),RECLEN
         B     RETCAMS                 RETURN TO IDCAMS
SWITCH   MVC   0(6,R5),VOLSER2         MAKE VOLSER SUBSTITUION
         B     CHECKIT                 GO BACK FOR MORE
OPENIT2  OPEN  (SYSINDCB,INPUT)
         B     RETCAMS                  RETURN TO IDCAMS
CLOSEIT  CLOSE (SYSINDCB)
         B     RETCAMS
EOFSYSIN L     13,SAVEARE2+4           RELOAD CALLER'S SAVEARA ADDR
         RETURN (14,12),RC=4           RELOAD TO IDCAMS. RC=4 IS EOF
RETCAMS  L     13,SAVEARE2+4           RELOAD CALLER'S SAVEARA ADDR
*        LR    R1,R9
         RETURN (14,12),RC=0           RELOAD TO OS
SAVEARE2 DS    18F                     18 FULLWORD SAVE AREA
         LTORG
SYSINDCB DCB   DDNAME=SYSIN,MACRF=(GM),DSORG=PS,LRECL=80,EODAD=EOFSYSIN
RECBLK   DS    0XL88
RECADR   DC    AL4(INREC)
RECLEN   DC    F'80'
INREC    DS    CL80
VOLSER2  DS    CL6
PGMEND   EQU   *
         END
