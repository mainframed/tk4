IKJ      TITLE 'IKJEFF10 - TSO SUBMIT EXIT ROUTINE '           HD APR87
***********************************************************************
*/* REF: ASYSHMD.MVS.ASM(IKJEFF10)
*/* COMP TSO
*/* DOC  THIS IS THE ASSEMBLER SOURCE CODE FOR THE
*/*      TSO SUBMIT EXIT ROUTINE.
*/*
*             MODULE NAME = IKJEFF10                                  *
*                                                                     *
*             DESCRIPTIVE NAME =  EXIT FOR TSO SUBMIT COMMAND         *
*                                                                     *
*             FUNCTION =                                              *
*              * ASSURE JOBNAME ADHERES TO 'APL' NAMING CONVENTIONS.  *
*                (CXXXYYYY) WHERE  C= COMPANY CODE                    *
*                                XXX= USERS ID & YYYY= ANYTHING       *
*                                                                     *
*                   NORMAL FORMAT IS 'CZZZXXXY' WHERE ZZZ -> GROUP APL*
*                                                                     *
*              * SUPPLY USERID FOR NOTIFY=*******                     *
*                                                                     *
*                OPERATION =                                          *
*                   SCAN JOBCARDS. CHANGE JOBNAME TO FIT       HD APR87
*                   CONVENTIONS. IF 'NOTIFY=*******' IS               *
*                   FOUND, SUBSTITUTE THE USERID FOR THE ASTERISKS    *
*                                                                     *
*              * SCAN JCL FOR ACF2 '//*LOGONID' AND            HD APR87
*                '//*PASSWORD' CARDS AND PROMPT USER FOR       HD APR87
*                OPERANDS. PLUG THESE VALUES INTO CONTROL      HD APR87
*                CARDS.                                        HD APR87
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER IS EBCDIC.  REASSEMBLE     *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = USES SPKA FORM OF MODESET MACRO      *
*                    INSTRUCTION SO MUST RUN SUPERVISOR STATE.        *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0,1     = WORK REGISTERS               *
*                    REGISTER  2       = MODESET KEYADDR              *
*                    REGISTERS 4       = ADDRESSABILITY TO EXIT       *
*                                              PARAMETER LIST         *
*                    REGISTER  5       = BASE FOR SUBMIT CONTROL INFO *
*                    REGISTER  6,7     = PTRS INTO  JCL CARD IMAGE    *
*                    REGISTERS 8,9     = WORK REGISTERS               *
*                    REGISTER  11      = ADDRESSABILITY TO GETMAINED  *
*                                              DATA AREA              *
*                    REGISTER  12      = ADDRESSABILITY TO IKJEFF10   *
*                                              CSECT                  *
*                    REGISTER  13      = SAVE AREA REGISTER           *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   'ZAP*'S)                                          *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 1K                                     *
*                                                                     *
*                ATTRIBUTES = PROTECT KEY 1, REENTRANT,               *
*                   SUPERVISOR STATE                                  *
*                                                                     *
*             ENTRY POINTS = IKJEFF10 (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                   IKJXXXXX:  (SUBMIT COMMAND) VIA CALL              *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO PARAMETER LIST MAPPED      *
*                BY IKJEFFIE MACRO                                    *
*                                                                     *
*             OUTPUT = SEE EXIT TOPICAL HEADING BELOW                 *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = AT PROGRAM END VIA BRANCH REGISTER 14    *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE =                                        *
*                   0  - CONTINUE                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PARMLIST, CVT, ASVT, ASCB, TSB      *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL DATA.                             *
*                                                                     *
*             MACROS = IKJEFFIE, FREEMAIN, GETMAIN, SAVE, MODESET,    *
*                      IKJTSB, IKTTSBX, IKJTCB, IKJRB, IHAPSA, IKJCPPL*
*                                                                     *
*             CHANGE ACTIVITY = MODIFIED 5/30/86 BY M. COST TO        *
*                               SUBSTITUTE LOGONID AND PASSWORD FOR   *
*                               ?? ON //*LOGONID AND //*PASSWORD.     *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                NO MESSAGES ARE ISSUED BY THIS MODULE.               *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
IKJEFF10 CSECT
         SAVE  (14,12),,IKJEFF10_&SYSTIME._&SYSDATE SAVE REGISTERS
*                                      WITH CSECT IDENTIFIER
         LR    R12,R15                 SET BASE REG.
         USING IKJEFF10,R12            CSECT ADDRESSABILITY
         L     R0,SIZDATD              LOAD REGISTER 0 WITH SIZE FOR
*                                      GETMAIN
         GETMAIN R,LV=(0)              ISSUE REGISTER FORM OF GETMAIN
*                                      FOR AREA IN SUBPOOL 0
         LR    R2,R1                   LOAD ADDRESS OF AREA    HD APR87
*                                                              HD APR87
         L     R3,SIZDATD                CLEAR                 HD APR87
         SLR   R5,R5                         THAT              HD APR87
         MVCL  R2,R4                             SAVE          HD APR87
*                                                     AREA     HD APR87
         LR    R11,R1                  LOAD REGISTER 11 WITH ADDRESS OF
*                                      GETMAINED AREA
         USING DATD,R11                ESTABLISH ADDRESSABILITY TO
*                                      GETMAINED AREA
         ST    R13,SAVEAREA+4          SAVE REGISTER 13 FOR SAVEAREA
*                                      CHAINING
         LM    R0,R1,20(R13)           RELOAD REGISTERS 0 AND 1 FROM
*                                      PREVIOUS SAVEAREA
         ST    R11,8(,R13)             SAVE ADDRESS OF CURRENT SAVEAREA
         SR    R15,R15
         ST    R15,16(,R13)            SET RETURN CODE TO ZERO.
         LR    R13,R11                 LOAD REGISTER 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         L     R4,0(,R1)               LOAD REGISTER 4 WITH POINTER TO
*                                      PARAMETER LIST
         USING IEEXITL,R4              ESTABLISH ADDRESSABILITY TO
*                                      PARAMETER LIST (USES
*                                      IKJEFFIE MAPPING MACRO)
         SPACE 2
*
         L     R6,IECARDP              SET POINTER TO JCL IMAGE.
*
         L     R5,IESUBCTP             SET BASE FOR CONTROL INFO.
*
         USING IESUBCTD,R5
         OI    IETAKEEX,IETCOMNT       CHECK FOR //* CARD      HD APR87
         SPACE 1
         TM    IESTMTP2,IESCOMNT       //*NONBLANK STATEMENT
         BO    CHECKIT                 CHECK FOR ACF2 CARD     HD APR87
         SPACE 1
         TM    IESTMTYP,IESJOB         JOB CARD?
         BZ    ENDPROC                 BY GOLLY IT IS          HD MAY87
*                                                              HD MAY87
IEJOBX   DS    0H                                              HD MAY87
         TM    IESTMTYP,IESCONTN       CONTINUATION?
         BO    CHKNOTIF                YES-SKIP JOB NAME STUFF.
         SPACE 2
         EXTRACT PSCBADDR,             GET                             X
               'S',                       PSCB                         X
               FIELDS=PSB,                    ADDRESS                  X
               MF=(E,EXTLIST)
         SPACE 2
         L     R8,IEUSRIDP             POINT TO USER ID.
         MVC   SAVEUSER(7),0(R8)       SAVE USERID
         L     R1,PSCBADDR             LOAD POINTER TO PSCB
         TM    PSCBATR1(R1),PSCBCTRL   SEE IF USER HAS OPERATOR
*                                      AUTHORITY
         BNO   REGUSER                 NO..GO DO REGULAR USER STUFF
         CLC   1(3,R8),=C'SYS'         'XSYS' USER?
         BNE   REGUSER                 NO-GO DO REGULAR USER STUFF
         CLC   2(7,R6),0(R8)           YES-1ST 7 CHARS = USER ID?
         BE    REGUSER                     YES-GO DROP OUT GROUP CODE.
         B     CHKNOTIF                    NO-ALLOW IT-GO CHECK NOTIF.
         SPACE 2
REGUSER  CLC   2(7,R6),0(R8)           1ST 7 CHARS OF JOBNAME = USERID?
         BNE   PLOP                    NO-GO PLOP IN RIGHT STUFF.
         SPACE
         MVC   3(4,R6),6(R6)           YES-DROP OUT
         MVC   7(4,R6),=CL4' '             GROUP CODE.
         B     CHKNOTIF
         SPACE
PLOP     DS    0H
         MVC   2(1,R6),0(R8)           PLOP IN COMP CODE
         MVC   3(3,R6),4(R8)            AND USER ID.
         SPACE 2
CHKNOTIF DS    0H
         LA    R14,1                   INDEX FOR BXLE.
         LA    R15,58(,R6)             END ADDR FOR SCAN.
CHKCARD  CLC   0(14,R6),=C'NOTIFY=*******'    GOT IT?
         BE    GOTIT       12345678901234     YES-BRANCH.
         BXLE  R6,R14,CHKCARD                 CONTINUE IF MORE.
         B     ENDPROC
         SPACE
GOTIT    DS    0H
         L     R8,IEUSRIDP             POINT TO USER ID.
         MVC   SAVEUSER(7),0(R8)       SAVE USERID
         MVC   7(7,R6),SAVEUSER        MOVE USERID
         SPACE
         CLI   14(R6),C','             NEED TO SHIFT DATA LEFT?
         BNE   ENDPROC                 NO-GO END.
         LA    R7,9(,R6)               START BLANK SEARCH AT 2ND CHAR.
         LA    R15,6(,R7)              SET END ADDR AT COMMA.
         LA    R14,1                   INDEX FOR BXLE.
CHKBLNK  CLI   0(R7),C' '              BLANK FOUND?
         BE    SHIFT                   YES-GO TO SHIFT DATA.
         BXLE  R7,R14,CHKBLNK          CONTINUE IF MORE TO CHECK.
         B     ENDPROC
         SPACE
SHIFT    DS    0H
         L     R6,IECARDP              SET POINTER TO JCL IMAGE.
         LA    R14,70(,R6)             COMPUTE # BYTES
         SR    R14,R15                 TO GO LEFT.
         EX    R14,MVCLEFT             SHIFT DATA LEFT
         B     ENDPROC
         SPACE
MVCLEFT  MVC   0(0,R7),0(R15)          'EX' ONLY TO SHIFT DATA.
         EJECT
***********************************************************************
*        WAS CARD //*PASSWORD OR //*LOGONID                           *
***********************************************************************
CHECKIT  DS    0H                      CHECK FOR ACF2 CARDS    HD APR87
         TM    IEEXITWD+3,X'C0'        FOUND LOGONID & PASSWORD?
         BO    ENDPROC                 GO TURN OFF CHECKING
*                                                              HD APR87
         CLC   0(14,R6),=C'//*PASSWORD ??'     CORRECT FORMAT? HD APR87
         BE    SETFLAG4                        FLAG FOR PW     HD APR87
         CLC   0(13,R6),=C'//*LOGONID ??'      CORRECT FORMAT? HD APR87
         BNE   ENDPROC                         NO, GET NEXT    HD APR87
*                                                              HD APR87
SETFLAG8 OI    IEEXITWD+3,X'80'                SAYS WE FOUND LOGONID
         B     CHAINS
SETFLAG4 OI    IEEXITWD+3,X'40'                SAYS WE FOUND PASSWORD
         B     CHAINS
***********************************************************************
*        FIND CPPL                                    *
*        FIND TCB OLD POINTER                         *
*        FIND RB POINTER                              *
*******************************************************
CHAINS   L     R3,PSATOLD-PSA
         USING TCB,R3
         L     R9,TCBRBP            -> RB FOR EXECUTING PGM
         USING RBBASIC,R9
*******************************************************
*        CHAIN THRU RB'S OR ADR OF TCB                *
*        WHEN FIRST RB ON QUEUE                       *
*******************************************************
RBLOOP   CLM   R3,7,RBLINKB        IF ADR OF TCB = THEN ITS 1ST RB
         BE    TOPRB
         CLC   RBLINKB(3),=X'000000'   JUST IN CASE
         BE    TOPRB
         ICM   R9,7,RBLINKB        PLUG IN ADR OF PREVIOUS RB
         B     RBLOOP
TOPRB    EQU   *
         DROP  R3                  DO NOT NEED ADRSING TO TCB
         L     R3,RBGRS1           SHOULD -> TO CPPL
         LA    R3,16(R3)           THIS IS YOUR CPPL
         USING CPPL,R3
         DROP  R9                  DO NOT NEED ADRSING TO RB
         L     R9,CPPLUPT          PLACE ADR OF UPT IN REG 9
         L     R8,CPPLECT          PLACE ADR OF ECT IN REG 8
********************************************
*        WHICH CARD WAS IT                 *
********************************************
WHICHONE CLC   0(14,R6),=C'//*PASSWORD ??'     CORRECT FORMAT?
         BE    PASSWORD                        GO MOVE IN PASSWORD
         CLC   0(13,R6),=C'//*LOGONID ??'      CORRECT FORMAT
         BE    LOGONID                         GO MOVE IN LOGONID
         B     ENDPROC
********************************************
*        ISSUE PUTGET FOR PASSWORD         *
********************************************
PASSWORD DS    0H                                              HD JAN87
         USING UPT,R9              ADDRES USER PROFILE TABLE   HD JAN87
         MVC   UPTBYTE(1),UPTSWS   SAVE THE PROFILE SWITCHES   HD JAN87
         NI    UPTSWS,255-UPTNPRM  TURN OFF NO PROMPTING FLAG  HD JAN87
PASSPTGT PUTGET PARM=APGPB,UPT=(9),ECT=(8),ECB=ECBADS,         HD JAN87X
               TERMPUT=ASIS,TERMGET=EDIT,                              X
               OUTPUT=(PWRDOLD,SINGLE,PTBYPS),MF=(E,IOPLADS)
         MVC   UPTSWS(1),UPTBYTE    RESTORE SWITCHES           HD JAN87
         LTR   R15,R15
         BNZ   ENDPROC
         DROP  R3
         LA    R3,APGPB             ADRESSABILITY TO PUTGET
         USING PGPB,R3              PARAMETER BLOCK
         L     R2,PGPBIBUF          ADR OF RESPONSE FROM TERMINAL USER
         DROP  R9                                              HD JAN87
********************************************
*        PROCESS RESPONSE FROM USER        *
*        THEN FREE INPUT BUFFER            *
********************************************
         LH    R3,0(R2)              LENGTH INCLUDING HEADER
         SH    R3,=H'5'             DECREMENT BY HEADER +1 FOR EXEC
         EX    R3,MOVEPWRD
*        OI    IEEXITWD+3,X'44'      SAYS WE HAVE SET PASSWORD
         B     ENDPROC
MOVEPWRD MVC   12(0,R6),4(R2)        MOVE PASSWORD STARTING AT ??
********************************************
*        ISSUE PUTGET FOR LOGONID          *
********************************************
LOGONID  DS    0H                                              HD JAN87
         USING UPT,R9              ADDRES USER PROFILE TABLE   HD JAN87
         MVC   UPTBYTE(1),UPTSWS   SAVE THE PROFILE SWITCHES   HD JAN87
         NI    UPTSWS,255-UPTNPRM  TURN OFF NO PROMPTING FLAG  HD JAN87
LOGPTGT  PUTGET PARM=APGPB,UPT=(9),ECT=(8),ECB=ECBADS,         HD JAN87X
               TERMPUT=ASIS,TERMGET=EDIT,                              X
               OUTPUT=(LOGOLD,SINGLE,PTBYPS),MF=(E,IOPLADS)
         MVC   UPTSWS(1),UPTBYTE    RESTORE SWITCHES           HD JAN87
         LTR   R15,R15
         BNZ   ENDPROC
         DROP  R3,R9                DROP OBSOLETE BASE REGS    HD JAN87
         LA    R3,APGPB             ADRESSABILITY TO PUTGET
         USING PGPB,R3              PARAMETER BLOCK
         L     R2,PGPBIBUF          ADR OF RESPONSE FROM TERMINAL USER
         LH    R3,0(R2)              LENGTH INCLUDING HEADER
         SH    R3,=H'5'             DECREMENT BY HEADER +1 FOR EXEC
UPCASE   TR    4(8,R2),TRANSTAB     UPPER CASE 8 CHARACTERS FOR LOGONID
         EX    R3,MOVELOG
*        OI    IEEXITWD+3,X'88'      SAYS WE HAVE SET PASSWORD
         B     ENDPROC
         SPACE 1
MOVELOG  MVC   11(0,R6),4(R2)        MOVE LOGONID STARTING AT ??
***********************************************************************
*                                                                     *
*        RETURN TO CALLER                                             *
*                                                                     *
***********************************************************************
         SPACE 2
ENDPROC  DS    0H
         L     R13,4(,R13)             RESTORE REGISTER 13
         L     R0,SIZDATD              LOAD REGISTER 0 WITH SIZE OF
*                                      GETMAINED AREA
         LR    R1,R11                  LOAD REGISTER 1 WITH ADDRESS OF
*                                      GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
*                                      ADDRESS
         LM    R14,R12,12(R13)         RESTORE REGISTERS & RETURN CODE.
         BR    R14                     RETURN VIA REGISTER 14
         SPACE 2
******** CONSTANTS AND DSECTS *********
*
DATA     DS    0H
DATD     DSECT
         DS    0F
SAVEAREA DS    18F
SAVEUSER DS    1D
UPTBYTE  DS    X                                               HD JAN87
         DS    0D
APGPB    PUTGET MF=L
PGL      EQU   *-APGPB                                         HD APR87
ECBADS   DC    F'0'
IOPLADS  DC    4F'0'
EXTLIST  EXTRACT MF=L
PSCBADDR DS    F
PSCBATR1 EQU   16
PSCBCTRL EQU   X'80'
         DS    0D
         ORG
ENDDATD  EQU   *
IKJEFF10 CSECT
         DS    0F
SIZDATD  DC    AL1(0)
         DC    AL3(ENDDATD-DATD)
PWRDOLD  DC    F'1'
         DC    A(PWRDMSG)
PWRDMSG  DC    H'26'
         DC    H'0'
         DC    CL22' >>***> ENTER PASSWORD'
LOGOLD   DC    F'1'
         DC    A(LOGMSG)
LOGMSG   DC    H'25'
         DC    H'0'
         DC    CL21' >>***> ENTER LOGONID'
PATCH    DC    8CL4'ZAP*'
TRANSTAB DS    0CL256
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040405B40404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040407B7C404040'
         DC    XL16'40C1C2C3C4C5C6C7C8C9404040404040'
         DC    XL16'40D1D2D3D4D5D6D7D8D9404040404040'
         DC    XL16'4040E2E3E4E5E6E7E8E9404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'40404040404040404040404040404040'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'
         DS    0F
R0       EQU   00                      EQUATES FOR REGISTERS 0-15
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
ASCBTSB  EQU   60
ASCBPTR  EQU   544
REJMSG   DSECT                         DSECT FOR RETURN MESSAGE
REJMLEN  DS    H
REJJOB   DS    CL4
REJSLOT  DS    CL9
         EJECT
         IKJEFFIE IETYPE=SUBMIT
         EJECT
         IKJTSB   LIST=YES,EXT=NO
         EJECT
         IKTTSBX
         EJECT
         IKJPGPB
         EJECT
         IKJCPPL
         EJECT
         IKJUPT ,                                              HD JAN87
         EJECT
         IHAPSA
         EJECT
         IKJTCB
         EJECT
         IKJRB
         END   IKJEFF10
