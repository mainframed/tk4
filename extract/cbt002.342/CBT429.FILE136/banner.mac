BANNER  TITLE '- PRINT PARM IN BLOCK LETTERS'
*
*  EQUATES
*
*
*  SYMBOLICS FOR THE GENERAL PURPOSE REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE REGISTER.
R13      EQU   13                      STANDARD LINKAGE.
R14      EQU   14                      STANDARD LINKAGE.
R15      EQU   15                      STANDARD LINKAGE.
         SPACE 3
*
*        UPON ENTRY TO BANNER :
*
*              R1  -  ADDRESS OF JCL PARM AREA.
*              R13 -  ADDRESS OF AN 18 WORD SAVE AREA.
*              R14 -  RETURN ADDRESS.
*              R15 -  ENTRY POINT ADDRESS.
*
         EJECT
*
*  OBSERVE LINKAGE CONVENTIONS
*
BANNER  CSECT
         SAVE   (14,12),,BANNER-COPYRIGHT_AMERICAN_PRESIDENT_LINES_&SYS-
               DATE_&SYSTIME
         USING BANNER,R12              SET BASE ADDRESS FOR MODULE.
         LR    R12,R15                 SET BASE REGISTER.
         CNOP  0,4                     ALIGN ON FULLWORD SO BAL IS
*                                         RIGHT.
         BAL   R2,APL00100             BRANCH AROUND AND POINT TO OUR
*                                         SAVE AREA.
APLSAVEA DC    18F'-1'                 OUR SAVE AREA.
         SPACE
APL00100 EQU   * ... BRANCH POINT AROUND SAVE AREA ...
         SPACE
         ST    R2,8(R13)               SAVE POINTER TO OUR SAVE AREA.
         ST    R13,4(,R2)              SAVE POINTER TO CALLERS SAVE
*                                         AREA.
         LR    R13,R2                  POINT TO OUR SAVE AREA.
         SPACE 3
         L     R1,0(R1)                GET THE PARM LIST
         LH    R2,0(R1)                GET LENGTH OF PARM DATA
         LA    R3,2(R1)                POINT TO PARM DATA
         LTR   R2,R2                   IS THE LENGTH ZERO
         BZ    RETURN                  YES, SIMPLY RETURN
         C     R2,=F'36'               IS THE LENGTH TOO LONG
         BNH   MOVEIT                  NO, PRESS ON
         LA    R2,36                   YES, TRUNCATE
*
MOVEIT   BCTR  R2,0                    DECREMENT 1 FOR EXECUTE
         EX    R2,MOVEDATA             MOVE THE PARM DATA
         OPEN  (SYSPRINT,OUTPUT)       OPEN SYSPRINT
         LA    R8,1                    MAKE 1 COPY
STRTPAGE EQU   *
         LA    R7,INPUT                GET THE INPUT ADDRESS
         ST    R7,INADDR               AND PUT IT IN PARM LIST
         XC    LINECT,LINECT           ZERO THE LINE COUNT
         MVI   CC,X'8B'                SKIP TO CHANNEL 1
         PUT   SYSPRINT,PRTLINE
         LA    R4,4                    "LINES" OF OUTPUT
STRTLINE LA    R5,12                   ACTUAL LINES PER "LINE"
         MVI   CC,X'09'                SPACE 1 LINE
PRTALINE L     R6,LINECT               GET LINE NUMBER TO OUTPUT
         LA    R6,1(R6)                BUMP BY ONE
         ST    R6,LINECT               TELL SUBROUTINE
         LA    R1,PARMLIST             FOR SUBROUTINE
         CALL  IEFSD095                CALL IT
         PUT   SYSPRINT,PRTLINE        WRITE IT OUT
         MVI   OUTPUT,X'40'            BLANK THE LINE
         MVC   OUTPUT+1(L'OUTPUT-1),OUTPUT
         BCT   R5,PRTALINE             LOOP BACK FOR 12 LINES
*
         MVI   CC,X'13'                PUT IN TWO BLANK LINES
         PUT   SYSPRINT,PRTLINE
         L     R7,INADDR               BUMP THROUGH INPUT DATA
         LA    R7,9(R7)
         ST    R7,INADDR               AND PUT IT BACK
         XC    LINECT,LINECT           ZERO THE LINE POINTER
         BCT   R4,STRTLINE
         BCT   R8,STRTPAGE             GO BACK FOR ANOTHER PAGE
         CLOSE SYSPRINT
RETURN   SR    R15,R15
         L     R13,4(R13)              EXIT HERE
         RETURN (14,12),T,RC=(15)
*
MOVEDATA MVC   INPUT(0),0(R3)          SAVE THE PARM DATA
*
PARMLIST DS    0F                      SUBROUTINE PARM LIST
INADDR   DC    A(INPUT)                ADDRESS OF INPUT DATA
         DC    A(LINECT)               ADDRESS OF LINE COUNTER
         DC    A(OUTPUT)               ADDRESS OF OUTPUT AREA
         DC    A(CHARCT)               ADDRESS OF CHARACTER COUNT
*
INPUT    DC    CL36' '                 INPUT AREA
LINECT   DC    F'0'                    LINE COUNTER
CHARCT   DC    F'9'                    CHARACTERS PER LINE
PRTLINE  DS    0CL133
CC       DC    C' '                    CARRIAGE CONTROL CHARACTER
OUTPUT   DC    CL132' '                PRINTLINE
*
         LTORG ,                       FORCE OUT LITERAL POOL.
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBM,  X
               BLKSIZE=1330
         END
