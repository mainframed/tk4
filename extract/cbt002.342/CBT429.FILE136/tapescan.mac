 TITLE 'TAPESCAN 3.5  -  IRSS TAPE SCANNING, ANALYSIS, COPYING UTILITY'
TAPESCAN CSECT
***********************************************************************
* THIS PROGRAM, CALLED TAPESCAN, WAS WRITTEN BY WILL DALAND, SOCIAL
* SCIENCE STATISTICAL LABORATORY, INSTITUTE FOR RESEARCH IN SOCIAL
* SCIENCE, UNIVERSITY OF NORTH CAROLINA AT CHAPEL HILL, MARCH 1974.
* SINCE PROGRAM FOR TAPESCAN WAS PUBLICALLY FUNDED, PERMISSION IS
* GRANTED FOR ANYONE TO USE THIS PROGRAM IN WHOLE OR IN PART.  IT
* IS REQUESTED THAT WHEN DOING SO YOU GIVE CREDIT (PREFERABLY BOTH
* IN SOURCE AND DOCUMENTATION) TO WILL DALAND, IRSS, UNC@CH.
* VERSION 3.1 INCLUDES EXPIRATION DATE, AVERAGE BLOCK SIZES, AND VTOC
* LISTING AND WAS PRODUCED BY C. WRANDLE BARTH, GODDARD SPACE FLIGHT
* CENTER, JANUARY 1975.
*
* THIS PROGRAM WAS MODIFIED FOR MVS, JUNE 1978 BY:
*  STEVE R. HAGGERTY
*  GTE DATA SERVICES
*  MARINA DEL REY, CA 90291
*  (213) 821-0511 EXT. 285
*
*  INSTALLED AT UNIONBANC COMPUTER CORPORATION ON 08/25/78
*  BY HOWARD DEAN (TECHNICAL SERVICES). IF ANY PROBLEMS WITH
*  THIS PROGRAM ARE ENCOUNTERED, PLEASE CONTACT:
*
*   HOWARD M. DEAN
*   UNION BANK COMPUTER CORPORATION
*   TECHNICAL SERVICES (8TH FLOOR)
*   605 W. OLYMPIC BLVD.
*   LOS ANGELES, CA. 90015
*   PHONE - (213) 687-5719
*
*   WILLIAM J. SMITH
*   SYNTEX (USA), INC.
*   3401 HILLVIEW AVENUE
*   MS A4-CIS
*   PALO ALTO, CA.  94304
*   PHONE - (415) 852-1638
*
*   LAST ONE TO WORK ON THIS CODE:                             HD DEC86
*                                                              HD DEC86
*   HOWARD M. DEAN                                             HD DEC86
*   SR. SYSTEMS SPECIALIST                                     HD DEC86
*   AMERICAN PRESIDENT LINES                                   HD DEC86
*   #3 WATERS PARK DRIVE                                       HD DEC86
*   SUITE 115                                                  HD DEC86
*   SAN MATEO, CA 94403                                        HD DEC86
*   (415) 570-2331                                             HD DEC86
*                                                              HD DEC86
* VERSION 3.2 CORRECTED VARIOUS BUGS.
* VERSION 3.3 CHANGED OUTPUT TAPE HANDLING TO USE ONLY EXCP.
* VERSION 3.4 CORRECTED FOR USE UNDER MVS REL. 3.7F      *GTEDS LA*SRH*
* VERSION 3.5 CORRECT BUGS AND ADD LINECNT PARAMETER     *GTEDS LA*HMD*
* VERSION 4.0 REMOVE 7-TRK CODE AND SUPPORT FOR 3480 CARTS     HD DEC86
* VERSION 5.0 COMPLETE SUPPORT FOR 3480 CARTRIDGE TAPES        HD JAN89
         EJECT
***** H I S T O R Y    L O G **********
*  08/29/78 - HOWARD M. DEAN
*  A.  INSTALLED AT UNIONBANC COMPUTER
*  B.  FINISH SUPPORT FOR 556 BPI TAPE (REMOVED BY S. HAGGERTY)
*      SEE COMMENT AROUND LABEL TRY556. IF DENSITY IS NOT
*      FOUND, THIS IS THE DEFAULT
*
*  09/15/78 - HOWARD M. DEAN
*   A.  DENSITY PRINTOUT WRONG ON FIRST PAGE OF LISTING. CHANGED
*       COPY ROUTINE TO ISSUE SENSE CHANNEL COMMAND BEFORE PRINTING
*       FIRST HEADING SO DENSITY WILL BE CORRECT.
*
*   B.  ADD ROUTINE TO CHECK FOR MAGNETIC TAPE DEVICES ON BOTH
*       INPUT AND OUTPUT, FOR IDIOTS THAT LIKE TO PLAY AROUND
*       WITH OTHER PEOPLES OVERSIGHTS.
*
*   C.  ADD ROUTINE TO CHECK FOR PRINTABLE OUTPUT VOLSER. IF THE
*       VOLSER FROM THE JFCB IS PRINTABLE, IT IS PRINTED. IF NOT,
*       THE UCB IS CHECKED FOR A VOLSER. IF THAT IS PRINTABLE IT
*       IS PRINTED. IF NOT, THE NAME OUTVOL IS USED. THE DCB VOLSER
*       OR JFCB VOLSER, WHICHEVER IS VALID IS COMPARED TO THE OUTPUT
*       TAPE LABEL TO DETERMINE IF THE LABEL IS VALID.
*       ** H. DEAN  09/78 **
*
*  10/30/78 - HOWARD DEAN
*   A.  ADD ROUTINE TO PRINT ERROR MESSAGE IF EXPDT OR
*       CREDT IN HDR1 OR EOF1 LABEL IS INVALID.
*
*   B.  INDICATE STD TRTCH ON VTOC INSTEAD OF BLANKS.
*
*   C.  FIX UNPK INSTRUCTION SO TOTAL COUNT OF BLOCKS READ
*       WOULD NOT BE TRUNCATED. ADDED 3 DIGITS TO LABEL BYTES.
*
*  11/22/78 - HOWARD DEAN
*   A. MOVE DENSITY SENSE ROUTINE TO AVOID ERROR IN SENSE
*      PROCESSING FOR 6250 BPI TAPES.
*
*   B. SET SENSEBYTS INITIALLY TO 3XL8'00'
*
*  01/23/79 - HOWARD DEAN
*   A. FIX DENSITY PRINT ON FIRST PAGE OF COPY LISTING BY ISSUING
*      READ COMMAND TO SET MODE BEFORE OUTPUT PROCESSING
*  04/09/79 - HOWARD DEAN
*   A. MAKE LINE COUNT A SYMBOLIC PARAMETER
*   B. MAKE "LINE" AN EXEC PARAMETER
*
*  01/09/84 - WILLIAM SMITH, SYNTEX (USA), INC., PALO ALTO, CA. 94304
*   A. CHANGED NAME TO "SYNTEX" FOR OBVIOUS REASONS
*   B. EXECUTED UNDER MVS/SP 1.3.3 PUT 8308 WITH UCC/1 4.7 AND
*      CGA'S "TOP SECRET" SECURITY SYSTEM
*   C. CREDIT IS HEREBY GIVEN TO A PERSONAL FRIEND, C. WRANDLE BARTH,
*      FORMERLY OF NASA GODDARD SPACE FLIGHT CENTER, FOR THE WORK HE
*      DID TO ENHANCE TAPESCAN IN THE MID 1970'S UNDER OS/MVT;  ALL
*      CHANGES IMPLEMENTED BY RANDY ARE FLAGGED -CWB-
*
*  08/23/86 - HOWARD M. DEAN, SYNTEX (USA), INC. PALO ALTO, CA 94304
*   A. FIX PAGE EJECT ROUTINE IN 'VTOC' LISTING TO CORRECTLY
*      SPACE WHEN A MULTIPLE PAGE VTOC IS PRINTED.
*
*  12/24/86 - HOWARD M. DEAN, AMER PRES LINES, SAN MATEO, CA 94403
*   A. REMOVE ALL VESTIGES OF SEVEN TRACK TAPE SUPPORT
*   B. ADD MINIMAL SUPPORT FOR 3480 TAPE CARDTRIDGES.
*   C. USE SYSTEM MACROS WHERE POSSIBLE INSTEAD OF HARDCODED VALUES
*
*  11/04/88 - HOWARD M. DEAN, AMER PRES LINES, SAN MATEO, CA 94403
*   A. ADD SUPPORT FOR 3480 TAPE CARTRIDGES - MAKE IT WORK
*
*  01/09/89 - HOWARD M. DEAN, AMER PRES LINES, SAN MATEO, CA 94403
*   A. FIX 3480 SUPPORT.
*   B. ADD OPTIONS LIST AT BEGINNING OF INVOCATION             HD JAN89
*
*  04/28/89 - HOWARD M. DEAN, AMER PRES LINES, SAN MATEO, CA 94403
*   A. FIX BUG IN OPTIONS LIST WITH "NOVOLSER" OPTION
*
**** NOTE: THIS PROGRAM MUST BE LINKED AUTHORIZED FOR MVS
****       OPERATION. THIS IS NOT A REQUIREMENT IN ANOTHER
****       OPERATING SYSTEM. ** HMD 05/79 **
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                     OPTIONS IN EFFECT MACRO                         *
*                                                                     *
***********************************************************************
         MACRO
         OPTN  &C,&CONST,&VALUE
         LCLA  &N,&P,&P1
         LCLC  &X,&Y,&Z                                        HD JAN89
         LCLC  &SANSNO                                         HD JAN89
&N       SETA  K'&CONST+1
&P       SETA  &N-2                                            HD JAN89
&W       SETC  'W'.'&SYSNDX'                                   HD JAN89
&X       SETC  'X'.'&SYSNDX'                                   HD JAN89
&Y       SETC  'Y'.'&SYSNDX'                                   HD JAN89
&Z       SETC  'Z'.'&SYSNDX'                                   HD JAN89
         AIF   (K'&C EQ 0).SKIP
.* ASSUME &CONST BEGINS WITH 'NO' IF &C IS NOT EQUAL TO NULLS  HD JAN89
&SANSNO  SETC  '&CONST'(3,&P)                                  HD JAN89
         B&C   &X                       NEGATIVE BRANCH        HD JAN89
         B     &Y                       POSITIVE BRANCH        HD JAN89
         AGO   .NONUM                                          HD JAN89
.SKIP    ANOP                                                  HD JAN89
         AIF   (K'&VALUE EQ 0).ERR01
         MVC   0(&N,R2),=C',&CONST'
         L     R1,&VALUE
         L     R15,=A(DBLWORK)          GET WORK AREA ADDRESS  HD JAN89
         CVD   R1,0(R15)                CONVERT IN WORK AREA   HD JAN89
         OI    7(R15),X'0F'             FIX SIGN
         UNPK  &N.(3,R2),6(2,R15)       UNPACK
&N       SETA  &N+3                     INCREMENT PAST NUMBER  HD JAN89
         LA    R2,&N.(R2)               INCREMENT POINTER      HD JAN89
&W       DS    0H                                              HD JAN89
         AGO   .MEND                                           HD JAN89
.NONUM   ANOP                                                  HD JAN89
&X       DS    0H
         MVC   0(&N,R2),=C',&CONST'     MOVE NEGATIVE PARM     HD JAN89
         LA    R2,&N.(R2)               INCREMENT POINTER      HD JAN89
         B     &Z                       EXIT                   HD JAN89
&Y       DS    0H                                              HD JAN89
         MVC   0(&P,R2),=C',&SANSNO'    MOVE POSITIVE PARM     HD JAN89
         LA    R2,&P.(R2)               INCREMENT POINTER      HD JAN89
         B     &Z                       EXIT                   HD JAN89
&Z       DS    0H                                              HD JAN89
         AGO   .MEND                                           HD JAN89
.ERR01   MNOTE 12,'VALUE MUST BE SPEIFIED FOR NUMERIC VARIABLE'
         MEXIT                                                 HD JAN89
.MEND    ANOP                                                  HD JAN89
         MEND
         EJECT
***********************************************************************
*                                                                     *
*                     INITIALIZATION ROUTINES                         *
*                                                                     *
***********************************************************************
         LCLA  &LINECNT                                    *HMD 04/79*
&LINECNT SETA  57                                          *HMD 04/79*
MSECT    DSECT
TRT1     DS    32D
BLANKBUF DS    CL136              BLANKS FOR BLANKING MSGBUF FAST
TRT2     DS    32D
MSGBUF   DS    CL136
RECBUF   DS    CL136              MINIMUM ALLOC, MAX ALLOC = 32K
HOLDBUF  DS    CL136                                           HD AUG86
         EJECT
TAPESCAN CSECT
R0       EQU   0   WORK REG + SYSTEM USES
R1       EQU   1   WORK REG + SYSTEM USES
R2       EQU   2   MOSTLY FOR LINKAGE TO PUTLINE & GETNUM
R3       EQU   3   USED IN PARM SCANNER + VTOC ENTRY POINTER.     -CWB-
R4       EQU   4   BELOW LABEL 'PROCESS' COUNTS BLKS READ FOLLOWING A  *
                   TAPEMARK OR THE LOAD POINT.
R5       EQU   5   WORK, INTERNAL SUBROUTINE LINKAGE
R6       EQU   6   WORK REG
R7       EQU   7   WORK REG + LENGTH OF LAST BLK READ
R8       EQU   8   PARM FIELD LENGTH CTR, TOTAL BYTES ON TAPE CTR
R9       EQU   9   BASE REG FOR DSECT MSECT
R10      EQU   10  FREE UNUSED REG
R11      EQU   11  2ND BASE REG
R12      EQU   12  1ST BASE REG
R13      EQU   13
R14      EQU   14  WORK REG + SYSTEM USES
R15      EQU   15  WORK REG + SYSTEM USES
IOBECBPT EQU   4
IOBSTART EQU   16
DEBUCBAD EQU   32
         EJECT
TAPESCAN CSECT                                              -HMD-
         USING *,R12              BASE FOR INITIALIZATION ONLY
         STM   14,12,12(13)
         LR    R12,R15            R12 = TEMP BASE FOR INITIALIZATION
         L     R11,=A(EXIT)       PERMANENT BASE FOR MAIN AND COMMON
         USING EXIT,R11
         LA    R9,SAVE
         ST    R9,8(R13)
         ST    13,SAVE+4
         LR    R13,R9
         L     R1,0(R1)           GET PTR TO PARM FIELD
         LH    R8,0(R1)           LOAD PARM FIELD LENGTH
         LA    R3,2(R1)           SET PTR TO PARM FIELD CHAR STRING
         LA    R9,SRCHPRM         LOAD TEMPORARY BASE FOR MSECT DSECT
         USING MSECT,R9           PERMANENT DECLARATION FOR MSECT BASE
         OPEN  (SYSPRINT,OUTPUT)
         TM    SYSPRINT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    EXITRC8
         GETMAIN VU,LA=GMCTRL,A=GMLOCS
         L     R9,GMLOCS          THERE IS AT LEAST THE MINIMUM CORE
*              ABOVE STMT SETS UP PERMANENT BASE FOR MSECT DSECT
*        MVI   BLANKBUF,C' '           (POSTPONE TIL AFTER CLEAR) -CWB-
*        MVC   BLANKBUF+1(135),BLANKBUF                           -CWB-
         LR    R6,R9              START CLEARING GOTTEN MAIN TO        *
                                  SHORTEN POSSIBLE DUMPS.
         L     R4,GMLOCS+4        ACTUAL LENGTH OF GOTTEN MAIN
         LA    R5,256             OFT-USED CONSTANT FOR CLEARING MAIN
CLOOP    CR    R4,R5              R5 CONTAINS F'256'
         BNH   LE256              BIF ONLY 256 OR FEWER BYTES LEFT
         XC    0(256,R6),0(R6)    CLEAR 256 BYTES
         SR    R4,R5              R5 CONTAINS F'256'
         AR    R6,R5              R5 CONTAINS F'256'
         B     CLOOP
LE256    BCTR  R4,0               SET TO MACHINE LENGTH
         EX    R4,XCLEAR          CLEAR LAST 1 TO 256 BYTES
XCLEAR   XC    0(0,R6),0(R6)      EXECUTED IN STMT ABOVE
         MVI   BLANKBUF,C' '           CLEAR BLANK AREA.          -CWB-
         MVC   BLANKBUF+1(135),BLANKBUF                           -CWB-
         LA    R1,MSGBUF+46       FOR ADDRESS CONSTANT IN DYNAMIC -CWB-
         ST    R1,AMSGBP46        STORAGE.                        -CWB-
         LA    R1,MSGBUF+72       SECOND ADCON.                   -CWB-
         ST    R1,AMSGBP72                                        -CWB-
         LA    R15,RECBUF         POINT THE WRITE
         O     R15,WRTCMND        CCW TO DYNAMICALLY
         ST    R15,WRTCMND        ALLOCATED RECORD BUFFER.
         LA    R15,RECBUF         SET FILE SEARCH CCW TO
         O     R15,READ81CM       POINT TO
         ST    R15,READ81CM
         LA    R15,RECBUF         INSERT POINTER IN               -CWB-
         O     R15,READCMND            READ COMMAND.              -CWB-
         ST    R15,READCMND                                       -CWB-
         LR    R5,R9              PTR TO TRT1
         LA    R6,16
         LA    R7,CTABLE
SETUPTR1 MVC   0(1,5),0(7)        MOVE SEED CHAR INTO TRT1
         MVC   1(15,5),0(5)       AND PROPAGATE IT
         LA    5,16(5)
         LA    7,1(7)
         BCT   6,SETUPTR1
         MVC   TRT2(16),CTABLE    MOVE 16 CHAR SEED INTO TRT2
         MVC   TRT2+16(240),TRT2  AND PROPAGATE IT 15 TIMES
         TIME  DEC                GET DATE IN R1 IN FORM 00YYJJJF
         ST    R0,BADLNGTH        SAVE TIME WHILE YOU'RE AT IT
         LR    R2,R1              PUT IN R2 TO PASS TO DATE SUBROUTINE
         LA    R1,=A(DATERSLT)    PTR TO PTR TO DATE'S RESULT FIELD
         L     R15,=V(DATE)       IRSS DATE SUBROUTINE (DATECONV)
         BALR  R14,R15            CONVERT 00YYJJJF TO MM/DD/YY
         ED    TIMERSLT,BADLNGTH  EDIT INTO PAGE HEADER LINE BUFFER
*        SR    R0,R0              SET TO AVOID RESERVING ANY LINES
*        BAL   R2,PAGECHK         PRINT PAGE HEADER
         DEVTYPE INPUT+40,DTYPE   CHECK TO SEE IF TAPE DEVICE     -HMD-
         LTR   R15,R15            DID WE FIND DD STATEMENT?       -HMD-
         BNZ   NOINPUT            NOPE                            -HMD-
         CLI   DTYPE+2,X'80'      IS THIS A TAPE DEVICE?          -HMD-
         BNE   NOTTAPEI           NOPE                            -HMD-
         RDJFCB (INPUT)           READ INPUT TAPE'S JFCB
         LTR   R15,R15            SEE IF DD STATEMENT THERE
         BNE   NOINPUT            EXIT WITH ERR MSG IF NOT
         MVC   DDVOL,JFCBINX+(JFCBVOLS-JFCB) PUT VOL INTO PAGE HEAD.
         MVC   JFCLTSV,JFCBINX+(JFCBLTYP-JFCB) SAVE INPUT LABEL TYPE
         TM    JFCLTSV,X'01'      SEE IF NL OR LTM SPECIFIED
         BO    *+8                IF IT WAS THEN LEAVE IT AS IT IS
         MVI   JFCBINX+(JFCBLTYP-JFCB),JFCBLP ELSE SET TO BLP
         MVC   JFCBINX+JFCBFLSQ-JFCB(2),=H'1' SET FILE SEQ. NO. TO = 1
         OPEN  (INPUT,INOUT),TYPE=J                            HD OCT88
         TM    INPUT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    NOINPUT            TERMINAL ERROR
         L     R1,INPUT+(DCBEODAD-IHADCB) GET CORRECT EODAD       -HMD-
         ST    R1,EODADDR         SAVE FOR EXCP ROUTINE           -HMD-
         L     R1,GMCTRL+4        LOAD MAXIMUM REQUESTED GETMAIN LENGTH
         S     R1,GMLOCS+4        SUBTRACT LENGTH ACTUALLY GOTTEN
         BZ    SENSLDPT           BIF GOT ALL CORE REQUESTED
         CVD   R1,BADLNGTH        CONVERT DIFERENCE TO PACKED DECIMAL
         UNPK  MORECORE+33(5),BADLNGTH
         OI    MORECORE+37,C'0'
         LH    R2,INPUT+(DCBBLKSI-IHADCB) TO PREVENT DATA 'OVERRUNS'
         SR    R2,R1              SUBTRACT DIFFERENCE OF MORE CORE
         STH   R2,INPUT+(DCBBLKSI-IHADCB) AND PUT BACK IN DCB
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'MORECORE),MORECORE
SENSLDPT LA    R1,=AL3(SENSCMND)  SET UP PTR FOR EXECEXCP CALL
         LA    R2,INPUT           PTR TO DCB  FOR EXCP CALL
         BAL   R4,EXECEXCP        CALL EXCP SUBROUTINE
         TM    SENSBYTS+1,X'08'   SEE IF LOAD POINT SENSED
         BO    SENSTYP            BIF LOAD POINT SENSED           -HMD-
         LA    R1,=AL3(RWNDCMND)  REWIND AND RE-SENSE COMMAND CHAIN
         LA    R2,INPUT           PTR TO DCB  FOR EXCP CALL
         BAL   R4,EXECEXCP
         TM    SENSBYTS+1,X'08'   SEE IF NOW AT LOAD POINT
         BO    SENSTYP            BIF AT LOAD POINT               -HMD-
LDPTERR  BAL   R2,PUTLINE         PRINT ERROR MESSAGE             -HMD-
         MVC   MSGBUF(66),=C'0UNABLE TO REWIND INPUT TAPE TO LOAD POINT*
                - TERMINATING EXECUTION'
         B     EXITRC8
SENSTYP  DS    0H                 SENSE DENSITY OF TAPE           -HMD-
         LA    R1,HDEOD           FAKE EOD ADDRESS                -HMD-
         ST    R1,EODADDR         SAVE EOD ADDRESS FOR EXECEXCP   -HMD-
         LA    R1,=AL3(READCMND)  READ TO SET UP SENSE BYTES      -HMD-
         LA    R2,INPUT           POINT TO DCB                    -HMD-
         BAL   R4,EXECEXCP        GO DO EXCP                      -HMD-
HDEOD    LA    R1,=AL3(SENSCMND)  REWIND TO LOAD POINT            -HMD-
         LA    R2,INPUT           POINT TO INPUT DCB              -HMD-
         XC    SENSBYTS(24),SENSBYTS                              -HMD-
         BAL   R4,EXECEXCP        ISSUE EXCP                      -HMD-
RESTREOD L     R1,INPUT+(DCBEODAD-IHADCB) GET EOD ADDRESS         -HMD-
         ST    R1,EODADDR         SAVE FOR FUTURE USE             -HMD-
         L     R2,(DCBDEBAD-IHADCB)+INPUT GET "DEB" ADDRESS    HD NOV86
         L     R2,DEBUCBAD(R2)    GET "UCB" ADDRESS            HD NOV86
         TM    UCBTBYT4-UCBOB(R2),UCB3480                      HD NOV86
         BO    HD3480             FLAG HEADER IF 3480 DEVICE   HD NOV86
         TM    SENSBYTS+3,4       P.E.= 1600 B.P.I?               -HMD-
         BZ    HD6250             NO, TRY 6250                    -HMD-
         MVI   TAPEDENS,C'3'      SET DENS FOR 1600 BPI           -HMD-
         MVC   PRTDENS(4),=CL4'1600'   SET PRINT DENS FOR 1600    -HMD-
         B     HDEND              HEADING END                     -HMD-
HD6250   L     R2,(DCBDEBAD-IHADCB)+INPUT GET INPUT DEB ADDR.     -HMD-
         L     R2,DEBUCBAD(R2)    GET UCB ADDRESS                 -HMD-
         TM    16(R2),2           UCBTYP = 6250?                  -HMD-
         BNO   HD800              NOPE, TRY 800 BPI               -HMD-
         MVI   TAPEDENS,C'4'      SET DEN FOR 6250 BPI            -HMD-
         MVC   PRTDENS(4),=CL4'6250'  SET PRINT FOR 6250          -HMD-
         B     HDEND                                              -HMD-
HD3480   MVI   TAPEDENS,C'5'      SELECT DENSITY=5             HD JAN89
         MVC   PRTDENS(4),=CL4' 38K'                           HD JAN89
         L     R1,=A(FLAG3480)    LOAD ADDRESS OF FLAG         HD NOV86
         MVI   0(R1),C'Y'         INDICATE 3480 DEVICE         HD NOV86
         B     HDEND              CONTINUE WITH PROGRAM        HD NOV86
HD800    TM    INPUT+(DCBDEN-IHADCB),B'10000011' 800 BPI?         -HMD-
         MVI   TAPEDENS,C'2'      SET DEN FOR 800 BPI             -HMD-
         MVC   PRTDENS(4),=CL4' 800'  SET PRINT FOR 800 BPI       -HMD-
         B     HDEND              END OF ROUTINE                  -HMD-
HDEND    DS    0H                 ENOUGH OF ALL THIS NONSENSE     -HMD-
         LA    R1,=AL3(RWNDCMND)  REWIND TO LOAD POINT            -HMD-
         LA    R2,INPUT           GET INPUT ADDRESS               -HMD-
         BAL   R4,EXECEXCP        GO DO IT                        -HMD-
REWOUND  TM    SENSBYTS+1,X'08'   ARE WE AT LOAD POINT NOW?       -HMD-
         BZ    LDPTERR            NO, INDICATE LOAD ERROR         -HMD-
SRCHPRM  LTR   R8,R8              LOAD AND TEST REMAINING PARM LENGTH
         BNH   ENDPARMS
         SR    R5,R5              ZERO CURRENT PARM LENGTH COUNTER
         LR    R4,R3              SAVE PTR TO START OF PARM
SRCHCOMA CLI   0(R3),C','
         LA    R3,1(R3)           BUMP PTR TO NEXT CHAR
         BE    GOTCOMMA
         LA    5,1(5)             COUNTS LENGTH OF CURRENT PARM
         BCT   R8,SRCHCOMA
GOTCOMMA CLC   0(6,R4),=C'NOLIST' GET HERE IF COMMA OR END OF PARM LIST
         BE    NOLIST
         CLC   0(4,4),=C'LIST'
         BE    LIST
         CLC   0(5,4),=C'MAXTM'   INITIALLY 32760           FJP/20FEB79
         BE    MAXTM
         CLC   0(6,4),=C'SKIPTM'
         BE    SKIPTM
         CLC   0(5,4),=C'NOHEX'
         BE    NOHEX
         CLC   0(6,4),=C'MAXMOV'
         BE    MAXEOV
         CLC   0(7,4),=C'SKIPEOV'
         BE    SKIPEOV
         CLC   0(9,4),=C'NOSUMMARY'
         BE    NOSUMARY
         CLC   0(5,4),=C'COUNT'
         BE    COUNT
         CLC   0(7,4),=C'NOCOUNT'
         BE    NOCOUNT
         CLC   0(6,4),=C'ERRLIM'
         BE    ERRLIM
         CLC   0(4,4),=C'COPY'
         BE    COPY               TAPE COPYING OPTION
         CLC   0(6,4),=C'EOVMOD'  MOD OPTION IMPLIES COPY & COUNT OPTNS
         BE    EOVMOD
         CLC   0(8,4),=C'NOVOLSER'
         BE    NOVOLSER
         CLC   0(3,4),=C'OPT'
         BE    OPT                MISCELLANEOUS OPTIONS
         CLC   0(4,4),=C'LINE'    LINE COUNT OPTION        *HMD 04/79*
         BE    LINE                                        *HMD 04/79*
         CLC   0(4,4),=C'VTOC'                                  MRX-JJJ
         BE    VTOCONLY                                         MRX-JJJ
UNRECOG  BAL   R2,PUTLINE
         MVC   MSGBUF(33),=C'0ERROR - UNRECOGNIZABLE PARAMETER'
PRLENERR BAL   2,PUTLINE
         MVC   MSGBUF(32),=C'0WARNING - INVALID PARM IGNORED.'
SRCHPARM BCTR  R8,0
         B     SRCHPRM
GETNUM   DS    0H                 CHAR STRNG INTGR TO BIN INTGR CONV SB
         SR    6,6                CLEAR ACCUMULATOR
         CLI   0(4),C'9'          * R4=PTR TO 1ST CHAR OF NUM         *
         BH    NUMERR             * R5=ACTUAL LENGTH OF NUM           *
         CLI   0(4),C'0'          * RESULT RETURNED IN R6             *
         BL    NUMERR             * R2,R4,R5,R6, AND R7 MODIFIED BY   *
         MH    R6,=H'10'          * USING GETNUM SUBROUTINE.          *
         IC    R7,0(4)            PICK UP DECIMAL CHARACTER
         SLL   7,28               CHOP OFF LEFT 4 BITS
         SRL   7,28               AND SHIFT BACK
         AR    6,7                ADD DIGIT INTO RESULT
         LA    4,1(4)             BUMP PTR TO NEXT CHAR
         BCT   5,GETNUM+2         GO TO TOP OF LOOP
         BR    2                  RETURN FROM GETNUM SUBROUTINE
NUMERR   BAL   R2,PUTLINE         ERROR DESCRIPTOR SUBROUTINE
         MVC   MSGBUF(50),=C'0ERROR - PARAMETER HAS INVALID NUMERICAL C*
               OMPONENT'
         B     PRLENERR
NOCOUNT  CH    R5,=H'7'
         BNE   PRLENERR           *** WARNING *** MODIFIED IN COPY/MOD *
                                                  OPTION ROUTINES.
         MVI   COUNTFLG,C'N'
         B     SRCHPARM
COUNT    CH    R5,=H'5'           FINAL PROCESSING OF COUNT OPTION
         BNE   UNRECOG
         MVI   COUNTFLG,C'Y'
         B     SRCHPARM
NOVOLSER CH    R5,=H'8'
         BNE   UNRECOG
         MVI   SVOUTFLG,C'Y'      SET 'NOVOLSER SPECIFIED' FLAG
         TM    JFCLTSV,X'31'      SEE IF INPUT HAD LABEL TYPE OF NL,
         BNZ   SRCHPARM           BLP, OR LTM, AND BIF SO.
         OI    WRTFLG,X'04'       OR IN 'DO NOT COPY INPUT VOL LABEL'
         B     SRCHPARM
COPY     CH    R5,=H'4'
         BNE   UNRECOG
         OI    COPYFLG,X'01'      INDICATE COPY OPTION SPECIFIED
         OI    WRTFLG,X'01'       INDICATE COPY   REQUESTED (THAT BIT)
         OI    NOCOUNT+5,X'F0'    NOP OUT NOCOUNT & SET FOR WARN MSG
         B     COUNT+8            COPY OPTION INVOKES COUNT AUTOMATICLY
EOVMOD   CH    R5,=H'6'           ADD DATASETS ONTO EOV
         BNE   UNRECOG
         OI    COPYFLG,X'03'      BITS = 'COPY OPT + MOD OPT REQUESTED'
         B     COPY+8             MOD OPTION IMPLIES COPY OPTION
OPT      CH    R5,=H'3'
         BNH   PRLENERR
         LA    R4,4(R4)
         SH    R5,=H'4'
         BAL   R2,GETNUM
         ST    R6,OPTNO
         B     SRCHPARM
ERRLIM   CH    R5,=H'5'           CHANGE SYNAD ERROR COUNT LIMIT
         BNH   PRLENERR
         LA    R4,6(R4)
         SH    R5,=H'6'
         BAL   R2,GETNUM
         ST    R6,SYNADNO
         B     SRCHPARM
NOLIST   CH    R5,=H'6'           FINAL PROCESSING OF NOLIST PARM
         BNE   UNRECOG
         SR    R0,R0
         ST    R0,LISTNO
         B     SRCHPARM
LIST     CH    R5,=H'4'
         BNH   PRLENERR
         LA    R4,4(R4)
         SH    R5,=H'4'           GET ACTUAL LENGTH OF PRESUMED NUMBER
         BAL   R2,GETNUM          GET PRESUMED NUMBER INTO BINARY FORM
         ST    R6,LISTNO          STORE NONNEGATIVE BINARY INTEGER
         B     SRCHPARM
NOHEX    CH    R5,=H'5'
         BNE   UNRECOG
         MVI   HEXFLG,C'N'
         B     SRCHPARM
VTOCONLY CH    R5,=H'4'                                         MRX-JJJ
         BNE   UNRECOG                                          MRX-JJJ
         MVI   VTOCFLAG,C'Y'      SET VTOC ONLY FLAG           HD OCT88
         B     SRCHPARM                                         MRX-JJJ
NOSUMARY CH    R5,=H'9'
         BNE   UNRECOG
         MVI   SUMFLG,C'N'        SET SUMMARY FLAG TO 'NOSUMMARY'
         LA    R0,1               READ MINIMUM OF 1 BLK AFTER A TAPEMRK
         ST    R0,READNO          SET # OF BLKS TO READ FOR SUMMARY=0
         B     SRCHPARM
SKIPTM   CH    R5,=H'6'
         BNH   PRLENERR
         LA    R4,6(R4)
         SH    R5,=H'6'
         BAL   R2,GETNUM
         ST    R6,SKIPTMNO
         B     SRCHPARM
LINE     CH    R5,=H'4'            IS THIS PARM 'LINE'?    *HMD 04/79*
         BNH   PRLENERR            NOPE, LENGTH ERROR      *HMD 04/79*
         LA    R4,4(R4)            BUMP POINTER            *HMD 04/79*
         SH    R5,=H'4'            DECREMENT COUNTER       *HMD 04/79*
         BAL   R2,GETNUM           GET NUMERIC VALUE       *HMD 04/79*
         CH    R6,=H'30'           TOO LOW?                *HMD 04/79*
         BL    NUMERR              YES, FORGET IT          *HMD 04/79*
         CH    R6,=H'99'           TOO HIGH?               *HMD 04/79*
         BH    NUMERR              YES, FORGET IT          *HMD 04/79*
         ST    R6,LINECNT          SAVE LINE COUNT PARM    *HMD 04/79*
         B     SRCHPARM            GET SOME MORE PARMS     *HMD 04/79*
MAXTM    CH    R5,=H'5'
         BNH   PRLENERR
         LA    R4,5(4)
         SH    R5,=H'5'
         BAL   R2,GETNUM
         LTR   R6,R6              MAKE SURE MAXTM IS NOT =0
         BZ    NUMERR             ERROR - INVALID NUMERICAL PARM
         ST    R6,MAXTMNO
         B     SRCHPARM
SKIPEOV  CH    R5,=H'7'
         BNH   PRLENERR
         LA    R4,7(R4)
         SH    R5,=H'7'
         BAL   R2,GETNUM
         ST    R6,SKPEOVNO
         B     SRCHPARM
MAXEOV   CH    R5,=H'6'           CHECK LENGTH OF PARM
         BNH   PRLENERR           LENGTH MUST BE GREATER THAN 6
         LA    R4,6(R4)           BUMP PTR TO START OF PARM'S NUMBER
         SH    R5,=H'6'           GET ACTUAL LENGTH OF PRESUMED NUMBER
         BAL   R2,GETNUM          CONVERT NUMBER FOLLOWING 'MAXEOV' PRM
         LTR   R6,R6              MAKE SURE MAXEOV IS NOT=0
         BZ    NUMERR             INVALID NUMERICAL COMPONENT
         ST    R6,MAXEOVNO        STORE RESULT
         B     SRCHPARM
ENDPARMS DS    0H
         CLC   SKPEOVNO,MAXEOVNO  PARM VALIDITY CHECKING
         BL    *+26               SKIP IF NO ERROR
         BAL   R2,PUTLINE
         MVC   MSGBUF(48),=C'0SKIPEOV PARM GE MAXEOV; SKIPEOV SET TO MA*
               XEOV-1'
         L     R2,MAXEOVNO
         SH    R2,=H'1'
         ST    R2,SKPEOVNO        STORE IT SET TO MAXEOV-1
         CLC   SKIPTMNO,MAXTMNO
         BL    *+26               SKIP IF SKIPTMNO & MAXTMNO CONSISTENT
         BAL   R2,PUTLINE         PRINT ERROR MESSAGE
         MVC   MSGBUF(44),=C'0SKIPTM PARM GE MAXTM; SKIPTM SET TO MAXTM*
               -1'
         L     R2,MAXTMNO         MAXIMUM ON TAPEMARKS TO BE READ
         SH    R2,=H'1'
         ST    R2,SKIPTMNO        STORE AS MAXTM-1
         CLC   SKPEOVNO,=F'0'     SPECIFIED?
         BE    ENDP010            NO, ITS OK
         CLC   SKIPTMNO,=F'0'     SPECIFIED?
         BE    ENDP010             NO, ITS OK
         BAL   R2,PUTLINE         BOTH SPECIFIED, ERROR
         MVC   MSGBUF(52),=C'0BOTH SKIPTM AND SKIPEOV SPECIFIED; SKIPEO+
               V IGNORED.'        PRINT ERROR MESSAGE
         MVC   SKPEOVNO,=F'0'
         EJECT
***********************************************************************
*                                                                     *
*                       PRINT OPTIONS IN EFFECT                       *
*                                                                     *
***********************************************************************
ENDP010  DS    0H                                              HD OCT88
         MVC   MSGBUF,BLANKBUF    PRINT A BLANK LINE           HD JAN89
         BAL   R2,PUTLINE3                                     HD JAN89
         DC    C'OPT111'                                       HD JAN89
         SPACE 2                                               HD JAN89
         MVC   MSGBUF(19),=C' OPTIONS IN EFFECT:'
         LA    R2,MSGBUF+19       R2=NEXT POSITION TO RECEIVE PARM
         OPTN  ,LIST,LISTNO                                    HD JAN89
         CLI   HEXFLG,C'N'                                     HD JAN89
         OPTN  E,NOHEX                                         HD JAN89
         CLI   COUNTFLG,C'N'                                   HD JAN89
         OPTN  E,NOCOUNT                                       HD JAN89
         CLC   READNO,=F'1'                                    HD JAN89
         OPTN  E,NOSUMMARY                                     HD JAN89
         CLI   COPYFLG,0                                       HD JAN89
         OPTN  E,NOCOPY                                        HD JAN89
         TM    COPYFLG,X'02'                                   HD JAN89
         OPTN  NO,NOEOVMOD                                     HD JAN89
         CLI   SVOUTFLG,C'Y'                                   HD APR89
         OPTN  E,NOVOLSER                                      HD JAN89
         MVI   MSGBUF+19,C' '    BLANK OUT FIRST COMMA         HD JAN89
         BAL   R2,PUTLINE3       PRINT FIRST OPTION SET        HD JAN89
         NOP   FWY101            POINT TO                      HD JAN89
         NOPR  0                    BRANCH AROUND              HD JAN89
         SPACE 2                                               HD JAN89
         MVC   MSGBUF,BLANKBUF   CLEAR BUFFER FAST             HD JAN89
         MVC   MSGBUF(19),=C' OPTIONS IN EFFECT:'
         LA    R2,MSGBUF+19       R2=NEXT POSITION TO RECEIVE PARM
         OPTN  ,SKIPEOV,SKPEOVNO                               HD JAN89
         OPTN  ,SKIPTM,SKIPTMNO                                HD JAN89
         OPTN  ,MAXEOV,MAXEOVNO                                HD JAN89
         OPTN  ,MAXTM,MAXTMNO                                  HD JAN89
         OPTN  ,OPT,OPTNO                                      HD JAN89
         CLI   VTOCFLAG,C'N'                                   HD JAN89
         OPTN  E,NOVTOCONLY                                    HD JAN89
         MVI   MSGBUF+19,C' '     BLANK OUT FIRST COMMA
         BAL   R2,PUTLINE3
         DC    C'OPT222'                                       HD JAN89
         SPACE 2                                               HD JAN89
FWY101   DS    0H                                              HD JAN89
         TM    COPYFLG,X'01'      SEE IF COPY AND/OR MOD SPECIFIED
         BZ    NOTBOTH            BIF COPY OPTION NOT SPECIFIEDHD JAN89
         MVI   COUNTFLG,C'Y'      TURN COUNT OPTION ON         HD OCT88
         MVI   SVOUTFLG,C'N'      TURN "NOVOLSER" OFF          HD OCT88
***************************************************************HD OCT88
**                                                             HD OCT88
**   CHECK OUTPUT TAPE, IF ANY FOR VALID DEVICE TYPE, ETC..    HD OCT88
**                                                             HD OCT88
***************************************************************HD OCT88
OUTCHECK DS    0H                                              HD OCT88
         DEVTYPE OUTPUT+40,DTYPE  CHECK FOR MAG TAPE DEVICE       -HMD-
         LTR   R15,R15            IS DD STMT THERE?               -HMD-
         BNZ   NOOUTPUT           NOPE                            -HMD-
         CLI   DTYPE+2,X'80'      IS THIS A TAPE DEVICE?          -HMD-
         BNE   NOTTAPEO           NOPE                            -HMD-
         RDJFCB (OUTPUT)          ELSE PROCESS COPY AND/OR MOD OPTIONS
         LTR   R15,R15            SEE IF RDJFCB WENT ALLRIGHT
         BNE   NOOUTPUT           'MISSING OR INVALID DD' ERROR MSG
         MVC   SAVEVOL,JFCBOUT+(JFCBVOLS-JFCB) SAVE VOLSER        -HMD-
         NI    VOLSW,255-VOLSWNV          TURN OFF BIT            -HMD-
         L     R3,=A(EBCDTBL)             GET TABLE ADDR          -HMD-
         TRT   SAVEVOL(6),0(R3)           CHECK PRINTABLES        -HMD-
         BZ    @VALID                     BIF OK                  -HMD-
         OI    VOLSW,VOLSWNV              TURN ON SWITCH          -HMD-
         B     *+10                       DONT MOVE IN VOLSER     -HMD
@VALID   MVC   POSMSG+22(6),SAVEVOL       MOVE VOLSER TO HEADING  -HMD-
         MVC   OUTLTYP,JFCBOUT+(JFCBLTYP-JFCB)
         TM    OUTLTYP,X'01'      SEE IF LABEL IS NL OR LTM
         BO    *+8                BIF YES - IT IS NL OR LTM
         MVI   JFCBOUT+(JFCBLTYP-JFCB),JFCBLP ELSE SET TO BLP
         LA    R0,1
         LH    R2,JOUTFLSQ        SAVE ORIGINAL FILE SEQ. NO.
         LTR   R2,R2              BUT IF IT'S
         BNZ   *+6                EQUAL TO ZERO THEN
         LR    R2,R0              SET IT TO = 1.
         STH   R0,JOUTFLSQ        TEMPORARILY SET FLSQ IN JFCB TO = 1
         ST    R2,SVR2            SAVE R2 OVER TRT                -HMD-
         OPEN  (OUTPUT,INOUT),TYPE=J                           HD OCT88
         TM    OUTPUT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    NOOUTPUT
         TM    VOLSW,VOLSWNV      IS VOLSER PRINTABLE?            -HMD-
         BNO   @VOLOK             YES, WE TOOK CARE OF IT EARLIER -HMD-
         L     R2,(DCBDEBAD-IHADCB)+OUTPUT GET DEB ADDR           -HMD-
         L     R2,DEBUCBAD(R2)    GET UCB ADDR                    -HMD-
         MVC   SAVEVOL(6),28(R2)  SAVE VOLSER                     -HMD-
         L     R3,=A(EBCDTBL)     GET TABLE ADDR                  -HMD-
         TRT   SAVEVOL(6),0(R3)   SEE IF CHARS ARE PRINTABLE      -HMD-
         BNZ   @VOLOK             NOPE, THEY ARE NOT..LEAVE CONST.-HMD-
         MVC   POSMSG+22(6),SAVEVOL   MOVE IN MESSAGE             -HMD-
@VOLOK   DS    0H                 END OF PRINTABLE CHECK          -HMD-
         L     R2,SVR2            RESTORE R2 FROM SAVE AREA       -HMD-
         LA    R1,EXCPIOBP        POINT TO IOB BSAM-TYPE PREFIX.  -CWB-
         IC    R0,OUTPUT+(DCBIOBA-IHADCB) CHANGE DCB POINTER FROM EXCP-
         ST    R1,OUTPUT+(DCBIOBA-IHADCB) TYPE TO BSAM-TYPE POINTER
         STC   R0,OUTPUT+(DCBIOBA-IHADCB) FOR "EXECEXCP" CONSISTANCY
         STH   R2,JOUTFLSQ        RESTORE ORIGINAL FLSQ IN JFCB
         TM    OUTLTYP,X'42'      SEE IF AL, AUL, SL, OR SUL
         BZ    COPYOUT            BIF NOT ONE OF ABOVE
         LA    R1,=AL3(READCMND)  USE EXCP TO READ PRESUMED VOL   -CWB-
         LA    R2,OUTPUT               LABEL (CHANGED FROM BSAM   -CWB-
         BAL   R4,EXECEXCP             READING).                  -CWB-
         LH    R7,READLENG        GET LENGTH OF READ COMMAND.     -CWB-
         LH    R6,RESIDL          GET RESIDUAL LENGTH AFTER READ. -CWB-
         SR    R7,R6
         BNH   BADREC             VERY BAD IF RECORD LENGTH LT 1
         TM    OUTLTYP,X'02'      SEE IF SL OR SUL, OR AL OR AUL
         BO    COPYSL             BIF SL OR SUL
*COPYAL  CLC   RECBUF(4),=X'B6AFAC51'  SEE IF ANSI 'VOL1'
         BNE   BADOUTVL           AL OR AUL IN JCL, BUT LABEL NOT ANSI
         C     R7,=F'80'
         BL    BADOUTVL
         B     REWINDCK
COPYSL   C     R7,=F'80'          SEE IF PRESUMED IBM SL IS 80 BYTES
         BNE   BADOUTVL           BIF PRESUMED IBM LABEL NOT 80 BYTES
         CLC   RECBUF(4),=C'VOL1'
         BNE   BADOUTVL
         CLC   SAVEVOL,RECBUF+4             COMPARE VOL SERS     -HMD-
         BNE   BADOUTVL
REWINDCK CLC   JOUTFLSQ(2),=H'1'  DO NOT REWIND IF FILE SEQ. NO. GT 1
         BH    COPYOUT            DO NOT REWIND IF FILE SEQ. NO. GT 1
         TM    COPYFLG,X'02'      DO NOT REWIND IF EOVMOD SPECIFIED
         BO    COPYOUT            DO NOT REWIND IF EOVMOD SPECIFIED
         CLI   SVOUTFLG,C'Y'      DO NOT REWIND IF NOVOLSER SPECIFIED
         BE    COPYOUT            DO NOT REWIND IF NOVOLSER SPECIFIED
         LA    R1,=AL3(RWNDCMND)  REWIND COMMAND CHAINED TO SENSE CMND
         LA    R2,OUTPUT          OUTPUT DCB
         BAL   R4,EXECEXCP        REWIND AND SENSE
         TM    SENSBYTS+1,X'08'   SEE IF LOAD POINT SENSED
         BO    COPYOUT            BIF SENSED LOADPOINT
         BAL   R2,PUTLINE
         MVC   MSGBUF(67),=C'0UNABLE TO REWIND OUTPUT TAPE TO LOAD POIN*
               T - TERMINATING EXECUTION'
         B     EXITRC8
BADOUTVL BAL   R2,PUTLINE
         MVC   MSGBUF(32),=C'0OUTPUT VOLUME LABEL IS INVALID:'
         LR    R3,R12             PROVIDE PROPER ADDRESSABILITY.
         L     R12,=A(EXIT+4096)
         DROP  R12
         USING TAPESCAN,R3
         BAL   R5,LISTON
         B     EXITRC8
         LR    R12,R3             RESTORE ADDRESSABILITY.
         DROP  R3
         USING TAPESCAN,R12
COPYOUT  DS    0H                 IF NECESS., MOD PROCESSING DONE HERE
         LA    R0,EODADOUT        ADDRESS OF NEW OUTPUT TAPE EODAD RTN
         ST    R0,OUTPUT+(DCBEODAD-IHADCB) AND SET IT UP IN DCB.
FILELOOP TM    COPYFLG,X'02'      SEE IF MOD SPECIFIED
         BO    FILEFSM            IF SO THEN DO SRCH FWRD FOR TPMK
         CLC   COUTFILE,JOUTFLSQ  ARE WE IN RIGHT FILE YET?
         BNL   FILEMSG            BIF YES
FILEFSM  LA    R1,=AL3(FSMCMND)   FWRD SPACE JUST PAST NEXT TAPEMARK
         LA    R2,OUTPUT
         BAL   R4,EXECEXCP
         LA    R0,1               MAINTAIN
         AH    R0,COUTFILE        CURRENT FILE
         STH   R0,COUTFILE        NUMBER.
         CH    R0,JOUTFLSQ        BIF NOT YET UP TO SPECIFIED FILE
         BL    *+12               BIF NOT YET UP TO SPECIFIED FILE
         TM    COPYFLG,X'02'      SEE IF EOVMOD SPECIFIED
         BZ    FILEMSG            BIF   EOVMOD NOT SPECIFIED
         LA    R1,=AL3(READCMND)  PREPARE TO READ WHAT IMMEDIATELY-CWB-
         LA    R2,OUTPUT               FOLLOWS THE TAPE MARK.     -CWB-
         BAL   R4,EXECEXCP        (CHANGED TO EXCP FROM BSAM.)    -CWB-
         B     FILELOOP           ELSE IT'S A BLOCK, SO JUST GO ON
FILEPOS  DS    0H                 BACKSPACE PAST 2ND TAPEMARK OF EOV
         LA    R2,OUTPUT          PTR TO OUTPUT TAPE'S DCB
         LA    R1,=AL3(BSFCMND)   BACKSPACE PAST ONE TAPEMARK
         BAL   R4,EXECEXCP
         LH    R0,COUTFILE        AND
         BCTR  R0,0               REDUCE CURRENT FILE NO. APPROPRIATELY
         STH   R0,COUTFILE
FILEMSG  DS    0H                 PRINT INITIAL OUTPUT POSITION MSG
         LH    R1,COUTFILE
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  POSMSG+32(4),BADLNGTH TELL WHERE 1ST FILE OUTPUT GO-CWB-
         MVC   POSMSG+47(4),POSMSG+32 MOVE INTO EXPLANATORY COMMEN-CWB-
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'POSMSG),POSMSG
         B     NOTBOTH
EODADOUT LA    R0,1               COUNT
         AH    R0,COUTFILE        THIS
         STH   R0,COUTFILE        TAPEMARK (THE 2ND ONE OF AN EOV).
         CH    R0,JOUTFLSQ        SEE IF UP TO SPECIFIED TAPEMARK
         BL    TOPT456            BIF NOT UP TO SPECIFIED TAPEMARK
         TM    COPYFLG,X'02'      SEE IF EOVMOD SPECIFIED
         BO    FILEPOS            BIF EOVMOD SPECIFIED
         TM    OPTNO,X'10'        SEE IF 'IGNORE EOV'S UNTIL FLSQ SATIS
         BO    FILEPOS            BIF IT IS SPECIFIED
         B     BADEOV             GIVE 'EM HELL, HARRY!
*
NOTTAPEO MVC   IDNTAPE+1(6),=C'OUTPUT'   MOVE 'OUTPUT' TO MSG     -HMD-
NOTTAPEI BAL   R2,PUTLINE         PUT OUT LINE.                   -HMD-
         MVC   MSGBUF(L'IDNTAPE),IDNTAPE MOVE MSG TO BUFFER       -HMD-
         B     EXITRC8            RETURN WITH BAD CODE            -HMD-
*
TOPT456  TM    OPTNO,X'10'        TEST FOR OPT268435456
         BO    FILELOOP           AND IF SPECIFIED IGNORE DOUBLE TPMK
BADEOV   BAL   R2,PUTLINE
         MVC   MSGBUF(83),=C'0ERROR - EOV INDICATION ENCOUNTERED BEFORE*
                OUTPUT TAPE FINISHED INITIAL POSITIONING'
         B     EXITRC8
EODADBAD BAL   R2,PUTLINE
         MVC   MSGBUF(092),=C'0ERROR - OUTPUT TAPE HAD TAPEMARK FOLLOWI*
               NG LOAD POINT, BUT NEITHER LTM OR BLP WAS SPECIFIED'
*                                 (LENGTH FIELD CORRECTED.)       -CWB-
         B     EXITRC8
NOTBOTH  DS    0H
         L     R12,=A(EXIT+4096)  LOAD 2ND BASE REG FOR MAIN CODE
         DROP  R12                DROP R12 FOR COMMON STUFF, ONLY R11
         B     SKIPEOVP           INITIALIZATION ENDS HERE
         USING TAPESCAN,R12
NOOUTPUT MVC   BADINPUT+1(6),=C'OUTPUT'
NOINPUT  BAL   R2,PUTLINE         BAD OR MISSING DD ROUTINE
         MVC   MSGBUF(L'BADINPUT),BADINPUT
         B     EXITRC8
         DROP  R12
BSFCMND  CCW   X'2F',0,X'70',1    BSF, CC,SLI,SKIP
         CCW   X'04',SENSBYTS,X'20',24 SENSE SLI UP TO 24 BYTES
READ81CM CCW   2,0,X'60',81       SET TO POINT TO RECBUF DYNAMICALLY
         CCW   4,SENSBYTS,X'20',24 AND SENSE FOR DEBUG
JFCBOUT  DS    22D
JOUTFLSQ EQU   JFCBOUT+(JFCBFLSQ-JFCB)
EXLSTOUT DC    0F'0',X'87',AL3(JFCBOUT)
GMCTRL   DC    A(RECBUF+136-TRT1)  MINIMUM LENGTH FOR THE GETMAIN
         DC    A(RECBUF-TRT1+32768) MAXIMUM LENGTH FOR THE GETMAIN
GMLOCS   DC    2F'0'
DTYPE    DC    2F'0'              TO HOLD DEVTYPE INFO            -HMD-
SVR2     DC    F'0'               SAVE AREA FOR R2 OVER TRT       -HMD-
EXITLIST DS    0F                 INPUT DCB EXIT LIST FOR RDJFCB
         DC    X'87'              LAST ENTRY AND RDJFCB
         DC    AL3(JFCBINX)       BUFFER FOR INPUT TAPE'S JFCB
BADINPUT DC    C'0INPUT  DD STATEMENT MISSING OR INVALID'
IDNTAPE  DC    C'0INPUT DEVICE IS NOT MAGNETIC TAPE - EXECUTION TERMINAX
               TED '                                              -HMD-
POSMSG   DC    C'0FIRST OUTPUT FILE ON SCRTCH IS 0000 -- LABEL=(0000,BL*
               P)'                                                -CWB-
MORECORE DC    C'0WARNING:  TAPESCAN SHOULD HAVE 00000 MORE BYTES OF CO*
               RE FOR RELIABLE OPERATION; PROCESSING WILL BE ATTEMPTED *
               ANYWAY.'
CTABLE   DC    C'0123456789ABCDEF'
SAVEVOL  DC    CL6' '             SAVE AREA FOR OUTPUT VOLSER     -HMD-
SVOUTFLG DC    C'N'               C'Y' = 'SAVE OUTPUT VOL LABEL '
VERIFLG  DC    C'N'
OUTLTYP  DC    C'0'               FOR SAVING OUTPUT TAPE'S LABEL TYPE
COPYFLG  DC    X'00'              COPY AND MOD OPTION REQUEST BITS
VOLSW    DC    X'00'              SWITCH FOR ALPHANUMERIC TEST    -HMD-
VOLSWNV  EQU   X'80'              VOLSER NOT ALPHANUMERIC         -HMD-
         LTORG
         EJECT
* COMMON ROUTINES FOR BOTH INITIALIZATION AND MAIN ARE HERE.
EXIT     MVI   RCINSTR+3,X'00'    NORMAL END, SET RET CODE = 0
EXITRC8  DS    0H                                                 -CWB-
         TM    OUTPUT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    CLOSEIN            IF NOT, SKIP CLOSE.             -CWB-
         LA    R1,EXCPIOB         RESTORE IOB POINTER IN DCB      -CWB-
         IC    R0,OUTPUT+(DCBIOBA-IHADCB) TO POINT TO TRUE IOB, NOT
         ST    R1,OUTPUT+(DCBIOBA-IHADCB) BSAM-TYPE IOB PREFIX.
         STC   R0,OUTPUT+(DCBIOBA-IHADCB)
         NI    OUTPUT+(DCBOFLGS-IHADCB),X'7F' PRETEND LAST I/O WAS A
         OI    OUTPUT+(DCBOFLGS-IHADCB),X'04' READ TO AVOID WRITING A
*                                      TAPE MARK.                 -CWB-
         CLOSE  OUTPUT            CLOSE OUTPUT TAPE.              -CWB-
CLOSEIN  TM    INPUT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    NOTOPEN            PROGRAM DIDN'T GET VERY FAR,    -CWB-
*                                      DID IT.                    -CWB-
         CLOSE INPUT              CLOSE INPUT TAPE.               -CWB-
NOTOPEN  DS    0H                                                 -CWB-
         L     R13,SAVE+4         MAY BYPASS UNRELEASED SYNAD AREA
         LM    14,12,12(13)
RCINSTR  LA    15,8
         BR    R14                FINAL EXIT FROM TAPESCAN IN ALL CASES
PUTLINE  DS    0H                 GENERAL PRINTING SUBROUTINE, ENTRY 1
         MVC   MSGBUF,BLANKBUF    CLEAR BUFFER FAST
PUTLINE2 EX    0,0(R2)            GENERAL PRINTING SUBROUTINE, ENTRY 2
PUTLINE3 CLI   MSGBUF,C' '        GENERAL PRINTING SUBROUTINE, ENTRY 3
         BE    LNCOUNT-4          BIF CARR. CTRL  CHAR IS A BLANK
         CLI   MSGBUF,C'0'        SEE IF CARRIAGE CONTROL CHAR IS ZERO
         BE    C0                 BIF IS A ZERO
         LA    R0,3               NO BLANK OR ZERO, MUST BE A MINUS
         B     LNCOUNT
C0       LA    R0,2               COUNT TWO LINES
         B     LNCOUNT
         LA    R0,1               COUNT ONE LINE
LNCOUNT  A     R0,LINENO
         ST    R0,LINENO
         C     R0,LINECNT         COMPARE WITH MAX LINES          -HMD-
         BNH   SAMEPAGE
         MVC   PAGECHAR,=X'40202120' EDIT PATTERN
         L     R1,PAGECNT         INCREMENT THE PAGE COUNT.       -CWB-
         LA    R1,1(R1)                (DECIMAL INSTRUCTIONS      -CWB-
         ST    R1,PAGECNT              REPLACED BY BINARY.)       -CWB-
         CVD   R1,DBLPAGE         MAKE IT PRINTABLE.              -CWB-
         ED    PAGECHAR,DBLPAGE+6                                 -CWB-
         MVI   LINENO+3,X'00'
         PUT   SYSPRINT,PAGEHDR
         SPACE 1                                               HD AUG86
         CLC   =C'VTOC--',0(R2)   ARE WE IN VTOC ROUTINE?      HD AUG86
         BNE   SAMEPAGE           NO, CONTINUE                 HD AUG86
         MVC   HOLDBUF,BLANKBUF   CLEAR BUFFER FAST            HD AUG86
         L     R1,=A(VTOCHED2)    GET HEADER TEXT AND MOVE     HD AUG86
         MVC   HOLDBUF(L'VTOCHED2),0(R1)                       HD AUG86
         MVI   LINENO+3,X'04'     INDICATE 3 LINES             HD AUG86
         PUT   SYSPRINT,HOLDBUF   PUT THE LINE OUT             HD AUG86
         MVI   MSGBUF,C'0'        INDICATE SKIP 1 LINE         HD DEC86
         SPACE 1                                               HD AUG86
SAMEPAGE PUT   SYSPRINT,MSGBUF
         B     6(R2)              RETURN FROM PUTLINE SUBROUTINE
PAGECHK  DS    0H                 LINE RESERVATION SUBROUTINE
         A     R0,LINENO          ADD LINES TO BE RESERVED TO LINE NO
         C     R0,LINECNT         COMPARE TO MAXIMUM LINE  NUMBER -HMD-
         BCR   13,R2              RETURN IF CURRENT PAGE HAS ENUF ROOM
         MVI   LINENO+3,0         ZERO LINE COUNTER
*        AP    PAGEPACK,=P'1'     COUNT NEW PAGE                  -CWB-
         MVC   PAGECHAR,=X'40202120'
*        ED    PAGECHAR,PAGEPACK                                  -CWB-
         L     R1,PAGECNT         INCREMENT THE PAGE COUNT.       -CWB-
         LA    R1,1(R1)                (DECIMAL INSTRUCTIONS      -CWB-
         ST    R1,PAGECNT              REPLACED BY BINARY.)       -CWB-
         CVD   R1,DBLPAGE         MAKE IT PRINTABLE.              -CWB-
         ED    PAGECHAR,DBLPAGE+6                                 -CWB-
         PUT   SYSPRINT,PAGEHDR
         BR    R2
*BADREC  WTL   'BAD (0) BLOCK SIZE' (OFTEN MEANS TAPE OFF END OF REEL)
BADREC   DS    0H                                               -CWB-
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'BADRECM),BADRECM
         B     EXITRC8
SYNERR   SYNADAF ACSMETH=BSAM
SYNERR2  ST    R14,SVR14
         CH    R0,=H'4'           CHECK SYNADAF'S RETURN CODE
         BNE   DIRECT
         LH    R14,12(R1)         LOAD NO. OF BYTES READ
         CVD   R14,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  32(5,R1),BADLNGTH
         MVC   8(24,R1),=C'0I/O ERROR - BYTES READ='
DIRECT   BAL   R2,PUTLINE         PRINT SYNAD ERROR MESSAGE
         MVC   MSGBUF(120),8(R1)
         LA    R14,1
         A     R14,ERRCOUNT       INCREMENT ERROR COUNT
         ST    R14,ERRCOUNT
         C     R14,SYNADNO        COMPARE ERROR COUNT WITH ERROR LIMIT
         BH    TOOMANY            PRINT MSG & EXIT IF ERR LIM EXCEEDED
         SYNADRLS
         L     R14,SVR14
         BR    R14
TOOMANY  BAL   R2,PUTLINE         YOU SHOULD PROBABLY SET ERRLIM TO 0 -
         MVC   MSGBUF(55),=C'0TAPESCAN TERMINATING DUE TO EXCESSIVE I/O*
                ERROR COUNT.'
         B     EXITRC8            FOR TAPE COPYING OPERATIONS.
EXECEXCP DS    0H                 EXCP SUBROUTINE, R2=PTR TO DCB, AND  *
                                  R1=PTR TO 3-BYTE ADCON WHICH POINTS  *
                                  TO CHANNEL PROGRAM.
         ST    R2,EXCPDCB         SAVE DCB POINTER.               -CWB-
         L     R2,DCBIOBA-IHADCB(R2) GET PTR TO A BSAM IOB PREFIX
         MVC   STARTSAV(3),IOBSTART+9(R2) SAVE PTR TO BSAM'S CCW
         MVC   IOBSTART+9(3,R2),0(R1) MOVE IN PTR TO CHAN PROG
         LA    R1,4(R2)           LOAD PTR TO ECB IN PREFIX
         ST    R1,IOBECBPT+8(R2)  STORE PTR TO ECB
         XC    0(4,R1),0(R1)      CLEAR ECB IN IOB PREFIX
         EXCP  8(R2)              EXCP USING BSAM'S IOB AND ECB
         WAIT  ECB=4(R2)
         L     R1,IOBSTART+8(R2)  RESTORE CCW POINTER.            -CWB-
         MVC   IOBSTART+9(3,R2),STARTSAV RESTORE PTR TO BSAM'S CCW
         CLI   4(R2),X'7F'        SEE IF EXCP WORKED PERFECTLY
         BCR   8,R4               RETURN IF IT DID
         CLI   4(R2),X'41'        DID PERMANENT ERROR OCCUR?      -CWB-
         BNE   DOSYN              OTHER ERRORS BRANCH.            -CWB-
         CLI   0(R1),X'02'        WAS COMMAND CODE A READ?        -CWB-
         BNE   DOSYN              IF NOT, DO ERROR.               -CWB-
         LH    R1,6(R1)           GET COMMAND LENGTH.             -CWB-
         CH    R1,22(R2)          IS RESIDUAL LENGTH THE SAME?    -CWB-
         BNE   DOSYN              IF NOT, BRANCH FOR ERROR.       -CWB-
         L     R1,EXCPDCB         ELSE, MUST BE TAPE MARK READ.   -CWB-
         C     R1,=A(INPUT)       IS THIS THE INPUT DCB?          -HMD-
         BNE   LOADEOD            NO, LOAD THE EODAD FROM THE DCB -HMD-
         L     R1,EODADDR         GET PREDEFINED EOD ADDRESS      -HMD-
         BR    R1                 GO TO IT                        -HMD-
LOADEOD  L     R1,DCBEODAD-IHADCB(R1) GET EOD FROM DCB            -HMD-
         BR    R1                 GO TO IT                        -CWB-
DOSYN    DS    0H                                                 -CWB-
         LA    R1,8(R2)           GET PTR TO IOB FOR SYNADAF MACRO
         SYNADAF ACSMETH=EXCP     WORKS OK EVEN THO IOBSTART RESTORED
         BAL   R14,SYNERR2        NOW DO REST OF ANALYSIS WITH SYNERR
         BR    R4
         EJECT
* CONSTANTS AND STORAGE FOR THIS SECTION
SAVE     DS    9D
JFCBINX  DS    22D                176 BYTES
BADLNGTH DC    D'0'
DBLPAGE  DC    D'0'               CVD WORK AREA FOR PAGE NUMBER.  -CWB-
FSMCMND  CCW   X'3F',0,X'30',1    FWRD SPACE FILE, SKIP/SLI
RWNDCMND CCW   7,0,X'60',1        REWIND TAPE, CHAIN CMND, SLI
SENSCMND CCW   4,SENSBYTS,X'20',24 SENSE UP TO 24 BYTES, SLI
READCMND CCW   X'02',0,X'20',32767  READ COMMAND.                 -CWB-
READLENG EQU   READCMND+6           READ LENGTH FIELD.            -CWB-
WRTCMND  CCW   1,0,X'20',0        PTR TO RECBUF UPDATED AFTER GM. -CWB-
WTMCMND  CCW   X'1F',0,X'60',1    WRITE TM, SLI, CC TO SENSE.     -CWB-
         CCW   X'04',SENSBYTS,X'20',1  SENSE CHAINED FOR DEV END. -CWB-
*                                 ABOVE FIELDS MOVED HERE FROM    -CWB-
*                                      LATTER PART OF PROGRAM TO  -CWB-
*                                      CORRECT ADDRESSABILITY.    -CWB-
SENSBYTS DC    3XL8'00'           BUFFER FOR SENSE BYTES
AMSGBP46 DC    A(0)               A(MSGBUF+46) SET UP DURING INIT -CWB-
AMSGBP72 DC    A(0)               A(MSGBUF+72) AS ABOVE.          -CWB-
ERRCOUNT DC    F'0'               NUMBER OF SYNAD EXITS TAKEN (I/O ERRS
OPTNO    DC    F'0'               BIT ORIENTED OPTIONS - MISC/DEBUG
LINENO   DC    F'90'                                              -CWB-
EODADDR  DC    F'0'               FAKE END OF DATA ADDRESS        -HMD-
LISTNO   DC    F'4'               NUMBER OF BLKS TO LIST PER DATASET
SKIPTMNO DC    F'0'
MAXTMNO  DC    F'32767'
SKPEOVNO DC    F'0'
MAXEOVNO DC    F'1'
SYNADNO  DC    F'50'              MAX SYNAD EXITS BEFORE TERMINATION
READNO   DC    F'3'               SET TO ONE  FOR NOSUMMARY OPTION
PAGECNT  DC    F'0'               PAGE COUNTER.                   -CWB-
LINECNT  DC    F'&LINECNT'        LINES/PAGE                      -HMD-
SVR14    DC    F'0'                                               -CWB-
COUTFILE DC    H'1'               CURRENT OUTPUT FILE SEQ. NO.
*PAGEPACK DC   PL2'0'             (REPL'D BY PAGECNT)             -CWB-
BADRECM  DC    C'0BAD (0) BLOCK SIZE ENCOUNTERED'
ERRSUMSG DC    C'0NUMBER OF I/O ERRORS=XXXXX'
PAGEHDR  DC    CL60'1TAPESCAN 5.0 - APL-SMDC TAPE ANALYSIS AND COPYING +
               PROGRAM  '                                   -CWB-
DATERSLT DC    CL8'MM/DD/YY'      DATE WILL BE PLACED HERE
         DC    CL2'  '
TIMERSLT DC    XL11'4021207A20207A20204B20'
         DC    C'   INPUT VOL='                                   -CWB-
DDVOL    DC    CL6'VVVVVV'                                        -CWB-
         DC    CL12'    DENSITY='                                 -SRH-
TAPEDENS DC    CL1'X'
         DC    CL2' ('
PRTDENS  DC    CL4' XXX'
         DC    CL4'BPI)'
         DC    CL6'  PAGE'
PAGECHAR DC    X'40202120'        EXAMPLE EDIT PATTERN FOR PAGE NUMBER
         DC    CL12'           '  PAGE TRAILING BLANKS
JFCLTSV  DC    X'FF'              FOR SAVING JFCBLTYP BYTE
SENSW    DC    X'00'              SENS INFO SWITCH                *SRH*
EXCPIOBP DC    0D'0',A(*)         IOB BSAM-TYPE PREFIX.           -CWB-
EXCPECB  DC    F'0'                                               -CWB-
EXCPIOB  DC    B'01000010',XL3'0',A(EXCPECB,0),H'0'  IOB PROPER.  -CWB-
RESIDL   DC    H'0'               RESIDUAL COUNT.                 -CWB-
EXCPCCW  DC    A(0)               ADDRESS OF CHANNEL PROGRAM.     -CWB-
         DC    A(OUTPUT,0,0)                                      -CWB-
EXCPDCB  DC    A(0)               DCB ADDRESS SAVE AREA.          -CWB-
STARTSAV DC    C'SAV'             FOR SAVING BSAM'S IOBSTART FIELD
WRTFLG   DC    X'00'              TWO BIT COPY FLAG, X'03'=WRITE BLOCK
COUNTFLG DC    C'Y'               COUNT OPTION DEFAULT VALUE
HEXFLG   DC    C'Y'
SUMFLG   DC    C'Y'
VTOCFLAG DC    C'N'               VTOC ONLY FLAG               HD OCT88
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FBA,            *
               BLKSIZE=3458,LRECL=133
         EJECT
* MAXIMUM BLKSIZE IS 32760                                     HD DEC86
INPUT    DCB   DDNAME=INPUT,MACRF=RC,DSORG=PS,RECFM=U,DEVD=TA,         *
               BLKSIZE=32760,EODAD=EODS,SYNAD=SYNERR,EXLST=EXITLIST
         EJECT
OUTPUT   DCB   DDNAME=OUTPUT,MACRF=(E),EODAD=EODADBAD,DSORG=PS,   -CWB-*
               IOBAD=EXCPIOB,DEVD=TA,EXLST=EXLSTOUT               -CWB-
         EJECT
         LTORG
         EJECT
* MAIN LOOP AND MAIN LINE CODE STARTS HERE
SKIPEOVP DS    0H                 SKIPEOV OPTION CONTROL ROUTINE
         USING EXIT+4096,R12
         SR    R3,R3              CLEAR R3 TILL WE GET A VTOC     -CWB-
*                                      BLOCK ENTRY.               -CWB-
         CLC   CEOVNO,SKPEOVNO
         BNL   SKIPTMPR
         SR    R4,R4              ZERO TO GET GOOD BLK CNT EVEN IF     *
                                  SKIPTM OR SKIPEOV USED.
         SR    R8,R8              (RE)-ZERO BYTE COUNTER
         ST    R8,BLKCNT          (RE)-ZERO BLKCNT
         BAL   R5,READER          CHECK FOR DOUBLE TAPEMARK
         C     R7,=F'80'          SEE IF BLKSIZE=80 (LIKE ALL LABELS)
         BNE   CONTROL            BIF BLKLNGTH NE 80 (I.E., IT'S NOT A *
                                                    LABEL).
         C     R4,=F'1'           SEE IF THIS IS THE 1ST BLK AFTER A   *
                                  TAPEMARK OR THE LOAD POINT.
         BNE   CONTROL            BIF IT ISN'T THE FIRST
         CLC   RECBUF(4),=C'EOV1'
         BNE   CONTROL
         MVI   EOV1FLG,C'Y'       SET 'EOV PENDING' FLAG TO 'YES'
         B     CONTROL            POSITION PAST TAPEMARK AND GOTO EODS
SKIPTMPR CLC   CTPMKNO,SKIPTMNO   SKIPTM OPTION CONTROL ROUTINE
         BL    SKIPEOVP+12
         SR    R8,R8              CLEAR CTR FOR TOTAL BYTES ON TAPE
         ST    R8,BLKCNT          (RE)-ZERO BLKCNT
         OI    WRTFLG,X'02'       OR IN  'ALL TM & EOV SKIPPING DONE'
         L     R1,CTPMKNO         GET TAPEMARK COUNT.             -CWB-
         SR    R0,R0              CALCULATE NUMBER OF SL FILES    -CWB-
         D     R0,=F'3'                WE HAVE SKIPPED.           -CWB-
         ST    R1,TRUESEQN        SAVE AS LABEL= VALUE.           -CWB-
         SR    R3,R3              CLEAR R3 TILL WE GET A VTOC     -CWB-
*                                      BLOCK ENTRY.               -CWB-
PROCESS  DS    0H                 TOP OF OUTER MAIN LOOP
         SR    R4,R4              ZERO BLK COUNTER
         ST    R4,FILEBYTS        CLEAR FILE BYTE COUNT.          -CWB-
         ST    R4,MAX             RESET MAX BLKSIZE WATCHER
         MVC   MIN,=F'32767'      RESET MIN BLKSIZE WATCHER
         MVC   PREVHDR1,HDR1FLAG     SAVE PREV LABEL INDICATION.  -CWB-
         MVI   HDR1FLAG,C'N'             ASSUME NO HDR1 LABEL.    -CWB-
         MVI   LABLFLAG,C'N'      ASSUME THIS FILE IS NOT A LABEL.-CWB-
PROCESS2 DS    0H                 TOP OF MAIN INNER LOOP
         C     R4,READNO          SEE IF BLK IS TO BE READ FOR SUMMARY
         BNL   OTHRCHKS
         BAL   R5,READON          READ BLK, CALC LENGTH, DO MIN/MAX
         C     R7,=F'80'          SEE IF BLKSIZE=80 (LIKE ALL LABELS)
         BNE   NOLABEL            BIF BLKLNGTH NE 80 (I.E., IT'S NOT A *
                                                    LABEL).
         C     R4,=F'1'           SEE IF THIS IS THE 1ST BLK AFTER A   *
                                  TAPEMARK OR THE LOAD POINT.
         BNE   *+18               BIF IT ISN'T THE FIRST
         CLC   RECBUF(4),=C'EOV1'
         BNE   *+8
         MVI   EOV1FLG,C'Y'       SET 'EOV PENDING' FLAG TO 'YES'
         CLI   SUMFLG,C'Y'        SEE IF SUMMARY OPTION IS YES
         BE    CHKLABEL           DO SUMMARY PROCESSING IF SO
NOLABEL  C     R4,LISTNO
         BNH   LISTER             BRANCH TO LIST BLOCK
         B     PROCESS2
OTHRCHKS C     R4,LISTNO
         BNL   CHKCNT             BRANCH IF EVERYTHING ALREADY LISTED
         BAL   R5,READON
LISTER   BAL   R5,LISTON
         B     PROCESS2
CHKCNT   CLI   COUNTFLG,C'N'
         BE    CONTROL
         BAL   R5,READER          TOP    OF MAIN COUNTBLK OPTION LOOP
         B     *-4                BOTTOM OF MAIN COUNTBLK OPTION LOOP
CONTROL  CNTRL INPUT,FSM          FORWARD SPACE TO NEXT TAPEMARK,      *
                                  THEN BACKSPACE OVER IT.
         SR    R4,R4              RE-ZERO CURRENT BLK COUNT
         ST    R4,MAX             ZERO MAX TO INHIBIT BLK COUNT MSG
         SR    R8,R8              IS THIS NECESSARY?
         BAL   R5,READON          NOW READ THE TAPEMARK
         BAL   R5,LISTON          RETURN HERE IF NO TAPEMARK - ERROR!
         BAL   R2,PUTLINE         PRINT ERR MSG
         MVC   MSGBUF(37),=C'0ERROR - EXPECTED TAPEMARK NOT FOUND.'
         B     EXITRC8
READON   DS    0H
         MVC   RECBUF,BLANKBUF    CLEAR FIRST PART OF BUFFER FAST
READER   XC    TAPE(4),TAPE       CLEAR ECB
         LA    R2,RECBUF
         READ  TAPE,SF,INPUT,(R2),'S' READ A BLK OR TAPEMARK (WE HOPE)
         CHECK TAPE
         MVI   MARK,X'00'         SET TO X'FF' WHEN TAPEMARK READ (EOD)
         LA    R4,1(R4)           COUNT THE BLK JUST READ FOR DATASET
         L     R6,TAPE+16         GET PTR TO IOB
         LH    R6,14(R6)          GET RESIDUAL COUNT
         LH    R7,INPUT+(DCBBLKSI-IHADCB)
         SR    R7,R6              COMPUTE BLOCK'S LENGTH
         BNH   BADREC             BIF ZERO OR NEG RECORD LENGTH
         L     R1,FILEBYTS        ADD BLOCK LENGTH TO TOTAL       -CWB-
         AR    R1,R7                   BYTE COUNT FOR THIS        -CWB-
         ST    R1,FILEBYTS             FILE.                      -CWB-
         AR    R8,R7              COUNT ALL BYTES READ DURING RUN
         C     R7,MAX             COMPARE CURRENT BLK'S SIZE WITH MAX
         BNH   *+8                SKIP IF OLD MAX IS BIGGER
         ST    R7,MAX             STORE NEW MAX BLK LENGTH
         C     R7,MIN             COMPARE BLK'S SIZE WITH PREVIOUS MIN
         BNL   *+8                SKIP IF OLD MIN IS SMALLER
         ST    R7,MIN             STORE NEW MINIMUM BLOCK LENGTH
         SPACE 1                                               HD DEC86
         TM    SENSW,1        DID WE GET THE SENS INFO?
         BO    DENEND         YEP,GO ON
         LA    R1,=AL3(SENSCMND) POINT TO SENS CMDS
         LA    R2,INPUT       POINT TO INPUT DATASET
         LR    R6,R4          SAVE R4
         BAL   R4,EXECEXCP    DO SENS EXCP
         LR    R4,R6          RESTORE R4
         OI    SENSW,1        TELL'M WE BEEN HERE BEFORE
         SPACE 1                                               HD DEC86
RD3480A  CLI   FLAG3480,C'Y'  ARE WE PROCESSING A 3480?        HD DEC86
         BNE   RD3480Z        NO, CHECK 1600/6250              HD DEC86
         MVI   TAPEDENS,C'5'  SELECT DENSITY=5                 HD NOV86
         MVC   PRTDENS(4),=CL4' 38K'                           HD JAN89
         B     DENEND         MOVE TEXT AND END DENSITY CHK    HD NOV86
RD3480Z  DS    0H                                              HD DEC86
         SPACE 1                                               HD DEC86
         TM    SENSBYTS+3,4   P.E. = 1600 BPI
         BNO   TRY6250        NOPE, TRY 6250BPI
         MVI   TAPEDENS,C'3'      SET DENS FOR 1600 BPI
         MVC   PRTDENS(4),=CL4'1600'
         B     DENEND
TRY6250  L     R2,(DCBDEBAD-IHADCB)+INPUT GET DEB PTR
         L     R2,DEBUCBAD(R2)    GET UCB PTR
         TM    16(R2),2           UCBTYP = 6250BPI?
         BNO   TRY800             NOPE, TRY 800 BPI
         MVI   TAPEDENS,C'4'      SET DEN FOR 6250BPI
         MVC   PRTDENS(4),=CL4'6250'
         B     DENEND
TRY800   TM    INPUT+(DCBDEN-IHADCB),B'10000011' 800BPI?
         BNO   TRY556             TRY 556BPI
         MVI   TAPEDENS,C'2'      SET DENS
         MVC   PRTDENS(4),=CL4' 800'  TO 800 BPI
         B     DENEND             BRANCH AROUND 556 CODE         -HMD-
TRY556   DS    0H ANYONE STILL USING THESE TURKEYS? DO YOUR OWN THING
* I AM, AND WHOSE CALLING THEM TURKEYS, SUCKER                   -HMD-
         MVI   TAPEDENS,C'1'      DEN=1 FOR 556 BPI              -HMD-
         MVC   PRTDENS(4),=CL4' 556'  MOVE DEN TO PRINT          -HMD-
DENEND   DS    0H
         CLI   WRTFLG,X'03'       SEE IF SHOULD WRITE TO OUTPUT TAPE
         BNE   READEREX
         STH   R7,WRTCMND+6       STORE BLK LENGTH IN WRITE CCW
         LA    R1,=AL3(WRTCMND)
         LA    R2,OUTPUT
         LR    R6,R4              SAVE R4 (CURRENT FILE BLK CNT)
         BAL   R4,EXECEXCP
         LR    R4,R6              RESTORE R4 (CURRENT FILE BLK COUNT)
READEREX NI    WRTFLG,X'03'       ZERO OFF POSSIBLE 'NOVOLSER' BIT
         BR    R5                 RETURN FROM READON SUBROUTINE
LISTON   DS    0H                                              HD JAN89
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYPD           YES..BYPASS PRINT            HD JAN89
         MVI   MSGBUF,C'0'
         LA    R0,4               PREPARE TO RESERVE 4 LINES
         BAL   R2,PAGECHK         RESERVE 4 LINES
         BAL   R2,PUTLINE2        LIST THE FIRST PART OF THE BLOCK
         MVC   MSGBUF+1(132),RECBUF
HEXON    DS    0H                                              HD JAN89
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYPD           YES, BYPASS PRINT            HD JAN89
         CLI   HEXFLG,C'N'        SEE IF HEXLIST SPECIFIED
         BCR   8,R5               BER R5 RETURN IF HEXLIST NOT SPECIFID
         MVC   MSGBUF+1(132),RECBUF
         MVI   TRINT1+1,131       SET UP DEFAULT  OF MOVE    =132 BYTES
         MVI   TRINT2+1,131       SET UP DEFAULT  OF MOVE    =132 BYTES
         C     R7,=F'132'         SEE IF LENGTH OF BLK EXCEEDS 1 LINE
         BH    TRINT1             BRANCH IF LE 132 BYTES
         BCTR  R7,0               CONVERT BLK LENGTH TO MACHINE LENGTH
         STC   R7,TRINT1+1        AND STORE IN 1ST TR INSTRUCTION
         STC   R7,TRINT2+1        AND IN THE SECOND ONE
TRINT1   TR    MSGBUF+1(132),TRT1 MODIFIED INSTRUCTION (LENGTH)
         MVI   MSGBUF,C' '        SET CARRIAGE CONTROL
         BAL   R2,PUTLINE3        PRINT FIRST LINE OF HEX
         DC    C'TRT111'
         MVC   MSGBUF+1(132),RECBUF
TRINT2   TR    MSGBUF+1(132),TRT2 MODIFIED INSTRUCTION (LENGTH)
         BAL   R2,PUTLINE3        PRINT SECOND LINE OF HEX
         DC    C'TRT222'          FILLER
VTOCBYPD BR    R5                 RETURN FROM LISTON OR HEXON SUBR
CHKLABEL DS    0H
         MVI   LABLFLAG,C'Y'      NOTE WE HAVE A LABEL.           -CWB-
         MVI   MSGBUF,C'0'
         MVC   MSGBUF+1(132),RECBUF
         CLC   RECBUF(4),=C'HDR1'
         BE    HDR1
         CLC   RECBUF(4),=C'EOF1'
         BE    EOF1EOV1
         CLC   RECBUF(4),=C'HDR2'
         BE    HDR2
         CLC   RECBUF(4),=C'EOF2'
         BE    EOF2EOV2
         CLC   RECBUF(4),=C'VOL1'
         BE    VOL1
         CLC   RECBUF(4),=C'EOV1'
         BE    EOF1EOV1
         CLC   RECBUF(4),=C'EOV2'
         BE    EOF2EOV2
         MVI   LABLFLAG,C'N'      OOPS, NO LABEL.                 -CWB-
         B     NOLABEL            IT'S NOT A LABEL AFTER ALL
HDR1     DS    0H
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP1           YES, BYPASS THIS MESS        HD JAN89
         MVI   HDR1FLAG,C'Y'      NOTE HEADER LABEL FILE.         -CWB-
         LA    R0,14              PREPARE TO RESERVE 14 LINES
         BAL   R2,PAGECHK
         MVC   MSGBUF,BLANKBUF    CLEAR PRINT BUFFER FAST
         MVI   MSGBUF+43,C'*'     MOVE IN SEED FOR FILL
         MVC   MSGBUF+44(47),MSGBUF+43 FILL IN REST OF ASTERISKS
         MVI   MSGBUF,C'-'
         BAL   R2,PUTLINE3        PRINT LINE OF ASTERISKS
         DC    C'HDR222'
         MVI   MSGBUF,C' '        SET CARRIAGE CONTROL TO BLANK
         BAL   R2,PUTLINE3        PRINT 2ND LINE OF ASTERISKS
         DC    C'HDR333'
         MVC   MSGBUF+45(44),=C' DATASET SEQUENCE NUMBER 0000  (LABEL=0*
               000) '
         MVC   MSGBUF+70(4),RECBUF+31  MOVE IN DATASET SEQUENCE NUMBER
*        MVC   MSGBUF+83(4),RECBUF+31  AND MOVE IT IN AGAIN       -CWB-
VTOCBYP1 DS    0H                                              HD JAN89
         L     R5,CURRVTOC        POINT TO CURRENT VTOC BLOCK.    -CWB-
         CLI   0(R5),VTOCEPB      IS THIS BLOCK FULL?             -CWB-
         BL    NXTENTRY           IF NOT, BRANCH.                 -CWB-
         GETMAIN  R,LV=VTOCBLSZ   ELSE, GET CORE FOR ANOTHER BLOCK-CWB-
         ST    R1,0(R5)           SAVE FOREWARD POINTER.          -CWB-
         MVI   0(R5),VTOCEPB      REINSERT THE ENTRY COUNT.       -CWB-
         LR    R5,R1              MAKE NEW BLOCK CURRENT.         -CWB-
         ST    R1,CURRVTOC                                        -CWB-
         LA    R3,8(R5)           STEP OVER INITIAL DOUBLEWORD.   -CWB-
*                                      R3 POINTS AT CURRENT ENTRY.-CWB-
         SR    R0,R0              CLEAR ENTRY COUNT AND FORWARD   -CWB-
         ST    R0,0(R5)                POINTER IN NEW BLOCK.      -CWB-
         MVI   0(R5),1            CHANGE ENTRY COUNT TO 1.        -CWB-
         B     CLEARVEN           GO CLEAR THE FIRST ENTRY.       -CWB-
NXTENTRY LA    R3,VTOCSIZE(R3)    ADVANCE TO NEXT VTOC ENTRY.     -CWB-
         SR    R1,R1              INCREMENT ENTRY COUNT.          -CWB-
         IC    R1,0(R5)                                           -CWB-
         LA    R1,1(R1)                                           -CWB-
         STC   R1,0(R5)                                           -CWB-
         USING VTOC,R3            R3 WILL ALWAYS POINT TO ENTRY.  -CWB-
CLEARVEN MVC   VTOC(VTOCSIZE),BLANKBUF  CLEAR OUT VTOC ENTRY.     -CWB-
         L     R1,TRUESEQN        ADVANCE THE TRUE DATA SET       -CWB-
         LA    R1,1(R1)                SEQUENCE NUMBER COUNT.     -CWB-
         ST    R1,TRUESEQN                                        -CWB-
         L     R1,TRUESEQN        USE TRUE SEQUENCE NUMBER (AS    -CWB-
         CVD   R1,DBLWORK              OPPOSED TO WHAT THE LABEL  -CWB-
         OI    DBLWORK+7,X'0F'         SAYS) IN THE LABEL=XXXX    -CWB-
         UNPK  MSGBUF+83(4),DBLWORK    PART OF THE MESSAGE.       -CWB-
         MVC   VTOCSEQN,MSGBUF+83 ALSO USE IT IN VTOC.            -CWB-
         CLI   VTOCFLAG,C'Y'           VTOC ONLY?              HD JAN89
         BE    VTOCBYP2                YES, FILL IN LABEL REC  HD JAN89
         BAL   R2,PUTLINE3        PRINT MSG BETWEEN TWO LINES OF STARS
         DC    C'HDR444'
         MVC   MSGBUF+44(47),MSGBUF+43 REFILL WITH ASTERISKS
         BAL   R2,PUTLINE3
         DC    C'HDR555'
         BAL   R2,PUTLINE3
         DC    C'HDR666'
         MVC   MSGBUF+40(56),BLANKBUF CLEAR ASTERISKS TO BLANKS
         MVC   MSGBUF+1(80),RECBUF
         MVC   MSGBUF+82(35),=C'1ST HEADER LABEL RECORD,  FILE NO. '
VTOCBYP2 DS    0H                                              HD JAN89
         MVC   MSGBUF+116(4),RECBUF+31 MOVE IN DATASET SEQUENCE #
         MVC   DATASEQ,RECBUF+31  SAVE DATASET SEQUENCE #
         MVI   MSGBUF,C'-'        CCTRL FOR 2 BLANK LINES, THEN PRINT
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP3           YES, BYPASS PRINT            HD JAN89
         BAL   R2,PUTLINE3
         DC    C'HDR111'
         BAL   R5,HEXON           PRINT HEX IF OPTION IS YES
VTOCBYP3 DS    0H                                              HD JAN89
         CLC   RECBUF+54(6),=C'000000' SEE IF BLK CNT = 0
         BNE   DOBLKCNT
         MVC   MSGBUF,BLANKBUF
         MVI   SWT2,X'01'               MOVE IN HEADR CODE        -HMD-
LBL1     DS    0H
         MVC   MSGBUF+6(7),=C'DSNAME='
         MVC   MSGBUF+13(17),RECBUF+4
         LTR   R3,R3                  DO WE HAVE A VTOC ENTRY?    -CWB-
         BZ    *+10                   IF NOT, SKIP DSN SAVE.      -CWB-
         MVC   VTOCDSN,RECBUF+4   SAVE DSN FOR VTOC.              -CWB-
         MVC   MSGBUF+32(14),=C'CREATION DATE=' (MOVED OVER)      -CWB-
CHKDATE  DS    0H                 CHECK FOR VALID DATA            -HMD-
         L     R6,=A(NUBTABL)     LOAD TABLE ADDRESS              -HMD-
         CLC   =C'00000',RECBUF+42 CHECK FOR ZERO CREATION DATE   -HMD-
         BE    BADCREDT           BAD CREATION DATE               -HMD-
         TRT   RECBUF+42(5),0(R6) CHECK FOR NUMERICS              -HMD-
         BZ    DATEOK1            DATE IS OK                      -HMD-
BADCREDT DS    0H                 CREATION DATE IS BAD            -HMD-
         L     R1,AMSGBP46        POINT TO MESSAGE BUFFER         -HMD-
         MVC   0(8,R1),=C'INVALID ' MOVE INVALID TO MSG           -HMD-
         OI    SWT2,C'0'          INDICATE BAD DATE               -HMD-
         B     CDATBAD            SKIP DATE CONVERSION         HD JAN89
DATEOK1  DS    0H                                                 -HMD-
         PACK  BADLNGTH+4(4),RECBUF+42(5)   CONVERT FOR DATE SUBROUTINE
         L     R2,BADLNGTH+4
         LA    R1,AMSGBP46        PTR TO PTR TO DATE SUBR'S RSULT -CWB-
         L     R15,=V(DATE)       IRSS DATECONV SUBROUTINE
         BALR  R14,R15            DATE SUBROUTINE
CDATBAD  DS    0H                                              HD JAN89
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    *+10               IF NOT, SKIP SAVE.              -CWB-
         MVC   VTOCCREA,MSGBUF+46 SAVE DATE FOR VTOC.             -CWB-
         CLC   RECBUF+48(5),=C'00000'  DOES TAPE HAVE EXP DATE?   -HMD-
         BE    EXPIRED            IF NOT, BRANCH.                 -CWB-
         MVC   MSGBUF+56(16),=C'EXPIRATION DATE='  ELSE, LIST IT. -CWB-
         TRT   RECBUF+48(5),0(R6)    SEE IF VALID EXPDT           -HMD-
         BZ    DATEOK2            YES, IS VALID                   -HMD-
BADEXPDT L     R1,AMSGBP72        GET POINTER TO MSG BUFFER       -HMD-
         MVC   0(8,R1),=C'INVALID ' MOVE INVALID TO MSG BUFFER    -HMD-
         OI    SWT2,C'0'          INDICATE ERROR                  -HMD-
         B     EDATBAD            FORGET DATE CONVERSION          -HMD
DATEOK2  PACK  BADLNGTH+4(4),RECBUF+48(5)                         -CWB-
         L     R2,BADLNGTH+4      DO CONVERSION AS BEFORE.        -CWB-
         LA    R1,AMSGBP72                                        -CWB-
         L     R15,=V(DATE)                                       -CWB-
         BALR  R14,R15                                            -CWB-
EDATBAD  DS    0H                                              HD JAN89
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    *+10               IF NOT, SKIP SAVE.              -CWB-
         MVC   VTOCEXPR,MSGBUF+72 SAVE EXP DATE FOR VTOC.         -CWB-
EXPIRED  EQU   *                                                  -CWB-
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP4           CONTINUE PROCESSING          HD JAN89
         MVI   MSGBUF,C'0'
         BAL   R2,PUTLINE3
         DC    CL6'LBL111'
         BAL   R5,PUTERROR        SEND WARNING MESSAGE            -HMD-
VTOCBYP4 B     LABELEND           END OF THIS LABEL           HD  JAN89
PUTERROR DS    0H                 ERROR MESSAGE ROUTINE           -HMD-
         CLI   SWT2,X'03'         SEE IF ERROR                    -HMD-
         BLR   R5                 BRANCH IF NO ERROR              -HMD-
         CLI   SWT2,C'1'          IS THIS A HDR ERROR?            -HMD-
         BNE   TRLRERR            NO, MUST BE A TRAILER ERROR     -HMD-
         MVC   ERR1MSG(4),=C'HDR1'                                -HMD-
         B     *+10                                               -HMD-
TRLRERR  MVC   ERR1MSG(4),=C'EOF1'                                -HMD-
         MVC   MSGBUF,BLANKBUF    CLEAR OUTPUT BUFFER             -HMD-
         BAL   R2,PUTLINE2                                        -HMD-
         MVC   MSGBUF(LMSG),ERR0MSG                               -HMD
         BR    R5                 RETURN TO CALLER                -HMD-
EOF1EOV1 DS    0H
         CLC   DATASEQ,RECBUF+31  COMPARE OLD DATASET SEQ. NO. TO  THE *
                                  CURRENT ONE.
         BE    NEWSEQNO           SKIP ERROR MSG IF EQUAL
         CLI   DATASEQ,C'N'       SEE IF 'NONE' STILL IN DATASEQ
         BE    NEWSEQNO           SKIP ERR MSG IF NO PREVIOUS HDR1
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP5           YES, BYPASS THIS MESS        HD JAN89
         BAL   R2,PUTLINE2
         MVC   MSGBUF(100),=C'0ERROR - THE DATASET SEQ. NO. ON THE FOLL*
               OWING LABEL DOES NOT MATCH THAT ON THE PRECEDING HDR1 LA*
               BEL'
         LA    R0,6               PREPARE TO RESERVE 6 LINES
         BAL   R2,PAGECHK         RESERVE 6 LINES OF PRINTOUT
VTOCBYP5 DS    0H                                              HD JAN89
         MVC   MSGBUF+1(132),RECBUF RESTORE MSGBUF
NEWSEQNO MVC   DATASEQ,RECBUF+31  GET NEW DATASET SEQUENCE NUMBER
         MVC   MSGBUF+82(35),=C'1ST TRAILER LABEL RECORD, FILE NO. '
         MVC   MSGBUF+116(4),RECBUF+31
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    DOBLKCNT           YES, BYPASS PRINT            HD JAN89
         BAL   R2,PUTLINE3
         DC    C'EOF1EV'
         BAL   R5,HEXON
DOBLKCNT MVC   MSGBUF,BLANKBUF
         MVI   SWT2,X'02'         INDICATE IN TRAILER ROUTINE     -HMD-
         MVC   MSGBUF+82(12),=C'BLOCK COUNT=' (MOVED OVER.)       -CWB-
         MVC   MSGBUF+94(6),RECBUF+54                             -CWB-
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    LBL1               IF NOT, SKIP SAVE.              -CWB-
         CLI   VTOCOUNT,C' '      HAS THE TRUE BLOCK COUNT BEEN   -CWB-
         BNE   LBL1                    FILLED IN?  IF SO, BRANCH. -CWB-
         MVC   VTOCOUNT,RECBUF+54 ELSE, FILL IT IN FROM THE LABEL.-CWB-
         B     LBL1
EOF2EOV2 DS    0H
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP6           YES, BYPASS THIS MESS        HD JAN89
         LA    R0,6               PREPARE TO RESERVE 6 LINES OF PRINT
         BAL   R2,PAGECHK         RESERVE 6 LINES OF PRINT
         MVC   MSGBUF+82(35),=C'2ND TRAILER LABEL RECORD, FILE NO. '
         B     LBL2
HDR2     DS    0H
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP6           YES, BYPASS THIS MESS        HD JAN89
         LA    R0,8               PREPARE TO RESERVE 8 LINES  FOR PRINT
         BAL   R2,PAGECHK         RESERVE PRINT
         MVC   MSGBUF+82(35),=C'2ND HEADER LABEL RECORD,  FILE NO. '
LBL2     DS    0H
         MVC   MSGBUF+116(4),DATASEQ
         BAL   R2,PUTLINE3
         DC    C'HDR222'
         BAL   R5,HEXON
VTOCBYP6 DS    0H                                              HD JAN89
         MVC   BLKSIZE(5),RECBUF+5
         MVC   LRECL(5),RECBUF+10
         MVC   RECFM(1),RECBUF+4  MOVE F, U, OR V PART OF RECFM DESC.
         SR    R1,R1
         CLI   RECBUF+38,C'R'     SEE IF BLOCKING ATTRIBUTE IS 'BS'
         BNE   *+18               BIF NOT BS (BS AS IN 'VBS')
         LA    R1,2
         MVC   RECFM+1(2),=C'BS'
         B     CTRLCHAR
         CLI   RECBUF+38,C' '     SEE IF BLOCKING ATTRIBUTE IS UNBLKED
         BE    CTRLCHAR           BIF UNBLOCKED
         MVC   RECFM+1(1),RECBUF+38   MOVE B OR S BLOCK ATTRIBUTE CHAR
         LA    R1,1               BUMP PTR PAST THE B OR S
CTRLCHAR LA    R1,RECFM+1(R1)
         MVC   0(1,R1),RECBUF+36
         MVC   TRTCH(2),RECBUF+34
         CLC   TRTCH(2),LBL2MSG+1 SEE IF IT'S 2 BLANKS
         BNE   *+10
         MVC   TRTCH(8),=C'STANDARD' 9-TRK
         CLI   VTOCFLAG,C'Y'      CHECK FOR VTOC ONLY          HD JAN89
         BE    VTOCBYP7           BYPASS PRINT                 HD JAN89
         BAL   R2,PUTLINE
VTOCBYP7 DS    0H                                              HD JAN89
         MVC   MSGBUF(109),LBL2MSG
         CLI   RECBUF,C'H'        SEE IF IS 'HDR2'
         BNE   LABELEND           BIF IT WAS EOF2 OR EOV2      HD JAN89
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    NOVSTUFF           IF NOT, SKIP SAVES.             -CWB-
         MVC   VTOCRECF,RECFM     COPY DATA FOR VTOC.             -CWB-
         MVC   VTOCLREC,LRECL                                     -CWB-
         MVC   VTOCBLKS,BLKSIZE                                   -CWB-
         MVC   VTOCDEN,PRTDENS                                    *SRH*
         MVC   VTOCTRTC,RECBUF+34                                 -CWB-
         MVC   VTOCJOBN,RECBUF+17                                 -CWB-
         MVC   VTOCSTEP,RECBUF+26                                 -CWB-
NOVSTUFF EQU   *                                                  -CWB-
         MVC   MSGBUF+2(120),MSGBUF+1    CLEAR MOST OF MSGBUF
         MVC   CRMSG+15(8),RECBUF+17
         MVC   CRMSG+32(8),RECBUF+26
         CLI   VTOCFLAG,C'Y'                                   HD JAN89
         BE    VTOCBYP8                                        HD JAN89
         BAL   R2,PUTLINE2
         MVC   MSGBUF+6(L'CRMSG),CRMSG
VTOCBYP8 B     LABELEND                                        HD JAN89
         EJECT                                                 HD JAN89
************************************************************** HD JAN89
**                                                             HD JAN89
**        ROUTINE TO HANDLE VOLSER FILE AND PRINT VOLSER       HD JAN89
**                                                             HD JAN89
************************************************************** HD JAN89
VOL1     DS    0H
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYP9           YES, BYPASS THIS MESS        HD JAN89
         LA    R0,6               PREPARE TO RESERVE 6 LINES OF PRINT
         BAL   R2,PAGECHK         AND RESERVE THEM
         MVC   MSGBUF+82(19),=C'VOLUME LABEL RECORD'
         MVI   MSGBUF,C'0'
         BAL   R2,PUTLINE3
         DC    C'VOL111'
         BAL   R5,HEXON
VTOCBYP9 DS    0H                                              HD JAN89
         MVC   MSGBUF,BLANKBUF
         MVC   MSGBUF+6(21),=C'VOLUME SERIAL NUMBER='
         MVC   MSGBUF+27(6),RECBUF+4
         MVC   MSGBUF+40(19),=C'OWNER INFORMATION='''
         MVC   MSGBUF+59(10),RECBUF+41
         MVI   MSGBUF+69,C''''
         MVC   VSNSAVE,RECBUF+4   SAVE VSN AND OWNER FOR USE      -CWB-
         MVC   OWNERSAV,RECBUF+41      IN VTOC LISTING.           -CWB-
         MVI   MSGBUF,C'0'
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYPA           YES, BYPASS PRINT            HD JAN89
         BAL   R2,PUTLINE3
         DC    C'VOL222'
         CLC   (JFCBINX+JFCBVOLS-JFCB)(6),RECBUF+4 SEE IF VOLUME MATCH
         BE    LABELEND           BIF SAME                     HD JAN89
         BAL   R2,PUTLINE
         MVC   MSGBUF(098),=C'0WARNING - VOLUME SERIAL NUMBER IN ABOVE *
               LABEL DOES NOT MATCH THAT SPECIFIED ON INPUT DD STATEMEN*
               T'
VTOCBYPA DS    0H                                              HD JAN89
         TM    WRTFLG,X'01'       ARE WE MAKING A COPY?        -CWB-
         BZ    LABELEND           IF NOT, CONTINUE PROCESSING. -CWB-
         L     R0,CTPMKNO         GET CURRENT TAPEMARK NUMBER
         LTR   R0,R0              SEE IF IT'S ZERO (NO TAPEMARKS YET)
         BNZ   LABELEND           BIF A TAPEMARK HAS ALREADY BEEN READ
         TM    JFCLTSV,X'02'      SEE IF 'SL' (OR SUL) BIT IS ON
         BZ    LABELEND           IF IT'S NOT THEN 'NO VERIFY' (BLP OR *
                                  AL) WAS SPECIFIED IN JCL; SO KEEP ON.
         BAL   R2,PUTLINE         PRINT OPERATOR ERROR TERMINATION MSG
         MVC   MSGBUF(38),=C'0TERMINATION DUE TO WRONG TAPE MOUNTED'
         B     EXITRC8
LABELEND B     PROCESS2                                        HD JAN89
         EJECT                                                 HD JAN89
************************************************************** HD JAN89
**                                                             HD JAN89
**               EOD PROCESSING                                HD JAN89
**                                                             HD JAN89
************************************************************** HD JAN89
EODS     DS    0H
*        AP    MARKNO,=P'1'       COUNT THE TAPEMARK              -CWB-
*        UNPK  MARKNUM,MARKNO                                     -CWB-
         L     R1,MARKNO          COUNT THE TAPEMARK (COUNT       -CWB-
         LA    R1,1(R1)                STORED IN BINARY FORM).    -CWB-
         ST    R1,MARKNO                                          -CWB-
         CVD   R1,DBLWORK         MAKE IT PRINTABLE.              -CWB-
         UNPK  MARKNUM,DBLWORK                                    -CWB-
         OI    MARKNUM+3,X'F0'
         LA    R1,1
         A     R1,CTPMKNO         CURRENT NUMBER OF TAPEMARKS PASSED
         ST    R1,CTPMKNO         IS NOW UPDATED TO ACTUAL VALUE
         L     R1,BLKCNT          BLKS IN PREVIOUS DATASETS
         AR    R1,R4              ADD NO. OF BLKS IN DATASET JUST READ
         ST    R1,BLKCNT          TO GET TOTAL BLKS READ SO FAR (EXCEPT*
                                  FOR SKIPPING - SKIPEOV OR SKIPTM).
         CLI   WRTFLG,X'03'       SEE IF COPY BEING DONE NOW
         BNE   *+24               SKIP WRITING TAPEMARK IF NOT
         LA    R1,=AL3(WTMCMND)   PTR TO PTR TO WTM CCW FOR OUTPUT TAPE
         LA    R2,OUTPUT          DCB FOR WTM EXCP OUTPUT
         ST    R4,SVR4            SAVE R4 FOR EXEC ESCP CALL
         BAL   R4,EXECEXCP        CAL SUBROUTINE TO ISSUE EXCP
         L     R4,SVR4            RESTORE R4
         CLI   COUNTFLG,C'N'
         BE    SIMPLETM           BIF COUNT OPTION NOT SPECIFIED
         L     R0,MAX
         LTR   R0,R0
         BZ    SIMPLETM           BIF NO COUNTING WORK HAS BEEN DONE
         LTR   R4,R4              BYPASS MAX/MIN/AVG IF           -CWB-
         BZ    SIMPLETM                NO RECORDS.                -CWB-
         L     R1,MIN
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  MINMSG,BADLNGTH
         L     R1,MAX
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  MAXMSG,BADLNGTH
         LR    R1,R4              COPY COUNT OF RECORDS AND DIVIDE-CWB-
         SRA   R1,1                    BY 2 TO ROUND THE AVERAGE. -CWB-
         A     R1,FILEBYTS        ADD NUMBER OF BYTES IN FILE.    -CWB-
         SR    R0,R0              CLEAR R0 FOR DIVIDE.            -CWB-
         ST    R0,FILEBYTS        ALSO CLEAR FILEBYTS FOR NEXT    -CWB-
*                                      FILE.                      -CWB-
         DR    R0,R4              DIVIDE FOR AVERAGE BLOCK SIZE.  -CWB-
         CVD   R1,BADLNGTH        MAKE AVERAGE SIZE PRINTABLE.    -CWB-
         OI    BADLNGTH+7,X'0F'                                   -CWB-
         UNPK  AVGMSG,BADLNGTH                                    -CWB-
         CVD   R4,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  COUNTMSG,BADLNGTH
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    NOSAVE             IF NOT, BRANCH.                 -CWB-
         CLI   LABLFLAG,C'Y'      IS THIS A LABEL FILE?           -CWB-
         BE    NOSAVE             IF SO, DON'T SAVE STAT'S.       -CWB-
         MVC   VTOCMINB,MINMSG    SAVE MIN, MAX, AVG, AND COUNT   -CWB-
         MVC   VTOCMAXB,MAXMSG         FOR VTOC LISTING.          -CWB-
         MVC   VTOCAVGB,AVGMSG                                    -CWB-
         MVC   VTOCOUNT,COUNTMSG                                  -CWB-
NOSAVE   EQU   *                                                  -CWB-
*        MVI   EOVM+1,120         SET LENGTH FOR POSSIBLE LONG EOV-CWB-
         MVI   EOVM+1,CNTSEND-TPMKMSG-1  SET LENGTH FOR MVC.      -CWB-
*        MVI   MSGBUF+100,C' '    PREPARE TO CLEAR PART OF MSG BUF-CWB-
*        MVC   MSGBUF+101(32),MSGBUF+100 CLEAR LAST PART OF  BUFFE-CWB-
         MVC   MSGBUF,BLANKBUF    CLEAR BUFFER COMPLETELY.        -CWB-
*        MVC   MSGBUF+18(87),EOVNUM+3                             -CWB-
         MVC   MSGBUF+18(CNTSEND-EOVNUM-3),EOVNUM+3 COPY MESG.    -CWB-
         LA    R5,PUTLINE2        SPECIFY LONG TYPE OF TAPEMARK MSG
         B     TMSGDONE
SIMPLETM MVI   EOVM+1,33          SET LENGTH OF POSSIBLE EOV MSG
         LA    R5,PUTLINE         SPECIFY SHORT TPMK FOUND MSG IF ANY
*************************************************************  HD AUG86
** LABEL TMSGDONE REPLACED BELOW                           **  HD AUG86
*************************************************************  HD AUG86
*TMSGDONE  CLI   MARK,X'FF'
*          BE    EOVPROC
*          MVI   MARK,X'FF'
*************************************************************  HD AUG86
** END OF REPLACED CODE SEGMENT                            **  HD AUG86
*************************************************************  HD AUG86
TMSGDONE DS    0H                                                 -CWB-
         CLI   EOV1FLG,C'Y'
         BE    EOVPROC            BIF 'EOV PENDING' FLAG SET
         CLI   MARK,X'FF'         TWO CONSECUTIVE MARKS?          -CWB-
         BNE   SKPCHK             IF NOT, BRANCH.                 -CWB-
         CLI   PREVHDR1,C'N'      PREV FILE A HDR1 LABEL?         -CWB-
         BE    EOVPROC            IF NOT, GO DO EOV.              -CWB-
SKPCHK   MVI   MARK,X'FF'         NOTE THIS MARK FOUND.           -CWB-
         CLC   SKPEOVNO,CEOVNO    SKIP SKIPTM PROCESSSING IF
         BH    SKIPEOVP           SKIPEOV PROCESSING IS BEING DONE
         CLC   CTPMKNO,SKIPTMNO   COMPARE CTPMKNO WITH SKIPTMNO
         BL    SKIPTMPR           BRANCH TO DO POSSIBLE SKIPTM PROCESS
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYPB           BYPASS PRINT IF YES.         HD JAN89
         BALR  R2,R5              TO PUTLINE OR PUTLINE2
VTOCBYPB DS    0H                                              HD JAN89
         MVC   MSGBUF(18),TPMKMSG
         OI    WRTFLG,X'02'       SET 'SKIPPING DONE' BIT FOR COPY
         CLC   CTPMKNO,MAXTMNO
         BL    PROCESS
         CLI   WRTFLG,X'03'       CHECK IF COPY BEING DONE
         BNE   *+16               SKIP IF IT'S NOT BEING DONE
         LA    R1,=AL3(WTMCMND)   OTHERWISE WRITE AN EXTRA TAPEMARK,
         LA    R2,OUTPUT          JUST TO MAKE SURE.  THIS COULD
         BAL   R4,EXECEXCP        RESULT  IN 3 TAPEMARKS IN LAST EOV.
         B     PRENDMSG
EOVPROC  DS    0H                 HANDLES END-OF-VOLUME INDICATIONS
         MVI   EOV1FLG,C'N'       CLEAR 'EOV PENDING' FLAG
*        AP    EOVNO,=P'1'        COUNT EOV'S                     -CWB-
*        UNPK  EOVNUM,EOVNO                                       -CWB-
         L     R1,EOVNO           COUNT EOV'S (COUNT STORED       -CWB-
         LA    R1,1(R1)                IN BINARY FORM).           -CWB-
         ST    R1,EOVNO                                           -CWB-
         CVD   R1,DBLWORK         MAKE IT PRINTABLE.              -CWB-
         UNPK  EOVNUM,DBLWORK                                     -CWB-
         OI    EOVNUM+2,X'F0'
*        MVC   WRITELOG+11(3),EOVNUM                              -CWB-
*WRITELOG WTL  'EOV000 ENCOUNTERED'                               -CWB-
         CLI   VTOCFLAG,C'Y'      VTOC ONLY?                   HD JAN89
         BE    VTOCBYPC           YES..BYPASS PRINT            HD JAN89
         BAL   R2,PUTLINE
VTOCBYPC DS    0H                                              HD JAN89
EOVM     MVC   MSGBUF(00),TPMKMSG LENGTH SPECIFIED IN EODS EXIT
         LA    R1,1
         A     R1,CEOVNO
         ST    R1,CEOVNO
         C     R1,SKPEOVNO
         BL    SKIPEOVP           BIF SKIPEOV BEING DONE
         BNE   NOTJUST            BIF SKIPEOV PROCESSING NOT JUST DONE
         CLC   CTPMKNO,SKIPTMNO   SEE IF SKIPTM PROCESSING COMPLETED
         BL    NOTJUST            BIF NOT, SKIPTMNO SHOULD BECOME      *
                                  ACTUAL NUMBER OF TAPEMARKS SKIPPED.
         MVC   SKIPTMNO,CTPMKNO   MORE THAN SKIPTMNO WERE SKIPPED DUE  *
                                  SKIPEOV; RESET SKIPTMNO TO INDICATE  *
                                  ACTUAL NUMBER SKIPPED (FOR LNGTH EST)
NOTJUST  DS    0H
         OI    WRTFLG,X'02'       SET 'SKIPPING DONE' BIT FOR COPY
         CLC   CEOVNO,MAXEOVNO
         BL    PROCESS
         SPACE 2                                               HD JAN89
PRENDMSG DS    0H                                              HD JAN89
         CLI   COUNTFLG,C'N'
         BE    SHORT              SKIP TO NOT PRINT COUNTS
         LA    R0,6               PREPARE TO RESERVE 6 LINES FOR PRINT
         BAL   R2,PAGECHK         RESERVE THEM
         LA    R1,=AL3(SENSCMND)  RE-SENSE AT END OF PROCESSING
         LA    R2,INPUT           PTR TO DCB FOR EXCP CALL
         BAL   R4,EXECEXCP
         CLI   FLAG3480,C'Y'      IS THIS A 3480 DEVICE        HD JAN89
         BE    ADDDEN3            YES, CONTINUE WITH PROCESS   HD JAN89
         MVI   INDEX+3,8          SINCE 9-TRK, LOAD OFFSET     HD JAN89
         TM    SENSBYTS+3,X'04'   CHECK IF PE, 1 = PE = 1600 BPI
         BNO   CK6250             CK FOR 6250 BPI 9TRK
         MVI   (DCBDEN-IHADCB)+INPUT,X'C3' SET DEN=1600 BPI INDICATOR
         MVI   LNGTHEST+50,C'3'   SET DENSITY = 3              HD JAN89
         B     ADDDEN3            CONTINUE WITH LENGTH CHECK   HD JAN89
         SPACE 1
EIGHTBPI DS    0H
         MVI   INDEX+3,0          SET OFFSET IN BPI TABLE      HD JAN89
         MVI   INPUT+(DCBDEN-IHADCB),X'83' SET DEN=800 BPI INDICATOR
         MVI   LNGTHEST+50,C'2'   SET DENSITY = 2              HD JAN89
         B     ADDDEN3            CONTINUE WITH LENGTH CHECK   HD JAN89
         SPACE 1
CK6250   L     R2,(DCBDEBAD-IHADCB)+INPUT GET DEB ADDR
         L     R2,DEBUCBAD(R2)    GET UCB ADDR
         TM    16(R2),2           CK UCBTYP FOR 6250 BPI
         BNO   EIGHTBPI           NOPE, ASSUME 800 BPI         HD JAN89
         MVI   (DCBDEN-IHADCB)+INPUT,X'D3' SET DENS=4 FOR 6250 BPI
         MVI   LNGTHEST+50,C'4'   SAY DENS=4
         MVI   INDEX+3,12         SET INDEX FOR 6250 BPI       HD JAN89
         B     ADDDEN3            BYPASS SOME CODE
         SPACE 1
*
*         FORMULA FOR LENGTH IN INCHES FOLLOWS:
*         INCHES=R8/BPI+(IBG*BLKCNT+CTPMKNO*TMLENGTH)/1000
         SPACE 1
ADDDEN3  L     R15,CTPMKNO        LOAD NUMBER OF TAPEMARKS READ
         S     R15,SKIPTMNO       SUBTRACT NUMBER OF TAPEMARKS SKIPPED
         MH    R15,TMLENGTH       MULTIPLY BY (TAPEMARK LENGTH*1000)
         CLI   FLAG3480,C'N'      3480?                        HD JAN89
         BE    ADDEN3A            YES, BYPASS THIS             HD JAN89
         MVI   LNGTHEST+50,C'5'   INDICATE DENSITY=5           HD JAN89
         L     R1,BLKCNT          LOAD BLOCK COUNT             HD JAN89
         MH    R1,GAP3480         MULT TO GET GAP LEN*1000     HD JAN89
         AR    R1,R15             GET TOTAL GAP + TM LGTH*1000 HD JAN89
         SR    R0,R0              CLEAR FOR DIVIDE             HD JAN89
         D     R0,=F'1000'        GET TOTAL TAPEMARK+GAP IN.   HD JAN89
         LR    R15,R1             SAVE FOR LATER               HD JAN89
         L     R2,BPI3480         LOAD DENSITY                 HD JAN89
         B     ADDEN3B            CONTINUE WITH CALCULATION    HD JAN89
ADDEN3A  DS    0H                                              HD JAN89
         L     R2,INDEX           LOAD INDEX INTO BPI/IBG TABLE
         L     R1,BLKCNT          LOAD BLOCK COUNT
         MH    R1,BPIBGTBL(R2)    MULTIPLY TO GET TOTAL GAP LENGTH*1000
*                                                              HD JAN89
         AR    R1,R15             GET TOTAL GAP + TAPEMARK LENGTH*1000
         SR    R0,R0
         D     R0,=F'1000'        GET TOTAL TAPEMARK+ GAP LENGTH INCHES
         LR    R15,R1             SAVE THIS FOR LATER
         LH    R2,BPIBGTBL+2(R2)  LOAD PHYSICAL BPI (NOT LOGICAL BPI)
*                                                              HD JAN89
ADDEN3B  DS    0H                                              HD JAN89
         LR    R1,R8              LOAD TOTAL BYTE COUNT
         SR    R0,R0
         DR    R0,R2              DIVIDE BYTE COUNT BY PHYSICAL BPI
         AR    R1,R15             GET TOTAL LENGTH IN INCHES
         SR    R0,R0
         D     R0,=F'12'          GET FEET IN R1, INCHES IN R0
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  LNGTHEST+17(4),BADLNGTH
         CVD   R0,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  LNGTHEST+27(2),BADLNGTH
         BAL   R2,PUTLINE
         MVC   MSGBUF(81),LNGTHEST
         BAL   R2,PUTLINE
         MVC   MSGBUF(106),LNGTHACC
         MVI   SHORTNOW+1,LNGTHEST-ENDMSG-1  SET LENGTH OF MESSAGE-HMD-
         CVD   R8,BADLNGTH        TOTAL BYTES READ (FOR COUNT OPT ONLY)
         OI    BADLNGTH+7,X'0F'   SET SIGN NIBBLE
         UNPK  BYTES,BADLNGTH
         L     R8,BLKCNT          TOTAL BLKS READ ON TAPE, EXCLUDING   *
                                  THOSE READ DURING SKIP PROC%SSING.
         CVD   R8,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  NBLKS,BADLNGTH
SHORT    BAL   R2,PUTLINE         PRINT 'SUCCESSFUL END' MSG
SHORTNOW MVC   MSGBUF(L'ENDMSG),ENDMSG (LENGTH MODIFIED FOR COUNTBLKS)
         L     R2,ERRCOUNT        COUNT OF SYNAD EXITS TAKEN
         LTR   R2,R2              SEE IF ZERO
*        BZ    EXIT               EXIT IF SO                      -CWB-
         BZ    LISTVTOC           GO DO VTOC IF SO.               -CWB-
         CVD   R2,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  ERRSUMSG+22(5),BADLNGTH
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'ERRSUMSG),ERRSUMSG
*        B     EXIT                                               -CWB-
         SPACE 3
LISTVTOC CLC   FRSTVTOC+1(3),=AL3(0)  DID WE MAKE A VTOC?         -CWB-
         BE    EXIT               IF NOT, SKIP IT.                -CWB-
         LA    R0,100             RESERVE 100 LINES (FORCE EJECT).-CWB-
         BAL   R2,PAGECHK                                         -CWB-
         BAL   R2,PUTLINE         OUTPUT VTOC HEADING.            -CWB-
         MVC   MSGBUF(VHEADLEN),VTOCHEAD                          -CWB-
         BAL   R2,PUTLINE         OUTPUT COLUMN HEADINGS.         -CWB-
         MVC   MSGBUF(L'VTOCHED2),VTOCHED2                        -CWB-
         L     R4,FRSTVTOC        GET ADDRESS OF FIRST VTOC BLOCK.-CWB-
         SR    R8,R8              CLEAR R8 FOR 1-BYTE COUNTS.     -CWB-
NEXTVBLK LA    R3,8(R4)           GET ADDRESS OF FIRST ENTRY IN   -CWB-
*                                      THE VTOC BLOCK.            -CWB-
         IC    R8,0(R4)           GET THE NUMBER OF ENTRIES IN    -CWB-
*                                      THIS VTOC BLOCK.           -CWB-
NEXTLINE MVC   MSGBUF,BLANKBUF    BUILD THE VTOC ENTRY LINE:      -CWB-
         MVC   MSGBUF+1(4),VTOCSEQN    DATA SET SEQUENCE NUMBER.  -CWB-
         MVC   MSGBUF+7(17),VTOCDSN    DATA SET NAME.             -CWB-
         MVC   MSGBUF+26(4),VTOCRECF   RECFM.                     -CWB-
         MVC   MSGBUF+32(5),VTOCLREC   LRECL.                     -CWB-
         MVC   MSGBUF+40(5),VTOCBLKS   BLKSIZE.                   -CWB-
         MVC   MSGBUF+47(4),VTOCDEN    DENSITY.                   -CWB-
         MVC   MSGBUF+53(2),VTOCTRTC   TRTCH.                     -CWB-
         CLC   VTOCTRTC,LBL2MSG+1    Q. TRTCH IS BLANKS.          -HMD-
         BNE   *+10                  A. NO, LEAVE AS IT IS        -HMD-
         MVC   MSGBUF+53(3),=C'STD'     INDICATE STD. TRTCH       -HMD-
         MVC   MSGBUF+59(5),VTOCMAXB   MAXIMUM BLOCK SIZE.        -CWB-
         MVC   MSGBUF+68(5),VTOCMINB   MINIMUM BLOCK SIZE.        -CWB-
         MVC   MSGBUF+77(5),VTOCAVGB   AVERAGE BLOCK SIZE.        -CWB-
         MVC   MSGBUF+86(6),VTOCOUNT   NUMBER OF BLOCKS.          -CWB-
         MVC   MSGBUF+95(8),VTOCCREA   CREATION DATE.             -CWB-
         MVC   MSGBUF+105(8),VTOCJOBN  JOB NAME.                  -CWB-
         MVC   MSGBUF+115(8),VTOCSTEP  STEP NAME.                 -CWB-
         MVC   MSGBUF+125(8),VTOCEXPR  EXPIRATION DATE.           -CWB-
         BAL   R2,PUTLINE3        OUTPUT THE LINE.                -CWB-
         DC    CL6'VTOC--'        REQUIRED DEAD SPACE.            -CWB-
         LA    R3,VTOCSIZE(R3)    ADVANCE TO NEXT ENTRY.          -CWB-
         BCT   R8,NEXTLINE        LOOP IF MORE IN THIS BLOCK.     -CWB-
         LR    R1,R4              POINT TO VTOC BLOCK.            -CWB-
         L     R4,0(R4)           GET ADDRESS OF NEXT BLOCK.      -CWB-
         LA    R0,VTOCBLSZ        GET VTOC BLOCK SIZE.            -CWB-
         FREEMAIN  R,LV=(0),A=(1) FREE UP VTOC BLOCK'S CORE.      -CWB-
         LA    R4,0(R4)           CLEAR HIGH-ORDER BYTE.          -CWB-
         LTR   R4,R4              IS FOREWARD POINTER ZERO?       -CWB-
         BZ    EXIT               IF SO, WE'RE DONE.              -CWB-
         B     NEXTVBLK           ELSE, GO DO NEXT BLOCK.         -CWB-
         EJECT
CTPMKNO  DC    F'0'               # OF TAPEMARKS ALREADY ENCOUNTERED
CEOVNO   DC    F'0'               # OF DOUBLE TAPEMARKS PASSED
SVR4     DC    F'0'               FOR SAVING R4 TEMPORARILY
BLKCNT   DC    F'0'               KEEPS TRACK OF TOTAL BLKS READ ON TAPE
                                  EXCLUDING THOSE READ DURING SKIPPING.
MAX      DC    F'0'               KEEPS TRACK OF MAX BLK LEN   HD DEC86
MIN      DC    F'32760'           KEEPS TRACK OF MIN BLK LEN   HD DEC86
TMLENGTH DC    H'3750'            DEFAULT TAPEMARK LENGTH*1000 (9-TRK)
INDEX    DC    F'0'               NOCONV=+4, +DEN*8            HD DEC86
         EJECT
*
*  -.-.-.- DO NOT CHANGE THE ORDER OF THE FOLLOWING TABLE -.-.-.-.
*
*TBLORG EQU BPIBGTBL-20           THEORETICAL ORIGIN OF BPIBGTBL
*                                 WHICH IS LIKE A 3-D ARRAY:
*                                 NOCONV=+4, +DEN*8
BPIBGTBL DC    H'601,800'    800 BPI NOCONV 9-TRK
         DC    H'1,1'       1600 BPI CONV   9-TRK (NOT USED)
         DC    H'651,1600'  1600 BPI NOCONV 9-TRK
         DC    H'300,6250'  6250 BPI NOCONV 9-TRK                 *SRH*
*  TABLE END                                                      *SRH*
GAP3480  DC    H'100'       3480 GAP                           HD JAN89
BPI3480  DC    F'38000'     3480 DENSITY                       HD JAN89
MARKNO   DC    F'0'               NUMBER OF TAPE MARKS READ.      -CWB-
EOVNO    DC    F'0'               NUMBER OF EOV'S PROCESSED.      -CWB-
TPMKMSG  DC    C'0TAPEMARK NO. '
MARKNUM  DC    C'    '
         DC    C' -- EOV NO. '
EOVNUM   DC    CL3'000'
         DC    C'    BLOCK LENGTHS:  MIN='                        -CWB-
MINMSG   DC    C'00000'
         DC    C'  MAX='                                          -CWB-
MAXMSG   DC    C'00000'
         DC    C'  AVG='                                          -CWB-
AVGMSG   DC    C'00000'                                           -CWB-
         DC    C'    NUMBER OF BLOCKS='
COUNTMSG DC    C'000000'
CNTSEND  EQU   *                                                  -CWB-
DATASEQ  DC    CL4'NONE'
ENDMSG   DC    C'0SUCCESSFUL PROCESSING OF THIS TAPE COMPLETED'
         DC    C':    TOTAL BYTES READ='
BYTES    DC    C'XXXXXXXXXXX'    NUMBER OF BYTES READ             -HMD-
         DC    C'    NUMBER OF DATA BLOCKS READ='
NBLKS    DC    C'XXXXXX'
LNGTHEST DC    C'0LENGTH ESTIMATE=XXXX FEET YY INCHES ASSUMING DEN=X AN*
               D TRTCH=STANDARD           '
LNGTHACC DC    C'0(LENGTH ESTIMATE USUALLY ACCURATE WITHIN PLUS OR MINU*
               S TEN PERCENT;  ALMOST ALWAYS WITHIN TWENTY PERCENT)'
LNGT3480 DC    C'0LENGTH TEST BYPASSED FOR 3480 CARTRIDGE DEVICE' -HMD-
LNGT348A DC    C'0LENGTH MEANINGLESS FOR CARTRIDGE TAPE'       HD DEC86
LBL2MSG  DC    CL12'0     RECFM='
RECFM    DC    CL18'          BLKSIZE='
BLKSIZE  DC    CL18'XXXXX       LRECL='
LRECL    DC    CL20'XXXXX               '
DENSITY  DC    CL6'TRTCH='
TRTCH    DC    CL35' '
CRMSG    DC    C'CREATED BY JOB          IN STEP         '
MARK     DC    X'00'              'TAPEMARK JUST READ' FLAG (00 = NOT)
EOV1FLG  DC    C'N'               SET EOV1FLG =C'Y' WHENEVER 1ST BLK   *
                                  AFTER A TAPEMARK IS 80 BYTES LONG &  *
                                  STARTS WITH 'EOV1'.
SWT2     DC    X'00'              SET FOR INVALID CREDT OR EXPDT  -HMD-
LABLFLAG DC    C'N'               INDICATES WHETHER WE ARE        -CWB-
*                                      PROCESSING A LABEL FILE.   -CWB-
PREVHDR1 DC    C'N'               C'Y' IF PREV FILE A HDR1 LABEL. -CWB-
HDR1FLAG DC    C'N'               C'Y' IF CURRENT FILE A HDR1.    -CWB-
FLAG3480 DC    C'N'               C'Y' IF TAPE IS A 3480       HD NOV86
DBLWORK  DC    D'0'               CVB/CVD WORK AREA.              -CWB-
CURRVTOC DC    A(FRSTVTOC)        CURRENT VTOC BLOCK.             -CWB-
FRSTVTOC DC    AL1(VTOCEPB),AL3(0)  ADDRESS OF FIRST VTOC BLOCK;  -CWB-
*                                      HIGH-ORDER BYTE SET TO     -CWB-
*                                      FORCE FIRST GETMAIN.       -CWB-
FILEBYTS DC    F'0'               NUMBER OF BYTES IN THIS FILE.   -CWB-
TRUESEQN DC    F'0'               LABEL= VALUE SEQUENCE NUMBER.   -CWB-
VTOCHEAD DC    C'-VOLUME TABLE OF CONTENTS FOR '                  -CWB-
VSNSAVE  DC    C'VSNVSN',C'    '                                  -CWB-
SAVETRK  DC    C'9 TRACK    '                                     -CWB-
OWNERSAV DC    CL10'          '                                   -CWB-
VHEADLEN EQU   *-VTOCHEAD                                         -CWB-
VTOCHED2 DC    C'-SEQ.  DATA SET NAME     RECFM  LRECL  BLKSIZE DEN TRT*
               CH  MAX BLK  MIN BLK  AVG BLK  BLK COUNT  CREATED  JOB N*
               AME  STEP      EXPIRES'                            -CWB-
ERR0MSG  DC    C'0WARNING - THE CREATION DATE AND/OR EXPIRATION DATE  OX
               N THE ABOVE '
ERR1MSG  DC    C'HDR1'
ERR2MSG  DC    C' LABEL IS INVALID '
LMSG     EQU   *-ERR0MSG
         EJECT
         LTORG
         EJECT
*****************************************************************-HMD-
**  THIS TABLE IS USED TO DETERMINE IF THE VOLSER IN THE JFCB  **-HMD-
**  OR IN THE UCB IS PRINTABLE EBCDIC. IF SO IT IS DISPLAYED;  **-HMD-
**  IF NOT, THE DEFAULT CONSTANT IS DISPLAYED..SEE POSMSG.     **-HMD-
*****************************************************************-HMD-
EBCDTBL  DS    0C
         DC    256XL1'FF'                FOR NON-PRINTABLES     -HMD-
         ORG   EBCDTBL+C' '              SPACES ARE OK          -HMD-
         DC    X'00'                     MAKE IT OK             -HMD-
         ORG   EBCDTBL+C'$'              DOLLAR-SIGN            -HMD
         DC    X'00'                     MAKE IT OK             -HMD-
         ORG   EBCDTBL+C'#'              POUND-SIGN AND AT-SIGN -HMD-
         DC    X'0000'                   BOTH ARE OK            -HMD-
         ORG   EBCDTBL+C'A'              ALPHABET (UPPER-CASE)  -HMD-
         DC    9X'00'                    OK                     -HMD-
         ORG   EBCDTBL+C'J'                                     -HMD-
         DC    9X'00'                                           -HMD-
         ORG   EBCDTBL+C'S'                                     -HMD-
         DC    8X'00'                                           -HMD-
         ORG   EBCDTBL+C'0'              NUMBERS                -HMD-
         DC    10X'00'                                          -HMD-
         ORG   ,                         BACK TO REALITY        -HMD-
         EJECT
****************************************************************-HMD-
** THIS TABLE IS USED TO DETERMINE WHETHER THE CREATION DATE  **-HMD-
** OR THE EXPIRATION DATE IN THE HDR1 OR EOF1 FIELD OF A      **-HMD-
** STANDARD LABELED TAPE IS NUMERIC. IF NOT, AN ERROR MSG IS  **-HMD-
** PRINTED AND TAPESCAN CONTINUES.                            **-HMD-
****************************************************************-HMD-
NUBTABL  DS    0C                 NUMERIC CHECK TABLE
         DC    256XL1'FD'         TO DISTINGUISH FROM EBCDTBL
         ORG   NUBTABL+C'0'       NUMBERS ONLY
         DC    10X'00'
         ORG   ,                  BACK TO REALITY
         EJECT
DATE     CSECT
         SAVE  (14,12),T,*
*      R1 = ADDR FOR OUTPUT (DS CL8'MM/DD/YY')
*      R2 = R1 FROM THE TIME MACRO
         LR    R12,R15
         USING DATE,R12
         LA    R3,DATESAVE
         ST    R13,4(,R3)
         ST    R3,8(,R13)
         LR    R13,R3
         USING PARMAREA,R1
         L     R1,0(,R1)   GET ADDR FOR OUTPUT            *LACCD*
         ST    R2,W2                    STORE DATE (00YYDDDF)
         TM    W2+1,01                  IF ODD NOT LEAP.
         BO    NOLEAP                    NOT LEAP
         TM    W2+1,X'12'               TEST FOR LEAP (VALID TILL 1999)
         BM    NOLEAP                   1NOT LEAP
         MVI   MONTHTBL+5,29            SETUP FEB LEAP YEAR
NOLEAP   UNPK  MMDDYY+6(3),W2+1(2)           UNPK YR
         XC    W1(6),W1                  CLEAR YR FOR DAY RTN.
         CVB   R4,W1                    GET DAY
         LA    R5,MONTHTBL-4            SET BACK PTR
MONLUPE  LA    R5,4(R5)                 INCR THRU MON TBL
         SH    R4,0(R5)                 DROP DOWN THRU MONTBL
         BH    MONLUPE                  NOT YET, TRY AGAIN
         AH    R4,0(R5)                 ADD BACK THE DAY
         CVD   R4,W1                    MAKE
         OI    W2+3,X'0F'                  THE DAY
         UNPK  MMDDYY+2(3),W2+2(2)            PRINTABLE
         MVI   MMDDYY+5,C'/'            RESTORE SLASH
         MVC   MMDDYY(2),2(R5)          MOVE THE MONTH
         MVI   MMDDYY+2,C'/'  RESTORE SLASH
         MVC   THEDATE,MMDDYY         MOVE DATE TO USER
RETURN   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),,RC=0 RESTORE REGS AND RETURN
         EJECT
         SPACE 2
MONTHTBL DS    0CL48
MTK      DC    H'31',C'01' JAN
         DC    H'28',C'02' FEB
         DC    H'31',C'03' MAR
         DC    H'30',C'04' APR
         DC    H'31',C'05' MAY
         DC    H'30',C'06' JUN
         DC    H'31',C'07' JLY
         DC    H'31',C'08' AUG
         DC    H'30',C'09' SEP
         DC    H'31',C'10' OCT
         DC    H'30',C'11' NOV
         DC    H'255',C'12' DEC
         SPACE 2
DATESAVE     DS    9D
W1       DS    F              WORKARE1
W2       DS    F              WORKAREA2
MMDDYY   DS    CL9
PARMAREA DSECT
THEDATE  DS    CL8
         SPACE 5
VTOC     DSECT
VTOCSEQN DS    CL4                SEQUENCE NUMBER                 -CWB-
VTOCDSN  DS    CL17               DATA SET NAME.                  -CWB-
VTOCRECF DS    CL4                RECFM.                          -CWB-
VTOCLREC DS    CL5                LRECL.                          -CWB-
VTOCBLKS DS    CL5                BLKSIZE.                        -CWB-
VTOCDEN  DS    CL4                DENSITY.                        -CWB-
VTOCTRTC DS    CL2                TRTCH.                          -CWB-
VTOCMAXB DS    CL5                MAXIMUM BLOCK SIZE.             -CWB-
VTOCMINB DS    CL5                MINIMUM BLOCK SIZE.             -CWB-
VTOCAVGB DS    CL5                AVERAGE BLOCK SIZE.             -CWB-
VTOCOUNT DS    CL6                BLOCK COUNT.                    -CWB-
VTOCCREA DS    CL8                CREATION DATE.                  -CWB-
VTOCJOBN DS    CL8                JOB NAME.                       -CWB-
VTOCSTEP DS    CL8                STEP NAME.                      -CWB-
VTOCEXPR DS    CL8                EXPIRATION DATE.                -CWB-
         DS    0D                 ADVANCE TO DOUBLE WORD BNDRY.   -CWB-
VTOCSIZE EQU   *-VTOC             SIZE OF VTOC ENTRY.             -CWB-
VTOCEPB  EQU   10                 NUMBER OF ENTRIES PER BLOCK.    -CWB-
VTOCBLSZ EQU   VTOCEPB*VTOCSIZE+8 SIZE OF VTOC BLOCK.             -CWB-
         EJECT
         IEFUCBOB LIST=YES                                        -HMD-
         EJECT                                                 HD DEC86
         DCBD  DSORG=PS,DEVD=TA                                HD DEC86
         EJECT                                                 HD DEC86
JFCB     DSECT                                                 HD DEC86
         IEFJFCBN LIST=YES                                     HD DEC86
         END
