WRU      TITLE 'W H O    A R E    Y O U    F O R   T S O'
WRU      TSOENTER PL=(CPPL,IO),REGS=YES,WORK=STORLEN,CPR=YES
         L     1,CPPLPSCB          GET  PSCB  ADDRESS
*  HOUSEKEEPING - INITIALIZE                                *HMD 02/87*
         MVC   ASYSID(LSYSID),SYSID                         *HMD 02/87*
         MVC   USERSEG(4),SEGA     MOVE IN SEGMENT BLOCK    *HMD 02/87*
         MVC   SIDSEG(4),SEGB       "    "   "       "      *HMD 02/87*
         MVC   UID,BLANKS          CLEAR USER ID FIELD      *HMD 02/87*
         MVC   SID,BLANKS          CLEAR SYSTEM ID FIELD    *HMD 02/87*
*  END OF HOUSEKEEPING                                      *HMD 02/87*
         SLR   2,2                 CLEAR REGISTER           *HMD 02/87*
         IC    2,7(1)              GET  USERID  LENGTH      *HMD 02/87*
         BCTR  2,0                 DECREMENT FOR EXECUTE    *HMD 02/87*
         EX    2,MOVEID            MOVE  IN  USERID
         LA    2,L'USERSEG+1(2)    LENGTH + HEADER          *HMD 02/87*
         STH   2,USERSEG           SAVE IN SEGMENT          *HMD 02/87*
         L     3,CVTPTR            GET CVT ADDRESS          *HMD 02/87*
         USING CVT,3
         L     3,CVTSMCA           GET SMCA ADDRESS
         MVC   SID+1(4),SMCASID-SMCABASE(3)                 *HMD 02/87*
*                                  MOVE IN SYSTEM ID        *HMD 02/87*
         LA    1,3                 NUMBER OF SEGMENTS       *HMD 02/87*
         ST    1,OLD               SAVE IN OLD              *HMD 02/87*
         LA    1,ASYSID            GET MESSAGE ADDRESS      *HMD 02/87*
         ST    1,OLD+4             SAVE IN OLD              *HMD 02/87*
         LA    1,USERSEG           GET USER SEGMENT         *HMD 02/87*
         ST    1,OLD+8             SAVE IN OLD              *HMD 02/87*
         LA    1,SIDSEG            GET SYSTEM ID SEG ADDR   *HMD 02/87*
         ST    1,OLD+12            SAVE IN OLD              *HMD 02/87*
         XC    PTPB(PTPBL),PTPB    CLEAR PARM BLOCK         *HMD 02/87*
         XC    TSOECB,TSOECB       CLEAR ECB                *HMD 02/87*
         SPACE 2                                            *HMD 02/87*
         PUTLINE PARM=PTPB,ECB=TSOECB,                      *HMD 02/87*X
               OUTPUT=(OLD,TERM,SINGLE,INFOR),              *HMD 02/87*X
               MF=(E,IOPL),TERMPUT=ASIS                     *HMD 02/87*
         SPACE 2                                            *HMD 02/87*
         TSOLEAVE  ISN=LTORG       GOOD  BYE
         SPACE 2                                            *HMD 02/87*
MOVEID   MVC   UID(0),0(1)         MOVE IN USERID           *HMD 02/87*
BLANKS   DC    CL8' '              CLEAR BLANKS QUICKLY     *HMD 02/87*
         SPACE 2                                            *HMD 02/87*
SYSID    DC    AL2(LSYSID),AL2(0)  OFFSET OF PRIME SEGMENT  *HMD 02/87*
MSGID    DC    C'IKJ99900I '       MESSAGE ID               *HMD 02/87*
TITLE    DC    C'You are user '
TITLE2   DC    C' and you are on system'                    *HMD 02/87*
NLCHAR   DC    X'4015'                                      *HMD 02/87*
LSYSID   EQU   *-SYSID
         SPACE 2
SEGA     DC    AL2(0),AL2(TITLE2-MSGID) AFTER THE TEXT      *HMD 02/87*
SEGB     DC    AL2(9),AL2(NLCHAR-MSGID) AFTER THE TEXT      *HMD 02/87*
TSODSECT DSECT                                              *HMD 02/87*
PTPB     PUTLINE MF=L                                       *HMD 02/87*
PTPBL    EQU   *-PTPB              PUTLINE PARM BLK LENGTH  *HMD 02/87*
OLD      DS    F                                            *HMD 02/87*
         DS    A                                            *HMD 02/87*
         DS    A                                            *HMD 02/87*
         DS    A                                            *HMD 02/87*
ASYSID   DS    (LSYSID)C           DEFINE STORAGE FOR MSG   *HMD 02/87*
USERSEG  DS    F                                            *HMD 02/87*
UID      DS    CL7                                          *HMD 02/87*
SIDSEG   DS    F                                            *HMD 02/87*
SID      DS    CL5                 SYSTEM ID SEGMENT        *HMD 02/87*
STORLEN  EQU   *-TSOUSER                                    *HMD 02/87*
         EJECT
         CVT DSECT=YES,LIST=YES
         EJECT                                              *HMD 02/87*
         IEESMCA                                            *HMD 02/87*
         END WRU
