INMX     TITLE 'INMXZ02 - TSO/E TRANSMIT TERMINATION EXIT'     HD JAN87
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        FIXED POINT REGISTERS                                        *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0        *USED BY O.S.
R1       EQU   1        *USED BY O.S. // ADDRESS OF PARAMETER LIST
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13       *USED BY O.S. // SAVE-AREA ADDRESS
R14      EQU   14       *USED BY O.S. // RETURN ADDRESS
R15      EQU   15       *USED BY O.S. // ENTRY-PT ADDR, RETURN CODE
         SPACE 1
***********************************************************************
*                                                                     *
*        FLOATING POINT REGISTERS                                     *
*                                                                     *
***********************************************************************
         SPACE 1
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
         EJECT                                                 HD JAN87
*
*  THIS IS THE INSTALLATION-WRITTEN TERMINATION EXIT FOR THE
*  TRANSMIT (XMIT) COMMAND PROCESSOR.
*
*  THE FUNCTION OF THIS EXIT IS TO NOTIFY EACH INTENDED
*  RECIPIENT THAT A TRANSMISSION IS WAITING FOR HIM TO RECEIVE.
*  THIS IS ACCOMPLISHED BY INVOKING THE SEND COMMAND PROCESSOR
*  WITH THE LOGON OPTION TO NOTIFY EACH INTENDED RECIPIENT
*  EITHER IMMEDIATELY OR WHEN NEXT HE LOGS ONTO TSO.
*
PLIST    DSECT
USERWAD  DS    AL4                    ADDRESS OF USER WORD
XMITPARM DS    AL4                    ADDRESS OF USER-ENTERED PARM
FLAGS    DS    AL4                    ADDRESS OF TRANSMIT ACTION FLAG
CPPLADDR DS    AL4                    CPPL ADDRESS
XMITMSG  DS    AL4                    ADDRESS OF MESSAGE TO SENDER
STATUS   DS    AL4                    ADDRESS OF STATUS FLAG BYTE
TUPLUSER DS    AL4                    TEXT UNIT POINTER LIST (SENDER)
TUPLDS   DS    AL4                    TEXT UNIT POINTER LIST (DATASET)
ADRCHAIN DS    AL4                    CHAIN OF RECIPIENTS
RCTRADDR DS    AL4                    POINTS TO NUMBER OF RECORDS
XMITRC   DS    AL4                    ADDRESS OF TRANSMIT RETURN CODE
*
         EJECT                                                 HD JAN87
RECEIVER DSECT
RCVLINK  DS    AL4                    PTR TO NEXT RECEIVER BLOCK
NODELEN  DS    XL2                    NODE NAME LENGTH
NODENAME DS    CL8                    NODE NAME LEFT JUSTIFIED
USERIDLN DS    XL2                    USERID LENGTH
USERID   DS    CL8                    USERID LEFT JUSTIFIED
NICKLEN  DS    XL2                    NICKNAME LENGTH
NICKNAME DS    CL8                    NICKNAME LEFT JUSTIFIED
UNAMELEN DS    XL2                    LENGTH OF USER NAME
USERNAME DS    CL30                   USER NAME FIELD
RCVFLAG  DS    XL1                    FLAGS
NOTIFY   EQU   X'80'                  REQUEST NOTIFICATION FOR USER
XMITOK   EQU   X'20'                  TRANSMISSION SUCCESSFUL
         DS    XL1                    RESERVED
UPARMLEN DS    XL2                    USER PARM FIELD LENGTH
UPARM    DS    CL30                   USER PARM FIELD
SUSERL   DS    XL2                    SENDERS USERID LENGTH    HD JAN89
SUSERID  DS    CL8                    SENDERS USERID           HD JAN89
SNODEL   DS    XL2                    SENDERS NODE NAME LENGTH HD JAN89
SNODE    DS    CL8                    SENDERS NODE NUMBER      HD JAN89
*
         EJECT                                                 HD JAN87
         IKJCPPL
         EJECT                                                 HD JAN87
         IKJUPT
         EJECT                                                 HD JAN87
         IKJECT
         EJECT                                                 HD JAN87
INMXZ02  CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING INMXZ02,R12
         LR    R8,R1                      SAVE PARM LIST POINTER
         USING PLIST,R8
         GETMAIN R,LV=WORKLEN             GET WORKAREA
         ST    R1,8(R13)                  STANDARD LINKAGE
         ST    R13,4(R1)                  TO GETMAINED SAVEAREA
         LR    R13,R1
         USING WORKAREA,R13
         L     R11,CPPLADDR
         USING CPPL,R11
         L     R10,CPPLECT
         USING ECT,R10
CMDOK    MVC   CPPLAREA(16),0(R11)        INITIALIZE DUMMY CPPL
         LA    R3,CPBUF                   POINT AT COMMAND BUFFER
         ST    R3,CPPLAREA                CONTAINING SEND COMMAND
         MVC   CPBUF(SMSGLEN),SMSGC       INITIALIZE COMMAND BUFFER
         LA    R3,ADRCHAIN                R3 <=== PREVIOUS POINTER
         USING RECEIVER,R4                R4 <=== CURRENT POINTER
         SLR   R5,R5                      CLEAR R5
RCVLOOP  L     R4,0(R3)                   LOAD POINTER TO NEXT
         LTR   R4,R4                      RECEIVER, CHECK FOR END
         BZ    INMXEND
         CLC   SNODE,NODENAME             ARE WE SENDING HERE? HD JAN89
         BNE   INMXEND                    NO LET JES2 HANDLE   HD JAN89
         LA    R5,5                       INITIALIZE
         STH   R5,CPBUF+2                 OFFSET TO OPERAND
         ICM   R5,3,USERIDLN              LOAD USERID LENGTH
         EX    R5,MOVEID                  MOVE USERID PLUS ONE BYTE
MOVEID   MVC   TOUSER,USERID              TO SEND COMMAND
         LA    R5,TOUSER(R5)              ADVANCE PAST USERID
         MVC   0(7,R5),AFTUSER            CLOSE PAREN AND ADD LOGON
         LA    R5,7(R5)                   END OF BUFFER ADDRESS
         LA    R7,CPBUF                   SUBTRACT START OF BUFFER
         SR    R5,R7                      ADDRESS AND PUT IN BUFFER
         STH   R5,CPBUF                   LENGTH FIELD
         LA    R1,CPPLAREA                POINT R1 AT DUMMY CPPL
         LINK  EP=SEND
         LR    R3,R4
         B     RCVLOOP
INMXEND  SLR   R15,R15
         LR    R1,R13                     SAVE WORKAREA ADDRESS
         L     R13,4(R13)                 RELOAD PREVIOUS SAVEAREA
         FREEMAIN R,LV=WORKLEN,A=(1)      POINTER AND FREE WORKAREA
         L     R14,12(R13)                RESTORE RETURN ADDRESS
         LM    R0,R12,20(R13)             RESTORE REGISTERS
         BR    R14                        RETURN TO TRANSMIT COMMAND
         LTORG
SMSGC    DC    H'0'
         DC    H'0'
         DC    CL5'SEND '
         DC    X'7D'                      APOSTROPHE
         DC    CL42'INMX001 MAIL IS AVAILABLE TO RECEIVE FROM '
         DC    X'7D'                      APOSTROPHE
         DC    CL6' USER('
SMSGLEN  EQU   *-SMSGC
AFTUSER  DC    CL7'),LOGON'
         EJECT                                                 HD JAN87
WORKAREA DSECT
SAVEAREA DS    18F
CPPLAREA DS    4F
CPBUF    DS    CL59                                            HD JAN89
TOUSER   DS    CL7
         DS    CL7                        ),LOGON
WORKLEN  EQU   *-WORKAREA
         END   INMXZ02
