         MACRO
&NAYME   TSOENTER &FAKE,&BASE=12,&REGS=YES,&PL=CPPL,&ADBIT=24,         X
               &WORK=0,&DSECT=NO,&SP=0,&CPR=NO,&XA=NO,&RMOD=24
.*
         GBLC  &R,&FSIZE,&POOL
         GBLB  &XAM                                         *HMD 11/86*
         LCLA  &A
         LCLC  &NAME,&FNAME             TO HOLD CSECT NAME    @HMD78174
         LCLB  &OPTS(8),&BIT
         LCLC  &BASEREG
         LCLC  &SECT                    CURRENT CSECT NAME    @HMD78174
         LCLC  &YEAR                                          @HMD86107
         LCLC  &LAB1,&LAB2,&LAB3,&LAB4  LABEL VARIABLES       @HMD86107
.*
&FNAME   SETC  'GM'.'&SYSNDX'           SET GETMAIN NAME    *HMD 11/86*
&FSIZE   SETC  '&FNAME'.'S'             SET SIZE EQUATE LABEL
&POOL    SETC  '&SP'
&XAM     SETB  ('&XA' EQ 'YES')         SET XA FLAG         *HMD 11/86*
&YEAR    SETC  '19'.'&SYSDATE'(7,8)                           @HMD76107
&SECT    SETC  '&SYSECT'                SET CONTROL SECT NAME @HMD78174
&NAME    SETC  '&NAYME'                 HOLD CSECT NAME       @HMD78174
TSODSECT DSECT ,                        DEFINE NAME OF DSECT
TSOSAVE  DS    18F .                    FIRST SAVE AREA
         CPPL  DSECT=NO                 PARAMETER LIST
&A       SETA  1
.A1      AIF   (&A GT N'&PL).L1
&OPTS(1) SETB  ((&OPTS(1))OR('&PL(&A)' EQ 'MESSAGE'))
&OPTS(2) SETB  ((&OPTS(2))OR('&PL(&A)' EQ 'PARSE'))
&OPTS(3) SETB  ((&OPTS(3))OR('&PL(&A)' EQ 'IO'))
&OPTS(4) SETB  ((&OPTS(4))OR('&PL(&A)' EQ 'CSCAN'))
&OPTS(5) SETB  ((&OPTS(5))OR('&PL(&A)' EQ 'DAIR'))
&A       SETA  &A+1
         AGO   .A1
.L1      ANOP
TSOECB   DS    F .                      DEFINE COMMAND ECB
.LL1     AIF   (NOT &OPTS(1)).L2
         MNOTE 0,'MESSAGE PARM LIST UNDEFINED'
.L2      AIF   (NOT &OPTS(2)).L3
         PPL   DSECT=NO                 PARSE PARM LIST
.L3      AIF   (NOT &OPTS(3)).L4
         IOPL  DSECT=NO                 I/O PARAMETER LIST
.L4      AIF   (NOT &OPTS(4)).L5
         CSPL  DSECT=NO                 CMD SCAN PARM LIST
.L5      AIF   (NOT &OPTS(5)).L6
         DAPL  DSECT=NO                 DAIR PARM LIST
.L6      ANOP
TSOWORK  DS    0D .                     ALIGN TO DOUBLE-WORD
TSOUSER  EQU   * .                      GIVE ALTERNATE NAME
         AIF   ('&REGS' NE 'YES').NOREGS
         EJECT
         REGS
&R       SETC  'R'
.NOREGS  AIF   ('&DSECT' EQ 'NO').GEN
         MEXIT
.GEN     EJECT                                                @HMD78174
         AIF   ('&NAME' EQ '').NONAME                         @HMD78174
&NAME    CSECT                                                @HMD78174
         AGO   .GEN1                                          @HMD78174
.NONAME  AIF   ('&SECT' NE '').GEN0                           @HMD78174
         MNOTE 16,'CSECT NAME MISSING, GENERATION TERMINATED'
         MEXIT                                                @HMD86107
.GEN0    ANOP                                                 @HMD78174
&SECT    CSECT                                                @HMD78174
&NAME    SETC  '&SECT'            SET NAME TO CSECT NAME      @HMD78174
.GEN1    ANOP
         AIF   (NOT &XAM).XAM1                              *HMD 11/86*
         AIF   ('&ADBIT' EQ '24' OR '&ADBIT' EQ '31').XAM2  *HMD 11/86*
         MNOTE 20,'MVS/XA ADDRESSING MODE &ADBIT IS INVALID'
         MEXIT                                              *HMD 11/86*
.XAM2    ANOP                                               *HMD 11/86*
&NAME    AMODE &ADBIT                                       *HMD 11/86*
         MNOTE *,'MVS/XA ADDRESSING MODE IS &ADBIT'         *HMD 11/86*
         AIF   ('&ADBIT' EQ '24').XAM3                      *HMD 11/86*
         SPLEVEL SET=2    .       SET XA MODE FOR 31 BIT    *HMD 11/86*
         AIF   ('&RMOD' EQ '24' OR '&RMOD' EQ '31').XAM0
         AIF   ('&RMOD' EQ 'ANY').XAM0
         MNOTE 20,'MVS/XA RESIDENCY MODE OF &RMOD IS INVALID'
.XAM0    ANOP
         AIF   ('&RMOD' EQ '24').XAM3
&NAME    RMODE ANY                                          *HMD 11/86*
         AGO   .XAM1                                        *HMD 11/86*
.XAM3    ANOP                                               *HMD 11/86*
&NAME    RMODE 24                                           *HMD 11/86*
.XAM1    ANOP                                               *HMD 11/86*
&LAB1    SETC  'IHB'.'&SYSNDX'    SET LABEL NAME            *HMD 04/86*
&LAB2    SETC  'IHC'.'&SYSNDX'    SET LABEL NAME            *HMD 04/86*
&LAB3    SETC  'IHD'.'&SYSNDX'    SET LABEL NAME            *HMD 04/86*
         SPACE 1                                            *HMD 04/86*
         B     &LAB1-&NAME.(,15)        BRANCH AROUND IDS     @HMD78174
         DC    AL1(&LAB1-&LAB2)         LENGTH OF ID AREA   *HMD 04/86*
&LAB2    DC    C'&NAME  &SYSDATE'       PGM ID AND DATE     *HMD 04/85*
         AIF   ('&CPR' EQ 'NO').NOCPR   BYPASS COPYRIGHT    *HMD 04/85*
         DC    C' COPYRIGHT (C) &YEAR HOWARD M. DEAN'       *HMD 04/85*
.NOCPR   ANOP                                               *HMD 04/85*
         SPACE 1                                            *HMD 04/86*
&LAB1    DS    0H                       POINT TO BRANCH TO    @HMD78174
         STM   14,12,12(13)             SAVE REGISTERS      *HMD 04/86*
         AIF   (T'&BASE NE 'N').LUSE1
&BASEREG SETC  '&R&BASE'
         AGO   .LUSE2
.LUSE1   ANOP
&BASEREG SETC  '&BASE'
.LUSE2   ANOP
         LUSE  &NAME,&BASEREG           PERMANANT BASE        @HMD78174
         CNOP  0,4                      ALIGNMENT             @HMD78174
         L     &BASEREG,&LAB3-&NAME.(,15)
*                                       ESTABLISH BASE REG  *HMD 04/86*
         B     &LAB3+L'&LAB3            BRANCH AROUND ADDR  *HMD 04/86*
         SPACE 1
&LAB3    DC    A(&NAME)                 CSECT ADDRESS       *HMD 04/86*
         SPACE 1
         LR    &R.2,1 .                 SAVE CPPL POINTER
&FSIZE   EQU   (((TSOUSER-TSODSECT)+(&WORK)+7)/8)*8 .  AREA LENGTH
         AIF   (&XAM).X003
&FNAME   GETMAIN R,LV=&FSIZE,SP=&SP
         AGO   .X004
.X003    ANOP
         SPACE 1
&FNAME   GETMAIN RU,LV=&FSIZE,SP=&SP,LOC=RES
.X004    ANOP
         SPACE 1
         ST    13,4(,1) .               CHAIN
         ST    1,8(,13) .               SAVE
         LR    &R.13,1 .                AREAS
         LUSE  TSODSECT,&R.13 .         ESTABLISH ADDRESSABILITY
         MVC   CPPL(CPPLLEN),0(&R.2) .  COPY PARM LIST
         AIF   (NOT &OPTS(2)).NPARSE
         MVC   PPLUPT(4),CPPLUPT .      USER PROFILE TAB
         MVC   PPLECT(4),CPPLECT
         MVC   PPLCBUF(4),CPPLCBUF      INIT CMD BUFFER       @HMD78174
         LA    &R.0,PPLRANS .              LOAD ANSWER PLACE
         ST    &R.0,PPLANS
         LA    &R.0,TSOECB
         ST    &R.0,PPLECB
&BIT     SETB  1
.NPARSE  AIF   (NOT &OPTS(3)).NIO
         MVC   IOPLECT(4),CPPLECT
         MVC   IOPLUPT(4),CPPLUPT
         AIF   (&BIT).Q1
         LA    &R.0,TSOECB
.Q1      ST    &R.0,IOPLECB
&BIT     SETB  1
.NIO     AIF   (NOT &OPTS(5)).NDAIR
         MVC   DAPLUPT(4),CPPLUPT
         MVC   DAPLECT(4),CPPLECT
         MVC   DAPLPSCB(4),CPPLPSCB
         AIF   (&BIT).Q2
&BIT     SETB  1
         LA    &R.0,TSOECB
.Q2      ST    &R.0,DAPLECB
.NDAIR   AIF   (NOT &OPTS(4)).NCSCAN
         MVC   CSPLUPT(4),CPPLUPT
         MVC   CSPLECT(4),CPPLECT
         MVC   CSPLCBUF(4),CPPLCBUF     INIT CMD BUFFER       @HMD78174
         AIF   (&BIT).Q3
&BIT     SETB  1
         LA    &R.0,TSOECB
.Q3      ST    &R.0,CSPLECB
.NCSCAN  INUSE
         EJECT
.MEND    MEND
