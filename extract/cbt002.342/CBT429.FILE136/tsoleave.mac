         MACRO
&NAME    TSOLEAVE &SPLV=TSOLEN,&ISN=,&RETCODE=0
         GBLC  &R,&FSIZE,&POOL
         GBLB  &XAM                     XA-MODE GLOBAL      *HMD 11/86*
         LCLB  &RTYPE
.*       TSOLEAVE MACRO                  BRUCE M. DANE
.*       LAST UPDATED:                  06 NOV 1973
.*       LAST UPDATED:                  24 NOV 1986         *HMD 11/86*
.*       HOWARD M. DEAN - AMERICAN PRESIDENT LINES          *HMD 11/86*
.*       MACRO GENERATES RE-ENTRANT CODE
.*
&NAME    LR    1,&R.13 .                LOAD RETURN ADDRESS
         L     &R.13,4(0,&R.13) .       LOAD REGISTER POINTER
         AIF   ('&RETCODE' EQ '').FREE1
         AIF   ('&RETCODE'(1,1) NE '(').FREE1
         ST    &RETCODE(1),16(,&R.13) . SAVE RETURN CODE IN R15 PLACE
&RTYPE   SETB  1
.FREE1   ANOP
*        RETURN DYNAMIC SAVE AREA TO SYSTEM
         FREEMAIN RU,LV=&FSIZE,A=(1),SP=&POOL ISSUE FREEMAIN
.RETTEST AIF   (&RTYPE).R0
         AIF   ('&RETCODE' EQ '').RETEQ0 * MESSAGE, ASSUME ZERO
         AIF   (T'&RETCODE NE 'N').REGLD1 * RETURN NUMBER?
         AIF   (&RETCODE EQ 0).R0       * RETCODE=0
         AGO   .REGLA                   * LOAD ADDRESS ON RETCODE
.RETEQ0  MNOTE 0,'*** WARNING *** RETCODE ASSUMED ZERO (0)'
         AGO   .R0
.REGLD1  L     &R.15,&RETCODE .         LOAD SAVED RETURN CODE
         AGO   .BR14
.REGLA   LA    &R.15,&RETCODE .         LOAD REQUESTED RETURN CODE
         AGO   .BR14
.R0      LM    14,12,12(13) .           RELOAD CALLERS' REGS
         AIF   (&RTYPE).R00
         SLR   15,15 .                  CLEAR RETURN CODE
         AGO   .R00
.BR14    LM    0,12,20(13) .            LOAD SAVED REGS
         L     14,12(0,13) .            AND RETURN
.R00     ANOP
         BR    14 .                     RETURN TO CALLER
         AGO   .ISNCHEK
.ISNCHEK AIF   ('&ISN' EQ '').MEND
         &ISN
.MEND    MEND
