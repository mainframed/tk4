HTIM     TITLE 'DISPLAY THE TIME OF DAY AND DATE FOR TSO'
***********************************************************************
*                                                                     *
* MODULE NAME = HTIME                                                 *
*                                                                     *
* STATUS = COMMAND PROCESSOR                                          *
*                                                                     *
* AUTHOR = HOWARD M. DEAN                                             *
*          SYNTEX USA M/S A5-5                                        *
*          3401 HILLVIEW AVENUE                                       *
*          PALO ALTO, CA 94303                                        *
*          (415) 852-1792
*                                                                     *
* FUNCTION = PUT OUT AN INFORMATIONAL MESSAGE INDICATING THE DATE     *
*            AND TIME OF DAY VIA THE PUTLINE FACILITY OF TSO.         *
*                                                                     *
* ENTRY POINTS = HTIME                                                *
*                                                                     *
* EXIT NORMAL =  EXIT TO MVS IS ALWAYS WITH A RETURN CODE OF ZERO     *
*                                                                     *
* EXIT ERROR = NO ERROR EXIT POINTS                                   *
*                                                                     *
* RESTRICTIONS = NONE                                                 *
*                                                                     *
* REGISTER-CONVENTIONS =                                              *
*                                                                     *
*  REGISTER 0: USED BY SYSTEM - UNPREDICTABLE                         *
*  REGISTER 1: PARAMETER REGISTER                                     *
*              USED BY SYSTEM AND SUBROUTINES - UNPREDICTABLE         *
*                                                                     *
*  REGISTERS 2-12: WORK REGISTERS AVAILABLE FOR USE BY PROGRAM        *
*  REGISTER  13 ADDRESS OF AN 18 WORD SAVE AREA                       *
*               AND POINTER TO WORK AREA/TSO DSECTS                   *
*  REGISTER  14 RETURN ADDRESS                                        *
*  REGISTER  15 ENTRY POINT OF HTIME                                  *
*                                                                     *
* ATTRIBUTES -  HTIME IS REENTERABLE AND REFRESHABLE                  *
*                                                                     *
* MACROS = SAVE, RETURN, GETMAIN, FREEMAIN, CALL, REGS,               *
*          LUSE, LDROP, CPPL, IOPL, TSOENTER, TSOLEAVE                *
*                                                                     *
* EXTERNAL                                                            *
*    ROUTINES =   HMDCHRON (LINKED WITH PROGRAM)                      *
*                 IKJPUTL  (PUTLINE SUBROUTINE VIA CVT)               *
*                                                                     *
* INSTALLED 05/28/85 - HOWARD M. DEAN                                 *
***********************************************************************
HTIME    TSOENTER  PL=(CPPL,IO),REGS=YES,WORK=WORKLEN
         SPACE 2
         LA    R0,TSOUSER          R0 -> USER AREA
         LA    R1,WORKLEN          R1 = LENGTH OF AREA
         SR    R15,R15             R15 = 0
         MVCL  R0,R14              CLEAR WORKAREA
         SPACE 2
         MVC   OUTONE(ONEMSGL),ONEMSG
         MVC   OUTTWO(TWOMSGL),TWOMSG
         MVC   OLD,=F'1'           ONE MESSAGE SEGMENT
         SPACE 1
         CALL  HMDCHRON,(TODAY,HMDTIME),VL,MF=(E,CALLEX)
         SPACE 1
         MVC   ONEJUL(L'HMDJULDT),HMDJULDT
         MVC   ONETIM(L'HMDTIMER),HMDTIMER
         SPACE 2
         MVC   TWODAY(L'HMDDAYNM),HMDDAYNM
         MVC   TWODATE(L'HMDDAYYR),HMDDAYYR
         MVC   TWOTIME(11),HMDTIMED
         SPACE 1
         LA    R1,OUTONE            PUT OUT FIRST MESSAGE
         BAL   R14,PUTOUT           CALL SUBROUTINE
         LA    R1,OUTTWO            PUT OUT SECOND MESSAGE
         BAL   R14,PUTOUT           CALL SUBROUTINE
         B     EOJ                  RETURN TO MVS
         EJECT
PUTOUT   DS    0H                 PUTLINE SUBROUTINE
         ST    R14,SAVELINK       SAVE LINK REGISTER
         ST    R1,OLDADDR         SAVE MESSAGE ADDRESS
         XC    PTPB(PTPBLEN),PTPB CLEAR THE PARM BLOCK
         XC    TSOECB,TSOECB      CLEAR THE EVENT CONTROL BLOCK
         SPACE 2
         PUTLINE PARM=PTPB,OUTPUT=(OLD,TERM,SINGLE,INFOR),             X
               MF=(E,IOPL)
         SPACE 2
         L     R14,SAVELINK       RETSORE LINK REGISTER
         BR    R14                RETURN TO CALLER
         EJECT
EOJ      TSOLEAVE ISN=LTORG,RETCODE=0
         SPACE 2
*
* PROGRAM CONSTANTS
*
ONEMSG   DS    0C
         DC    AL2(ONEMSGL),AL2(00)
         DC    C'TSO036I '
         DC    C'DATE='
         DC    C'XX.XXX'
         DC    C'      TIME='
         DC    C'XX:XX:XX:XX'
ONEMSGL  EQU   *-ONEMSG
         SPACE 2
TWOMSG   DS    0C
         DC    AL2(TWOMSGL),AL2(00)
         DC    C'TSO037I '
         DC    C'WEDNESDAY'       NAME OF DAY
         DC    C' '
         DC    C'XXXXXXXXXXX'     DATE MMM DD,YYYY
         DC    C' '
         DC    C'XXXXXXXXXXX'     XX:XX:XX  AM/PM
TWOMSGL  EQU   *-TWOMSG
         EJECT
TSODSECT DSECT
*
* LIST FORM MACRO INSTRUCTIONS
*
PTPB     PUTLINE MF=L             PUTLINE MACRO - LIST FORM
PTPBLEN  EQU   *-PTPB
         SPACE 2
CALLEX   CALL  ,(0,0),MF=L        CALL MACRO - LIST FORM
         SPACE 2
*
* PROGRAM VARIABLES
*
OLD      DS    F                  NUMBER OF MESSAGE SEGMENTS
OLDADDR  DS    A                  ADDRESS OF FIRST SEGMENT
         SPACE 2
SAVELINK DS    F                  R14 LINK REGISTER SAVE AREA
         SPACE 1
TODAY    DS    F                  TODAY'S DATE
HMDTIME  DS    0CL72              AREA RETURNED BY HMDCHRON
*******************************************************************
** THIS 72 BYTE AREA MAPS THE RETURN AREA FOR THE 'HMDCHRON'     **
** SUBROUTINE. THE TIME OF DAY AND DATE IS RETURNED AS MAPPED.   **
** A FULLWORD DATE FIELD CAN BE PASSED TO THE ROUTINE AND THAT   **
** VALUE WILL BE USED IN THE CALCULATIONS. IF ZEROED, THE        **
** CURRENT DATE (VIA SVC 11)  WILL BE USED IN THE COMPUTATIONS.  **
** THE ADDRESS OF THIS AREA IS IN THE SECOND WORD OF THE PLIST.  **
*******************************************************************
HMDJULDT DS    CL6                JULIAN DATE (FORM 'YY.DDD')
HMDENGDT DS    CL8                GREGORIAN DATE (FORM MM/DD/YY)
HMDMONTH DS    CL9                MONTH NAME
HMDDAYYR DS    CL11               DATE IN FORM MMM DD,YYYY
HMDDAYNO DS    H                  HALF WORD DAY NUMBER
HMDDAYNM DS    CL9                DAY OF THE WEEK
HMDTIMER DS    CL11               TIME OF DAY (24 HR FORM HH.MM.SS.HH)
HMDTIMED DS    CL8                TIME OF DAY (12 HR FORM HH.MM.SS)
HMDAMPM  DS    CL3                AM/PM INDICATOR (' AM' OR ' PM')
HMDPAD   DS    CL(72-(*-HMDTIME)) PAD TO 72 BYTES
         SPACE 2
OUTONE   DC    AL2(00),AL2(00)    LENTH/OFFSET
ONEMID   DC    C'TSO036I '
         DC    C'DATE='
ONEJUL   DC    C'XX.XXX'
         DC    C'      TIME='
ONETIM   DC    C'XX:XX:XX:XX'
         SPACE 2
OUTTWO   DC    AL2(00),AL2(00)    LENGTH/OFFSET
TWOMID   DC    C'TSO037I '
TWODAY   DC    C'WEDNESDAY'       NAME OF DAY
         DC    C' '
TWODATE  DC    C'XXXXXXXXXXX'     DATE MMM DD,YYYY
         DC    C' '
TWOTIME  DC     C'XXXXXXXXXXX'     XX:XX:XX  AM/PM
         SPACE 1
WORKLEN  EQU   ((*-TSOUSER+7)/8)*8   ALIGN TO DOUBLEWORD
         END   HTIME
