         TITLE 'TLABEL PROGRAM FOR AFDSC 360'
*---------------------------------------------------------------------*
*                  T   L   A   B   E   L                              *
*                                                                     *
*        TLABEL IS STARTED THROUGH THE CONSOLE AND WILL LABEL TAPES   *
*        UNDER THE FOLLOWING CRITERON:                                *
*             1. WITH A  PARM PASSED TO THE PROGRAM ONLY AFDSC 360    *
*                TAPES ARE LABELED I.E. 6 NUMERIC DIGITS BEGINNING    *
*                WITH 08  (AFDSC STANDARD).                           *
*             2. IF A PARM IS NOT DETECTED, THEN A 6 DIGIT ALPHA-     *
*                NUMERIC VOLSER CAN BE WRITTEN. OR THEY CAN WRITE A   *
*                TAPEMARK TO THE TAPE (I.E. TRANSIT TAPES THAT        *
*                LEAVE THE 360).                                      *
*                                    JIM MARSHALL                     *
*                                                                     *
*        NOTE: WORKS ON MVS TOO.                                      *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
TLABEL   AMODE 24
TLABEL   RMODE 24
TLABEL   CSECT
         SAVE  (14,12),,TLABEL_AFDSC_&SYSDATE &SYSTIME
         LR    R12,R15
         USING TLABEL,R12
         LR    R15,R13
         LA    R13,SAVE
         ST    R15,4(R13)
         ST    R13,8(R15)
         SPACE
         L     R2,0(R1)                CK FOR PRESENCE
         LH    R4,0(R2)                OF
         LTR   R4,R4                   PARM TO MEAN TRANSIT TAPE
         BNZ   THREE60
         MVI   FLAG,X'FF'              TRANSIT TAPE PROCESSING
         LA    R10,MSG0                SO WE BRANCH TO CORRECT PLACE
MSG0     EQU   *
         MVC   VSN(6),BLANKS           CLEAR THE FIELD
         WTOR  'CMI0341A ENTER TRANSIT VOLSER OR ''TM'' FOR TAPEMARK  OX
               R ''END'' TO TERMINATE.',VSN,6,ECB
         WAIT  ECB=ECB
         MVC   ECB(4),=F'0'            CLEAR THE ECB
         CLC   VSN(6),=C'END   '       DO WE TERMINATE
         BE    STOP
         CLC   VSN(6),=CL6'TM'         DO WE TAPEMARK
         BE    RITETM
         LA    R6,VSN
         LA    R5,6
CK1      EQU   *
         CLI   0(R6),X'40'             CAN NOT HAVE BLANK
         BE    ERR1
         LA    R6,1(R6)                BUMP PTER
         BCT   R5,CK1
         MVI   TMSW,X'00'              INSURE SL PROCESSING
         B     VALID
*---------------------------------------------------------------------
THREE60  EQU   *
         LA    R10,MSG360              SETUP R10
MSG360   EQU   *
         MVC   VSN(6),BLANKS           CLEAR THE FIELD
         WTOR  'CMI0340A ENTER 3032 VOLSER OR ''END'' TO TERMINATE.',  X
               VSN,6,ECB
         WAIT  ECB=ECB
         MVC   ECB(4),=F'0'            CLEAR THE ECB NOW
         CLC   VSN(6),=CL6'END'        DO WE TERMINATE
         BE    STOP
         CLC   VSN(6),=CL6'TM'         JUST IN CASE
         BE    TMERR
         CLC   VSN(2),=C'08'           CK 1ST 2 DIGITS
         BNE   NOT360
         LA    R5,4
         LA    R6,VSN
         LA    R6,2(R6)                ALREADY CKED 1ST 2 DIGITS
LOOP     EQU   *
         TM    0(R6),X'F0'             IS IT NUMERIC
         BNO   NOGOOD
         LA    R6,1(R6)                BUMP PTER
         BCT   R5,LOOP
         MVI   TMSW,X'00'              MAKE SURE NO TAPEMARK PROCESS
         B     VALID
VALID    EQU   *
         RDJFCB (TAPE)                 GET JFCB
         CLI   TMSW,X'FF'              ARE WE DOING TAPEMARK
         BE    V2
         MVC   JFCB(26),SLWORDS        MOVE IN CORRECT DSNAME FOR OP
         MVC   JFCB+118(6),VSN         MOVE IN VOLSER TO MOUNT
         B     V3
V2       EQU   *
         MVC   JFCB(26),TMWORDS        OR OTHER DSNAME
         MVC   JFCB+118(6),=C'TRANST'
V3       EQU   *
         MVI   JFCB+66,X'10'           BLP PROCESSING
         OPEN  (TAPE,(OUTPUT)),TYPE=J  THE OLE TYPE J OPEN
         CLI   TMSW,X'FF'              DO WE WRITE VOL1 HEADER
         BE    CLOSE
         MVC   SN(6),VSN               MOVE VOLSER TO VOL1
         PUT   TAPE,HDR                WRITE IT
CLOSE    CLOSE (TAPE)
         BR    R10                     BACK TO CORRECT WTOR
*----------------------------------------------------------------------
ERR1     EQU   *
         MVC   SN3,VSN
         MVC   WTOPER+8(54),MSSG4      LOAD BAD MESSAGE
         B     WTOPER
NOT360   EQU   *
         MVC   SN2(6),VSN
         MVC   WTOPER+8(60),MSSG3
         B     WTOPER
NOGOOD   EQU   *
         MVC   SN1(6),VSN
         MVC   WTOPER+8(61),MSSG2
         B     WTOPER
         DS    0F
WTOPER   EQU   *
         WTO   '                                                       $
                             '
         MVI   WTOPER+8,X'40'
         MVC   WTOPER+9(60),WTOPER+8
         BR    R10                     GO TO PROPER WTOR
RITETM   EQU   *
         MVI   TMSW,X'FF'              SETUP FOR TAPEMARK
         B     VALID
TMERR    EQU   *
         WTO   'CMI0341I TAPEMARKS CAN NOT BE WRITTEN TO 3032 TAPES, ONX
                LY LABELS!'
         BR    R10
STOP     EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
TAPE     DCB   DSORG=PS,MACRF=(PM),RECFM=F,LRECL=80,BLKSIZE=80,        X
               DDNAME=TAPE,EXLST=MYJFCB
SAVE     DC    18F'0'               SAVE AREA
*----------------------------------------------------------------------
HDR      DS    0CL80
VOL1     DC    CL4'VOL1'
SN       DC    CL6'0'
SECURE   DC    C'0'
         DC    CL30' '
OWNERID  DC    C'AFDSC/WASH'
         DC    CL29' '
ECB      DC    F'0'
VSN      DC    CL6' '
BLANKS   DC    CL6' '
MYJFCB   DC    X'87'
         DC    AL3(JFCB)
JFCB     DC    44F'0'
SLWORDS  DC    CL26'ASMI00U.TAPE.TO.BE.LABELED'
TMWORDS  DC    CL26'ASMI00U.TAPE.FOR.TAPEMARK'
FLAG     DC    X'00'
TMSW     DC    X'00'
*----------------------------------------------------------------------
MSSG2    DS    0CL61
         DC    CL17'CMI0340I  REPLY '''
SN1      DC    CL6' '
         DC    CL13''' IS INVALID,'
         DC    CL25' VOLSER MUST BE NUMERIC!!'
*----------------------------------------------------------------------
MSSG3    DS    0CL60
         DC    CL17'CMI0340I  REPLY '''
SN2      DC    CL6' '
         DC    CL13''' IS INVALID,'
         DC    CL24' NOT 3032 VOLSER(08XXXX)'
*----------------------------------------------------------------------
MSSG4    DS    0CL54
         DC    CL17'CMI0340I  REPLY '''
SN3      DC    CL6' '
         DC    CL13''' IS INVALID,'
         DC    CL18' MUST BE NO BLANKS'
TRANS    DC    CL7'TRANSIT'
*----------------------------------------------------------------------
         LTORG
         END
