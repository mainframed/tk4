         TITLE 'BLOKHF-BAL PARM FORMATTING PGM FOR BLOKHD'
***********************************************************************
*                            B  L  O  K  H  F                         *
*        ASSEMBLER LANGUAGE INTERFACE ROUTINE TO FORMAT FROM A        *
*        FORTRAN CALL A PARM PARAMETER LIST TO THE SFI BLOCK LETTER-  *
*        ING ROUTINES. THE FORMAT OF THE FORTRAN CALL IS AS FOLLOWS:  *
*                                                                     *
*                  CALL  BLOKHF(XX,'  --------   ')                   *
*                                                                     *
*             WHERE XX IS THE NUMBER OF CHARACTERS WITHIN SINGLE      *
*        QUOTES. THIS IS THE NORMAL PARAMETER LIST THAT IS CON-       *
*        STRUCTED(LIKE IN PARM FIELD PASSING) TO BLOKHD.              *
*                                                                     *
*        I.E.                LA    R1,ADLIST                          *
*                            CALL  BLOKHD                             *
*                              .                                      *
*                              .                                      *
*                  ADLIST    DC    A(LEN)                             *
*                            DC    X'80',AL3(DATA)                    *
*                              .                                      *
*                  LEN       DC    H'XX'     LENGTH IN BYTES OD DATA  *
*                  DATA      DC    C' -- '   VALUE IN SINGLE QUOTES   *
*                                                                     *
*        COURTESY - JIM MARSHALL, CAPT, USAF-RET                      *
***********************************************************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
BLOKHF   AMODE 24
BLOKHF   RMODE 24
BLOKHF   CSECT
         SAVE  (14,12),,BLOKHF_&SYSDATE
         LR    R12,R15
         USING BLOKHF,R12
         LR    R15,R13
         LA    R13,SAVE
         ST    R15,4(R13)
         ST    R13,8(R15)
         PRINT ON,NOGEN
         L     R5,0(R1)
         L     R5,0(R5)
         STH   R5,LEN              SAVE LEN FOR PARMS
         ST    R1,TEMP
         CLI   TEMP,X'80'          SHOULD NOT BE LAST
         BNE   CONT
         ABEND 4065
CONT     EQU   *
         MVC   TEMP(4),4(R1)
         L     R5,4(R1)            PT TO SECOND VALUE IN CALL
         CLI   TEMP,X'80'
         BE    CONT1
         ABEND 4066,DUMP
CONT1    EQU   *
         LA    R10,PARMAREA
         XR    R3,R3
         LH    R3,LEN
         BCTR  R3,0
         EX    R3,MVCIN
         LA    R1,ADLIST
         L     R15,=V(BLOKHD)
         BALR  R14,R15
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE
         DS    0F
MVCIN    MVC   0(0,R10),0(R5)
SAVE     DC    18F'0'
ADLIST   DC    A(LEN)
         DC    A(PARMAREA)
TEMP     DC    F'0'
         DS    0F
         DC    X'0000'
PARMS    EQU   *
LEN      DC    H'0'
PARMAREA DC    CL80' '
         LTORG
         END
