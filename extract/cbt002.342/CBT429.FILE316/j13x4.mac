         TITLE '2ISG EXIT-4  JECL  CONTROL CARD SCAN ROUTINE'
*---------------------------------------------------------------------*
*
*        JES2 EXIT 4.  JCL AND CONTROL CARD SCAN ROUTINE.
*
*        COURTESY: JIM MARSHALL, CAPT, USAF (RET)
*
*     A. THIS EXIT SCANS JECL CARDS AND THROWS AWAY CERTAIN INVALID
*        JECL CARDS. THESE CARDS WERE USED WITH NIH-WYLBUR AND TO KEEP
*        THE TRANSITION SMOOTH TO A SUPPORTED VERSION OF WYLBUR, WE
*        MUST TRASH THE INVALID CARDS. THESE JECL (SUPPOSED) ARE THE
*        THE  /*KEYWORD  AND /*CONTROL  JECL CARDS.
*
*     B. REGISTERS ON ENTRY ARE:
*
*        R0  - 0  IMPLIES ADDR OF JECL STATEMENT
*              4  IMPLIES ADDR OF JCL STATEMENT
*        R1     - ADDR OF 3 WORD PARAMETER LIST:
*                   WORD 1-ADDRESS OF STATEMENT BUFFER.
*                   WORD 2-ADDR. OF EXIT FLAG BYTE, RXITFLAG, IN PCE.
*                      X'01' ==> JCL STATEMENT
*                      X'02' ==> CONTROL STATEMENT
*                      X'04' ==> JOB STATEMENT
*                      X'08' ==> CONTINUE STATEMENT
*                      X'10' ==> EXIT SUPPLIED NEXT STATEMENT
*                      X'20' ==> ERROR MSG. SUPPLIED BY EXIT
*                   WORD 3-ADDRESS OF THE JCTXWRK FIELD IN THE JCT.
*        R2-R9  - NOT SPECIFIED
*        R10    - ADDR OF JCT
*        R11    - ADDR OF HCT
*        R12    - NOT SPECIFIED
*        R13    - ADDR OF  HASPRDR PCE
*        R14    - RETURN ADDR
*        R15    - ENTRY POINT ADDR
*
*     C. REGISTER USAGE IN THIS EXIT:
*        R1-R2  - VOLATILE TRT REGISTERS.
*        R3     - USED TO HOLD COUNT REMAINING FOR TRT
*        R4     - USED TO HOLD STARTING POINT FOR TRT
*        R5     - QUOTE COUNT REGISTER IN TRT LOOP
*        R6-R7  - USED AS HOLDING REGISTERS FOR REG. 3 AND 1
*        R8     - USED TO HOLD ORIGINAL REG. 0 VALUE (LO BYTE)
*        R9     - USED TO HOLD ORIGINAL R1.  (--> 3 WORD PARM LIST)
*                 HI BYTE OF REG. 9 USED TO HOLD FLAG-
*                      X'80'-ORIGINAL CARD WAS A COMMENT
*                      X'40'-MAKE CARD A COMMENT (RC=8)
*                      X'20'-MAKE CARD A CONTROL CARD ("/*...")
*        R10    - ADDR OF JCT
*        R11    - ADDR OF HCT
*        R12    - MY BASE REG
*        R13    - ADDR OF PCE
*        R14    - RETURN ADDR
*        ALL OTHER REGISTERS ARE USED AS LOCAL WORK REGISTERS,
*        LINKAGE REGISTERS, MACRO PARAMETER REGISTERS, ETC.
*
*     C. RETURN FROM EXIT004.
*        RETURN CODES FOR R15 ARE:
*          0 - CONTINUE WITH NORMAL EXIT AND HASPRDR PROCESSING
*          4 - IGNORE ADDITIONAL EXITS, CONTINUE PROCESSING
*          8 - FOR CONTROL STATEMENTS-CONVERT TO A JCL COMMENT
*              AND SKIP HASPRCCS PROCESSING.  WRITE STMT. TO JCL
*              DATA SET.  FOR JCT STMT., PERFORM NORMAL PROCESSING.
*         12 - CANCEL THE JOB-OUTPUT (JCL) IS PRODUCED.
*         16 - CANCEL THE JOB-NO OUTPUT IS PRODUCED.
*
*     D. INSTALLED: 26 AUG 84
*
*     E. CHANGE LOG:
*
*         6NOV85-CONVERT THE CODE FOR JES2/SP1.3.4. USE CONDITIONAL
*                ASSEMBLY FOR EASE OF MAINTENANCE FOR NOW.       JDM1
*---------------------------------------------------------------------*
         SPACE
         GBLB  &SP134                                            JDM1
&SP134   SETB  1                   1 - SP1.3.4 / 0 - SP1.3.2     JDM1
         AIF   (NOT &SP134).SKIP10                               JDM1
         COPY  $HASPGBL
ISG4    $MODULE $BUFFER,                                               X
               $JCT,                                                   X
               $JQE,                                                   X
               $HASPEQU,                                               X
               $PCE,                                                   X
               $CAT,                                                   X
               $DCT,                                                   X
               $HCT,                                                   X
               $PDDB,                                                  X
               $MIT,                                                   X
               $XIT,                                                   X
               $RDRWORK,                                               X
               $JOE,                                                   X
               $PPPWORK,                                               X
               $CMB
         PRINT ON,GEN
.SKIP10  ANOP                                                    JDM1
         AIF   (&SP134).SKIP15                                   JDM1
         COPY  $HASPGEN
         PRINT ON,NOGEN
ISG4     $MODULE                  CREATE THE MIT
         SPACE
         PRINT ON,NOGEN           TURN OFF FOR DSECT DEFINITIONS
         $SYSPARM (ON,GEN,DATA,YES,YES)
         $BUFFER DOC=YES
         $JCT    DOC=YES
         $JQE    DOC=YES
         $PCE    DOC=YES
         $CAT    DOC=YES
         $DCT    DOC=YES
         $HCT    DOC=YES
         $PDDB   DOC=YES
         $TAB    DOC=YES
         $IOT    DOC=YES
         $JOE    DOC=YES
         $XIT    DOC=YES
         $EXITPL DOC=YES
         $LRC    DOC=YES
         $MIT    DOC=YES
         PRINT ON,NOGEN
         $RDRWORK DOC=YES
         PRINT ON,GEN
.SKIP15  ANOP                                                    JDM1
         EJECT
         AIF   (NOT &SP134).SKIP20                               JDM1
J13X4   $ENTRY BASE=R12
.SKIP20  ANOP                                                    JDM1
         AIF   (&SP134).SKIP25                                   JDM1
J13X4   $ENTRY BASE=R12,CSECT=YES
.SKIP25  ANOP                                                    JDM1
         SPACE
        $SAVE
         SPACE
         LR    R12,R15            R12 WILL BE OUR BASE REGISTER
         USING JCTDSECT,R10
         SPACE
         USING PCEDSECT,R13
         SPACE
         L     R2,0(R1)           POINT TO STATEMENT IMAGE
         SPACE
         TM    RXITFLAG,RCONTCRD  IS IT A CONT CARD ?
         BO    EX4RET0            IF SO, RETURN IT TO JES2
         SPACE
         L     R3,8(R1)           GET ADDRESS OF JCTXWRK
         LTR   R3,R3              MAKE SURE IT'S NON-ZERO
         BZ    EX4RET0            IF ZERO, GO BACK RC=0
         SPACE
         LTR   R10,R10            ARE WE IN A JOB (DOUBLE CHECK)
         BZ    EX4RET0            BRANCH IF NOT
         SPACE
         LTR   R0,R0              IS THIS A JES2 JECL CARD ?
         BNZ   EX4RET0            BRANCH IF NOT
         SPACE
*---------------------------------------------------------------------*
*        PROCESS JES2 JECL CARD                                       *
*---------------------------------------------------------------------*
         SPACE
         CLC   2(7,R2),KEYWORD    IS THE WORD KEYWORD ????
         BE    EX4RET8            IF SO, TRASH THE CARD
         CLC   2(7,R2),CONTROL    IS THE WORD CONTROL ????
         BE    EX4RET8            IF SO, TRASH THE CARD
         SPACE
EX4RET0  EQU   *
         SR    R15,R15            SET FOR NORMAL PROCESSING
EX4RET  $RETURN RC=(R15)          RETURN TO MAINLINE PROCESSING
         SPACE
EX4RET8  EQU   *
         LA    R15,8              TRASH THE CARD
         B     EX4RET             GO EXIT
         EJECT
KEYWORD  DC    CL7'KEYWORD'
CONTROL  DC    CL7'CONTROL'
         SPACE
         LTORG
         SPACE
        $MODEND
         SPACE
         END
