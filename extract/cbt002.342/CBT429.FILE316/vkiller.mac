          TITLE ' V K I L L E R '
*---------------------------------------------------------------------*
*         SUBTASK OF TAPEL TO LET YOU RUN MANY TAPELS AND LET         *
*         THIS ROUTINE BRING YOU DOWN.                                *
*                                                                     *
* WRITTEN: MR FRANK YATES, AEROSPACE CORP ASSIGNED TO THE MULTI-      *
*          PURPOSE FACILITY, LOWRY AFB, CO.   OS/MVT                  *
*                                                                     *
* INSTALLED: JUNE 1976 AIR FORCE DATA SVCS CENTER, WASH D.C. OS/MVT   *
*            JUNE 1978 AFDSC ASIS ON OS/MVS                           *
*                                                                     *
* CHANGE LOG: JUNE 1981, CLEANED UP CODE, JIM MARSHALL, AFDSC         *
*             JUNE 1991, DUSTED OFF CODE, JIM MARSHALL, FMS           *
*---------------------------------------------------------------------*
          PRINT ON,NOGEN
R1        EQU   1
R12       EQU   12
R13       EQU   13
R14       EQU   14
R15       EQU   15
          SPACE
VKILLER   AMODE 24
VKILLER   RMODE 24
VKILLER   CSECT
          STM   R14,R12,12(R13)
          LR    R12,R15
          USING VKILLER,R12
          LR    R15,R13
          LA    R13,SAVE0001
          ST    R15,4(R13)
          ST    R13,8(R15)
          L     R1,0(R1)
          MVC   NAME,0(R1)       COPY NAME TO WTOR MESSAGE
          LA    R1,MSG
          SVC   35               WRITE TO OPERATOR WITH REPLY
          WAIT  ECB=ECB
          CLI   REPLY,C'S'
          BE    STOP
          CLI   REPLY,X'A2'      (SMALL S)
          BE    STOP
DUMP      ABEND 100,DUMP,STEP     PRODUCE DUMP IF REPLY IS NOT S
STOP      ABEND 100,,STEP
ECB       DC    F'0'
REPLY     DC    F'0'
MSG       DC    AL1(4),AL3(REPLY),A(ECB),AL2(E-*,0)
          DC    C'CMI003I JOB='
NAME      DC    CL8' '
          DC    C' HAS ENABLED KELLER, REPLY ''S'' TO STOP JOB BEFORE '
          DC    C'IT''S NORMAL END'
E         EQU   *
          LTORG
          END
