         TITLE '   S M F D A T E '
*---------------------------------------------------------------------*
*        'SMFDATE'
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        DATE WRITTEN. OCTOBER 10 1977.
*        DATE UPDATED. APRIL 1, 1991.    JIM MARSHALL
*        ATTRIBUTES. NOT RE-ENTRANT ANY MORE.
*        REMARKS.
*            PROGRAM READS A SMF DATASET AND PRINTS A REPORT SHOWING
*            THE NUMBER OF RECORDS PRESENT FOR EACH DAY.
*
*        JCL:  //SMFDATE  EXEC PGM=SMFDATE
*              //STEPLIB  DD  ---- YOUR LIBRARY ----
*              //SYSPRINT DD SYSOUT=*
*              //SYSOUT   DD SYSOUT=*
*              //SYSUT1   DD -- SMF INPUT FILE TO COUNT ---
*              //SORTLIB  DD DSN=SYS1.SORTLIB,DISP=SHR
*              //SORTIN   DD UNIT=VIO
*              //SORTOUT  DD UNIT=VIO
*
*        CHANGE LOG:
*        01APR91 CODE WAS UPDATED TO COUNT JOB AND TSO COMPLETIONS
*            FROM TYPE 30 SUBTYPE 5'S VERSUS OLD TYPE 5 AND 35'S JDM1
*        15APR19 PGM ASSUMES SMF RECS ARE IN DATE ORDER. ALTER LOGIC
*            TO CREATE A DATA POOL OF RECORD INFORMATION AND TALLY;
*            THIS WILL BE A MAJOR REWRITE OF THE CODE            JDM2
*        13SEP91 MAKE DCB'S NOT REENTRANT                        JDM3
*        14SEP91 ADD DSNAME OF INPUT FILE TO REPORT              JDM4
*        06JUN98 UPDATED Y2K SUPPORT + INCREASED RECORD NUMBERS  JDM5
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SP000    EQU    0
         SPACE
SMFDATE  AMODE 24
SMFDATE  RMODE 24
SMFDATE  CSECT
         SAVE  (14,12),,SMFDATE_&SYSDATE
         LR    R12,R15
         USING SMFDATE,R12
         GETMAIN RU,LV=MYDSECTL,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R14,R13
         LR    R13,R1
         L     R1,24(,R14)         RESTORE R1
         USING MYDSECT,R13
         SPACE
         SLR   R9,R9
         SLR   R15,R15
         ST    R15,SWITCH          ZERO VALUE
         ST    R15,RC              ZERO VALUE
         ST    R15,COUNTIN         ZERO VALUE
         ST    R15,COUNTDAY        ZERO VALUE
         ST    R15,SAVEDAY         ZERO VALUE
         ST    R15,COUNTJOB        ZERO VALUE
         ST    R15,COUNTTSO        ZERO VALUE
         ST    R15,COUNTDMP        ZERO VALUE
         MVC   CTRPAG,=P'000'
         MVC   CTRLIN,=P'000'
         MVC   CTRMAX,=P'050'
         MVC   CTRSEQ,=P'0000000'
         MVI   OUTBLANK,C' '
         SPACE
         L     R2,0(,R1)           POINT TO PARM
         LH    R3,0(,R2)           GET LENGTH
         LTR   R3,R3               LENGTH ZERO?
         BZ    NOPARM              YES - BRANCH
         LR    R15,R3
         CH    R15,=H'8'
         BNH   *+8
         LH    R15,=H'8'
         BCTR  R15,0
         B     *+10
         PACK  DOUBLE(8),2(0,R2)
         EX    R15,*-6
         CVB   R1,DOUBLE
NOPARM   EQU   *
         LA    R4,DCBP
         OPEN  (DCBP,OUTPUT)
         LA    R15,DCBP                                          JDM3
         USING IHADCB,R15                                        JDM3
         TM    DCBOFLG,DCBOFOPN    DID WE HAVE GOOD OPEN ?       JDM3
         BO    OKDCBP
         DROP  R15                                               JDM3
         LA    R1,MSG01
         WTO   MF=(E,(1))
         B     EXIT12
         SPACE
OKDCBP   EQU   *
         LA    R5,DCBS
         OPEN  (DCBS)
         LA    R15,DCBS                                          JDM3
         USING IHADCB,R15                                        JDM3
         TM    DCBOFLG,DCBOFOPN    DID WE HAVE GOOD OPEN ?       JDM3
         BO    OKDCBS
         DROP  R15                                               JDM3
         LA    R1,MSG02
         LA    R0,L'MSG02
         BAL   R14,PRINT
         B     EXIT12
         SPACE
OKDCBS   EQU   *
         RDJFCB (DCBS)                                           JDM4
         SPACE
READ     EQU   *
         GET   (R5)
         LA    R6,0(,R1)           POINT TO RECORD
         USING SMFRCD30,R6         GET ADDRESSABILITY
         SPACE
         L     R1,COUNTIN
         A     R1,=F'1'
         ST    R1,COUNTIN
         CLI   SMF30RTY,2          DUMP HEADER
         BE    DUMPS
         CLI   SMF30RTY,3          DUMP TRAILER
         BNE   OK
DUMPS    EQU   *
         L     R1,COUNTDMP
         A     R1,=F'1'
         ST    R1,COUNTDMP
         SPACE
*---------------------------------------------------------------------*
*        WILL HAVE TO RUN THE DATA TABLE TO EITHER FIND A MATCH       *
*        OT INITIALIZE A NEW ENTRY.                                   *
*---------------------------------------------------------------------*
         SPACE
OK       EQU   *
         L     R11,=V(SMFDATA)     GET DATA TABLE ADDRESS
SLOOP    EQU   *
         CLC   0(4,R11),FENCE      AT PHYSCIAL END OF TABLE ?
         BE    ABORT               MORE THAN 1000 DAYS ?
         CLC   0(4,R11),=F'0'      AT LOGICAL END OF TABLE ?
         BE    TABADD              ADD NEW ENTRY TO TABLE
         CLC   0(4,R11),SMF30DTE   DO WE MATCH THIS DATE
         BE    TABUPD              IF SO UPDATE TABLE
         A     R11,=F'16'          IF NOT MOVE ON IN TABLE
         B     SLOOP               CONTINUE LOOP
         SPACE
TABADD   EQU   *
         MVC   0(4,R11),SMF30DTE   MOVE IN THE NEW DATE
         SLR   R15,R15             GET VALUE OF 0
         ST    R15,4(,R11)         ZERO TOTAL COUNT
         ST    R15,8(,R11)         ZERO JOB COUNT
         ST    R15,12(,R11)        ZERO TSO COUNT
TABUPD   EQU   *
         L     R15,4(,R11)         GET TOTAL FROM TABLE
         A     R15,=F'1'           UP COUNT
         ST    R15,4(,R11)         STORE UPDATED TOTAL BACK
         CLI   SMF30RTY,30         JOB RECORD
         BE    EVAL30
         B     READ
         SPACE
EVAL30   EQU   *
         CLI   SMF30STP+1,X'05'    IS IT A JOB OR TSO END
         BNE   READ
         LR    R15,R6              GET ADDRESS OF RECORD
         A     R15,SMF30IOF        ADD OFFSET TOP ID SECT
         USING SMF30JBN,R15        GET ADDRESSABILITY
         CLI   SMF30JNM,C'J'       IS IT A JOB
         BE    JOB                 MUST BE A JOB
         CLI   SMF30JNM,C'T'       BE IT TSO
         BE    TSO
         B     READ
         DROP  R15
JOB      EQU   *
         L     R15,8(R11)          GET TOTAL FROM TABLE
         A     R15,=F'1'           UP COUNT
         ST    R15,8(R11)          STORE UPDATED TOTAL BACK
         B     READ
         SPACE
TSO      EQU   *
         L     R15,12(,R11)        GET TOTAL FROM TABLE
         A     R15,=F'1'           UP COUNT
         ST    R15,12(,R11)        STORE UPDATED TOTAL BACK
         B     READ
         SPACE
*---------------------------------------------------------------------*
*        TABLE OF ALL DAYS IS NOW BUILT AND READY TO GO BUT IT MAY    *
*        NOT BE IN DATE SEQUENCE. SO SEND IT OFF TO SORT.             *
*---------------------------------------------------------------------*
         SPACE
SORTIT   EQU   *
         OPEN  (SORTIN,(OUTPUT))
         L     R11,=V(SMFDATA)     GET DATA TABLE ADDRESS
GLOOP    EQU   *
         CLC   0(4,R11),=F'0'      AT LOGICAL END OF TABLE ?
         BE    PRTREPT             AT END OF TABLE ???
         CLC   0(4,R11),FENCE      AT PHYSCIAL END OF TABLE ?
         BE    PRTREPT             AT END OF TABLE ???
         MVC   SAVEDAY(4),0(R11)   SAVE THE NEW VALUE
         MVC   COUNTDAY(4),4(R11)  SAVE THE TOTAL NUMBER
         MVC   COUNTJOB(4),8(R11)  SAVE THE NUMBER OF JOBS
         MVC   COUNTTSO(4),12(R11) SAVE THE NUMBER OF TSO
         PUT   SORTIN,SORTREC      WRITE THE RECORD
         A     R11,=F'16'          MOVE ON IN TABLE
         B     GLOOP
         SPACE
PRTREPT  EQU   *
         CLOSE (SORTIN)
         LINK  EP=SORT,PARAM=(SORTDEF),VL=1
         LTR   R15,R15             TEST RESULTS
         BNZ   ABORT2              COMPLAIN
         OPEN  (SORTOUT,INPUT)     GET THE RESULTS
PLOOP    EQU   *
         GET   SORTOUT,SORTREC     GO GET SORTED RECORD
         BAL   R10,SUMMARY         PRINT THE INFORMATION
         B     PLOOP
         SPACE
SUMMARY  BAL   R14,LINEB
         LA    R15,LINE+3                                        JDM5
         MVC   0(9,R15),=X'40212020204B202020'                   JDM5
         AP    SAVEDAY(4),=P'1900000'                            JDM5
         ED    0(9,R15),SAVEDAY
         L     R1,COUNTDAY
         CVD   R1,DOUBLE
         LA    R15,LINE+15
         MVC   0(12,R15),=X'402020206B2020206B202020'            JDM5
         ED    0(12,R15),DOUBLE+3                                JDM5
         L     R1,COUNTJOB
         CVD   R1,DOUBLE
         LA    R15,LINE+30
         MVC   0(10,R15),=X'40206B2020206B202020'
         ED    0(10,R15),DOUBLE+4
         L     R1,COUNTTSO
         CVD   R1,DOUBLE
         LA    R15,LINE+45
         MVC   0(10,R15),=X'40206B2020206B202020'
         ED    0(10,R15),DOUBLE+4
         BAL   R14,PRINTL
         BR    R10
SMFEOD   EQU   *
         CLOSE (SORTOUT)
*        BAL   R10,SUMMARY
         BAL   R14,LINEB
         MVC   LINE+5(7),=C'  DUMPS'
         L     R1,COUNTDMP
         CVD   R1,DOUBLE
         LA    R15,LINE+15
         MVC   0(12,R15),=X'402020206B2020206B202020'            JDM5
         ED    0(12,R15),DOUBLE+3                                JDM5
         BAL   R14,PRINTL
         BAL   R14,LINEB
         MVC   LINE+5(7),=C'  TOTAL'
         L     R1,COUNTIN
         CVD   R1,DOUBLE
         LA    R15,LINE+15
         MVC   0(12,R15),=X'402020206B2020206B202020'            JDM5
         ED    0(12,R15),DOUBLE+3                                JDM5
         MVC   LINE+30(25),=C'(INCLUDING TYPES 2 AND 3)'
         BAL   R14,PRINTL
         B     EXIT0
         SPACE
HEX      MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXT-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         BR    R14                 RETURN TO CALLER
HEXT     DC    C'0123456789ABCDEF' TRANSLATE TABLE
         SPACE
NUMBER   MVC   FULL,0(R1)
         L     R1,FULL
         CVD   R1,DOUBLE
         MVC   0(9,R15),=X'40202020206B202120'
         ED    0(9,R15),DOUBLE+4
         MVC   0(8,R15),1(R15)
         MVI   8(R15),C' '
         BR    R14
         SPACE
TIMEX    MVC   DOUBLE(4),0(R1)
         L     R1,DOUBLE
         SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  0(2,R15),DOUBLE+6(2)
         MVI   2(R15),C'.'
         SRDL  R0,32
         D     R0,=F'6000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  3(2,R15),DOUBLE+6(2)
         MVI   5(R15),C'.'
         SRDL  R0,32
         D     R0,=F'100'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  6(2,R15),DOUBLE+6(2)
         BR    R14
         SPACE
PUTREC   EQU   *
         ST    R14,PRINTS
         AP    CTRSEQ,=P'1'
         UNPK  OUTSEQ(7),CTRSEQ
         OI    OUTSEQ+6,X'F0'
         MVI   OUTSEQ+7,C'0'
*        PUT   (R6),OUTREC
         L     R14,PRINTS
         BR    R14
         SPACE
PRINTL   EQU   *
         LA    R1,LINE
         LA    R0,132
         SPACE
PRINT    EQU   *
         STM   R14,R1,PRINTS
         CP    CTRLIN,CTRMAX
         BNH   *+10
         ZAP   CTRLIN,=P'0'
         CP    CTRLIN,=P'0'
         BE    PRINTH
         SPACE
PRINTD   EQU   *
         BAL   R14,PRINTB
         L     R14,PRINTS+8        GET LENGTH FROM R0
         L     R1,PRINTS+12        RELOAD R1
         LTR   R14,R14             LENGTH IN R0 = ZERO?
         BNP   PRINTD1             YES - PRINT BLANK LINE
         CH    R14,=H'132'         TOO LONG?
         BNH   *+8                 NO - BRANCH AROUND NEXT INS
         LH    R14,=H'132'         YES - TRUNCATE IT
         BCTR  R14,0               LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   PRINTA+1(0),0(R1)   MOVE DATA
         EX    R14,*-6
PRINTD1  EQU   *
         PUT   (R4),PRINTA
         AP    CTRLIN,=P'1'
         LM    R14,R1,PRINTS
         BR    R14
         SPACE
PRINTH   EQU   *
         AP    CTRPAG,=P'1'
         MVI   PRINTA,C' '         MOVE IN BLANK                 JDM4
         MVC   PRINTA+1(132),PRINTA  ROLL THE BALNK              JDM4
         MVI   PRINTA,C'1'         CARRIAGE CONTROL
         MVC   HEADDSN(44),JFCBWRKI  MOVE IN DSN                 JDM4
         MVC   PRINTA+1(L'HEADI),HEADI                           JDM4
         LA    R15,PRINTA+125
         MVC   3(4,R15),=X'40202020'
         ED    3(4,R15),CTRPAG
         MVC   0(4,R15),=C'PAGE'
         PUT   (R4),PRINTA                                       JDM4
         BAL   R14,PRINTB                                        JDM4
         PUT   (R4),PRINTA                                       JDM4
         MVC   PRINTA+5(L'HEADC),HEADC                           JDM4
         PUT   (R4),PRINTA
         BAL   R14,PRINTB
         PUT   (R4),PRINTA
         BAL   R14,PRINTB
         MVC   PRINTA+5(L'HEADD),HEADD
         PUT   (R4),PRINTA
         BAL   R14,PRINTB
         PUT   (R4),PRINTA
         B     PRINTD
         SPACE
PRINTB   EQU   *
         MVI   PRINTA,C' '
         MVC   PRINTA+1(132),PRINTA
         BR    R14
         SPACE
LINEB    EQU   *
         MVI   LINE,C' '
         MVC   LINE+1(131),LINE
         BR    R14
         SPACE
EXIT12   EQU   *
         LA    R15,12
         B     EXITR
         SPACE
EXIT0    EQU   *
         SLR   R15,R15
EXITR    EQU   *
         ST    R15,RC
         LA    R15,DCBP                                          JDM3
         USING IHADCB,R15                                        JDM3
         TM    DCBOFLG,DCBOFOPN    IS IT OPEN ??                 JDM3
         BNO   NOCS                NO - BRANCH                   JDM3
         DROP  R15                                               JDM3
         CLOSE (DCBS)                                            JDM3
NOCS     EQU   *
         LA    R15,DCBS                                          JDM3
         USING IHADCB,R15                                        JDM3
         TM    DCBOFLG,DCBOFOPN    IS IT OPEN ??                 JDM3
         BNO   NOCP                NO - BRANCH                   JDM3
         DROP  R15                                               JDM3
         CLOSE (DCBP)                                            JDM3
NOCP     EQU   *
         L     R15,RC
         SPACE
EXIT     EQU   *
         LR    R1,R13
         L     R13,4(,R13)
         ST    R15,16(,R13)
         FREEMAIN RU,LV=MYDSECTL,A=(1),SP=SP000
         LM    R14,R12,12(R13)
         BR    R14
         SPACE
ABORT    ABEND 1                   TABLE OVERFLOWETH
ABORT2   ABEND 2                   BAD SORT
         SPACE
************************************************************
*        CONSTANTS                                         *
************************************************************
         SPACE
MSG01    WTO   'SMFDATE  UNABLE TO OPEN SYSPRINT',                     +
               ROUTCDE=(11),MF=L
         SPACE
MSG02    DC    C'*** ERROR ***  UNABLE TO OPEN SYSUT1'
MSG03    DC    C'*** ERROR ***  UNABLE TO OPEN SYSUT2'
         SPACE
         PRINT NOGEN
         SPACE
DCBP     DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    +
               RECFM=FBA,LRECL=133,BLKSIZE=133
         SPACE
DCBS     DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),BFTEK=A,              X
               LRECL=32756,BLKSIZE=32760,                        JDM3  X
               EODAD=SORTIT,                                     JDM3  X
               EXLST=JFCBIN                                      JDM4
         SPACE
SORTIN   DCB   DDNAME=SORTIN,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=16,    X
               BLKSIZE=7200
         SPACE
SORTOUT  DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=16,   X
               BLKSIZE=7200,EODAD=SMFEOD
         SPACE
HEADI    DS    0CL60                                             JDM4
         DC    CL16'            DSN='                            JDM4
HEADDSN  DC    CL44'TO.BE.FILLED.IN '                            JDM4
HEADC    DC    C'        S M F    D A T E    S U M M A R Y '
HEADD    DC    C'   DATE        TOTALS         # JOBS          # TSO'
FENCE    DC    XL8'FFFFFFFFFFFFFFFF'
         CNOP  2,4                 FORCE HALFWORD NOT FULLWORD
SORTDEF  DC    H'24'
         DC    A(SORTC)            ADDR OF SORT CARD
         DC    A(SORTCE)           ADDR OF END OF SORT CARD
         DC    A(SORTR)            ADDR OF RECORD CARD
         DC    A(SORTRE)           ADDR OF END OF RECORD CARD
         DC    2F'0'               NOT USED HERE
SORTC    DC    C' SORT FIELDS=(1,4,A),FORMAT=BI '
SORTCE   DC    C' '
SORTR    DC    C' RECORD LENGTH=16,TYPE=F '
SORTRE   DC    C' '
         SPACE 1                                                 JDM4
JFCBIN   DS    0F                                                JDM4
         DC    X'07'                                             JDM4
IJFCB    DC    AL3(JFCBWRKI)                                     JDM4
JFCBWRKI DC    CL176'JUNK'                                       JDM4
         SPACE 1                                                 JDM4
         LTORG
OFLGS    EQU   48
RECFM    EQU   36
LRECL    EQU   82
BLKSI    EQU   62
EODAD    EQU   32
EXLST    EQU   36
         SPACE
************************************************************
*        DSECTS                                            *
************************************************************
         SPACE
MYDSECT  DSECT
         DS    18F                 REGISTER SAVEAREA
*YOPEN   DS    F
*YCLOSE  DS    F
*YDCBP   DS    0F,(DCBPL)X
*YDCBS   DS    0F,(DCBSL)X
COUNTIN  DS    F
SORTREC  DS    0CL16               DO NOT CHANGE THE NEXT STATEMENTS
SAVEDAY  DS    F
COUNTDAY DS    F
COUNTJOB DS    F
COUNTTSO DS    F
COUNTDMP DS    F
HOUNTDAY DS    F
HOUNTJOB DS    F
HOUNTTSO DS    F
SWITCH   DS    F
RC       DS    F
UT2EXL   DS    F
DOUBLE   DS    D
FULL     EQU   DOUBLE,4
PRINTS   DS    4F
CTRSEQ   DS    PL4
CTRPAG   DS    PL2
CTRLIN   DS    PL2
CTRMAX   DS    PL2
OUTBLANK DS    C
OUTREC   DS    0CL80
OUTTYPE  DS    CL1
OUTSMFR  DS    CL2
         DS    CL1
OUTDATE  DS    CL5
         DS    CL1
OUTTIME  DS    CL6
         DS    CL1
OUTRDEV  DS    CL8
         DS    CL1
OUTJOB   DS    CL8
         DS    CL1
OUTLINES DS    CL7
         DS    CL1
OUTCLASS DS    CL1
         DS    CL1
OUTFORM  DS    CL4
         DS    CL23
OUTSEQ   DS    CL8
         SPACE
PRINTA   DS    CL133
         SPACE
LINE     DS    CL133
         SPACE
MYDSECTL EQU   *-MYDSECT
         SPACE
         DCBD  DSORG=(PS)                                        JDM3
         SPACE
         PRINT ON,GEN
         IFASMFR 30
         END
*---------------------------------------------------------------------*
*        RECORD DATA STORAGE AREA                                     *
*---------------------------------------------------------------------*
SMFDATA  CSECT
         DS     0F
ONEDAY   DC     F'0'               STORE DAY 00YYDDDF
         DC     F'0'               STORE TOTAL COUNTS
         DC     F'0'               STORE JOB COUNTS
         DC     F'0'               STORE TSO COUNTS
         DC     3996F'0'           TOTAL OF 1000 DAYS MAX
         DC     16X'FF'            FENCE
         END
