         TITLE '   J E S X R D R   '
*$DOC@*****************************************************************
*                                                                     *
*        'JESXRDR' - SECONDARY JES READER                             *
*                                                                     *
***********************************************************************
         SPACE
*  WRITTEN BY. BILL GODFREY, PLANNING RESEARCH CORPORATION, MCLEAN VA.
*  INSTALLATION. AFDSC/PENTAGON.
*  DATE WRITTEN. JUNE 4 1982.
*  DATE UPDATED. JUNE 7 1982.
*  ATTRIBUTES. NOT RE-ENTRANT.
*   MUST BE LINK-EDITED WITH AC=1 IN AUTHORIZED LIBRARY.
*  DESCRIPTION.
*   THIS PROGRAM ALLOCATES AN INTERNAL READER TO A SECONDARY JES2
*   AND COPIES A FILE INTO IT.
*
*  LOG OF CHANGES.
*   07JUN82 - PARM FIELD NOW CONTAINS SUBSYSTEM NAME.
*             OPEN SYSIN EARLY TO SEE IF IT WILL ABEND.
*
*
***********************************************************************
         SPACE
JESXRDR  START
         USING *,R11,R12
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'JESXRDR'
         DC    CL16' &SYSDATE &SYSTIME '
SUBPOOL  DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(13)
         LR    R11,R15
         LA    R15,1
         LA    R12,4095(R15,R11)
         LR    R2,R1
         SPACE
         L     R0,SUBPOOL
         GETMAIN R,LV=(0)
         SPACE
         LR    R9,R1
         XC    000(256,R9),000(R9)
         SPACE
         ST    R13,4(,R9)
         ST    R9,8(,R13)
         LR    R13,R9              NEW SAVEAREA POINTER
         USING @DATA,R9
         SPACE
************************************************************
*                                                          *
*        GO INTO KEY 0                                     *
*                                                          *
************************************************************
         SPACE
         MODESET KEY=ZERO
         SPACE
************************************************************
*                                                          *
*        GET SUBSYSTEM NAME FROM PARM                      *
*                                                          *
************************************************************
         SPACE
         L     R1,0(,R2)
         CLI   1(R1),4             IS PARM LENGTH 4
         BNE   EXIT0               NO
         MVC   MASKSSIB+8(4),2(R1) YES, COPY SUBSYSTEM NAME
         SPACE
************************************************************
*                                                          *
*        SET UP ESTAE                                      *
*                                                          *
************************************************************
         SPACE
         STAE$  MSG=WTO
         OI    STATUS,STATEST
         SPACE
************************************************************
*                                                          *
*        OPEN AND CLOSE SYSIN (IN CASE OPEN ABENDS)        *
*                                                          *
************************************************************
         SPACE
         LA    R2,UT1DCB
         MVI   OPEN,X'80'
         OPEN  ((R2),INPUT),MF=(E,OPEN)
         TM    48(R2),X'10'
         BNO   EXIT0
         MVI   CLOSE,X'80'
         CLOSE ((R2)),MF=(E,CLOSE)
         SPACE
************************************************************
*                                                          *
*        FIND OUR SSIB                                     *
*                                                          *
************************************************************
         SPACE
         L     R2,540              PSATOLD-PSA
         L     R2,180(,R2)         TCBJSCB-TCB(,R2)
         L     R2,X'15C'(,R2)      JSCBSACT-JSCB(,R2)
         L     R2,X'13C'(,R2)      JSCBSSIB-JSCB(,R2)
         USING SSIB,R2
         ST    R2,OLDSSIB
         SPACE
************************************************************
*                                                          *
*         BUILD A NEW SSIB                                 *
*                                                          *
************************************************************
         SPACE
*              FOR OTHERS      SSIBSSNM IS JES2, SSIBJBID IS STC NNNN
         SPACE
         LA    R0,36               LENGTH OF SSIB
         O     R0,=A(X'FF000000')  SUBPOOL 255
         GETMAIN R,LV=(0)
         ST    R1,NEWSSIB
         OI    STATUS,STATGOT
         MVC   0(36,R1),MASKSSIB
         SPACE
************************************************************
*                                                          *
*                                                          *
*                                                          *
************************************************************
         SPACE
         L     R3,16               CVTPTR
         USING CVT,R3
         L     R4,CVTJESCT         JES CONTROL TABLE
         USING JESCT,R4
         SPACE
         BAL   R14,REQJOBID        REQUEST JOB ID FROM JESX
         BAL   R14,ALLOCATE
         BAL   R14,COPY
*        BAL   R14,RETJOBID        RETURN JOB ID TO JESX
*              COMMENTED OUT UNTIL BUG IN HASPSSSM AT HJLHSVC FIXED
         NOP       RETJOBID        RETURN JOB ID TO JESX
         B     EXIT0
         EJECT
************************************************************
*                                                          *
*        REQUEST JOB ID FROM PRIMARY SUBSYSTEM             *
*                                                          *
************************************************************
         SPACE
REQJOBID LR    R6,R14
         LA    R5,LCLSSOB
         USING SSOB,R5
         MVC   SSOBID(4),=C'SSOB'
         LA    R15,SSOBHSIZ
         STH   R15,SSOBLEN
         LA    R15,SSOBRQST
         STH   R15,SSOBFUNC
         L     R15,NEWSSIB
         ST    R15,SSOBSSIB
         LA    R15,SSRRBGN
         ST    R15,SSOBINDV
         LA    R1,PTRSSOB
         ST    R5,0(,R1)
         OI    0(R1),X'80'
         LA    R15,8
         STH   R15,SSRRLEN
         LA    R15,LCLRSECB
         ST    R15,SSRRSECB
         DROP  R5                  SSOB
         SPACE
         LR    R5,R1
         MODESET MODE=SUP
         LR    R1,R5
         SPACE
         IEFSSREQ
         SPACE
         LR    R5,R15
         MODESET MODE=PROB
         LR    R15,R5
         SPACE
         LTR   R15,R15
         BZ    OKREQ
         WTO   MF=(E,MS02)         JOBID REQUEST FAILED
         B     EXIT0
OKREQ    EQU   *
*
*              OPENING A SYSOUT DATA SET WILL CAUSE A 913-C0 ABEND
*              UNLESS THE JSCB POINTS TO A JES2 SSIB.  WE COULD
*              MODIFY THE SSIB, OR POINT THE JSCB TO A NEW SSIB.
*              THE LATTER IS USED HERE.  THE OTHER METHOD HAS
*              NOT BEEN TESTED.
*
         SPACE
         L     R1,NEWSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         OI    STATUS,STATSIB      RUNNING UNDER NEW SSIB
         SPACE
         BR    R6
         SPACE
************************************************************
*                                                          *
*        RETURN JOB ID TO PRIMARY SUBSYSTEM                *
*                                                          *
************************************************************
         SPACE
*              IF THIS IS NOT DONE, JES2 ISSUES MESSAGE
*              $HASP310 SYSLOG  TERMINATED AT END OF MEMORY
         SPACE
RETJOBID EQU   *
         LR    R6,R14              SAVE RETURN ADDRESS
         SPACE
         L     R1,OLDSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         NI    STATUS,255-STATSIB
         SPACE
         LA    R5,LCLSSOB          SSOB
         USING SSOB,R5
         LA    R15,SSOBRTRN        RETURN JOB ID
         STH   R15,SSOBFUNC
         L     R15,NEWSSIB
         ST    R15,SSOBSSIB
         DROP  R5                  SSOB
         PRINT GEN
         SPACE
         MODESET MODE=SUP
         SPACE
         LA    R1,PTRSSOB
         SPACE
         IEFSSREQ
         SPACE
         MODESET MODE=PROB
         SPACE
         PRINT GEN
         LR    R14,R6              RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         SPACE
************************************************************
*                                                          *
*         ALLOCATE AN INTERNAL READER                      *
*                                                          *
************************************************************
         SPACE
ALLOCATE ST    R14,ALLOCR
         XC    O99DSECT(52),O99DSECT
         MVC   O99DD,=CL8' '
         MVI   O99SO,C'A'          SYSOUT=A
         MVI   O99FC,C'C'          FREE=CLOSE
         MVC   O99PG,=CL8'INTRDR'
         LA    R1,O99PTR
         LA    R0,O99DSECT
         ST    R0,0(,R1)
         OI    0(R1),X'80'
         L     R15,=V(SYSO99)
         BALR  R14,R15
         SPACE
         LTR   R15,R15
         BNZ   EXIT0
         SPACE
         L     R14,ALLOCR
         BR    R14
         SPACE
************************************************************
*                                                          *
*         COPY INPUT FILE TO INTERNAL READER               *
*                                                          *
************************************************************
         SPACE
COPY     STM   R14,R4,ALLOCR
         LA    R2,UT1DCB
         LA    R3,UT2DCB
         MVC   40(8,R3),O99DD
         MVI   OPEN,X'80'
         OPEN  ((R2),INPUT),MF=(E,OPEN)
         TM    48(R2),X'10'
         BNO   EXIT0
         MVI   OPEN,X'80'
         OPEN  ((R3),OUTPUT),MF=(E,OPEN)
         TM    48(R3),X'10'
         BNO   EXIT0
         OI    STATUS,STATOP2      INTRDR DCB IS OPEN
         SPACE
COPYLOOP EQU   *
         GET   (R2)
         LR    R0,R1
         PUT   (R3),(0)
         B     COPYLOOP
         SPACE
EOF      EQU   *
         MVI   CLOSE,X'80'
         CLOSE ((R2)),MF=(E,CLOSE)
         NI    STATUS,255-STATOP1
         CLOSE ((R3)),MF=(E,CLOSE)
         NI    STATUS,255-STATOP2
         LM    R14,R4,ALLOCR
         BR    R14
         SPACE
************************************************************
*                                                          *
*         TERMINATE                                        *
*                                                          *
************************************************************
         SPACE
EXIT0    SR    15,15
EXIT     LR    R2,R15
         TM    STATUS,STATOP2
         BZ    EXITOP2
         LA    R3,UT2DCB
         MVI   CLOSE,X'80'
         CLOSE ((R3)),MF=(E,CLOSE)
         NI    STATUS,255-STATOP2
EXITOP2  EQU   *
         TM    STATUS,STATOP1
         BZ    EXITOP1
         LA    R3,UT1DCB
         MVI   CLOSE,X'80'
         CLOSE ((R3)),MF=(E,CLOSE)
         NI    STATUS,255-STATOP1
EXITOP1  EQU   *
         TM    STATUS,STATSIB      ARE WE RUNNING UNDER NEW SSIB
         BZ    EXITSIB             NO, BRANCH
         L     R1,OLDSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         NI    STATUS,255-STATSIB
EXITSIB  EQU   *
         TM    STATUS,STATGOT
         BZ    EXITGOT
         LA    R0,36               LENGTH OF SSIB
         O     R0,=A(X'FF000000')  SUBPOOL 255
         L     R1,NEWSSIB
         FREEMAIN R,LV=(0),A=(1)
         NI    STATUS,255-STATGOT
EXITGOT  EQU   *
         TM    STATUS,STATEST
         BZ    EXITX1
         ESTAE 0
         NI    STATUS,255-STATEST
EXITX1   EQU   *
         LR    R3,R13              SAVE R13 FOR FREEMAIN
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         SPACE
         MODESET KEY=NZERO
         SPACE
         LR    R1,R3               AREA TO BE FREEMAINED
         L     R0,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2              RETURN CODE
         LM    0,12,20(R13)
         L     14,12(,R13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
STAE$END EQU   *
MS01     WTO   'JOBID REQUEST SUCCESSFUL',                             X
               ROUTCDE=(2),MF=L
MS01L    EQU   *-MS01
         SPACE
MS02     WTO   'JOBID REQUEST FAILED',                                 X
               ROUTCDE=(2),MF=L
MS02L    EQU   *-MS02
         SPACE
MS03     WTO   'INTRDR ALLOCATED SUCCESSFULLY',                        X
               ROUTCDE=(2),MF=L
MS03L    EQU   *-MS03
         SPACE
MS04     WTO   'DATA COPIED TO INTRDR',                                X
               ROUTCDE=(2),MF=L
MS04L    EQU   *-MS04
         SPACE
MS05     WTO   'JOBID RETURNED',                                       X
               ROUTCDE=(2),MF=L
MS05L    EQU   *-MS05
         SPACE
         DC    0F'0'
MASKSSIB DC    C'SSIB',AL2(36,0),C'JESX',CL16' ',XL8'00'
         SPACE
         PRINT NOGEN
UT1DCB   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EODAD=EOF
         SPACE
UT2DCB   DCB   DDNAME=DYNAM,DSORG=PS,MACRF=(PM),                       X
               RECFM=FB,LRECL=80,BLKSIZE=80
         SPACE
         PRINT GEN
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
STATUS   DS    X
STATOP1  EQU   X'80'
STATOP2  EQU   X'40'
STATEST  EQU   X'20'
STATSIB  EQU   X'10'
STATGOT  EQU   X'08'
PTRSSOB  DS    F
LCLSSOB  DS    5F,7F
LCLRSECB DS    F
NEWSSIB  DS    F
OLDSSIB  DS    F
ALLOCR   DS    7F
OPEN     DS    F
CLOSE    DS    F
STAE$WRK DS    32F
STAE$LST DS    16F
*
O99PTR   DS    F
O99DSECT DS    0D
O99DD    DS    CL8                 DDNAME
O99SO    DS    CL1                 SYSOUT CLASS
O99FC    DS    C                   FREE=CLOSE IF 'C'
O99HO    DS    C                   HOLD=YES IF 'H'
         DS    C                   RESERVED
O99CPPL  DS    A                   ADDRESS OF CPPL OR ZERO
O99DE    DS    CL8                 DEST
O99PG    DS    CL8                 PROGRAM NAME OR 'INTRDR'
O99AT    DS    CL8                 ATTRIBUTE LIST NAME
O99FO    DS    CL4                 FORM NUMBER
O99CO    DS    H                   COPIES
         DS    CL2                 RESERVED
O99RSC   DS    CL4                 REASON CODES RETURNED HERE
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
************************************************************
*                                                          *
*         MAP OF CVT                                       *
*                                                          *
************************************************************
         SPACE
CVTPTR   EQU   16
CVT      DSECT
CVTMAP   EQU   *
         ORG   CVT+X'128'
CVTJESCT DS    A
         SPACE
         IEFJESCT
         SPACE 3
         IEFJSCVT
         SPACE 3
         SPACE
         IEFJSSIB
         SPACE 3
         IEFJSSOB (CM,RR),CONTIG=YES
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
