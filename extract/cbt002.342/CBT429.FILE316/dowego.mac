         TITLE 'DOWEGO              OCT  1983'
*---------------------------------------------------------------------*
*
*        THIS PROGRAM IS USED BY OPERATIONS AS THE FIRST STEP IN
*        SOME CRITICAL PROCESSING. IT GIVES THEM THE CHANCE TO
*        STOP THE PROCESSING OF THE JOB BEFORE THEY DO REAL DAMAGE.
*
*        PROGRAM LOGIC:
*
*        - IF THE OPERATOR RESPONDS 'GO' THEN THE PGM ENDS WITH RC=0.
*
*        - IF THE OPERATOR RESPONDS 'NOGO', THEN THE PGM ABORTS WITH
*          A USER 0069.
*
*        - IF THE OPERATOR RESPONDS ANYTHING BUT 'GO' OR 'NOGO' THEN
*          TELL PERSON OF ERROR AND REPROMPT FOR CORRECT ANSWER.
*
*
*        JCL:     //DECISION EXEC PGM=DOWEGO
*
*
*
*        JIM MARSHALL, CAPTAIN, USAF-RET
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
DOWEGO   AMODE 24
DOWEGO   RMODE 24
DOWEGO   CSECT
         SAVE  (14,12),,DOWEGO_SADSC_&SYSDATE &SYSTIME
         LR    R12,R15
         USING DOWEGO,R12
         LA    R8,SAVE
         ST    R13,SAVE+4
         ST    R8,8(R13)
         LR    R13,R8
*-------------------------------------------------------------------*
*        NOW WE EXTRACT FOR THE JOBNAME
*-------------------------------------------------------------------*
         SPACE
         LA    R4,JOBNADDR             WHERE WE PUT IT
         EXTRACT (4),'S',FIELDS=(TIOT)
         L     R5,JOBNADDR
         MVC   WTOMSG+17(8),0(R5)      MOVE IN THE JOBNAME
         SPACE
ASK      EQU   *
         LA    R1,WTOMSG
         WTO   MF=(E,(R1))
         SPACE
         WTOR  'ISG0011A  REPLY ''GO'' TO CONTINUE OR ''NOGO'' TO TERMIX
               NATE!',REPLY,4,ECB
         SPACE
         WAIT  ECB=ECB
         MVC   ECB,=F'0'
         TR    REPLY,CAPS
         CLC   REPLY(2),GO
         BE    EOF
         CLC   REPLY(4),NOGO
         BE    BLOWUP
         WTO   'ISG0001I REPLY IS INVALID, TRY AGAIN !!!!!',ROUTCDE=(2)
         B     ASK
         SPACE
EOF      EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         SPACE
BLOWUP   EQU   *
         ABEND 69
         EJECT
*------------------------------------------------------------------*
*        DCB'S AND DATA AREAS
*------------------------------------------------------------------*
         SPACE
SAVE     DC    18F'0'
ECB      DC    F'0'
JOBNADDR DC    F'0'
GO       DC    CL2'GO'
NOGO     DC    CL4'NOGO'
JOBNAME  DC    CL8' '
REPLY    DC    CL9' '
BLANKS   DC    CL9' '
         SPACE
WTOMSG   WTO   'ISG00010I  J=XXXXXXXX  HAS BEGUN.',ROUTCDE=(1),MF=L
         LTORG
CAPS     DC CL256' '         TRANSLATE TABLE TO MAKE CAPITALS
         ORG CAPS+C'A'
         DC C'ABCDEFGHI'
         ORG CAPS+129        SMALL A
         DC C'ABCDEFGHI'
         ORG CAPS+C'J'
         DC C'JKLMNOPQR'
         ORG CAPS+145        SMALL J
         DC C'JKLMNOPQR'
         ORG CAPS+C'S'
         DC C'STUVWXYZ'
         ORG CAPS+162        SMALL S
         DC C'STUVWXYZ'
         ORG CAPS+C'0'
         DC C'0123456789'
         ORG CAPS+C'#'
         DC C'#'
         ORG CAPS+C'$'
         DC C'$'
         ORG CAPS+C'*'
         DC C'*'
         ORG CAPS+C'/'
         DC C'/'
         ORG CAPS+C'('
         DC C'('
         ORG CAPS+C')'
         DC C')'
         ORG CAPS+C'.'
         DC C'.'
         ORG CAPS+C','
         DC C','
         ORG CAPS+C'+'
         DC C'+'
         ORG CAPS+C'-'
         DC C'-'
         ORG CAPS+C'='
         DC C'='
         ORG CAPS+C''''
         DC C''''
         ORG CAPS+C'&&'
         DC C'&&'
         ORG
         END
