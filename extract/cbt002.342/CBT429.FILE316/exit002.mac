EXIT002  TITLE 'JES2/SP3 EXIT #2 - COMMENTS'
* * * * * * * * * *  NOTES ON THE NEW JES2  * * ** * * * * * ** * * *
* 1.  THE JESPARMS THAT CORRESPOND TO THIS EXIT ARE:                  *
*       . . .                                                         *
*       >LOAD EXIT002                                                 *
*       >EXIT002 (JESMASK),ENABLE,TRACE=NO                            *
*                                                                     *
* 2.  THE ORDER OF THE DSECTS IN THE PREAMBLE ASSEMBLY  CAN BE        *
*     SIGNIFICANT.  ALSO THE REGISTER CONTENTS ARE AS FOLLOWS UPON    *
*     ENTRY TO THIS EXIT:                                             *
*         R0 = CODE INDICATING TYPE OF JOB STATEMENT.                 *
*              0 => JOB STATEMENT IMAGE.                              *
*              4 => JOB STATEMENT CONTINUATION.                       *
*         R1 = ADDRESS OF 3 WORD PARAMETER LIST.                      *
*              WORD 1 => JOB STATEMENT IMAGE BUFFER.                  *
*              WORD 2 => EXIT FLAG BYTE IN PCE.                       *
*              WORD 3 => POINTS TO JCTXWRK FIELD IN JCT.              *
*        R2-R9 = N/A                                                  *
*        R10 = @JCT                                                   *
*        R11 = @HCT                                                   *
*        R12 = N/A                                                    *
*        R13 = @PCE                                                   *
*                                                                     *
*  3.  THIS EXIT WILL  SET THE JES SPOOL MASK IN THE JCT TO           *
*      DIRECT THE JOB TO SPOOL ONE (HIGH ORDER BIT ON). THIS IS       *
*      DONE BECAUSE SPOOL ONE VOLUME IS LARGER THAN SPOOL TWO.        *
*      JOBS WILL ONLY GO TO SPOOL TWO IF SPOOL ONE IS FULL.           *
*                                                                     *
*  4.  THIS EXIT ALSO CHECKS THE JOB CLASS FOR CLASS 'Q'.  IF CLASS   *
*      'Q' THEN THE JOB NAME MUST BEGIN WITH '//ASMI' OR ELSE THE     *
*      JOB IS FLUSHED.  IN ADDITION 'TIME=' IS SCANNED FOR ON EACH    *
*      JOB AND CONTINUATION CARD.  IF FOUND, AN ERROR MESSAGE IS      *
*      ISSUED AND THE JOB IS FLUSHED.  'TIME=' MUST BE DISALLOWD      *
*      BECAUSE EXIT006 ADDS THE TIME VALUE FOR THE RESPECTIVE JOB     *
*      CLASS.                                                         *
*                                                                     *
*                    *   *   *   *   *                                *
*                    CAPT JIM CARTER                                  *
*                    LT PAUL FINDLEY                                  *
*                    AIR FORCE DATA SERVICES CENTER                   *
*                    IBM OPERATING SYSTEMS SUPPORT BRANCH             *
*                    PENTAGON, CMIS, ROOM 1C1039                      *
*                    WASHINGTON  D.C.      20330                      *
*                    CALL: 202/695-1274                               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'JES2/SP3 EXIT #2 - MIT CSECT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                  P R E A M B L E  -  E X I T 2                    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT OFF
         $SYSPARM (OFF,NOGEN,NODATA,NO,NO)   DOCUMENTATION OPTIONS
         COPY  $HASPGEN                 COPY HASPGEN PARAMETERS
         PRINT ON,NOGEN
         $SYSPARM (ON,GEN,DATA,YES,YES)    DOCUMENTATION OPTIONS
MITABLE  $MODULE                        CREATE THE MIT CSECT
         $BUFFER  DOC=YES               REQ'D BY $GETBUF, $FREEBUF,
*                                       AND MUST PRECEDE $JCT
         $JCT     DOC=YES               MUST PRECEDE $CAT
         $JQE     DOC=YES               MUST PRECEDE $HCT
         $PCE     DOC=YES               MUST PRECEDE $HCT
         $CAT     DOC=YES               MUST PRECEDE $HCT
         $DCT     DOC=YES               NORMALLY REQUIRED FOR EXIT #1
         $HCT     DOC=YES               REQ'D BY $SAVE, $RETURN...
         $PDDB    DOC=YES               MUST PRECEDE $IOT
         $TAB     DOC=YES               MUST PRECEDE $IOT
         $IOT     DOC=YES
         $JOE     DOC=YES               REQ'D TO GET SYSOUT CLASS
         $MIT     DOC=YES               REQ'D BY $MODEND
         $PPPWORK DOC=YES               REQ'D TO FIND JOE
         PRINT ON,NOGEN
         TITLE 'JES2/SP3 EXIT #2 - JESMASK CSECT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*         BEGIN JES MASK  CSECT                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
JESMASK  $ENTRY CSECT=YES,BASE=R12      EXIT ROUTINE ENTRY POINT
         $SAVE
         LR       R12,R15               LOAD BASE REGISTER
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       SPOOL MASK FOR PARTITIONING SPOOL VOLUMES                   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         OC       JCTSAMSK,SPOOLMSK
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       INITIALIZATION VALUES FOR SUBSEQUENT CODE                   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA       R15,0               CLEAR FOR RECURSION
         L        R3,0(R1)            R3 = START FOR 'TIME=' SCAN
         LA       R4,1                R4 = INCREMENT
         LA       R5,65(R3)           R5 = STOP FOR 'TIME='
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       SCAN FOR  'TIME='                                           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         CLC      0(L'TIME,R3),TIME   'TIME=' FOUND ?
         BE       BARF                IF SO, ISSUE BARF MESSAGE
         BXLE     R3,R4,*-10          KEEP CHECKING UNTIL CC 66
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       IF 'CLASS=Q' THEN JOB MUST BIGIN WITH '//ASMI'              *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CLASSQ   LTR       R0,R0              JCTJCLAS BLANK UNTIL CONT CARD
         BZ        RETURN
         CLI       JCTJCLAS,C'Q'
         BNE       RETURN
         CLC       JCTJNAME(4),ASMI
         BE        RETURN
         MVC       MESS2,MESS1         MOVE CLASS Q BARF MESSAGE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       BARF ROUTINE FOR DISALLOWING 'TIME=' OR 'CLASS=Q'           *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BARF     LM       R7,R9,0(R1)
         LA       R15,8
         MVC      0(L'MESS2,R9),MESS2
         OI       0(R8),32            TURN ON RXERRMSG BIT
RETURN   $RETURN  RC=(R15)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                  C O N S T A N T S                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SPOOLMSK DC       X'80000000'
ASMI     DC       C'ASMI'
TIME     DC       C'TIME='
MESS1    DC       C' CANNOT SUBMIT TO CLASS Q CONTACT CMI'
MESS2    DC       C' TIME NOT ALLOWED ON JOB CARD        '
         ORG
         LTORG
         $MODEND
         END
