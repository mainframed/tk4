         TITLE ' T A P E L '
*---------------------------------------------------------------------*
*        T A P E L     ( TAPE LABEL ANALYSIS )                        *
*                                                                     *
*        THIS PROGRAM WAS ORIGINALLY WRITTEN BY MR FRANK YATES,       *
*        AEROSPACE CORPORATION, WHILE AT THE MULTI-PURPOSE FACILITY,  *
*        LOWRY AFB, CO. THIS PROGRAM WILL FORMAT ALL FILES TAPE       *
*        LABELS IN A PLEASANTLY PLEASING APPEARANCE TO THE EYE.       *
*                                                                     *
* NOTE:  1. LKED AC=1                                                 *
*        2. NEEDS APPENDAGES IGG019WD & IGG019WE                      *
*        3. NEEDS SOME SUBRTNES, TLPRINT & MSGWRITE                   *
*                                                                     *
* COURTESY:  JIM MARSHALL, CAPT, USAF-RET                             *
*                                                                     *
* WRITTEN: MR FRANK YATES, AEROSPACE CORP WHILE AT MULTIPURPOSE       *
*          FACILITY, LOWRY AFB, CO  FOR USE IN OS/MVT.                *
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*     28 SEP 1973 - INSTALLED OS/MVT                                  *
*      1 MAR 1978 - INSTALLED OS/MVS AT AFDSC, WASH DC                *
*     21 JUN 1981 - CLEANED UP THE CODE SOMEWHAT.                     *
*     11 OCT 1981 - UP THE ALLOWABLE NUMBER OF SL FILES IT WILL       *
*                   PROCESS, INCREASED BUFFER FOR LABELS TO 24,000.   *
*                   ALSO CONTINUED TO DOCUMENT THE CODE.              *
*     10 OCT 1986 - INSTALL MVS/XA. NEED TO HARDCODE A MACRO TO  JDM1 *
*                   GET AROUND ASMH V2 COMPILE ERROR.            JDM1 *
*     30 OCT 1986 - TESTED ON IBM 3480 DRIVES IN FULL FUNCTION        *
*                   MODE. WORKS LIKE A CHAMP. NEEDED TO ADJUST THE    *
*                   'TLPRINT' SUBROUTINE TO TAKE INTO ACCOUNT THE     *
*                   DEN=0 COMES OUT FOR FULL FUNCTION MODE. JUST      *
*                   NEGATE  ITS PRINTING IF DEN=0.  J MARSHALL        *
*     15 FEB 1995 - CHECKED OUT ON MASSTOR CORP M960 DEVICE. IT       *
*                   APPEARS TO HOST AS 3480 CARTRIDGE.                *
*     03 MAR 1995 - NEED TO INCREASE ALLOWABLE NUMBER OF SL FILES     *
*                   TO 3,600 (MSS STORES A BUNCH) TO 432,000 BYTES    *
*                   OR 3,600 * (60*2) = 432,000                  JDM2 *
*---------------------------------------------------------------------*
         EJECT
         MACRO
&NAME    DO &CCW
&NAME    MVC IOB+17(3),=AL3(&CCW)      SELECT A CHANNEL PROGRAM,
         BAL 10,EXCP                   AND EXECUTE IT.
         MEND
         SPACE 2
         MACRO
&NAME    TEST  &WHAT
&NAME    CLC   =C'&WHAT',LABEL     TEST LABEL.
         BNE   STOP                EXIT IF TEST FAILS.
         MVC   0(60,8),LABEL       COPY LABEL TO POOL.
         LA    R8,60(8)            ADVANCE LABEL POINTER.
         MEND
         SPACE
         PRINT ON,NOGEN
TAPEL    AMODE 24
TAPEL    RMODE 24
TAPEL    CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING TAPEL,R12
         LR    R15,R13
         LA    R13,SAVE
         ST    R15,4(R13)
         ST    R13,8(R15)
         SPACE 2
         L     R2,0(R1)      REG 2 = ADDRESS OF EXEC OR START PARAMETER
         LH    R3,0(R2)      REG 3 = PARAMETER LENGTH
         LA    R6,1          REG 6 = NO. OF TAPES, DEFAULT=1
         LTR   R3,R3         TEST FOR PARAMETER.
         BZ    Q1
         BCTR  R3,0          REG 3 = LENGTH CODE FOR PARM
         EX    R3,PACK
         CVB   R6,WORK       REG 6 = NO. OF TAPES TO BE PROCESSED
         L     R10,16        LOAD CVT ADDR
         L     R10,0(R10)    ADDR OF DBLE WORD PT TO TCB
         L     R10,4(R10)    PTR TO CURRENT TCB
         L     R10,12(R10)   GET TIOT ADDR
         MVC   NAME(8),0(R10)  MOVE IN JOBNAME FOR MSG
         ATTACH EP=VKILLER,PARAM=NAME
         ST    1,KILLER      SAVE ADDRESS OF KILLER TCB.
         MVI   KILLSW+1,0    ENABLE DETACH.
Q1       EQU   *
         LOAD  EP=IGG019WE   GET CHANNEL END APPENDAGE.
         LOAD  EP=IGG019WD
         RDJFCB IN
         MVI   JFCB+66,16    BYPASS LABEL PROCESSING.
         GETMAIN EU,LV=432000,A=POOL   GET LABEL POOL.           JDM2
         SPACE 2
OPEN     OPEN  IN,TYPE=J
         LA    R1,IN         GET ADDRESS OF DCB                  JDM1
         TM    48(R1),X'10'  SUCCESSFUL OPEN ??                  JDM1
         BO    GOODOPEN                                          JDM1
         COMMENT 'OPEN TEST FAILED.'                             JDM1
         SVC   3             EXIT                                JDM1
GOODOPEN EQU   *                                                 JDM1
         L    R8,POOL        REG 8 = LABEL POINTER
         LA   R7,3600        REG 7 = LOOP LIMIT                  JDM2
         DO   REWIND         REWIND AND READ VOLUME LABEL.
         TEST VOL1
         SPACE 2
LOOP     DO    READ
         TEST  HDR1
         DO    READ
         TEST  HDR2
         DO    SKIP1
         DO    SKIP1
         DO    READ
         TEST  EO
         CLI   LABEL+2,C'V'  TEST FOR END OF VOLUME.
         BE    STOP
         DO    SKIP1
         BCT   R7,LOOP
         SPACE 2
STOP     MVC   0(4,8),=F'0'  MARK END OF DATA.
         CLOSE IN
         LA    R1,POOL
         CALL  TLPRINT       PRINT LABEL INFORMATION.
AGAIN    EQU   *
         BCT   R6,OPEN       DO MORE TAPES.
         FREEMAIN E,LV=432000,A=POOL                             JDM2
KILLSW   B     Q2
         DETACH KILLER
Q2       EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 2
EXCP     EXCP  IOB
         WAIT  ECB=ECB
         TM    IOB+12,1      TEST FOR UNIT EXCEPTION.
         BO    STOP
         TM    ECB,X'7F'     TEST FOR SUCESSFUL EXCP
         BCR   1,10
         CLI   ECB,X'41'     TEST RETURN CODE.
         BNE   ABEND3
         TM    IOB+12,2      TEST FOR UNIT CHECK.
         BZ    ABEND3
         TM    IOB+3,1       TEST FOR WRONG DENSITY.
         BZ    ABEND3
         WTO   'CMI223I  TAPE  NOT CORRECT DENSITY FOR SELECTED UNIT'
         COMMENT 'CMI223I TAPE  NOT CORRECT DENSITY FOR SELECTED UNIT'
         CLOSE IN
         B     AGAIN
ABEND3   ABEND 3,DUMP
         SPACE
***DATA AREA
POOL     DC    F'0'          ADDRESS OF LABEL POOL
IN       DCB   MACRF=E,CENDA=WE,XENDA=WD,EXLST=EXLST,DDNAME=TAPELIN
EXLST    DC    X'87',AL3(JFCB)
JFCB     DC    44F'0'
ECB      DC    F'0'
SAVE     DC    18F'0'           SAVE AREA
IOB      DC    X'42000000',A(ECB,0,0,0,IN,0,0)
REWIND   CCW   7,0,X'40',1      REWIND.
READ     CCW   2,LABEL,0,80     READ A LABEL.
SKIP2    CCW   X'3F',0,X'40',1  SKIP TWO TAPE MARKS.
SKIP1    CCW   X'3F',0,0,1      SKIP ONE TAPE MARK.
WORK     DC    D'0'
KILLER   DC    F'0'             ADDRESS OF KILLER TCB
LABEL    DC    CL80' '
NAME     DC    CL8'TAPEL.'
PACK     PACK  WORK,2(0,2)
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         END
