MIM3     TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                   INDEX, LIST, COPY, PUNCH'
***********************************************************************
*                                                                     *
*                       C. J. GOELZ                                   *
*                       GILBERT/COMMONWEALTH COS., INC.               *
*                       P. O. BOX 1498,                               *
*                       READING, PA  19603                            *
*                                                                     *
*                       PHONE (215) 775-2600  EXT 2252                *
*                       SHARE CODE GAI                                *
*                                                                     *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*        MIM#3 IS A UTILITY PROGRAM INTENDED FOR PROCESSING           *
*        MICHMODS(MICHIGAN MODIFICATIONS) TAPE DATA SETS.  MIM#3      *
*        ALLOWS SELECTIVE OR EN MASSE RETRIEVAL OF MODIFICATIONS      *
*        ARRANGED IN A NON-LABELED TAPE DATA SET CALLED MICHMODS.     *
*        RECORDS ARE EIGHTY BYTE CARD IMAGES (MOSTLY SOURCE CODE),    *
*        AND WILL BE IN COMPRESSED FORM AS DISTRIBUTED.  MIM#3 WILL   *
*        HANDLE THE NECESSARY EXPANSION AND COMPRESSION TRANSPARENTLY *
*        TO THE USER.         EACH MODIFICATION IS FORMATTED AS A     *
*        'MEMBER', PRECEDED BY HEADER CARDS (A MEMBER NAME CARD, AND  *
*        ALIAS NAME CARDS AS APPROPRIATE), ARRANGED IN COLATING SE-   *
*        QUENCE BY MEMBER NAME.  EACH HEADER CARD MAY ALSO CONTAIN    *
*        AN IDENTIFICATION FIELD OF UP TO 52 CHARACTERS.  HEADER CARD *
*        EXAMPLES:                                                    *
*                                                                     *
*        CC                2                                        7 *
*        1                 0                                        1 *
*                                                                     *
*        @@  NAME=ABLE     UP TO 52 CHARACTER IDENTIFICATION FIELD... *
*        @@ ALIAS=BAKER    UP TO 52 CHARACTER IDENTIFICATION FIELD... *
*                                                                     *
***********************************************************************
*                                                                     *
*        MIM#3 IS CONSTRAINED TO SELECT OR REJECT MEMBERS BY PARM     *
*        FIELD SPECIFICATIONS, AND, WHERE APPROPRIATE, BY SYSIN DATA  *
*        FURNISHED AT EXECUTION TIME.                                 *
*                                                                     *
*        OUTPUT FROM MIM#3 IS DIRECTED TO PRINT, PUNCH, OR TAPE DATA  *
*        SETS.                                                        *
*                                                                     *
***********************************************************************
*                                                                     *
*        JCL REQUIRED FOR EXECUTION OF MIM#3:                         *
*                                                                     *
*        // EXEC PGM=MIM#3,REGION=38K,                                *
*        // PARM='PARAMETERS,SEPARATED BY COMMAS'          *** NOTE 7 *
*        //SYSPRINT DD SYSOUT=A                            *** NOTE 1 *
*        //INTAPE DD DSN=MICHMODS,UNIT=TAPE,               *** NOTE 2 *
*        // VOL=SER=000000,LABEL=(0,NL),DISP=OLD           *** NOTE 2 *
*        //*         DCB=(RECFM=VB,BLKSIZE=5380,LRECL=84)  *** NOTE 6 *
*        //SYSPUNCH DD SYSOUT=B                            *** NOTE 3 *
*        //OUTAPE DD  (PER USERS INSTALLATION)           *** NOTE 4,6 *
*        //SYSIN DD *                                      *** NOTE 5 *
*        INPUT DATA                                        *** NOTE 5 *
*        .                                                            *
*        .                                                            *
*        .                                                            *
*                                                                     *
***********************************************************************
*                                                                     *
*        NOTE 1 -THE 'SYSPRINT' DD IS REQUIRED FOR EACH EXECUTION OF  *
*               MIM#3, AS UNUSUAL CONDITION MESSAGES, AND A RUN SUM-  *
*               MARY ARE DIRECTED TO IT.                              *
*                                                                     *
*        NOTE 2 -THE 'INTAPE' DD DEFINES THE MICHMODS DATA SET.  DATA *
*               SET SEQUENCE NUMBER AND VOLSER WILL BE DETERMINED BY  *
*               THE FINAL DISTRIBUTION TAPE FORM.  SEE ALSO NOTE 6.   *
*                                                                     *
*        NOTE 3 -THE 'SYSPUNCH' DD IS REQUIRED ONLY WHEN THE 'DECK',  *
*               OR 'SDEC', PARM OPTION IS SPECIFIED.  THIS SHOULD BE  *
*               A FIXED, BLOCKED DATA SET OF 80 BYTE RECORDS, WITH A  *
*               BLOCKING FACTOR CONVENIENT FOR THE USER.              *
*                                                                     *
*        NOTE 4 -THE 'OUTAPE' DD IS REQUIRED ONLY WHEN THE 'TAPE'     *
*               PARM OPTION IS SPECIFIED.  SEE ALSO NOTE 6.           *
*                                                                     *
*        NOTE 5 -THE 'SYSIN' DD IS NOT REQUIRED, AND SHOULD BE        *
*               OMITTED  WHERE NO INPUT DATA IS PRESENT.  IF INPUT    *
*               DATA IS UTILIZED, IT SHOULD BE IN 80 BYTE CARD IMAGE  *
*               FORMAT, CONSISTING OF MEMBER AND/OR ALIAS NAMES, AP-  *
*               PEARING ANY WHERE IN CARD COLUMNS 1 TO 80, EACH SEPA- *
*               RATED BY AT LEAST ONE BLANK FROM ANY OTHER.  NO PRO-  *
*               VISION IS MADE FOR CONTINUATION OF A NAME TO ANOTHER  *
*               CARD, BUT AS MANY CARDS MAY BE ENTERED AS NECESSARY,  *
*               EACH CONTAINING ONE OR MORE NAMES.  NAMES MAY APPEAR  *
*               IN ANY ORDER, AND WILL BE COLLATED BY THE PROGRAM.    *
*                                                                     *
*        NOTE 6 -A DATA COMPRESSION/EXPANSION FEATURE HAS BEEN ADDED  *
*               TO MIM#3, RELEASE 2, AND IS APPLICABLE TO BOTH INPUT  *
*               AND OUTPUT TAPE DATA SETS.  THE DISTRIBUTED LIBRARY   *
*               FORMAT(INPUT) WILL BE RECFM=VB, LRECL=84(MAX), AND    *
*               BLKSIZE=5380.  THESE WILL BE THE DEFAULTS FOR ALL     *
*               INPUT AND OUTPUT TAPE DATA SETS.  HOWEVER, RECFM=FB   *
*               FOR OUTPUT, MAY BE SPECIFIED IN JCL IF UN-COMPRESSED  *
*               OUTPUT IS REQUIRED, OR FOR INPUT, IF MIM#3 IS TO BE   *
*               RUN AGAINST UN-COMPRESSED INPUT DATA.  IN THIS CASE,  *
*               LRECL WILL BE 80, OF COURSE, AND THE DEFAULT BLOCK    *
*               SIZE WILL BE 5120.  OTHER VALID FB BLOCK SIZES MAY BE *
*               SPECIFIED.                                            *
*               -TWO NEW CSECTS, 'SHRINK' AND 'EXPAND', TOGETHER WITH *
*               THE MACROS 'GETX', 'PUTS', AND 'CHEX', ACCOMPLISH THE *
*               DATA COMPRESSION/EXPANSION.  THEY ARE "KEYED" BY THE  *
*               RECORD FORMAT OF THE DATA SET: IF RECFM=VB, THE AS-   *
*               SUMPTION IS COMPRESSED DATA; IF RECFM=FB, THE ASSUMP- *
*               TION IS NORMAL DATA.  THE COMPRESSION TECHNIQUE IN-   *
*               VOLVES ELIMINATION OF DUPLICATED CHARACTERS (STRINGS  *
*               OF THREE OR MORE) AND A 6-BIT-FOR-8 TRANSLATION AND   *
*               PACKING (3-CHARACTERS-FOR-4).  A RECORD CONTAINING    *
*               DATA WHICH MIGHT BE ALTERED OR LOST IS LEFT UN-COM-   *
*               PRESSED (THUS THE REQUIREMENT OF A MAX LRECL OF 84).  *
*               OVER ALL DATA LENGTH REDUCTION, WITH THE CURRENT      *
*               520,000 PLUS CARD IMAGES OF THE MICHIGAN MODS IS ON   *
*               THE ORDER OF 55 TO 60 PERCENT.                        *
*                                                                     *
*        NOTE 7 -FUNCTIONAL CONTROL IS PROVIDED BY SEVERAL PARAMETER  *
*               OPTIONS.  ALL ARE KEYWORD TYPE, AND IN THE LIST BELOW *
*               THE CRITICAL (REQUIRED) PORTION IS SHOWN, FOLLOWED BY *
*               THE REMAINDER (NOT REQUIRED) OF THE COMPLETE NAME OF  *
*               THE PARAMETER IN PARENTHESES.                         *
*                                                                     *
*           LIST, -INDICATES THAT A LISTING IS TO BE PRODUCED AS DE-  *
*               FINED BY THE 'SYSPRINT' DD.  THIS LISTING CONSISTS OF *
*               ONE OR MORE HEADER CARDS, FOLLOWED BY THE CARD IMAGE  *
*               MEMBER DATA, FOLLOWED BY THE HEADER CARD(S) FOR THE   *
*               NEXT MEMBER, ETC.                                     *
*                                                                     *
*           L(INECNT)=NN, -INDICATES THE NUMBER OF LINES PER PAGE FOR *
*               BOTH THE LIST AND SYSPRINT DD'S, WHERE NN IS A ONE OR *
*               TWO DIGIT DECIMAL NUMBER.  THE DEFAULT IS 60.  NOTICE *
*               THAT IT IS POSSIBLE TO COMBINE THE LIST AND LINECNT   *
*               PARMS, SINCE ONLY THE 'L' AND '=NN' ARE CRITICAL FOR  *
*               LINECNT (IE., LIST=55, WILL CAUSE A FULL LISTING TO   *
*               BE PRODUCED HAVING 55 LINES PER PAGE).                *
*                                                                     *
*           DECK, -INDICATES THAT A CARD DECK IS TO BE PUNCHED AS DE- *
*               FINED BY THE 'SYSPUNCH' DD.  HEADER CARDS ARE PUNCHED *
*               BUT THE '@'S IN CC 1 AND 2 ARE CONVERTED TO 12-11-0-  *
*               7-8-9 PUNCHES, FOR EASE IN LOCATING THEM.             *
*                                                                     *
*           SDEC(K), -(SOURCE DECK) INDICATES THAT A CARD DECK IS TO  *
*               BE PUNCHED AS DEFINED BY THE 'SYSPUNCH' DD.  NO HEAD- *
*               ER CARDS ARE PUNCHED, SO THAT THIS DECK IS DIRECTLY   *
*               SUITABLE AS SOURCE CODE INPUT TO A SUBSEQUENT ASSEM-  *
*               BLY OR COMPILATION.  'SDECK' AND 'DECK' ARE MUTUALLY  *
*               EXCLUSIVE PARAMETERS.                                 *
*                                                                     *
*           TAPE, -INDICATES THAT A TAPE COPY IS TO BE PRODUCED AS    *
*               DEFINED BY THE 'OUTAPE' DD.                           *
*                                                                     *
*           MAXC(ARDS)=N..., -INDICATES THE MAXIMUM TOTAL NUMBER OF   *
*               CARDS TO BE PUNCHED ('DECK' OR 'SDEC' OPTION), RE-    *
*               GARDLESS OF OTHER PARAMETERS.  MAXCARDS DEFAULTS TO   *
*               2000, BUT MAY BE SPECIFIED HIGHER OR LOWER AS REQUIR- *
*               ED.  N... MAY BE A 1 TO 6 DIGIT DECIMAL NUMBER (1 TO  *
*               999999).  MAXCARDS DOES NOT AFFECT OUTPUT TAPE PRO-   *
*               CESSING.                                              *
*                                                                     *
*           MAXL(INES)=N..., -INDICATES THE MAXIMUM TOTAL NUMBER OF   *
*               LINES TO BE PRINTED ('LIST' OPTION), REGARDLESS OF O- *
*               THER PARAMETERS.  MAXLINES DEFAULTS TO 20000, BUT MAY *
*               BE SPECIFIED HIGHER OR LOWER AS REQUIRED.  N... MAY   *
*               BE A 1 TO 6 DIGIT DECIMAL NUMBER (1 TO 999999).  MAX- *
*               LINES DOES NOT AFFECT OUTPUT TAPE PROCESSING.         *
*                                                                     *
*           MAXM(EMBERLINES)=N..., -INDICATES THE MAXIMUM NUMBER OF   *
*               LINES ('LIST' OPTION) TO BE PRINTED FOR EACH TAPE     *
*               MEMBER, REGARDLESS OF OTHER PARAMETERS.  MAXMEMBER-   *
*               LINES DEFAULTS TO 999999 (EFFECTIVELY THE ENTIRE      *
*               MEMBER), BUT MAY BE SPECIFIED LOWER AS REQUIRED.      *
*               N... MAY BE A 1 TO 6 DIGIT DECIMAL NUMBER (1 TO       *
*               999999).  MAXMEMBERLINES DOES NOT AFFECT OUTPUT TAPE  *
*               PROCESSING.                                           *
*               *******************************************************
*               *  MAXMEMBERLINES IS A NEW PARAMETER SUGGESTED BY     *
*               *  ROGER BURROWS OF STATISTICS CANADA.  THANKS ROGER. *
*               *******************************************************
*                                                                     *
*           KEYS(TATEMENTSONLY), -INDICATES THAT A LISTING OF BAL KEY *
*               STATEMENTS ONLY IS TO BE PRODUCED.  NOTE THAT KEYS    *
*               IMPLIES 'LIST'.  KEY STATEMENTS, AS DEFINED IN THE    *
*               DISTRIBUTED VERSION OF MIM#3, REL. 2 ARE:             *
*                                                                     *
*               COMMENTS (* IN CC1), MACRO COMMENTS (.* IN CC1-2),    *
*               MACRO, MEXIT, MEND, START, CSECT, DSECT, SAVE, RETURN,*
*               OPEN, ENQ, GETMAIN, CALL, ATTACH, LOAD, LINK, XCTL,   *
*               IDENTIFY, ABEND, WAIT, POST, SPIE, STAE, SEGLD, SEGWT,*
*               WTO, WTOR, WTL, HEDIT, AND EXIT STATEMENTS, ONLY      *
*               WHERE THEY BEGIN IN CC 10.                            *
*                                                                     *
*           IND(EX), -INDICATES THAT A LISTING OF HEADER CARDS ONLY   *
*               IS TO BE PRODUCED,DIRECTED TO THE 'SYSPRINT' DD. NAME *
*               CARDS ARE LISTED, WITH A COUNT OF THE MEMBER CARDS,   *
*               FOLLOWED BY ANY ALIAS CARDS.  IF BOTH 'INDEX' AND     *
*               'LIST' ARE SPECIFIED, THE INDEX WILL BE PRINTED FIRST.*
*               TWO PASSES OF THE TAPE WILL BE REQUIRED.              *
*                                                                     *
*           INCL(UDE) OR EXCL(UDE), -APPLIES TO MEMBER AND/OR ALIAS   *
*               NAMES APPEARING AS SYSIN DATA ONLY, AND INDICATES IF  *
*               SUCH ARE TO BE SELECTED FOR OUTPUT TO THE SYSPRINT,   *
*               SYSPUNCH, AND/OR OUTAPE DD'S.  IF A TAPE MEMBER NAME  *
*               OR ANY OF IT'S ALIASES APPEARS AS A SYSIN DATA ITEM,  *
*               THEN:  (A) IF EXCLUDE WAS SPECIFIED, THAT TAPE MEMBER *
*               IS BY-PASSED; OR (B) IF INCLUDE WAS SPECIFIED, THAT   *
*               TAPE MEMBER BECOMES A CANDIDATE FOR OUTPUT.  IF NIE-  *
*               THER WAS SPECIFIED, BUT SYSIN DATA WAS FURNISHED, IN- *
*               CLUDE IS ASSUMED.                                     *
*                                                                     *
*           ID(ENTITY)=1 TO 52 CHARACTER STRING, -INDICATES A CHECK   *
*               OF EACH MEMBER NAME CARD IS TO BE MADE TO DETERMINE   *
*               IF THIS PARTICULAR STRING APPEARS ANY WHERE WITHIN    *
*               THE 52 CHARACTER IDENTITY FIELD.  ACTION OF THE PRO-  *
*               GRAM IS DETERMINED BY THE SELECT OR REJECT PARMS.     *
*                                                                     *
*           SEL(ECT) OR REJ(ECT), -INDICATES THE ACTION TO BE TAKEN   *
*               WHEN THE IDENTITY=FIELD IS INVOKED.  IF A MATCHING    *
*               CHARACTER STRING IS FOUND, THEN: (A) IF REJECT WAS    *
*               SPECIFIED, THAT TAPE MEMBER IS BY-PASSED; OR (B) IF   *
*               SELECT WAS SPECIFIED, THAT TAPE MEMBER BECOMES A CAN- *
*               DIDATE FOR OUTPUT TO THE SYSPRINT, SYSPUNCH, AND/OR   *
*               OUTAPE DD'S.  IF NIETHER WAS SPECIFIED, SELECT IS     *
*               ASSUMED.                                              *
*                                                                     *
*                                                                     *
*        OPERATIONAL ORDERING OF THE PARAMETERS IS AS FOLLOWS:        *
*                                                                     *
*           (A) -IF AN INDEX IS REQUESTED, THE ENTIRE INPUT TAPE IS   *
*               READ THROUGH, A COMPLETE INDEX ALWAYS BEING PRODUCED. *
*               THE INDEX CONTENT IS NOT AFFECTED BY OTHER PARAMETERS,*
*               AND THE LINES PRODUCED DO NOT COUNT TOWARD MAXLINES.  *
*               THE TAPE IS REWOUND.
*                                                                     *
*           (B) -A MEMBER NAME, AND ANY ALIASES, ARE READ FROM TAPE.  *
*                                                                     *
*           (C) -AN IDENTITY CHECK IS MADE, IF INDICATED.  PROCESSING *
*               CONTINUES ON THE BASIS OF THIS CHECK AND THE SELECT   *
*               OR REJECT PARAMETERS.                                 *
*                                                                     *
*           (D) -MATCH-UP OF SYSIN NAMES IS PERFORMED, IF SYSIN DATA  *
*               IS PRESENT.  PROCESSING CONTINUES ON THE BASIS OF     *
*               THIS MATCH-UP AND THE INCLUDE OR EXCLUDE PARAMETERS.  *
*                                                                     *
*           (E) -THE TAPE MEMBER, IT'S ALIASES, AND DATA ARE EITHER   *
*               OUTPUT IN THE SELECTED FORMS, OR ARE BY-PASSED.  OUT- *
*               PUT OF PRINT LINES AND PUNCHED CARDS, IF REQUESTED,   *
*               IS STOPPED AFTER MAXLINES AND MAXCARDS, RESPECTIVELY, *
*               ARE REACHED.  PROCESSING CONTINUES AS AT (B).  AN EX- *
*               AMPLE FOLLOWS:                                        *
*                                                                     *
*                                                                     *
*        // EXEC PGM=MIM#3,REGION=38K,                                *
*        // PARM='LIST=35,DECK,TAPE,EXCL,ID=TSO MOD,IND'              *
*        //SYSPRINT DD SYSOUT=A                                       *
*        //INTAPE DD DSN=MICHMODS,UNIT=TAPE,                          *
*        // VOL=SER=000000,LABEL=(0,NL),DISP=OLD                      *
*        //SYSPUNCH DD SYSOUT=B                                       *
*        //OUTAPE DD DSN=PARTMODS,UNIT=TAPE,DISP=(,KEEP),             *
*        // DCB=(RECFM=FB,BLKSIZE=3200)                               *
*        //SYSIN DD *                                                 *
*        JOBWTR DSPACE  CON                                           *
*          MCP                                                        *
*         IJP    ALTER                                                *
*        /*                                                           *
*                                                                     *
*        IN THIS EXAMPLE, A COMPLETE INDEX IS TO BE PRODUCED.  THEN,  *
*        ANY MEMBER WHOSE HEADER CARD CONTAINS THE PHRASE 'TSO MOD',  *
*        AND WHOSE NAME (OR ALIAS) DOES NOT APPEAR IN THE LIST:       *
*                                                                     *
*               ALTER                                                 *
*               CON                                                   *
*               DSPACE                                                *
*               IJP                                                   *
*               JOBWTR                                                *
*               MCP                                                   *
*                                                                     *
*        IS TO BE:  LISTED, THIRTY-FIVE LINES PER PAGE, TO A MAXIMUM  *
*        OF 20000 LINES;  PUNCHED, TO A MAXIMUM OF 2000 CARDS;  AND   *
*        COPIED TO A STANDARD LABEL, UNCOMPRESSED, TAPE DATA SET      *
*        NAMED PARTMODS, HAVING A BLOCK SIZE OF 3200 (LRECL DEFAULTS  *
*        TO 80).                                                      *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*        THE AUTHOR IS GRATEFUL TO PEYTON REED OF ETS, AND OTHER      *
*        SHARE MEMBERS, FOR SEVERAL SUGGESTIONS FOR INCREASED EFFICI- *
*        ENCY INCORPORATED IN MIM#3, RELEASE 2.1.  MANIPULATION OF    *
*        DATA IS MINIMIZED;  WHEN CREATING AN INDEX, OR SKIPPING A    *
*        MEMBER, ONLY HEADER CARDS ARE 'EXPAND'ED;  SEARCHES BY MEM-  *
*        BER NAME ARE TERMINATED WHEN THE SEARCH LIST IS SATISFIED;   *
*        THE 'EXPAND' SUBROUTINE HAS BEEN RE-WRITTEN.                 *
*        THE 'SHRINK' AND 'EXPAND' SUBROUTINES HAVE BEEN RE-WRITTEN   *
*        AGAIN BY DARRELL D. WILCOX OF BOEING COMPUTER SERVICES       *
*        TO EFFECT A 40 PERCENT REDUCTION IN CPU TIME USED (370/155)  *
*        DURING THOSE OPERATIONS.  OTHER MINOR IMPROVEMENTS IN        *
*        CPU SAVINGS WERE ALSO MADE.  THOSE CARDS CHANGED ARE NOTED   *
*        WITH A 'BA' IN COLUMNS 70-71.  THIS IS KNOWN AS RELEASE 4.0  *
*        OF THE MICHIGAN MODS UTILITY #3.   06/14/78                  *
*                                                                     *
*                                                                     *
***********************************************************************
***********************************************************************
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                          MACRO DEFINITIONS'
         MACRO
&NAME    ADRTAB &R1,&BR1,&R2,&BRC,&BR2
         LCLC  &R
         AIF   (T'&R2 NE 'O').NOP
&R       SETC  '&R1'
.NOP     ANOP
&NAME    TM    0(&R1),X'FF' 
         BNO   &BR1 
         L     &R2&R,0(,&R1) 
         LA    &R2&R,0(,&R2&R) 
         LTR   &R2&R,&R2&R 
         B&BRC &BR2 
         MEND
         EJECT
         MACRO
&LABEL   GETX  &DCBR,&LNG=64,&TYPE=
         USING IHADCB,&DCBR                                          BA
&LABEL   LR    1,&DCBR 
         L     15,48(,1) 
         BALR  14,15 
         TM    SHEXADDR,COMPRESS  IS INPUT COMPRESSED                BA
         BNO   GETX&SYSNDX 
         CLI   1(1),&LNG ×          IS IT EXPANDABLE LENGTH ?
         BH    BUMP&SYSNDX ×         NO, FORGET IT
         AIF   ('&TYPE' NE '@@').NOTYPE
         CLC   ADBL&SYSNDX,4(1)   SEE IF '@@'
         BNE   BUMP&SYSNDX .         NO, BYPASS
.NOTYPE  ANOP
         IC    15,1(,1) 
         BCTR  15,0 
         STC   15,MOVE&SYSNDX+1   STORE LENGTH IN MVC                BA
*        BR    0                  SERIALIZE CPU                      BA
         L     15,SHEXADDR        LOAD EXPAND ENTRY ADDRESS          BA
MOVE&SYSNDX MVC SHEXWORK(0),0(1)  MOVE DATA TO WORK AREA             BA
         LA    1,SHEXWORK         R1 PTR TO DATA FOR EXPAND RTN      BA
         BALR  14,15 
         AIF   ('&TYPE' NE '@@').NOTYPE2
         B     BUMP&SYSNDX        BRANCH OVER '@@'                   BA
ADBL&SYSNDX DC    BL2'1111001111000010' .  '@@' COMPRESSED
.NOTYPE2 ANOP
BUMP&SYSNDX LA    1,4(,1)            R1 PTR TO 'EXPAND'ED DATA       BA
GETX&SYSNDX DS    0H 
         DROP  &DCBR                                                 BA
         MEND
         EJECT
         MACRO
&LABEL   PUTS  &DCBR
         USING IHADCB,&DCBR                                          BA
&LABEL   LR    1,&DCBR 
         L     15,48(,1) 
         BALR  14,15 
         TM    SHEXADDR,COMPRESS  IS OUTPUT TO BE COMPRESSED         BA
         BNO   PUTS&SYSNDX 
         ST    1,SHEXWORK         SAVE BUFFER ADDRESS                BA
         LA    1,SHEXWORK+4       R1 PTR TO WORK AREA                BA
PUTS&SYSNDX DS    0H 
         DROP  &DCBR                                                 BA
         MEND
         EJECT
         MACRO
&LABEL   CHEX  &DCBR,&LNG=80
         USING IHADCB,&DCBR                                          BA
&LABEL   TM    SHEXADDR,COMPRESS  IS OUTPUT TO BE COMPRESSED         BA
         BNO   CHEX&SYSNDX 
         L     0,SHEXWORK         R0 SAVES PTR TO BUFFER             BA
         LA    1,&LNG+4           R1=LENGTH OF DATA, PLUS 4          BA
         SLL   1,16 
         ST    1,SHEXWORK              HALF WORD OF WORK AREA        BA
         LA    1,SHEXWORK         R1 PTR TO DATA FOR SHRINK RTN      BA
         L     15,SHEXADDR        LOAD SHRINK ENTRY POINT ADDRESS    BA
         BALR  14,15 
         LH    15,SHEXWORK        NEW DATA LENGTH IN R15             BA
         BCTR  15,0 
         STC   15,MOVE&SYSNDX+1   STORE LENGTH IN MVC                BA
*        BR    0                  SERIALIZE CPU                      BA
         LR    1,0 
MOVE&SYSNDX MVC 0(0,1),SHEXWORK   MOVE 'SHRINK'ED DATA TO BUFFER     BA
CHEX&SYSNDX DS    0H 
         DROP  &DCBR                                                 BA
         MEND
         EJECT
         MACRO PATCH                   AREA SPECIFICATION
&NAME    PATCH &SIZE,             SPECIFY SIZE IN BYTES.               X
               &ALIGN=H,          H=HALFWORD,F=FULLWORD,D=DOUBLEWORD,  X
               &LABEL=YES         ELSE NO ALIGNMENT.  FIRST EIGHT      X
                                  BYTES IDENTIFY PATCH AREA.
.*
.*
         LCLA  &LL
         LCLA  &ML
         LCLA  &NL
.*
         LCLC  &AL
         LCLC  &LB
.*
.*
.*
&NL      SETA  1
&ML      SETA  0
&AL      SETC  'C'
         AIF   (K'&SIZE LE 0).NSIZ
.*
&ML      SETA  &SIZE
.*
.*
.NSIZ    AIF   (K'&ALIGN LE 0).NAL
         AIF   ('&ALIGN' EQ 'D').DAL
         AIF   ('&ALIGN' EQ 'F').FAL
         AIF   ('&ALIGN' NE 'H').NAL
.*
&NL      SETA  2
&AL      SETC  'H'
         AGO   .NAL
.*
.*
.FAL     ANOP
&NL      SETA  4
&AL      SETC  'F'
         AGO   .NAL
.*
.*
.DAL     ANOP
&NL      SETA  8
&AL      SETC  'D'
.*
.*
.NAL     AIF   (&ML GT 0).LBL
.*
&ML      SETA  0
         AGO   .NOL
.*
.*
.LBL     ANOP
&ML      SETA  (&ML+&NL-1)/&NL
         AIF   ('&LABEL' EQ 'NO').NOL
.*
&LL      SETA  &ML-(8/&NL)
         AIF   (&LL LT 0).NOL
.*
&ML      SETA  &LL
         AIF   (K'&NAME LE 0).LON
.*
&NAME    DS    0&AL
         DC    CL8'&NAME.'
         AGO   .LAD
.*
.*
.LON     ANOP
&LB      SETC  'PATCH'.'&SYSNDX'(2,3)
&LB      DS    0&AL
         DC    CL8'&LB.'
.*
.*
.LAD     DC    &ML&AL.'0'
         MEXIT
.*
.*
.NOL     AIF   (K'&NAME LE 0).NON
.*
&NAME    DS    &ML&AL
         MEXIT
.*
.*
.NON     ANOP
&LB      SETC  'PATCH'.'&SYSNDX'(2,3)
&LB      DS    &ML&AL
         MEND
         EJECT
         MACRO
&NAME    HEDIT &STRING,           STRING IS EXPANDED INSTEAD OF &NAME  X
               &TYPE=,            MAY BE START OR CSECT (OR BLANK)     X
               &ORG=0,            ORG FOR CSECT ONLY                   X
               &REGS=(YES,HEX),   R0 THRU RF EQU 0 THRU 15             X
               &BASE=12,          BASE EQU 12 (DEFAULT)                X
               &SAVE=13,          SAVE EQU 13 (DEFAULT)                X
               &SIZE=,            FULL WORD FOR R-TYPE GETMAIN (SAVE)  X
               &SECT=,            LABEL OF SAVE AREA (IF ANY)          X
               &PARM=NO,          SPEC YES IF R1 MUST BE PRESERVED     X
               &ADREG=,           LIST OF ADDITIONAL EQUATES NEEDED    X
               &TITLE=            (A,B) - GEN TITLE STATEMENT WITH     X
                                  NAME FIELD A, AND TITLE 'B'
.*
.*
         LCLA  &I
         LCLA  &J
         LCLA  &K
         LCLA  &L
         LCLA  &M
         LCLA  &N
         LCLA  &BR
         LCLA  &HX
         LCLA  &JL
         LCLA  &R0
         LCLA  &R1
.*
         LCLC  &R
         LCLC  &R12
         LCLC  &R13
         LCLC  &R14
         LCLC  &R15
         LCLC  &ADE
         LCLC  &ADR
         LCLC  &SAV
         LCLC  &IM(48)
         LCLC  &FLD(48)
         LCLC  &STR(48)
         LCLC  &C(432)
.*
.*
&IM(1)   SETC  '    *   '
&IM(2)   SETC  '   ***  '
&IM(3)   SETC  '  ** ** '
&IM(4)   SETC  ' **   **'
&IM(5)   SETC  ' *******'
&IM(6)   SETC  ' ****** '
&IM(7)   SETC  '  ***** '
&IM(8)   SETC  ' **     '
&IM(9)   SETC  ' ****   '
&IM(10)  SETC  ' *****  '
&IM(11)  SETC  '   *    '
&IM(12)  SETC  '  ****  '
&IM(13)  SETC  '   **   '
&IM(14)  SETC  '      **'
&IM(15)  SETC  ' **  ** '
&IM(16)  SETC  ' ** **  '
&IM(17)  SETC  ' *     *'
&IM(18)  SETC  ' *** ***'
&IM(19)  SETC  ' ** * **'
&IM(20)  SETC  ' ***  **'
&IM(21)  SETC  ' **** **'
&IM(22)  SETC  ' ** ****'
&IM(23)  SETC  ' **  ***'
&IM(24)  SETC  '********'
&IM(25)  SETC  '  *   * '
&IM(26)  SETC  '     ** '
&IM(27)  SETC  '    **  '
&IM(28)  SETC  '  **    '
&IM(29)  SETC  '      * '
&IM(30)  SETC  '  *     '
&IM(31)  SETC  '     *  '
&IM(32)  SETC  '   * *  '
&IM(33)  SETC  '  *  *  '
&IM(34)  SETC  '   **** '
&IM(35)  SETC  ' ** *   '
&IM(36)  SETC  '    * **'
&IM(37)  SETC  ' *  ** *'
&IM(38)  SETC  ' * * * *'
&IM(39)  SETC  '  *  * *'
&IM(40)  SETC  ' *   * *'
&IM(41)  SETC  '  *** * '
&IM(42)  SETC  '  * * * '
&IM(43)  SETC  '  * *   '
&IM(44)  SETC  '       *'
&IM(45)  SETC  ' *      '
&IM(46)  SETC  '*       '
&IM(47)  SETC  '   *  * '
&IM(48)  SETC  '        '
.*
&C(1)    SETC  ' '
&C(2)    SETC  '&IM(48)'
&C(3)    SETC  '&IM(48)'
&C(4)    SETC  '&IM(48)'
&C(5)    SETC  '&IM(48)'
&C(6)    SETC  '&IM(48)'
&C(7)    SETC  '&IM(48)'
&C(8)    SETC  '&IM(48)'
&C(9)    SETC  '&IM(48)'
&C(10)   SETC  'A'
&C(11)   SETC  '&IM(1)'
&C(12)   SETC  '&IM(2)'
&C(13)   SETC  '&IM(3)'
&C(14)   SETC  '&IM(4)'
&C(15)   SETC  '&IM(4)'
&C(16)   SETC  '&IM(5)'
&C(17)   SETC  '&IM(4)'
&C(18)   SETC  '&IM(4)'
&C(19)   SETC  'B'
&C(20)   SETC  '&IM(6)'
&C(21)   SETC  '&IM(4)'
&C(22)   SETC  '&IM(4)'
&C(23)   SETC  '&IM(6)'
&C(24)   SETC  '&IM(4)'
&C(25)   SETC  '&IM(4)'
&C(26)   SETC  '&IM(4)'
&C(27)   SETC  '&IM(6)'
&C(28)   SETC  'C'
&C(29)   SETC  '&IM(7)'
&C(30)   SETC  '&IM(4)'
&C(31)   SETC  '&IM(4)'
&C(32)   SETC  '&IM(8)'
&C(33)   SETC  '&IM(8)'
&C(34)   SETC  '&IM(4)'
&C(35)   SETC  '&IM(4)'
&C(36)   SETC  '&IM(7)'
&C(37)   SETC  'D'
&C(38)   SETC  '&IM(6)'
&C(39)   SETC  '&IM(4)'
&C(40)   SETC  '&IM(4)'
&C(41)   SETC  '&IM(4)'
&C(42)   SETC  '&IM(4)'
&C(43)   SETC  '&IM(4)'
&C(44)   SETC  '&IM(4)'
&C(45)   SETC  '&IM(6)'
&C(46)   SETC  'E'
&C(47)   SETC  '&IM(6)'
&C(48)   SETC  '&IM(8)'
&C(49)   SETC  '&IM(8)'
&C(50)   SETC  '&IM(9)'
&C(51)   SETC  '&IM(8)'
&C(52)   SETC  '&IM(8)'
&C(53)   SETC  '&IM(8)'
&C(54)   SETC  '&IM(5)'
&C(55)   SETC  'F'
&C(56)   SETC  '&IM(5)'
&C(57)   SETC  '&IM(8)'
&C(58)   SETC  '&IM(8)'
&C(59)   SETC  '&IM(10)'
&C(60)   SETC  '&IM(8)'
&C(61)   SETC  '&IM(8)'
&C(62)   SETC  '&IM(8)'
&C(63)   SETC  '&IM(8)'
&C(64)   SETC  'G'
&C(65)   SETC  '&IM(7)'
&C(66)   SETC  '&IM(4)'
&C(67)   SETC  '&IM(4)'
&C(68)   SETC  '&IM(8)'
&C(69)   SETC  '&IM(23)'
&C(70)   SETC  '&IM(4)'
&C(71)   SETC  '&IM(4)'
&C(72)   SETC  '&IM(7)'
&C(73)   SETC  'H'
&C(74)   SETC  '&IM(4)'
&C(75)   SETC  '&IM(4)'
&C(76)   SETC  '&IM(4)'
&C(77)   SETC  '&IM(5)'
&C(78)   SETC  '&IM(4)'
&C(79)   SETC  '&IM(4)'
&C(80)   SETC  '&IM(4)'
&C(81)   SETC  '&IM(4)'
&C(82)   SETC  'I'
&C(83)   SETC  '&IM(12)'
&C(84)   SETC  '&IM(13)'
&C(85)   SETC  '&IM(13)'
&C(86)   SETC  '&IM(13)'
&C(87)   SETC  '&IM(13)'
&C(88)   SETC  '&IM(13)'
&C(89)   SETC  '&IM(13)'
&C(90)   SETC  '&IM(12)'
&C(91)   SETC  'J'
&C(92)   SETC  '&IM(14)'
&C(93)   SETC  '&IM(14)'
&C(94)   SETC  '&IM(14)'
&C(95)   SETC  '&IM(14)'
&C(96)   SETC  '&IM(14)'
&C(97)   SETC  '&IM(4)'
&C(98)   SETC  '&IM(4)'
&C(99)   SETC  '&IM(7)'
&C(100)  SETC  'K'
&C(101)  SETC  '&IM(4)'
&C(102)  SETC  '&IM(15)'
&C(103)  SETC  '&IM(16)'
&C(104)  SETC  '&IM(9)'
&C(105)  SETC  '&IM(9)'
&C(106)  SETC  '&IM(16)'
&C(107)  SETC  '&IM(15)'
&C(108)  SETC  '&IM(4)'
&C(109)  SETC  'L'
&C(110)  SETC  '&IM(8)'
&C(111)  SETC  '&IM(8)'
&C(112)  SETC  '&IM(8)'
&C(113)  SETC  '&IM(8)'
&C(114)  SETC  '&IM(8)'
&C(115)  SETC  '&IM(8)'
&C(116)  SETC  '&IM(8)'
&C(117)  SETC  '&IM(5)'
&C(118)  SETC  'M'
&C(119)  SETC  '&IM(17)'
&C(120)  SETC  '&IM(4)'
&C(121)  SETC  '&IM(18)'
&C(122)  SETC  '&IM(5)'
&C(123)  SETC  '&IM(19)'
&C(124)  SETC  '&IM(4)'
&C(125)  SETC  '&IM(4)'
&C(126)  SETC  '&IM(4)'
&C(127)  SETC  'N'
&C(128)  SETC  '&IM(4)'
&C(129)  SETC  '&IM(20)'
&C(130)  SETC  '&IM(20)'
&C(131)  SETC  '&IM(21)'
&C(132)  SETC  '&IM(22)'
&C(133)  SETC  '&IM(23)'
&C(134)  SETC  '&IM(23)'
&C(135)  SETC  '&IM(4)'
&C(136)  SETC  'O'
&C(137)  SETC  '&IM(7)'
&C(138)  SETC  '&IM(4)'
&C(139)  SETC  '&IM(4)'
&C(140)  SETC  '&IM(4)'
&C(141)  SETC  '&IM(4)'
&C(142)  SETC  '&IM(4)'
&C(143)  SETC  '&IM(4)'
&C(144)  SETC  '&IM(7)'
&C(145)  SETC  'P'
&C(146)  SETC  '&IM(6)'
&C(147)  SETC  '&IM(4)'
&C(148)  SETC  '&IM(4)'
&C(149)  SETC  '&IM(4)'
&C(150)  SETC  '&IM(6)'
&C(151)  SETC  '&IM(8)'
&C(152)  SETC  '&IM(8)'
&C(153)  SETC  '&IM(8)'
&C(154)  SETC  'Q'
&C(155)  SETC  '&IM(7)'
&C(156)  SETC  '&IM(4)'
&C(157)  SETC  '&IM(4)'
&C(158)  SETC  '&IM(4)'
&C(159)  SETC  '&IM(4)'
&C(160)  SETC  '&IM(22)'
&C(161)  SETC  '&IM(23)'
&C(162)  SETC  '&IM(7)'
&C(163)  SETC  'R'
&C(164)  SETC  '&IM(6)'
&C(165)  SETC  '&IM(4)'
&C(166)  SETC  '&IM(4)'
&C(167)  SETC  '&IM(4)'
&C(168)  SETC  '&IM(6)'
&C(169)  SETC  '&IM(16)'
&C(170)  SETC  '&IM(15)'
&C(171)  SETC  '&IM(4)'
&C(172)  SETC  'S'
&C(173)  SETC  '&IM(7)'
&C(174)  SETC  '&IM(4)'
&C(175)  SETC  '&IM(8)'
&C(176)  SETC  '&IM(7)'
&C(177)  SETC  '&IM(14)'
&C(178)  SETC  '&IM(4)'
&C(179)  SETC  '&IM(4)'
&C(180)  SETC  '&IM(7)'
&C(181)  SETC  'T'
&C(182)  SETC  '&IM(24)'
&C(183)  SETC  '&IM(13)'
&C(184)  SETC  '&IM(13)'
&C(185)  SETC  '&IM(13)'
&C(186)  SETC  '&IM(13)'
&C(187)  SETC  '&IM(13)'
&C(188)  SETC  '&IM(13)'
&C(189)  SETC  '&IM(13)'
&C(190)  SETC  'U'
&C(191)  SETC  '&IM(4)'
&C(192)  SETC  '&IM(4)'
&C(193)  SETC  '&IM(4)'
&C(194)  SETC  '&IM(4)'
&C(195)  SETC  '&IM(4)'
&C(196)  SETC  '&IM(4)'
&C(197)  SETC  '&IM(4)'
&C(198)  SETC  '&IM(7)'
&C(199)  SETC  'V'
&C(200)  SETC  '&IM(4)'
&C(201)  SETC  '&IM(4)'
&C(202)  SETC  '&IM(4)'
&C(203)  SETC  '&IM(4)'
&C(204)  SETC  '&IM(4)'
&C(205)  SETC  '&IM(3)'
&C(206)  SETC  '&IM(2)'
&C(207)  SETC  '&IM(1)'
&C(208)  SETC  'W'
&C(209)  SETC  '&IM(4)'
&C(210)  SETC  '&IM(4)'
&C(211)  SETC  '&IM(4)'
&C(212)  SETC  '&IM(4)'
&C(213)  SETC  '&IM(19)'
&C(214)  SETC  '&IM(5)'
&C(215)  SETC  '&IM(18)'
&C(216)  SETC  '&IM(25)'
&C(217)  SETC  'X'
&C(218)  SETC  '&IM(4)'
&C(219)  SETC  '&IM(4)'
&C(220)  SETC  '&IM(3)'
&C(221)  SETC  '&IM(2)'
&C(222)  SETC  '&IM(2)'
&C(223)  SETC  '&IM(3)'
&C(224)  SETC  '&IM(4)'
&C(225)  SETC  '&IM(4)'
&C(226)  SETC  'Y'
&C(227)  SETC  '&IM(4)'
&C(228)  SETC  '&IM(4)'
&C(229)  SETC  '&IM(4)'
&C(230)  SETC  '&IM(3)'
&C(231)  SETC  '&IM(2)'
&C(232)  SETC  '&IM(13)'
&C(233)  SETC  '&IM(13)'
&C(234)  SETC  '&IM(13)'
&C(235)  SETC  'Z'
&C(236)  SETC  '&IM(5)'
&C(237)  SETC  '&IM(14)'
&C(238)  SETC  '&IM(26)'
&C(239)  SETC  '&IM(27)'
&C(240)  SETC  '&IM(13)'
&C(241)  SETC  '&IM(28)'
&C(242)  SETC  '&IM(8)'
&C(243)  SETC  '&IM(5)'
&C(244)  SETC  '1'
&C(245)  SETC  '&IM(1)'
&C(246)  SETC  '&IM(13)'
&C(247)  SETC  '&IM(1)'
&C(248)  SETC  '&IM(1)'
&C(249)  SETC  '&IM(1)'
&C(250)  SETC  '&IM(1)'
&C(251)  SETC  '&IM(1)'
&C(252)  SETC  '&IM(2)'
&C(253)  SETC  '2'
&C(254)  SETC  '&IM(2)'
&C(255)  SETC  '&IM(25)'
&C(256)  SETC  '&IM(29)'
&C(257)  SETC  '&IM(29)'
&C(258)  SETC  '&IM(2)'
&C(259)  SETC  '&IM(30)'
&C(260)  SETC  '&IM(30)'
&C(261)  SETC  '&IM(7)'
&C(262)  SETC  '3'
&C(263)  SETC  '&IM(2)'
&C(264)  SETC  '&IM(25)'
&C(265)  SETC  '&IM(29)'
&C(266)  SETC  '&IM(27)'
&C(267)  SETC  '&IM(29)'
&C(268)  SETC  '&IM(25)'
&C(269)  SETC  '&IM(25)'
&C(270)  SETC  '&IM(2)'
&C(271)  SETC  '4'
&C(272)  SETC  '&IM(31)'
&C(273)  SETC  '&IM(27)'
&C(274)  SETC  '&IM(32)'
&C(275)  SETC  '&IM(33)'
&C(276)  SETC  '&IM(6)'
&C(277)  SETC  '&IM(31)'
&C(278)  SETC  '&IM(31)'
&C(279)  SETC  '&IM(31)'
&C(280)  SETC  '5'
&C(281)  SETC  '&IM(12)'
&C(282)  SETC  '&IM(30)'
&C(283)  SETC  '&IM(30)'
&C(284)  SETC  '&IM(12)'
&C(285)  SETC  '&IM(29)'
&C(286)  SETC  '&IM(25)'
&C(287)  SETC  '&IM(25)'
&C(288)  SETC  '&IM(2)'
&C(289)  SETC  '6'
&C(290)  SETC  '&IM(2)'
&C(291)  SETC  '&IM(25)'
&C(292)  SETC  '&IM(30)'
&C(293)  SETC  '&IM(12)'
&C(294)  SETC  '&IM(25)'
&C(295)  SETC  '&IM(25)'
&C(296)  SETC  '&IM(25)'
&C(297)  SETC  '&IM(2)'
&C(298)  SETC  '7'
&C(299)  SETC  '&IM(7)'
&C(300)  SETC  '&IM(29)'
&C(301)  SETC  '&IM(29)'
&C(302)  SETC  '&IM(31)'
&C(303)  SETC  '&IM(1)'
&C(304)  SETC  '&IM(1)'
&C(305)  SETC  '&IM(1)'
&C(306)  SETC  '&IM(1)'
&C(307)  SETC  '8'
&C(308)  SETC  '&IM(2)'
&C(309)  SETC  '&IM(25)'
&C(310)  SETC  '&IM(25)'
&C(311)  SETC  '&IM(2)'
&C(312)  SETC  '&IM(25)'
&C(313)  SETC  '&IM(25)'
&C(314)  SETC  '&IM(25)'
&C(315)  SETC  '&IM(2)'
&C(316)  SETC  '9'
&C(317)  SETC  '&IM(2)'
&C(318)  SETC  '&IM(25)'
&C(319)  SETC  '&IM(25)'
&C(320)  SETC  '&IM(25)'
&C(321)  SETC  '&IM(34)'
&C(322)  SETC  '&IM(29)'
&C(323)  SETC  '&IM(25)'
&C(324)  SETC  '&IM(2)'
&C(325)  SETC  '0'
&C(326)  SETC  '&IM(2)'
&C(327)  SETC  '&IM(25)'
&C(328)  SETC  '&IM(25)'
&C(329)  SETC  '&IM(25)'
&C(330)  SETC  '&IM(25)'
&C(331)  SETC  '&IM(25)'
&C(332)  SETC  '&IM(25)'
&C(333)  SETC  '&IM(2)'
&C(334)  SETC  '$'
&C(335)  SETC  '&IM(1)'
&C(336)  SETC  '&IM(7)'
&C(337)  SETC  '&IM(19)'
&C(338)  SETC  '&IM(35)'
&C(339)  SETC  '&IM(7)'
&C(340)  SETC  '&IM(36)'
&C(341)  SETC  '&IM(19)'
&C(342)  SETC  '&IM(7)'
&C(343)  SETC  '#'
&C(344)  SETC  '&IM(3)'
&C(345)  SETC  '&IM(3)'
&C(346)  SETC  '&IM(5)'
&C(347)  SETC  '&IM(3)'
&C(348)  SETC  '&IM(3)'
&C(349)  SETC  '&IM(5)'
&C(350)  SETC  '&IM(3)'
&C(351)  SETC  '&IM(3)'
&C(352)  SETC  '@'
&C(353)  SETC  '&IM(7)'
&C(354)  SETC  '&IM(17)'
&C(355)  SETC  '&IM(37)'
&C(356)  SETC  '&IM(38)'
&C(357)  SETC  '&IM(39)'
&C(358)  SETC  '&IM(40)'
&C(359)  SETC  '&IM(40)'
&C(360)  SETC  '&IM(41)'
&C(361)  SETC  ','
&C(362)  SETC  '&IM(48)'
&C(363)  SETC  '&IM(48)'
&C(364)  SETC  '&IM(48)'
&C(365)  SETC  '&IM(48)'
&C(366)  SETC  '&IM(13)'
&C(367)  SETC  '&IM(13)'
&C(368)  SETC  '&IM(1)'
&C(369)  SETC  '&IM(11)'
&C(370)  SETC  '.'
&C(371)  SETC  '&IM(48)'
&C(372)  SETC  '&IM(48)'
&C(373)  SETC  '&IM(48)'
&C(374)  SETC  '&IM(48)'
&C(375)  SETC  '&IM(48)'
&C(376)  SETC  '&IM(13)'
&C(377)  SETC  '&IM(13)'
&C(378)  SETC  '&IM(48)'
&C(379)  SETC  '('
&C(380)  SETC  '&IM(1)'
&C(381)  SETC  '&IM(11)'
&C(382)  SETC  '&IM(30)'
&C(383)  SETC  '&IM(30)'
&C(384)  SETC  '&IM(30)'
&C(385)  SETC  '&IM(30)'
&C(386)  SETC  '&IM(11)'
&C(387)  SETC  '&IM(1)'
&C(388)  SETC  ')'
&C(389)  SETC  '&IM(1)'
&C(390)  SETC  '&IM(1)'
&C(391)  SETC  '&IM(31)'
&C(392)  SETC  '&IM(31)'
&C(393)  SETC  '&IM(31)'
&C(394)  SETC  '&IM(31)'
&C(395)  SETC  '&IM(1)'
&C(396)  SETC  '&IM(11)'
&C(397)  SETC  '+'
&C(398)  SETC  '&IM(1)'
&C(399)  SETC  '&IM(2)'
&C(400)  SETC  '&IM(42)'
&C(401)  SETC  '&IM(43)'
&C(402)  SETC  '&IM(2)'
&C(403)  SETC  '&IM(43)'
&C(404)  SETC  '&IM(42)'
&C(405)  SETC  '&IM(2)'
&C(406)  SETC  '/'
&C(407)  SETC  '&IM(44)'
&C(408)  SETC  '&IM(29)'
&C(409)  SETC  '&IM(31)'
&C(410)  SETC  '&IM(1)'
&C(411)  SETC  '&IM(11)'
&C(412)  SETC  '&IM(30)'
&C(413)  SETC  '&IM(45)'
&C(414)  SETC  '&IM(46)'
&C(415)  SETC  '"'
&C(416)  SETC  '&IM(13)'
&C(417)  SETC  '&IM(13)'
&C(418)  SETC  '&IM(1)'
&C(419)  SETC  '&IM(11)'
&C(420)  SETC  '&IM(48)'
&C(421)  SETC  '&IM(48)'
&C(422)  SETC  '&IM(48)'
&C(423)  SETC  '&IM(48)'
&C(424)  SETC  '*'
&C(425)  SETC  '&IM(48)'
&C(426)  SETC  '&IM(25)'
&C(427)  SETC  '&IM(32)'
&C(428)  SETC  '&IM(5)'
&C(429)  SETC  '&IM(32)'
&C(430)  SETC  '&IM(25)'
&C(431)  SETC  '&IM(48)'
&C(432)  SETC  '&IM(48)'
.*
.*
         AIF   (K'&TITLE LE 0).NTITL
.*
&TITLE(1) TITLE &TITLE(2)
.*
.*
.NTITL   ANOP
&K       SETA  K'&STRING
         AIF   (&K GT 48).DONE
.*
&M       SETA  1
         AIF   (&K GT 0).COPY2
&K       SETA  K'&NAME
.*
.*
.COPY1   ANOP
&STR(&M) SETC  '&NAME'(&M,1)
&M       SETA  &M+1
         AIF   (&M LE &K).COPY1
         AGO   .COPYD
.*
.*
.COPY2   ANOP
&STR(&M) SETC  '&STRING'(&M,1)
&M       SETA  &M+1
         AIF   (&M LE &K).COPY2
.*
.COPYD   ANOP
&J       SETA  0
&M       SETA  0
.*
.*
.EDIT    AIF   (&M EQ &K).AGIN
&M       SETA  &M+1
&I       SETA  &M
         AIF   ('&STR(&M)' NE '''').EDIT
.*
         AIF   (&J GT 0).QTCK
.*
&J       SETA  1
         AGO   .COMP
.*
.*
.QTCK    AIF   ('&STR(&M+1)' NE '''').COMP
.*
&STR(&M) SETC  '"'
&J       SETA  0
&M       SETA  &M+1
&I       SETA  &M
.*
.*
.COMP    ANOP
&STR(&I) SETC  '&STR(&I+1)'
&I       SETA  &I+1
         AIF   (&I LT &K).COMP
.*
&STR(&K) SETC  ' '
&K       SETA  &K-1
&M       SETA  &M-1
         AGO   .EDIT
.*
.*
.AGIN    ANOP
&I       SETA  1
&L       SETA  &K-&N
         AIF   (&L LE 0).HEXS
.*
         AIF   (&L LE 6).CK4
.*
&L       SETA  6
.*
.*
.CK4     AIF   (&L GT 4).SETL
.*
&J       SETA  8
         AIF   (&L GT 2).FILT
.*
&J       SETA  16
.*
.*
.FILT    AIF   (&I GT &J).SETL
.*
&FLD(&I) SETC  '&C(2)'
&I       SETA  &I+1
         AGO   .FILT
.*
.*
.SETL    ANOP
&L       SETA  &L+&N
.*
.*
.LOOP    AIF   (&N EQ &L).FINI
.*
&N       SETA  &N+1
&J       SETA  1
&JL      SETA  9
.*
.*
.MTCH    AIF   ('&STR(&N)' EQ '&C(&J)').GO
.*
&J       SETA  &J+9
&JL      SETA  &J+8
         AIF   (&J LT 424).MTCH
.*
.*
.GO      AIF   (&J EQ &JL).LOOP
.*
&J       SETA  &J+1
&FLD(&I) SETC  '&C(&J)'
&I       SETA  &I+1
         AGO   .GO
.*
.*
.FINI    AIF   (&I GT 48).DOIT
.*
&FLD(&I) SETC  ' '
&I       SETA  &I+1
         AGO   .FINI
.*
.*
.DOIT    ANOP
         SPACE 2
 MNOTE *,'            &FLD(1)&FLD(9)&FLD(17)&FLD(25)&FLD(33)&FLD(41)'
 MNOTE *,'            &FLD(2)&FLD(10)&FLD(18)&FLD(26)&FLD(34)&FLD(42)'
 MNOTE *,'            &FLD(3)&FLD(11)&FLD(19)&FLD(27)&FLD(35)&FLD(43)'
 MNOTE *,'            &FLD(4)&FLD(12)&FLD(20)&FLD(28)&FLD(36)&FLD(44)'
 MNOTE *,'            &FLD(5)&FLD(13)&FLD(21)&FLD(29)&FLD(37)&FLD(45)'
 MNOTE *,'            &FLD(6)&FLD(14)&FLD(22)&FLD(30)&FLD(38)&FLD(46)'
 MNOTE *,'            &FLD(7)&FLD(15)&FLD(23)&FLD(31)&FLD(39)&FLD(47)'
 MNOTE *,'            &FLD(8)&FLD(16)&FLD(24)&FLD(32)&FLD(40)&FLD(48)'
         AGO   .AGIN
.*
.*
.HEXS    ANOP
&HX      SETA  2*((K'&NAME)/2)+1
&BR      SETA  &HX+5
&R0      SETA  0
&R1      SETA  1
&R       SETC  ''
&R12     SETC  '12'
&R13     SETC  '13'
&R14     SETC  '14'
&R15     SETC  '15'
         AIF   (K'&TYPE EQ 0).DONE
         AIF   ('&TYPE' NE 'START').CSEC
.*
         SPACE 3
&NAME    START &ORG
         AGO   .REGS
.*
.*
.CSEC    AIF   ('&TYPE' NE 'CSECT').DONE
.*
         SPACE 3
&NAME    CSECT
.*
.REGS    AIF   ('&REGS(1)' NE 'YES').BASE
.*
         SPACE 3
.*
R0       EQU   0 
R1       EQU   1 
R2       EQU   2 
R3       EQU   3 
R4       EQU   4 
R5       EQU   5 
R6       EQU   6 
R7       EQU   7 
R8       EQU   8 
&R       SETC  'R'
         AIF   ('&REGS(2)' EQ 'NUM').NREG
.*
R9       EQU   9 
RA       EQU   10 
RB       EQU   11 
RC       EQU   12 
RD       EQU   13 
RE       EQU   14 
RF       EQU   15 
&R12     SETC  'C'
&R13     SETC  'D'
&R14     SETC  'E'
&R15     SETC  'F'
         AIF   ('&REGS(2)' EQ 'HEX').BASE
.*
.*
.NREG    ANOP
R9       EQU   9 
R10      EQU   10 
R11      EQU   11 
R12      EQU   12 
R13      EQU   13 
R14      EQU   14 
R15      EQU   15 
.*
.*
.BASE    ANOP
&SAV     SETC  '&R.&R13'
         AIF   (K'&BASE EQ 0).SAVE
         AIF   (&BASE LE 0).SAVE
.*
         SPACE 1
BASE     EQU   &BASE 
.*
.*
.SAVE    AIF   (K'&SAVE EQ 0).AREG
         AIF   (&SAVE LE 0).AREG
.*
         SPACE 1
SAVE     EQU   &SAVE 
&SAV     SETC  'SAVE'
.*
.*
.AREG    ANOP
&K       SETA  N'&ADREG
         AIF   (&K LE 0).NTER
.*
         SPACE 1
*                                 MISCELANEOUS EQUATES
.*
.*
.LA      AIF   (&K LE 0).NTER
.*
&N       SETA  K'&ADREG(&K)
&M       SETA  &N
.*
.*
.LB      AIF   (&N LE 0).DA
         AIF   ('&ADREG(&K)'(&N,1) EQ '=').SB
.*
&N       SETA  &N-1
         AGO   .LB
.*
.*
.SB      ANOP
&ADR     SETC  '&ADREG(&K)'(1,&N-1)
&ADE     SETC  '&ADREG(&K)'(&N+1,&M-&N)
&ADR     EQU   &ADE
.*
.*
.DA      ANOP
&K       SETA  &K-1
         AGO   .LA
.*
.*
.NTER    ANOP
         EJECT
         B     &BR.(15)           ENTRY POINT
         DC    X'&HX' 
         DC    CL&HX'&NAME' 
         STM   &R.&R14,&R.&R12,12(&SAV) 
         AIF   (&BASE LE 0).DONE
.*
         SPACE 1
         BALR  BASE,0 
         USING *,BASE 
         AIF   (K'&SIZE EQ 0).NSIZ
         SPACE 1
         L     &R.&R0,&SIZE 
         BAL   &R.&R1,*+4 
         SVC   10 
         ST    &SAV,4(,&R.&R1) 
         ST    &R.&R1,8(,&SAV) 
         AGO   .SAVR
.*
.*
.NSIZ    AIF   (K'&SECT EQ 0).RPAR
         SPACE 1
         ST    &SAV,&SECT.+4 
         LA    &R.&R1,&SECT 
         ST    &R.&R1,8(,&SAV) 
.*
.*
.SAVR    LR    &SAV,&R.&R1 
         AIF   (K'&SECT EQ 0).RPAR
.*
         USING &SECT,&SAV 
.*
.*
.RPAR    AIF   ('&PARM' EQ 'NO').DONE
.*
         SPACE 1
         L     &R.&R1,4(,&SAV) 
         L     &R.&R1,24(,&R.&R1) 
.*
.*
.DONE    ANOP
         SPACE 3
         MEND
         EJECT
         MACRO
&NAME    EXIT  &REGS=(YES,HEX),   EQUATES AVAILABLE R0-RF EQU 0-15     X
               &SAVE=SAVE,        SAVE REGISTER EQUATE (REG 13)        X
               &SIZE=,            SAVE AREA SIZE (TO BE FREE'D)        X
               &RC=0              RETURN CODE (DECIMAL), OR            X
               &RC=(REG),              OR &RC='LABEL'
.*
.*
         LCLA  &R0
         LCLA  &R1
         LCLA  &M
         LCLA  &N
.*
         LCLC  &R12
         LCLC  &R13
         LCLC  &R14
         LCLC  &R15
         LCLC  &R
         LCLC  &RCD
         LCLC  &SAV
.*
.*
&R0      SETA  0
&R1      SETA  1
.*
&R12     SETC  '12'
&R13     SETC  '13'
&R14     SETC  '14'
&R15     SETC  '15'
&R       SETC  ''
.*
.*
.*
         AIF   (K'&NAME LE 0).NONM
.*
&NAME    DS    0H
.*
.*
.NONM    AIF   ('&REGS(1)' EQ 'NO').SRC
.*
&R       SETC  'R'
         AIF   ('&REGS(2)' EQ 'NUM').SRC
.*
&R12     SETC  'C'
&R13     SETC  'D'
&R14     SETC  'E'
&R15     SETC  'F'
.*
.*
.SRC     AIF   (T'&RC EQ 'O').SREG
         AIF   (T'&RC EQ 'F').FWRC
         AIF   (T'&RC EQ 'H').HWRC
         AIF   (T'&RC EQ 'C').M255
         AIF   (T'&RC EQ 'N').SDRC
         AIF   (T'&RC NE 'U').SREG
.*
         LR    &R.&R15,&RC(1) 
         AGO   .SREG
.*
.*
.SDRC    LA    &R.&R15,&RC 
         AGO   .SREG
.*
.*
.M255    SLR   &R.&R15,&R.&R15    LOAD RETURN                        BA
         IC    &R.&R15,&RC 
         AGO   .SREG
.*
.*
.HWRC    LH    &R.&R15,&RC 
         AGO   .SREG
.*
.*
.FWRC    L     &R.&R15,&RC 
.SREG    ANOP
&SAV     SETC  '&R.&R13'
         AIF   (K'&SAVE EQ 0).NSAV
.*
&SAV     SETC  '&SAVE'
.*
.*
.NSAV    AIF   (K'&SIZE EQ 0).NSIZ
.*
         LR    &R.&R0,&SAV 
.*
.*
.NSIZ    L     &SAV,4(,&SAV) 
         AIF   (K'&SIZE EQ 0).RS14
.*
         ST    &R.&R1,24(,&SAV) 
         LR    &R.&R1,&R.&R0 
         L     &R.&R0,&SIZE 
         LA    &R.&R1,0(,&R.&R1) 
         SVC   10 
.*
.*
.RS14    L     &R.&R14,12(,&SAV) 
.*
.*
.DONE    LM    &R.&R0,&R.&R12,20(&SAV) 
         BR    &R.&R14 
         SPACE 3
         MEND
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                          ENTRY AND EQUATES'
MIM#3    HEDIT 'MIM #3 REL    4.0',TYPE=START,SECT=SAVEA,SIZE=GETSAVE, X
               PARM=YES
* *  INPUT PARAMETERS (INDIN)
*
STDECK   EQU   1                  A STANDARD DECK WILL BE PUNCHED
SSDECK   EQU   2                  A SOURCE DECK WILL BE PUNCHED
KEYSONLY EQU   16                 KEY STATEMENTS ONLY PRINTED
INDXLIST EQU   32                 AN INDEX WILL BE PRINTED
TAPECOPY EQU   64                 A TAPE COPY WILL BE PRODUCED
LISTING  EQU   128                A LISTING WILL BE PRODUCED
*
* *  INPUT & OPERATIONAL PARAMETERS (INDON)
*
MAXMLINE EQU   1                  MAXMEMBERLINES REACHED (INT INDIC)
MORTAPE  EQU   2                  MORE TAPE MEMBERS (INTERNAL INDIC)
FORSNEXT EQU   4                  FORCE PRINT OF NEXT LINE (INT INDIC)
ANYTAPE  EQU   8                  A TAPE MEMBER LOCATED (INTERN INDIC)
INCLOPT  EQU   16                 INCLUDE/EXCLUDE INPUT OPTION
SELECTIT EQU   32                 NAME FOUND IN INPUT LIST (INT INDIC)
NOREJECT EQU   64                 REJECT NOT SPEC'D (INPUT OPTION)
CARDATA  EQU   128                CARD INPUT DATA PRESENT
*
* *  OUTPUT OPERATIONAL PARAMETERS (INDOT)
*
HAVEMAXC EQU   4                  INDIC MAX CARDS PUNCHED
HAVEMAXL EQU   8                  INDIC MAX LINES PRINTED
*  ALL OTHER BITS & MEANINGS SAME AS IN 'INDIN' - USED OPERATIONALLY
*  TO FLAG COMPLETION OF REQUESTED OPTIONS
         EJECT
* *  COMPLETION CODES (CMPLCD)
*
CMPL00   EQU   0                  0000 - NO SIGNIFICANT ERRORS
CMPL04   EQU   4                  0004 - STD DECK REQUESTED BUT
*                                        SYSPUNCH COULD NOT BE OPENED
CMPL08   EQU   8                  0008 - TAPE REQUESTED BUT
*                                        OUTAPE COULD NOT BE OPENED
CMPL12   EQU   12                 0012 - SOURCE DECK REQUESTED BUT
*                                        SYSPUNCH COULD NOT BE OPENED
CMPL16   EQU   16                 0016 - SYSPRINT, OR INTAPE,
*                                        COULD NOT BE OPENED
CMPLHI   EQU   240                0016 OR ABOVE - DISASTROUS ERROR
*                                        EXECUTION TERMINATES
*
* *  IDENTITY FIELD COUNT (IDNTNO)
*
IDHERE   EQU   128                INDICATES VALID ID COUNT PRESENT
*                                 REST OF BYTE HOLDS CHAR COUNT
*
* *  MISCELANEOUS INTERNAL FLAGS
*
INFL     EQU   0                  OPEN FLAG - 'INPUT'
OUTFL    EQU   15                 OPEN FLAG - 'OUTPUT'
RERDFL   EQU   16                 OPEN FLAG - 'REREAD'
LASTFL   EQU   128                OPEN FLAG - 'LAST'
OPENOK   EQU   16                 OPEN SUCCESSFUL
KILLFL   EQU   240                INDIC FILE NOT OPEN
VBLOCK   EQU   80                 DCB RECFM - VARIABLE, BLOCKED
FBLOCK   EQU   144                DCB RECFM - FIXED, BLOCKED
COMPRESS EQU   128                ADDED DCB FLAG - IND COMPRESSED DATA
JFCBR    EQU   7                  DCB EXLST INDICATE READ JFCB
DCBXT    EQU   5                  DCB EXLST INDICATE DCB EXIT
*
NUL      EQU   0                  NULL BYTE
BLANK    EQU   64                 CHARACTER BLANK
FF       EQU   255                'FF' BYTE
PMSK     EQU   15                 MASK TO EXTRACT NUMERIC
NUMR     EQU   240                NUMERIC CHARACTER MASK
*
CCSP1    EQU   64                 CARR CNTRL - SINGLE SPACE
CCSP2    EQU   240                CARR CNTRL - DOUBLE SPACE
CCSKP    EQU   241                CARR CNTRL - SKIP TO CH 1
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                             INITIALIZATION'
         MVC   LINECNT,LINEDFLT   DEFAULT LINES/PAGE COUNT
         XC    INDIN,INDIN        DEFAULT ALL OPTIONS OFF
         MVI   INDON,INCLOPT+NOREJECT  DEFAULT INCLUDE & NO REJECT
         XC    INDOT,INDOT        DEFAULT ALL INDICATORS OFF
         XC    IDNTNO,IDNTNO      DEFAULT NO IDENTITY
         MVI   IDENT,BLANK        BLANK OUT ID
         MVC   IDENT+1(51),IDENT       SAVE FIELD
         MVI   CMPLCD,CMPL00      DEFAULT COMPLETION CODE ZERO
         ZAP   CMXCOUNT,CMXDFLT   DEFAULT MAXIMUM CARD COUNT
         ZAP   LMXCOUNT,LMXDFLT   DEFAULT MAXIMUM LINE COUNT
         ZAP   MMXCOUNT,MMXDFLT   DEFAULT MAXIMUM LINES/MEMBER
         MVI   PONE,X'1C'         INIT PACKED ONE CONSTANT
         MVI   PZRO,X'0C'         INIT PACKED ZERO CONSTANT
         ZAP   INPCOUNT,PZRO      INIT NAME COUNT
         ZAP   MEMCOUNT,PZRO      INIT MEMBER COUNT
         ZAP   CARCOUNT,PZRO      INIT 'CARDS' COUNT
         ZAP   CARTOTAL,PZRO      INIT TOTAL CARDS
         ZAP   PGECOUNT,PZRO      INIT PAGE COUNTER
         ZAP   TAPCOUNT,PZRO      INIT OUTAPE MEMBER COUNT ZERO
         ZAP   TPCCOUNT,PZRO      INIT OUTAPE CARD COUNT ZERO
         ZAP   LISCOUNT,PZRO      INIT LIST MEMBER COUNT ZERO
         ZAP   LSCCOUNT,PZRO      INIT LIST CARD COUNT ZERO
         ZAP   DEKCOUNT,PZRO      INIT DECK MEMBER COUNT ZERO
         ZAP   DKCCOUNT,PZRO      INIT DECK CARD COUNT ZERO
*---------------------------------------------------------------JSF@RPI
*              DETERMINE WHETHER SYSTEM IS OS OR CMS            JSF@RPI
*---------------------------------------------------------------JSF@RPI
         MVI   SYSTEM,C'O'        ASSUME SYSTEM = OS.           JSF@RPI
         TM    0(R1),X'7F'        OS OR CMS PARAMETER?          JSF@RPI
         BZ    OSPARM             ..ADCON => OS SYSTEM.         JSF@RPI
         MVI   SYSTEM,C'C'        ..CHARS => CMS SYSTEM.        JSF@RPI
CMSPARM  DS    0H                                               JSF@RPI
         LR    R6,R1              START OF PARAMETERS.          JSF@RPI
         S     R6,=F'8'           BACKUP BY INITIAL INCREMENT.  JSF@RPI
         B     NEXTPARM                                         JSF@RPI
         SPACE ,                                                JSF@RPI
OSPARM   DS    0H                                               JSF@RPI
         L     R1,0(,R1)          R1 PTR TO PARM
         LH    R5,0(,R1)          IS PARM
         LTR   R5,R5                   LENGTH ZERO ?
         BNP   GETCARDS           YES, FORGET IT
*
         LA    R3,1(,R1)          R3 ADDR TO START SCAN
         LA    R5,0(R5,R3)        R5 ADDR TO STOP SCAN
         LA    R4,1               R4 SCAN INCREMENT
*
*
NEXTPARM DS    0H                                               JSF@RPI
         CLI   SYSTEM,C'O'        OS/VS SYSTEM?                 JSF@RPI
         BE    OSNEXTP            ..YES.                        JSF@RPI
         LA    R6,8(,R6)          NEXT PARAMETER TOKEN.         JSF@RPI
         CLI   0(R6),X'FF'        CMS END-OF-PARM FENCE?        JSF@RPI
         BL    CHEKLIST           ..NO, SEE WHAT PARM IS.       JSF@RPI
         B     GETCARDS           ..YES, PARM SCAN COMPLETE.    JSF@RPI
OSNEXTP  DS    0H                                               JSF@RPI
         LA    R6,1(0,R3)         R6 PTR TO POSSIBLE PARM
         CR    R6,R5              IS THERE MORE ?
         BNL   GETCARDS           NO, PARM SCAN COMPLETE
*
*
NEXTCHAR BXH   R3,R4,CHEKLIST     BUMP TO NEXT PARM CHAR
         CLI   0(R3),C','         IS IT A COMMA ?
         BNE   NEXTCHAR           NO, GO SCAN SOME MORE
*
*
CHEKLIST CLC   0(4,R6),LIST       IS IT 'LIST' OPTION ?
         BNE   DECKOPT            NO, GO CHECK IF DECK OPTION
*
         OI    INDIN,LISTING      SET LIST OPTION 'ON'
         B     CHEKCNT2           GO CHECK IF 'LIST= ' OPTION
*
*
DECKOPT  CLC   0(4,R6),DECK       IS IT 'DECK' OPTION ?
         BNE   SDECOPT            NO, GO CHECK IF SOURCE DECK OPTION
*
         NI    INDIN,FF-STDECK-SSDECK  TURN OFF DECK OPTIONS
         OI    INDIN,STDECK       SET DECK OPTION 'ON'
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
SDECOPT  CLC   0(4,R6),SDECK      IS IT 'SDEC(K)' OPTION ?
         BNE   TAPEOPT            NO, GO CHECK TAPE OPTION
*
         NI    INDIN,FF-STDECK-SSDECK  TURN OFF DECK OPTIONS
         OI    INDIN,SSDECK       SET SOURCE DECK OPTION 'ON'
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
TAPEOPT  CLC   0(4,R6),TAPE       IS IT 'TAPE' OPTION ?
         BNE   CHEKCNT            NO, GO CHECK IF LINE COUNT
*
         OI    INDIN,TAPECOPY     SET TAPE OPTION ON
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
CHEKCNT  CLI   0(R6),C'L'         IS FIRST CHAR 'L' ?
         BNE   CMAXCHK            NO, THEN CAN'T BE 'L=, LIN=, ETC'
*
*
CHEKCNT2 BAL   RE,GETNUM          GO TRY FOR A NUMBER
         MVI   DUBL,NUL           LIMIT LINES/PAGE
         MVC   DUBL+1(5),DUBL          COUNT TO
         NI    DUBL+6,PMSK             TWO DIGITS
         CVB   R7,DUBL            CONVERT TO BINARY
         LTR   R7,R7              IS COUNT GT ZERO ?
         BNP   NEXTPARM           NO, FORGET IT
*
         ST    R7,LINECNT         SAVE SPECIFIED LINES/PAGE COUNT
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
CMAXCHK  CLC   0(4,R6),MAXCARDS   IS IT 'MAXC(ARDS)' OPTION ?
         BNE   LMAXCHK            NO, GO CHECK IF MAXLINES
*
         BAL   RE,GETNUM          GO TRY FOR A NUMBER
         NI    DUBL+4,PMSK        LIM MAXCARDS TO SIX DIGITS
         MVC   CMXCOUNT,DUBL+4    SAVE MAXCARDS COUNT
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
LMAXCHK  CLC   0(4,R6),MAXLINES   IS IT 'MAXL(INES)' OPTION ?
         BNE   MMAXCHK            NO, GO CHECK FOR MAXMEMBERLINES
*
         BAL   RE,GETNUM          GO TRY FOR NUMBER
         NI    DUBL+4,PMSK        LIM MAXLINES TO SIX DIGITS
         MVC   LMXCOUNT,DUBL+4    SAVE MAXLINES COUNT
         OI    INDIN,LISTING      IMPLIED 'LIST' OPTION
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
MMAXCHK  CLC   0(4,R6),MAXMEM     IS IT 'MAXM(EMBERLINES' OPTION ?
         BNE   KEYSCHK            NO, GO CHECK FOR KEYSTATEMENTS
*
         BAL   RE,GETNUM          GO TRY FOR A NUMBER
         NI    DUBL+4,PMSK        LIMIT MAXM TO SIX DIGITS
         MVC   MMXCOUNT,DUBL+4    SAVE MAXM COUNT
         OI    INDIN,LISTING      IMPLIED 'LIST' OPTION
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
KEYSCHK  CLC   0(4,R6),KEYS       IS IT 'KEYS(TATEMENTS)' OPTION
         BNE   INDXOPT            NO, GO CHECK FOR INDEX OPTION
*
         OI    INDIN,KEYSONLY+LISTING  SET TO LIST KEY STATEMENTS ONLY
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
INDXOPT  CLC   0(3,R6),INDEX      IS IT IND(EX) OPTION ?
         BNE   INCLCHK            NO, GO CHECK INCLUDE
*
         OI    INDIN,INDXLIST     SET INDEX OPTION 'ON'
         B     NEXTPARM           GO CHECK NEXT PARM
*
*
INCLCHK  CLC   0(4,R6),INCLUDE    IS IT 'INCL(UDE)' OPTION ?
         BNE   EXCLCHK            NO, GO CHECK EXCLUDE
*
         OI    INDON,INCLOPT      SET TO INCLUDE SYSIN NAMES
         B     NEXTPARM           GO CHECK FOR MORE
*
*
EXCLCHK  CLC   0(4,R6),EXCLUDE    IS IT 'EXCL(UDE)' OPTION ?
         BNE   SELCHK             NO, GO CHECK SELECT OPTION
*
         NI    INDON,FF-INCLOPT   SET TO EXCLUDE SYSIN NAMES
         B     NEXTPARM           GO CHECK FOR MORE
*
*
SELCHK   CLC   0(3,R6),SELECT     IS IT 'SEL(ECT)' OPTION ?
         BNE   REJCHK             NO, GO CHECK REJECT OPTION
*
         OI    INDON,NOREJECT     SET REJECT OPTION NO
         B     NEXTPARM           GO CHECK FOR MORE
*
*
REJCHK   CLC   0(3,R6),REJECT     IS IT 'REJ(ECT)' OPTION ?
         BNE   IDCHECK            NO, GO CHECK IDENTITY OPTION
*
         NI    INDON,FF-NOREJECT  SET REJECT OPTION YES
         B     NEXTPARM           GO CHECK FOR MORE
*
*
IDCHECK  CLC   0(2,R6),ID         IS IT 'ID(...)' OPTION ?
         BNE   NEXTPARM           GO CHECK FOR MORE
*
         LR    R1,R6              R1 PTR TO START SCAN
         LA    R2,1               R2 SCAN INCREMENT
*
         CLI   SYSTEM,C'O'        OS/VS SYSTEM?                 JSF@RPI
         BE    OSIDCHK            ..YES, SCAN THE HARD WAY.     JSF@RPI
         CLI   8(R6),X'FF'        CMS FENCE COMING UP?          JSF@RPI
         BNL   NEXTPARM           ..YES, QUIT THIS STUFF.       JSF@RPI
         LA    R6,8(,R6)          BUMP TO NEXT PARM TOKEN.      JSF@RPI
         MVC   IDENT(8),0(R6)                                   JSF@RPI
         LA    R2,IDENT+7                                       JSF@RPI
         LA    R0,7                                             JSF@RPI
CMSIDLP  DS    0H                                               JSF@RPI
         CLI   0(R2),X'40'        TRIM TRAILING BLANKS.         JSF@RPI
         BNE   CMSIDSTC                                         JSF@RPI
         BCTR  R2,0                                             JSF@RPI
         BCT   R0,CMSIDLP                                       JSF@RPI
CMSIDSTC DS    0H                                               JSF@RPI
         STC   R0,IDNTNO          STORE LENGTH OF FIELD.        JSF@RPI
         OI    IDNTNO,IDHERE      INDIC IDENT PRESENT           JSF@RPI
         B     NEXTPARM                                         JSF@RPI
OSIDCHK  DS    0H                                               JSF@RPI
         BXH   R1,R2,NEXTPARM     BUMP TO NEXT SUB-FIELD CHAR
         CLI   0(R1),C'='         IS IT '=' ?
         BNE   *-8                NO, GO CHECK NEXT
*
         LA    R1,1(,R1)          R1 PTR TO FIRST ID CHAR
         LR    R2,R3              R2 LENGTH FOR
         SR    R2,R1                   EXECUTE MOVE
         BNP   NEXTPARM           GO CHECK FOR MORE
*
         BCTR  R2,0               R2 ONE LESS FOR EXEC
         LA    R7,51              R7 MAX MOVE LENGTH
         CR    R2,R7              IS COUNT GT MAX ?
         BNH   *+6                NO, FORGET IT
*
         LR    R2,R7              MAKE COUNT MAX
*
         LA    R7,IDENT           R7 PTR TO RCVNG FIELD
         EX    R2,MOVID           MOVE ID FIELD TO SAVE AREA
         STC   R2,IDNTNO          SAVE LENGTH OF FIELD
         OI    IDNTNO,IDHERE      INDIC IDENT PRESENT
         B     NEXTPARM           GO CHECK FOR MORE
*
*
MOVID    MVC   0(0,R7),0(R1)
*
*
GETNUM   DS    0H                                               JSF@RPI
         CLI   SYSTEM,C'O'        OS/VS SYSTEM?                 JSF@RPI
         BE    OSGETNUM           ..YES.                        JSF@RPI
         CLI   8(R6),X'FF'        CMS END-OF-PARM FENCE COMING? JSF@RPI
         BNH   NEXTPARM           ..YES, SKIP THIS JUNK.        JSF@RPI
         LA    R1,7(,R6)          SET UP BXLE INCREMENT.        JSF@RPI
         LA    R2,1               ..AND INCREMENT.              JSF@RPI
         LA    R3,15(,R6)         ..AND COMPARAND.              JSF@RPI
         LA    R6,8(,R6)          BUMP PARAMETER POINTER.       JSF@RPI
         B     GETSCAN            GO PROCESS NUMERICS.          JSF@RPI
OSGETNUM DS    0H                                               JSF@RPI
         LR    R1,R6              R1 PTR TO START SCAN
         LA    R2,1               R2 SCAN INCREMENT
*
         BXH   R1,R2,NEXTPARM     BUMP TO NEXT CHARACTER
         CLI   0(R1),C'='         IS IT EQUALL SIGN
         BNE   *-8                NO, GO CHECK NEXT
*
GETSCAN  DS    0H                                               JSF@RPI
         LA    R7,1(,R1)          R7 ADDR OF FIRST NUMERIC
*
         BXH   R1,R2,*+12         START NUMERIC SCAN
         TM    0(R1),NUMR         IS IT A NUMERIC ?
         BO    *-8                YES, GO CHECK FOR MORE
*
         SR    R1,R7              R1 COUNT OF NUMERICS
         BNP   NEXTPARM           MUST BE AT LEAST ONE
*
         XC    DUBL,DUBL          CLEAR DOUBLE WORD WORK AREA
         LA    R8,DUBL+8          R8 PTR TO LOCATE
         SR    R8,R1                   NUMERICS WITHIN DOUBLE WORD
         BCTR  R1,0               R1 ONE LESS FOR EXECUTE
         EX    R1,MOVNUM          MOVE NUMERICS TO DOUBLE WORD
         PACK  DUBL,DUBL          CONVERT TO PACKED DECIMAL
         BR    RE                 RETURN
*
*
MOVNUM   MVC   0(0,R8),0(R7)
*
*
GETCARDS LA    RB,SYSINDCB        R11 BASE FOR 'SYSIN' DD
         MVI   OPENL,INFL+LASTFL        FLAG AS INPUT & LAST
         OPEN  ((RB)),MF=(E,OPENL)
         L     R0,GETJFCB         GET JFCB
         GETMAIN R,LV=(0)              READ AREA
         ST    R1,EXITA           MAKE THIS EXIT ADDR
         MVI   EXITA,JFCBR+LASTFL  FLAG AS READ JFCB & LAST
         MVC   0(14,R1),NOFILE    DEFAULT DSNAME
         MVC   14(30,R1),13(R1)        NOT DEFINED
         XC    FRSTNAME,FRSTNAME  INIT NAME CHAIN PTR
         MVI   FRSTNAME,FF             ZERO AND FLAG IT
         LA    R9,FRSTNAME-8      R9 PTR TO TOP OF CHAIN
         USING IHADCB,RB
         TM    DCBOFLGS,OPENOK    SYSIN OPEN O.K. ?
         BNO   NOCARDS            NO, SKIP SYSIN PROCESSING
*
         LA    R1,EXITA           MOVE JFCB
         O     R1,DCBRECFM             EXIT ADDR
         ST    R1,DCBRECFM             TO DCB
         MVI   READJFCB,LASTFL    FLAG AS LAST
         RDJFCB ((RB)),MF=(E,READJFCB)
*
*
NEXTCARD GET   (RB)
         LR    RA,R1              R10 SAVE ADDR OF CARD START
         BCTR  R1,0               R1 PTR TO FIRST CHAR LESS ONE
         LH    R5,DCBLRECL        R5 LENGTH OF
         BCTR  R5,0                    RECORD LESS ONE
*
*
NEXTNAME LA    R1,1(,R1)          INCR PTR TO NEXT CHAR
         LR    R3,R1              SAVE FOR MOVE OPERATION
         LA    R4,0(5,RA)         R4 LENGTH OF FIELD
         SR    R4,R3                   FOR EXECUTE
         BM    NEXTCARD           GO FETCH ANOTHER CARD
*
         EX    R4,XLATE           FIND NEXT NON-VALID CHAR
         LR    R4,R1              R1-R4 ADDR OF CHAR FOUND
         SR    R4,R3              R4 COUNT OF CHARS FOR MOVE
         BNP   NEXTNAME           GO LOOK AGAIN
*
         BCTR  R4,0               COUNT ONE LESS FOR EXECUTE
         LA    R9,8(,R9)          INCR R9 PTR TO NEXT NAME SPACE
         TM    0(R9),FF           IS THIS AN ADDR PTR ?
         BNO   *+8                NO, IT MUST BE A NAME
*
         BAL   RE,NAMEBLOK        GO FETCH A NEW BLOCK
*
         MVI   0(R9),BLANK        BLANK OUT NEW
         MVC   1(7,R9),0(R9)           NAME SPACE
         EX    R4,MOVIT           MOVE NAME FROM CARD TO TABLE
         AP    INPCOUNT,PONE      INCR INPUT COUNTER
         B     NEXTNAME           GO LOOK FOR MORE
*
*
MOVIT    MVC   0(0,R9),0(R3)
*
*
XLATE    TRT   0(0,R3),XLTAB
*
*
         PRINT DATA
XLTAB    DS    0CL256
         DC    16X'20'
         DC    16X'20'
         DC    16X'20'
         DC    16X'20'
         DC    16X'20'
         DC    11X'20',X'00',4X'20'    $
         DC    16X'20'
         DC    11X'20',2X'00',3X'20'   #,@
         DC    X'20',9X'10',6X'20'     A - I (LC)
         DC    X'20',9X'10',6X'20'     J - R (LC)
         DC    2X'20',8X'10',6X'20'    S - Z (LC)
         DC    16X'20'
         DC    X'20',9X'00',6X'20'     A - I
         DC    X'20',9X'00',6X'20'     J - R
         DC    2X'20',8X'00',6X'20'    S - Z
         DC    10X'00',6X'20'          0 - 9
         PRINT NODATA
*
*
ENDCARDS CLOSE ((RB)),MF=(E,OPENL)
*
*
NOCARDS  ST    R9,LASTNAME        SAVE ADDR OF LAST NAME
         CP    INPCOUNT,PONE      WERE ANY NAMES INPUT ?
         BL    OPNFILES           NO, SKIP SORT
*
         OI    INDON,CARDATA      INDIC INPUT NAMES PRESENT
*
******** SHELL SORT OF TABULATED NAMES ***************************** *
*                                                                  * *
         ZAP   DUBL,INPCOUNT ×    CONVERT COUNT                    * *
         CVB   R7,DUBL ×               TO BINARY IN R7             * *
         SLR   R2,R2              CLEAR R2                         * BA
*                                                                  * *
         LA    R2,1(R2,R2)        R2 * 2 + 1                       * BA
         CR    R2,R7 ×            R2 GT ENTRY COUNT ?              * *
         BL    *-6                NO, BUMP AGAIN                   * BA
*                                                                  * *
*                                                                  * *
HALVIT   SRL   R2,1               R2 / 2                           * BA
         LTR   R2,R2 ×            ARE THERE MORE ?                 * *
         BNP   OPNFILES ×         NO, SORT COMPLETE                * *
*                                                                  * *
         LR    R5,R7 ×            R5 UPPER                         * *
         SR    R5,R2 ×                 LOOP LIMIT                  * *
         LA    R4,1 ×             R4 LOOP INCR                     * *
         SLR   R3,R3              R3 LOWER LOOP LIMIT              * BA
*                                                                  * *
*                                                                  * *
OUTRLOOP BXH   R3,R4,HALVIT ×                                      * *
         LR    R6,R3 ×            R6 INDEX TO FIRST COMP           * *
*                                                                  * *
*                                                                  * *
INNRLOOP LR    R1,R6 ×            R1 INDEX TO FIRST COMP           * *
         BAL   RE,LOCATOR ×       CONVERT INDEX TO                 * *
         LR    R9,R1 ×                 COMP ADDR IN R9             * *
         LA    R1,0(R2,R6)        R1 INDEX TO SECOND COMPARE       * BA
         BAL   RE,LOCATOR ×       CONVERT INDEX TO                 * *
         LR    RA,R1 ×                 COMP ADDR IN R10            * *
         CLC   0(8,R9),0(RA) ×    ARE THEY IN SEQUENCE ?           * *
         BNH   OUTRLOOP ×         YES, GO CHECK NEXT               * *
*                                                                  * *
         XC    0(8,R9),0(RA) ×    NOT IN                           * *
         XC    0(8,RA),0(R9) ×         SEQUENCE                    * *
         XC    0(8,R9),0(RA) ×         SWAP                        * *
         SR    R6,R2 ×            GO DOWN                          * *
         BP    INNRLOOP ×              INNER LOOP ?                * *
*                                                                  * *
         B     OUTRLOOP ×         NO, GO CHECK MORE                * *
*                                                                  * *
*                                                                  * *
LOCATOR  STM   R2,R3,SAVR ×       SAVE R2, R3                      * *
         BCTR  R1,0 ×             DECR INDEX FOR OFFSET            * *
         SLR   R2,R2              CLEAR R2                         * BA
         LTR   R3,R1 ×            REG PAIR 2-3 HAS INDEX-1         * *
         BNP   *+10 ×             MUST BE FIRST                    * *
*                                                                  * *
         LA    R1,15 ×            R2 NUMBER OF BLOCK OFFSETS       * *
         DR    R2,R1 ×                 R3 OFFSET INTO END BLOCK    * *
         L     R1,FRSTNAME ×      R1 ADDR OF FIRST NAME            * *
         LA    R1,0(,R1) ×        CLEAR FLAG BYTE                  * *
         LTR   R3,R3 ×            ANY BLOCK OFFSET ?               * *
         BNP   *+16 ×             NO, GO CALC ADDR OFFSET          * *
*                                                                  * *
         L     R1,120(,R1) ×      FETCH NEXT BLOCK PTR             * *
         LA    R1,0(,R1) ×        CLEAR FLAG BYTE                  * *
         BCT   R3,*-8 ×           ANY MORE BLOCK OFFSETS ?         * *
*                                                                  * *
         ALR   R2,R2              OFFSET * 2                       * BA
         ALR   R2,R2              OFFSET * 4                       * BA
         ALR   R2,R2              OFFSET * 8 FOR BYTES             * BA
         ALR   R1,R2              R1 ADDR OF REQD NAME             * BA
         LM    R2,R3,SAVR ×       RESTORE R2, R3                   * *
         BR    RE ×               RETURN                           * *
*                                                                  * *
******************************************************************** *
*
*
OPNFILES LA    RB,INPDCB          R11 BASE FOR 'INTAPE' DD
         MVI   OPENL,INFL         FLAG AS INPUT
         MVC   SHEXADDR,ADEXPAND  HIGH BYTE=0,AL3(ROUTINE)           BA
         LA    R5,DCBPAR          MOVE DCB EXIT ADDR
         ST    R5,EXITB                TO EXIT LIST
         MVI   EXITB,DCBXT+LASTFL FLAG AS DCB EXIT & LAST
         NI    EXITA,FF-LASTFL    CLEAR JFCB EXIT LAST FLAG
         LA    R5,EXITA           MOVE EXIT
         O     R5,DCBRECFM             LIST ADDR
         ST    R5,DCBRECFM             TO DCB
         LA    RA,OUTPDCB         RA BASE FOR 'OUTAPE' DD
         MVI   OPENL+4,OUTFL      FLAG AS OUTPUT
         DROP  RB
         USING IHADCB,RA
         MVC   SHEXADDR,ADSHRINK  HIGH BYTE=0,AL3(ROUTINE)           BA
         LA    R5,EXITA           MOVE EXIT
         O     R5,DCBRECFM             LIST ADDR
         ST    R5,DCBRECFM             TO DCB
*
         LA    R9,PRNTDCB         R9 BASE FOR 'SYSPRINT' DD
         MVI   OPENL+8,OUTFL      FLAG AS OUTPUT
*
         LA    R8,PNCHDCB         R8 BASE FOR 'SYSPUNCH' DD
         MVI   OPENL+12,OUTFL+LASTFL     FLAG AS OUTPUT & LAST
*
         OPEN  ((RB),,(RA),,(R9),,(R8)),MF=(E,OPENL)
         DROP  RA
         USING IHADCB,RB
*
         TM    DCBOFLGS,OPENOK    INTAPE OPEN OK ?
         BO    CHEKTAPE           YES, FORGET IT                     BA
*
         OI    OPENL,KILLFL       KILL OPEN FLAG
         OI    CMPLCD,CMPL16           COMPLETION CODE 0016
         DROP  RB
         USING IHADCB,RA
*
*
CHEKTAPE TM    DCBOFLGS,OPENOK    OUTAPE OPEN OK ?
         BO    CHEKPRNT           YES, FORGET IT                     BA
*
         OI    OPENL+4,KILLFL     KILL OPEN FLAG
         TM    INDIN,TAPECOPY     WAS TAPE REQUESTED ?
         BNO   CHEKPRNT           NO, FORGET IT
*
         NI    INDIN,FF-TAPECOPY  TURN TAPE OFF
         OI    CMPLCD,CMPL08           COMPLETION CODE 0008
         DROP  RA
         USING IHADCB,R9
*
*
CHEKPRNT TM    DCBOFLGS,OPENOK    SYSPRINT OPEN OK ?
         BO    *+16               YES, FORGET IT
*
         OI    OPENL+8,KILLFL     KILL OPEN FLAG
         OI    CMPLCD,CMPL16           COMPLETION CODE 0016
         NI    INDIN,FF-INDXLIST-LISTING  TURN OFF INDEX AND LIST
         DROP  R9
         USING IHADCB,R8
         TM    DCBOFLGS,OPENOK    SYSPUNCH OPEN OK ?
         BO    *+40               YES, FORGET IT
*
         OI    OPENL+12,KILLFL    KILL OPEN FLAG
         TM    INDIN,STDECK       WAS DECK REQUESTED ?
         BNO   *+12               NO, FORGET IT
*
         OI    CMPLCD,CMPL04           COMPLETION CODE 0004
         B     *+16               GO TURN DECK OFF
*
         TM    INDIN,SSDECK       WAS SOURCE DECK REQUESTED ?
         BNO   *+12               NO, FORGET IT
         OI    CMPLCD,CMPL12           COMPLETION CODE 0012
         NI    INDIN,FF-STDECK-SSDECK  TURN DECKS OFF
*
         TM    CMPLCD,CMPLHI      COMPL CODE 16 OR MORE ?
         BM    ENDIT              YES, GO TO END
*
         TM    INDIN,FF           ANY OUTPUT LEFT ?
         BZ    ENDIT              NO, GO TO END
*
         L     RF,ADDRLOD2        BRANCH TO
         BALR  RE,RF                   PROCESSING PHASE
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                                TERMINATION'
ENDIT    L     R5,EXITA           R5 PTR TO JFCB AREA
         LA    R5,0(,R5)          CLEAR FLAG BYTE
         TM    OPENL+8,KILLFL     SYSPRINT OPEN ?
         BO    OUTPOPE            NO, FORGET IT
*
         PUT   (R9)
         MVC   0(46,R1),MIMSG01   MOVE COMPLETION MSG TO PRINT
         MVC   46(75,R1),45(R1)   BLANK OUT REM OF LINE
         SR    R2,R2              COMPLETION CODE
         IC    R2,CMPLCD               RIGHT JUSTIFIED IN R2
         CVD   R2,DUBL            CONVERT TO DECIMAL
         MVC   46(6,R1),CPMASK    MOVE IN EDIT MASK
         ED    46(6,R1),DUBL+5    EDIT CODE INTO PRINT LINE
         PUT   (R9)
         MVC   0(40,R1),MIMSG02   MOVE SYSIN MSG TO PRINT
         MVC   40(81,R1),39(R1)   BLANK OUT REM OF LINE
         TM    INDON,INCLOPT      WAS INCLUDE SPECIFIED
         BO    *+10               YES, FORGET IT
*
         MVC   19(2,R1),EXCLUDE   MAKE IT 'EXCLUDED'
         MVC   1(10,R1),EDMASK    MOVE EDIT MASK TO LINE
         ED    1(10,R1),INPCOUNT  EDIT SYSIN COUNT INTO LINE
         MVC   40(44,R1),0(R5)    MOVE DSNAME TO PRINT LINE
         TM    OPENL,KILLFL       INTAPE OPEN ?
         BO    CLOSETWO           NO, FORGET IT
*
         PUT   (R9)
         MVC   0(38,R1),MIMSG03   MOVE IN 'MEMBERS & CARDS'
         MVC   38(17,R1),MIMSG04  MAKE IT 'READ FROM INTAPE'
         MVI   99(R1),BLANK       BLANK OUT
         MVC   100(21,R1),99(R1)       END OF LINE
         MVC   1(10,R1),EDMASK    MOVE IN EDIT MASK
         ED    1(10,R1),MEMCOUNT  EDIT INPUT MEMBER COUNT INTO LINE
         MVC   20(10,R1),EDMASK   MOVE IN EDIT MASK
         ED    20(10,R1),CARCOUNT EDIT INPUT CARD COUNT INTO LINE
         LR    R2,R1              SAVE PUT BUFFER PTR
         DROP  R8
         USING IHADCB,RB
         LA    R6,EXITA           MOVE JFCB
         O     R6,DCBRECFM             EXIT ADDR
         ST    R6,DCBRECFM             TO DCB
         OI    EXITA,LASTFL       FLAG IT AS LAST
         RDJFCB ((RB)),MF=(E,READJFCB)
         MVC   55(44,R2),0(R5)    MOVE DSNAME TO PRINT LINE
         MVI   CCTRL,CCSP2        SET CARR CNTRL DOUBLE SPACE
         TM    IDNTNO,IDHERE      WAS THERE AN IDENTITY FIELD ?
         BNO   TAPEOP             NO, FORGET IT
*
         PUT   (R9)
         MVC   0(43,R1),MIMSG10   MOVE IN IDENTITY MSG
         MVC   43(78,R1),42(R1)   BLANK OUT REM OF LINE
         TM    INDON,NOREJECT     WAS REJECT SPECIFIED ?
         BO    *+10               NO, FORGET IT
*
         MVC   29(3,R1),REJECT    MAKE IT 'REJECTED'
         MVC   42(52,R1),IDENT    MOVE IN IDENTITY FIELD
         LA    R3,127             R3 IDENT LENGTH MASK
         IC    R2,IDNTNO          R2   IDENT FIELD
         NR    R2,R3                   LENGTH (LESS ONE)
         LA    R2,43(2,R1)        R2 PTR TO 'QUOTE' LOCATION
         MVI   0(R2),X'7D'        MOVE IN ENDING QUOTE
*
*
TAPEOP   TM    INDOT,TAPECOPY     WAS THERE TAPE OUTPUT ?
         BNO   LISTOP             NO, FORGET IT
*
         PUT   (R9)
         MVC   0(38,R1),MIMSG03   MOVE IN 'MEMBERS & CARDS'
         MVC   0(1,R1),CCTRL      SET CARR CNTRL
         MVI   CCTRL,CCSP1        SINGLE SPACE FROM NOW ON
         MVC   38(17,R1),MIMSG05  MAKE IT 'COPIED TO OUTAPE'
         MVI   99(R1),BLANK       BLANK OUT
         MVC   100(21,R1),99(R1)       END OF LINE
         MVC   1(10,R1),EDMASK    MOVE IN EDIT MASK
         ED    1(10,R1),TAPCOUNT  EDIT TAPE MEMBER COUNT INTO LINE
         MVC   20(10,R1),EDMASK   MOVE IN EDIT MASK
         ED    20(10,R1),TPCCOUNT EDIT TAPE CARD COUNT INTO LINE
         LR    R2,R1              SAVE PUT BUFFER PTR
         DROP  RB
         USING IHADCB,RA
         LA    R6,EXITA           MOVE JFCB
         O     R6,DCBRECFM             EXIT ADDR
         ST    R6,DCBRECFM             TO DCB
         RDJFCB ((RA)),MF=(E,READJFCB)
         MVC   55(44,R2),0(R5)    MOVE DSNAME TO PRINT LINE
*
*
LISTOP   TM    INDOT,LISTING      WAS THERE LIST OUTPUT ?
         BNO   DECKOP             NO, FORGET IT
*
         PUT   (R9)
         MVC   0(38,R1),MIMSG03   MOVE IN 'MEMBERS & CARDS'
         MVC   0(1,R1),CCTRL      SET CARR CNTRL
         MVI   CCTRL,CCSP1        SINGLE SPACE FROM NOW ON
         MVC   31(4,R1),LINE      MAKE IT 'LINES'
         MVC   38(8,R1),MIMSG06   MAKE IT 'IN LIST'
         MVI   90(R1),BLANK       BLANK OUT
         MVC   91(30,R1),90(R1)        END OF LINE
         MVC   1(10,R1),EDMASK    MOVE IN EDIT MASK
         ED    1(10,R1),LISCOUNT  EDIT LIST MEMBER COUNT INTO LINE
         MVC   20(10,R1),EDMASK   MOVE IN EDIT MASK
         ED    20(10,R1),LSCCOUNT EDIT LIST CARD COUNT INTO LINE
         LR    R2,R1              SAVE PUT BUFFER PTR
         DROP  RA
         USING IHADCB,R9
         LA    R6,EXITA           MOVE JFCB
         O     R6,DCBRECFM             EXIT ADDR
         ST    R6,DCBRECFM             TO DCB
         RDJFCB ((R9)),MF=(E,READJFCB)
         MVC   46(44,R2),0(R5)    MOVE DSNAME TO PRINT LINE
         TM    INDOT,HAVEMAXL     MAX LINES REACHED ?
         BNO   DECKOP             NO, FORGET IT
*
         PUT   (R9)
         MVC   0(86,R1),MIMSG09   MOVE EXCESION MSG TO PRINT
         MVC   32(4,R1),LINE      MAKE IT READ 'MAXLINES'
         MVC   37(10,R1),EDMASK   MOVE EDIT MASK TO LINE
         ED    37(10,R1),LMXCOUNT EDIT MAX LINE COUNT INTO LINE
         MVC   86(35,R1),85(R1)   BLANK OUT REM OF LINE
*
*
DECKOP   TM    INDOT,STDECK+SSDECK     WAS A DECK PRODUCED ?
         BZ    CLOSEONE           NO, FORGET IT
*
         PUT   (R9)
         MVC   0(38,R1),MIMSG03   MOVE IN 'MEMBERS & CARDS'
         MVC   0(1,R1),CCTRL      SET CARR CNTRL
         MVI   CCTRL,CCSP1        SINGLE SPACE FROM NOW ON
         MVC   38(12,R1),MIMSG07  MAKE IT 'ON SYSPUNCH'
         MVI   94(R1),BLANK       BLANK OUT
         MVC   95(26,R1),94(R1)        END OF LINE
         MVC   1(10,R1),EDMASK    MOVE IN EDIT MASK
         ED    1(10,R1),DEKCOUNT  EDIT DECK MEMBER COUNT INTO LINE
         MVC   20(10,R1),EDMASK   MOVE IN EDIT MASK
         ED    20(10,R1),DKCCOUNT EDIT DECK CARD COUNT INTO LINE
         LR    R2,R1              SAVE PUT BUFFER PTR
         DROP  R9
         USING IHADCB,R8
         LA    R6,EXITA           MOVE JFCB
         O     R6,DCBRECFM             EXIT ADDR
         ST    R6,DCBRECFM             TO DCB
         RDJFCB ((R8)),MF=(E,READJFCB)
         MVC   50(44,R2),0(R5)    MOVE DSNAME TO PRINT LINE
         TM    INDOT,SSDECK       WAS IT SOURCE ?
         BNO   EXCARDS            NO, FORGET IT
*
         PUT   (R9)
         MVC   0(40,R1),MIMSG08   MOVE SOURCE MSG TO PRINT
         MVC   40(81,R1),39(R1)   BLANK OUT REM OF LINE
*
*
EXCARDS  TM    INDOT,HAVEMAXC     MAX CARDS REACHED ?
         BNO   CLOSEONE           NO, FORGET IT
*
         PUT   (R9)
         MVC   0(86,R1),MIMSG09   MOVE EXCESION MSG TO PRINT
         MVC   37(10,R1),EDMASK   MOVE EDIT MASK TO LINE
         ED    37(10,R1),CMXCOUNT EDIT MAX CARD COUNT INTO LINE
         MVC   86(35,R1),85(R1)   BLANK OUT REM OF LINE
*
*
CLOSEONE MVI   OPENL,NUL          CLEAR OPEN FLAG
         TM    OPENL+4,KILLFL     OUTAPE OPEN ?
         BO    CLOSINTP           NO, FORGET IT
*
         OI    OPENL+4,INFL+LASTFL      FLAG AS INPUT & LAST
         CLOSE ((RB),,(RA)),MF=(E,OPENL)
         OI    OPENL+4,KILLFL     KILL OPEN FLAG
         B     CLOSETWO           GO CLOSE INPUT
*
*
CLOSINTP OI    OPENL,INFL+LASTFL        FLAG AS INPUT & LAST
         CLOSE ((RB)),MF=(E,OPENL)
*
*
CLOSETWO OI    OPENL+8,INFL+LASTFL      FLAG AS INPUT & LAST
         CLOSE ((R9)),MF=(E,OPENL+8)
*
*
OUTPOPE  TM    OPENL+4,KILLFL     OUTAPE OPEN ?
         BO    PNCHOPE            NO, FORGET IT
*
         OI    OPENL+4,INFL+LASTFL      FLAG AS INPUT & LAST
         CLOSE ((RA)),MF=(E,OPENL+4)
*
*
PNCHOPE  TM    OPENL+12,KILLFL    SYSPUNCH OPEN ?
         BO    ENDALL             NO, FORGET IT
*
         OI    OPENL+12,INFL+LASTFL     FLAG AS INPUT & LAST
         CLOSE ((R8)),MF=(E,OPENL+12)
*
*
ENDALL   EXIT  RC=CMPLCD
*
*
         DROP  R8
         USING IHADCB,R1
         USING DCBPAR,RF
DCBPAR   CLI   DCBRECFM,NUL       ANY RECORD FORMAT SPECIFIED ?
         BH    *+12               YES, GO CHECK IT OUT
*
         OI    DCBRECFM,VBLOCK    DEFAULT VARIABLE, BLOCKED
         B     *+12               GO CHECK RECORD LENGTH
*
         CLI   DCBRECFM,VBLOCK    IS IT VARIABLE, BLOCKED ?
         BNE   *+46               NO, GO CHECK IF FIXED
*
         MVI   SHEXADDR,COMPRESS  INDIC COMPRESSION WITH THIS DCB
         LH    R2,DCBLRECL        ANY RECORD
         LTR   R2,R2                   LENGTH SPECIFIED ?
         BNZ   *+12               YES, GO CHECK BLOCK SIZE
*
         LA    R2,84              DEFAULT VARIABLE
         STH   R2,DCBLRECL             RECORD LENGTH 84 BYTES
         CH    R2,DCBBLKSI        BLOCK SIZE SPECIFIED GT LRECL ?
         BCR   4,RE               YES, USE IT & RETURN
*
         SLL   R2,6               SET BLOCK SIZE
         LA    R2,4(,R2)               64 * LRECL, PLUS
         STH   R2,DCBBLKSI             FOUR BYTE BDW
         BR    RE                 RETURN
*
         CLI   DCBRECFM,FBLOCK    IS IT FIXED, BLOCKED ?
         BCR   7,RE               NO, FORGET IT
*
         LH    R2,DCBLRECL        ANY RECORD
         LTR   R2,R2                   LENGTH SPECIFIED ?
         BNZ   *+12               YES, GO CHECK BLOCK SIZE
*
         LA    R2,80              DEFAULT FIXED RECORD
         STH   R2,DCBLRECL             LENGTH 80 BYTES
         CH    R2,DCBBLKSI        BLOCK SIZE SPECIFIED GE LRECL ?
         BCR   13,RE              YES, USE IT & RETURN
*
         SLL   R2,6               SET BLOCK
         STH   R2,DCBBLKSI             SIZE 64 * LRECL
         BR    RE                 RETURN
         DROP  R1
         DROP  RF
*
*
NAMEBLOK ST    R1,SAVR            SAVE R1 ACCROSS GETMAIN
         L     R0,GETNMBLK        GET A NEW
         GETMAIN R,LV=(0)              NAME BLOCK
         ST    R1,0(,R9)          CHAIN OLD BLOCK TO
         MVI   0(R9),FF                NEW AND FLAG IT
         LR    R9,R1              R9 PTR TO NEW BLOCK
         MVI   0(R9),NUL          CLEAR THE AREA
         MVC   1(127,R9),0(R9)         TO ZERO'S
         MVI   120(R9),FF         FLAG LAST FOR CHAIN ADDR
         L     R1,SAVR            RESTORE R1
         BR    RE                 RETURN
*
*
         PATCH 64
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                                  CONSTANTS'
ADDRLOD2 DC    A(MIM#3LD2)
ADEXPAND DC    A(EXPAND)
ADSHRINK DC    A(SHRINK)
GETSAVE  DC    A(SAVEND-SAVEA)
GETJFCB  DC    F'176'
GETNMBLK DC    F'128'
LINEDFLT DC    F'60'
CMXDFLT  DC    PL4'2000'
LMXDFLT  DC    PL4'20000'
MMXDFLT  DC    PL4'999999'
ID       DC    CL2'ID'
LIST     DC    CL4'LIST'
DECK     DC    CL4'DECK'
SDECK    DC    CL4'SDEC'
TAPE     DC    CL4'TAPE'
MAXCARDS DC    CL4'MAXC'
MAXLINES DC    CL4'MAXL'
MAXMEM   DC    CL4'MAXM'
KEYS     DC    CL4'KEYS'
INCLUDE  DC    CL4'INCL'
EXCLUDE  DC    CL4'EXCL'
INDEX    DC    CL3'IND'
SELECT   DC    CL3'SEL'
REJECT   DC    CL3'REJ'
LINE     DC    CL4'LINE'
NOFILE   DC    CL14'- NOT DEFINED'
MIMSG01  DC    CL46'1MIM#3 PROCESSING COMPLETED - COMPLETION CODE'
MIMSG02  DC    CL12'0',CL28'NAMES ''INCLUDED'' FROM SYSIN'
MIMSG03  DC    CL12' ',CL7'MEMBERS',CL12',',CL7'CARDS,'
MIMSG04  DC    CL17'READ FROM INTAPE'
MIMSG05  DC    CL17'COPIED TO OUTAPE'
MIMSG06  DC    CL8'IN LIST'
MIMSG07  DC    CL12'ON SYSPUNCH'
MIMSG08  DC    CL15' ',CL25'*** SOURCE DECK PRODUCED'
MIMSG09  DC    CL15' ',CL33'*** WARNING - MAXCARDS',CL38'REACHED, LAST X
               MEMBER MAY BE TRUNCATED'
MIMSG10  DC    CL15' ',CL28'*** IDENTITY ''SELECTED'' - '''
EDMASK   DC    XL10'40206B2020206B202120'
CPMASK   DC    XL6'402120202020'
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                        DATA CONTROL BLOCKS'
         PRINT NOGEN
SYSINDCB DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),EODAD=ENDCARDS
*
INPDCB   DCB   DDNAME=INTAPE,DSORG=PS,MACRF=(GL),EODAD=ENDTAPE
         ORG   INPDCB+96                                             BA
         DC    CL92'EXPAND WORK AREA'                                BA
*
OUTPDCB  DCB   DDNAME=OUTAPE,DSORG=PS,MACRF=(PL)
         ORG   OUTPDCB+96                                            BA
         DC    CL92'SHRINK WORK AREA'                                BA
*
PRNTDCB  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PL),                    X
               RECFM=FBA,BLKSIZE=2420,LRECL=121
*
PNCHDCB  DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(PL),                    X
               RECFM=FB,BLKSIZE=800,LRECL=80
         PRINT GEN
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                     DATA EXPANSION ROUTINE'
EXPAND   CSECT
         HEDIT 'EXPAND'
         USING *,RF                                                  BA
         STM   R2,R7,12(SAVE)     SAVE REGISTERS                     BA
         LA    R4,4               R4 CONSTANT FOUR
         LH    R5,0(,R1)          LENGTH OF FIELD
         SR    R5,R4              LESS FOUR BYTE DESCRIPTOR
         BNP   EXENDUP            NOTHING THERE, FORGET IT
         SPACE 1
         LA    R2,EXWORK-16       R2 PTR TO OUTPUT WORK AREA         BA
         LA    R3,4(,R1)          R3 PTR TO START LOOP               BA
         LA    R5,3(R5,R1)        R5 PTR TO STOP LOOP                BA
         MVI   1(R5),0            IN CASE LAST BYTE=X'00'            BA
         LA    R4,3               START LOOP                         BA
EXPLOOP  DC    0H'0'                                                 BA
         SLR   R6,R6              ZERO INITIALLY                     BA
         IC    R6,2(,R3)          INSERT RIGHT BYTE                  BA
         SRDL  R6,8               SHIFT INTO R7                      BA
         IC    R6,1(,R3)          INSERT MIDDLE BYTE                 BA
         SRDL  R6,8               SHIFT INTO R7                      BA
         IC    R6,0(,R3)          INSERT LEFT-MOST BYTE              BA
         SRDL  R6,2               SHIFT TWO BITS, LEAVE 6 BITS IN R6 BA
         SPACE 1
         ALR   R6,R6              EXPAND                             BA
         ALR   R6,R6                  COMPOSITE                      BA
         SLDL  R6,6                    CHARACTER TO 8 BITS           BA
         ALR   R6,R6              EXPAND                             BA
         ALR   R6,R6                  COMPOSITE                      BA
         SLDL  R6,6                    CHARACTER TO 8 BITS           BA
         ALR   R6,R6              EXPAND                             BA
         ALR   R6,R6                  COMPOSITE                      BA
         SLDL  R6,6                    CHARACTER TO 8 BITS           BA
         LA    R2,4(,R2)          A(NEXT OUTPUT AREA WORD)           BA
         ST    R6,12(,R2)         STORE EXPANDED WORD                BA
         BXLE  R3,R4,EXPLOOP      LOOP FOR MORE                      BA
         MVI   16(R2),X'3F'       ENSURE STOP OF FOLLOWING LOOP      BA
         LR    R5,R2              WANT RESULT IN R5 FOR LATER        BA
         CLI   1(R1),19           MORE THAN MINIMUM LENGTH/Q         BA
         BNH   *+8                NO, LESS, DO LONG SEARCH           BA
         LA    R5,7(,R5)          YES, DO SHORTER SEARCH             BA
         LA    R4,1               R4 CONSTANT = 1                    BA
         ALR   R5,R4              UP TO NEXT CHARACTER               BA
         CLI   1(R5),X'3F'        NEXT CHARACTER TERMINATOR/Q        BA
         BNE   *-6                NO, TRY NEXT                       BA
         LA    R3,EXWORK-1        R3 PTR TO INIT LOOP                BA
         LA    R2,4(,R1)          R2 PTR TO EXPANDED FIELD
         SPACE 2
MULTLOOP LA    R6,1(,R3)          R6 BEGINNING OF SOURCE FIELD
         ALR   R3,R4              UP TO NEXT CHARACTER               BA
         TM    0(R3),X'2F'        REPETITION OR TERMINATION FLAG/Q   BA
         BNO   *-6                NO, TRY AGAIN                      BA
         SPACE 2
         SR    R3,R6              ANY MOVE REQUIRED/Q                BA
         BNP   CKFLDEND           BRANCH IF NOT                      BA
         SPACE 1
         SLR   R3,R4              CONVERT TRUE LENGTH INTO MACHINE   BA
         STC   R3,EXMOVIT+1       LENGTH OF MOVE                     BA
*        BR    0                  SERIALIZE CPU                      BA
         ALR   R3,R4              RESTORE R3                         BA
EXMOVIT  MVC   0(0,R2),0(R6)      MOVE REQUIRED CHARACTERS           BA
         ALR   R2,R3              BUMP RECEIVING FIELD PTR           BA
CKFLDEND ALR   R3,R6              RESTORE MULTI POINTER              BA
         CR    R3,R5              BEYOND FIELD END ?
         BNL   TRANSIT            YES, GO TRANSLATE
         SPACE 1
         SLR   R6,R6              R6 MULTI                           BA
         IC    R6,1(,R3)               COUNT (MODULO 256)            BA
         SLR   R6,R4              ONE LESS FOR EXEC                  BA
         SLR   R6,R4              ONE LESS FOR OFFSET                BA
         STC   R6,FILIT+1         LENGTH OF REQ'D MOVE               BA
*        BR    0                  SERIALIZE CPU                      BA
         MVC   0(1,R2),2(R3)      MOVE IN FILL CHAR
         LA    R3,2(,R3)          R3 PTR FOR NEXT POSSIBILITY        BA
FILIT    MVC   1(0,R2),0(R2)      FILL FOR REQ'D COUNT               BA
         LA    R2,2(R6,R2)        BUMP RECEIVING FIELD PTR
         CR    R3,R5              BEYOND FIELD END ?
         BL    MULTLOOP           NO, GO CHECK SOME MORE
         SPACE 2
TRANSIT  LA    R3,5(,R1)          R3 PTR TO SECOND CHAR              BA
         SLR   R2,R3              R2 LENGTH OF TRANSLATE             BA
         STC   R2,EXXLATE+1       LENGTH OF TRANSLATE                BA
*        BR    0                  SERIALIZE CPU                      BA
         LA    R2,5(,R2)          R2 TOTAL FIELD LENGTH
         STH   R2,0(,R1)          SAVE BYTE COUNT
EXXLATE  TR    4(0,R1),EXXLTAB    TRANSLATE SPECIAL TO EBCDIC        BA
         SPACE 2
EXENDUP  LM    R2,R7,12(SAVE)     RESTORE REGISTERS                  BA
         BR    RE                 RETURN
         DROP  RF                                                    BA
         SPACE 2
         PATCH 32
         SPACE 3
EXWORK   DS    22F                                                   BA
         PRINT DATA
EXXLTAB  DS    0CL48
         DC    X'00C1C2C3C4C5C6C7C8C9404B4C4D4E4F'
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C7F6EFF'
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7EFF'
         PRINT NODATA
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                   DATA COMPRESSION ROUTINE'
SHRINK   CSECT
         HEDIT 'SHRINK'
         USING *,RF                                                  BA
         STM   R2,R7,12(SAVE)     SAVE REGISTERS                     BA
         LH    R5,0(,R1)          R5 LENGTH
         LA    R4,5                    FOR EXEC
         SR    R5,R4              IS ACTION POSSIBLE ?
         BM    SHENDUP            NO, FORGET IT
         STC   R5,SHXLTST+1       STORE LENGTH OF TEST               BA
         SPACE 1
         LA    R3,3(,R1)          R3 ADDR OF FIELD (-1)
         STC   R5,SHXLATE+1       STORE LENGTH OF TRANSLATE          BA
*        BR    0                  SERIALIZE CPU                      BA
         LR    R6,R1              SAVE R1 ACCROSS TRANSLATE & TEST
SHXLTST  TRT   1(0,R3),SHXLTTB    TEST FOR NON-COMPRESSIBILITY       BA
         BZ    *+10               ALL COMPRESSIBLE
         SPACE
         LR    R1,R6              RESTORE R1
         B     SHENDUP            GET OUT
         SPACE
SHXLATE  TR    1(0,R3),SHXLTAB    TRANSLATE EBCDIC TO SPECIAL        BA
         LA    R5,1(R5,R3)        R5 ADDR TO TERM BXH LOOP
         LA    R4,1               R4 INCREM FOR BXH
         LA    R6,3               R6 CONSTANT = 3                    BA
         LA    R7,62              R7 CONSTANT = 62                   BA
PRECHECK IC    R2,0(,R5)          INSERT LAST VALID CHARACTER        BA
         SLR   R2,R4              ENSURE LAST AND NEXT NOT EQUAL     BA
         STC   R2,1(,R5)          STORE PAST END TO STOP LOOP        BA
         SPACE 2
CHEKNEXT BXH   R3,R4,ADJUST       LOOP TO CHECK ENTIRE FIELD
         CLC   1(3,R3),0(R3)      ARE NEXT THREE THE SAME AS THIS/Q  BA
         BNE   CHEKNEXT           NO, TRY NEXT COLUMN                BA
         SPACE 2
         IC    R2,0(,R3)          INSERT REPETITIVE CHARACTER        BA
         STC   R2,CLIREPET+1      STORE TEST CHARACTER               BA
*        BR    0                  SERIALIZE CPU                      BA
         SLR   R2,R4              ENSURE DIFERENT CHARACTER          BA
         STC   R2,1(,R5)          ENSURE LOOP STOPS                  BA
         LA    R2,3(,R3)          PLACE TO KEEP LOOKING              BA
         ALR   R2,R4              NEXT CHARACTER SPOT                BA
CLIREPET CLI   0(R2),0            IS IT SAME CHARACTER               BA
         BE    *-6                YES, GO LOOK FOR MORE              BA
         MVI   0(R3),X'2F'        FLAG AS REPETITIOUS                BA
         SLR   R2,R3              R2 = LENGTH OF REPETITIVE SUB-FIELDBA
         CR    R2,R7              IS IT GREATER THAN 62              BA
         BNH   *+6                NO, IT'S OK
         SPACE 1
         LR    R2,R7              USE MAX OF 62                      BA
         STC   R2,1(,R3)          SAVE COUNT
         ALR   R2,R3              R2 PTR TO SHIFT BALANCE OF FIELD   BA
         ALR   R3,R6              R3 ADDR OF NEW FIELD               BA
         SR    R5,R2              R5 LENGTH OF MOVE & CHECK END      BA
         BM    ADJUST             BEYOND FIELD END, FORGET IT
         SPACE 1
         STC   R5,SHIFT+1         STORE LENGTH OF SHIFT              BA
*        BR    0                  SERIALIZE CPU                      BA
         ALR   R5,R3              R5 NEW FIELD END                   BA
SHIFT    MVC   0(0,R3),0(R2)      MOVE IT OVER                       BA
         BCT   R3,PRECHECK        GO ENSURE NEW END SET              BA
ADJUST   BCTR  R3,0               R3 PTR TO ACTIVE FIELD END
         LA    R5,4(,R3)          LENGTH OF FIELD                    BA
         SRL   R5,2                    TO NEAREST
         ALR   R5,R5                   FOUR                          BA
         ALR   R5,R5                     CONT.                       BA
         SLR   R5,R4              R5 PTR TO                          BA
         LA    R7,23(,R1)         CHECK FOR                          BA
         CR    R7,R5                   MINIMUM LENGTH                BA
         BNH   *+6                IT 'S OK
         SPACE 1
         LR    R5,R7              USE MINIMUM LENGTH                 BA
         SPACE 1
         BXH   R3,R4,*+20         TERMINAL
         MVI   0(R3),X'3F'             FLAG BYTE
         SPACE 1
         BXH   R3,R4,*+12         LOOP TO
         MVI   0(R3),NUL               FILL FIELD
         BXLE  R3,R4,*-4               WITH NULL CHARACTERS          BA
         SPACE 1
         LA    R2,4(,R1)          R2 PTR TO FIRST COMPOSITE
         LR    R3,R2              R3 PTR TO FIRST INPUT WORD         BA
         SPACE 2
         LA    R4,4               PRIME FOR BXLE INSTRUCTION IN LOOP BA
COMPLOOP DC    0H'0'                                                 BA
         L     R7,0(,R3)          FETCH NEXT FOUR CHAR'S
         ALR   R7,R7              STRIP OFF 0 BIT                    BA
         ALR   R7,R7                     AND 1 BIT                   BA
         SLDL  R6,6                       SAVE BITS 2 THROUGH 7      BA
         ALR   R7,R7              STRIP OFF 0 BIT                    BA
         ALR   R7,R7                     AND 1 BIT                   BA
         SLDL  R6,6                       SAVE BITS 2 THROUGH 7      BA
         ALR   R7,R7              STRIP OFF 0 BIT                    BA
         ALR   R7,R7                     AND 1 BIT                   BA
         SLDL  R6,6                       SAVE BITS 2 THROUGH 7      BA
         ALR   R7,R7              STRIP OFF 0 BIT                    BA
         ALR   R7,R7                     AND 1 BIT                   BA
         SLDL  R6,6                       SAVE BITS 2 THROUGH 7      BA
         SPACE 1
         STC   R6,2(,R2)          SAVE THIRD COMPOSITE BYTE
         SRL   R6,8               SAVE SECOND
         STC   R6,1(,R2)               COMPOSITE BYTE
         SRL   R6,8               SAVE FIRST
         STC   R6,0(,R2)               COMPOSITE BYTE
         LA    R2,3(,R2)          BUMP PTR TO NEXT COMPOSITE FIELD
         BXLE  R3,R4,COMPLOOP     LOOP TO COMPRESS 4 INTO 3          BA
         SPACE 2
COUNTEM  LA    R5,17(,R1)         R5 PTR TO MINIMUM FIELD END        BA
         SLR   R4,R4              R4 MINUS ONE                       BA
         BCTR  R4,0                    BXLE INCREM
         SPACE 1
         BXLE  R2,R4,*+12         BUMP DOWN TO NEXT
         CLI   0(R2),NUL          IS IT EXCESS BYTE ?
         BE    *-8                YES,STRIP IT
         SPACE 1
         LA    R2,1(,R2)          POINT TO FIELD END PLUS ONE
         SLR   R2,R1              SAVE LENGTH                        BA
         STH   R2,0(,R1)               OF FIELD
         SPACE 2
SHENDUP  LM    R2,R7,12(SAVE)     RESTORE REGISTERS                  BA
         DROP  RF                                                    BA
         BR    RE                 RETURN
         SPACE 2
         PATCH 32
         SPACE 3
         PRINT DATA
SHXLTTB  DS    0CL256
         DC    X'00010101010101010101010101010101'
         DC    X'01010101010101010101010101010101'
         DC    X'01010101010101010101010101010102'
         DC    X'01010101010101010101010101010102'
         DC    X'00010101010101010101010000000000'
         DC    X'00010101010101010101000000000000'
         DC    X'00000101010101010101000000010001'
         DC    X'01010101010101010101000000000000'
         DC    X'01010101010101010101010101010101'
         DC    X'01010101010101010101010101010101'
         DC    X'01010101010101010101010101010101'
         DC    X'01010101010101010101010101010101'
         DC    X'01000000000000000000010101010101'
         DC    X'01000000000000000000010101010101'
         DC    X'01010000000000000000010101010101'
         DC    X'00000000000000000000010101010101'
         SPACE
SHXLTAB  DS    0CL256
         DC    X'000F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0A0F0F0F0F0F0F0F0F0F0F0B0C0D0E0F'
         DC    X'100F0F0F0F0F0F0F0F0F1A1B1C1D1E1F'
         DC    X'20210F0F0F0F0F0F0F0F2A2B2C0F2E0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F3A3B3C3D3E2D'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
         DC    X'0F0102030405060708090F0F0F0F0F0F'
         DC    X'0F1112131415161718190F0F0F0F0F0F'
         DC    X'0F0F22232425262728290F0F0F0F0F0F'
         DC    X'303132333435363738390F0F0F0F0F0F'
         PRINT NODATA
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                         REQUEST PROCESSING'
MIM#3LD2 CSECT
         HEDIT 'MIM#3  LOAD2'
         USING SAVEA,SAVE
         B     14(RF)             ENTRY POINT
         DC    X'09'              I.D. CHAR COUNT
         DC    CL9'MIM#3LD2'      MODULE I.D.
         ST    BASE,SAV12         SAVE PRIMARY BASE
         ST    RE,RETURN          SAVE RETURN ADDR
         BALR  BASE,0
         USING *,BASE
*
         L     R0,GETAB           GET FIRST
         GETMAIN R,LV=(0)              TAPE TAB AREA
         ST    R1,FIRSTAB         SAVE FIRST TAB ADDR
         XC    60(4,R1),60(R1)    ZERO NEXT PTR
         OI    INDON,MORTAPE      INIT MORE TAPE YES
*
         TM    INDIN,INDXLIST     WAS AN INDEX REQUESTED ?
         BNO   NOINDEX            NO, SKIP INDEX PROCESSING
*
         NI    INDON,FF-ANYTAPE   INDIC NO NAME FOUND
         L     R6,LINECNT         FORCE NEW PAGE FIRST NAME
         LA    R1,ENDINDEX        SET BRANCH TO INDEX
         ST    R1,ENDBRNCH             END PROCESSING AT EOD
         L     R2,FIRSTAB         R2 PTR TO FIRST TABLE ENTRY
*
*
GETLOOP  GETX  RB,TYPE=@@
         LR    R4,R1              R4 RECORD PTR
         AP    CARCOUNT,PONE      INCREMENT CARD COUNT
         CLC   0(2,R4),ATDBL      IS IT '@@ ..' CARD ?
         BNE   GETLOOP            NO, GO GET ANOTHER
*
         CLC   4(5,R4),NAMEQ      IS IT '@@  NAME=...' ?
         BNE   EQALALIS           NO, GO CHECK FOR ALIAS
*
         TM    INDON,ANYTAPE      IS THIS FIRST ONE ?
         BO    LISTEM             NO, GO TO PRINT OUTPUT
*
         OI    INDON,ANYTAPE      INDIC HAVE FIRST NAME
         LA    R1,LISTEM+6        NOW EOD BRANCH
         ST    R1,ENDBRNCH             MUST CLEAR OUTPUT
*
*
ZAPCOUNT ZAP   CARCOUNT,PONE      RESET CARD COUNT TO ONE
         B     TABSAVE+2          GO SAVE MEMBER NAME
*
*
EQALALIS CLC   3(6,R4),ALISEQ     IS IT '@@ ALIAS=...' ?
         BNE   GETLOOP            NO, ASSUME IT'S DATA
*
         LA    R2,60(,R2)         R2 PTR TO ADDR AREA OF TAB
         L     R1,0(,R2)          R1 ADDR OF NEXT TAB
         LTR   R1,R1              IS THERE ANOTHER ?
         BNZ   TABSAVE            YES, GO TABULATE
*
         L     R0,GETAB           GET A NEW
         GETMAIN R,LV=(0)              TAB AREA
         ST    R1,0(,R2)          SAVE OLD NEXT PTR
         XC    60(4,R1),60(R1)    ZERO NEXT PTR
*
*
TABSAVE  LR    R2,R1              R2 PTR TO NEXT TAB
         MVC   0(8,R2),9(R4)      MOVE NAME TO TAB
         MVC   8(52,R2),19(R4)    MOVE IDENT FIELD TO TAB
         ST    R2,LASTAB          MAKE THIS LAST TAB
         B     GETLOOP            GO GET ANOTHER RECORD
*
*
LISTEM   SP    CARCOUNT,PONE      CARD COUNT IS ONE TOO HIGH
         AP    MEMCOUNT,PONE      INCREMENT MEMBER COUNT
         L     R2,FIRSTAB         R2 PTR TO NAME TAB
         MVI   CCTRL,CCSP1        DEFAULT CARR CNTRL SINGLE SPACE
         C     R6,LINECNT         IS A NEW PAGE INDICATED ?
         BL    NAMELINE           NO, GO PRINT NAME LINE
*
         PUT   (R9)
         AP    PGECOUNT,PONE      INCREMENT PAGE COUNT
         MVC   0(23,R1),INDXHEDR  MOVE PAGE HEADER TO LINE
         MVC   23(89,R1),22(R1)   BLANK OUT MIDDLE OF LINE
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE NUMBER INTO LINE
         MVC   112(4,R1),PAGE     MOVE 'PAGE' TO LINE
         MVI   CCTRL,CCSP2        SET DOUBLE SPACE FIRST ON PAGE
         LA    R6,2               RESET LINE COUNT TWO
*
*
NAMELINE PUT   (R9)
         MVC   0(1,R1),CCTRL      CARR CNTRL AS APPROPRIATE
         MVC   1(19,R1),NAMECARD  MOVE NAME CARD IMAGE TO LINE
         MVC   10(8,R1),0(R2)     MOVE NAME TO PRINT LINE
         MVC   20(52,R1),8(R2)    MOVE IDENT FIELD TO LINE
         MVC   72(7,R1),CDMASK    MOVE EDIT MASK TO LINE
         ED    72(7,R1),CARCOUNT+1 EDIT CARD COUNT INTO LINE
         MVC   79(7,R1),CARDS     MOVE 'CARDS' TO LINE
         MVC   86(35,R1),85(R1)   BLANK OUT REM OF LINE
         LA    R6,1(,R6)          INCREMENT LINE COUNT
*
*
TABNEXT  C     R2,LASTAB          WAS THIS LAST TAB ENTRY ?
         BE    MORECHEK           YES, GO CHECK MORE INPUT
*
         L     R2,60(,R2)         R2 PTR TO NEXT TAB
         MVI   CCTRL,CCSP1        DEFAULT CARR CNTRL SINGLE SPACE
         C     R6,LINECNT         IS A NEW PAGE INDICATED ?
         BL    ALISLINE           NO, GO PRINT ALIAS LINE
*
         PUT   (R9)
         AP    PGECOUNT,PONE      INCREMENT PAGE COUNT
         MVC   0(23,R1),INDXHEDR  MOVE PAGE HEADER TO LINE
         MVC   23(89,R1),22(R1)   BLANK OUT MIDDLE OF LINE
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE NUMBER INTO LINE
         MVC   112(4,R1),PAGE     MOVE 'PAGE' TO LINE
         MVI   CCTRL,CCSP2        SET DOUBLE SPACE FIRST ON PAGE
         LA    R6,2               RESET LINE COUNT TWO
*
*
ALISLINE PUT   (R9)
         MVC   0(1,R1),CCTRL      CARR CNTRL AS APPROPRIATE
         MVC   1(19,R1),ALISCARD  MOVE NAME CARD IMAGE TO LINE
         MVC   10(8,R1),0(R2)     MOVE NAME TO PRINT LINE
         MVC   20(52,R1),8(R2)    MOVE IDENT FIELD TO LINE
         MVI   72(R1),BLANK       BLANK OUT
         MVC   73(48,R1),72(R1)        REMAINDER OF LINE
         LA    R6,1(,R6)          INCREMENT LINE COUNT
         B     TABNEXT            GO CHECK FOR MORE
*
*
MORECHEK L     R2,FIRSTAB         R2 PTR TO FIRST NAME TAB
         AP    CARTOTAL,CARCOUNT  ADD THESE CARDS TO TOTAL
         TM    INDON,MORTAPE      IS THERE MORE INPUT ?
         BO    ZAPCOUNT           YES, LOOP BACK FOR MORE
*
*
ENDINDEX PUT   (R9)
         MVC   0(23,R1),INDXMSG   MOVE INDEX COMPLETE TO LINE
         LA    R6,2(,R6)          INSURE AT LEAST TWO MORE LINES
         MVC   23(10,R1),GPMASK   MOVE EDIT MASK TO LINE
         ED    23(10,R1),MEMCOUNT EDIT MEMBER COUNT INTO LINE
         MVC   33(9,R1),MEMBERS   MOVE 'MEMBERS' TO LINE
         MVC   42(10,R1),GPMASK   MOVE EDIT MASK TO LINE
         ED    42(10,R1),CARTOTAL EDIT CARD COUNT INTO LINE
         MVC   52(7,R1),CARDS     MOVE 'CARDS' TO LINE
         MVC   59(62,R1),58(R1)   BLANK OUT REM OF LINE
         C     R6,LINECNT         IS A NEW PAGE INDICATED ?
         BL    SETCOUNT           NO, FORGET IT
*
         MVI   0(R1),CCSKP        SET CARR CNTRL FOR SKIP
         AP    PGECOUNT,PONE      INCR PAGE COUNT
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE COUNT INTO LINE
         MVC   112(4,R1),PAGE     MOVE 'PAGE' TO LINE
         USING IHADCB,RB
*
*
SETCOUNT L     R6,SHEXADDR        SAVE POSSIBLE 'EXPAND' ADDR
         OI    OPENL,RERDFL+LASTFL  FLAG AS REREAD & LAST
         CLOSE ((RB)),MF=(E,OPENL)
         OPEN  ((RB)),MF=(E,OPENL)
         ST    R6,SHEXADDR        RESTORE POSSIBLE 'EXPAND' ADDR
         DROP  RB
         L     R6,LINECNT         FORCE SKIP TO NEW PAGE
*
         OI    INDOT,INDXLIST     INDICATE INDEX COMPLETED
         NI    INDIN,FF-INDXLIST  TURN OFF INDEX REQUIREMENT
         ZAP   MEMCOUNT,PZRO      INIT INPUT MEMBER COUNT ZERO
         ZAP   CARCOUNT,PZRO      INIT INPUT CARD COUNT ZERO
*
*
NOINDEX  LA    R1,ENDUP           R1 END ADDR
         TM    INDIN,FF           ANY MORE OUTPUT REQUIRED ?
         BCR   8,R1               NO, GO TO END
*
         ST    R1,ENDBRNCH        SET BRANCH TO END ON EOD
         OI    INDON,MORTAPE      INIT MORE TAPE SWITCH YES
         GETX  RB
         LR    R4,R1              R4 PTR TO INPUT RECORD
         AP    CARCOUNT,PONE      INCREMENT INPUT CARD COUNT
*
*
TAPENEXT NI    INDON,FF-ANYTAPE   INIT NO TAPE NAME FOUND
         LA    R1,ENDOK           BRANCH ADDR FOR EOD
         TM    INDON,MORTAPE      IS THERE MORE TAPE INPUT ?
         BCR   14,R1              NO, GO TO END
*
         ST    R1,ENDBRNCH        SAVE EOD BRANCH ADDR
         L     R2,FIRSTAB         R2 PTR TO TAPE NAME TAB
         B     FINDNAME           SKIP GET FIRST TIME THROUGH
*
*
CHEKNAME TM    INDON,ANYTAPE      DO WE HAVE TAPE NAME ?
         BO    IDNTCHEK           YES, GO CHECK IDENTITY OPTION
*
*
GETAPE@  GETX  RB,TYPE=@@
         B     INSTAT             GO TO INPUT COMMON
*
*
GETAPE   GETX  RB
*
*
INSTAT   LR    R4,R1              R4 PTR TO INPUT RECORD
         AP    CARCOUNT,PONE      INCR INPUT CARD COUNT
*
*
FINDNAME CLC   0(2,R4),ATDBL      IS IT '@@'
         BNE   CHEKNAME           NO, GO CHECK TAPE NAME
*
         CLC   4(5,R4),NAMEQ      IS IT 'NAME=' CARD ?
         BNE   *+18               NO, GO CHECK ALIAS
*
         OI    INDON,ANYTAPE      INDIC TAPE NAME FOUND
         AP    MEMCOUNT,PONE      INCREMENT INPUT MEMBER COUNT
         B     SAVETAB+2          GO SAVE TAPE NAME
*
         CLC   3(6,R4),ALISEQ     IS IT 'ALIAS=' CARD ?
         BNE   CHEKNAME           GO CHECK TAPE NAME
*
         TM    INDON,ANYTAPE      DO WE HAVE TAPE NAME ?
         BNO   GETAPE@            NO, SKIP THIS ALIAS CARD
*
         LA    R2,60(,R2)         R2 PTR TO ADDR AREA OF TAB
         L     R1,0(,R2)          R1 ADDR OF NEXT TAB
         LTR   R1,R1              IS THERE ANOTHER ?
         BNZ   SAVETAB            YES, GO TABULATE
*
         L     R0,GETAB           GET A NEW
         GETMAIN R,LV=(0)              TAB AREA
         ST    R1,0(,R2)          SAVE OLD NEXT PTR
         XC    60(4,R1),60(R1)    ZERO NEXT PTR
*
*
SAVETAB  LR    R2,R1              R2 PTR TO NEXT TAB
         MVC   0(8,R2),9(R4)      MOVE NAME TO TAB
         MVC   8(52,R2),19(R4)    MOVE IDENT FIELD TO TAB
         ST    R2,LASTAB          MAKE THIS LAST TAB
         B     GETAPE             GO GET ANOTHER INPUT RECORD
*
*
ENDTAPE  NI    INDON,FF-MORTAPE   INDIC NO MORE TAPE DATA
         L     R1,ENDBRNCH        R1 BRANCH ADDR
         BR    R1                 BRANCH AS APPROPRIATE
*
*
COMPARE  CLC   0(0,R3),0(R5)
*
*
IDNTCHEK TM    IDNTNO,IDHERE      WAS IDENTITY CHECK SPEC'D ?
         BNO   NAMECHEK           NO, GO CHECK SYSIN OPTION
*
         NI    INDON,FF-SELECTIT  DEFAULT SELECT NO
         LA    R2,127             R2 RETRIEVE
         IC    R1,IDNTNO               LENGTH OF
         NR    R2,R1                   IDENT FIELD
         LA    R1,52              R1 COUNT
         SLR   R1,R2                   OF COMPARISONS                BA
         LA    R3,IDENT           R3 PTR TO IDENT FIELD
         L     R5,FIRSTAB         R5 PTR TO TAPE
         LA    R5,7(,R5)               RECORD IDENT FIELD
*
         LA    R5,1(,R5)          BUMP TO NEXT CHAR
         EX    R2,COMPARE         DO FIELDS MATCH ?
         BNE   *+12               NO, GO CHECK NEXT
*
         OI    INDON,SELECTIT     INDIC MATCH FOUND
         B     *+8                GO CHECK IT OUT
*
         BCT   R1,*-20            LOOP TO CHECK NEXT
*
         TM    INDON,SELECTIT+NOREJECT IS SELECTION INDICATED ?
         BM    TAPENEXT           NO , SKIP THIS TAPE MEMBER
*
*
NAMECHEK TM    INDON,CARDATA      WAS THERE NAME INPUT ?
         BNO   OUTPUT             NO, GO CHECK NAMES
*
         L     R6,LASTNAME        HAS LAST NAME
         TM    0(R6),FF                BEEN DELETED ?
         BM    *+12               NO, CONTINUE
*
         TM    INDON,INCLOPT      IS IT INCLUDE OPTION ?
         BO    ENDOK              YES, STOP PROCESSING
*
         NI    INDON,FF-SELECTIT  INIT SELECT NO
         L     R2,FIRSTAB         R2 PTR TO FIRST TAPE NAME
*
*
NXTAPNAM LA    R6,FRSTNAME        R6 PTR TO TOP OF NAME CHAIN
*
*
TESTNEXT ADRTAB R6,*+18,,NP,CHEKAGIN
*
         CLC   0(8,R2),0(R6)      IS NAME SAME ?
         BE    *+20               YES, SET SWITCH
*
         C     R6,LASTNAME        WAS THIS LAST NAME ?
         BE    CHEKAGIN           YES, GO CHECK NEXT TAPE NAME
*
         LA    R6,8(,R6)          BUMP PTR TO NEXT NAME
         B     TESTNEXT           GO CHECK NEXT NAME
*
         OI    INDON,SELECTIT     INDIC MATCH FOUND
         MVI   0(R6),NUL          DELETE THIS NAME ENTRY
*
*
CHEKAGIN C     R2,LASTAB          WAS THIS LAST TAPE NAME ?
         BE    CHECKSW            YES, GO CHECK FOR SELECT
*
         L     R2,60(,R2)         LOAD PTR TO NEXT TAPE NAME
         B     NXTAPNAM           GO CHECK NEXT TAPE NAME
*
*
CHECKSW  TM    INDON,SELECTIT+INCLOPT  SELECTED AND INCLUDED ?
         BM    TAPENEXT           NO, SKIP THIS TAPE MEMBER
*
*
OUTPUT   L     R3,FIRSTAB         R3 PTR TO FIRST TAPE ENTRY
         TM    INDIN,TAPECOPY     WAS TAPE REQUESTED ?
         BNO   CKLISTO            NO, SKIP IT
*
         PUTS  RA
         MVC   0(19,R1),NAMECARD  MOVE 'NAME' CARD IMAGE TO BUFFER
         MVC   9(8,R1),0(R3)      MOVE NAME TO LINE
         MVC   19(52,R1),8(R3)    MOVE IDENT FIELD TO LINE
         MVI   71(R1),BLANK       BLANK OUT
         MVC   72(8,R1),71(R1)         REM OF LINE
         CHEX  RA
         AP    TAPCOUNT,PONE      INCR TAPE MEMBER COUNT
         AP    TPCCOUNT,PONE      INCR TAPE CARD COUNT
*
*
CKLISTO  TM    INDIN,LISTING      WAS LIST REQUESTED ?
         BNO   CKDECKO            NO, SKIP IT
*
         PUT   (R9)
         MVI   0(R1),CCSKP        SET CARR CNTRL FOR SKIP
         MVC   1(19,R1),NAMECARD  MOVE NAME IMAGE TO PRINT LINE
         MVC   20(38,R1),19(R1)   BLANK OUT MIDDLE OF LINE
         MVC   10(8,R1),0(R3)     MOVE NAME TO LINE
         MVC   58(52,R1),8(R3)    MOVE IDENT IMAGE TO LINE
         MVI   110(R1),BLANK      BLANK OUT
         MVC   111(10,R1),110(R1)      REMAINDER OF LINE
         TM    INDIN,KEYSONLY     IS THIS KEY STATEMENTS ONLY LIST ?
         BNO   *+10               NO, FORGET IT
*
         MVC   22(29,R1),KEYMSG   MOVE IN KEY STATEMENTS MSG
         NI    INDON,FF-FORSNEXT  INSURE FORCE PRINT INDIC OFF
         AP    LISCOUNT,PONE      INCR LIST MEMBER COUNT
         AP    LSCCOUNT,PONE      INCR LIST CARD COUNT
         ZAP   MEMLINCT,PONE      INIT LINE COUNT THIS MEMBER
         NI    INDON,FF-MAXMLINE  INDIC MAX LINES NOT REACHED
         ZAP   PGECOUNT,PONE      RESET PAGE COUNT TO ONE
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE COUNT INTO LINE
         MVC   112(4,R1),PAGE     MOVE 'PAGE' TO PRINT LINE
         MVC   PGEHEADR,0(R1)     SAVE PAGE HEADER FOR NEW PAGES
         MVI   CCTRL,CCSP2        SET CARR CNTRL FOR DOUBLE SPACE
         LA    R5,2               RESET LINE COUNT
         CP    LSCCOUNT,LMXCOUNT  MAX LINES REACHED ?
         BL    *+12               NO, FORGET IT
*
         OI    INDOT,LISTING+KEYSONLY+HAVEMAXL  IND MAX LINES ET AL
         NI    INDIN,FF-LISTING-KEYSONLY  TURN OFF LIST
*
         CP    MEMLINCT,MMXCOUNT  MAX MEMBER LINES REACHED ?
         BL    CKDECKO            NO, FORGET IT
*
         OI    INDON,MAXMLINE     INDIC MAX LINES REACHED
         PUT   (R9)
         MVC   0(42,R1),MMXMSG    MOVE MAXMEMBERLINE MSG TO PRINT
         MVC   42(79,R1),41(R1)   BLANK OUT REMAINDER OF LINE
         MVC   23(10,R1),GPMASK   MOVE IN EDIT MASK
         ED    23(10,R1),MEMLINCT EDIT COUNT INTO LINE
*
*
CKDECKO  TM    INDIN,STDECK+SSDECK     WAS ANY DECK REQUESTED ?
         BZ    NEXTAB             NO, SKIP IT
*
         TM    INDIN,SSDECK       WAS SOURCE REQUESTED ?
         BO    NEXTAB             YES, SKIP NAME CARD
*
         PUT   (R8)
         MVC   0(19,R1),NAMECARD  MOVE 'NAME' CARD IMAGE TO CARD
         MVI   0(R1),FF           CC 1 & 2 PUNCH
         MVI   1(R1),FF                12-11-0-7-8-9
         MVC   9(8,R1),0(R3)      MOVE NAME TO CARD
         MVC   19(52,R1),8(R3)    MOVE IDENT IMAGE TO CARD
         MVI   71(R1),BLANK       BLANK OUT
         MVC   72(8,R1),71(R1)         REMAINDER OF CARD
         AP    DEKCOUNT,PONE      INCR DECK MEMBER COUNT
         AP    DKCCOUNT,PONE      INCR DECK CARD COUNT
         CP    DKCCOUNT,CMXCOUNT  MAX CARDS REACHED ?
         BL    NEXTAB             NO, FORGET IT
*
         OI    INDOT,STDECK+HAVEMAXC   IND MAX CARDS & DECK DONE
         NI    INDIN,FF-STDECK-SSDECK  TURN OFF DECKS
*
*
NEXTAB   TM    INDIN,FF           ANY OUTPUT LEFT ?
         BZ    ENDUP              NO, GO TO END
         C     R3,LASTAB          WAS THIS LAST ?
         BE    CHKNEXT            GO OUTPUT DATA
*
         L     R3,60(,R3)         R3 ADDR OF NEXT TAB
         TM    INDIN,TAPECOPY     WAS TAPE REQUESTED ?
         BNO   CKALISTO           NO, SKIP IT
*
         PUTS  RA
         MVC   0(19,R1),ALISCARD  MOVE 'ALIAS' CARD IMAGE TO PRINT
         MVC   9(8,R1),0(R3)      MOVE NAME INTO LINE
         MVC   19(52,R1),8(R3)    MOVE IN IDENT FIELD
         MVI   71(R1),BLANK       BLANK OUT
         MVC   72(8,R1),71(R1)         REM OF LINE
         CHEX  RA
         AP    TPCCOUNT,PONE      INCR TAPE CARD COUNT
*
*
CKALISTO TM    INDIN,LISTING      WAS LIST REQUESTED ?
         BNO   CKADECKO           NO, SKIP IT
*
         TM    INDON,MAXMLINE     MAX MEMBER LINES REACHED ?
         BO    CKADECKO           YES, FORGET IT
*
         C     R5,LINECNT         IS A NEW PAGE INDICATED ?
         BL    NXTAPUT            NO, FORGET IT
*
         AP    PGECOUNT,PONE      INCREMENT PAGE COUNT
         PUT   (R9)
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE COUNT INTO LINE
         MVC   0(116,R1),PGEHEADR MOVE PAGE HEADER TO LINE
         MVI   0(R1),CCSKP        SET CARR CNTRL FOR SKIP
         LA    R5,2               RESET LINE COUNT TO TWO
         MVI   CCTRL,CCSP2        SET CARR CNTRL DOUBLE SPACE
*
NXTAPUT  PUT   (R9)
         MVC   0(1,R1),CCTRL      SET APPROPRIATE CARR CNTRL
         MVI   CCTRL,CCSP2        SET CARR CNTRL DOUBLE SPACE
         MVC   1(19,R1),ALISCARD  MOVE 'ALIAS' IMAGE TO PRINT LINE
         MVC   20(38,R1),19(R1)   BLANK OUT MIDDLE OF LINE
         MVC   10(8,R1),0(R3)     MOVE NAME TO LINE
         MVC   58(52,R1),8(R3)    MOVE IDENT IMAGE TO LINE
         MVI   110(R1),BLANK      BLANK OUT
         MVC   111(10,R1),110(R1)      REMAINDER OF LINE
         AP    LSCCOUNT,PONE      INCR LIST LINE COUNT
         AP    MEMLINCT,PONE      INCR MEMBER LINE COUNT
         LA    R5,1(,R5)          INCREMENT LINE COUNT
         CP    LSCCOUNT,LMXCOUNT  MAX LINES REACHED ?
         BL    *+12               NO, FORGET IT
*
         OI    INDOT,LISTING+KEYSONLY+HAVEMAXL  IND MAX LINES ET AL
         NI    INDIN,FF-LISTING-KEYSONLY  TURN OFF LIST
*
         CP    MEMLINCT,MMXCOUNT  MAX MEMBER LINES REACHED ?
         BL    CKADECKO           NO, FORGET IT
*
         OI    INDON,MAXMLINE     INDIC MAX LINES REACHED
         PUT   (R9)
         MVC   0(42,R1),MMXMSG    MOVE MAXMEMBERLINE MSG TO PRINT
         MVC   42(79,R1),41(R1)   BLANK OUT REMAINDER OF LINE
         MVC   23(10,R1),GPMASK   MOVE IN EDIT MASK
         ED    23(10,R1),MEMLINCT EDIT COUNT INTO LINE
*
*
CKADECKO TM    INDIN,STDECK+SSDECK     WAS ANY DECK REQUESTED ?
         BZ    NEXTAB             NO, SKIP IT
*
         TM    INDIN,SSDECK       WAS SOURCE REQUESTED ?
         BO    NEXTAB         YES, SKIP ALIAS CARD
         PUT   (R8)
         MVC   0(19,R1),NAMECARD  MOVE 'NAME' CARD IMAGE TO CARD
         MVI   0(R1),FF           CC 1 & 2 PUNCH
         MVI   1(R1),FF                12-11-0-7-8-9
         MVC   9(8,R1),0(R3)      MOVE NAME TO CARD
         MVC   19(52,R1),8(R3)    MOVE IDENT IMAGE TO CARD
         MVI   71(R1),BLANK       BLANK OUT
         MVC   72(8,R1),71(R1)         REMAINDER OF CARD
         AP    DKCCOUNT,PONE      INCR DECK CARD COUNT
         CP    DKCCOUNT,CMXCOUNT  MAX CARDS REACHED ?
         BL    NEXTAB             NO, FORGET IT
*
         OI    INDOT,STDECK+HAVEMAXC   IND MAX CARDS & DECK DONE
         NI    INDIN,FF-STDECK-SSDECK  TURN OFF DECKS
         B     NEXTAB             GO CHECK FOR MORE
*
*
GETNEXT  GETX  RB
         LR    R4,R1              R4 PTR TO INPUT RECORD
         AP    CARCOUNT,PONE      INCR INPUT CARD COUNT
*
*
CHKNEXT  TM    INDIN,FF           ANY OUTPUT LEFT ?
         BZ    ENDUP              NO, GO TO END
*
         CLC   0(2,R4),ATDBL      IS IT '@@' CARD ?
         BE    TAPENEXT           YES, GO CHECK NAME
*
         TM    INDIN,TAPECOPY     WAS TAPE REQUESTED ?
         BNO   CKCLISTO           NO, SKIP IT
*
         PUTS  RA
         MVC   0(80,R1),0(R4)     MOVE CARD TO OUTPUT BUFFER
         CHEX  RA
         AP    TPCCOUNT,PONE      INCR TAPE CARD COUNT
*
*
CKCLISTO TM    INDIN,LISTING      WAS LIST REQUESTED ?
         BNO   CKCDECKO           NO, SKIP IT
*
         TM    INDON,MAXMLINE     MAX MEMBER LINES REACHED ?
         BO    CKCDECKO           YES, FORGET IT
*
         TM    INDIN,KEYSONLY     IS THIS KEY STATEMENTS LISTING ?
         BNO   CHKLINES           NO, FORGET IT
*
         TM    INDON,FORSNEXT     IS FORCE PRINT INDICATED ?
         BNO   *+12               NO, GO CHECK IT OUT
*
         NI    INDON,FF-FORSNEXT  TURN OFF FORCE INDIC
         B     CHKLINES           GO PRINT IT
*
         CLI   0(R4),C'*'         IS IT A COMMENT ?
         BE    CHKLINES           YES, GO PRINT IT
*
         CLI   0(R4),C'.'         MAYBE A MACRO COMMENT ?
         BNE   CHEKEYS            NO, GO CHECK FOR KEY
*
         CLI   1(R4),C'*'         IS IT A MACRO COMMENT ?
         BE    CHKLINES           YES, GO PRINT IT
*
         B     CKCDECKO           ELSE FORGET IT
*
*
CHEKEYS  LA    R2,KEYSEND         R2 HIGH PTR FOR CHECK LOOP
         CLC   0(6,R2),9(R4)      IS IT 'MACRO' STATEMENT ?
         BNE   *+12               NO, GO CHECK OTHER KEYS
*
         OI    INDON,FORSNEXT     FORCE PRINT TWO LINES
         B     CHKLINES           GO PRINT IT
*
         LA    R6,8               R6 MINUS EIGHT
         LNR   R6,R6                   LOOP INCREMENT
         LA    R7,KEYLIST         R7 LOW PTR FOR CHECK LOOP
*
         BXLE  R2,R6,*+18         BUMP TO NEXT KEY IN TABLE
         CLC   0(6,R2),9(R4)      IS IT A KEY STATEMENT ?
         BNE   *-10               NO, GO CHECK SOME MORE
*
         B     CHKLINES           GO PRINT IT
*
         CLC   0(5,R2),9(R4)      IS IT 'MEND ' STATEMENT ?
         BE    CHKLINES           YES, GO PRINT IT
*
         CLC   1(4,R2),9(R4)      IS IT 'END ' STATEMENT ?
         BNE   CKCDECKO           NO, FORGET IT
*
*
CHKLINES C     R5,LINECNT         IS A NEW PAGE INDICATED ?
         BL    NXTPUT             NO, FORGET IT
*
         AP    PGECOUNT,PONE      INCREMENT PAGE COUNT
         PUT   (R9)
         MVC   115(6,R1),PGMASK   MOVE EDIT MASK TO LINE
         ED    115(6,R1),PGECOUNT EDIT PAGE COUNT INTO LINE
         MVC   0(116,R1),PGEHEADR MOVE PAGE HEADER TO LINE
         MVI   0(R1),CCSKP        SET CARR CNTRL FOR SKIP
         LA    R5,2               RESET LINE COUNT TO TWO
         MVI   CCTRL,CCSP2        SET CARR CNTRL DOUBLE SPACE
*
*
NXTPUT   PUT   (R9)
         MVC   0(1,R1),CCTRL      SET APPROPRIATE CARR CNTRL
         MVI   CCTRL,CCSP1        SET CARR CNTRL TO SINGLE SPACE
         MVI   1(R1),BLANK        BLANK OUT
         MVC   2(14,R1),1(R1)          FOR'D PRINT AREA
         MVI   95(R1),BLANK       BLANK OUT TRAILING
         MVC   96(25,R1),95(R1)        PRINT AREA
         MVC   15(80,R1),0(R4)    MOVE CARD TO OUTPUT COL 15
         LA    R5,1(,R5)          INCREMENT LINE COUNT
         AP    MEMLINCT,PONE      INCR MEMBER LINE COUNT
         AP    LSCCOUNT,PONE      INCR LIST LINE COUNT
         CP    LSCCOUNT,LMXCOUNT  MAX LINES REACHED ?
         BL    *+12               NO, FORGET IT
*
         OI    INDOT,LISTING+KEYSONLY+HAVEMAXL  IND MAX LINES ET AL
         NI    INDIN,FF-LISTING-KEYSONLY  TURN OFF LIST
*
         CP    MEMLINCT,MMXCOUNT  MAX MEMBER LINES REACHED ?
         BL    CKCDECKO           NO, FORGET IT
*
         OI    INDON,MAXMLINE     INDIC MAX LINES REACHED
         PUT   (R9)
         MVC   0(42,R1),MMXMSG    MOVE MAXMEMBERLINE MSG TO PRINT
         MVC   42(79,R1),41(R1)   BLANK OUT REMAINDER OF LINE
         MVC   23(10,R1),GPMASK   MOVE IN EDIT MASK
         ED    23(10,R1),MEMLINCT EDIT COUNT INTO LINE
*
*
CKCDECKO TM    INDIN,STDECK+SSDECK     WAS DECK OR SOURCE REQUESTED ?
         BZ    GETNEXT            NO, SKIP IT
*
         PUT   (R8)
         MVC   0(80,R1),0(R4)     MOVE CARD IMAGE TO OUTPUT CARD
         AP    DKCCOUNT,PONE      INCR DECK CARD COUNT
         CP    DKCCOUNT,CMXCOUNT  MAX CARDS REACHED ?
         BL    GETNEXT            NO, FORGET IT
*
         MVI   INDTT,STDECK+SSDECK     SET MASK FOR EITHER DECK
         NC    INDTT,INDIN      PICK UP CORRECT ONE
         OI    INDTT,HAVEMAXC     SET MASK INDIC MAX CARDS REACHED
         OC    INDOT,INDTT      INDIC MAX CARDS & DECK DONE
         NI    INDIN,FF-STDECK-SSDECK  TURN OFF DECKS
         B     GETNEXT            GO CHECK FOR MORE
*
*
         PATCH 64
*
*
ENDOK    OC    INDOT,INDIN      INDICATE ALL FUNCTIONS DONE
*
*
ENDUP    L     BASE,SAV12         RESTORE PRIMARY BASE
         L     RE,RETURN          BRANCH BACK TO
         BR    RE                      PRIMARY MODULE
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                       PROCESSING CONSTANTS'
GETAB    DC    F'64'
NAMECARD DC    CL19'@@  NAME='
ALISCARD DC    CL19'@@ ALIAS='
ATDBL    DC    CL3'@@'
NAMEQ    DC    CL5'NAME='
ALISEQ   DC    CL6'ALIAS='
CARDS    DC    CL7' CARDS'
MEMBERS  DC    CL9' MEMBERS,'
INDXHEDR DC    CL23'1SHARE GRAFLIB INDEX -'
INDXMSG  DC    CL23'0MIM#3 INDEX COMPLETE -'
MMXMSG   DC    CL5'0',CL29'*** MAXMEMBERLINES',CL8'REACHED'
KEYMSG   DC    CL29'***  KEY STATEMENTS ONLY  ***'
GPMASK   DC    XL10'40206B2020206B202120'
CDMASK   DC    XL7'4020206B202120'
PAGE     DC    CL4'PAGE'
PGMASK   DC    XL6'402020202020'
         SPACE
KEYLIST  DC    CL8'MEND'
         DC    CL8'EXIT'
         DC    CL8'HEDIT'
         DC    CL8'WTL'
         DC    CL8'WTOR'
         DC    CL8'WTO'
         DC    CL8'SEGWT'
         DC    CL8'SEGLD'
         DC    CL8'STAE'
         DC    CL8'SPIE'
         DC    CL8'POST'
         DC    CL8'WAIT'
         DC    CL8'ABEND'
         DC    CL8'IDENTIFY'
         DC    CL8'XCTL'
         DC    CL8'LINK'
         DC    CL8'LOAD'
         DC    CL8'ATTACH'
         DC    CL8'CALL'
         DC    CL8'GETMAIN'
         DC    CL8'ENQ'
         DC    CL8'OPEN'
         DC    CL8'RETURN'
         DC    CL8'SAVE'
         DC    CL8'DSECT'
         DC    CL8'CSECT'
         DC    CL8'START'
         DC    CL8'MEXIT'
KEYSEND  DC    CL8'MACRO'
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                   MAIN SAVE AND WORK AREAS'
SAVEA    DSECT
         DS    18F
DUBL     DS    D
SAVR     DS    2F
OPENL    DS    4F
LASTAB   DS    F
LINECNT  DS    F
FIRSTAB  DS    F
CMXCOUNT DS    F
LMXCOUNT DS    F
MMXCOUNT DS    F
MEMLINCT DS    F
INPCOUNT DS    F
MEMCOUNT DS    F
CARCOUNT DS    F
CARTOTAL DS    F
TAPCOUNT DS    F
TPCCOUNT DS    F
LISCOUNT DS    F
LSCCOUNT DS    F
DEKCOUNT DS    F
DKCCOUNT DS    F
EXITA    DS    F
EXITB    DS    F
READJFCB DS    F
FRSTNAME DS    F
LASTNAME DS    F
ENDBRNCH DS    F
SAV12    DS    F
RETURN   DS    F
IDENT    DS    52C
PGEHEADR DS    CL118
PGECOUNT DS    PL3
         DS    2C
PZRO     DS    C
PONE     DS    C
CCTRL    DS    C
CMPLCD   DS    C
IDNTNO   DS    C
INDIN    DS    C
INDON    DS    C
INDOT    DS    C
INDTT    DS    C
SYSTEM   DS    C                  = C'O' IF OS/VS, C'C' IF CMS  JSF@RPI
SAVEND   EQU   *
         TITLE '           MICHIGAN MODS UTILITY #3 (REL 4.0, 06/14/78)X
               -                    DATA CONTROL BLOCK AREA'
         PRINT NOGEN
         DCBD  DSORG=PO,DEVD=DA
         ORG   IHADCB+96
SHEXADDR DS    F
SHEXWORK DS    CL84
*
*
*
         END   MIM#3
