         TITLE 'SET THE OPTIONS FOR PRIME, NON-PRIME, AND CHEAP'
*---------------------------------------------------------------------*
*                S  E  T  I  N  I  T
*
*        THIS PROGRAM WILL BE USED TO DETERMINE WHAT OPTIONS WE
*        WANT SET (INITIATOR CLASSES, ETC ) BASED ON THE FACT WHAT
*        TIME OF THE DAY AND DAY OF THE WEEK IT IS. WE OFFER OUR
*        USERS A DISCOUNT IF THEY WILL RUN THERE JOBS IN THE OFF
*        HOURS. WE CONSIDER THERE IS EITHER PRIME OR NON-PRIME, AND
*        VERY CHEAP HOURS. COMMANDS ARE PUT THROUGH THE INTRDR FOR
*        JES2 COMMANDS. IF WE EVER NEED ANY MVS CMDS, I WILL PUT IN
*        SVC 34 CODE.
*
*                                      JIM MARSHALL, CAPT, USAF
*
*        NOTES:
*
*        PRIME HOURS - MONDAY-FRIDAY,  0600-1759  (EXCEPT HOLIDAYS)
*
*        NONPRIME HOURS -  MONDAY-FRIDAY, 1800-2259 (EXCEPT HOLIDAYS)
*
*        CHEAP HOURS -  MONDAY-FRIDAY, 2300-0559 (EXCEPT HOLIDAYS),
*                          SATURDAY, SUNDAY, AND ALL HOLIDAYS (EVEN
*                          IF THE HOLIDAY FALLS IN PRIME)
*
*        LINKS TO 'GETDATE' TO GET THE DAY OF THE WEEK AND IF IT
*        IS A HOLIDAY.
*
*        INSTALLED:  5 JAN 84
*
*        CHANGE LOG: NONE
*
*---------------------------------------------------------------------*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
SETINIT  AMODE 24
SETINIT  RMODE 24
SETINIT  CSECT
         SAVE  (14,12),,SADSC_SETINIT_&SYSDATE &SYSTIME
         LR    R12,R15
         USING SETINIT,R12
         LA    R11,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R11,8(R13)
         LR    R13,R11
         SPACE
*-------------------------------------------------------------------*
*        WE MUST FIRST FIND OUT IF THIS PROGRAM HAS BEEN STARTED
*        SOMEHOW. WE DO NOT WANT TO HAVE 18 OF THESE GOING
*        ON AT THE SAME TIME. THUS WE WILL TEST TO SEE IF AN
*        ENQUEUE IS ACTIVE.  MAJOR - ISGQUEUE, MINOR - ISGOPTS.
*-------------------------------------------------------------------*
         SPACE
         ENQ   (QNAME,RNAME,E,L'RNAME,SYSTEM),RET=TEST
         LTR   R15,R15
         BNZ   RETURN
         SPACE
*-------------------------------------------------------------------*
*        SINCE NO OTHER TASK IS ACTIVE WITH THE ENQUEUE, WE NEED
*        TO GET THE ENQUEUE TO LOCK OUT ALL OTHERS.
*-------------------------------------------------------------------*
         SPACE
         ENQ   (QNAME,RNAME,E,L'RNAME,SYSTEM)
         SPACE
*-------------------------------------------------------------------*
*        NOW SINCE WE HAVE THE ENQUEUE, WE WANT TO WAIT A MINUTE.
*        THIS IS BECAUSE AT IPL TIME, ALL THE AUTOMATIC COMMANDS
*        ARE SCHEDULED. SINCE THIS ROUTINE WILL RUN MORE THAN ONE
*        TIME A DAY, AT IPLTIME AND WHAT TIME THE IPL HAS HAPPENED,
*        WE CAN HAVE 2 OR 3 OF THESE COMING OUT ALL AT ONCE.
*        THUS IF WE WAIT A BIT, ALL OF THEM WILL GO AWAY FOR
*        WE HAVE THE ENQUEUE.
*------------------------------------------------------------------*
         SPACE
         STIMER WAIT,BINTVL=SNOOZE
         SPACE
*-------------------------------------------------------------------*
*        NOW WE NEED TO GO FIND OUT WHAT DAY OF THE WEEK IT IS
*        AND IF IT IS A HOLIDAY.
*-------------------------------------------------------------------*
         SPACE
         LA    R1,LIST
         LINK  EP=GETDATE
         SPACE
*-------------------------------------------------------------------*
*        NOW WE NEED SEE IF IT IS A HOLIDAY, IF SO, THEN WE
*        DO NOT NEED TO KNOW WHAT TIME IT IS.
*-------------------------------------------------------------------*
         SPACE
         CLI   HOLIDAY,C'H'            IS IT A HOLIDAY ?
         BE    SETCHEAP
         SPACE
*--------------------------------------------------------------------*
*        SINCE IT IS NOT A HOLDIAY, THEN WE MUST TEST TO SEE IF
*        IT IS SATURDAY OR SUNDAY. FOR IF SO, THEN WE GIVE OFF
*        HOURS RATES.
*--------------------------------------------------------------------*
         SPACE
         CLC   DAY(6),=C'SUNDAY'
         BE    SETCHEAP
         CLC   DAY(8),=C'SATURDAY'
         BE    SETCHEAP
         SPACE
*-------------------------------------------------------------------*
*        SINCE IT IS NOT A HOLIDAY, SATURDAY, OR SUNDAY, WE HAVE
*        TO GO GET THE TIME TO DO A FURTHER TESTS.
*-------------------------------------------------------------------*
         SPACE
GETCLOCK EQU   *
         TIME  DEC
         ST    R0,THECLOCK             SAVE THE CLOCK IN WORKAREA
         SPACE
*------------------------------------------------------------------*
*        NOW WE NEED TO SEE IF IT IS BETWEEN ACCORDING TO CONSTANTS
*        AT THE END OF THE PROGRAM.
*------------------------------------------------------------------*
         SPACE
         CLC  THECLOCK(4),STRPRIME
         BL   SETCHEAP                 MUST BE VERY CHEAP
         CLC  THECLOCK(4),STRNPRIM
         BL   SETPRIME                 MUST BE PRIME
         CLC  THECLOCK(4),STRCHEAP
         BL   SETNPRIM                 MUST BE NON-PRIME
         B    SETCHEAP                 MUST BE VERY CHEAP
         SPACE
*-------------------------------------------------------------------*
*        THIS IS WHERE WE SET THE PRIME OPTIONS
*-------------------------------------------------------------------*
         SPACE
SETPRIME EQU   *
         OPEN  (OPTPRIME,INPUT,READER,(OUTPUT))
LOOP1    EQU   *
         GET   OPTPRIME,BUF
         PUT   READER,BUF
         B     LOOP1
         SPACE
EOJPRIME EQU   *
         SPACE
         WTO   'ISG0121I  MVS IS NOW SET FOR PRIME HOUR PROCESSING.',  X
               ROUTCDE=(1)
         SPACE
         CLOSE (OPTPRIME,,READER)
         B     EOJ
         SPACE
*-------------------------------------------------------------------*
*        THIS IS WHERE WE SET THE NON-PRIME OPTIONS
*-------------------------------------------------------------------*
         SPACE
SETNPRIM EQU   *
         OPEN  (OPTOFFHR,INPUT,READER,(OUTPUT))
LOOP2    EQU   *
         GET   OPTOFFHR,BUF
         PUT   READER,BUF
         B     LOOP2
EOJOFFHR EQU   *
         SPACE
         WTO   'ISG0122I MVS IS NOW SET FOR NON-PRIME HOUR PROCESSING.'X
               ,ROUTCDE=(1)
         SPACE
         CLOSE (OPTOFFHR,,READER)
         B     EOJ
*-------------------------------------------------------------------*
*        THIS IS WHERE WE SET THE VERY CHEAP OPTIONS
*-------------------------------------------------------------------*
         SPACE
SETCHEAP EQU   *
         OPEN  (OPTCHPHR,INPUT,READER,(OUTPUT))
LOOP3    EQU   *
         GET   OPTCHPHR,BUF
         PUT   READER,BUF
         B     LOOP3
EOJCHPHR EQU   *
         SPACE
         WTO   'ISG0123I MVS IS NOW SET FOR VERY CHEAP HRS PROCESSING.'X
               ,ROUTCDE=(1)
         SPACE
         CLOSE (OPTCHPHR,,READER)
         B     EOJ
         SPACE
*---------------------------------------------------------------------*
*        RETURN AND WRAP THINGS UP, WE MUST FREE THE ENQUEUE AND
*        TIDY UP AFTER OURSELVES.
*---------------------------------------------------------------------*
EOJ      EQU  *
         DEQ  (QNAME,RNAME,L'RNAME,SYSTEM)
RETURN   EQU  *
         L    R13,4(R13)
         RETURN (14,12),RC=0
         EJECT
*-------------------------------------------------------------------*
*        DEFINE ALL OUR CONSTANTS, ETC
*-------------------------------------------------------------------*
         SPACE
OPTPRIME DCB   DSORG=PS,MACRF=GM,DDNAME=PRIME,EODAD=EOJPRIME
         SPACE
OPTOFFHR DCB   DSORG=PS,MACRF=GM,DDNAME=NPRIME,EODAD=EOJOFFHR
         SPACE
OPTCHPHR DCB   DSORG=PS,MACRF=GM,DDNAME=VCHEAP,EODAD=EOJCHPHR
         SPACE
READER   DCB   DSORG=PS,MACRF=PM,LRECL=80,BLKSIZE=80,RECFM=F,          X
               DDNAME=READER
         SPACE
SAVEAREA DC   18F'0'                   MY SAVE AREA
SNOOZE   DC   A(100*15)                15 SECONDS
BUF      DS   CL80                     INPUT BUFFER
QNAME    DC   CL8'ISGQUEUE'            MAJOR QUEUE NAME
RNAME    DC   CL7'ISGOPTS'             MINOR QUEUE NAME
         DS   0F
*                 HHMMSSTH             TIME RETURN VALUE FORMAT
THECLOCK DC   XL4'00000000'            WORK AREA FOR TIME
STRPRIME DC   XL4'06000000'            PRIME TIME START
STRNPRIM DC   XL4'18000000'            NON-PRIME START
STRCHEAP DC   XL4'23000000'            CHEAP START
         DS   0F
LIST     DC   A(DATE)
         DC   X'80',AL3(DAY)
DATE     DC   CL9' '
DAY      DS   0CL10
         DC   CL9' '
HOLIDAY  DC   CL1' '
         LTORG
         END
