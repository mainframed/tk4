         TITLE  'SMF IEFU83 EXIT PGM FOR HSRP SYSTEM'
*-------------------------------------------------------------------*
*                 I   E   F   U   8   3                             *
*                                                                   *
*        MAJOR  MODIFICATION OF THE IEFU83 EXIT I GOT FROM          *
*        BLUE CROSS - BLUE SHIELD, RICHMOND, VA. IT HAD THE         *
*        BRANCH TABLE CONCEPT IN IT, BUT IT USED THE BRANCH         *
*        TABLE TO DECIDE ONLY IF IT WAS TO WRITE A RECORD OR        *
*        NOT. I USED THE TABLE TO BRANCH TO THE APPROPRIATE         *
*        CODE TO FURTHER PROCESS A TYPE OF RECORD.                  *
*                                                                   *
*                             JIM MARSHALL, CAPT, USAF (RET)        *
*                             GRUMMAN DATA SYSTEMS                  *
*                                                                   *
*  INSTALLED: 10 APRIL  1991                                        *
*                                                                   *
*  CHANGE LOG :                                                     *
*  16 JUL 82 - ADDED SOME CODE TO LOOK AT TEMPORARY ERRORS ON TAPE  *
*              FROM THE TYPE 21 RECORDS. THUS WE CAN TELL THE OPER- *
*              ATORS WHEN TO CLEAN A TAPE DRIVE. THIS CAME BY WAY   *
*              OF THE U.S. SENATE, I TOOK THERE CODE AS A MODEL.    *
*              CODE IS IN LIMBO FOR A WHILE, SOMETHING IS WRONG. JDM*
*  10 FEB 83 - MODIFIED THE EXIT TO LOOK AT TYPE 40 RECORDS. THE    *
*              GOAL IS TO RID OURSELVES OF THOSE RECORDS WITH ZERO  *
*              EXCP COUNTS.                                      JDM*
*   8 JUN 83 - TRYING TO ELIMINATE RECORDS, SO 14'S AND 15'S ARE A  *
*              GOOD PLACE TO DO IT. RECORDS WITH ZERO EXCP COUNTS   *
*              CAN BE TRASHED. SEE THE CODE FOR DETAILS          JDM*
*  20 DEC 84 - FINALLY GETTING AROUND TO PRODUCING MSGS ON TAPES    *
*              TYPE 21 RECORDS. THE SENATE CODE THIS IS BASED IS IN *
*              ERROR IN THE 'SIO' FIELD SIZE. CAPT JIM PETERSON     *
*              FOUND THIS ERROR AND GAVE ME THE FIX. ADDED SOME     *
*              LOGIC TO THE CODE ADD THE VOLSER TO THE MSG THAT     *
*              TELLS THE OPERATOR TO CLEAN THE TAPE DRIVE. ADDED    *
*              THE TAPE NUMBER TO IT. ALSO I PUT OUT THE 'STATS'    *
*              MSG TO BEFORE I PUT THE CLEAN MSG. USERS THEN GET    *
*              THE IDEA THAT THE TAPE CAUSED THE CLEAN MSG TO COME  *
*              OUT AND CAUSED THE PROBLEM. SEE THE CODE !!!!    JDM4*
*  30 JAN 91 - ADDED STATEMENTS FOR 24-BIT ADDRESSING.          JDM5*
*  01 FEB 91 - ADDED ANALYSIS OF TYPE30 SUBTYPE 5 RECORDS TO TELL   *
*              A USER IF THE JOB CLASS USED WAS CORRECT BASED ON    *
*              TOTAL CPU TIME USED. SEE CODE BELOW.             JDM6*
*  10 APR 91 - DISABLE PROCESSING TYPE 21 RECORDS FOR NOW       JDM7*
*-------------------------------------------------------------------*
         EJECT
         MACRO                                                   JDM4
.*------------------------------------------------------------*  JDM4
.*       THIS EDITS ONE BYTE                                  *  JDM4
.*------------------------------------------------------------*  JDM4
&LAB     EDITL1 &FIELD,&DISPL                                    JDM4
         SR    R1,R1                                             JDM4
         IC    R1,&FIELD                                         JDM4
         CVD   R1,DBLEWD                                         JDM4
         MVC   AREA(4),PATL1                                     JDM4
         ED    AREA(4),DBLEWD+6                                  JDM4
         MVC   WTOAREA+&DISPL.(3),AREA+1                         JDM4
         MEND                                                    JDM4
         SPACE
         MACRO                                                   JDM4
.*------------------------------------------------------------*  JDM4
.*       THIS EDITS TWO BYTES                                 *  JDM4
.*------------------------------------------------------------*  JDM4
&LAB     EDITL2  &FIELD,&DISPL                                   JDM4
         SR    R1,R1                                             JDM4
         MVC   DBLEWD(2),&FIELD                                  JDM4
         LH    R1,DBLEWD                                         JDM4
         CVD   R1,DBLEWD                                         JDM4
         MVC   AREA(6),PATL2                                     JDM4
         ED    AREA(6),DBLEWD+5                                  JDM4
         MVC   WTOAREA+&DISPL.(5),AREA+1                         JDM4
         MEND                                                    JDM4
         SPACE
         MACRO                                                   JDM6
.*------------------------------------------------------------*  JDM6
.*       THIS EDITS FOUR BYTES                                *  JDM6
.*------------------------------------------------------------*  JDM6
&LAB     EDITL4  &FIELD,&DISPL                                   JDM6
         SR    R1,R1                                             JDM6
         MVC   DBLEWD(4),&FIELD                                  JDM6
         L     R1,DBLEWD                                         JDM6
         CVD   R1,DBLEWD                                         JDM6
         MVC   AREA(10),PATL4                                    JDM6
         ED    AREA(10),DBLEWD+3                                 JDM6
         MVC   WTOAREA+&DISPL.(9),AREA+1                         JDM6
         MEND                                                    JDM6
         SPACE
R0       EQU   0                  NOT USED
R1       EQU   1                  ENTRY VALUE IS SAVED
R2       EQU   2                  BAL  REGISTER
R3       EQU   3                  SAVED REGISTER 1 AT ENTRY
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R6       EQU   6                  WORK REGISTER
R7       EQU   7                  WORK REGISTER
R8       EQU   8                  ADDRESSABILITY TO SMF REC DSECT
R9       EQU   9                  TEMPORARY USAGE REGISTER
R10      EQU   10                 TEMPORARY ADDRESSABILITY REG
R11      EQU   11                 KEEP FREE FOR EXTRA BASE
R12      EQU   12                 BASE REGISTER
R13      EQU   13                 ADDR TO WORKAREA DSECT
R14      EQU   14                 USE IF YOU KNOW WHAT YOU ARE DOING
R15      EQU   15                 UPON ENTRY THE BASE ADDRESS, AFTER
*                                    THAT A WORK REGISTER
IEFU83   AMODE 24                                                JDM5
IEFU83   RMODE 24                                                JDM5
IEFU83   CSECT
         SAVE  (14,12),,IEFU83_JDM_&SYSDATE &SYSTIME             JDM5
         LR    R12,R15             SETUP BASE REGISTER
         LR    R11,R12             SETUP BASE REGISTER
         LA    R11,4095(R12)
         USING IEFU83,R12,R11      GET NEW BASE REGISTERS
         LR    R3,R1               SAVE REGISTER 1
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)          GO GET IT
         LR    R9,R1               SAVE THE ADDRESS
         USING WORKAREA,R9         ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4          SETUP
         ST    R9,8(R13)            SAVEAREA
         LR    R13,R9                   CHAIN
         EJECT
*--------------------------------------------------------------------*
*        SMF EXIT. USES SMF RECORD TYPE AS INDEX INTO                *
*         A BYTE TABLE. THE BYTE IS THEN MULTIPLIED BY 4             *
*         AND USED AS A BRANCH INDEX TO BRANCH TO A BRANCH.          *
*                                                                    *
*        INPUT  R1 POINTS TO THE ADDRESS OF THE SMF RECORD           *
*               ( SAVED IN R3 )                                      *
*        OUTPUT R15 CONTAINS                                         *
*            0 - WRITE THE RECORD                                    *
*            4 - DELETE THE RECORD                                   *
*        EXIT - RETURN VIA R14                                       *
*--------------------------------------------------------------------*
         SPACE
         L     R8,0(R3)            GET ADDRESS OF RECORD
         USING SMFREC,R8           GET ADDR TO RECORD
         SPACE
         L     R7,TABADD           GET OFFSET TO TABLE DSECT
         LA    R7,0(R7,R12)        ADDRESS OF TABLE
         SR    R15,R15             CLEAR WORK REGISTER
         SR    R4,R4               CLEAR WORK REGISTER
         IC    R4,5(R8)            GET REC TYPE AT  SMFREC+4
         IC    R15,0(R4,R7)        USE AS INDEX INTO TABLE
         SLL   R15,2               MULTIPLY BY 4
         B     BRTAB(R15)          BRANCH TO BRANCH
         B     RETURN0             DUMMY FOR SAFETY
         SPACE
*--------BRANCH TABLE AS FOLLOWS ------------------------------------*
         SPACE
BRTAB    DS    0H                  BRANCH TO CORRECT ROUTINE
         B     RETURN0       0     WRITE
         B     RETURN4       1     NOWRITE
         B     TYPE14R       2
         B     TYPE15R       3
         B     TYPE40R       4
         B     RETURN0       5     WAS TYPE21R                   JDM7
         B     TYPE30R       6     PROCESS TYPE 30 RECORDS       JDM6
         B     RETURN0       7     SPARE                         JDM6
         B     RETURN0       8     SPARE                         JDM6
         SPACE
*--------RETURN WITH RET CODE = 0   ( WRITE THE RECORD )  -----------*
         SPACE
RETURN0  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,0               GET RETURN CODE
         ST    R15,16(R4)          PUT IN RETURN CODE
         B     RETURN              GO RETURN
         SPACE
*------- RETURN WITH RET CODE = 4   ( DO NOT WRITE RECORD )  --------*
         SPACE
RETURN4  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,4               PUT IN RETURN CODE = 4
         ST    R15,16(R4)          PUT IN RETURN CODE
         SPACE
*------- RETURN TO CALLER FOR GOOD, DO FREEMAIN AND GO AWAY !!!  ----*
         SPACE
RETURN   EQU   *
         LR    R1,R13              GET ADDRESS FOR FREEMAIN
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         L     R13,4(R13)          GET HIGH SAVE ADDRESS
         FREEMAIN R,LV=(0),A=(1)   FREE SAVE AREA
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         BR    R14                 RETURN FOR GOOD
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 14 RECORDS. WE WANT TO ELIMINATE           *
*        ALL RECORDS THAT HAVE ZERO EXCP COUNTS.                     *
*--------------------------------------------------------------------*
         SPACE
TYPE14R  EQU   *
         CLC   SMFEXCP(4),=XL4'00' ZERO EXCP COUNTS ??
         BE    RETURN4
         B     RETURN0
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 15 RECORDS. WE WANT TO ELIMINATE           *
*        ALL RECORDS THAT HAVE ZERO EXCP COUNTS BUT ONLY FOR         *
*        VIO AND TEMPORARY FILES. PERSON COULD OPEN A REAL FILE      *
*        AND IMMEDIATELY CLOSE IT DESTROYING THE FILE. NOT GOOD      *
*        NOTE: FOR TYPE 15 RECORDS YOU USE THE 14 DSECT              *
*--------------------------------------------------------------------*
         SPACE
TYPE15R  EQU   *
         CLC   SMFEXCP(4),=XL4'00' ZERO EXCP COUNTS ???
         BNE   RETURN0             DO SAVE THE RECORD
         TM    SMF14RIN,SMF14VIO   IS IT VIO
         BO    RETURN4             DO NOT SAVE IT
         TM    SMF14RIN,SMF14TDS   IS IT A TEMPORARY FILE ???
         BO    RETURN4             DO NOT SAVE IT
         B     RETURN0             SAVE ALL OTHERS
         EJECT
*----------------------------------------------------------------JDM4*
*        LOOK AT THE TYPE 21 RECORDS TO SEE WHEN A TAPE DRIVE    JDM4*
*        SHOULD BE CLEANED (ALSO TELL THE OPERATOR).             JDM4*
*                                                                JDM4*
*        TO ME, THE SEQUENCE OF MSGS COMING OUT ON THE USER LOG  JDM4*
*        IS IMPORTANT. IF THE USER SEES THE CLEAN MSG FOR THE    JDM4*
*        TAPE DRIVE BEFORE THE 'STATS' MSG, THEN THEY WILL       JDM4*
*        BELIEVE THAT THE DRIVE WAS DIRTY BEFORE THERE TAPE WAS  JDM4*
*        MOUNTED AND WILL THEY BE UNHAPPY. WHAT REALLY HAPPENED  JDM4*
*        IS PROBABLY THEIR TAPE HAS LEFT THE DIRT BEHIND. THUS   JDM4*
*        WE WANT THE 'STATS' MSG BEFORE THE 'CLEAN' MSG.         JDM4*
*----------------------------------------------------------------JDM4*
         SPACE 1                                                 JDM4
TYPE21R  EQU   *                                                 JDM4
         SR    R1,R1               CLEAR R1 FOR WORKING          JDM4
         IC    R1,SMF21TR                                        JDM4
         SR    R2,R2               CLEAR R2                      JDM4
         IC    R2,SMF21TW                                        JDM4
         AR    R1,R2               ADD THE 2 VALUES              JDM4
         ST    R1,NUMERRS          SAVE THE ERROR CT FOR LATER   JDM4
         SPACE 1                                                 JDM4
*----------------------------------------------------------------JDM4*
*        NOW WE WILL CONSTRUCT THE 'STATS' MSG FOR THE USER.     JDM4*
*----------------------------------------------------------------JDM4*
         SPACE 1                                                 JDM4
         MVC   WTOAREA(MSG21BL),MSG21B   MOVE IN THE MSG         JDM4
         MVC   WTOAREA+19(6),SMF21VOL    MOVE IN THE VOLSER      JDM4
         EDITL1 SMF21TR,29         # OF TEMP READ ERRORS         JDM4
         EDITL1 SMF21TW,36         # OF TEMP WRITE ERRORS        JDM4
         EDITL1 SMF21PR,43         # OF PERMANENT READ ERRORS    JDM4
         EDITL1 SMF21PW,50         # OF PERMANENT WRITE ERRORS   JDM4
         EDITL1 SMF21NB,57         # OF NOISE BLOCKS             JDM4
         EDITL2 SMF21ERG,64        # OF ERASE GAPS               JDM4
         EDITL2 SMF21CLN,73        # OF CLEANER ACTIONS          JDM4
         EDITL2 SMF21SIO,83        # OF SIOS                     JDM4
         SPACE 1                                                 JDM4
         LA    R1,WTOAREA                                        JDM4
         WTO   MF=(E,(R1))         PUT OUT THE MSG               JDM4
S21WTO   EQU   *                                                 JDM4
         CLC   NUMERRS(4),MAXERR   ARE WE ABOVE MAX THRESHOLD    JDM4
         BH    S21CLEAN            IF SO, GO TELL OPERATOR       JDM4
         CLI   SMF21PR,0           ANY PERMANENT READ ERRORS???  JDM4
         BNE   S21CLEAN            IF SO, BYPASS                 JDM4
         CLI   SMF21PW,0           ANY PERMANENT WRITE ERRORS??? JDM4
         BE    RETURN0                                           JDM4
S21CLEAN EQU   *                                                 JDM4
         MVC   WTOAREA(MSG21AL),MSG21A  MOVE IN THE MSG          JDM4
         MVC   WTOAREA+15(6),SMF21VOL   MOVE IN THE VOLSER       JDM4
         UNPK  DBLEWD(5),SMF21CA(3)   GET DEV ADDR               JDM4
         TR    DBLEWD(4),S21TABLE  CONVERT ADDR TO HEX           JDM4
         MVC   WTOAREA+38(3),DBLEWD+1  PUT IN MSG                JDM4
         LA    R1,WTOAREA          GET MSG ADDR                  JDM4
         WTO   MF=(E,(R1))         PUT IT OUT                    JDM4
         SPACE 1                                                 JDM4
         B     RETURN0                                           JDM4
         EJECT
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 40 RECORDS. WE WANT TO ELIMINATE           *
*           1.  DD DUMMY RECORDS IF THEY ARE NOT IN A CONCATENATION  *
*           2.  TERM=TS                                              *
*           3.  ZERO EXCP COUNTS IN THE RECORD                       *
*--------------------------------------------------------------------*
         SPACE
TYPE40R  EQU   *
         CLC   TDDVAR(2),=X'000A'     IS THERE ONLY ONE DD CARD ?    R2
         BNE   RETURN0                IF CONCAT, THEN WRITE IT
         CLC   TDDCUAD(4),=X'00000FFF' IS IT VIO ?
         BE    RETURN0                IF SO, WRITE IT
         CLC   TDDNEXCP(4),=XL4'0'    IS EXCP COUNT = 0 ??
         BE    RETURN4                IF SO, DO NOT WRITE IT
         B     RETURN0                GO WRITE IT
         EJECT
*----------------------------------------------------------------JDM6*
*        PROCESS THE TYPE 30 RECORDS. LOOKING AT THE SUBTYPE 5   JDM6*
*        RECORDS AT JOB END, TOTAL CPU TIME IS REPORTED.         JDM6*
*        IF YOUR JOB CLASS STRUCTURE IS BASED ON TOTAL CPU       JDM6*
*        TIME,  THEN YOU KNOW IF THE USER PROCESSED IN THE RIGHT JDM6*
*        JOB CLASS. YOU CAN TELL THE USER THE FOLLOWING TYPES    JDM6*
*        OF THINGS:                                              JDM6*
*         1. THE CORRECT CLASS TO USE.                           JDM6*
*         2. IF YOU HAVE A JOB CLASS FOR NO TAPE (LIKE CLASS=A)  JDM6*
*            AND THE USER USED A TAPE FOR CLASS=A, THEN YOU CAN  JDM6*
*            INFORM HIM OF THE CHANGE.                           JDM6*
*        PHILOSOPHICALLY SOME INSTALLATIONS PLACE THIS CHECK UP  JDM6*
*        FRONT AND KILL THE USER. BRUTAL BUT EFFECTIVE. OTHER    JDM6*
*        SERVICE BUREAUS DO NOT WANT TO KILL PAYING CUSTOMERS    JDM6*
*        BUT WANT THE USER TOLD OF THE CORRECT. SINCE THE MSG    JDM6*
*        WILL ALSO SHOW UP IN THE SYSLOG, THE BUREAU COULD DO    JDM6*
*        SOME PENALTY BILLING, ETC.   ENJOY                      JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
TYPE30R  EQU   *                                                 JDM6
         LR    R10,R8                 GET ADDRESS OF RECORD      JDM6
         USING SMFRCD30,R10           GET ADDRESSABILITY         JDM6
         LR    R1,R8                  GET ADDRESS OF RECORD      JDM6
         A     R1,SMF30SOF            DISPLACE TO SUBSYS SECT    JDM6
         USING SMF30TYP,R1            GET ADDRESSABILITY         JDM6
         CLI   SMF30TYP+1,X'05'       SUBTYPE = 5  ????          JDM6
         BNE   RETURN0                BYPASS AND WRITE IT        JDM6
         DROP  R1                                                JDM6
         LR    R1,R8                  GET ADDRESS OF RECORD      JDM6
         A     R1,SMF30IOF            DISPLACEMENT TO ID SECT    JDM6
         USING SMF30JBN,R1            GET ADDRESSABILITY         JDM6
         CLI   SMF30JNM,C'J'          IS IT A BATCH JOB ???      JDM6
         BNE   RETURN0                BYPASS AND WRITE IT        JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        STRATEGY WILL BE TO GET ALL THE INFOMATION FOR ANALYSIS JDM6*
*        AND THEN MAKE OUR DECISIONS, ETC                        JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
         MVC   T30JNAME(8),SMF30JBN   SAVE THE CLASS             JDM6
         MVC   T30CLASU(1),SMF30CLS   SAVE THE CLASS             JDM6
         DROP  R1                                                JDM6
         LR    R1,R8                  GET ADDRESS OF RECORD      JDM6
         A     R1,SMF30COF            DISPL TO PROC ACCT SECT    JDM6
         USING SMF30PTY,R1            GET ADDRESSABILITY         JDM6
         MVC   DBLEWD(4),SMF30CPT     GET CPU UNDER TCB          JDM6
         L     R15,DBLEWD             LOAD CPU UNDER TCB         JDM6
         MVC   DBLEWD(4),SMF30CPS     GET CPU UNDER SRB          JDM6
         A     R15,DBLEWD             ADD CPU UNDER TCB          JDM6
         ST    R15,T30TCPUU           SAVE TOTAL CPU             JDM6
         DROP  R1                                                JDM6
         LR    R1,R8                  GET ADDRESS OF RECORD      JDM6
         A     R1,SMF30OOF            DISPLACEMENT TO OPER SECT  JDM6
         USING SMF30PDM,R1            GET ADDRESSABILITY         JDM6
         L     R3,SMF30TPR            GET SPECIFIC MOUNTS        JDM6
         A     R3,SMF30PTM            ADD NON-SPECIFIC MOUNTS    JDM6
         STC   R3,T30MOUNT            SAVE TOTAL MOUNTS          JDM6
         DROP  R1                                                JDM6
         DROP  R10                                               JDM6
         SPACE 1                                                 JDM6
         CLC   T30TCPUU(4),=F'0'      POSSIBLE JCL ERROR         JDM6
         BE    T30MSGE                YES A JCL ERROR            JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        LOOP TABLE CHECKING FOR VALID CLASSES                   JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
         LA    R1,CLASSTAB             GET ADDRESS OF TABLE      JDM6
T30LOOP  EQU   *                                                 JDM6
         CLC   0(4,R1),=XL4'FFFFFFFF'  HIT FENCE ??              JDM6
         BE    T30MSGB                 SOME OTHER CLASS          JDM6
         CLC   0(1,R1),T30CLASU        DO WE MATCH               JDM6
         BE    T30ANAL                 GO PROCESS                JDM6
         LA    R1,8(R1)                BUMP POINTER              JDM6
         B     T30LOOP                 KEEP ON TRUCKIN           JDM6
T30ANAL  EQU   *                                                 JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        NEED TO RUN THE TABLE TO FIND OUT WHAT IS BEST CLASS    JDM6*
*         1. USER SELECTED RIGHT CLASS FOR CPU TIME  USED        JDM6*
*         2. USER SELECTED WRONG CLASS FOR CPU TIME  USED AND    JDM6*
*            SHOULD BE TOLD.                                     JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
         LA    R1,CLASSTAB             GET ADDRESS OF TABLE      JDM6
T30LOOP1 EQU   *                                                 JDM6
         CLC   0(4,R1),=XL4'FFFFFFFF'  HIT FENCE ??              JDM6
         BE    T30ANALF                SOME OTHER CLASS          JDM6
         L     R7,4(R1)                GET TABLE VALUE           JDM6
         S     R7,T30TCPUU             DO WE MATCH               JDM6
         BP    T30ANAL1                TABLE > USER              JDM6
         LA    R1,8(R1)                BUMP POINTER              JDM6
         B     T30LOOP1                KEEP ON TRUCKIN           JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        WE KNOW THE CLASS RUN AND THE CLASS JOB SHOULD HAVE     JDM6
*        USED. NOW ANALYZE                                       JDM6
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
T30ANAL1 EQU   *                                                 JDM6
         MVC   T30CLAST(1),0(R1)       MOVE IN THE CLASS         JDM6
         CLC   T30CLAST(1),T30CLASU    TAB CLASS = USER CLASS    JDM6
         BE    T30ANAL2                DO FURTHER CHECKING       JDM6
         CLC   T30CLAST(1),CLASSTAB    WAS SERV CLASS = A        JDM6
         BNE   T30MSGC                 GIVE IT TO THEM           JDM6
         CLI   T30MOUNT,X'00'          DID HE HAVE MOUNTS        JDM6
         BE    T30MSGC                                           JDM6
         MVC   T30CLAST(1),CLASSTAB+8                            JDM6
         B     T30MSGC                                           JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        KNOW CLASS RUN NOT MATCH CLASS USER SHOULD HAVE         JDM6
*        SELECTED.  NOW ANALYZE FURTHER.                         JDM6
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
T30ANAL2 EQU   *                                                 JDM6
         CLC   T30CLASU(1),CLASSTAB    WAS CLASS = A  USED ?     JDM6
         BNE   T30MSGA                                           JDM6
         CLI   T30MOUNT,X'00'          DID HE HAVE MOUNTS        JDM6
         BE    T30MSGA                                           JDM6
         MVC   T30CLAST(1),CLASSTAB+8                            JDM6
         B     T30MSGC                                           JDM6
T30ANALF EQU   *                                                 JDM6
         MVC   T30CLAST(1),WORSTCL     GET  WORST CLASS          JDM6
         B     T30MSGC                                           JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        THIS WHERE WE WRITE OUT ALL THE MESSAGES.               JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
T30MSGA  EQU   *                                                 JDM6
         MVI   WTOAREA,C' '           MOVE IN BLANK              JDM6
         MVC   WTOAREA+1(129),WTOAREA ROLL THE BLANKS            JDM6
         MVC   WTOAREA(MSG30AL),MSG30A  MOVE IN THE MSG          JDM6
         MVC   WTOAREA+13(8),T30JNAME   MOVE JOBNAME             JDM6
         MVC   WTOAREA+28(1),T30CLASU   MOVE JOBCLASS USED       JDM6
         LA    R15,WTOAREA+39           GET ADDR OF OUTPUT       JDM6
         L     R1,T30TCPUU              GET TOTAL CPU OF USER    JDM6
         BAL   R4,CONVTME                                        JDM6
         LA    R15,WTOAREA+58           GET ADDR OF OUTPUT       JDM6
         SLR   R1,R1                    CLEAR R1                 JDM6
         IC    R1,T30MOUNT              GET MOUNTS               JDM6
         BAL   R4,CONMOUNT                                       JDM6
         MVC   WTOAREA+68(1),T30CLAST   MOVE JOBCLASS USED       JDM6
         BAL   R4,T30ISSUE            ALWAYS WRITE THIS RECORD   JDM6
         B     RETURN0                                           JDM6
         SPACE 1                                                 JDM6
T30MSGB  EQU   *                                                 JDM6
         MVI   WTOAREA,C' '           MOVE IN BLANK              JDM6
         MVC   WTOAREA+1(129),WTOAREA ROLL THE BLANKS            JDM6
         MVC   WTOAREA(MSG30BL),MSG30B  MOVE IN THE MSG          JDM6
         MVC   WTOAREA+13(8),T30JNAME   MOVE JOBNAME             JDM6
         MVC   WTOAREA+28(1),T30CLASU   MOVE JOBCLASS USED       JDM6
         LA    R15,WTOAREA+39           GET ADDR OF OUTPUT       JDM6
         L     R1,T30TCPUU              GET TOTAL CPU OF USER    JDM6
         BAL   R4,CONVTME                                        JDM6
         LA    R15,WTOAREA+58           GET ADDR OF OUTPUT       JDM6
         SLR   R1,R1                    CLEAR R1                 JDM6
         IC    R1,T30MOUNT              GET MOUNTS               JDM6
         BAL   R4,CONMOUNT                                       JDM6
         BAL   R4,T30ISSUE            ALWAYS WRITE THIS RECORD   JDM6
         B     RETURN0                                           JDM6
         SPACE 1                                                 JDM6
T30MSGC  EQU   *                                                 JDM6
         MVI   WTOAREA,C' '           MOVE IN BLANK              JDM6
         MVC   WTOAREA+1(129),WTOAREA ROLL THE BLANKS            JDM6
         MVC   WTOAREA(MSG30CL),MSG30C  MOVE IN THE MSG          JDM6
         MVC   WTOAREA+13(8),T30JNAME   MOVE JOBNAME             JDM6
         MVC   WTOAREA+28(1),T30CLASU   MOVE JOBCLASS USED       JDM6
         LA    R15,WTOAREA+39           GET ADDR OF OUTPUT       JDM6
         L     R1,T30TCPUU              GET TOTAL CPU OF USER    JDM6
         BAL   R4,CONVTME                                        JDM6
         LA    R15,WTOAREA+58           GET ADDR OF OUTPUT       JDM6
         SLR   R1,R1                    CLEAR R1                 JDM6
         IC    R1,T30MOUNT              GET MOUNTS               JDM6
         BAL   R4,CONMOUNT                                       JDM6
         MVC   WTOAREA+68(1),T30CLASU   MOVE JOBCLASS USED       JDM6
         BAL   R4,T30ISSUE            ALWAYS WRITE THIS RECORD   JDM6
         MVI   WTOAREA,C' '           MOVE IN BLANK              JDM6
         MVC   WTOAREA+1(129),WTOAREA ROLL THE BLANKS            JDM6
         MVC   WTOAREA(MSG30DL),MSG30D  MOVE IN THE MSG          JDM6
         MVC   WTOAREA+13(8),T30JNAME   MOVE JOBNAME             JDM6
         MVC   WTOAREA+52(1),T30CLAST   MOVE JOBCLASS SHOULD USE JDM6
         BAL   R4,T30ISSUE            ALWAYS WRITE THIS RECORD   JDM6
         B     RETURN0                                           JDM6
T30MSGE  EQU   *                                                 JDM6
         MVI   WTOAREA,C' '           MOVE IN BLANK              JDM6
         MVC   WTOAREA+1(129),WTOAREA ROLL THE BLANKS            JDM6
         MVC   WTOAREA(MSG30EL),MSG30E  MOVE IN THE MSG          JDM6
         MVC   WTOAREA+13(8),T30JNAME   MOVE JOBNAME             JDM6
         MVC   WTOAREA+28(1),T30CLASU   MOVE JOBCLASS USED       JDM6
         LA    R15,WTOAREA+39           GET ADDR OF OUTPUT       JDM6
         L     R1,T30TCPUU              GET TOTAL CPU OF USER    JDM6
         BAL   R4,CONVTME                                        JDM6
         BAL   R4,T30ISSUE            ALWAYS WRITE THIS RECORD   JDM6
         B     RETURN0                                           JDM6
         SPACE 1                                                 JDM6
T30ISSUE EQU   *                                                 JDM6
         LA    R1,WTOAREA          GET MSG ADDR                  JDM6
         WTO   MF=(E,(R1))         PUT IT OUT                    JDM6
         BR    R4                                                JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        ROUTINE WILL CONVERT TIME FROM HUNDRETHS OF A SECOND    JDM6*
*        TO THE FORM HH.MM.SS                                    JDM6*
*                                                                JDM6*
*        R15 - POINTS TO OUTPUT AREA (8BYTES)                    JDM6*
*        R1  - HAS VALUE TO BE CONVERTED COMING IN.              JDM6*
*        R0 & R1 USED AS WORK REGS FOR EVEN ODD PAIR             JDM6*
*        R4  - RETURN REGISTER                                   JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
CONVTME  EQU   *                                                 JDM6
         SLR   R0,R0                   ZERO OUT REG              JDM6
         D     R0,=F'360000'           MAKE HOURS                JDM6
         CVD   R1,DBLEWD               CONVERT                   JDM6
         OI    DBLEWD+7,X'0F'          FLIP HALF BYTE            JDM6
         UNPK  0(2,R15),DBLEWD+6(2)    UNPACK IT                 JDM6
         MVI   2(R15),C'.'             MOVE IN PERIOD            JDM6
         SRDL  R0,32                   SLID IT OVER              JDM6
         D     R0,=F'6000'             MAKE MINUTES              JDM6
         CVD   R1,DBLEWD               CONVERT                   JDM6
         OI    DBLEWD+7,X'0F'          FLIP HALF BYTE            JDM6
         UNPK  3(2,R15),DBLEWD+6(2)    UNPACK IT                 JDM6
         MVI   5(R15),C'.'             MOVE IN PERIOD            JDM6
         SRDL  R0,32                   SLID IT OVER              JDM6
         D     R0,=F'100'              MAKE HOURS                JDM6
         CVD   R1,DBLEWD               CONVERT                   JDM6
         OI    DBLEWD+7,X'0F'          FLIP HALF BYTE            JDM6
         UNPK  6(2,R15),DBLEWD+6(2)    UNPACK IT                 JDM6
         MVI   8(R15),C'.'             MOVE IN PERIOD            JDM6
         CVD   R0,DBLEWD               CONVERT HUNDRETHS         JDM6
         OI    DBLEWD+7,X'0F'          FLIP HALF BYTE            JDM6
         UNPK  9(2,R15),DBLEWD+6(2)    UNPACK IT                 JDM6
         BR    R4                                                JDM6
         SPACE 1                                                 JDM6
*----------------------------------------------------------------JDM6*
*        ROUTINE WILL PRINT 1 BYTE MOUNT QUANTITY                JDM6*
*        R15 - POINTS TO OUTPUT AREA (2BYTES)                    JDM6*
*        R1  - HAS VALUE TO BE CONVERTED COMING IN.              JDM6*
*        R4  - RETURN REGISTER                                   JDM6*
*----------------------------------------------------------------JDM6*
         SPACE 1                                                 JDM6
CONMOUNT EQU   *                                                 JDM6
         CVD   R1,DBLEWD                CONVERT TO PACKED        JDM6
         OI    DBLEWD+7,X'0F'                                    JDM6
         UNPK  0(2,R15),DBLEWD+6(2) UNPACK IT                    JDM6
         BR    R4                                                JDM6
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS                                                   *
*--------------------------------------------------------------------*
         SPACE
         DS    0F
WORKSIZE DC    AL1(245),AL3(WORKEND-WORKAREA)
RECPOOL  DC    AL1(245),AL3(000000)
TABADD   DC    A(SMFTAB-IEFU83)
ONE      DC    F'1'
MAXERR   DC    F'20'               20 ERRORS MAX                 JDM4
PATL1    DC    XL4'F0212020'       EDIT PATTERN 1BYTE            JDM4
PATL2    DC    XL6'F02120202020'   EDIT PATTERN 2BYTES           JDM4
PATL4    DC    XL10'F0212020202020202020' PATTERN 4 BYTES        JDM6
S21TABLE DC    240C'/',C'0123456789ABCDEF'
         SPACE
*----------------------------------------------------------------JDM4*
*        WTO'S FOLLOWS, GIVE US SOME NUMBERS FOR DISPLACEMENTS.  JDM4*
*----------------------------------------------------------------JDM4*
* UPPER LINE          1         2         3         4         5  JDM4
*               4567890123456789012345678901234567890123456789012345678
* LOWER LINE    6         7         8         9                  JDM4
*              90123456789012345678901234567890                  JDM4
*----------------------------------------------------------------JDM4*
         DS    0F                                                JDM4
MSG21A   WTO   'ISG0058A V=XXXXXX PLSE CLEAN UNIT=XXX, PROBABLE DIRT ON+
                DRIVE',ROUTCDE=(5),DESC=(2),MF=L                 JDM4
MSG21AL  EQU   *-MSG21A     LENGTH OF MSG                        JDM4
*----------------------------------------------------------------JDM4*
         DS    0F                                                JDM4
MSG21B   WTO   'ISG0059I STATS XXXXXX TR=XXX,TW=XXX,PR=XXX,PW=XXX,NB=XX+
               X,EG=XXXXX,CL=XXXXX,SIO=XXXXX',                   JDM4  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM4
MSG21BL  EQU   *-MSG21B     LENGTH OF MSG                        JDM4
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM6
MSG30A   WTO   '7CG0071I UUUUUUU, CLASS=?, CPUTIME=YY.YY.YY.YY, TAPES=N+
               N; CLASS=? CORRECT FOR CPUTIME USED.',            JDM6  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM6
MSG30AL  EQU   *-MSG30A     LENGTH OF MSG                        JDM6
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM6
MSG30B   WTO   '7CG0072I UUUUUUU, CLASS=?, CPUTIME=YY.YY.YY.YY, TAPES=N+
               N; JOB NOT ANALYZED.',                            JDM6  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM6
MSG30BL  EQU   *-MSG30B     LENGTH OF MSG                        JDM6
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM6
MSG30C   WTO   '7CG0073I UUUUUUU, CLASS=?, CPUTIME=XX.XX.XX.XX, TAPES=N+
               N, CLASS=? SELECTION INCORRECT.',                 JDM6  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM6
MSG30CL  EQU   *-MSG30C     LENGTH OF MSG                        JDM6
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM6
MSG30D   WTO   '7CG0074I UUUUUUU, BASED ON TOTAL CPUTIME, CLASS=? IS TH+
               E CORRECT CHOICE.',                               JDM6  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM6
MSG30DL  EQU   *-MSG30D     LENGTH OF MSG                        JDM6
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM4
MSG30E   WTO   '7CG0075I UUUUUUU, CLASS=?, CPUTIME=XX.XX.XX.XX; JOB NOT+
                ANALYZED, PROBABLE JCL ERROR.',                  JDM6  +
               ROUTCDE=(5),DESC=(3),MF=L                         JDM6
MSG30EL  EQU   *-MSG30E     LENGTH OF MSG                        JDM6
         SPACE 1                                                 JDM6
         LTORG
         SPACE
*----------------------------------------------------------------JDM6*
*        BUILD YOURSELF A TABLE OF CLASSES AND CPUTIME UNITS.    JDM6
*----------------------------------------------------------------JDM6*
         DS    0F                                                JDM6
CLASSTAB EQU   *         MAX SU       UPPER VALUE                JDM6
         DC    CL4'A',F'000001501'    A <    15.01 SECS          JDM6
         DC    CL4'B',F'000006001'    B <    60.01 SECS          JDM6
         DC    CL4'C',F'000030001'    C <   300.01 SECS (05 MIN) JDM6
         DC    CL4'D',F'000180001'    D <  1800.01 SECS (30 MIN) JDM6
         DC    CL4'E',F'000360001'    E <  3600.01 SECS (60 MIN) JDM6
WORSTCL  DC    CL4'F',F'999999991'    F < 72000.00 SECS (120MIN) JDM6
         DC    8X'FF'                  FENCE                     JDM6
         SPACE
*--------------------------------------------------------------------*
*                                                                    *
*        THIS TABLE CONTAINS A BRANCH INDEX VALUE FOR EACH           *
*        SMF RECORD TYPE. THE VALUE IS THEN MULTIPLIED BY 4          *
*        TO GET TO THE PROCESSING PROCESSING CODES (BRANCH           *
*        TABLE.  PRESENTLY WE ARE PROCESSING :                       *
*                                                                    *
*         0   -  RETURN WITH R15=0 AND WRITE THE RECORD              *
*         1   -  RETURN WITH R15=4 AND DO NOT WRITE THE RECORD       *
*         2   -  PROCESS TYPE 14 RECORDS                             *
*         3   -  PROCESS TYPE 15 RECORDS                             *
*         4   -  PROCESS TYPE 40 RECORDS                             *
*         5   -  WAS     TYPE 21 RECORDS                    JDM7 JDM4*
*         6   -  PROCESS TYPE 30 RECORDS                         JDM6*
*--------------------------------------------------------------------*
         SPACE
SMFTAB   CSECT
SMFTYPES DS    0XL256
TYPE0    DC    HL1'0'        WRITE
TYPE1    DC    HL1'0'        WRITE
TYPE2    DC    HL1'0'        WRITE
TYPE3    DC    HL1'0'        WRITE
TYPE4    DC    HL1'0'        WRITE
TYPE5    DC    HL1'0'        WRITE
TYPE6    DC    HL1'0'        WRITE
TYPE7    DC    HL1'0'        WRITE
TYPE8    DC    HL1'0'        WRITE
TYPE9    DC    HL1'0'        WRITE
TYPE10   DC    HL1'0'        WRITE
TYPE11   DC    HL1'0'        WRITE
TYPE12   DC    HL1'0'        WRITE
TYPE13   DC    HL1'0'        WRITE
TYPE14   DC    HL1'2'        *****  PROCESS IT
TYPE15   DC    HL1'3'        *****  PROCESS IT
TYPE16   DC    HL1'0'        WRITE
TYPE17   DC    HL1'0'        WRITE
TYPE18   DC    HL1'0'        WRITE
TYPE19   DC    HL1'0'        WRITE
TYPE20   DC    HL1'0'        WRITE
TYPE21   DC    HL1'5'        *****  PROCESS IT                   JDM4
TYPE22   DC    HL1'0'        WRITE
TYPE23   DC    HL1'0'        WRITE
TYPE24   DC    HL1'0'        WRITE
TYPE25   DC    HL1'0'        WRITE
TYPE26   DC    HL1'0'        WRITE
TYPE27   DC    HL1'0'        WRITE
TYPE28   DC    HL1'0'        WRITE
TYPE29   DC    HL1'0'        WRITE
TYPE30   DC    HL1'6'        *****  PROCESS IT                   JDM6
TYPE31   DC    HL1'0'        WRITE
TYPE32   DC    HL1'0'        WRITE
TYPE33   DC    HL1'0'        WRITE
TYPE34   DC    HL1'0'        WRITE
TYPE35   DC    HL1'0'        WRITE
TYPE36   DC    HL1'0'        WRITE
TYPE37   DC    HL1'0'        WRITE
TYPE38   DC    HL1'0'        WRITE
TYPE39   DC    HL1'0'        WRITE
TYPE40   DC    HL1'4'        *****  PROCESS IT
TYPE41   DC    HL1'0'        WRITE
TYPE42   DC    HL1'0'        WRITE
TYPE43   DC    HL1'0'        WRITE
TYPE44   DC    HL1'0'        WRITE
TYPE45   DC    HL1'0'        WRITE
TYPE46   DC    HL1'0'        WRITE
TYPE47   DC    HL1'0'        WRITE
TYPE48   DC    HL1'0'        WRITE
TYPE49   DC    HL1'0'        WRITE
TYPE50   DC    HL1'0'        WRITE
TYPE51   DC    HL1'0'        WRITE
TYPE52   DC    HL1'0'        WRITE
TYPE53   DC    HL1'0'        WRITE
TYPE54   DC    HL1'0'        WRITE
TYPE55   DC    HL1'0'        WRITE
TYPE56   DC    HL1'0'        WRITE
TYPE57   DC    HL1'0'        WRITE
TYPE58   DC    HL1'0'        WRITE
TYPE59   DC    HL1'0'        WRITE
TYPE60   DC    HL1'0'        WRITE
TYPE61   DC    HL1'0'        WRITE
TYPE62   DC    HL1'0'        WRITE
TYPE63   DC    HL1'0'        WRITE
TYPE64   DC    HL1'0'        WRITE
TYPE65   DC    HL1'0'        WRITE
TYPE66   DC    HL1'0'        WRITE
TYPE67   DC    HL1'0'        WRITE
TYPE68   DC    HL1'0'        WRITE
TYPE69   DC    HL1'0'        WRITE
TYPE70   DC    HL1'0'        WRITE
TYPE71   DC    HL1'0'        WRITE
TYPE72   DC    HL1'0'        WRITE
TYPE73   DC    HL1'0'        WRITE
TYPE74   DC    HL1'0'        WRITE
TYPE75   DC    HL1'0'        WRITE
TYPE76   DC    HL1'0'        WRITE
TYPE77   DC    HL1'0'        WRITE
TYPE78   DC    HL1'0'        WRITE
TYPE79   DC    HL1'0'        WRITE
TYPE80   DC    HL1'0'        WRITE
TYPE81   DC    HL1'0'        WRITE
TYPE82   DC    HL1'0'        WRITE
TYPE83   DC    HL1'0'        WRITE
TYPE84   DC    HL1'0'        WRITE
TYPE85   DC    HL1'0'        WRITE
TYPE86   DC    HL1'0'        WRITE
TYPE87   DC    HL1'0'        WRITE
TYPE88   DC    HL1'0'        WRITE
TYPE89   DC    HL1'0'        WRITE
TYPE90   DC    HL1'0'        WRITE
TYPE91   DC    HL1'0'        WRITE
TYPE92   DC    HL1'0'        WRITE
TYPE93   DC    HL1'0'        WRITE
TYPE94   DC    HL1'0'        WRITE
TYPE95   DC    HL1'0'        WRITE
TYPE96   DC    HL1'0'        WRITE
TYPE97   DC    HL1'0'        WRITE
TYPE98   DC    HL1'0'        WRITE
TYPE99   DC    HL1'0'        WRITE
TYPE100  DC    HL1'0'        WRITE
TYPE101  DC    HL1'0'        WRITE
TYPE102  DC    HL1'0'        WRITE
TYPE103  DC    HL1'0'        WRITE
TYPE104  DC    HL1'0'        WRITE
TYPE105  DC    HL1'0'        WRITE
TYPE106  DC    HL1'0'        WRITE
TYPE107  DC    HL1'0'        WRITE
TYPE108  DC    HL1'0'        WRITE
TYPE109  DC    HL1'0'        WRITE
TYPE110  DC    HL1'0'        WRITE
TYPE111  DC    HL1'0'        WRITE
TYPE112  DC    HL1'0'        WRITE
TYPE113  DC    HL1'0'        WRITE
TYPE114  DC    HL1'0'        WRITE
TYPE115  DC    HL1'0'        WRITE
TYPE116  DC    HL1'0'        WRITE
TYPE117  DC    HL1'0'        WRITE
TYPE118  DC    HL1'0'        WRITE
TYPE119  DC    HL1'0'        WRITE
TYPE120  DC    HL1'0'        WRITE
TYPE121  DC    HL1'0'        WRITE
TYPE122  DC    HL1'0'        WRITE
TYPE123  DC    HL1'0'        WRITE
TYPE124  DC    HL1'0'        WRITE
TYPE125  DC    HL1'0'        WRITE
TYPE126  DC    HL1'0'        WRITE
TYPE127  DC    HL1'0'        WRITE
TYPE128  DC    HL1'0'        WRITE
TYPE129  DC    HL1'0'        WRITE
TYPE130  DC    HL1'0'        WRITE
TYPE131  DC    HL1'0'        WRITE
TYPE132  DC    HL1'0'        WRITE
TYPE133  DC    HL1'0'        WRITE
TYPE134  DC    HL1'0'        WRITE
TYPE135  DC    HL1'0'        WRITE
TYPE136  DC    HL1'0'        WRITE
TYPE137  DC    HL1'0'        WRITE
TYPE138  DC    HL1'0'        WRITE
TYPE139  DC    HL1'0'        WRITE
TYPE140  DC    HL1'0'        WRITE
TYPE141  DC    HL1'0'        WRITE
TYPE142  DC    HL1'0'        WRITE
TYPE143  DC    HL1'0'        WRITE
TYPE144  DC    HL1'0'        WRITE
TYPE145  DC    HL1'0'        WRITE
TYPE146  DC    HL1'0'        WRITE
TYPE147  DC    HL1'0'        WRITE
TYPE148  DC    HL1'0'        WRITE
TYPE149  DC    HL1'0'        WRITE
TYPE150  DC    HL1'0'        WRITE
TYPE151  DC    HL1'0'        WRITE
TYPE152  DC    HL1'0'        WRITE
TYPE153  DC    HL1'0'        WRITE
TYPE154  DC    HL1'0'        WRITE
TYPE155  DC    HL1'0'        WRITE
TYPE156  DC    HL1'0'        WRITE
TYPE157  DC    HL1'0'        WRITE
TYPE158  DC    HL1'0'        WRITE
TYPE159  DC    HL1'0'        WRITE
TYPE160  DC    HL1'0'        WRITE
TYPE161  DC    HL1'0'        WRITE
TYPE162  DC    HL1'0'        WRITE
TYPE163  DC    HL1'0'        WRITE
TYPE164  DC    HL1'0'        WRITE
TYPE165  DC    HL1'0'        WRITE
TYPE166  DC    HL1'0'        WRITE
TYPE167  DC    HL1'0'        WRITE
TYPE168  DC    HL1'0'        WRITE
TYPE169  DC    HL1'0'        WRITE
TYPE170  DC    HL1'0'        WRITE
TYPE171  DC    HL1'0'        WRITE
TYPE172  DC    HL1'0'        WRITE
TYPE173  DC    HL1'0'        WRITE
TYPE174  DC    HL1'0'        WRITE
TYPE175  DC    HL1'0'        WRITE
TYPE176  DC    HL1'0'        WRITE
TYPE177  DC    HL1'0'        WRITE
TYPE178  DC    HL1'0'        WRITE
TYPE179  DC    HL1'0'        WRITE
TYPE180  DC    HL1'0'        WRITE
TYPE181  DC    HL1'0'        WRITE
TYPE182  DC    HL1'0'        WRITE
TYPE183  DC    HL1'0'        WRITE
TYPE184  DC    HL1'0'        WRITE
TYPE185  DC    HL1'0'        WRITE
TYPE186  DC    HL1'0'        WRITE
TYPE187  DC    HL1'0'        WRITE
TYPE188  DC    HL1'0'        WRITE
TYPE189  DC    HL1'0'        WRITE
TYPE190  DC    HL1'0'        WRITE
TYPE191  DC    HL1'0'        WRITE
TYPE192  DC    HL1'0'        WRITE
TYPE193  DC    HL1'0'        WRITE
TYPE194  DC    HL1'0'        WRITE
TYPE195  DC    HL1'0'        WRITE
TYPE196  DC    HL1'0'        WRITE
TYPE197  DC    HL1'0'        WRITE
TYPE198  DC    HL1'0'        WRITE
TYPE199  DC    HL1'0'        WRITE
TYPE200  DC    HL1'0'        WRITE
TYPE201  DC    HL1'0'        WRITE
TYPE202  DC    HL1'0'        WRITE
TYPE203  DC    HL1'0'        WRITE
TYPE204  DC    HL1'0'        WRITE
TYPE205  DC    HL1'0'        WRITE
TYPE206  DC    HL1'0'        WRITE
TYPE207  DC    HL1'0'        WRITE
TYPE208  DC    HL1'0'        WRITE
TYPE209  DC    HL1'0'        WRITE
TYPE210  DC    HL1'0'        WRITE
TYPE211  DC    HL1'0'        WRITE
TYPE212  DC    HL1'0'        WRITE
TYPE213  DC    HL1'0'        WRITE
TYPE214  DC    HL1'0'        WRITE
TYPE215  DC    HL1'0'        WRITE
TYPE216  DC    HL1'0'        WRITE
TYPE217  DC    HL1'0'        WRITE
TYPE218  DC    HL1'0'        WRITE
TYPE219  DC    HL1'0'        WRITE
TYPE220  DC    HL1'0'        WRITE
TYPE221  DC    HL1'0'        WRITE
TYPE222  DC    HL1'0'        WRITE
TYPE223  DC    HL1'0'        WRITE
TYPE224  DC    HL1'0'        WRITE
TYPE225  DC    HL1'0'        WRITE
TYPE226  DC    HL1'0'        WRITE
TYPE227  DC    HL1'0'        WRITE
TYPE228  DC    HL1'0'        WRITE
TYPE229  DC    HL1'0'        WRITE
TYPE230  DC    HL1'0'        WRITE
TYPE231  DC    HL1'0'        WRITE
TYPE232  DC    HL1'0'        WRITE
TYPE233  DC    HL1'0'        WRITE
TYPE234  DC    HL1'0'        WRITE
TYPE235  DC    HL1'0'        WRITE
TYPE236  DC    HL1'0'        WRITE
TYPE237  DC    HL1'0'        WRITE
TYPE238  DC    HL1'0'        WRITE
TYPE239  DC    HL1'0'        WRITE
TYPE240  DC    HL1'0'        WRITE
TYPE241  DC    HL1'0'        WRITE
TYPE242  DC    HL1'0'        WRITE
TYPE243  DC    HL1'0'        WRITE
TYPE244  DC    HL1'0'        WRITE
TYPE245  DC    HL1'0'        WRITE
TYPE246  DC    HL1'0'        WRITE
TYPE247  DC    HL1'0'        WRITE
TYPE248  DC    HL1'0'        WRITE
TYPE249  DC    HL1'0'        WRITE
TYPE250  DC    HL1'0'        WRITE
TYPE251  DC    HL1'0'        WRITE
TYPE252  DC    HL1'0'        WRITE
TYPE253  DC    HL1'0'        WRITE
TYPE254  DC    HL1'0'        WRITE
TYPE255  DC    HL1'0'        WRITE
         SPACE 1
WORKAREA DSECT
SAVE     DS    18F
DBLEWD   DS    D                                                 JDM4
NUMERRS  DS    F                                                 JDM4
AREA     DS    CL12                    EDIT AREA            JDM6 JDM4
T30TCPUU DS    F                       TOTAL CPU TIME USER SPEC  JDM6
T30JNAME DS    CL8                     TOTAL TAPE MOUNTS  USER   JDM6
T30MOUNT DS    X                       TOTAL TAPE MOUNTS  USER   JDM6
T30CLASU DS    C                       JOB CLASS OF USER         JDM6
T30CLAST DS    C                       JOB CLASS OF TABLE        JDM6
         DS    0F                                                JDM4
WTOAREA  DS    CL130                                        JDM6 JDM4
WORKEND  EQU   *
         SPACE
         DS    0D
SMFREC   DSECT
         IFASMFR 14
         ORG   SMFJFCB1
         IEFJFCBN LIST=YES
         ORG   SMFREC
         IFASMFR 21                                              JDM4
         ORG   SMFREC
         IFASMFR 40
         SPACE
         IFASMFR 30                                              JDM6
         END
