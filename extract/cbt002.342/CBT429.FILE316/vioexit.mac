         TITLE 'ACF2 VIOLATION EXIT'
*---------------------------------------------------------------------*
*                                                                     *
*       ACF2 VIOLATION EXIT. THE BASE FOR THE CODE CAME FROM          *
*       CHRIS SENKO, HUGHES AIRCRAFT. THE GOALS OF THE EXIT ARE:      *
*                                                                     *
*       1. ALLOW USER TO SPECIFY ANY DATASET NAME FOR TMS FOREIGN     *
*          TAPES AND NOT GET SECURITY VIOLATIONS.                     *
*       2. ALLOW USERS BLP PROCESSING AGAINST OTHER THAN TMS CODED    *
*          TAPES (VOLSER CODED IN JCL) & NOT GET SECURITY VIOLATION.  *
*          YOU CAN CHOOSE TO "LOG" THE ACCESS TO ACF2 OR NOT. THIS    *
*          DOES RETURN RC=12 (ALLOW & LOG THE ACCESS).                *
*       3. IN THIS IMPLEMENTATION, EXIT WILL NOT PROTECT UNIVERSAL    *
*          FOREIGN TAPES CALL T-TAPES (TRANSIT TAPES).                *
*       4. IN YOUR JES2 PARMS YOU NEED TO ALLOW BLP ON THE INITS.     *
*       5. LASTLY, YOU WILL DENY BLP USE TO EVERYONE AS A DEFAULT     *
*          SO THE EXIT GETS CONTROL.                                  *
*                                                                     *
*          JIM MARSHALL, GRUMMAN DATA SYSTEMS (WRITTEN FOR CUSTOMER)  *
*                                                                     *
*          CHANGE LOG: NONE                                           *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0                   UNUSED
R1       EQU   1                   ADDR OF ACSXP AT ENTRY
R2       EQU   2                   UNUSED
R3       EQU   3                   UNUSED
R4       EQU   4                   UNUSED
R5       EQU   5                   SAVED ADDR OF ACSXP
R6       EQU   6                   WORK REG
R7       EQU   7                   UNUSED
R8       EQU   8                   UNUSED
R9       EQU   9                   UNUSED
R10      EQU   10                  UNUSED
R11      EQU   11                  UNUSED
R12      EQU   12                  BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
VIOEXIT  CSECT
         GBLC  &SYSSPLV            GLOBAL FOR SPLEVEL
         SPLEVEL TEST              GET CURRENT SP LEVEL OF SYSTEM
         AIF   ('&SYSSPLV' EQ '1').MVSSP  SKIP AMODE/RMODE IF MVS/SP
VIOEXIT  AMODE 31
VIOEXIT  RMODE ANY
.MVSSP   ANOP
         SAVE  (14,12),,VIOEXIT_&SYSDATE
         LR    R12,R15             SAVE ENTRY ADDR
         USING VIOEXIT,R12         SET THE BASE
         LR    R5,R1               SAVE OFF R1
         USING ACSXP,R5            GET ADDRESSABILITY
         SPACE
*-------------------------------------------------------------------*
*        SEE IF ABNORMAL END IN PROGRESS DUE TO UNKNOWN ERR CONDITION
*-------------------------------------------------------------------*
         SPACE
         TM    ACUUFLG1,ACUU1ABE   SVC DUMP IN PROGRESS
         BO    RET0                STAY OUT OF ITS WAY
         SPACE
*-------------------------------------------------------------------*
*        SEE HOW WE WERE ENTERED, IF IT WAS NOT FOR BLP VIOLATION
*        THEN CONTINUE THE DENIAL.
*-------------------------------------------------------------------*
         SPACE
         TM    ACUUFLG2,ACUU2BLP   BLP VIOLATION ?
         BNO   RET0                IF NOT, ABORT PROCESSING
         SPACE
*-------------------------------------------------------------------*
*        NOW WE RUN THROUGH TAPE OPEN PROBLEMS. ACF2 MANUAL STATES WE
*        MUST USE 'CLI' INSTRUCTION FOR ACUUFLG3 !!
*-------------------------------------------------------------------*
         SPACE
         TM    ACUUFLG1,ACUU1TDS   RULE ALREADY FOR TAPE ????
         BO    CHKTAPE             GO PROCESS TAPE
         CLI   ACUUFLG3,ACUU3TPO   TAPE OPEN (NOT MOUNTED)
         BE    CHKTAPE             GO PROCESS TAPE
         CLI   ACUUFLG3,ACUU3TPE   TAPE EOV (NOT MOUNTED)
         BE    CHKTAPE             GO PROCESS TAPE
         CLI   ACUUFLG3,ACUU3TPX   TAPE OPEN (MOUNTED)
         BE    CHKTAPE             GO PROCESS TAPE
         CLI   ACUUFLG3,ACUU3TOX   TAPE OPEN (AFTER MOUNT)
         BE    CHKTAPE             GO PROCESS TAPE
         CLI   ACUUFLG3,ACUU3TEX   TAPE EOV (AFTER NEW VOL MOUNT)
         BE    CHKTAPE             GO PROCESS TAPE
         B     RET0                GIVE UP, NOT OUR BUSINESS
         SPACE
*-------------------------------------------------------------------*
*        NOW CHECK FOR NON-SPECIFIC TAPE MOUNT
*        CONTINUE PROCESSING.
*-------------------------------------------------------------------*
         SPACE
CHKTAPE  EQU   *
         L     R6,ACUUVOL          GET ADDR OF 8 BYTE AREA
         CLC   0(6,R6),=C'SCRTCH'  IS IT A SCRATCH MOUNT?
         BE    RET0                CONTINUE WITH VIOLATION
         CLC   0(6,R6),=C'PRIVAT'  IS IT A SCRATCH MOUNT?
         BE    RET0                CONTINUE WITH VIOLATION
         SPACE
*--------------------------------------------------------------------*
*        NOW WE PROCESS SEE IF TAPE IS VOLSER BEGINS WITH A "T". THIS
*        IS WHERE YOU CAN GET FANCY WITH THE CHECKS. FOR EXAMPLE, HAVE
*        SEEN PEOPLE CHECK FOR ALL SERIES OF TAPES USED IN TMS'S AT
*        THERE DATA CENTER AND ALL ACCESS TO ALL OTHERS IS ALLOWED.
*        CHECK BELOW COULD HAVE FURTHERED CHECKED FOR "T" FOLLOWED
*        BY 5 NUMBERS. HAVE FUN (IT IS CALLED FEXIBILITY).
*--------------------------------------------------------------------*
         SPACE
         CLI   0(R6),C'T'          IS 1ST CHARACTER A 'T' ?
         BE    RET12               IF A 'T', THEN IS OK
         B     RET0                OTHERWISE IS A VIOLATION
         SPACE
*--------------------------------------------------------------------*
*        GO BACK WITH RC=0   (CONT VIOLATION PROCESSING)
*--------------------------------------------------------------------*
         SPACE
RET0     EQU   *
         RETURN (14,12),RC=0
         SPACE
*--------------------------------------------------------------------*
*        GO BACK WITH RC=4   (PERMIT ACCESS WITH NO LOGGING)
*--------------------------------------------------------------------*
         SPACE
RET4     EQU   *
         RETURN (14,12),RC=4
         SPACE
*--------------------------------------------------------------------*
*        GO BACK WITH RC=12  (PERMIT ACCESS WITH LOGGING)
*--------------------------------------------------------------------*
         SPACE
RET12    EQU   *
         RETURN (14,12),RC=12
         SPACE
*--------------------------------------------------------------------*
*        GO BACK WITH RC=36  (WARN USER, ALLOW ACCESS, AND LOG)
*--------------------------------------------------------------------*
         SPACE
RET36    EQU   *
         SPACE
         RETURN (14,12),RC=36
         SPACE
*--------------------------------------------------------------------*
*        DATA AREA, LTORG POOL, AND ANY OTHER TRASH
*--------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
         ACSXP                     ACF2 DSECT
         SPACE
         ACSUCB                    ACF2 DSECT
         SPACE
         ACCVT                     ACF2 DSECT
         SPACE
         END
