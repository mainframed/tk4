         TITLE '   T O D       '
*---------------------------------------------------------------------*
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. FEDERAL POWER COMMISSION.
*        DATE WRITTEN. JAN 31 1977.
*        DATE UPDATED.
*        ATTRIBUTES. RE-ENTRANT.
*
* CHNGE LOG:  7JUL90 - CLEANED UP THE CODE  JIM MARSHALL
*---------------------------------------------------------------------*
         SPACE
TOD      CSECT
         SAVE  (14,12),,&SYSDATE
         LR    R12,15
         USING TOD,R12
         GETMAIN R,LV=MYDSECTL
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R14,R13
         LR    R13,R1
         L     R1,24(,R14)             RESTORE R1
         USING MYDSECT,R13
         SPACE 1
         LA    R0,MODULE
         SPACE
         LOAD  EPLOC=(0)
         SPACE
         ST    R0,ENTRY
         SPACE
         LA    R9,3
TODLOOP  EQU   *
         LA    R15,16
         LA    R15,0
         ST    R15,TIMEZONE
         SPACE
         STCK  CLOCK
         SPACE
         MVI   ANSWER,C' '
         MVC   ANSWER+1(L'ANSWER-1),ANSWER
         SPACE
         MVI   OPTION,1
         SPACE
         LA    R1,PARAM
         LA    R15,TIMEZONE
         ST    R15,0(,R1)
         LA    R15,CLOCK
         ST    R15,4(,R1)
         LA    R15,ANSWER
         ST    R15,8(,R1)
         LA    R15,=X'01'
         ST    R15,12(,R1)
         LA    R15,WORKA
         ST    R15,16(,R1)
         SPACE
         L     R15,ENTRY
         BALR  R14,R15
         SPACE
         LA    R1,ANSWER
         LA    R0,L'ANSWER
         SVC   93
         SPACE
         BCT   R9,TODLOOP
         SPACE
         LA    R0,MODULE
         SPACE
         DELETE EPLOC=(0)
         SPACE 1
EXIT0    SR    R15,R15
EXIT     LR    R1,R13
         L     R13,4(,R13)
         ST    R15,16(,R13)
         FREEMAIN R,A=(1),LV=MYDSECTL
         LM    R14,R12,12(R13)
         BR    R14
         SPACE
*---------------------------------------------------------------------*
*        CONSTANTS
*---------------------------------------------------------------------*
         SPACE
MODULE   DC    CL8'TODCNVRT'
         LTORG
         SPACE
*---------------------------------------------------------------------*
*        DSECTS
*---------------------------------------------------------------------*
         SPACE
MYDSECT  DSECT
         DS    18F                 REGISTER SAVEAREA
ENTRY    DS    F
PARAM    DS    5F
CLOCK    DS    D
TIMEZONE DS    F
ANSWER   DS    CL64
OPTION   DS    C
WORKA    DS    10D
MYDSECTL EQU   *-MYDSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
