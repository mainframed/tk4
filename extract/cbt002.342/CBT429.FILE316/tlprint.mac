         TITLE ' T L P R I N T '
*--------------------------------------------------------------------*
*                                                                    *
*        TLPRINT, A SUBROUTINE TO PRINT INFORMATION FROM TAPE LABELS *
*                                                                    *
* COURTESY:  JIM MARSHALL, CAPT, USAF-RET                            *
*                                                                    *
* WRITTEN: MR FRANK YATES, AEROSPACE CORP WHILE AT MULTIPURPOSE      *
*          FACILITY, LOWRY AFB, CO  FOR USE IN OS/MVT                *
*                                                                    *
* NOTE:    THIS ROUTINE PRINTS A PERCENTAGE OF VOLUME USED. THE      *
*          FIGURE IS BASED ON A 2400 FT REEL RECORDED AT 1600BPI     *
*          WILL ATTEMPT TO CHANGE IT FOR ALL DENSITIES LATER         *
*                                                                    *
* CHANGE LOG:                                                        *
*                                                                    *
*     11 OCT 81 - CLEANED UP THE CODE.  JDM                          *
*     19 MAY 82 - CONTINUED CLEANUP AND MESS-UP CODE WHEN I          *
*                 REPLACED FRANK'S MACROS WITH IBM STANDARD.         *
*                 R1 SHOULD REMAIN UNCHANGED IN THE RETURN.      JDM *
*     30 OCT 86 - CHECKED OUT ROUTINE ON IBM 3480. IF YOU ARE        *
*                 RUNNING THE DRIVES IN 3420 COMPATIBILITY MODE      *
*                 THE DENSITY WILL BE DEN=4. IF YOU ARE RUNNING      *
*                 IN FULL FUNCTION MODE THEN DEN=0. SO WE WILL       *
*                 CHECK FOR DEN=0 AND IF SO, NEGATE THE PRT      JDM1*
*                                                                    *
*-$DOC$--------------------------------------------------------------*
         SPACE
         MACRO
&NAME    FETCH &WHAT
&NAME    LA    L,60(L)         ADVANCE LABEL POINTER.
         CLC   =C'&WHAT',0(L)  TEST LABEL.
         BE    FQ&SYSNDX
         MVC   MISLABEL,=CL4'&WHAT'
         B     ERROR1
FQ&SYSNDX EQU *
         MEND
         SPACE
         MACRO
&NAME    OUT   &LINE
&NAME    LA    R0,&LINE        R0 = ADDRESS OF LINE TO BE PRINTED
         BAL   R9,PRINT        LINK TO THE PRINT SUBROUTINE.
         MEND
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
TLPRINT  AMODE 24
TLPRINT  RMODE 24
TLPRINT  CSECT
         SAVE  (14,12),,TLPRINT_&SYSDATE &SYSTIME
         LR    R12,R15
         LA    R11,4095(R12)
         LA    R11,1(R11)
         USING TLPRINT,R12,R11     SET BASE
         LR    R15,R13
         LA    R13,SAVE
         ST    R15,4(R13)
         ST    R13,8(R15)
         L     L,0(1)              SET LABEL POINTER.
         S     L,=F'60'
         MVC   CLOSE+1(1),0(1)     SET CLOSING SWITCH.
         TM    OUT+48,X'10'        SEE IF DCB IS ALREAY OPEN.
         BO    Q5
         OPEN  (OUT,OUTPUT)
Q5       EQU   *
         L     COUNT,MAXCOUNT      SET FILE COUNTER TO PRINT HEADER.
         MVC   TOTALPCT,=F'0'      RESET VOLUME USE PERCENTAGE.
         SPACE
*--------------------------------------------------------------------*
*        COMPLETE THE HEADER.                                        *
*--------------------------------------------------------------------*
         TIME  DEC                 TIME PLEASE
         STM   0,1,WORK            SAVE IT ALL OFF
         MVC   DATE,=X'40202061202120'  MOVE IN EDIT PATTERN
         ED    DATE,WORK+5         EDIT THE DATE
         UNPK  TIME(3),WORK(2)     NOW WE
         MVI   TIME+2,C'.'               DO
         UNPK  TIME+3(3),WORK+1(2)         THE
         MVI   TIME+5,C' '                    TIME
         LA    L,60(L)             ADVANCE LABEL POINTER
         CLC   =C'VOL1',0(L)       TEST FOR VOL1 HEADER
         BE    SK1
         MVC   MISLABEL,=CL4'VOL1' TELL USER IT'S NOT SL
         MVI   MSG1,C'1'
         B     ERROR1
SK1      EQU   *
         MVC   SERIAL,4(L)         COPY VOLUME SERIAL NO.
         MVC   OWNER,41(L)         COPY OWNER ID.
         SPACE
LOOP     EQU   *
         LA    COUNT,1(COUNT)      UPDATE COUNT OF FILES ON THIS PAGE.
         FETCH HDR1                GET FIRST HEADER LABEL.
         MVC   FILENO,31(L)        COPY FILE NO.
         OUT   LINE1               PRINT LINE 1.
         MVC   DSN,4(L)            COPY DSN.
         MVC   CRDATE,42(L)        COPY CREATION DATE.
         MVC   EXDATE,48(L)        COPY EXPIRATION DATE.
         FETCH HDR2                GET SECOND HEADER LABEL.
         MVC   CREATOR(8),17(L)    WHO CREATED THE TAPE
         MVC   RECFM(1),4(L)       COPY DCB PARAMETERS.
         CLI   15(L),C'0'          3480 FULL FUNCTION MODE ?     JDM1
         BNE   NOT3480F                                          JDM1
         MVC   DENSPEC(7),=CL7' '  BLANK OUT WORDS               JDM1
         B     IS3480F                                           JDM1
NOT3480F EQU   *                                                 JDM1
         MVC   DEN(1),15(L)        COPY DEN NO.
IS3480F  EQU   *                                                 JDM1
         MVC   RECFM+1(1),38(L)
         MVC   RECFM+2(1),36(L)
         MVC   LRECL,10(L)         MOVE IN LRECL
         MVC   BLKSIZE,5(L)        MOVE IN BLKSIZE
         OUT   LINE2               PRINT LINE 2.
         FETCH EO                  GET FIRST TRAILER LABEL.
         MVC   BLOCKS,54(L)        COPY NO. OF BLOCKS.
         MVC   VOLSEQNO,27(L)      COPY VOLUME SEQUENCE NO.
         BAL   10,COMPUTE          COMPUTE %  OF VOLUME USED BY FILE
         OUT   LINE3               PRINT LINE 3.
         CLC   VOLSEQNO,=C'0001'   TEST FOR NOT VOLUME 1.
         BNE   Q1
         CLI   2(L),C'V'           TEST FOR END OF VOLUME LABEL.
         BNE   Q2
Q1       EQU   *
         OUT   MSG2                PRINT MESSAGE 2.
Q2       EQU   *
         CLI   60(L),0             TEST FOR LAST LABEL.
         BNE   LOOP
         SPACE
LASTFILE EQU   *
         L     R1,TOTALPCT
         C     R1,=F'10000'        COMPARE WITH 100 PERCENT.
         BL    Q3
         L     R1,=F'10000'        LIMIT TOTAL TO 100 PERCENT.
Q3       EQU   *
         CVD   R1,WORK
         MVC   TOTPCT,=X'402021204B2020'
         ED    TOTPCT,WORK+5
         OUT   LASTLINE            PRINT VOLUME SUMMARY LINE.
CLOSE    EQU   *
         B     Q6                  SWITCH CONTROLLED BY CALLING PGM
         CLOSE OUT
         FREEPOOL OUT
Q6       EQU   *
         LA    R1,OUT              REG 1 = ADDRESS OF DCB
         L     R13,4(R13)
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         LA    R15,0(0,0)
         BR    R14
         SPACE
PRINT    EQU   *
         C     COUNT,MAXCOUNT      TEST NO. OF FILES ALREADY ON PAGE
         BH    PAGE                START A NEW PAGE WHEN NECESSARY.
         PUT   OUT                 PRINT THE LINE.
         BR    R9                  RETURN
PAGE     EQU   *
         LR    R2,R0               SAVE REG 0.
         PUT   OUT,HEADER
         LA    COUNT,1             RESET FILE COUNT FOR A NEW PAGE.
         LR    R0,R2
         B     PRINT
         SPACE
*-------------------------------------------------------------------*
*        COMPUTE PERCENT OF VOLUME USED BY A FILE.                  *
*-------------------------------------------------------------------*
COMPUTE  EQU *
         PACK  WORK,BLKSIZE
         CVB   R1,WORK             REG 1 = BLKSIZE
         SRL   R1,4                DIVIDE BY 16.
         LA    R1,70(1)            ADD 70.
         PACK  WORK,BLOCKS
         CVB   R14,WORK            REG 14 = NO. OF BLOCKS
         MR    R0,14
         LA    R1,1400(1)          ADD 5*280.
         D     R0,=F'280'          REG 1 = 100*(PERCENT OF VOLUME)
         C     R1,=F'10000'        COMPARE WITH 100 PERCENT.
         BL    Q4
         L     R1,=F'10000'        LIMIT RESULT TO 100 PERCENT.
Q4       EQU   *
         CVD   R1,WORK
         A     R1,TOTALPCT         ACCUMULATE TOTAL FOR ALL FILES.
         ST    R1,TOTALPCT
         MVC   FILEPCT,=X'402021204B2020'
         ED    FILEPCT,WORK+5
         BR    R10                 RETURN.
         SPACE
ERROR1   EQU   *
         OUT   MSG1                PRINT MESSAGE 1.
         MVI   MSG1,C'0'
         B     CLOSE
         SPACE
*---------------------------------------------------------------------*
*        CONSTANTS DEFINED BELOW                                      *
*---------------------------------------------------------------------*
OUT      DCB   DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=120,BLKSIZE=120,      X
               DDNAME=TAPELOUT
WORK     DC    D'0'
MAXCOUNT DC    F'7'            MAX NO. OF FILES PER PAGE
TOTALPCT DC    F'0'            100*(TOTAL %  OF VOLUME USED SO FAR)
SAVE     DC    18F'0'          SAVE AREA
L        EQU   8               REG 8 = LABEL POINTER
COUNT    EQU   7               REG 7 = COUNT OF FILES PER PAGE
         SPACE
*--OUTPUT FORMATS--***************************************************
         SPACE
HEADER   DC    C'1',13C'*',C' INFORMATION FROM LABELS OF TAPE NO. '
SERIAL   DC    C'XXXXXX',C' ON DATE'
DATE     DC    C' YY/DDD',C' AT TIME '
TIME     DC    C'HH.MM ',13C'*',C'   OWNER. '
OWNER    DC    CL10' '
***********************************************************************
LINE1    DC    C'-   FILE NO. '
FILENO   DC    C'XXXX',CL103' '
***********************************************************************
LINE2    DC    CL4'0',C'DSN='
DSN      DC    CL17' ',CL3' ',C'CREATED '
CRDATE   DC    C'YYDDD',C' BY '
CREATOR  DC    CL8' ',C'   EXPIRES '
EXDATE   DC    C'YYDDD',CL1' ',C'RECFM='
RECFM    DC    C'XXX',C'   LRECL='
LRECL    DC    C'XXXXX',C'   BLKSIZE='
BLKSIZE  DC    C'XXXXX'                                          JDM1
DENSPEC  DC    C'   DEN='                                        JDM1
DEN      DC    CL4' '
***********************************************************************
LINE3    DC    CL8'0',C'THERE ARE '
BLOCKS   DC    C'XXXXXX',CL38' BLOCKS IN THIS FILE.'
         DC    C'THIS FILE OCCUPIES'
FILEPCT  DC    C' XXX.XX',CL33' PERCENT OF THE VOLUME.'
***********************************************************************
LASTLINE DC    C'-',13C'*'
TOTPCT   DC    C' XXX.XX'
         DC    CL99' PERCENT OF THIS VOLUME HAS BEEN USED.'
***********************************************************************
MSG1     DC    CL8'0',C'***THE '
MISLABEL DC    C'XXXX',CL101' LABEL WAS NOT FOUND.'
***********************************************************************
MSG2     DC    CL8'0',C'***THIS IS VOLUME '
VOLSEQNO DC    C'XXXX',CL90' OF A MULTI-VOLUME DATA SET.'
***********************************************************************
         LTORG
         END
