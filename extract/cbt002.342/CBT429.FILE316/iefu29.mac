         TITLE 'SMF -  I E F U 2 9   EXIT'
*-----------------------------------------------------------------*
*               I   E   F    U    2    9                          *
*                                                                 *
*                                 JIM MARSHALL, CAPT, USAF (RET)  *
*                                                                 *
* DESCRIPTION: STANDARD SMF EXIT WHICH GETS CONTROL WHEN THE      *
*              THE SYSTEM DETECTS 'IEE362A' MESSAGE WHEN A        *
*              SWITCH OF THE RECORDING DATASETS HAS OCCURRECD.    *
*              WE ISSUE AN ENQ WITH QNAME=SFIQUEUE AND            *
*              RNAME=DUMPSMF. IN THIS EXIT WE TEST TO SEE IF      *
*              IT IS ACTIVE, IF SO, BYPASS THE ISSUE OF THE       *
*              STC.                                               *
*                                                                 *
*   PROCESSOR: ASM
*                                                                 *
* ATTRIBUTES : KEY=0, REENTRANT, SUPERVISOR STATE, ENABLED,       *
*              AND NO LOCKS HELD.                                 *
*                                                                 *
*    INPUT   : REGISTER 1 POINTS TO FULLWORD ADDRESS OF DATASET   *
*              NAME (SYS1.MANX/SYS1.MANY) TO BE DUMPED. NOT       *
*              REALLY USED HERE SINCE THE STC DUMPS BOTH.         *
*                                                                 *
*    RET CODE: WE RETURN 0 ALWAYS, BUT A RET CODE OF FOUR WILL    *
*              SUPPRESS THE IEE362A MSG ( GOOD IN MVS/SE ONLY,    *
*              RET CODE DOES NOT EFFECT MVS ).                    *
*                                                                 *
*   INSTALLED:  10 MAY 1982  (IN TESTING)                         *
*                                                                 *
*  CHANGE LOG:  NONE YET                                          *
*-----------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1             POINTER AT ENTRY
R2       EQU   2             NOT USED
R3       EQU   3             NOT USED
R4       EQU   4             NOT USED
R5       EQU   5             NOT USED
R6       EQU   6             NOT USED
R7       EQU   7             NOT USED
R8       EQU   8             NOT USED
R9       EQU   9             NOT USED
R10      EQU   10            NOT USED
R11      EQU   11            NOT USED
R12      EQU   12            BASE REGISTER
R13      EQU   13            BASE TO WKAREA DSECT
R14      EQU   14            WORK REGISTER
R15      EQU   15            WORK REGISTER
         EJECT
IEFU29   CSECT
         SAVE  (14,12),,IEFU29_AFDSC_&SYSDATE_&SYSTIME
         LR    R12,R15       SAVE ENTRY ADDRESS
         USING IEFU29,R12    ADDRESSABILITY WITH NEW BASE
         USING WKAREA,R13    GAIN ADDRESSABILITY TO DSECT
         L     R0,GETAREA    SETUP FOR GETMAIN
         GETMAIN R,LV=(0)    DO IT !!!!
         ST    R13,4(R1)     STANDARD
         ST    R1,8(R13)        LINKAGE
         LR    R13,R1             CONVENTIONS
         SPACE
         MVC   ENQLIST(ENQLISTL),ENQLSTX
         ENQ   MF=(E,ENQLIST)     SEE IF ENQ IS ACTIVE ??
         LTR   R15,R15            TEST RESULTS
         BNZ   RETURN             IF SO, TERMINATE
         MVC   CMDAREA(CMDL),CMD  SETUP CMD IN GETMAINED AREA
         XR    R0,R0              WANT TO DO OPER CMD
         LA    R1,CMDAREA         POINT TO THE CMD
         SVC   34                 ISSUE THE CMD
         SPACE
RETURN   EQU   *
         LR    R1,R13              POINT TO GETMAINED AREA
         L     R0,GETAREA          SETUP FOR FREEMAIN
         L     R13,4(R13)          SAVE ADDR TO SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   DO IT
         RETURN (14,12),RC=0       SO LONG FOR NOW
         EJECT
*---------------------------------------------------------------------*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         SPACE
QNAME    DC    CL8'SFIQUEUE'       MAJOR NAME
RNAME    DC    CL7'DUMPSMF'        MINOR NAME
         SPACE
         DS    0F                  FORCE FULL WORD BOUNDARY
CMD      WTO   'S $SMISMF         IEFU29',ROUTCDE=(2),DESC=(5),MF=L
         DS    0D                  GIVE A LITTLE EXTRA ROOM
CMDL     EQU   *-CMD
         DS    0D                  FORCE DBLE WORD BOUNDARY
         SPACE
GETAREA  DC    0F'0',AL1(000),AL3(WKAREAL) SP=0 AND LENGTH FOR GETMAIN
         SPACE
ENQLSTX  ENQ   (QNAME,RNAME,E,,SYSTEM),RET=TEST,MF=L
         SPACE
*--------------------------------------------------------------------*
*        DEFINE DSECT                                                *
*--------------------------------------------------------------------*
         SPACE
         DS    0F
WKAREA   DSECT
SAVEAREA DS    18F
CMDAREA  DS    0F,XL(CMDL)
ENQLIST  ENQ   (QNAME,RNAME,E,,SYSTEM),RET=TEST,MF=L
ENQLISTL EQU   *-ENQLIST
FILLER   DS    4D
WKAREAL  EQU   *-WKAREA
         END
