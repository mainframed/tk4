         TITLE 'TMSLABL - LABEL T-TAPES'
*---------------------------------------------------------------------*
*
* COURTESY: JIM MARSHALL, CAPT, USAF-RET
*
*        THIS PROGRAM WAS WRITTEN FOR THE SADSC TAPE LIBRARIAN SO
*        THEY CAN PUT LABELS ONTO OUR FOREIGN TAPES. THEY ARE PROMPTED
*        FOR THE VOLSER AND CHECKED, THEN WE BUILD THE CONTROL CARDS
*        TO 'TMSTPNIT'. WE GO  TO THE UCC1 PROGRAM AND PUT THE LABEL
*        ONTO THE TAPE AND THEN RETURN TO PROMPT FOR THE TAPE NUMBER
*        OR 'END'.
*
* INSTALLED: 10 DEC 84
*
* CHNGE LOG: 26 DEC 84 - MUST CHANGE THE LOGIC FROM DOING A LINK
*            TO TMSTPNIT TO AN ATTACH. UCC VERIFIED THAT THEY HAVE
*            BUG IN THEIR CODE THAT THEY ARE NOT CLOSING A FILE.
*            WHEN YOU PROCESS TMSTPNIT AS A JOB, THE SYSTEM IS NICE
*            ENOUGH TO CLEANUP AFTER YOU. BUT WHEN YOU DO LINK'S
*            AND MULTIPLE IN FACT, YOU GET A 'SC03' ABEND WHEN YOU
*            TERMINATE THIS PROGRAM FOR EACH TIME YOU LINK'D. NOT
*            TOO SWIFT. SO WE CHANGE THE LOGIC PER UCCEL TO DO AN
*            'ATTACH' AND ALL IS FINE.    JIM MARSHALL
*$DOC$----------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
TMSLABL  AMODE 24
TMSLABL  RMODE 24
TMSLABL  CSECT
         SAVE  (14,12),,SADSC_LABELTP_&SYSDATE &SYSTIME
         LR    R12,R15
         USING TMSLABL,R12
         SPACE
         LA    R8,SAVE
         ST    R13,SAVE+4
         ST    R8,8(R13)
         LR    R13,R8
         SPACE
*---------------------------------------------------------------------*
*        THIS IS JUST IN CASE SOMEONE IS TRYING TO DO SOMETHING
*        THEY SHOULD NOT, WHAT THIS DOES IS TO DESTROY ANY FILE
*        IN CASE SOMEONE IS TRYING TO PULL A FAST ONE ON ME.
*        THINK SNEAKY TO SOLVE THE MYSTERY.
*---------------------------------------------------------------------*
         SPACE
         OPEN  (SYSIN,(OUTPUT))    IN CASE SOMEONE IS PULLING
         PUT   SYSIN,RECORD        COPY IN SKEL RECORD
         CLOSE (SYSIN)               A FAST ONE.
         SPACE
*---------------------------------------------------------------------*
*        NOW WE GO OUT TO ASK THE OPERATORS FOR THE VOL SER
*---------------------------------------------------------------------*
         SPACE
DOMORE   EQU   *
         MVC   WTOECB,ZERO         CLEAR THE ECB FOR THIS TIME
         MVC   VSN(6),BLANKS       CLEAR THE TAPE NUMBER
         SPACE
         WTOR  'SAD1001A REPLY T-NUMBER OR ''END'' TO TERMINATE.',     X
               VSN,6,WTOECB
         WAIT  ECB=WTOECB
         SPACE
         OI    VSN,X'40'           FORCE UPPER CASE
         CLC   VSN(1),END          DO WE END ??
         BE    RETURN
         CLI   VSN,C'T'            IS IT A T ?
         BNE   ERR1
         LA    R6,VSN+1            POINT TO VOLSER
         L     R5,FIVE             CHK FOR LENGTH OF 5
         SPACE
LOOP     EQU   *
         TM    0(R6),X'F0'         MUST BE NUMBER
         BNO   ERR2
         A     R6,ONE              BUMP POINTER
         BCT   R5,LOOP
         CLC   VSN(6),LOWVAL
         BL    ERR3
         CLC   VSN(6),HIGHVAL
         BH    ERR3
         MVC   VOLSER(6),VSN       MOVE IN THE VOLSER
         SPACE
         OPEN  (SYSIN,(OUTPUT))    OPEN FOR CTL CARDS
         PUT   SYSIN,RECORD        WRITE THE RECORD FOR TMS
         CLOSE (SYSIN)             CLOSE FOR PREP TO LINK
         SPACE
         MVC   TMSECB,ZERO         ZERO TMS ECB
         MVC   TMSADDR,ZERO        ZERO TMS TCB ADDR
         SR    R0,R0               ZERO R0
         LA    R1,PARMS            PT TO DUMMY LIST.
         ATTACH EP=TMSTPNIT,ECB=TMSECB
         ST    R1,TMSADDR
         WAIT  ECB=TMSECB
         DETACH TMSADDR
         B     DOMORE
         SPACE
*---------------------------------------------------------------------*
*        THIS IS THE PLACE WE ISSUE THE ERROR MSGS TO THE OPERATORS
*---------------------------------------------------------------------*
         SPACE
ERR1     EQU   *
         LA    R1,ERRWTO1          POINT TO ERROR MSG
         SVC   35
         B     DOMORE
         SPACE
ERR2     EQU   *
         LA    R1,ERRWTO2          POINT TO ERROR MSG
         SVC   35
         B     DOMORE
         SPACE
ERR3     EQU   *
         LA    R1,ERRWTO3          POINT TO ERROR MSG
         SVC   35
         B     DOMORE
         SPACE
*-------------------------------------------------------------------*
*        RETURN TO THE CALLER. FIRST WE MUST FREE THE WORKAREA
*        AND THEN RETURN TO THE CALLER.
*-------------------------------------------------------------------*
         SPACE
RETURN   EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0
*-------------------------------------------------------------------*
*        PUT ALL OUR CONSTANTS THE THIS AREA
*-------------------------------------------------------------------*
         SPACE
         DS    0F
         SPACE
ERRWTO1  WTO  'SAD1002E ''END'' OR VALID T-NUMBER WAS NOT SPECIFIED.', X
               ROUTCDE=(11),MF=L
         SPACE
ERRWTO2  WTO   'SAD1003E  T-NUMBER IS NOT VALID? ',ROUTCDE=(11),MF=L
         SPACE
ERRWTO3  WTO   'SAD1003E  T-NUMBER IS NOT BETWEEN T00001-T00500',      X
               ROUTCDE=(11),MF=L
         SPACE
SYSIN    DCB   DSORG=PS,MACRF=PM,DDNAME=PRESYSIN,RECFM=FB,             X
               LRECL=80,BLKSIZE=80
         SPACE
ZERO     DC    F'0'                0 BIG ONE
ONE      DC    F'1'                1 BIG ONE
FIVE     DC    F'5'                5 BIG ONES
END      DC    C'END'              SIGNAL END
BLANKS   DC    CL6' '              BLANKS
LOWVAL   DC    CL6'T00001'
HIGHVAL  DC    CL6'T00500'
RECORD   DS    0CL80               TMSTPNIT RECORD SKEL.
         DC    CL8'OUTTAPE '
         DC    CL4'SER='
VOLSER   DC    CL6'XXXXXX'
         DC    CL62',OWNER=SADSC'
         SPACE
SAVE     DC    18F'0'
WTOECB   DC    F'0'
TMSECB   DC    F'0'
TMSADDR  DC    F'0'
VSN      DC    CL6' '              REPLY AREA
         DS    0F
PARMS    DC    X'80',AL3(LPARMS)
LPARMS   DC    H'00'
         LTORG
         END
