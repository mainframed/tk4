         TITLE 'I E F U J I  -  JOB INITIATION EXIT FROM SMF '
*-------------------------------------------------------------------*
*
*        COURTESY:  JIM MARSHALL, CAPT, USAF (RET)
*                   FROM MY DAYS WHEN I WAS IN TEXAS AT THE
*                   SAN ANTONIO DATA SERVICES CENTER
*
*   A.   THE FIRST TWO ACCOUNTING FIELDS OF THE JOB CARD ARE CHECKED.
*        THE CHECKS ARE AS FOLLOWS:
*
*            A. WE CHECK THE 1ST TWO ACCOUNTING FIELDS OF THE JOB
*               CARD TO ENSURE THEY ARE 4 CHARACTERS IN LENGTH.
*            B. CONSTRUCT A PARAMETER LIST CONSISTING OF JOBNAME,
*               AND 1ST TWO ACCOUNTING FIELDS TO BE PASSED TO A
*               VALIDATION ROUTINE (IEFPVSCK). WHEN WE RETURN WE
*               KNOW IF WE ARE GOOD OR NOT.
*            C. AS A PART OF THAT PARAMETER LIST IS AN ENTRY CODE
*               FOR IEFPVSCK. THIS WILL ALLOW YOU TO SELECTIVELY
*               BRING USERS ONTO A FULL CHECK (WE CALL THIS PVS
*               OR PROJECT VALIDATION SYSTEM).
*
*   B.   THE PROGRAM CHECKS THE PROGRAMMER NAME FIELD ON THE JOB CARD
*        FOR PROPER FORMAT AND VALID ENTRIES.
*
*        THE 20-CHARACTER PROGRAMMER NAME FIELD MUST BE FORMATTED AND
*        COMPLETED AS FOLLOWS:
*
*               POS  1 -  8   COULD BE USED BY CUSTOMER. NOT CHECKED
*               POS  9 - 10   RUN CODE (MUST BE NUMERIC)
*               POS 11        SECURITY CLASSIFICATION.
*                             MUST BE AN 'U','P' OR 'O'.
*                             U - UNCLASSIFIED
*                             P - CONTAINS PRIVACY ACT DATA
*                             O - OFFICIAL USE ONLY
*               POS 12        UNUSED. NOT CHECKED.
*               POS 13 - 20   PROGRAMMER NAME. NOT CHECKED.
*
*   C.   WE HAVE UCC1-TMS INSTALLED, SO WE HAVE A SECTION SETUP
*        TO PASS INFORMATION TO TMS BY LINKING OFF TO 'TMSACTRT'
*
*   D.   JOBS ARE CANCELED BY ENTERING A RETURN CODE OF '4' IN
*        R15.  IN ADDITION A FLAG IS SET IN THE JMR USERID AREA
*        TO INDICATE THE EXACT ERROR CONDITION.
*        THIS IS USED BY THE 'IEFACTRT' EXIT ROUTINE TO WRITE AN
*        APPROPRIATE ERROR MESSAGE TO 'SYSPRINT'.
*
*   E.   THE ABOVE STATEMENT IS TRUE AS IT IS THE RESPONSIBLITY
*        OF 'IEFACTRT' TO PUT ANY MSGS TO THE USER. WE DO NOT
*        DO THAT AT PRESENT. IF YOU JUST TERMINATE THEM WITH A JCL
*        ERROR AT EXECUTION TIME, THEY MAY GET CONFUSED. BUT YOU
*        HAVE STOPPED THE JOB FROM EXECUTING. A FAR BETTER PLACE
*        FOR THIS CODE IS IN JES2/SP1.3 EXIT-2. YOU CAN THEN DO
*        THE CHECK AT SUBMISSION TIME  AND FULLY INFORM THEM OF
*        THE ERROR OF THERE WAYS !!!!!!
*
*     INSTALLED:  4 JAN 84.
*
*     CHNGE LOG:  1 JUL 84 - ADDED UCC1 (TMS) SUPPORT. JDM
*-------------------------------------------------------------------*
         SPACE
R0       EQU   0                   STANDARD OS LINKAGE
R1       EQU   1                   WORK REGISTER
R2       EQU   2                   ADDRESS OF SMF COMMON PARAMTER AREA
R3       EQU   3                   ADDRESS OF PGM NAME FIELD
R4       EQU   4                   ADDRESS OF JOB PRIORITY
R5       EQU   5                   ADDR OF ACCT INFO
R6       EQU   6                   ADDR OF SMF POINTERS
R7       EQU   7                   WORK REGISTERS
R8       EQU   8                   WORK REGISTERS
R9       EQU   9                   WORK REGISTERS
R10      EQU   10                  WORK REGISTERS
R11      EQU   11                  WORK REGISTERS
R12      EQU   12                  BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
IEFUJI   CSECT
         SAVE  (14,12),,SADSC_IEFUJI_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING IEFUJI,R12
         SPACE
*-------------------------------------------------------------------*
*        SETUP ALL THE ADDRESSABILITY WE NEED TO DO AND GET
*        ALL THE ADDRESSES FOR LATER.
*-------------------------------------------------------------------*
         SPACE
         LR    R6,R1                   SAVE INPUT PARM REG
         USING ACTPARM,R6
         SPACE
         L     R2,ACPJMR               ADDR OF COMMON PARM AREA
         USING JMR,R2
         SPACE
         L     R3,ACPPGMNM             ADDR OF PGM NAME FIELD
         L     R4,ACPPRIO              ADDR OF PGM PRIORITY
         L     R5,ACPJACCT             ADDR OF ACCT INFO
         SPACE
*-------------------------------------------------------------------*
*        NOW GO GET SOME STORAGE TO ENSURE WE STAY RE-ENTRANT.
*        WE NEED SOME ROOM TO PLAY !!!!!!
*-------------------------------------------------------------------*
         SPACE
         LA    R0,WORKEND-WORKAREA
         O     R0,SUBPOOL              INDICATE WHAT SUBPOOL
         GETMAIN R,LV=(0)              GET SOME STORAGE
         USING WORKAREA,R1             GET ADDRESSABILITY
         XC    SAVEAREA,SAVEAREA       CLEAR OUT SAVEAREA
         ST    R13,SAVEAREA+4          PLUG IN LOW SAVE
         ST    R1,8(R13)               PLUG IN HIGH SAVE
         LR    R13,R1                  POINT TO GETAMINED AREA
         DROP  R1
         USING WORKAREA,R13            RE-ESTABLISH ADDRESSABILITY
         SPACE
*------------------------------------------------------------------*
*        CLEAR JMRUSEID FIELD TO BLANKS UNTIL RESTART FLAGS
*          ARE UNDERSTOOD MORE FULLY.
*------------------------------------------------------------------*
         SPACE
         MVC   JMRUSEID,=CL8' '
         SPACE
*------------------------------------------------------------------*
*        CHECK TO SEE IF THIS IS A STARTED TASK SUCH AS JES2
*        STC'S DON'T HAVE ANY ACCOUNTING FIELDS. WE STILL WILL
*        PERFORM ALL THE CHECKS FOR TSO USERS.
*------------------------------------------------------------------*
         SPACE
         CLI   0(R5),X'00'
         BE    ADDPROJ            ANY FOR THIS CALL, PROBABLY STC
         SPACE
         SPACE
*------------------------------------------------------------------*
*        CHECK PROGRAMMER NAME FIELD FOR CORRECTNESS. TYPE TIME
*        CODE OF 10-40 IN POS 9-10 OF PGM NAME FIELD.
*------------------------------------------------------------------*
         SPACE
         B     BY1
         CLI   8(R3),X'F1'        CHECK RUN CODE
         BL    CANCEL2            IF NOT CORRECT, CANCEL
         CLI   8(R3),X'F4'
         BH    CANCEL2
         CLI   9(R3),X'F0'
         BL    CANCEL2
         CLI   9(R3),X'F9'
         BH    CANCEL2
BY1      EQU   *
         SPACE
*------------------------------------------------------------------*
*        CHECK PROGRAMMER NAME FIELD FOR CORRECTNESS. CLASSIFICATION
*        OF THE PRINT MUST BE  'U', 'O', OR 'P'.
*------------------------------------------------------------------*
         SPACE
         CLI   10(R3),C'U'        IS IT UNCLASSIFIED
         BE    CORRECT
         CLI   10(R3),C'P'        IS IT PRIVACY ACT DATA
         BE    CORRECT
         CLI   10(R3),C'O'        IS IT FOR OFFICIAL USE ONLY
*        BNE   CANCEL2            DO NOT ENFORCE
         SPACE
*------------------------------------------------------------------*
*        CHECK ACCOUNTING FIELDS FOR CORRECTNESS
*------------------------------------------------------------------*
         SPACE
CORRECT  EQU   *
         B     BY2
         CLI   0(R5),X'02'         MUST BE AT LEAST  2 ACCT FIELDS
         BL    CANCEL5
         CLI   1(R5),X'04'         FIRST ACCT FLD LENGTH =4
         BNE   CANCEL5
         CLI   6(R5),X'04'         SECOND ACCT FLD LENGTH =4
         BNE   CANCEL5
BY2      EQU   *
         SPACE
*------------------------------------------------------------------*
*        NOW WE CHECK TO SEE IF THE ACCOUNTING INFO IS VALID
*        IF IT IS A SADSC JOB, THEN WE WILL DO FULL CHECKING.
*        THIS IS DONE BY GOING TO PVSCHEK WITH A HEX CODE
*        OF X'FAB0'.
*------------------------------------------------------------------*
         SPACE
         CLC   0(4,R2),=C'Z#ZZ'        DO WE BYPASS ALL CHKS
         BE    ISOK
         SPACE
FULLCHK  EQU   *
         MVC   PVSCODE,=XL2'FAB0'      CODE TO DO FULL CHK
         MVC   JOBNAME(2),0(R2)        PUT IN 2 CHARS OF JOBNAME
         MVC   PROJCODE(4),2(R5)       PUT IN PROJ CODE   ACCT 1ST 4
         MVC   DSDCODE(4),7(R5)        PUT IN DSD CODE    ACCT 2ND 4
         LA    R1,PVSCODE              POINT TO LIST
         LINK  EP=PVSCHEK              GO SEE IF VALID
         LTR   R15,R15                 IS IT VALID
         BNZ   CANCEL5                 FAIL THE JOB
ISOK     EQU   *
         SPACE
*-----------------------------------------------------------------*
*        WE WANT TO PASS TMS THE 1ST 2 ACCT FIELDS FROM THE
*        JOB CARD PUSHED TOGETHER.
*-----------------------------------------------------------------*
         SPACE
         LA    R1,PROJCODE             POINT ACCT CODES FOR TMS
         B     TMSLINK
         SPACE
*-----------------------------------------------------------------*
*        SINCE STC'S DO NOT HAVE ACCOUNTING INFO. WE MUST SUPPLY
*        SOME TO TMS. SINCE ALL STC'S BELONG TO SYSTEMS, WE WILL
*        USE SYSTEM'S ACCT CODES.
*-----------------------------------------------------------------*
         SPACE
ADDPROJ  EQU   *
         LA    R1,STCPROJ              DEFAULT ACCT CODE
TMSLINK  EQU   *
         LINK  EP=TMSACTRT
         SPACE
         LA    R15,0                   INDICATE OK
         SPACE
*-----------------------------------------------------------------*
*        RETURN LOGIC, FREE UP THE GETMAINED AREA AND BYEEEEE!
*-----------------------------------------------------------------*
         SPACE
RETURN   EQU   *
         LA    R0,WORKEND-WORKAREA     CALCULATE THE LENGTH
         O     R0,SUBPOOL              INDICATE THE SUBPOOL
         LR    R4,R15                  SAVE THE RETURN CODE
         LR    R1,R13                  GET ADDR OF GOTTNE AREA
         L     R13,SAVEAREA+4          GET LOW SAVE AREA
         FREEMAIN R,A=(1),LV=(0)       GIVETH BACK
         LR    R15,R4
         L     R14,12(0,R13)
         LM    R0,R12,20(R13)
         BR    R14
         SPACE
*------------------------------------------------------------------*
*        CANCELLATION REASONS AREA
*------------------------------------------------------------------*
         SPACE
CANCEL2  MVI   JMRUJICD,X'01'
         B     CANCEL
         SPACE
CANCEL5  MVI   JMRUJICD,X'02'
         SPACE
CANCEL   EQU   *
         LA    R15,4              RC=4, SYSTEM WILL CANCEL JOB
         B     RETURN
         SPACE
*------------------------------------------------------------------*
*        LITERAL POOL FOLLOWS
*------------------------------------------------------------------*
         SPACE
SUBPOOL  DC    F'0'
STCPROJ  DC    CL8'SS02P999'       SYSTEMS ACCT CODE
         LTORG
         EJECT
*-----------------------------------------------------------------*
*        DEFINE OUR DSECTS AS FOLLOWS
*-----------------------------------------------------------------*
         SPACE
WORKAREA DSECT
SAVEAREA DS   18F
PVSCODE  DS   XL2                  ENTRY CODE TO IEFPVSCK
JOBNAME  DS   CL2                  COST CODE
PROJCODE DS   CL4                  PROJECT CODE
DSDCODE  DS   CL4                  DSD CODE
WORKEND  EQU  *
         SPACE
ACTPARM  DSECT
ACPJMR   DS    A                   ADDR OF JMR
ACPPGMNM DS    A                   ADDR OF 20 BYTE PGM NAME FIELD
ACPPRIO  DS    A                   ADDR OF 1 BYTE PRIORITY FIELD
ACPJACCT DS    A                   ADDR OF ACCT INFO
         SPACE
         IEFJMR
         SPACE
         ORG   JMRUSEID
JMRCHRG  DS    PL4
JMRCCODE DS    C
JMRUJICD DS    C
JMRHCLAS DS    C
JMRHPRTY DS    C
         ORG
         END
