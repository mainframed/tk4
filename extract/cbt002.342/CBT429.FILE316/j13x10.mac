         TITLE 'JES2/SP3 EXIT #010'
*---------------------------------------------------------------------*
*
* COURTESY: JIM MARSHALL, CAPT, USAF (RET)
*
*        THIS EXIT IS BASED ON THE JES2/SP1.3 EXIT 10 FROM PAUL
*        FINDLEY 1ISG (FORMERLY AFDSC) - PENTAGON.
*        MUCH THANKS TO PAUL FOR ALL THE WORK. I USE THE RECEIPT
*        PRINTER CONCEPT THAT PAUL DEVELOPED AT THE PENTAGON.
*        WE NEED TO SUPPRESS THE MSG NUMBER SO IT DOES NOT PRINT
*        ON THE RECEIPT. SEE RELATED EXITS #1 AND #255.
*
*        1. THE JESPARMS THAT CORRESPOND TO THIS EXIT ARE:
*            . . .
*            >LOAD J13X10
*            >EXIT010 (J13X10),ENABLE,TRACE=YES
*            . . .
*
*        2. THE ORDER OF THE DSECTS IN THE PREAMBLE ASSEMBLY
*           CAN BE SIGNIFICANT.  ALSO THE REGISTER CONTENTS
*           ARE AS FOLLOWS UPON ENTRY TO THIS EXIT:
*           R0  = N/A
*           R1  = ADDRESS OF THE CMB.
*           R11 = ADDRESS OF THE HCT.
*
*        3. THIS EXIT WILL SCAN EACH WTO MESSAGE PRODUCED BY JES2.
*           IT IS  KEYING ON ALL WTO FOR RECEIPT, I HAVE CHOSEN
*           (GOD KNOWS WHY) THE MESSAGE ID OF 066 AS A RECEIPT IF IBM
*           CHOSES TO USE THIS ID THEN IT WILL HAVE TO BE CHANGED.
*
*        4. THIS EXIT WILL SCAN FOR THE FOLLOWING MSGS AND DISCARD
*           THEM (THEY ARE WORTHLESS). EXAMPLES FOLLOWS
*
*           $HASP125 INTRDR   SKIPPING FOR JOB CARD
*           $HASP317 JOBNAME NNNN DATA SETS CANCELLED
*
* INSTALLED: 4 SEP 84
*
*    5NOV85-REQUIRED TO CONVERT THE CODE FOR JES2 SP1.3.4 . SO
*           I WILL CODE A MECHANISM FOR EASIER MAINTENACE OF
*           THE CODE (THE OLE CONDITIONAL ASSEMBLY.             JDM1
*
*---------------------------------------------------------------------*
         SPACE
         GBLB  &SP134              1 - SP1.3.4 / 0 - SP1.3.2     JDM1
&SP134   SETB  1                                                 JDM1
         AIF   (NOT &SP134).SKIP10                               JDM1
         COPY  $HASPGBL
USREXIT $MODULE $BUFFER,                                               X
               $JCT,                                                   X
               $JQE,                                                   X
               $HASPEQU,                                               X
               $PCE,                                                   X
               $CAT,                                                   X
               $DCT,                                                   X
               $HCT,                                                   X
               $PDDB,                                                  X
               $MIT,                                                   X
               $JOE,                                                   X
               $PPPWORK,                                               X
               $CMB
         PRINT GEN
.SKIP10  ANOP                                                    JDM1
         AIF   (&SP134).SKIP15                                   JDM1
         PRINT OFF
        $SYSPARM (OFF,NOGEN,NODATA,NO,NO)   DOCUMENTATION OPTIONS
         COPY  $HASPGEN            COPY HASPGEN PARAMETERS
         PRINT ON
        $SYSPARM (ON,GEN,DATA,YES,YES)    DOCUMENTATION OPTIONS
MITABLE $MODULE                    CREATE THE MIT CSECT
        $BUFFER DOC=YES            REQ'D BY $GETBUF, $FREEBUF,
*                                     AND MUST PRECEDE $JCT
        $JCT   DOC=YES             MUST PRECEDE $CAT
        $JQE   DOC=YES             MUST PRECEDE $HCT
        $PCE   DOC=YES             MUST PRECEDE $HCT
        $CAT   DOC=YES             MUST PRECEDE $HCT
        $DCT   DOC=YES             NORMALLY REQUIRED FOR EXIT #1
        $HCT   DOC=YES             REQ'D BY $SAVE, $RETURN...
        $PDDB  DOC=YES             MUST PRECEDE $IOT
        $TAB   DOC=YES             MUST PRECEDE $IOT
        $IOT   DOC=YES
        $JOE   DOC=YES             REQ'D TO GET SYSOUT CLASS
        $MIT   DOC=YES             REQ'D BY $MODEND
        $PPPWORK DOC=YES           REQ'D TO FIND JOE
        $CMB   DOC=YES             REQ'D TO FIND JOE
         PRINT GEN
.SKIP15  ANOP                                                    JDM1
         SPACE
*-------------------------------------------------------------------*
*         BEGIN CSECT TO SCAN EACH WTO MESSAGE FOR RECEIPTS
*-------------------------------------------------------------------*
         SPACE
         AIF   (NOT &SP134).SKIP20                               JDM1
J13X10  $ENTRY BASE=R12            EXIT ROUTINE ENTRY POINT
.SKIP20  ANOP                                                    JDM1
         AIF   (&SP134).SKIP25                                   JDM1
J13X10  $ENTRY BASE=R12,CSECT=YES  EXIT ROUTINE ENTRY POINT
.SKIP25  ANOP                                                    JDM1
        $SAVE
         LR    R12,R15             LOAD BASE REGISTER
         SR    R15,R15             SET RC=0 IN R15
         LR    R9,R1               SAVE ADDR OF CMB
         USING CMBDSECT,R9         SETUP ADDRESSABILITY
         SPACE
*------------------------------------------------------------------*
*        FIRST CHECK FOR MSGS TO DISCARD. IF WE FIND A MSG TO
*        DISCARD, WE HAVE TO RETURN WITH RC=8.
*------------------------------------------------------------------*
         SPACE
BODY     EQU   *
         CLC   CMBMID+1(7),MESDEL1 CHECK FOR DISCARD ID
         BE    FINI8               IF NOT FOUND CHECK FOR OTHERS
         CLC   CMBMID+1(7),MESDEL2 CHECK FOR DISCARD ID
         BE    FINI8               IF NOT FOUND CHECK FOR OTHERS
         SPACE
*------------------------------------------------------------------*
*        NOW WE CHECK FOR A MSGID OF $HASP066 AND SUPPRESS THE
*        HASP066. IT IS IN SUPPORT OF OUR 3287 TYPE RECEIPT PRTER.
*------------------------------------------------------------------*
         SPACE
LABEL1   EQU   *
         CLC   CMBMID+1(7),MESSID  CHECK FOR RECEIPT ID
         BNE   RETURN              IF NOT FOUND GET OUT
         MVC   CMBMID+1(7),BLANKS  ELSE BLANK OUT HASP ID
         MVC   CMBROUT,=X'0004'    SET ROUTE CODE TO 14
         SPACE
FINI4    EQU   *
         L     R15,FOUR            SET RETURN CODE
         B     RETURN
         SPACE
FINI8    EQU   *
         L     R15,EIGHT           TELL JES TO BYPASS HASPWQUE
         SPACE
RETURN   $RETURN RC=(R15)          RETURN TO HASPPRPU
         SPACE
*-------------------------------------------------------------------*
*        C O N S T A N T S
*-------------------------------------------------------------------*
         SPACE
MESSID   DC    C'HASP066'          MESSAGE ID FOR RECEIPT
MESDEL1  DC    C'HASP125'          MESSAGE ID TO DISCARD
MESDEL2  DC    C'HASP317'          MESSAGE ID TO DISCARD
BLANKS   DC    C'       '
FOUR     DC    F'4'
EIGHT    DC    F'8'
         LTORG
         $MODEND
         END
