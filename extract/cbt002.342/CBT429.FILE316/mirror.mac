         TITLE '   M I R R O R      '
************************************************************
*                                                          *
*        'MIRROR'                                          *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        DATE WRITTEN. MARCH 18 1977.
*        DATE UPDATED.
*        ATTRIBUTES. RE-ENTRANT.
*        REMARKS.
*            THIS PROGRAM COPIES A SEQUENTIAL DATASET OF
*            80-BYTE FIXED LENGTH RECORDS AND REVERSES THE
*            CONTENTS OF EACH RECORD, MOVING COLUMN 1 TO 80,
*            COLUMN 2 TO 79, AND SO ON.
         SPACE
MIRROR   START
         USING *,R10
         B     32(,15)
         DC    AL1(11),CL11'MIRROR '
         DC    CL16' &SYSDATE &SYSTIME '
         STM   14,12,12(13)
         LR    R10,15
         GETMAIN R,LV=MYDSECTL
         ST    13,4(,1)
         ST    1,8(,13)
         LR    14,13
         LR    13,1
         L     1,24(,14) RESTORE R1
         LR    R12,R13
         USING MYDSECT,R12
         SPACE 1
         LR    R2,R1
         SLR   R15,R15
         ST    R15,COUNTIN
         ST    R15,RC
         MVC   DCBUT1W(DCBUT1L),DCBUT1
         MVC   DCBUT2W(DCBUT2L),DCBUT2
         LA    R3,DCBUT1W
         LA    R4,DCBUT2W
         SPACE
         LA    R1,OPEN
         MVI   0(R1),X'80'
         SPACE
         OPEN  ((R3),INPUT),MF=(E,(1))
         SPACE
         TM    OFLGS(R3),X'10'
         BO    OKUT1
         LA    R15,16
         B     EXIT
OKUT1    EQU   *
         SPACE
         LA    R1,EXLUT2
         LA    R0,EXRUT2
         ST    R0,0(,R1)
         MVI   0(R1),X'85'
         IC    R0,EXLST(,R4)
         ST    R1,EXLST(,R4)
         STC   R0,EXLST(,R4)
         SPACE
         LA    R1,OPEN
         MVI   0(R1),X'80'
         SPACE
         OPEN  ((R4),OUTPUT),MF=(E,(1))
         SPACE
         TM    OFLGS(R4),X'10'
         BO    OKUT2
         LA    R15,16
         B     EXIT
OKUT2    EQU   *
READ     EQU   *
         GET   (R3),INREC
         SPACE
         L     R1,COUNTIN
         A     R1,=F'1'
         ST    R1,COUNTIN
         LA    R1,INREC
         LA    R15,OUTREC+79
         LA    R0,80
SCANLOOP IC    R14,0(,R1)
         STC   R14,0(,R15)
         LA    R1,1(,R1)
         BCTR  R15,0
         BCT   R0,SCANLOOP
SCRAWL   EQU   *
         SPACE
         PUT   (R4),OUTREC
         SPACE
         B     READ
         SPACE
EODUT1   EQU   *
         LA    R1,CLOSE
         MVI   0(R1),X'80'
         CLOSE ((R4)),MF=(E,(1))
         LA    R1,CLOSE
         MVI   0(R1),X'80'
         CLOSE ((R3)),MF=(E,(1))
         SPACE 1
         B     EXITRC
         SPACE
EXRUT2   LH    R0,BLKSI(,R4)
         LTR   R0,R0
         BNZR  R14
         MVC   BLKSI(2,R4),BLKSI(R3)
         BR    R14
         SPACE
EXITRC   L     R15,RC
EXIT     LR    1,13
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=MYDSECTL
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         PRINT NOGEN
         SPACE
DCBUT1   DCB   DDNAME=SYSUT1,MACRF=(GM),DSORG=PS,EODAD=EODUT1,         +
               RECFM=FB,LRECL=80
DCBUT1L  EQU   *-DCBUT1
         SPACE
DCBUT2   DCB   DDNAME=SYSUT2,MACRF=(PM),DSORG=PS,                      +
               RECFM=FB,LRECL=80
DCBUT2L  EQU   *-DCBUT2
         SPACE
         PRINT GEN
         SPACE
         LTORG
OFLGS    EQU   48
DDNAM    EQU   40
EXLST    EQU   36
BLKSI    EQU   62
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
MYDSECT  DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
OPEN     DS    F
CLOSE    EQU   OPEN
COUNTIN  DS    F
RC       DS    F
DCBUT1W  DS    0F,(DCBUT1L)X
DCBUT2W  DS    0F,(DCBUT2L)X
EXLUT2   DS    F
INREC    DS    0D,CL80
OUTREC   DS    CL80
         DS    0D
MYDSECTL EQU   *-MYDSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
