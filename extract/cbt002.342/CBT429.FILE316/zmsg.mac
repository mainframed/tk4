         TITLE 'PLACE MESSAGE FROM EXEC CARD ON OPERATOR CONSOLE'
*---------------------------------------------------------------------*
*                                                                     *
*        THIS PROGRAM IS MEANT TO PLACE A MESSAGE FROM THE PARM       *
*        ON THE OPERATOR CONSOLE.  IBM SUPPLIES YOU WITH A PGM        *
*        CALLED 'WTO' BUT YOU MUST FEED IT THE MESSAGE THROUGH        *
*        '//SYSIN DD *'. A REAL BUMMER IF YOU WANT TO PUT THIS        *
*        IN A JCL PROC. IT IS MY OPINION THAT IF YOU CAN'T SAY        *
*        IT IN ONE SENTENCE, THAN YOU SHOULD THINK HARDER! IN FACT    *
*        KEEP IT TO 60 CHARS MAX.                                     *
*                                                                     *
*        FOR EXAMPLE: PLACE AT END OF DAILY JOB.                      *
*                                                                     *
*        //GOODJOB  EXEC PGM=ZMSG,COND=(0,NE),                        *
*        //         PARM='OPS124I  TESTJOB SUCCESSFUL.'               *
*        //*                                                          *
*        //BADJOB   EXEC PGM=ZMSG,COND=(0,EQ),                        *
*        //         PARM='OPS125E  TESTJOB UNSUCCESSFUL.'             *
*        //*                                                          *
*        //ABORT    EXEC PGM=ZMSG,COND=ONLY,                          *
*        //         PARM='OPS126E  TESTJOB ABENDED ! CALL SYSTEMS'    *
*                                                                     *
*                                                                     *
*        JIM MARSHALL, CAPTAIN, USAF-RET                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
ZMSG     AMODE 24
ZMSG     RMODE 24
ZMSG     CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING ZMSG,R12
         LA    R15,ZSAVE
         ST    R15,8(R13)
         ST    R13,ZSAVE+4
         LR    R13,R15
         SPACE
         L     R2,0(R1)            GET PARM(MESSAGE) LOCATION
         SR    R3,R3
         IC    R3,1(R2)            GET PARM LENGTH
         CH    R3,=H'60'           IF LENGTH > 60 THEN
         BL    *+8                 TRUNCATE TO 60 BYTES
         LH    R3,=H'60'           FORCE LENGTH OF 60 BYTES
         BCT   R3,*+4              ADJUST LENGTH FOR MOVE
         STC   R3,ZMOVE+1          MODIFY MOVE LENGTH ...
         LA    R3,5(R3)            ADJUST AND MODIFY     .
*>DESC<  STH   3,ZWTO+4            THE WTO MESSAGE LENGTH
ZMOVE    MVC   ZWTO+08(00),2(2)    PUT MESSAGE INTO WTO
ZWTO     WTO   '                                                       +
                   ***********',ROUTCDE=(2),DESC=(4)
         STIMER WAIT,BINTVL=SNOOZE
EOJ      L     R13,ZSAVE+4
         RETURN (14,12),T,RC=0
         SPACE
         LTORG
         SPACE
ZSAVE    DC    18F'0'
         DS    0D
SNOOZE   DC    A(5*100)           FIVE SECONDS
         END
