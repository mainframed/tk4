         TITLE 'PDS UTILITY PGM, PDS TO SEQ OUTPUT IN DIRECTORY SEQ'
***********************************************************************
*
*        DESIGNED TO PROCESS MEMBERS OF A PDS SEQUENTIALLY.
*        RESTRICTED TO LRECL=80 PDS, ANY BLOCKING FACTOR.
*        OPTIONS: VARIOUS FACILITIES OF THIS PROGRAM ARE INVOKED BY
*                 THE PARM FIELD OF THE EXEC CARD.
*                 PARM=LIST, PROVIDES A LISTING OF THE PDS IN MEMBER
*                 NAME SEQUENCE. ALSO PROVIDES LIST OPTION ON IEBUPDAT
*                 'ADD' CARD WHEN AN UPDATE OPTION IS SELECTED.
*                 PARM=SPACE, PROVIDES 5 SPACES BETWEEN LISTED MEMBERS.
*                 PARM=EJECT, EJECTS FOR EACH LISTED MEMBER.
*                 PARM=INDEX, PROVIDES MEMBER/PAGE # CROSS REFERENCE.
*                 NOTE: PARM='LIST,SPACE,INDEX' ARE DEFAULTS.
*                 PARM=UPDATE, PROVIDES 'ADD' CARD & 'NUMBR' CARD FOR
*                 EACH MEMBER.
*                 PARM=UPDTE, PROVIDES AN IEBUPDTE 'ADD' CARD INSTEAD
*                 OF AN IEBUPDAT 'ADD' CARD.
*                 PARM=NOADD, ELIMINATES CTL CARDS ON UPDATE OPERATNS.
*                 PARM=ALPHA, PROVIDES SELECTIVE EXTRACTION OF MEMBERS.
*                 USES SYSIN WITH SELECT VALUE IN CC16, BLANK DELIMITS.
*                 MULTIPLE CARDS MUST BE IN ALPHA SEQUENCE.
*                 NOTE: MORE THAN ONE OPTION MAY BE SPECIFIED IN PARM
*                       FIELD PROVIDED THEY ARE SEPARATED BY COMMAS
*                       AND NOT MUTUALLY EXCLUSIVE ('EJECT,SPACE' AND
*                       'UPDAT,UPDTE' ARE INVALID COMBINATIONS).  IF
*                       EITHER 'EJECT' OR 'SPACE' AND 'UPDAT' OR
*                       'UPDTE', THEN THE PDS WILL BE LISTED AND
*                       SEQUENTIALLY PUT TO OUTPUT DATA SET AT THE
*                       SAME TIME.
*
*        REGISTER USAGE
*
*        REG 5 USED FOR ADDRESS OF BLOCK AREA FOR INPUT PDS DATA
*        REG 6 USED TO POINT TO AN OUTPUT RECORD
*        REG 7 USED TO DEBLOCK A BLOCKED PDS - HANDS OFF
*        REG 13 IS THE PGM BASE REG
*        REGS 3,4,9,10,11,12 ARE AVAILABLE FOR PERMANENT ASSIGNMENT
*
*         THIS PROGRAM WAS RECONSTRUCTED FROM LOAD
*         MODULES BY CAPT. P. SCHAFER,AFDSC/SFI,
*         10 FEBRUARY 1979.  IT HAS ONLY BEEN TESTED
*         WITH PARMS:  LIST,SPACE,INDEX.
*
***********************************************************************
         SPACE
PDSUTIL  AMODE 24
PDSUTIL  RMODE 24
PDSUTIL  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING  *,R15
         B     22(0,R15)     BRANCH AROUND ID
         DC    FL1'18'
         DC    CL9'PDSUTIL'  IDENTIFIER
         DC    CL8'V001'     VERSION
         STM   R14,R12,12(R13)          SAVE REGISTERS
         ST    R13,A+4       CHAIN FORWARD
         CNOP  0,4
         BAL   R13,A+72      SETUP SAVE AREA POINTER AND BASE
         DROP  15
         USING *,R13         ESTABLISH ADDRESSABILITY
A        DC    18F'0'        SAVE AREA
         L     R15,A+4
         ST    R13,8(0,R15)   CHAIN BACK
         OI    BLKSW,X'04'
         L     1,0(1)
         CLC   0(2,1),=H'0'             TEST IF PARM FIELD PRESENT
         BNE   SETSCAN
         OI    UPDATE,X'08'
         OI    BLKSW,X'08'
         OI    UPDATE,X'01'
         OI    UPDATE,X'04'
         B     PFINI
         SPACE
SETSCAN  LH    2,0(1)              SAVE PARM FIELD LENGTH
         LA    3,2(1)
SCAN     CLC   0(5,3),=C'CONSL'
         BE    TSTCONSL
         CLC   0(5,3),=C'UPDTE'
         BE    TSTUPDTE
         CLC   0(5,R3),=C'DUMMY'
         BE    SUBFIVE
         CLC   0(5,3),=C'SPACE'
         BE    TSTSPACE
         CLC   0(5,3),=C'EJECT'
         BE    TSTEJECT
         CLC   0(4,3),=C'LIST'
         BE    TSTLIST
         CLC   0(5,3),=C'INDEX'
         BE    TSTINDEX
         CLC   0(5,3),=C'ALPHA'
         BE    TSTALPHA
         CLC   0(5,3),=C'NOADD'
         BE    TSTNOADD
PARMERR1 WTO   'INCORRECT OR INVALID PARAMETER IN EXEC STATEMENT'
         ABEND  100,DUMP
RETNX100 EQU   *
         L     R13,A+4
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         LA    R15,100
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 3
TSTCONSL OI    BLKSW,X'02'
         B     TSTALPHA
SUBFIVE  SH    2,=H'5'
         LA    3,5(3)
         B     CHKCOMMA
         SPACE 2
TSTUPDTE EQU   *
         TM    UPDATE,X'20'
         BO    PARMERR2
         TM    UPDATE,X'40'
         BO    PARMERR3
         OI    UPDATE,X'20'
         OI    UPDATE,X'80'
         B     SUBFIVE
         SPACE 2
TSTSPACE EQU   *
         TM    UPDATE,X'04'
         BO    PARMERR2
         OI    UPDATE,X'04'
         TM    UPDATE,X'02'
         BO    PARMERR3
         OI    UPDATE,X'08'
         B     SUBFIVE
TSTEJECT EQU   *
         TM    UPDATE,X'02'
         BO    PARMERR2
         TM    UPDATE,X'04'
         BO    PARMERR3
         OI    UPDATE,X'02'
         OI    UPDATE,X'08'
         B     SUBFIVE
         SPACE 2
TSTNOADD EQU   *
         TM    BLKSW,X'20'
         BO    PARMERR2
         TM    BLKSW,X'10'
         B     SUBFIVE
         SPACE 2
TSTINDEX EQU   *
         TM    UPDATE,X'01'
         BO    PARMERR2
         OI    UPDATE,X'01'
         OI    BLKSW,X'08'
         B     SUBFIVE
TSTALPHA EQU   *
         TM    BLKSW,X'40'
         BO    PARMERR2
         OI    BLKSW,X'40'
         NI    BLKSW,X'FB'
         TM    BLKSW,X'02'
         BO    SUBFIVE
         OPEN  CARD
         B     SUBFIVE
         SPACE
TSTLIST  EQU   *
         TM    UPDATE,X'10'
         BO    PARMERR2
         OI    UPDATE,X'10'
         SH    2,=H'4'
         LA    3,4(3)
         SPACE 2
CHKCOMMA LTR   2,2                      TEST FOR MORE OPTIONS
         BZ    PFINI
         CLI   0(3),C','                CHECK FOR CORRECT DELIMITER
         BNE   PARMERR1
         LA    3,1(3)
         BCT   2,SCAN
         B     PARMERR1
         SPACE 2
PARMERR2 WTO   'DUPLICATE OPTIONS IN PARM FIELD OF EXEC CARD'
         B     RETNX100
         SPACE 2
PARMERR3 WTO   'INCOMPATIBLE OPTIONS IN PARM FIELD OF EXEC CARD'
         B     RETNX100
PFINI    EQU   *
         RDJFCB  (UTPDS)
         MVC   DSNAME(44),JOBQREC
         MVC   VOLNO(6),JOBQREC+118
         OI    JOBQREC+52,X'08'
         OPEN  (UTPDS),TYPE=J
         LH    0,UTPDS+62          PICKUP BLKSIZE
         GETMAIN R,LV=(0)
         LR    5,1                 SETUP REG5 WITHBLOCK AREA
*        ACQUIRE DATE
         L     2,16
         UNPK  DATE(5),57(3,2)
         MVZ   DATE+4(1),=C'1'
         SPACE 1
* SETUP SYSPRINT BLOCKSIZE
         L     2,16                CVT
         L     2,148(2)            CVTMSER
         LTR   2,2
         BZ    OPENPR              PCP NO CHNG TO PRINT BLOCKING
         LA    3,PRINTER           PRINT DCB
         LH    2,=H'798'
         STH   2,62(3)             BLKSIZE ALTERED FOR MFT + MVT
OPENPR   EQU   *
         OPEN  (PRINTER,(OUTPUT))
         B     CALLNAME
ENDPDS   EQU   *
         TM    UPDATE,X'08'
         BZ    CALLNAME
         MVC   PRTID1(16),=C'***** END OF MBR'
         PUT   PRINTER,PRTLINE
         MVI   PRTCC1,C' '
         MVC   PRTCC1+1(133-1),PRTCC1
         AP    LINECT,=P'1'
CALLNAME EQU   *
         L     15,=A(DPQGMBR)
         BALR  14,15
         LTR   15,15
         BNZ   EOFPDS
         MVC   FINDAREA,0(1)
         MVC   MBRNME,FINDAREA
         TM    UPDATE,X'02'
         BZ    BYEJ
         MVC   LINECT,=P'60'
BYEJ     EQU   *
         TM    BLKSW,X'40'
         BZ    NOSEL
COMPNME  CLC   FINDAREA,ALPHAX
         BE    NOSEL
         BL    CALLNAME
         TM    BLKSW,X'02'
         BO    WTOR
         GET   CARD
         MVC   CARDIN,0(1)
A0003CE  EQU   *
         SR    2,2
         LA    1,ALPHAX+1
LNGTH    CLC   0(1,1),=C' '
         BE    LNGFND
         LA    1,1(1)
         LA    2,1(2)
         B     LNGTH
ECB      DC    F'0'
WTOR     WTOR  'ENTER SEARCH NAME (MAX 8 CH), OR 99 TO QUIT',          X
               ALPHAX,8,ECB
         WAIT  ECB=ECB
         SR    R1,R1
         ST    R1,ECB
         CLC   ALPHAX(2),=C'99'
         BE    EOFPDS
         B     A0003CE
LNGFND   STC   2,COMPNME+1
         B     COMPNME
NOSEL    EQU   *
         BC    R0,BALHDG
         OI    NOSEL+1,X'F0'
         OI    BLKSW,X'04'
BALHDG   EQU   *
         BAL   R14,HDG
         FIND  UTPDS,FINDAREA,D
         LTR   15,15
         BNZ   ERRFND
READPDS  EQU   *
         TM    BLKSW,X'80'
         BO    BYPREAD
READ     READ  DECBPDS,SF,UTPDS,(5),'S'
         CHECK DECBPDS
         LR    6,5                 INIT REG6 TO BLOCK BEGIN
         LR    7,5                 INIT REG7 TO BLOCK BEGIN
         LH    1,UTPDS+62
         L     3,DECBPDS+16
         SH    1,14(3)
         STH   1,DECBPDS+6
         OI    BLKSW,X'80'
         B     PUTPDS
         SPACE 1
BYPREAD  LH    1,DECBPDS+6
         SH    1,=H'80'
         BZ    BLKEMPTY
         STH   1,DECBPDS+6
         LA    7,80(7)
         LR    6,7
PUTPDS   EQU   *
         BAL   R14,PUTRCD
         B     READPDS
         SPACE 2
BLKEMPTY EQU   *
         NI    BLKSW,X'7F'
         B     READ
         SPACE 2
HDG      EQU   *
         ST    R14,ENDHDG+6  ENTRY - BEGIN SUBROUTINE
         TM    UPDATE,X'80'
         BO    GENADD
PRINTON  EQU   *
         TM    UPDATE,X'04'
         BO    GENSPACE
         TM    UPDATE,X'02'
         BO    MJRHDGRQ
HDGWR1   MVI   PRTCC1,C'0'
HDGWR    MVC   PRTID1,MBRCON
         MVC   PRTID2,FINDAREA
         MVC   PRTID3+1(5),MBRCON
         PUT   PRINTER,PRTLINE
         MVI   PRTCC1,C' '
         MVC   PRTCC1+1(133-1),PRTCC1
         MVI   PRTCC1,C'0'
         AP    LINECT,=P'4'
         TM    UPDATE,X'01'
         BZ    ENDHDG        SKIP ROUTINE IF NO INDEX PAGE
         TM    BLKSW,X'08'
         BZ    ADDTABLE
         NI    BLKSW,X'F7'
GETCORE  L     0,=F'2004'
* INDEX PAGE 50 LINES X 40 + CHAIN POINTER
         GETMAIN R,LV=(0)
         SR    2,2
         ST    2,0(1)              SET CHAIN POINTER ZERO
         NOP   EXTNTA
         OI    *-3,X'F0'
         ST    1,INDEXES           SAVE INDEX START POINT
EXTNTA   NOP   *+12
         OI    *-3,X'F0'
         B     EXTNTB
         L     2,PREVINDX          ESTABLISH CHAIN
         ST    1,0(2)              LET PREVIOUS POINT TO CURRENT
EXTNTB   ST    1,PREVINDX          SAVE POINTER OF CURRENT FOR NEXT ST
         LA    1,4(1)              SKIP OVER POINTER
         ST    1,CURINDEX
         LR    2,1
         A     2,=F'2000'
         ST    2,LIMIT
         LR    2,1
         L     1,=F'1999'
         MVI   0(2),C' '
BLANKOUT MVC   1(250,2),0(2)            INITIALIZE INDEX AREA
         LA    2,250(2)
         SH    1,=H'250'
         CH    1,=H'250'
         BH    BLANKOUT
         BCTR  1,0
         EX    1,MOVEBLNK
         B     ADDTABLE
MOVEBLNK MVC   1(1,2),0(2)
ADDTABLE L     1,CURINDEX
         MVC   0(8,1),FINDAREA          MOVE MEMBER NAME AND
         MVC   8(2,1),PGCTR              PAGE NUMBER TO INDEX
         LA    1,10(1)
         ST    1,CURINDEX
         C     1,LIMIT
         BL    ENDHDG
         OI    BLKSW,X'08'   FOR INDEX
         B     ENDHDG
         SPACE 2
GENSPACE MVI   PRTCC1,C'0'
         AP    LINECT,=P'4'
         CP    LINECT,=P'48'
         BH    MJRHDGRQ
         PUT   PRINTER,PRTLINE
         PUT   PRINTER,PRTLINE
         B     HDGWR
         SPACE 2
MJRHDGRQ EQU   *
         BAL   R14,MJRHDG     GO TO SUBROUTINE
         B     HDGWR1
         SPACE 1
GENADD   EQU   *
         TM    BLKSW,X'10'
         BO    CHKPRTXX
         TM    UPDATE,X'20'
         BO    GENUPDTE
         MVC   ADDMBR,FINDAREA
         LA    2,ADDMBR
         TM    UPDATE,X'10'
         BZ    POINT
         BO    SCANADD
GENUPDTE MVC   ADDMBR(5),=C'NAME='
         LA    2,ADDMBR+5
         MVC   0(8,2),FINDAREA
         TM    UPDATE,X'10'
         BO    POINT
SCANADD  MVI   8(2),C' '
         CLI   0(2),C' '
         BE    FNDBLK
         LA    2,1(2)
         B     SCANADD+4
FNDBLK   EQU   *
         MVC   0(15,2),=C',LIST=ALL     '
POINT    LA    6,ADDCARD
         BAL   R14,PUTRCD    GO TO SUBROUTINE
         TM    BLKSW,X'20'
         BZ    CHKPRTXX
         LA    6,NUMBCARD
         BAL   R14,PUTRCD    GO TO SUBROUTINE
CHKPRTXX EQU   *
         TM    UPDATE,X'08'
         BO    PRINTON
         NOPR  R0            CNOP2,4
ENDHDG   EQU   *
         L     R14,*+6
         BR    R14           EXIT - END OF SUBROUTINE
         DC    F'0'                     RETURN ADDRESS
         SPACE 5
PUTRCD   EQU   *
         ST    R14,ENDPUT+6  ENTRY - BEGIN SUBROUTINE
         TM    UPDATE,X'80'
         BZ    PRTDTL
         NOP   PRTDTL
         OI    *-3,X'F0'
         OPEN  (OUTPDS,(OUTPUT))
PUTDTL   PUT   OUTPDS,(6)
         TM    UPDATE,X'08'
         BZ    ENDPUT
         SPACE 2
PRTDTL   CLC   0(15,6),ADDCARD
         BE    ENDPUT
         AP    LINECT,=P'1'
         CP    LINECT,=P'56'
         BL    NOHDG
         BAL   R14,MJRHDG    GO TO SUBROUTINE
NOHDG    MVC   PRTID1(80),0(6)
         PUT   PRINTER,PRTLINE
         MVI   PRTCC1,C' '
         MVC   PRTCC1+1(133-1),PRTLINE
ENDPUT   EQU   *
         L     R14,*+6
         BR    R14           EXIT - END OF SUBROUTINE
         DC    F'0'           RETURN ADDRESS
         SPACE 5
MJRHDG   EQU   *
         ST    R14,EXMJR+6   ENTRY - BEGIN SUBROUTINE
         AP    PGCTR,=P'1'
         UNPK  PGNO(3),PGCTR(2)
         MVZ   PGNO+2(1),=C'1'
         PUT   PRINTER,HEADNG
         MVI   PRTCC1,X'6D'
         MVC   PRTCC1+1(133-1),PRTLINE
         MVI   PRTLINE,X'40'
         PUT   PRINTER,PRTLINE
         MVI   PRTCC1,C' '
         MVC   PRTCC1+1(133-1),PRTCC1
         ZAP   LINECT,=P'0'
         MVI   PRTLINE,C'0'
         NOPR  R0
EXMJR    EQU   *
         L     R14,*+6
         BR    R14           EXIT - END OF SUBROUTINE
         DC    F'0'
         SPACE 5
EOFPDS   CLOSE (UTPDS,,OUTPDS,,CARD)
         TM    BLKSW,X'04'
         BZ    A000816
         TM    UPDATE,X'08'
         BZ    RETURN
         TM    UPDATE,X'01'
         BZ    RETURN
         L     6,INDEXES
         ZAP   PGCTR,=P'0'
NXTX     LA    8,4(6)
         MVC   HDGDSNAM,DSNAME
         MVC   HEADNG(120),INDEXHDG
         BAL   R14,MJRHDG
         BAL   R14,MINORHDG
PUTINDEX CLC   0(8,8),BLANKS
         BE    RETURN
         MVC   COLUMNAN,0(8)
         UNPK  COLUMNAP,8(2,8)
         OI    COLUMNAP+2,X'F0'
ENDCOLB  NOP   INCINDEX
         CLC   500(8,8),BLANKS
         BNE   PUTCOLB
         MVC   COLUMNBN(20),BLANKS
         OI    ENDCOLB+1,X'F0'
         B     BLANKCLC
A000816  PUT   PRINTER,A00083A
         L     R13,A+4
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         LA    R15,200
         MVI   12(R13),X'FF'
         BR    R14
A00083A  EQU   *
          DC    C'1PARM=ALPHA-NO MEMBERS SELECTED: POSSIBLE CONTROL '
          DC    C'CARD ERROR - MEMBER NAME MUST START IN COL. 16'
          DC    37C' '
PUTCOLB  MVC   COLUMNBN,500(8)
         UNPK  COLUMNBP,508(2,8)
         OI    COLUMNBP+2,X'F0'
ENDCOLC  NOP   INCINDEX
         CLC   1000(8,8),BLANKS
         BNE   PUTCOLC
BLANKCLC MVC   COLUMNCN(20),BLANKS
         OI    ENDCOLC+1,X'F0'
         B     BLANKCLD
PUTCOLC  MVC   COLUMNCN,1000(8)
         UNPK  COLUMNCP,1008(2,8)
         OI    COLUMNCP+2,X'F0'
ENDCOLD  NOP   INCINDEX
         CLC   1500(8,8),BLANKS
         BNE   PUTCOLD
BLANKCLD MVC   COLUMNDN(20),BLANKS
         OI    ENDCOLD+1,X'F0'
         B     INCINDEX
PUTCOLD  MVC   COLUMNDN,1500(8)
         UNPK  COLUMNDP,1508(2,8)
         OI    COLUMNDP+2,X'F0'
INCINDEX LA    8,10(8)
         AP    LINECT,=P'1'
         PUT   PRINTER,INDEXLIN
         MVI   INDEXLIN,C' '
         CP    LINECT,=P'50'
         BL    PUTINDEX
         L     1,0(6)              CHAIN POINTER
         LTR   1,1
         BZ    RETURN              NO MORE INDEX PAGES
         LR    6,1
         B     NXTX                PUMP OUT ANOTHER PAGE
RETURN EQU *
         L     R13,A+4
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         LA    R15,0
         MVI   12(R13),X'FF'
          BR    R14
         SPACE 2
MINORHDG EQU   *
         ST    R14,EXITMINR            ENTRY - BEGIN SUBROUTINE
         MVC   PRTLINE+11(19),=C'MEMBER         PAGE'
         MVC   PRTLINE+41(20),PRTLINE+11
         MVC   PRTLINE+71(60),PRTLINE+11
         PUT   PRINTER,PRTLINE
         MVI   INDEXLIN,C'0'
         CNOP 2,4
         L     R14,EXITMINR
         BR    R14
EXITMINR EQU   *
         DC    F'0'
ERRFND   WTO   'ERROR ON PDS'
         L     R13,A+4
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         LA    R15,4044
         MVI   12(R13),X'FF'
         BR    R14
         EJECT
UTPDS    DCB   DSORG=PO,MACRF=R,DDNAME=SYSUT9,EODAD=ENDPDS,NCP=1,      X
               LRECL=80,RECFM=FB,EXLST=EXITLIST
         SPACE 3
OUTPDS   DCB   DSORG=PS,MACRF=PM,DDNAME=OUTPDS,BUFNO=8,BFTEK=S
         SPACE 3
*        BLKSIZE CHANGED BY PGM FOR MFT + MVT SYSTEMS
PRINTER  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=133,BLKSIZE=133,X
               RECFM=FBA
         SPACE 3
CARD     DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,LRECL=80,BFTEK=S,        X
               EODAD=EOFPDS
         SPACE 3
FINDAREA DC    D'0'
EXITLIST EQU   *
         DC    X'87'
         DC    AL3(JOBQREC)
INDEXES  DC    F'0'
CURINDEX DC    F'0'
PREVINDX DC    F'0'
LIMIT    DC    F'0'
PGCTR    DC    PL2'0'
BLKSW    DC    X'00'
UPDATE   DC    X'00'
PRTLINE  DS    0CL133
PRTCC1   DC    CL1' '
PRTID1   DC    CL18' '
PRTID2   DC    CL8' '
PRTID3   DC    CL6' '
         DC    CL100' '
CARDIN   DS    0CL80
         DC    CL15' '
ALPHAX   DC    CL8' '
         DC    CL68' '
LINECT   DC    PL2'60'
MBRCON   DC    CL18'***** MEMBER NAME='
ADDCARD  EQU   *
         DC    CL15'./       ADD  '
ADDMBR   DC    CL8' '
         DC    CL57' '
NUMBCARD EQU   *
         DC    CL15'./       NUMBR '
         DC    CL35'NEW1=10,INCR=10'
         DC    CL30' '
HEADNG   EQU   *
         DC    C'1MVS/3032   DATE='
DATE     DC    CL5' '
         DC    CL6' '
         DC    C'VOLUME='
VOLNO    DC    CL6' '
         DC    CL6' '
         DC    C'DSNAME='
DSNAME   DC    CL44' '
         DC    CL6' '
         DC    C'MEMBER='
MBRNME   DC    CL8' '
         DC    CL6' '
         DC    C'PAGE '
PGNO     DC    CL3' '
INDEXHDG EQU   *
         DC    CL11'1'
         DC    C'TABLE OF CONTENTS FOR PARTITIONED DATA SET '
HDGDSNAM DS    CL44
BLANKS   DC    CL22' '
INDEXLIN DC    CL11'0'
COLUMNAN DS    CL8
         DC    C'  ...  '
COLUMNAP DS    CL3
         DC    CL12' '
COLUMNBN DS    CL8
         DC    C'  ...  '
COLUMNBP DS    CL3
         DC    CL12' '
COLUMNCN DS    CL8
         DC    C'  ...  '
COLUMNCP DS    CL3
         DC    CL12' '
COLUMNDN DS    CL8
         DC    C'  ...  '
COLUMNDP DS    CL3
         DC    CL14' '
JOBQREC  DS    CL187
         LTORG
         DROP  13
         TITLE 'ACQUIRE PDS MEMBER NAME'
DPQGMBR  CSECT
         SPACE 2
*        NO PARAMETERS PASSED ON ENTRY
*        RETURN REGISTERS
*              R1 - ADDRESS OF 8 BYTE MEMBER NAME
*              R15 - RETURN CODE (0=NORMAL,4=EOF)
         SPACE 2
         USING *,R15
         B     22(0,R15)     BRANCH AROUND ID
         DC    FL1'18'
         DC    CL9'DPQGMBR'       IDENTIFIER
         DC    CL8'V1'       VERSION
         STM   R14,R12,12(R13)
         ST    R13,AA+4
         CNOP  0,4
         BAL   R13,AA+72
         DROP  R15
         USING *,R13
AA       DC    18F'0'
         L     R15,AA+4
         ST    R13,8(0,R15)
         L     4,SAVE4
         L     8,SAVE8
         NOP   GETNXT
         OI    *-3,X'F0'
         OPEN  PDS
READDRTY LA    8,BUFF256
         ST    8,BUFF
         LH    4,TWO
         READ  DECBAA,SF,PDS,KEY,256
         CHECK DECBAA
         CLC   C14,0(8)            LESS THAN 14
         BH    ERR
         CLC   TWOFS+2(2),0(8)     GR THAN 256
         BL    ERR
         MVC   BLKLTH(2),0(8)
FINBLK   L     8,BUFF
         AR    8,4                 8 NOW AT MEMBER NAME
         CLC   0(8,8),ALLFS        LAST CHK
         LR    1,8
         BE    PDSFINI
         SR    15,15
         ST    8,SAVE8
         ST    4,SAVE4
         B     EXIT
GETNXT   EQU   *
         LH    5,10(8)
         N     5,MASK1
         AR    5,5                 5= # BYTES USER DATA
         AR    4,5                 STEP 4
         AH    4,TWELVE            NAME + TTRC
         CH    4,BLKLTH
         BL    FINBLK
         B     READDRTY
ERR      LA    15,2004
         B     DUMP
ERROREND LA    15,2008
DUMP     ABEND (15),DUMP
PDSFINI  LA    15,4
EXIT     EQU   *
         L     R13,AA+4
         LM    R2,R12,28(R13)
         L     R14,12(R13)
         MVI   12(R13),X'FF'
         BR    R14
TWOFS    DC    F'256'
MASK1    DC    F'31'
ALLFS    DC    8X'FF'
SAVE4    DC    F'0'
SAVE8    DC    F'0'
BUFF     DC    F'0'
BLKLTH   DC    H'0'
TWO      DC    H'2'
TWELVE   DC    H'12'
C14      DC    H'14'
PDS      DCB   DSORG=PS,MACRF=R,RECFM=F,BLKSIZE=256,BUFNO=1,           X
               DDNAME=SYSUT9,KEYLEN=8,SYNAD=ERROREND,DEVD=DA
KEY      DC    D'0'
BUFF256  DS    CL256
         LTORG
         END
