         TITLE  'SYSLOG '
*-------------------------------------------------------------------*
*            S  Y  S  L  O  G                                       *
*                                                                   *
*  COURTESY: JIM MARSHALL, CAPT, USAF-RET                           *
*                                                                   *
*            THIS PROGRAM ALLOWS YOU TO "SAFELY" WRITE YOUR         *
*            CONSOLE LOGS TO DASD. MANY TIMES WE PUT THE LOGS       *
*            ON DASD AND BROWSE THEM (GREAT IDEA). BUT THEN TO      *
*            WRITE THE LOG, YOU CAN SHOOT YOURSELF WITH             *
*            ENQUEUE PROBLEMS.                                      *
*                                                                   *
*            SO THIS LOGIC SAYS:                                    *
*                                                                   *
*            1. SEE IF ENQUEUE 'ISGQUEUE' - 'SYSLOG' IS ACTIVE      *
*               AND IF SO GO AWAY (SYSLOG PGM IS ALREADY ACTIVE).   *
*            2. SEE IF SOMEONE IS BROWSING THE FILE, IF SO WAIT.    *
*               DONE BY ISSUING ENQUEUE 'SYSDSN' - 'FILENAME';      *
*               IF SO WE WILL WAIT.                                 *
*            3. ISSUE THE COMMAND TO START THE WRITER AND WAIT      *
*               A BIT. THIS SUCKS IN ANY LOGS ALREADY QUEUED.       *
*               OTHERWISE SINCE WE SET THINGS UP FOR SMALLEST       *
*               PRINT TO GO FIRST, THE LOGS WILL BE OUT OF          *
*               ORDER IN THE DATASET.                               *
*            4. ISSUE CMD TO WRITE THE LOG AND SNOOZE.              *
*            5. NOW WE ISSUE THE COMMAND TO END THE WRITER.         *
*            6. WE NOW GO AWAY. I HAVE A JES2 AUTO CMD TO INVOKE    *
*               THE PGM AT 2400 EVERY NIGHT AND WHEN I IPL THE      *
*               SYSTEM.                                             *
*                                                                   *
*            INSTALLATION CONSIDERATIONS:                           *
*                                                                   *
*            THIS PGM MUST BE LKED 'AC=1' AND PUT INTO              *
*            AN AUTHORIZED LIBRARY !                                *
*                                                                   *
*            CHANGE THE ENQUEUE NAMES IF YOU CHOOSE.                *
*                                                                   *
*            CHANGE THE DSN OF THE FILE YOU CHOOSE TO RECEIVE       *
*            THE LOGS (SEE CONSTANTS)                               *
*                                                                   *
*            CHANGE THE EXTERNAL WRITER PROC NAME PLUS THE          *
*            STOP CMD TO END IT.                                    *
*                                                                   *
*  INSTALLED: 1 JUL 1982                                            *
*                                                                   *
*  CHANGE LOG : 12/28/88 - CLEANED UP THE DOCUMENTATION.   JDM      *
*-------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
SYSLOG   AMODE 24
SYSLOG   RMODE 24
SYSLOG   CSECT
         SAVE  (14,12),,SYSLOG_2ISG_&SYSDATE &SYSTIME
         LR    R12,R15             SETUP BASE REGISTER
         USING SYSLOG,R12          GET NEW BASE REGISTERS
         LA    R9,SAVE             GET ADDR OF SAVEAREA
         ST    R13,SAVE+4          SETUP
         ST    R9,8(R13)            SAVEAREA
         LR    R13,R9                   CHAIN
         SPACE
*--------------------------------------------------------------------*
*        SEE IF THERE IS ANOTHER SYSLOG ACTIVE, IF THERE IS THEN
*        WE TERMINATE IMMEDIATELY, IF NOT WE WILL GET AN ENQUEUE
*        MAJOR OF 'ISGQUEUE' AND MINOR OF 'SYSLOG'.
*--------------------------------------------------------------------*
         SPACE
         ENQ   (QNAME,RNAME,E,L'RNAME,SYSTEM),RET=USE
         LTR   R15,R15
         BZ    START
         WTO   'ISG0016I SYSLOG ALREADY ACTIVE.',ROUTCDE=(2)
         B     RETURN4
         SPACE
*--------------------------------------------------------------------*
*        SEE IF THE FILE IS IN USE, IF SO WE WILL WAIT
*--------------------------------------------------------------------*
         SPACE
START    EQU   *
         ENQ   (MAJORQ,MINORQ,E,L'MINORQ,SYSTEM)
         DEQ   (MAJORQ,MINORQ,L'MINORQ,SYSTEM)
         SPACE
*--------------------------------------------------------------------*
*        ISSUE THE COMMAND TO WRITE THE LOGS. THIS WILL GET
*        ANY SYSLOGS THAT HAVE ALREADY BEEN QUEUED TO PRINT.
*--------------------------------------------------------------------*
         SPACE
         MODESET KEY=ZERO
         LA    R1,CMDSLWTR             POINT TO THE COMMAND
         XR    R0,R0                   SETUP FOR SVC 34
         SVC   34                      DO IT
         STIMER WAIT,BINTVL=WAITTIME   WAIT FOR A WHILE
         SPACE
*--------------------------------------------------------------------*
*        ISSUE THE COMMAND TO  WRITE THE LOG. SINCE THE WRITER
*        IS ALREADY ACTIVE, THEN IT WILL BE PULLED OFF BY THE
*        LOGWTR EXTERNAL WRITER.
*--------------------------------------------------------------------*
         SPACE
         LA    R1,CMDWL                POINT TO THE COMMAND
         XR    R0,R0                   SETUP FOR SVC 34
         SVC   34                      DO IT
         STIMER WAIT,BINTVL=WAITTIME   WAIT FOR A WHILE
         SPACE
*--------------------------------------------------------------------*
*        ISSUE THE COMMAND TO  STOP THE WRITER
*--------------------------------------------------------------------*
         SPACE
         LA    R1,CMDPL                POINT TO THE COMMAND
         XR    R0,R0                   SETUP FOR SVC 34
         SVC   34                      DO IT
         STIMER WAIT,BINTVL=SNOOZE     WAIT FOR A WHILE
         MODESET KEY=NZERO              PROBLEM STATE
         B     RETURN0
         SPACE
*--------------------------------------------------------------------*
*        RETURN TO CALLER FOR GOOD, WITH BAD RETURN CODE
*--------------------------------------------------------------------*
         SPACE
RETURN4  EQU   *
         L     R13,4(R13)          GET OLD SAVEAREA
         RETURN (14,12),RC=4       RETURN FOR GOOD
         SPACE
*--------------------------------------------------------------------*
*        RETURN TO CALLER FOR GOOD, DO FREEMAIN AND GO AWAY !!!
*--------------------------------------------------------------------*
         SPACE
RETURN0  EQU   *
         DEQ   (QNAME,RNAME,L'RNAME,SYSTEM)
         L     R13,4(R13)          GET OLD SAVEAREA
         RETURN (14,12),RC=0       RETURN FOR GOOD
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS
*--------------------------------------------------------------------*
         SPACE
SAVE     DS    18F
         DS    0F
WAITTIME DC    A(100*60)               60 SECONDS
SNOOZE   DC    A(100*15)               15 SECONDS
QNAME    DC    CL8'ISGQUEUE'
RNAME    DC    CL6'SYSLOG'
MAJORQ   DC    CL6'SYSDSN'
MINORQ   DC    CL44'SS.SS02.SYSLOG.WEEKLY'   OUT DUMP FILE ON DISK
         SPACE
*--------------------------------------------------------------------*
*
*        SOME WORDS OF WISDOM ABOUT START CMD INVOKING AN EXTERNAL
*        WRITERS. 'SSLOGWTR' IS A PROC IN OUR 'SYS1.PROCLIB'. IF YOU
*        SAY 'S SSLOGWTR', THEN THE SYSTEM ASSIGNS THE DEVICE ADDRESS
*        OF WHERE THE DATASET DUMPING TO RESIDES; TO STOP IT YOU
*        ENTER 'P XXXX' WHERE XXXX IS THAT ADDRESS. IF YOU MOVE THE
*        FILE TO ANOTHER VOLUME, THEN YOU NEED TO CHANGE XXXX !
*
*        INSTEAD, ENTER 'S SSLOGWTR.LOGWTR,,,L' WHERE LOGWTR IS THE
*        PROCSTEPNAME AND 'L' IS THE CLASS TO ACCEPT FOR THE WRITER.
*
*        'W L' WRITES THE LOG TO CLASS L. AND FINALLY YOU CAN ENTER
*        'P SSLOGWTR.LOGWTR' NO MATTER WHERE THE DSN RESIDES.
*
*--------------------------------------------------------------------*
         SPACE
CMDSLWTR WTO   'S SSLOGWTR.LOGWTR,,,L        SYSLOG',MF=L
CMDWL    WTO   'W L                          SYSLOG',MF=L
CMDPL    WTO   'P SSLOGWTR.LOGWTR            SYSLOG',MF=L
         SPACE
         LTORG
         SPACE
         END
