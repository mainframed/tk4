IEFUJI   TITLE 'GAS AND FUEL - COST CENTRE VALIDATION EXIT'
*
*   GREG PRICE 14JAN93   (FERNTREE COMPUTER SERVICES)
*                        CUSTOMIZE FOR GAS AND FUEL CORPORATION
*
WORKAREA DSECT
SAVEAREA DS    9D                      SAVE AREA
         ACGRSRC DSECT=NO              RESOURCE PARM BLOCK
MESSAGE  DS    CL128                   MESSAGE AREA
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         PRINT NOGEN
         ACFASVT
         PRINT GEN
         ACCVT
         ACUCB
         LIDREC
         ACFREGS
         EJECT
IEFUJI   CSECT    , REENTRANT
IEFUJI   AMODE 31
IEFUJI   RMODE ANY
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 COPY ENTRY PT
         USING IEFUJI,R12              ADDRESSABILITY
         LR    R11,R1                  COPY PARM LIST ADDR
*
*        GET WORKING STORAGE (BELOW THE 16MB LINE FOR TPUT)
*
         GETMAIN RU,LV=WORKLEN,SP=229,LOC=BELOW
         ST    R13,4(,R1)              CHAIN SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1                  NEW SAVE AREA
         USING WORKAREA,R13            GET ADDRESSABILITY
*
*        GET ADDRESSABILITY TO ACF2 RELATED CONTROL BLOCKS
*
         ACFGACVT R10,NONE=NOACF2      GET ACF2 CVT ADDRESS
         USING ACCVT,R10               ADDRESS IT
         ACFGUCB R8,NONE=CKSTCOPT      GET USERS ACF UCB ADDR
         USING ACUCB,R8                ADDRESS ACUCB
*            NOTE:  AFTER ACFGUCB ISSUED R15 HAS ADDR OF ACFASVT ENTRY
         LR    R9,R15                  GET ACF ASVT ENTRY ADDRESS
         USING ACFASVT,R9              ADDRESS IT
         ICM   R7,15,ACULRECP           GET USERS LIDREC ADDR
         BZ    NOACF2                  PUNT IF NONE
         USING LIDREC,R7               ADDRESS LIDREC
*
*        SUPPLY ERROR MESSAGE AND CHECK FORMAT OF ACCOUNT CODE
*
         MVC   MESSAGE(LMISSING),MISSING  SET MISSING ACCT MESSAGE
         LM    R3,R6,0(R11)            JMR, PGMRNAME, PRIORITY, ACCOUNT
         USING JMR,R3
         CLI   0(R6),0                 WAS IT SPECIFIED?
         BE    FAILCHK                 NO, ERROR
         MVC   MESSAGE(LINVALID),INVALID  SET INVALID ACCT MESSAGE
         CLI   1(R6),8                 DOES 1ST FIELD HAVE LENGTH OF 8?
         BNE   FAILCHK                 NO, ERROR
*
*        CHECK CONTENTS OF ACCOUNT CODE
*
         CLC   2(8,R6),GFCSTCNT        YES, USER'S DEFAULT COST CENTRE?
         BE    ALLOW                   YES, NO NEED FOR SECURITY CALL
         CLC   2(8,R6),=C'00000000'    NO, EIGHT ZEROS SUPPLIED?
         BNE   CICSCHK                 NO, CHECK FOR CICS-SUBMITTED JOB
         MVC   2(8,R6),GFCSTCNT        YES, USE USER'S DEFAULT ACCOUNT
         B     ALLOW
         SPACE
CICSCHK  CLI   ASVJID,C'J'             BATCH JOB?
         BNE   MULTICHK                NO, CHECK FOR MULTIPLE ACCOUNTS
         CLC   =C'CI',LIDLID           CICS SUBMITTED JOB?
         BNE   MULTICHK                NO, CHECK FOR MULTIPLE ACCOUNTS
         CLC   =C'@ ',LIDLID+3         CICS SUBMITTED JOB?
         BNE   MULTICHK                NO, CHECK FOR MULTIPLE ACCOUNTS
         CLC   =CL6' ',JMRUSEID        LEADING BLANKS IN USER FIELD?
         BNE   ALLOW                   NO, ALLOW USER ACCOUNT FOR CICS
         CLI   0(R4),C'Z'              PGMRNAME HAS USER TYPE USER-ID?
         BNE   ALLOW                   NO, ALLOW USER ACCOUNT FOR CICS
         CLI   4(R4),C'0'              PGMRNAME HAS USER TYPE USER-ID?
         BL    ALLOW                   NO, ALLOW USER ACCOUNT FOR CICS
         CLI   5(R4),C'0'              PGMRNAME HAS USER TYPE USER-ID?
         BL    ALLOW                   NO, ALLOW USER ACCOUNT FOR CICS
         MVC   JMRUSEID(6),0(R4)       YES, COPY USERID INTO JMR
         B     ALLOW                   ALLOW USER ACCOUNT FOR CICS
MULTICHK MVC   MESSAGE(LCANTUSE),CANTUSE  SET CAN'T USE ACCT MESSAGE
         MVC   MESSAGE+MSGACCT(8),2(R6)   LOAD ACCOUNT FROM JOB CARD
         MVC   MESSAGE+MSGUSER(8),LIDLID  LOAD SECURITY USER-ID
         TM    ULIDBIT1,ULIDB13        MULTI-COST CENTRE USER?
         BZ    ERROR                   NO, WRONG CODE SO FAIL JOB
*
*        SET UP RESOURCE VALIDATION CONTROL BLOCK
*
         XC    ACGRSRC(ACGRSLEN),ACGRSRC  ZERO RESOURCE PARM BLOCK
         MVI   ACGFCN,4                SET FUNCTION CODE
         MVI   ACGSFCN,ACGSINP         SET SUB FUNCTION - INTERPRET
         LA    R0,MESSAGE              GET MESSAGE AREA ADDR
         ST    R0,ACGMSG               STORE INTO PARM BLOCK
         MVC   ACGRTYPE,=CL4'RTAC'     RESOURCE TYPE
         MVI   ACGFLGS,ACGFNRES        DON'T MAKE RULES RESIDENT
         MVI   ACGRNAME,C' '           PRE-BLANK FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(8),2(R6)       MOVE ACCOUNT TO BLOCK
*
*        ISSUE THE ACF2 RESOURCE VALIDATION CALL
*
         ACFSVC ACGRSRC,NONE=NOACF2    CALL ACF2 TO VALIDATE ACCT
         LTR   R15,R15                 WAS IT GOOD?
         BNZ   ERROR                   NO, ERROR - ACF2 RET MESSAGE
*
*        ALLOW THE USE OF THE DESCRIBED ACCOUNTING FIELD
*
ALLOW    CLC   =CL6' ',JMRUSEID        LEADING BLANKS IN USER FIELD?
         BNE   ALLOWOK                 NO, DO NOT OVERLAY PREVIOUS DATA
*        CLI   LIDLID,C'Z'             USER TYPE USER-ID?
*        BNE   ALLOWOK                 NO, DO NOT COPY IT
         CLI   LIDLID+6,C' '           USERID LESS THAN 7 CHARACTERS?
         BNE   ALLOWOK                 NO, DO NOT COPY IT
         MVC   JMRUSEID(6),LIDLID      YES, COPY USERID INTO JMR
ALLOWOK  SR    R2,R2                   ZERO RETURN CODE
         B     RETURN                  AND ENTER RETURN SEQUENCE
         SPACE 2
FAILCHK  CLI   ASVJID,C'T'             TSO SESSION?
         BE    ERROR                   YES, DO NOT ALLOW BAD ACCOUNT
         CLI   ASVJID,C'J'             BATCH JOB?
         BE    ERROR                   YES, DO NOT ALLOW BAD ACCOUNT
         WTO   'UJI002W WARNING - STARTED TASK HAS BAD ACCOUNT CODE',  X
               ROUTCDE=(1,9,11)
         B     ALLOW                   ALLOW BAD ACCOUNT
*
*        CONVERT THE MESSAGE TO WTO FORMAT AND ISSUE WTO
*
ERROR    LH    R1,MESSAGE              GET MESSAGE LENGTH
         LA    R1,MESSAGE(R1)          NEXT AVAILABLE BYTE IN MSG
         MVC   0(4,R1),ROUTCDE         ADD ROUTING CODES
         CLI   ASVJID,C'T'             TSO SESSION?
         BNE   ERRORWTO                NO, GO ISSUE ERROR MESSAGE
         NI    3(R1),255-X'20'         YES, TURN OFF WTP (ROUTCDE 11)
         LA    R1,4                         TO AVOID IEF170I 3
         LH    R0,MESSAGE              GET MESSAGE AREA LENGTH
         SR    R0,R1                   GET MESSAGE TEXT LENGTH
         LA    R1,MESSAGE+4            POINT TO MESSAGE TEXT
         TPUT  (1),(0),R               SEND MESSAGE TO TSO TERMINAL
ERRORWTO MVI   MESSAGE+2,X'80'         SET MCS FLAG
         WTO   MF=(E,MESSAGE)          WRITE IT OUT
         LA    R2,4                    SET RETURN CODE - CAN JOB
         B     RETURN                  AND RETURN
         SPACE 2
*
*        NO ACUCB WAS FOUND FOR THE JOB. IF THE JOB IS AN
*        STC, AND STC=NO WAS GENNED IN THE FDR, DO NOT ISSUE
*        THE 'ACF2 NOT STARTED' MESSAGE.
*
CKSTCOPT LR    R9,R15                  ADDRESS ACF ASVT
         USING ACFASVT,R9
         CLI   ASVJID,C'S'             IS IT AN STC?
         BNE   NOACF2                  NO: ISSUE ERROR MSG
         TM    ACCC2FLG,ACCC2STC       STC = YES GENNED IN FDR?
         BZ    ALLOW                   NO: BYPASS ERROR MSG
*        B     NOACF2                  YES: ISSUE ERROR MSG
*
*        ACF2 IS NOT UP, ISSUE WARNING MESSAGE
*        TO THE OPERATOR AND ALLOW JOB TO CONTINUE
*
NOACF2   WTO   'UJI001W WARNING - ACF2 NOT STARTED',                   X
               ROUTCDE=(1,9,11)
         SR    R2,R2                   CLEAR RETURN CODE
*        B     RETURN                  AND RETURN
         SPACE 2
*
*        DROP ADDRESSABILITY TO ACF2 RELATED CONTROL BLOCKS
*
         DROP  R7,R8,R9,R3             DROP ADDRESSABILITY
         SPACE 2
*
*        ENTER RETURN SEQUENCE TO RETURN CONTROL
*        TO CALLING ROUTINE
*
RETURN   LR    R1,R13                  COPY WORKAREA ADDR
         L     R13,4(,R13)             GET OLD SAVE AREA ADDR
         FREEMAIN RU,LV=WORKLEN,A=(1),SP=229   FREE STORAGE
         LR    R15,R2                  COPY RETURN CODE
         RETURN (14,12),RC=(15)        AND RETURN
         SPACE 2
*
*        CONSTANTS, EXECUTED INSTRUCTIONS, ETC.
*
MISSING  DC    AL2(LMISSING,0)         MISSING ERROR MSG HEADER
         DC    C'UJI003I ACCOUNT NOT SPECIFIED ON JOB CARD'
LMISSING EQU   *-MISSING               MESSAGE LENGTH
         SPACE
INVALID  DC    AL2(LINVALID,0)         INVALID ERROR MSG HEADER
         DC    C'UJI004I ACCOUNT LENGTH INVALID - MUST BE 8'
LINVALID EQU   *-INVALID               MESSAGE LENGTH
         SPACE
CANTUSE  DC    AL2(LCANTUSE,0)         INVALID ERROR MSG HEADER
         DC    C'UJI005I USE OF ACCOUNT '
MSGACCT  EQU   *-CANTUSE
         DC    C'________ NOT ALLOWED FOR ID '
MSGUSER  EQU   *-CANTUSE
         DC    C'________'
LCANTUSE EQU   *-CANTUSE               MESSAGE LENGTH
         SPACE
MVCACCT  MVC   ACGRNAME(0),2(R1)       COPY ACCOUNT FIELD
ROUTCDE  DC    XL4'000080A0'           DESC=0,ROUT=(1,9,11)
         SPACE
         LTORG
         SPACE
         DS    0D                      END OF CSECT
         TITLE 'DSECTS'
         IEFJMR
         END
