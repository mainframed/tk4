GRPSTR   TITLE 'ROUTINE TO SET A TSO CLIST VARIABLE TO THE VALUE OF THE*
                RACF GROUP NAME'
***********************************************************************
*                                                                     *
*  GRPSTR IS A TSO COMMAND PROCESSOR WHICH OBTAINS THE RACF           *
*  GROUP NAME VIA THE RACF ACEE.  IT THEN USES THE TSO/E CLIST        *
*  VARIABLE ACCESS MODULE (IKJCT441) TO SET A CLIST VARIABLE          *
*  &GRPSTR TO THIS VALUE.                                             *
*                                                                     *
*  GRPSTR CAN ONLY BE SUCCESSFULLY INVOKED WITHIN A TSO COMMAND       *
*  PROCEDURE (CLIST).  UPON RETURN, GRPSTR ISSUES A RETURN CODE       *
*  OF 8 IF RACF IS NOT ACTIVE, ELSE THE RETURN CODE FROM IKJCT441.    *
*                                                                     *
***********************************************************************
         SPACE 2
GRPSTR   START 0
         EJECT
***********************************************************************
*                                                                     *
*  REGISTER EQUATES                                                   *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                   WORK REGISTER
R1       EQU   1                   WORK REGISTER
R2       EQU   2                   WORK REGISTER
R3       EQU   3                   WORK REGISTER
R4       EQU   4                   WORK REGISTER
R5       EQU   5                   WORK REGISTER
R12      EQU   12                  CSECT BASE REGISTER
R13      EQU   13                  SAVE REGISTER
R14      EQU   14                  LINK REGISTER
R15      EQU   15                  ENTRY POINT AND RETURN CODE REG
         EJECT
***********************************************************************
*                                                                     *
*  HOUSEKEEPING                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
GRPSTR   CSECT
         SAVE  (14,12),,GRPSTR     SAVE REGISTERS
         LR    R12,R15
         USING GRPSTR,R12          ESTABLISH CSECT BASE REGISTER
         GETMAIN R,LV=WALEN        OBTAIN STORAGE FOR WORK AREA
         ST    R13,4(,R1)
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         LR    R13,R1
         USING GRPSTRWA,R13        ADDRESSABILITY TO WORK AREA
         SPACE 2
***********************************************************************
*                                                                     *
*  OBTAIN RACF GROUP NAME                                             *
*                                                                     *
***********************************************************************
         SPACE 2
         USING PSA,0
         L     R15,PSAAOLD         ADDRESS OF CURRENT ASCB
         USING ASCB,R15
         L     R15,ASCBASXB        ADDRESS OF CURRENT ASXB
         DROP  R15                 ASCB
         USING ASXB,R15
         ICM   R15,15,ASXBSENV     ADDRESS OF ACEE
         BZ    NOACF
         DROP  R15                 ASXB
         USING ACEE,R15
         LA    R5,ACEEGRPN         ADDRESS OF GROUP NAME
         DROP  R15                 ACEE
         B     SETVAR              GO TO SET TSO VARIABLE
NOACF    LA    R15,8               SET RETURN CODE TO 8
         B     RETURN              RETURN
         SPACE 2
***********************************************************************
*                                                                     *
*  SET VARIABLE &GRPSTR TO THE VALUE OF THE RACF GROUP NAME           *
*                                                                     *
***********************************************************************
         SPACE 2
SETVAR   L     R15,CVTPTR          ADDRESS OF CVT
         USING CVT,R15             ADDRESSABILITY TO CVT
         L     R15,CVTTVT          ADDRESS OF TSVT
         USING TSVT,R15            ADDRESSABILITY TO TSVT
         L     R15,TSVTVACC        ADDRESS OF IKJCT441 ENTRY POINT
         MVC   CT441PRM(CTPRMLEN),CT441DUM INITIALIZE IKJCT441 PARMLIST
         ST    R5,VALUEPTR         STORE ADDRESS IN WORK AREA
         LA    R2,VALUEPTR         ADDRESS OF POINTER TO VARIABLE VALUE
         ST    R2,CTVALPTR         STORE IN IKJCT441 PARAMETER LIST
         LA    R1,CT441PRM         ADDRESS OF IKJCT441 PARAMETER LIST
         BALR  R14,R15             INVOKE IKJCT441 TO SET &GRPSTR
         SPACE 2
***********************************************************************
*                                                                     *
*  RETURN WITH RETURN CODE FROM IKJCT441 TO SET &GRPSTR               *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   LR    R2,R15              SAVE IKJCT441 RETURN CODE
         LR    R1,R13              ADDRESS OF GRPSTR WORK AREA
         L     R13,4(,R1)          RESTORE SAVE REGISTER
         FREEMAIN R,LV=WALEN,A=(1) FREE WORK AREA STORAGE
         LR    R15,R2              RESTORE IKJCCT441 RETURN CODE
         RETURN (14,12),RC=(15)    RETURN
         SPACE 2
***********************************************************************
*                                                                     *
*  STORAGE DECLARATIONS                                               *
*                                                                     *
***********************************************************************
         SPACE 2
CT441DUM DS    0F                  DUMMY IKJCT441 PARAMETER LIST
         DC    A(ECODE)            ADDRESS OF ENTRY CODE
         DC    A(NAMEPTR)          ADDRESS OF POINTER TO VARIABLE NAME
         DC    A(NAMELEN)          ADDRESS OF VARIABLE NAME LENGTH
         DC    A(0)                ADDRESS OF POINTER TO VARIABLE VALUE
         DC    A(VALUELEN)         ADDRESS OF VARIABLE VALUE LENGTH
         DC    X'80',AL3(TOKEN)    ADDRESS OF IKJCT441 TOKEN (UNUSED)
ECODE    DC    A(TSVEUPDT)         ENTRY CODE FOR VARIABLE CREATE
NAMEPTR  DC    A(NAME)             ADDRESS OF VARIABLE NAME
NAMELEN  DC    F'6'                LENGTH OF VARIABLE NAME
VALUELEN DC    F'8'                LENGTH OF VARIABLE VALUE
TOKEN    DC    F'0'                TOKEN FOR USE BY IKJCT441
NAME     DC    CL6'GRPSTR'         &GRPSTR VARIABLE NAME
         EJECT
***********************************************************************
*                                                                     *
*  DSECTS                                                             *
*                                                                     *
***********************************************************************
         SPACE 2
*  GRPSTR WORK AREA
         SPACE
GRPSTRWA DSECT
SAVE     DS    18F                 LOCAL SAVE AREA
CT441PRM DS    0F                  IKJCT441 PARAMETER LIST
CTECODE  DS    F                   ADDRESS OF ENTRY CODE
CTNAMPTR DS    F                   ADDRESS OF POINTER TO VARIABLE NAME
CTNAMLEN DS    F                   ADDRESS OF VARIABLE NAME LENGTH
CTVALPTR DS    F                   ADDRESS OF POINTER TO VARIABLE VALUE
CTVALLEN DS    F                   ADDRESS OF VARIABLE VALUE LENGTH
CTTOKEN  DS    F                   ADDRESS OF IKJCT441 TOKEN (UNUSED)
CTPRMLEN EQU   *-CT441PRM          LENGTH OF IKJCT441 PARAMETER LIST
VALUEPTR DS    F                   POINTER TO VARIABLE VALUE
WALEN    EQU   *-GRPSTRWA          LENGTH OF GRPSTR WORK AREA
         SPACE 2
         PRINT NOGEN
         SPACE
         CVT   DSECT=YES           MAP MVS CVT
         SPACE
         IKJTSVT                   MAP MVS TSVT
         SPACE
         IHAPSA                    MAP MVS PSA
         SPACE
         IHAASCB                   MAP MVS ASCB
         SPACE
         IHAASXB                   MAP MVS ASXB
         SPACE
         IHAACEE                   MAP RACF ACEE
         SPACE
         SPACE 2
         END
