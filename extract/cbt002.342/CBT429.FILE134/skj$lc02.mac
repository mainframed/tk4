         TITLE 'SKJ$LC02 - $LISTC/$SPACE CATALOG SEARCH'
SKJ$LC02 START 0
         EJECT
         SKJ$LCPD
         EJECT
         IKJEFFMT MTDSECT=YES
         EJECT
         CVT   DSECT=YES
         EJECT
***********************************************************************
*                                                                     *
*  EQUATES                                                            *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0                  WORK REGISTER
R1       EQU   1                  PARAMETER LIST ADDRESS REGISTER
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R11      EQU   11                 COMMON AREA BASE REGISTER
R12      EQU   12                 CSECT BASE REGISTER
R13      EQU   13                 SAVE REGISTER AND WORK AREA BASE
R14      EQU   14                 LINK REGISTER
R15      EQU   15                 ENTRY POINT AND RETURN CODE REGISTER
         SPACE 1
PERIOD   EQU   C'.'
CTGNAME  EQU   X'04'              CTGENT FIELD POINTS TO DSNAME
CTGGENLD EQU   X'01'              GENERIC LOCATE REQUEST
CTGRCATN EQU   X'20'              RETURN CATALOG NAME IN WORK AREA
CTGSUPLT EQU   X'10'              SUPERLOCATE FUNCTION
CTGAM0   EQU   X'01'              OS/VS2 CATALOG MANAGEMENT REQUEST
CTGF2WKA EQU   X'04'              FORMAT-2 WORK AREA (FULLWORD LENGTH)
MTMASK   EQU   X'81'              MTPL INSERT LENGTH MASK
         EJECT
***********************************************************************
*                                                                     *
*  HOUSEKEEPING                                                       *
*                                                                     *
***********************************************************************
         SPACE 1
SKJ$LC02 CSECT
         BC    15,14(,R15)
         DC    X'08'
         DC    CL8'SKJ$LC02'
         DS    0H
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15
         USING SKJ$LC02,R12            ESTABLISH CSECT BASE REGISTER
         ST    R13,SAVE+4
         LR    R2,R13
         LA    R13,SAVE                ESTABLISH LOCAL SAVE AREA
         ST    R13,8(,R2)
         L     R11,0(,R1)              PICK UP ADDRESS OF COMMON AREA
         USING SKJ$LCPD,R11            ADDRESSABILITY TO COMMON AREA
         SPACE 1
***********************************************************************
*                                                                     *
*  INITIALIZE CATALOG PARAMETER LIST AND WORK AREA                    *
*                                                                     *
***********************************************************************
         SPACE 1
         LH    R2,$LCINDL              LENGTH OF INDEX NAME
         CH    R2,H43                  LENGTH < 43?
         BNL   INVINDX                 NO  - CANNOT BE VALID INDEX NAME
         EX    R2,MOVINDX              SET INDEX FOR GENERIC LOCATE
         LA    R3,CTGSRCH+1(R2)
         MVI   0(R3),PERIOD            APPEND PERIOD FOR GENERIC LOCATE
         LA    R2,1(,R2)
         STC   R2,CTGSRCH              SET (UPDATED) LENGTH OF INDEX
         SPACE
         L     R2,CTGWALEN             SET INITIAL WORK AREA LENGTH
GETM     LR    R0,R2                   SET LENGTH FOR STORAGE MACRO
         STORAGE OBTAIN,LENGTH=(0),    OBTAIN STORAGE FOR CTGWA        *
               ADDR=(1),SP=0,BNDRY=PAGE,LOC=ANY
         ST    R1,$LCCTGWA             STORE ADDRESS IN COMMON
         OI    $LCFLGS2,$LCGETM        INDICATE WORK AREA ACQUIRED
         ST    R1,CTGWKA               STORE ADDRESS IN CTGPL
         ST    R2,0(,R1)               SET WORK AREA LENGTH
         LA    R3,8
         ST    R3,4(,R1)               SET INITIAL DATA LENGTH
         SPACE 1
***********************************************************************
*                                                                     *
*  ISSUE GENERIC LOCATE; IF SUCCESSFUL, PRODUCE REPORT                *
*                                                                     *
***********************************************************************
         SPACE 1
LOCATE   XC    CTGFDBK,CTGFDBK         CLEAR CTGPL FEEDBACK AREA
         LA    R1,CTGPL                LOAD PARAMETER LIST ADDRESS
         SVC   26                      INVOKE CATALOG MANAGEMENT
         NR    R15,R15                 LOCATE RETURN CODE ZERO?
         BNZ   LOCERR                  NO  - ANALYZE LOCATE RETURN CODE
         SPACE 1
         L     R3,$LCCTGWA             ADDRESS OF WORK AREA
         CLC   4(4,R3),F53             CATALOG NAME (AT MOST) RETURNED?
         BNH   INVINDX                 YES - CANNOT BE VALID INDEX NAME
         SPACE 1
         CALL  SKJ$LC03,((11))         CALL REPORT ROUTINE
         B     FREEM                   FREE GOTTEN STORAGE AND RETURN
         SPACE 1
***********************************************************************
*                                                                     *
*  LOCATE FAILED - ANALYZE LOCATE RETURN CODE                         *
*                                                                     *
***********************************************************************
         SPACE 1
LOCERR   STH   R15,LOCRC               SAVE LOCATE RETURN CODE
         CH    R15,RC08                LOCATE RETURN CODE 8?
         BE    INVINDX                 YES - SEND INVALID INDEX MESSAGE
         CH    R15,RC40                LOCATE RETURN CODE 40?
         BNE   LOCMSG                  NO  - SEND LOCATE ERROR MESSAGE
         SPACE
RETRY    TM    $LCFLGS2,$LCRETRY       SUPERLOCATE ALREADY RETRIED?
         BO    LOCMSG                  YES - SEND LOCATE ERROR MESSAGE
         OI    $LCFLGS2,$LCRETRY       SET SUPERLOCATE RETRY INDICATOR
         LR    R0,R2                   WORK AREA LENGTH
         L     R1,$LCCTGWA             WORK AREA ADDRESS
         L     R2,4(,R1)               GET WORK AREA LENGTH REQUIRED
         STORAGE RELEASE,LENGTH=(0),   RELEASE STORAGE FOR CTGWKA      *
               ADDR=(1),SP=0
         NI    $LCFLGS2,255-$LCGETM    INDICATE WORK AREA RELEASED
         SPACE
         LA    R2,4095(,R2)
         SRL   R2,12                   ROUND UP TO PAGE BOUNDARY
         SLL   R2,12
         B     GETM                    RETRY WITH LARGER WORK AREA
         SPACE 1
***********************************************************************
*                                                                     *
*  ISSUE ERROR MESSAGES                                               *
*                                                                     *
***********************************************************************
         SPACE 1
LOCMSG   LA    R1,$LCMTPL              ADDRESS OF IKJEFF02 PARM LIST
         USING MTDSECTD,R1             ADDRESSABILITY TO PARM LIST
         MVC   MTMSGID,MSGID1          SET MESSAGE NUMBER
         LA    R3,LOCRC+1              ADDRESS OF RETURN CODE INSERT
         ST    R3,MTINSRTS             STORE IN PARAMETER LIST
         MVI   MTINSRTS,MTMASK         SET LENGTH OF INSERT
         B     WRTMSG                  ISSUE ERROR MESSAGE
         SPACE
INVINDX  LA    R1,$LCMTPL              ADDRESS OF IKJEFF02 PARM LIST
         USING MTDSECTD,R1             ADDRESSABILITY TO PARM LIST
         MVC   MTMSGID,MSGID2          SET MESSAGE NUMBER
         LA    R3,$LCINDN              ADDRESS OF INDEX NAME INSERT
         ST    R3,MTINSRTS             STORE IN PARAMETER LIST
         LH    R3,$LCINDL              LENGTH OF INDEX NAME INSERT
         STC   R3,MTINSRTS             STORE IN PARAMETER LIST
         SPACE
WRTMSG   OI    $LCFLGS1,$LCERR         SET ERROR INDICATOR
         TM    $LCFLGS1,$LCSPFF        $LCSPF ENTRY?
         BO    FREEM                   YES - DON'T ISSUE ERROR MESSAGE
         CALLTSSR EP=IKJEFF02,MF=(E,(1))    ISSUE ERROR MESSAGE
         SPACE 1
***********************************************************************
*                                                                     *
*   RETURN                                                            *
*                                                                     *
***********************************************************************
         SPACE 1
FREEM    TM    $LCFLGS2,$LCGETM        WORK AREA GETMAINED?
         BZ    RETURN                  NO  - RETURN IMMEDIATELY
         LR    R0,R2                   WORK AREA LENGTH
         L     R1,$LCCTGWA             WORK AREA ADDRESS
         STORAGE RELEASE,LENGTH=(0),   RELEASE ACQUIRED STORAGE        *
               ADDR=(1),SP=0
         SPACE
RETURN   L     R13,SAVE+4              RESTORE REGISTER 13
         RETURN (14,12),RC=0           RETURN
         SPACE 1
***********************************************************************
*                                                                     *
*  STORAGE DECLARATIONS                                               *
*                                                                     *
***********************************************************************
         SPACE 1
SAVE     DS    18F                LOCAL SAVE AREA
CTGPL    DS    0F                 CATALOG PARAMETER LIST
CTGOPTN1 DC    AL1(CTGNAME+CTGGENLD)   OPTION BYTE 1
CTGOPTN2 DC    AL1(CTGRCATN)           OPTION BYTE 2
CTGOPTN3 DC    AL1(CTGSUPLT+CTGAM0)    OPTION BYTE 3
CTGOPTN4 DC    XL1'00'                 OPTION BYTE 4
CTGENT   DC    A(CTGSRCH)              ADDRESS OF SEARCH ARGUMENT
CTGCAT   DC    A(0)                    ADDRESS OF CATALOG NAME (NULL)
CTGWKA   DS    A                       ADDRESS OF CATALOG WORK AREA
CTGOPTNS DC    AL1(CTGF2WKA)           CATALOG REQUEST OPTIONS
         DC    XL1'00'                 RESERVED
CTGTYPE  DC    XL1'00'                 CATALOG ENTRY TYPE (NULL)
CTGNOFLD DC    AL1(0)                  NUMBER OF CTGFIELDS (NULL)
CTGFDBK  DS    XL2                     CATALOG MANAGEMENT FEEDBACK AREA
CTGFBFLG DC    XL1'00'                 CATALOG MANAGEMENT FLAGS
         DC    XL1'00'                 RESERVED
CTGPSWD  DC    A(0)                    ADDRESS OF PASSWORD (NULL)
CTGWALEN DC    F'32768'           INITIAL WORK AREA LENGTH
F53      DC    F'53'
MOVINDX  MVC   CTGSRCH+1(0),$LCINDN    INDEX NAME MOVE INSTRUCTION
LOCRC    DS    H                  GENERIC LOCATE RETURN CODE
RC08     DC    H'8'
RC40     DC    H'40'
RC44     DC    H'44'
H43      DC    H'43'
CTGSRCH  DC    CL44' '            CATALOG SEARCH ARGUMENT (INDEX NAME)
MSGID1   DC    CL4'S03A'
MSGID2   DC    CL4'F05 '          MESSAGE IDENTIFIERS
         SPACE 2
         END   SKJ$LC02
