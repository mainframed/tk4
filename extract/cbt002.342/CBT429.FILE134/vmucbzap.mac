VMUCBZAP TITLE 'ZAP UCBVTOC OF VM DISK TO POINT TO PHONY OS VTOC'
**********************************************************************
*                                                                    *
*                                                                    *
*    THE PURPOSE OF THIS PROGRAM IS TO ZAP A SELECTED UCB IN THE     *
*    NUCLEUS WHICH CORRESPONDS TO AN ONLINE VM DISK TO ALLOW THE     *
*    DISK TO BE PROCESSED BY OS DISK UTILITIES.  THE UPDATED FIELD   *
*    IS UCBVTOC WHICH CONTAINS THE RELATIVE (FROM START OF VOLUME)   *
*    TRACK ADDRESS OF THE VTOC.  THIS IS CHANGED TO POINT TO THE     *
*    LAST TRACK OF THE VOLUME WHICH SHOULD CONTAIN A PRE-FORMATTED   *
*    ONE TRACK OS VTOC.  THE VOLUME LABEL ON TRACK ZERO WHICH        *
*    CONTAINS THE PERMANENT VTOC POINTER IS NOT ALTERED.  THE        *
*    VOLUME ALLOCATED TO DDNAME "VMDAUNIT" WILL BE PROCESSED.        *
*    ALLOCATION ENSURES THAT THE VOLUME IS ONLINE AND HAS A          *
*    STANDARD VOLUME LABEL.  THE SELECTED UCB IS CHECKED FOR         *
*    STANDARD FORMAT, DIRECT ACCESS DEVICE CLASS, 3350 DEVICE        *
*    TYPE, AND NON-OS FORMAT VTOC POINTER.  AN OS FORMAT VTOC        *
*    POINTER POINTS TO RECORD ONE OF ANY TRACK EXCEPT TRACK ZERO.    *
*                                                                    *
*    IF UCBVTOC IS UPDATED IN A SHARED DASD UCB THEN THE SHARED      *
*    BIT (DA2HEX20 OF UCBTBYT2) IS RESET TO PREVENT VM HANGING       *
*    FOR THE DURATION OF A DISK BACKUP.                              *
*                                                                    *
*    THIS PROGRAM IS DESIGNED TO BE USED IN JOB STREAMS PRIOR        *
*    TO A DISK BACKUP STEP.  THE BACKUP STEP SHOULD NOT RUN IF       *
*    VMUCBZAP ABENDS, OR ISSUES A COND CODE GREATER THAN FOUR.       *
*                                                                    *
*    ONLY 3350 VOLUMES ARE SUPPORTED.       GP@SECV  10 JULY 1985    *
*                                                                    *
*    THIS PROGRAM MUST RUN APF AUTHORIZED BECAUSE KEY ZERO IS        *
*    REQUIRED TO ALTER THE UNIT CONTROL BLOCK.  MODESET IS ISSUED    *
*    AFTER ALL CHECKS HAVE BEEN VERIFIED.                            *
*                                                                    *
*    RETURN CODES -                                                  *
*                                                                    *
*               0 - UCBVTOC REPLACED                                 *
*               4 - UCBVTOC UNCHANGED - ALREADY IN OS FORMAT         *
*               8 - UCBVTOC UNCHANGED - DASD IS NOT A 3350           *
*              12 - UCBVTOC UNCHANGED - UNIT IS NOT A DASD           *
*              16 - UCBVTOC UNCHANGED - OPERATOR DISALLOWED REQUEST  *
*              20 - DDNAME "VMDAUNIT" NOT DEFINED                    *
*                                                                    *
*    PROGRAMMED ABEND S0C1 -                                         *
*              NON-STANDARD UCB ENCOUNTERED, OR                      *
*              BYTE 4 OF UCBVTOC WAS NOT A NULL                      *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT                                                       *
**********************************************************************
*                                                                    *
*                                                                    *
*    CHANGES -                                                       *
*                                                                    *
*                                                                    *
*    MODIFIED TO ALSO CHANGE THE NUMBER OF DSCB'S PER TRACK FROM 2   *
*    TO 47 (FOR 3350 ONLY) IN THE VTOC INFORMATION BLOCK (VIB).      *
*                                                                    *
*    PROGRAMMED ABEND S0C1 -                                         *
*              VIB DID NOT INDICATE 2 DSCB'S PER TRACK               *
*                                       GP@SECV  08 NOVEMBER 1985    *
*                                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
VMUCBZAP CSECT
         SAVE  (14,12)
         LR    11,15
         USING VMUCBZAP,11
         L     4,X'21C'         GET POINTER TO CURRENT TCB.
         L     4,12(0,4)        POINT TO TIOT.
         LA    4,24(0,4)        POINT TO TIOELNGH.
         SR    5,5              CLEAR REGISTER.
         SPACE
CHKDDNAM CLC   4(8,4),VMDAUNIT  IS THIS THE RIGHT DDNAME?
         BE    CHECKUCB         YES, FILE EXISTS SO VERIFY THE UCB.
         IC    5,0(0,4)         NO, GET TIOT ENTRY LENGTH.
         AR    4,5              POINT TO NEXT TIOT ENTRY.
         CLI   0(4),0           ZERO LENGTH ENTRY?
         BNE   CHKDDNAM         NO, CHECK OUT THIS ENTRY.
         WTO   'VMUCBZAP - VMDAUNIT DDNAME NOT DEFINED - NOTHING DONE',X
               ROUTCDE=(9,11)
         LA    15,20            RETURN CODE IS 20.
         B     VMUCBEND         EXIT.
         SPACE
CHECKUCB ICM   5,7,17(4)        GET UCB ADDRESS.
         CLI   2(5),X'FF'       IS THIS A STANDARD UCB?
         BE    CHECKTYP         YES.
UCBABEND WTO   'VMUCBZAP - INVALID UCB FOUND - ABENDING',ROUTCDE=(9,11)
         WTO   'VMUCBZAP - R4 => TIOT ENTRY - R5 => UCB',ROUTCDE=(11)
         DC    H'0'             ABEND S0C1.
         B     VMUCBEND         EXIT.
         SPACE
CHECKTYP CLI   18(5),X'20'      IS THIS A DASD UNIT?
         BE    CHEK3350         YES.
         MVC   NOTDAMSG+19(3),13(5)     SHOW UNIT ADDRESS.
NOTDAMSG WTO   'VMUCBZAP - CUA IS NOT A DASD UNIT - NOTHING DONE',     X
               ROUTCDE=(9,11)
         LA    15,12            RETURN CODE IS 12.
         B     VMUCBEND         EXIT.
         SPACE
CHEK3350 CLI   19(5),X'0B'      IS THIS A 3350 UNIT?
         BE    DVTY3350         YES.
         MVC   NTDT3350+19(3),13(5)   SHOW UNIT ADDRESS.
         MVC   NTDT3350+23(6),28(5)   SHOW VOLUME SERIAL NUMBER.
NTDT3350 WTO   'VMUCBZAP - CUA VOLSER IS NOT A 3350 - NOTHING DONE',   X
               ROUTCDE=(9,11)
         LA    15,8             RETURN CODE IS 8.
         B     VMUCBEND         EXIT.
         SPACE
DVTY3350 MVC   INVTOC+19(3),13(5)    SHOW UNIT ADDRESS.
         MVC   INVTOC+23(6),28(5)    SHOW VOLUME SERIAL NUMBER.
         UNPK  INVTOC+41(9),24(5,5)  SHOW UCBVTOC VALUE.
         TR    INVTOC+41(8),HEX-C'0' SHOW IN EBCDIC.
         MVI   INVTOC+49,C' '        ERASE RUBBISH.
INVTOC   WTO   'VMUCBZAP - CUA VOLSER UCBVTOC IS 12345678 (TTR0)',     X
               ROUTCDE=(9,11)
         CLI   27(5),0          IS 0 PART OF TTR0 ZERO?
         BNE   UCBABEND         NO, UCB ERROR SO GO AND ABEND.
         CLI   ZAPREPLY,C'Y'    HAS WE CHANGED UCBVTOC?
         BE    UCBZAPED         YES, EXIT.
         SPACE
         ICM   2,3,24(5)        GET TT PART OF TTR0 IN UCBVTOC.
         BZ    ISVMDISK         VTOC STARTS ON TRACK 0, SO NOT OS VTOC.
         CLI   26(5),1          DOES VTOC BEGIN AT FIRST RECORD OF TRK?
         BNE   ISVMDISK         NO, SO NOT AN OS VTOC.
         MVC   OSPTRMSG+19(3),13(5)    SHOW UNIT ADDRESS.
         MVC   OSPTRMSG+23(6),28(5)    SHOW VOLUME SERIAL NUMBER.
OSPTRMSG WTO   'VMUCBZAP - CUA VOLSER HAS OS VTOC POINTER - NOTHING DONX
               E',ROUTCDE=(9,11)
         LA    15,4             RETURN CODE IS 4.
         B     VMUCBEND         EXIT.
         SPACE
ISVMDISK MVC   VTOCWTOR+27(3),13(5)    SHOW UNIT ADDRESS.
         MVC   VTOCWTOR+31(6),28(5)    SHOW VOLUME SERIAL NUMBER.
VTOCWTOR WTOR  'VMUCBZAP - CUA VOLSER - OK TO ZAP UCBVTOC? (''Y'' OR ''X
               N'')',ZAPREPLY,1,VMUCBECB,ROUTCDE=(1,2,4,6)
         WAIT  1,ECB=VMUCBECB,LONG=YES
         SPACE
         CLI   ZAPREPLY,C'Y'    DID OPERATOR SAY YES?
         BE    GOZAPUCB         YES, LET'S DO IT.
         SPACE
         CLI   ZAPREPLY,C'N'    DID OPERATOR SAY NO?
         BNE   ASKRETRY         NO, ASK OPERATOR AGAIN.
         LA    15,16            RETURN CODE IS 16.
         B     VMUCBEND         EXIT.
         SPACE
ASKRETRY XC    VMUCBECB,VMUCBECB    RESET ECB.
         B     VTOCWTOR             RE-ISSUE WTOR.
         SPACE
GOZAPUCB MODESET KEY=ZERO       GET INTO STORAGE KEY ZERO.
         MVC   24(2,5),LASTRACK REPLACE TT PART OF UCBVTOC.
         MVI   26(5),1          REPLACE R PART OF UCBVTOC.
         TM    17(5),X'20'      IS THIS DASD SHARED?
         BZ    ZAPVIB           NO.
         SPACE
         XI    17(5),X'20'      RESET SHARED BIT.
         MVC   SHARDMSG+19(3),13(5)   SHOW UNIT ADDRESS.
         MVC   SHARDMSG+23(6),28(5)   SHOW VOLUME SERIAL NUMBER.
SHARDMSG WTO   'VMUCBZAP - CUA VOLSER - SHARED BIT HAS BEEN RESET',    X
               ROUTCDE=(4,6,9,11)
ZAPVIB   L     6,20(,5)         POINT TO COMMON UCB EXTENSION.
         L     6,24(,6)         POINT TO DASD CLASS EXTENSION.
         L     6,4(,6)          POINT TO VTOC INFORMATION BLOCK.
         CLI   8(6),2           ONLY FORMAT-4 AND FORMAT-5 IN VTOC?
         BNE   UCBABEND         NO, VIB ERROR SO GO AND ABEND.
         MVI   8(6),47          INDICATE 47 DSCB'S PER TRACK FOR 3350.
         MODESET KEY=NZERO      RETURN TO USER KEY.
         B     DVTY3350         REPORT NEW UCBVTOC VALUE.
         SPACE
UCBZAPED SR    15,15            UCB UPDATED OK, SO RETURN CODE IS 0.
         SPACE
VMUCBEND RETURN (14,12),RC=(15) GO AWAY.
         SPACE 3
VMUCBECB DC    F'0'
LASTRACK DC    AL2(30*555-1)    LAST TRACK OF A 3350.
VMDAUNIT DC    C'VMDAUNIT'
HEX      DC    C'0123456789ABCDEF'
ZAPREPLY DC    C'?'
         SPACE 3
         END
