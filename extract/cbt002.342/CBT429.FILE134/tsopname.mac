         TITLE 'INITIALIZE JES2 PROGRAMMER NAME FOR TSO USERS'
TSOPNAME CSECT
TSOPNAME AMODE 31          REQUIRED FOR HJE3311 JES2 3.1.1
TSOPNAME RMODE ANY
         SPACE 2
* ASSEMBLY REQUIRES //ASM.SYSLIB DD  DSN=SYS1.MACLIB,DISP=SHR
*                   //           DD  DSN=SYS1.AMODGEN,DISP=SHR
*                   //           DD  DSN=SYS1.HASPSRC,DISP=SHR
* KEY ZERO REQUIRED FOR UPDATING PROGRAMMER NAME
* COPIED FROM ROOM FROM CBT TAPE -- GREG PRICE  SEPT 1986.
*    THE CPPL IS NOT USED.  THIS PROGRAM IS RE-ENTRANT.
         SPACE 2
         SAVE  (14,12),,TSOPNAME_V3R1  SAVE REGISTERS
         LR    R12,R15                 LOAD R12 WITH ENTRY POINT
         USING TSOPNAME,R12            ESTABLISH ADDRESSABILITY
         L     R7,CVTPTR               LOAD ADDR OF CVT POINTER
         L     R7,CVTTCBP-CVTMAP(R7)   LOAD ADDR OF NEXT/CURR TCB'S
         L     R8,12(R7)               LOAD ADDR OF CURRENT ASCB
         L     R8,ASCBASXB-ASCB(R8)    LOAD ADDR OF CURRENT ASXB
         L     R8,ASXBSENV-ASXB(R8)    LOAD ADDR OF RACF ACEE
         ICM   R8,15,100(R8)           LOAD ADDR OF RACF PGMR NAME STRG
         BZ    NOPGMRNM                SKIP PROCESSING IF NO NAME
         L     R7,4(R7)                LOAD ADDR OF CURRENT TCB
         L     R7,TCBJSCB-TCB(R7)      LOAD ADDR OF JSCB
         L     R7,JSCBACT-IEZJSCB(R7)  LOAD ADDR OF ACTIVE JSCB
         L     R7,JSCBSSIB-IEZJSCB(R7) LOAD ADDR OF LIFE-OF-JOB SSIB
         L     R7,SSIBSUSE-SSIB(R7)    LOAD ADDR OF JES2 SJB
         L     R7,SJBJCT-SJB(R7)       LOAD ADDR OF JES2 JCT
         MODESET KEY=ZERO
         MVC   JCTPNAME-JCT(20,R7),1(R8) CHANGE JES2 PGMR NAME
         MODESET KEY=NZERO
         RETURN (14,12),RC=0
NOPGMRNM RETURN (14,12),RC=4
         EJECT
*********************** EQUATES AND DSECTS ****************************
         PRINT NOGEN
         CVT   DSECT=YES
         IKJTCB
         IEZJSCB
         IEFJSSIB
         IHAASCB
         IHAASXB
         IFGRPL
         $SJB
         $BUFFER
         $HASPEQU
         $JCT
         $TQE
         END   TSOPNAME
