PS4      CSECT
         SAVE  (14,12)
         LR    11,15
         USING PS4,11
         SR     2,2
         SR     3,3
         SR     4,4
         SR     5,5
         SR     6,6
         SR     7,7
         SR     8,8
         SR     9,9
L1       LH     2,STEPS                  LOAD NUMBER OF STEPS
L2       LH     4,X1FINAL                LOAD FINAL X1 CO-ORD
L3       SH     4,X1INIT                 SUBTRACT INIT X1 CO-ORD
L4       SRDA   4,32(0)                  SHIFT INTO EVEN-ODD PAIR
L5       DR     4,2                      DIVIDE MOVEMENT BY STEPS
L6       STH    5,X1INCR                 STORE XI INCREMENT
L7       LH     6,Y1FINAL                LOAD FINAL Y1 CO-ORD
L8       SH     6,Y1INIT                 SUBTRACT INIT Y1 CO-ORD
L9       SRDA   6,32(0)                  SHIFT INTO EVEN-ODD PAIR
L10      DR     6,2                      DIVIDE MOVEMENT BY STEPS
L11      STH    7,Y1INCR                 STORE Y1 INCREMENT
LOOP1    LH     5,X1INIT                 STORE LAST X1 VALUE
         LH     7,Y1INIT                 STORE LAST Y1 VALUE
         AH     5,X1INCR                 INCREMENT LAST X VALUE
L12      AH     7,Y1INCR                 INCREMENT LAST Y VALUE
L13      STH    5,X1INIT                 STORE NEW X1 VALUE
L14      STH    7,Y1INIT                 STORE NEW Y1 VALUE
L15      MVC    XI,X1INIT                MOVE NEW X1 INTO ORDER
L16      MVC    YI,Y1INIT                MOVE NEW Y1 INTO ORDER
L17      TPUT  WSFMSG,WSFMSGLN,NOEDIT    DRAW
L19      BCT   2,LOOP1               LOOP FOR NUMBER OF STEPS
L20      RETURN (14,12),RC=(15)
         DS    0D
STEPS    DC    X'0020'
X1INIT   DC    X'FF00'               X1 CO-ORD OF ANIM START
Y1INIT   DC    X'0060'               Y1 CO-ORD OF ANIM START
X1FINAL  DC    X'0100'               X1 CO-ORD OF ANIM END
Y1FINAL  DC    X'0001'               Y1 CO-ORD OF ANIM END
X1INCR   DC    X'0020'               NUMBER OF MOVEMENTS X
Y1INCR   DC    X'0020'               NUMBER OF Y MOVEMENTS
WSFMSG   DC    X'F3'                 WRITE STRUCTURED FIELD
*
* FIRST STRUCTURED FIELD
*
         DC    X'0004'               LENGTH OF FIRST STRUCTURED FIELD
         DC    X'03'                 ERASE/RESET
         DC    B'00000000'           IMPLICIT PARTITION SIZE - DEFAULT
*
* SECOND STRUCTURED FIELD
*
WSF2     DC    AL2(WSF2LEN)          LENGTH OF SECOND STRUCTURED FIELD
         DC    X'0F10'               GRAPHIC picture
         DC    X'00'                 PID
         DC    B'11000000'           FLAGS - SPAN : FIRST AND LAST
*                                          - MODE : INTERMEDIATE MODE
         DC    X'00'                 RESERVED
*
         DC    X'70'                 BEGIN segment
         DC    X'0C'                 LENGTH OF FOLLOWING PARAMETERS
         DC    CL4'CH01'             NAME OF PROCEDURE TO BE CREATED
         DC    B'01110100'           VISIBLE   NOHILITE
         DC    B'01101000'           noprol NEW SEG DATA
         DC    AL2(SEGLEN)           LENGTH OF PROCEDURE TO BE CREATED
         DC    X'00000000'           P/S NAME
SEGDATA  DC    X'0A05'               SET COLOR
         DC    X'0C00'               SET MIX
         DC    X'1807'               LINE TYPE
         DC    X'1901'               LINE WIDTH
         DC    X'C1'                 GLINE
         DC    X'08'                 LENGTH
XI       DC    X'0001'               X CO-ORD OF LINE START
YI       DC    X'0060'               Y CO-ORD OF LINE START
XF       DC    X'0000'               X CO-ORD OF LINE END
YF       DC    X'0000'               Y CO-ORD OF LINE END
SEGLEN   EQU   *-SEGDATA
WSF2LEN  EQU   *-WSF2
*
* THIRD STRUCTURED FIELD
*
WSF3     DC    AL2(WSF3LEN)          LENGTH OF THIRD STRUCTURED FIELD
         DC    X'40'                 OUTBOUND 3270DS
         DC    X'00'                 PARTITION ID (PID)
         DC    X'F1'                 WRITE COMMAND
         DC    X'C2'                 WCC - RESET KEYBOARD
WSF3LEN  EQU   *-WSF3
* LENGTH OF TPUT DATA
WSFMSGLN EQU   *-WSFMSG
BUFFER   DC    CL80' '
BUFFLEN  EQU   *-BUFFER
         END
