         TITLE 'IEFUJV - USER JOB VALIDATION EXIT ROUTINE'
***********************************************************************
*                                                                     *
*  CSECT IEFUJV                                                       *
*                                                                     *
*  FUNCTION                                                           *
*                                                                     *
*        IEFUJV IS A USER SMF JOB VALIDATION EXIT. IT RECEIVES        *
*        CONTROL FROM THE CONVERTER, VIA MODULE IEFVHEB, BEFORE EACH  *
*        JOB CONTROL STATEMENT (OR CATALOGED PROCEDURE STATEMENT),    *
*        IN THE INPUT STREAM IS CONVERTED; VIA MODULE IEFVHF, AFTER   *
*        ALL JCL IS CONVERTED; AND AGAIN, VIA MODULE IEFVHH, AFTER    *
*        ALL JCL IS INTERPRETED. IT IS NOT TAKEN FOR JCL COMMENT      *
*        STATEMENTS.                                                  *
*                                                                     *
*        THE FUNCTION OF THIS VERSION OF THE EXIT ROUTINE IS TO       *
*        CHECK WHETHER THE ACCOUNT NUMBER IS CODED ON THE JOB CARD.   *
*                                                                     *
*        IF IT IS THEN IT IS VALIDATED FOR LENGTH (8) AND CONTENT     *
*        (NUMERIC).  IF EITHER OF THESE TESTS FAIL THEN THE JOB       *
*        IS CANCELLED.                                                *
*                                                                     *
*        IF THE ACCOUNT NUMBER IS NOT SUPPLIED THEN A TEMPLATE OF     *
*        EIGHT (8) ZEROS IS INSERTED INTO THE JOB CARD.  IF THERE     *
*        IS NO ROOM FOR THIS INSERTION THEN THE JOB IS CANCELLED.     *
*                                                                     *
*                                                                     *
*  ENTRY POINT                                                        *
*                                                                     *
*        IEFUJV                                                       *
*                                                                     *
*  INPUT                                                              *
*                                                                     *
*        IEFUJV IS PASSED IN THE USUAL WAY VIA REGISTER 1 A POINTER   *
*        TO A LIST OF FOUR-BYTE ADDRESSES, AS FOLLOWS:                *
*                                                                     *
*        1. THE ADDRESS OF THE SMF COMMON EXIT PARAMETER AREA         *
*        2. THE ADDRESS OF AN 80-BYTE JCL STATEMENT IMAGE             *
*        3. THE ADDRESS OF A ONE-BYTE FIELD INDICATING THE TYPE       *
*           OF JCL STATEMENT, AS FOLLOWS:                             *
*              0 - NULL STATEMENT                                     *
*              1 - JOB STATEMENT                                      *
*              2 - EXEC STATEMENT                                     *
*              4 - DD STATEMENT                                       *
*              8 - PROC STATEMENT                                     *
*             16 - ALL JCL HAS BEEN CONVERTED                         *
*             32 - ALL JCL HAS BEEN INTERPRETED                       *
*             64 - JCL DEFINITION TABLE-DEFINED VERB                  *
*            128 - EXTENDED JCL STATEMENT                             *
*        4. THE ADDRESS OF A ONE-BYTE FIELD CONTAINING THE FIRST      *
*           BYTE OF THE JES CONVERTER PARAMETER FIELD                 *
*              1 - PROGRAMMER NAME REQUIRED                           *
*              2 - ACCOUNT NUMBER REQUIRED                            *
*              4 - OBTAIN SWA BLOCKS ABOVE 16MB VIRTUAL               *
*                                                                     *
*  OUTPUT                                                             *
*                                                                     *
*        IEFUJV RETURNS CONTROL TO THE CONVERTER WITH A RETURN CODE   *
*        IN REGISTER 15 AS FOLLOWS:                                   *
*              0 - CONTINUE PROCESSING                                *
*              4 - CANCEL THE JOB. IN THIS CASE, THE CHARACTERS       *
*                  'BAD-ACCT' ARE INSERTED IN BYTES 73-80 OF THE      *
*                  OFFENDING JCL STATEMENT.                           *
*                                                                     *
*  EXTERNAL ROUTINES                                                  *
*                                                                     *
*        NONE                   (NO STORAGE IS ACQUIRED OR FREED)     *
*                                                                     *
*  EXIT                                                               *
*                                                                     *
*        NORMAL - BR 14                                               *
*                                                                     *
*  ATTRIBUTES                                                         *
*                                                                     *
*        REENTRANT, REUSABLE, REFRESHABLE, AMODE=31 AND RMODE=ANY     *
*                                                                     *
*  WRITTEN BY                                                         *
*                                                                     *
*        GREG PRICE, FERNTREE COMPUTER SERVICES, 6TH JANUARY 1993.    *
*                                                                     *
***********************************************************************
         SPACE 5
IEFUJV   START 0
IEFUJV   AMODE 31
IEFUJV   RMODE ANY
         EJECT
***********************************************************************
*                                                                     *
*  DSECTS                                                             *
*                                                                     *
***********************************************************************
         SPACE 2
*                    SMFCEPA - SMF COMMON EXIT PARAMETER AREA
         SPACE 1
SMFCEPA  DSECT
         SPACE 1
SMFJOBN  DS    CL8                 JOBNAME
SMFTIME  DS    CL4                 TIME (WHEN JOB CARD WAS RECOGNIZED)
SMFDATE  DS    CL4                 DATE (WHEN JOB CARD WAS RECOGNIZED)
SMFSID   DS    CL4                 SYSTEM IDENTIFICATION
SMFUSID  DS    CL8                 USER IDENTIFICATION (OR BLANK)
SMFSTEP  DS    CL1                 STEP NUMBER
SMFOPT   DS    CL1                 SMF OPTION FLAGS
SMFREST  DS    CL1                 RESTART INDICATOR FLAGS
SMFCLASS DS    CL1                 JOB CLASS
SMFUSER  DS    CL4                 USER COMMUNICATIONS AREA
         SPACE 2
*                    UJVPARM - IEFUJV PARAMETER LIST
         SPACE 1
UJVPARM  DSECT
         SPACE 1
UJVCEPA  DS    A                   ADDRESS OF SMFCEPA
UJVCARD  DS    A                   ADDRESS OF JCL STATEMENT IMAGE
UJVTYPE  DS    A                   ADDRESS OF JCL STATEMENT TYPE FIELD
UJVCONV  DS    A                   ADDRESS OF CONVERTER PARAMETER FIELD
         EJECT
***********************************************************************
*                                                                     *
*  EQUATES                                                            *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                   UNUSED
R1       EQU   1                   PARAMETER LIST ADDRESS
R2       EQU   2                   ADDRESS OF JCL CARD TYPE FIELD
R3       EQU   3                   ADDRESS OF JCL CARD IMAGE
R4       EQU   4                   ADDRESS OF START OF AREA TO SCAN
R5       EQU   5                   ADDRESS OF END OF AREA TO SCAN
R6       EQU   6                   END OF ACCOUNTING DELIMITER
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                  SMF EXIT PARAMETER AREA BASE
R12      EQU   12                  CSECT BASE REGISTER
R13      EQU   13                  SAVE REGISTER
R14      EQU   14                  LINKAGE REGISTER
R15      EQU   15                  ENTRY AND RETURN CODE REGISTER
         SPACE
QUOTE    EQU   X'7D'               A QUOTE CHARACTER
         SPACE
NULL     EQU   X'00'               NULL CARD INDICATOR
JOB      EQU   X'01'               JOB CARD INDICATOR
EXEC     EQU   X'02'               EXEC CARD INDICATOR
DD       EQU   X'04'               DD CARD INDICATOR
PROC     EQU   X'08'               PROC CARD INDICATOR
CONV     EQU   X'10'               JCL CONVERTED INDICATOR
INTERP   EQU   X'20'               JCL INTERPRETED INDICATOR
JDT      EQU   X'40'               JDT-DEFINED VERB CARD INDICATOR
XTND     EQU   X'80'               EXTENDED CARD INDICATOR
         EJECT
***********************************************************************
*                                                                     *
*  HOUSEKEEPING                                                       *
*                                                                     *
***********************************************************************
         SPACE 2
IEFUJV   CSECT
         USING IEFUJV,R15              ESTABLISH CSECT BASE REGISTER
         B     UJVSTART                BRANCH AROUND MODULE ID
         DROP  R15
         DC    AL1(6)                  MODULE NAME
         DC    CL6'IEFUJV'             MODULE IDENTIFICATION
         DS    0H
UJVSTART STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15
         USING IEFUJV,R12              ESTABLISH CSECT BASE REGISTER
         SPACE
         USING UJVPARM,R1              ADRESSABILITY TO PARM LIST
         L     R11,UJVCEPA             ADDRESS OF SMF CEPA
         USING SMFCEPA,R11             ADDRESSABILITY TO SMF CEPA
         XR    R15,R15                 SET RETURN CODE TO 0 (CONTINUE)
         SPACE 2
         L     R2,UJVTYPE              ADDRESS OF JCL CARD TYPE FIELD
         CLI   0(R2),JOB               JOB STATEMENT?
         BNE   RETURN                  NO - TAKE NORMAL RETURN
         SPACE 2
***********************************************************************
*                                                                     *
*        SCAN JOB STATEMENT FOR ACCOUNTING DATA                       *
*                                                                     *
***********************************************************************
         SPACE
         L     R3,UJVCARD              ADDRESS OF JCL CARD IMAGE
         CLI   2(R3),C' '              JOB CARD CONTINUATION?
         BE    RETURN                  YES - TAKE NORMAL RETURN
         LA    R4,3(0,R3)              POINT TO START OF AREA TO SCAN
         LA    R5,53(0,R3)             POINT TO END OF AREA TO SCAN
         SPACE
JSCAN    CLC   CJOB,0(R4)              ' JOB ' FOUND?
         BE    GOTJVERB                YES - CONTINUE
         LA    R4,1(0,R4)              NO, INCREMENT SCAN ADDRESS
         CR    R4,R5                   END OF SCAN?
         BNH   JSCAN                   NO - CONTINUE SCAN
         B     FAILJOB                 YES - FAIL THE JOB
         SPACE
GOTJVERB LA    R4,5(,R4)               POINT PAST ' JOB '
         LA    R5,5(,R5)               SET NEW SEARCH LIMIT
JBLNKSCN CLI   0(R4),C' '              FOUND AN OPERAND?
         BNE   GOTJOP                  YES - CONTINUE
         LA    R4,1(0,R4)              NO, INCREMENT SCAN ADDRESS
         CR    R4,R5                   END OF SCAN?
         BNH   JBLNKSCN                NO - CONTINUE SCAN
         B     FAILJOB                 YES - FAIL THE JOB
         SPACE
GOTJOP   LA    R6,QUOTE                GET EXPECTED TRAILING DELIMITER
         CLI   0(R4),QUOTE             LEADING QUOTE?
         BE    CHKNULL                 YES, CHECK FOR NULL ACCOUNT
         LA    R6,C')'                 GET EXPECTED TRAILING DELIMITER
         CLI   0(R4),C'('              OPEN PARENTHESIS?
         BE    CHKNULL                 YES, CHECK FOR NULL ACCOUNT
         LA    R6,C','                 GET EXPECTED TRAILING DELIMITER
         BCTR  R4,0                    BACK UP ONE FOR NO DELIMITER
         CLI   1(R4),C'0'              ACCOUNT NUMBER?
         BNL   CHKACCT                 YES, CHECK ACCOUNT
         LA    R4,1(,R4)               NO, POINT BACK TO FIRST OPERAND
         SPACE
         LA    R6,59(,R3)              NO, POINT TO LAST BYTE TO MOVE
         CLC   0(10,R6),BLANKS         SPARE ROOM TO INSERT ACCOUNT?
         BNE   FAILJOB                 NO - FAIL THE JOB
         CLI   0(R4),C','              LEADING COMMA ON JOB CARD?
         BE    JAPOSIOK                YES, JOB ACCT POSITION IS OKAY
         BCTR  R6,0                    NO, ONE MORE BYTE TO SHUFFLE UP
         CLI   0(R6),C' '              STILL ROOM FOR SHUFFLE?
         BNE   FAILJOB                 NO - FAIL THE JOB
JAPOSIOK LA    R7,69(,R3)              POINT TO LAST TARGET BYTE
SHFFLOOP MVC   0(1,R7),0(R6)           SHUFFLE UP A CHARACTER
         CR    R4,R6                   WAS THAT THE LAST?
         BE    PUTZEROS                YES, GO LOAD ACCT TEMPLATE
         BCTR  R6,0                    BACK UP SOURCE POINTER
         BCT   R7,SHFFLOOP             BACK UP TARGET POINTER AND LOOP
PUTZEROS MVC   0(11,R4),ZEROACCT       SUPPLY ACCOUNT TEMPLATE
         B     RETURN
         SPACE
CHKNULL  CLM   R6,B'0001',1(R4)        TRAILING DELIMITER WITHOUT ACCT?
         BNE   CHKACCT                 NO, SOME ACCOUNT IS SPECIFIED
         LA    R6,61(,R3)              YES, POINT TO LAST BYTE TO MOVE
         CLC   0(8,R6),BLANKS          SPARE ROOM TO INSERT ACCOUNT?
         BNE   FAILJOB                 NO - FAIL THE JOB
         B     JAPOSIOK                YES, PROCEED WITH INSERT
CHKACCT  LA    R0,8                    GET EXPECTED ACCOUNT LENGTH
CHKACCTL CLI   1(R4),C'0'              NUMERIC CHARACTER?
         BL    FAILJOB                 NO - INVALID ACCOUNT CODE
         LA    R4,1(,R4)               YES, INCREMENT BYTE POINTER
         BCT   R0,CHKACCTL             CONTINUE
         CLM   R6,B'0001',1(R4)        EXPECTED TRAILING DELIMITER?
         BE    RETURN                  YES - ACCOUNT CODE CHECKS OUT
*        B     FAILJOB                 NO - BAD CODE OR INVALID JCL
         SPACE
FAILJOB  MVC   72(8,R3),CIEFUJV        MOVE 'BAD-ACCT' TO CARD IMAGE
         WTO   MF=(E,WTOFAIL)          LEAVE A CLUE ON SYSLOG
         LA    R15,4                   SET RETURN CODE TO 4
RETURN   L     R14,12(0,R13)           RESTORE LINKAGE REGISTER
         LM    R0,R12,20(R13)          RESTORE REMAINING REGISTERS
         BR    R14                     RETURN
         SPACE 2
***********************************************************************
*                                                                     *
*  STORAGE DECLARATIONS                                               *
*                                                                     *
***********************************************************************
         SPACE 2
WTOFAIL  WTO   'UJV001W  JOB FLUSHED DUE TO BAD ACCOUNT CODE',         +
               ROUTCDE=(2),MF=L
BLANKS   DC    CL10' '
CIEFUJV  DC    CL8'BAD-ACCT'           IDENTIFIER FOR CARD IN ERROR
CJOB     DC    CL5' JOB '              JOB CARD VERB
ZEROACCT DC    CL11'(00000000),'       ACCOUNT NUMBER TEMPLATE
         LTORG
         SPACE
         DS    0D                      END OF CSECT
         SPACE 3
         END
