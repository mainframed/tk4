       IDENTIFICATION                  DIVISION.
      *--------------                  ---------

       PROGRAM-ID.                     RIAWHO.

       AUTHOR.                         ROYAL INSURANCE.

      *REMARKS.
      *
      *     SEARCHES THE TCT.
      *
      *     IF USERID SUPPLIED,
      *        RETURNS THE TRANSACTION, TERMINAL ID AND NETNAME.
      *     IF TERMID SUPPLIED,
      *        RETURNS THE USERID, TRANSACTION AND NETNAME.
      *
      *     INFORMATION ABOUT THIS TRANSACTION IS ALSO DISPLAYED.
      *
      *
       ENVIRONMENT                     DIVISION.
      *-----------                     ---------

       CONFIGURATION                   SECTION.
      *-------------                   --------

       SOURCE-COMPUTER.                IBM.

       OBJECT-COMPUTER.                IBM.
      /
       DATA                            DIVISION.
      *----                            ---------

       WORKING-STORAGE                 SECTION.
      *---------------                 --------


       01  WS-COMMAREA                 PIC X(01).
           88  WS-SUBSEQUENT-TIME      VALUE 'S'.


       01  WS-DATE-FIELDS.

           03  WS-DDMMYY.
               05  WS-DD               PIC X(02).
               05  FILLER              PIC X(01).
               05  WS-MM               PIC 9(02).
               05  FILLER              PIC X(01).
               05  WS-YY               PIC X(02).

           03  WS-MONTH-X              PIC X(36)
               VALUE 'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.
           03  FILLER  REDEFINES  WS-MONTH-X.
               05  WS-MONTH            PIC X(03)  OCCURS 12.

           03  WS-TODAY.
               05  WS-TODAY-DD         PIC X(02).
               05  FILLER              PIC X(01)  VALUE SPACE.
               05  WS-TODAY-MMM        PIC X(03).
               05  FILLER              PIC X(01)  VALUE SPACE.
               05  WS-TODAY-YY         PIC X(02).


       01  WS-ERROR-MSGS.

           03  WS-ERROR-NO-INPUT       PIC X(38)
               VALUE 'USERID OR TERMINAL ID MUST BE ENTERED'.
           03  WS-ERROR-TERMID-NOT-FOUND PIC X(31)
               VALUE 'SPECIFIED TERMINAL ID NOT FOUND'.
           03  WS-ERROR-USERID-NOT-FOUND PIC X(26)
               VALUE 'SPECIFIED USERID NOT FOUND'.


       01  WS-MISC.

           03  WS-ABSTIME              PIC S9(10) COMP.
           03  WS-APPLID-TEXT.
               05  FILLER              PIC X(07)  VALUE 'APPLID='.
               05  WS-APPLID           PIC X(08).
           03  WS-INPUT-TERMID         PIC X(04).
           03  WS-INPUT-USERID         PIC X(08).
           03  WS-ONE                  PIC S9(04) COMP VALUE +1.
           03  WS-TCT-NETID            PIC X(08).
           03  WS-TCT-TERMID           PIC X(04).
           03  WS-TCT-USERID           PIC X(08).
           03  WS-TCT-TRANID           PIC X(04).


       01  WS-SYSTEM-RELEASE-FIELDS.

           03  WS-SYS-REL.
               05  WS-SYS-REL-VER      PIC 99.
               05  WS-SYS-REL-REL      PIC X.
               05  WS-SYS-REL-MOD      PIC X.
           03  WS-SYS-REL-EDIT.
               05  WS-SYS-REL-V        PIC Z9.
               05  FILLER              PIC X      VALUE '.'.
               05  WS-SYS-REL-R        PIC X.
               05  FILLER              PIC X      VALUE '.'.
               05  WS-SYS-REL-M        PIC X.

      /
           COPY RIAWHOS.


      /
       PROCEDURE                       DIVISION.
      *---------                       ---------

       A-MAINLINE                      SECTION.
      *-----------                     --------

           IF  EIBCALEN  = 0
               GO TO FIRST-TIME
           ELSE
               GO TO SUBSEQUENT-TIME.


       FIRST-TIME.

           MOVE SPACES TO TCTUSERO
                          TCTTERMO
                          TCTNETO
                          TCTTRANO
                          MSGO.

           PERFORM GET-THIS-TRANS-DATA.


           EXEC CICS SEND MAP('RIAWHOM')
                     ERASE
                     FREEKB
           END-EXEC.

           MOVE 'S' TO WS-COMMAREA.

           EXEC CICS RETURN
                     TRANSID(EIBTRNID)
                     COMMAREA(WS-COMMAREA)
                     LENGTH(WS-ONE)
           END-EXEC.


       SUBSEQUENT-TIME.

           EXEC CICS HANDLE AID
                     CLEAR(ALL-FINISHED)
           END-EXEC.

           EXEC CICS HANDLE CONDITION
                     MAPFAIL(NO-INPUT)
           END-EXEC.


           EXEC CICS RECEIVE MAP('RIAWHOM')
           END-EXEC.


           EXEC CICS INQUIRE TERMINAL
                     START
           END-EXEC.


           IF  TCTUSERL  NOT =  0
               GO TO USERID-SEARCH
           ELSE
               GO TO TERMID-SEARCH.

      /
       USERID-SEARCH.

           MOVE TCTUSERI TO WS-INPUT-USERID.

           EXEC CICS HANDLE CONDITION
                     END(USERID-NOT-FOUND)
           END-EXEC.

       NEXT-USERID.

           EXEC CICS INQUIRE TERMINAL(WS-TCT-TERMID)
                     NETNAME(WS-TCT-NETID)
                     TRANSACTION(WS-TCT-TRANID)
                     USERID(WS-TCT-USERID)
                     NEXT
           END-EXEC.

           IF  WS-TCT-USERID NOT = WS-INPUT-USERID
               GO TO NEXT-USERID.


           MOVE WS-TCT-USERID  TO TCTUSERO.
           MOVE WS-TCT-TERMID  TO TCTTERMO.
           MOVE WS-TCT-NETID   TO TCTNETO.
           MOVE WS-TCT-TRANID  TO TCTTRANO.

           GO TO SEND-MAP.


       USERID-NOT-FOUND.

           MOVE SPACES                    TO TCTTERMO
                                             TCTNETO
                                             TCTTRANO.
           MOVE WS-INPUT-USERID           TO TCTUSERO.
           MOVE WS-ERROR-USERID-NOT-FOUND TO MSGO.

           GO TO SEND-MAP.

      /
       TERMID-SEARCH.

           MOVE TCTTERMI TO WS-INPUT-TERMID.

           EXEC CICS HANDLE CONDITION
                     END(TERMID-NOT-FOUND)
           END-EXEC.

       NEXT-TERMID.

           EXEC CICS INQUIRE TERMINAL(WS-TCT-TERMID)
                     NETNAME(WS-TCT-NETID)
                     TRANSACTION(WS-TCT-TRANID)
                     USERID(WS-TCT-USERID)
                     NEXT
           END-EXEC.

           IF  WS-TCT-TERMID NOT = WS-INPUT-TERMID
               GO TO NEXT-TERMID.


           MOVE WS-TCT-USERID TO TCTUSERO.
           MOVE WS-TCT-TERMID TO TCTTERMO.
           MOVE WS-TCT-NETID  TO TCTNETO.
           MOVE WS-TCT-TRANID TO TCTTRANO.

           GO TO SEND-MAP.


       TERMID-NOT-FOUND.

           MOVE SPACES                    TO TCTUSERO
                                             TCTNETO
                                             TCTTRANO.
           MOVE WS-INPUT-TERMID           TO TCTTERMO.
           MOVE WS-ERROR-TERMID-NOT-FOUND TO MSGO.

           GO TO SEND-MAP.

      /
       NO-INPUT.

           MOVE WS-ERROR-NO-INPUT TO MSGO.

           GO TO SEND-MAP.

      /
       SEND-MAP.

           PERFORM GET-THIS-TRANS-DATA.


           EXEC CICS SEND MAP('RIAWHOM')
                     ERASE
                     FREEKB
           END-EXEC.


           MOVE 'S' TO WS-COMMAREA.

           EXEC CICS RETURN
                     TRANSID(EIBTRNID)
                     COMMAREA(WS-COMMAREA)
                     LENGTH(WS-ONE)
           END-EXEC.

      /
       ALL-FINISHED.

           EXEC CICS SEND
                     CONTROL
                     FREEKB
           END-EXEC.

           GOBACK.

      /
       GET-THIS-TRANS-DATA.

           EXEC CICS INQUIRE
                     TERMINAL(EIBTRMID)
                     USERID(RUNUSERO)
                     NETNAME(RUNNETO)
           END-EXEC.


           MOVE EIBTRMID    TO RUNTERMO.
           MOVE EIBTASKN    TO RUNTASKO.


           EXEC CICS INQUIRE
                     SYSTEM RELEASE(WS-SYS-REL)
           END-EXEC.

           MOVE WS-SYS-REL-VER  TO WS-SYS-REL-V.
           MOVE WS-SYS-REL-REL  TO WS-SYS-REL-R.
           MOVE WS-SYS-REL-MOD  TO WS-SYS-REL-M.
           MOVE WS-SYS-REL-EDIT TO RUNVRMO.


           EXEC CICS ASSIGN
                     APPLID(WS-APPLID)
           END-EXEC.

           MOVE WS-APPLID-TEXT TO RUNAPPLO.


           EXEC CICS ASKTIME
                     ABSTIME(WS-ABSTIME)
           END-EXEC.

           EXEC CICS FORMATTIME
                     ABSTIME(WS-ABSTIME)
                     DDMMYY(WS-DDMMYY)
                     DATESEP
                     TIME(RUNTIMEO)
                     TIMESEP('.')
           END-EXEC.

           MOVE WS-DD           TO WS-TODAY-DD.
           MOVE WS-MONTH(WS-MM) TO WS-TODAY-MMM.
           MOVE WS-YY           TO WS-TODAY-YY.

           MOVE WS-TODAY        TO RUNDATEO.
