         TITLE 'SKJ$LC00 - $LISTC/$SPACE ENTRY AND MAINLINE'
SKJ$LC00 START 0
         EJECT
         CVT   DSECT=YES
         EJECT
***********************************************************************
*                                                                     *
*  EQUATES                                                            *
*                                                                     *
***********************************************************************
         SPACE 1
R1       EQU   1                  PARAMETER LIST ADDRESS
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R12      EQU   12                 CSECT BASE REGISTER
R13      EQU   13                 SAVE REGISTER
R14      EQU   14                 LINK REGISTER
R15      EQU   15                 ENTRY AND RETURN CODE REGISTER
         SPACE 1
$LCLISTC EQU   X'80'              $LISTC ENTRY INDICATOR
$LCSPACE EQU   X'40'              $SPACE ENTRY INDICATOR
$LCSPFF  EQU   X'20'              $LCSPF ENTRY INDICATOR
$LCERR   EQU   X'01'              $LISTC/$SPACE/$LCSPF ERROR INDICATOR
         EJECT
***********************************************************************
*                                                                     *
*  HOUSEKEEPING                                                       *
*                                                                     *
***********************************************************************
         SPACE 1
SKJ$LC00 CSECT
SKJ$LC00 AMODE 31
SKJ$LC00 RMODE 24
         ENTRY $LISTC,$SPACE,$LCSPF
         SPACE 1
$LISTC   DS    0H
         BC    15,20(,R15)
         DC    AL1(15)
         DC    CL15'SKJ$LC00/$LISTC'
         STM   R14,R12,12(R13)         SAVE REGISTERS
         USING $LISTC,R15              ESTABLISH TEMPORARY BASE
         OI    $LCFLGS1,$LCLISTC       SET $LISTC INDICATOR ON
         B     START                   BRANCH TO COMMON CODE
         DROP  R15                     DROP TEMPORARY BASE
         SPACE 1
$SPACE   DS    0H
         BC    15,20(,R15)
         DC    AL1(15)
         DC    CL15'SKJ$LC00/$SPACE'
         DS    0H
         STM   R14,R12,12(R13)         SAVE REGISTERS
         USING $SPACE,R15              ESTABLISH TEMPORARY BASE
         OI    $LCFLGS1,$LCSPACE       SET $SPACE INDICATOR ON
         B    START                    BRANCH TO COMMON CODE
         DROP  R15                     DROP TEMPORARY BASE
         SPACE 1
$LCSPF   DS    0H
         BC    15,20(,R15)
         DC    AL1(15)
         DC    CL15'SKJ$LC00/$LCSPF'
         DS    0H
         STM   R14,R12,12(R13)         SAVE REGISTERS
         USING $LCSPF,R15              ESTABLISH TEMPORARY BASE
         OI    $LCFLGS1,$LCSPFF        SET $LCSPF INDICATOR ON
         DROP  R15                     DROP TEMPORARY BASE
         SPACE 1
START    BALR  R12,0
         USING *,R12                   ESTABLISH CSECT BASE REGISTER
         ST    R13,SAVE+4
         LR    R2,R13
         LA    R13,SAVE                ESTABLISH LOCAL SAVE AREA
         ST    R13,8(,R2)
         TM    $LCFLGS1,$LCSPFF        $LCSPF ENTRY?
         BZ    NOTSPF                  NO  - CONTINUE
         L     R2,0(,R1)               ADDRESS OF PARAMETER LIST
         ST    R2,$LCPARM              STORE IN COMMON AREA
         B     INIT                    GO TO PERFORM INITIALIZATIONS
NOTSPF   LM    R2,R5,0(R1)             PICK UP PARAMETERS (CPPL)
         STM   R2,R5,$LCCPPL           STORE IN COMMON AREA
         SPACE 1
***********************************************************************
*                                                                     *
*  PERFORM INITIALIZATIONS (PARSE INPUT COMMAND)                      *
*                                                                     *
***********************************************************************
         SPACE 1
INIT     CALL  SKJ$LC01,($LCWORK)      CALL INITIALIZATION ROUTINE
         TM    $LCFLGS1,$LCERR         INITIALIZATION SUCCESSFUL?
         BO    TERM                    NO - TERMINATE
         SPACE 1
***********************************************************************
*                                                                     *
*  LIST CATALOG OR DETERMINE SPACE USAGE                              *
*                                                                     *
***********************************************************************
         SPACE 1
PROCESS  CALL  SKJ$LC02,($LCWORK)      CALL CATALOG SEARCH ROUTINE
         SPACE 1
***********************************************************************
*                                                                     *
*  TERMINATE AND RETURN                                               *
*                                                                     *
***********************************************************************
         SPACE 1
TERM     CALL   SKJ$LC04,($LCWORK)     CALL TERMINATION ROUTINE
         XR     R15,R15                SET RETURN CODE TO ZERO
         TM     $LCFLGS1,$LCERR        HAS ERROR OCCURRED?
         BZ     RETURN                 NO - RETURN
         LA     R15,16                 SET RETURN CODE TO 16
         SPACE 1
RETURN   L     R13,SAVE+4              RESTORE REGISTER 13
         RETURN (14,12),RC=(15)        RETURN
         SPACE 1
***********************************************************************
*                                                                     *
*  STORAGE DECLARATIONS                                               *
*                                                                     *
***********************************************************************
         SPACE 1
*                  COMMON AREA (ELSEWHERE DESCRIBED BY DSECT SKJ$LCPD)
         SPACE 1
$LCWORK  DS    0F
$LCPARM  DS    0F                 ADDRESS OF $LCSPF PARM FIELD
$LCCPPL  DS    4F                 COMMAND PROCESSOR PARAMETER LIST
$LCMTPL  DS    0F                 MESSAGE ISSUER PARAMETER LIST
MTPLPTR  DC    A(MTCSECTP)        ADDRESS OF MESSAGE DESCRIPTION SECTN
MTCPPLP  DC    A($LCCPPL)         ADDRESS OF $LISTC/$SPACE'S CPPL
MTECBP   DC    A($LCECB)          ADDRESS OF $LISTC/$SPACE'S ECB
MTRESV1  DC    A(0)               RESERVED
         ORG   MTRESV1
MTHIGH   DC    BL1'10000000'      HIGH ORDER BIT ON (STANDARD LINKAGE)
         ORG   MTRESV1+4
MTCSECTP DC    V(SKJ$LCMT)        ADDRESS OF MESSAGE TEXT CSECT
MTSW1    DC    BL1'01000000'      INDICATE ISSUE MESSAGE AS PUTLINE
MTREPLYP DC    AL3(0)             ADDRESS OF REPLY FROM PUTGET (NULL)
MTSW2    DC    BL1'00100000'      INDICATE REMOVE X' ' FOR HEX INSERTS
MTRESV2  DC    AL3(0)             RESERVED
MTOLDPTR DC    A(0)               POINTER TO OLD (NULL)
MTEXTRLN DC    AL1(0)             LENGTH OF EXTRACT BUFFER (NULL)
MTEXTRBF DC    AL3(0)             ADDRESS OF EXTRACT BUFFER (NULL)
MTEXTRL2 DC    AL1(0)             LENGTH OF EXTRACT BUFFER (NULL)
MTEXTRB2 DC    AL3(0)             ADDRESS OF EXTRACT BUFFER (NULL)
MTMSGID  DC    CL4'    '          MESSAGE IDENTIFIER IN MESSAGE CSECT
MTINSRTS DC    4A(0)              MESSAGE INSERT INFORMATION
MTEND    EQU   *
         ORG   MTINSRTS
MTLEN    DS    AL1                LENGTH OF INSERT
         ORG   MTLEN+1
MTADDR   DS    AL3                ADDRESS OF INSERT
         ORG   MTEND
$LCECB   DC    F'0'               $LISTC/$SPACE/$LCSPF'S ECB
$LCODCB  DC    A(ODSDCB)          OUTPUT DATA SET DCB ADDRESS
$LCCTGWA DS    A                  ADDRESS OF CATALOG WORK AREA
$LCCREDT DC    F'0'               CREATION DATE THRESHOLD VALUE
$LCREFDT DC    F'0'               LAST REFERENCE DATA THRESHOLD VALUE
$LCTRKAL DC    F'0'               TOTAL OF TRACKS ALLOCATED
$LCKBAL  DC    F'0'               TOTAL KB ALLOCATED
$LCTRKUS DC    F'0'               TOTAL OF TRACKS USED
$LCKBUS  DC    F'0'               TOTAL KB USED
$LCINDEX DS    0H
$LCINDL  DS    H                  $LISTC/$SPACE/$LCSPF INDEX LENGTH
$LCINDN  DC    CL44' '            $LISTC/$SPACE/$LCSPF INDEX NAME
$LCDSN   DC    CL46''''           $LCSPF DATA SET NAME
$LCVSER  DC    CL6' '             $LCSPF VOLUME SERIAL
$LCTABL  DC    CL8'$LCTABL'       $LCSPF ISPF TABLE NAME
$LCFLGS  DS    0XL2               CONTROL FLAGS OR INDICATORS
$LCFLGS1 DC    X'00'
$LCFLGS2 DC    X'00'
         SPACE 1
*                  LOCAL VARIABLES
         SPACE 1
SAVE     DS    18F                LOCAL SAVE AREA
         SPACE 1
         PRINT NOGEN
ODSDCB   DCB   DSORG=PS,MACRF=PL,DCBE=ODSDCBE
ODSDCBE  DCBE  RMODE31=BUFF
         SPACE 2
         END   SKJ$LC00
