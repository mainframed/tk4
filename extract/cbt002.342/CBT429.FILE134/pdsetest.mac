PDSETEST CSECT
*
* PROGRAM TO ILLUSTRATE A DFP BUG.  QSAM READING OF PDSE DIRECTORY
* RETURNS A ZERO BLOCK AFTER THE FIRST GET.  THE SECOND GET RETURNS
* THE FIRST BLOCK, AND EXPECTED PROCESSING FOLLOWS.  THIS IS NOW FIXED
* BY APAR OY54003, WHICH IS PTF UY82238 ON 9302 FOR DFP 3.3.
*
         USING PDSETEST,R11
         STM   R14,R12,12(R13)
         LR    R11,R15
         LA    R2,SAVEAREA
         ST    R2,8(,R13)
         ST    R13,4(,R2)
         LR    R13,R2
         MVI   OPEND,X'80'
         OPEN  (DIRDCB,INPUT),MF=(E,OPEND)
GETLOOP  DS    0H
         GET   DIRDCB,DIRBLK
         UNPK  TPUTAREA(9),DIRBLK(5)
         UNPK  TPUTAREA+8(9),DIRBLK+4(5)
         UNPK  TPUTAREA+16(9),DIRBLK+8(5)
         UNPK  TPUTAREA+24(9),DIRBLK+12(5)
         UNPK  TPUTAREA+32(9),DIRBLK+16(5)
         UNPK  TPUTAREA+40(9),DIRBLK+20(5)
         UNPK  TPUTAREA+48(9),DIRBLK+24(5)
         UNPK  TPUTAREA+56(9),DIRBLK+28(5)
         UNPK  TPUTAREA+64(9),DIRBLK+32(5)
         TR    TPUTAREA(72),HEX-C'0'
         MVI   TPUTAREA+72,C' '
         LA    R1,TPUTAREA
         LA    R0,L'TPUTAREA
         TPUT  (1),(0),R
         B     GETLOOP
EOFCLOSE MVI   CLOSED,X'80'
         CLOSE (DIRDCB),MF=(E,CLOSED)
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
SAVEAREA DC    18F'0'
OPEND    DC    F'0'
CLOSED   DC    F'0'
DIRBLK   DC    CL256' '
TPUTAREA DC    CL80' '
HEX      DC    C'0123456789ABCDEF'
DIRDCB   DCB   DDNAME=PDSEFILE,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=256,   +
               BLKSIZE=256,EODAD=EOFCLOSE,BUFNO=4
         YREGS
         END
