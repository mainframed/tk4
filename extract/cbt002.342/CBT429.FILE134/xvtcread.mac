XVTCREAD TITLE 'XVTCREAD - VTOC READING SUBROUTINE'
         SPACE 2
* AUTHOR:  R. F. MORSE, MIT INSTRUMENTATION LABORATORY.
         SPACE
* FUNCTION: THIS SUBROUTINE READS THE VOLUME TABLE OF CONTENTS (VTOC)
*        FROM A DIRECT-ACCESS DEVICE AND PRESENTS IT TO THE CALLER
*        ONE RECORD (DSCB) AT A TIME.
         SPACE
* OPERATION: THIS ROUTINE IS A SPECIALIZED SEQUENTIAL ACCESS METHOD
*        FOR VTOC'S.  ITS ADVANTAGE OVER ORDINARY BSAM IS THAT IT READS
*        AN ENTIRE TRACK IN ONE REVOLUTION, THUS SAVING CONSIDERABLE
*        TIME.  THE ROUTINE HAS THREE CALL MODES:
*
*        0 - READ.  RETURNS WITH THE CORE ADDRESS OF A DSCB IN REGISTER
*              1.  THE CORE CONSISTS OF 148 CONSECUTIVE BYTES, CONTAIN-
*              ING THE COUNT (8 BYTES), KEY (44 BYTES), AND DATA (96
*              BYTES) FOR ONE DSCB.  RETURN CODES ARE:
*                      0 - NORMAL;
*                      4 - END OF FILE, NO DATA PRESENTED;
*                      8 - PERMANENT I/O ERROR.  THE KEY AND DATA AREAS
*                          WILL BE SET TO ZEROS; THE COUNT AREA WILL
*                          CONTAIN THE CORRECT CCHHR.  SINCE READING
*                          IS DONE A TRACK AT A TIME, ALL THE DSCB'S
*                          FOR THAT TRACK WILL BE MARKED IN ERROR.
*                          READING MAY CONTINUE ON TO THE NEXT TRACK.
*
*        1 - OPEN.  REGISTER 1 SHOULD POINT TO AN 8-BYTE FIELD
*              CONTAINING THE DDNAME TO BE USED IN THE DCB.  THE
*              CORRESPONDING DD CARD SHOULD SPECIFY A DISPOSITION OF
*              (OLD,KEEP).  RETURN CODES:
*                      0 - NORMAL;
*                      4 - UNABLE TO OPEN (PROBABLY MISSING DD CARD);
*                      8 - DD CARD DID NOT REFER TO A DIRECT-ACCESS
*                          DEVICE, OR DEVICE TYPE UNKNOWN.
*
*        2 - CLOSE.  NO ARGUMENTS ARE REQUIRED OR RETURNED.  RETURN
*              CODE IS 0.
         SPACE
* ENTRY POINTS:  ENTRY IS ALWAYS TO 'XVTCREAD' VIA A BALR 14,15 WITH
*        REGISTER 13 SET TO A SAVE AREA.  REGISTER 0 CONTAINS A
*        BINARY INTEGER TO INDICATE THE CALL MODE AND REGISTER 1
*        POINTS TO PARAMETERS AS REQUIRED FOR EACH MODE.
*        AN ADDITIONAL ENTRY POINT, XVTCDCB, IS DEFINED TO ALLOW ACCESS
*        TO THE VTOC DCB FROM CALLING PROGRAMS (E.G. XVTCLIST).
         SPACE
* DATA SETS:  READS VOLUME TABLE OF CONTENTS FROM ANY DIRECT-ACCESS
*        DEVICE.  USES EXCP TO EXECUTE A CHAINED CHANNEL PROGRAM TO
*        READ AN ENTIRE TRACK AT A TIME.
         SPACE
* EXTERNAL ROUTINES:  USES SUPERVISOR ROUTINE 'IECPCNVT' TO CONVERT
*        A RELATIVE TRACK NUMBER TO AN ABSOLUTE ADDRESS.
         SPACE
* EXITS - NORMAL:  RETURNS VIA REGISTER 14 WITH RETURN CODE IN REGISTER
*        15.  (SEE ABOVE FOR RETURN CODE VALUES.)
         SPACE
* EXITS - ERROR:  NONE.
         SPACE
* TABLES AND WORK AREAS:  DOES A GETMAIN TO OBTAIN A BUFFER LARGE
*        ENOUGH TO HOLD AN ENTIRE TRACK FROM THE DEVICE BEING READ.
         SPACE
* ATTRIBUTES:  SERIALLY REUSABLE.
         EJECT
* SECTION DEFINITION AND REGISTER ASSIGNMENTS:
         SPACE
XVTCREAD CSECT
         ENTRY XVTCDCB
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
RWA      EQU   2
RWB      EQU   3
RWC      EQU   4
RPARM    EQU   9
RRCODE   EQU   10              RETURN CODE REGISTER
RRET     EQU   11              LOCAL SUBROUTINE EXIT REGISTER
RBASE    EQU   12              LOCAL BASE REGISTER
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
* TAGS FOR CHANNEL COMMANDS AND FLAG BITS:
         SPACE
READR0   EQU   X'16'           READ RECORD 0
READCKD  EQU   X'1E'           READ COUNT, KEY, AND DATA
         SPACE
CC       EQU   X'40'           COMMAND CHAIN FLAG
SLI      EQU   X'20'           SUPPRESS LENGTH INDICATION FLAG
SKIP     EQU   X'10'           SKIP DATA TRANSFER FLAG
         SPACE 3
* COMMUNICATION VECTOR TABLE (CVT) DEFINITIONS:
         SPACE
CVT      EQU   16              LOCATION OF CVT BASE ADDRESS
CVTPCNVT EQU   28              OFFSET TO CONVERT ROUTINE ADDRESS
         EJECT
************
* PROLOGUE *
************
         SPACE
* ENTER HERE AND PERFORM STANDARD REGISTER SAVE AREA HOUSEKEEPING.
         SPACE
         SAVE  (14,12),,XVTCREAD
         SPACE
         LR    RBASE,R15               SET LOCAL BASE REGISTER
         USING XVTCREAD,RBASE
         LR    R14,R13                 SAVE CALLER'S R13
         LA    R13,SAVEAREA            ADDRESS LOCAL SAVE AREA
         ST    R13,8(,R14)             CHAIN FORWARD
         ST    R14,SAVEAREA+4          CHAIN BACKWARD
         SR    RRCODE,RRCODE           ZERO RETURN CODE REGISTER
         SPACE
* SELECT MODE FROM CONTENTS OF REGISTER 0.
         SPACE
         LA    RWA,3                   MASK FOR LOW 2 BITS
         NR    RWA,R0                  GET CALL MODE
         SLL   RWA,2                   MODE TIMES 4
         B     *+4(RWA)                BRANCH ON MODE
         SPACE
         B     GET                     MODE 0, GET A DSCB
         B     OPEN                    MODE 1, OPEN A NEW VTOC
         B     CLOSE                   MODE 2, CLOSE
         B     RETURN0                 MODE 3 NOT DEFINED, NO OP
         SPACE 3
***********
* RETURNS *
***********
         SPACE
RETURN8  LA    RRCODE,4(,RRCODE)       ENTRY FOR RETURN CODE 8
RETURN4  LA    RRCODE,4(,RRCODE)       ENTRY FOR RETURN CODE 4
RETURN0  LR    R15,RRCODE              ENTRY FOR RETURN CODE 0
         SPACE
         L     R13,SAVEAREA+4          RECOVER CALLER'S SAVE AREA
         L     R14,12(,R13)            LOAD RETURN ADDRESS
         LM    R2,R12,28(R13)          RESTORE OTHER GENERAL REGISTERS
         MVI   12(R13),X'FF'           SET RETURN FLAG ON
         BR    R14                     RETURN TO CALLER
         EJECT
*********************
* MODE 0 - GET DSCB *
*********************
         SPACE
* IF END-OF-FILE WAS REACHED, RETURN AT ONCE.
         SPACE
GET      DS    0H
         TM    MODESW,EOFSW            TEST END-OF-FILE BIT
         BO    RETURN4                 RETURN CODE 4 IF ON
         SPACE
* IF CHANNEL PROGRAM HAS BEEN STARTED, GO TO CHECK IT.  OTHERWISE,
* ASSUME THERE IS AT LEAST ONE FULL BUFFER.
         SPACE
         TM    MODESW,XCPRUN           TEST IF EXCP ISSUED
         BO    XCPTEST                 BRANCH IF SO
         SPACE
* SET BUFFER ADDRESS TO NEXT DSCB AND TEST IF LAST ON TRACK.  IF NOT,
* EXIT WITH ITS ADDRESS IN R1.
         SPACE
         L     RWA,DSCBADR             LOAD BUFFER POINTER
         LA    RWA,148(,RWA)           ADVANCE TO NEXT DSCB
NDXSTORE ST    RWA,DSCBADR             STORE UPDATED POINTER
         C     RWA,DSCBLIM             TEST IF LAST DSCB IN BUFFER
         BNL   LASTDSCB                BRANCH IF SO
         LR    R1,RWA                  PASS ADDRESS TO USER
GETOUT   TM    MODESW,RDERR            TEST IF ERROR ON THIS TRACK
         BZ    RETURN0                 RETURN CODE 0 IF NOT
         B     RETURN8                 RETURN CODE 8 IF ERROR
         SPACE
* IF THIS IS THE LAST DSCB, MOVE IT TO THE JFCB BUFFER AND START
* READING THE NEXT TRACK.
         SPACE
LASTDSCB MVC   JFCB(148),0(RWA)        MOVE LAST DSCB
         L     RWB,TTRN                LOAD RELATIVE TRACK NUMBER
         AL    RWB,=X'00010000'        INCREMENT TO NEXT TRACK
         ST    RWB,TTRN
         BAL   RRET,EXCP               START CHANNEL PROGRAM
         LA    R1,JFCB                 LOAD DSCB ADDRESS FOR CALLER
         B     GETOUT                  TO RETURN
         EJECT
* WAIT FOR CHANNEL PROGRAM COMPLETION AND TEST THE OUTCOME.
         SPACE
XCPTEST  WAIT  ECB=VTOCECB
         SPACE
         NI    MODESW,X'FF'-XCPRUN     TURN EXCP STARTED BIT OFF
         CLI   VTOCECB,X'7F'           TEST COMPLETION CODE
         BNE   PERMERR                 BRANCH IF ERROR
SETDSCBA L     RWA,DSCBSTRT            SET BUFFER POINTER TO 1ST DSCB
         B     NDXSTORE
         SPACE
* PERMANENT ERROR FOR THIS TRACK.  ZERO THE DSCB'S AND FILL IN THE
* CCHHR PORTIONS OF THE COUNT AREAS.
         SPACE
PERMERR  OI    MODESW,RDERR            SIGNAL READ ERROR
         NI    IOBFLAG1,X'FB'          TURN OFF BIT 5 OF IOB FLAG
         NI    DCBIFLGS,X'3F'          TURN OFF BITS 0 AND 1
         L     RWA,DSCBSTRT            LOAD ADDRESS OF FIRST DSCB
         LA    RWB,1                   LOAD RECORD NUMBER
         SPACE
DSCBELUP XC    0(148,RWA),0(RWA)       ZERO DSCB BUFFER
         MVC   0(4,RWA),IOBSEEK+3      INSERT CCHH IN COUNT FIELD
         STC   RWB,4(,RWA)             INSERT R IN COUNT FIELD
         LA    RWA,148(,RWA)           POINT TO NEXT BUFFER
         LA    RWB,1(,RWB)             INCREMENT RECORD NUMBER
         C     RWA,DSCBLIM             TEST FOR LAST BUFFER
         BNH   DSCBELUP
         B     SETDSCBA                BRANCH TO RESET BUFFER POINTER
         EJECT
*****************
* MODE 1 - OPEN *
*****************
         SPACE
* ENTER WITH A DDNAME ADDRESSED BY REGISTER 1.  PERFORM CLOSE
* SUBROUTINE FIRST TO BE SURE EVERYTHING IS INITIALIZED.
         SPACE
OPEN     DS    0H
         LR    RPARM,R1                SAVE REGISTER 1
         BAL   RRET,CLOSESUB           CALL CLOSE SUBROUTINE
         SPACE
         MVC   DCBDDNAM(8),0(RPARM)    INSERT DDNAME IN DCB
         SPACE
* CHECK DEVICE TYPE TO BE SURE IT IS DIRECT ACCESS.
         SPACE
         DEVTYPE  DCBDDNAM,DWORK       GET DEVICE TYPE
         LTR   R15,R15                 TEST IF IT FOUND DD CARD
         BNZ   RETURN4                 ERROR, DD CARD MISSING
         CLI   DWORK+2,X'20'           TEST DEVICE TYPE
         BNE   RETURN8                 ERROR IF NOT DIRECT ACCESS
         SPACE
         CLI   DWORK+3,MAXDVT          TEST FOR MAX DEVICE TYPE
         BNL   RETURN8                 ERROR IF TOO LARGE
         SR    R1,R1
         IC    R1,DWORK+3              LOAD DEVICE NUMBER
         IC    R1,DVICETAB(R1)         LOAD DSCB'S/TRK FROM TABLE
         LTR   R1,R1                   ZERO IS UNDEFINED DEVICE
         BZ    RETURN8                 EXIT IF UNKNOWN
         ST    R1,NDSCBS               STORE NUMBER OF DSCB'S/TRACK
         SPACE
* READ JFCB AND INSERT DATA SET NAME OF 44 X'04'.
         SPACE
         RDJFCB  XVTCDCB
         MVI   JFCBDSNM,X'04'          GENERATE DATA SET NAME
         MVC   JFCBDSNM+1(43),JFCBDSNM
         SPACE
* OPEN THE DSCB.
         SPACE
         OPEN  (XVTCDCB,(INPUT)),TYPE=J
         TM    DCBOFLGS,OPENBIT        TEST IF OPEN WORKED
         BZ    RETURN4                 ERROR IF OPEN FAILED
         SPACE
* OBTAIN CORE FOR CHANNEL PROGRAM AND DSCB BUFFERS.
         SPACE
         LA    R0,156                  CORE FOR ONE DSCB AND ITS CCW
         MH    R0,NDSCBS+2             TIMES NUMBER PER TRACK
         AH    R0,=H'15'               PLUS 1 CCW AND ROUNDING
         N     R0,=X'FFFFFFF8'         ROUND TO DOUBLE-WORD MULTIPLE
         ST    R0,CBSIZE               SAVE SIZE OF GOTTEN CORE
         GETMAIN  R,LV=(0)             GET TRACK BUFFERS
         ST    R1,CBADDR               SAVE ADDRESS OF GOTTEN CORE
         OI    MODESW,CBGOT            INDICATE CORE GOTTEN
         SPACE
* GENERATE CHANNEL PROGRAM.  IT CONSISTS OF A 'READ R0' ORDER WITH
* THE SKIP FLAG ON, FOLLOWED BY A 'READ COUNT-KEY-AND-DATA' ORDER FOR
* EACH DSCB.
         SPACE
         L     RWA,NDSCBS              NUMBER OF DSCB'S
         SLL   RWA,3                   TIMES   8
         LA    RWA,8(RWA,R1)           PLUS 8 AND BASE = 1ST BUFFER ADD
         ST    RWA,DSCBSTRT            SAVE ADDRESS OF FIRST BUFFER
         SPACE
         ST    R1,IOBSTART             ADDRESS OF CHANNEL PROGRAM
         MVC   0(8,R1),INITCCW         INSERT FIRST CCW
         LA    RWB,8(,R1)              PLACE FOR NEXT CCW
         LA    RWC,1                   BUFFER COUNTER
         SPACE
CCWLOOP  MVC   0(8,RWB),READCCW        INSERT READ CCW FOR ONE DSCB
         ST    RWA,0(,RWB)             SET ITS BUFFER ADDRESS
         MVI   0(RWB),READCKD          RESTORE COMMAND CODE
         C     RWC,NDSCBS              TEST BUFFER COUNTER
         BNL   LASTCCW                 BRANCH IF LAST BUFFER
         LA    RWB,8(,RWB)             INCREMENT CCW ADDRESS
         LA    RWA,148(,RWA)           INCREMENT BUFFER ADDRESS
         LA    RWC,1(,RWC)             INCREMENT BUFFER COUNTER
         B     CCWLOOP                 DO NEXT BUFFER
         SPACE
LASTCCW  NI    4(RWB),X'FF'-CC         TURN OFF COMMAND CHAIN BIT
         ST    RWA,DSCBLIM             SAVE ADDRESS OF LAST DSCB BUFFER
         SPACE
* SET OTHER THINGS AND START PROGRAM TO FILL BUFFER.
         SPACE
         SR    R0,R0
         ST    R0,TTRN                 SET RELATIVE TRACK NUMBER TO 0
         NI    MODESW,X'FF'-XCPRUN-RDERR-EOFSW   SET FLAGS OFF
         BAL   RRET,EXCP               START CHANNEL PROGRAM
         B     RETURN0                 INDICATE SUCCESSFUL OPEN
         EJECT
******************
* MODE 2 - CLOSE *
******************
         SPACE
CLOSE    BAL   RRET,CLOSESUB           CALL CLOSED CLOSE SUBROUTINE
         B     RETURN0
         SPACE 2
* IF THE CHANNEL PROGRAM IS RUNNING, WAIT FOR IT BEFORE TAKING FURTHER
* ACTION.
         SPACE
CLOSESUB DS    0H
         TM    MODESW,XCPRUN           TEST IF CHANNEL PROGRAM RUNNING
         BZ    NOEXCP                  BRANCH IF NOT
         WAIT  ECB=VTOCECB             WAIT UNTIL COMPLETE
         NI    MODESW,X'FF'-XCPRUN     TURN RUNNING SWITCH OFF
NOEXCP   DS    0H
         SPACE
* CLOSE THE DCB.
         SPACE
         TM    DCBOFLGS,OPENBIT        TEST IF DCB OPEN
         BZ    NOCLOSE                 BRANCH IF NOT
         CLOSE XVTCDCB
NOCLOSE  DS    0H
         SPACE
* RELEASE CORE OBTAINED FOR DSCB BUFFERS.
         SPACE
         TM    MODESW,CBGOT            TEST IF CORE GOTTEN
         BZ    NOFREE                  BRANCH IF NOT
         LM    R0,R1,CBSIZE            LOAD SIZE AND LOCATION
         FREEMAIN  R,LV=(0),A=(1)      FREE CORE
         NI    MODESW,X'FF'-CBGOT      SET CORE GOTTEN BIT OFF
NOFREE   DS    0H
         SPACE
         NI    MODESW,X'FF'-RDERR      CLEAR ERROR SWITCH
         BR    RRET
         EJECT
****************
* EXCP ROUTINE *
****************
         SPACE
* CONVERT RELATIVE TRACK ADDRESS IN 'TTRN' TO ABSOLUTE SEEK ADDRESS IN
* 'IOBSEEK', USING SUPERVISOR CONVERSION ROUTINE.
         SPACE
EXCP     DS    0H
         STM   R2,R13,EXCPSAVE         SAVE IMPORTANT REGISTERS
         L     R0,TTRN                 LOAD RELATIVE TRACK NUMBER
         L     R1,DCBDEBAD             LOAD DEB ADDRESS
         LA    R2,IOBSEEK              LOAD ADDR TO RECEIVE MBBCCHHR
         L     R15,CVT                 LOAD CVT ADDRESS
         L     R15,CVTPCNVT(,R15)      LOAD ADDR OF CONVERT ROUTINE
         BALR  R14,R15                 CONVERT TTRN TO MBBCCHHR
         DROP  RBASE                   THAT CLOBBERED BASE REG
         USING *,R14                   R14 SET BY BALR ABOVE
         LM    R2,R13,EXCPSAVE         RESTORE REGISTERS
         DROP  R14
         USING XVTCREAD,RBASE          BASE REGISTER RECOVERED
         LTR   R15,R15                 TEST IF EXTENT VIOLATED (RC=4)
         BNZ   SETEOF                  IF SO, MEANS END-OF-FILE
         SPACE
* ZERO ECB AND START CHANNEL PROGRAM.
         SPACE
         SR    R0,R0
         ST    R0,VTOCECB              CLEAR ECB
         NI    MODESW,X'FF'-RDERR      RESET ERROR SWITCH
         EXCP  VTOCIOB                 START CHANNEL PROGRAM
         OI    MODESW,XCPRUN           SET 'RUNNING' FLAG
         BR    RRET
         SPACE
* WHEN EXTENT IS VIOLATED, SET END-FILE AND EXIT VIA CLOSE ROUTINE.
         SPACE
SETEOF   OI    MODESW,EOFSW            SET END-OF-FILE BIT
         B     CLOSESUB                EXIT VIA CLOSE SUBROUTINE
         EJECT
********************************
* CONSTANTS, VARIABLES, ETC... *
********************************
         SPACE
INITCCW  CCW   READR0,0,CC+SLI+SKIP,8
READCCW  CCW   READCKD,0,CC,148
         SPACE
DVICETAB DC    X'00'           TABLE OF NUMBER OF DSCB'S/TRACK
         DC    X'00'              2311   (NOT SUPPORTED IN MVS/XA)
         DC    X'00'              2301   (NOT SUPPORTED IN MVS/XA)
         DC    X'00'              2303   (NOT SUPPORTED IN MVS/XA)
*        DC    X'00'              2302   (NOT SUPPORTED IN MVS/XA)
         DC    X'45'              9345
         DC    X'00'              2321   (NOT SUPPORTED IN MVS/XA)
         DC    X'00'              2305-1 (NOT SUPPORTED IN MVS/XA)
         DC    AL1(34)            2305-2
         DC    X'00'              2314   (NOT SUPPORTED IN MVS/XA)
         DC    AL1(39)            3330
         DC    AL1(27)            3340 MODEL 35 AND MODEL 70
         DC    AL1(47)            3350
         DC    AL1(51)            3375
         DC    AL1(39)            3330 MODEL 11
         DC    AL1(53)            3380 AND 3380-E AND 3380-K
         DC    AL1(50)            3390
MAXDVT   EQU   *-DVICETAB
         SPACE
DWORK    DS    D               WORK CELL
SAVEAREA DS    19F             SAVE AREA
EXCPSAVE EQU   SAVEAREA+12
CBSIZE   DS    2F              SIZE AND LOCATION OF GOTTEN CORE
CBADDR   EQU   CBSIZE+4
NDSCBS   DS    F               NUMBER OF DSCB'S PER TRACK
DSCBSTRT DS    F               ADDRESS OF 1ST DSCB BUFFER
DSCBLIM  DS    F               ADDRESS OF LAST DSCB BUFFER
DSCBADR  DS    F               ADDRESS OF CURRENT DSCB
TTRN     DS    F               RELATIVE TRACK NUMBER
         SPACE
* MODE SWITCH AND BIT DEFINITIONS:
         SPACE
MODESW   DC    X'00'
CBGOT    EQU   X'80'           CORE GOTTEN FOR BUFFER
XCPRUN   EQU   X'40'           CHANNEL PROGRAM STARTED BUT NOT CHECKED
RDERR    EQU   X'20'           PERMANENT I/O ERROR
EOFSW    EQU   X'10'           END-OF-FILE SENSED
         SPACE
         LTORG
         EJECT
* DATA CONTROL BLOCK
         SPACE
XVTCDCB  DCB   DDNAME=VOLUME01,MACRF=(E),EXLST=JFCBADDR
         SPACE
DCBDDNAM EQU   XVTCDCB+40
DCBIFLGS EQU   XVTCDCB+44
DCBDEBAD EQU   XVTCDCB+44
DCBOFLGS EQU   XVTCDCB+48
OPENBIT  EQU   X'10'
         EJECT
* IOB FOR CHANNEL PROGRAM:
         SPACE
VTOCIOB  DS    0D
IOBFLAG1 DC    X'42000000'     COMMAND CHAIN, NOT RELATED
         DC    A(VTOCECB)
         DC    2F'0'
IOBSTART DC    A(0)            CHANNEL PROGRAM BEGINNING
         DC    A(XVTCDCB)
         DC    X'03000000'
         DC    F'0'
IOBSEEK  DC    D'0'            INITIAL SEEK ADDRESS
         SPACE
* EVENT CONTROL BLOCK FOR CHANNEL PROGRAM:
         SPACE
VTOCECB  DC    F'0'            EVENT CONTROL BLOCK
         SPACE 3
* BUFFER FOR JFCB AND DCB EXIT LIST:
         SPACE
JFCBADDR DS    0F
         DC    X'87'
         DC    AL3(JFCB)
         SPACE
JFCB     DS    0D
         DS    CL176
         SPACE
JFCBDSNM EQU   JFCB            DATA SET NAME
JFCBVOLS EQU   JFCB+118        VOLUME SERIAL NUMBER
         SPACE 6
         END
