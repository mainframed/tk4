XDATEDIT TITLE 'XDATEDIT--CONVERT JULIAN DATE TO DAY-MONTH-YEAR'
         SPACE 2
* AUTHOR:  R. F. MORSE, MIT INSTRUMENTATION LABORATORY
         SPACE
* FUNCTION/OPERATION:  EDITS A PACKED DECIMAL DATE IN YEAR-DAY FORM TO
*        EBCDIC DAY-MONTH-YEAR FORM.  CORRECTS FOR LEAP YEARS AND
*        FOR THE TURN OF A CENTURY.
         SPACE
* ENTRY POINTS:  ENTER AT "XDATEDIT" VIA BALR 14,15 WITH REG 13 SET
*        TO A STANDARD 18-WORD SAVE AREA.  REGISTER 1 POINTS TO A
*        PARAMETER LIST CONTAINING ONE ADDRESS.
         SPACE
* INPUT:  THE ADDRESS IN THE PARAMETER LIST IS THAT OF AN ALIGNED
*        DOUBLE WORD CONTAINING THE ARGUMENT IN PACKED DECIMAL AS:
*        X'0000000000YYDDD+'.
         SPACE
* OUTPUT:  THE RESULT IS RETURNED IN THE SAME DOUBLE WORD IN EBCDIC AS
*        C'DD/MM/YY'.
         SPACE
* DATA SETS:  NONE.
         SPACE
* EXTERNAL ROUTINES:  NONE.
         SPACE
* EXITS-NORMAL:  RETURN VIA REG 14 WITH RETURN CODE 0 IN REG 15.
         SPACE
* EXITS-ERROR:  IF THE DAY NUMBER IS ZERO OR EXCEEDS THE MAXIMUM VALUE
*        APPROPRIATE TO THE YEAR, RETURN VIA REG 14 WITH RETURN CODE 4
*        IN REG 15 AND THE RESULT FIELD SET TO C' YY.DDD '.
         SPACE
* TABLES/WORK AREAS:  NONE.
         SPACE
* ATTRIBUTES:  REENTRANT, READ ONLY.
         SPACE
* NOTES:  NONE.
         EJECT
* DUMMY SECTION TO DEFINE THE ARGUMENT AND RETURN FIELD.
         SPACE
DUMMY    DSECT
DWORD    DS    1D
         SPACE 6
* BEGIN CONTROL SECTION HERE.  DEFINE REGISTER TAGS.
         SPACE
XDATEDIT CSECT
         SPACE
RENTRY   EQU   15              ENTRY POINT REGISTER
RRC      EQU   15              RETURN CODE REGISTER
RBASE    EQU   12              LOCAL BASE REGISTER
RDSECT   EQU   11              BASE REG FOR ARGUMENT/RESULT
RPARM    EQU   1               PARAMETER LIST POINTER
RARGSAVE EQU   2               TO SAVE ORIGINAL PACKED ARGUMENT
RMONTH   EQU   3
RDAY     EQU   4               RDAY AND RYEAR ARE AN EVEN/ODD
RYEAR    EQU   5                 PAIR FOR DIVISION
RWORK    EQU   6
RLIST    EQU   7
         SPACE 6
* ENTER HERE.  SAVE GENERAL REGISTERS AND SET UP BASE REGISTERS.
         SPACE
         SAVE  (14,12),,XDATEDIT
         SPACE
         LR    RBASE,RENTRY
         USING XDATEDIT,RBASE
         L     RDSECT,0(RPARM)         LOAD ARGUMENT ADDRESS
         USING DWORD,RDSECT
         EJECT
* CONVERT YEAR AND DAY TO BINARY, AND SEPARATE BY DIVISION.
         SPACE
         MVI   DWORD,X'00'             CLEAR HIGH DECIMAL DIGITS
         MVC   DWORD+1(4),DWORD          IN ARG TO ZEROS
         OI    DWORD+7,X'0F'           FORCE PLUS SIGN
         L     RARGSAVE,DWORD+4        SAVE ARGUMENT FOR 'BADDATE'
         SPACE
         CVB   RYEAR,DWORD             CONVERT YYDDD TO BINARY
         SR    RDAY,RDAY               CLEAR HIGH-ORDER DIVIDEND
         D     RDAY,F1000              QUOTIENT=YEAR; REMAINDER=DAY
         SPACE
* CHECK FOR LEAP YEAR, AND LOAD RLIST WITH BASE ADDRESS OF APPROPRIATE
* LIST OF MONTH SIZES.
         SPACE
DAYOK    DS    0H
         LA    RLIST,STDLIST           POINT RLIST TO STANDARD MONTHS
         LTR   RWORK,RYEAR             LOAD BINARY YEAR NUMBER
         BZ    NOTLEAP                 BR IF TURN OF CENTURY
         N     RWORK,=F'3'             TEST LOW TWO BITS
         BC    4,NOTLEAP               BR IF NON-ZERO
         LA    RLIST,LPYLIST           POINT RLIST TO LEAP-YEAR MONTHS
NOTLEAP  DS    0H
         SPACE
* TEST DAY NUMBER AGAINST ZERO AND THE UPPER LIMIT DETERMINED BY
* THE YEAR NUMBER.
         SPACE
         LTR   RDAY,RDAY               TEST FOR ZERO
         BZ    BADDATE                 BR IF SO
         CH    RDAY,0(,RLIST)          TEST FOR UPPER LIMIT
         BH    BADDATE                 BR IF TOO LARGE
         SPACE
* REDUCE JULIAN DAY TO DAY-OF-MONTH, ACCUMULATING MONTH NUMBER.
         SPACE
         LA    RMONTH,1                INITIAL MONTH NUMBER
         SR    RWORK,RWORK
DAYLOOP  IC    RWORK,1(RMONTH,RLIST)   LENGTH OF MONTH INTO RWORK
         CR    RDAY,RWORK              TEST IF DAY IN THIS MONTH
         BNH   DAYDONE                 BR IF DAY LESS THAN MONTH SIZE
         SR    RDAY,RWORK              REDUCE DAY BY LENGTH OF MONTH
         LA    RMONTH,1(,RMONTH)       INCREMENT MONTH
         B     DAYLOOP
DAYDONE  DS    0H
         SPACE
* COMBINE DAY, MONTH, AND YEAR IN BINARY IN ONE REGISTER, MULTIPLYING
* EACH BY A FACTOR TO PLACE IT PROPERLY IN THE DECIMAL RESULT.
* THE TRICK IS THAT  1000(1000(DAY)+MONTH)+YEAR  GIVES  DD0MM0YY  WHEN
* CONVERTED TO DECIMAL.  AFTER UNPACKING, THE ZEROS ARE REPLACED BY
* SLASHES.
         SPACE
         MH    RDAY,H1000
         AR    RDAY,RMONTH
         MH    RDAY,H1000
         AR    RDAY,RYEAR
         CVD   RDAY,DWORD              CONVERT TO DECIMAL
         MVC   DWORD(5),DWORD+3        MOVE OVER FOR UNPACKING
         UNPK  DWORD(8),DWORD(5)       CONVERT TO ALPHA
         OI    DWORD+7,X'F0'           COVER UP SIGN
         MVI   DWORD+2,C'/'            INSERT SLASHES
         MVI   DWORD+5,C'/'
         SR    RRC,RRC                 SET NORMAL RETURN CODE OF 0
         SPACE
EXIT     RETURN  (14,12),T,RC=(15)     RETURN TO CALLER
         SPACE 2
* FOR ARGUMENTS WHOSE DAY NUMBER IS ZERO OR TOO LARGE, RETURN IN
* 'DWORD' THE EDITED VALUE  C' YY.DDD ' AND SET A RETURN CODE OF 4.
         SPACE
BADDATE  ST    RARGSAVE,DWORD          RESTORE ORIGINAL PACKED ARGUMENT
         UNPK  DWORD+2(5),DWORD+1(3)   UNPACK INTO ALPHA
         MVC   DWORD+1(2),DWORD+2      SHIFT YEAR 1 LEFT
         MVI   DWORD,C' '              APPLY COSMETICS
         MVI   DWORD+3,C'.'
         MVI   DWORD+7,C' '
         LA    RRC,4                   SET RETURN CODE
         B     EXIT
         EJECT
* LISTS OF MONTH SIZES FOR STANDARD AND LEAP YEARS:
         SPACE
STDLIST  DC    H'365'          DAY LIMIT FOR STANDARD YEARS
         DC    AL1(31)
         DC    AL1(28)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         SPACE
LPYLIST  DC    H'366'          DAY LIMIT FOR LEAP YEARS
         DC    AL1(31)
         DC    AL1(29)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         SPACE
F1000    DC    F'1000'
H1000    EQU   F1000+2
         LTORG
         SPACE
         END
