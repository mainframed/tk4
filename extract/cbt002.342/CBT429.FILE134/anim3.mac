PS4      CSECT
         SAVE  (14,12)
         LR    11,15
         USING PS4,11
         SR     2,2
         SR     3,3
         SR     4,4
         SR     5,5
         SR     6,6
         SR     7,7
         SR     8,8
         SR     9,9
         LH     2,STEPS                  LOAD NUMBER OF STEPS
         LH     4,X1FINAL                LOAD FINAL X1 CO-ORD
         SH     4,X1INIT                 SUBTRACT INIT X1 CO-ORD
         SRDA   4,32(0)                  SHIFT INTO EVEN-ODD PAIR
         DR     4,2                      DIVIDE MOVEMENT BY STEPS
         STH    5,X1INCR                 STORE XI INCREMENT
         LH     6,Y1FINAL                LOAD FINAL Y1 CO-ORD
         SH     6,Y1INIT                 SUBTRACT INIT Y1 CO-ORD
         SRDA   6,32(0)                  SHIFT INTO EVEN-ODD PAIR
         DR     6,2                      DIVIDE MOVEMENT BY STEPS
         STH    7,Y1INCR                 STORE Y1 INCREMENT
         SR     4,4
         SR     5,5
         SR     6,6
         SR     7,7
         LH     4,X2FINAL                LOAD FINAL X2 CO-ORD
         SH     4,X2INIT                 SUBTRACT INIT X2 CO-ORD
         SRDA   4,32(0)                  SHIFT INTO EVEN-ODD PAIR
         DR     4,2                      DIVIDE MOVEMENT BY STEPS
         STH    5,X2INCR                 STORE X2 INCREMENT
         LH     6,Y2FINAL                LOAD FINAL Y2 CO-ORD
         SH     6,Y2INIT                 SUBTRACT INIT Y2 CO-ORD
         SRDA   6,32(0)                  SHIFT INTO EVEN-ODD PAIR
         DR     6,2                      DIVIDE MOVEMENT BY STEPS
         STH    7,Y2INCR                 STORE Y2 INCREMENT
         TPUT  WSFMSG,5,NOEDIT           DRAW
LOOP1    LH     5,X1INIT                 STORE LAST X1 VALUE
         LH     7,Y1INIT                 STORE LAST Y1 VALUE
         AH     5,X1INCR                 INCREMENT LAST X VALUE
         AH     7,Y1INCR                 INCREMENT LAST Y VALUE
         STH    5,X1INIT                 STORE NEW X1 VALUE
         STH    7,Y1INIT                 STORE NEW Y1 VALUE
         MVC    XI,X1INIT                MOVE NEW X1 INTO ORDER
         MVC    YI,Y1INIT                MOVE NEW Y1 INTO ORDER
         SR     5,5
         SR     7,7
         LH     5,X2INIT                 STORE LAST X1 VALUE
         LH     7,Y2INIT                 STORE LAST Y1 VALUE
         AH     5,X2INCR                 INCREMENT LAST X VALUE
         AH     7,Y2INCR                 INCREMENT LAST Y VALUE
         STH    5,X2INIT                 STORE NEW X2 VALUE
         STH    7,Y2INIT                 STORE NEW Y2 VALUE
         MVC    XF,X2INIT                MOVE NEW X2 INTO ORDER
         MVC    YF,Y2INIT                MOVE NEW Y2 INTO ORDER
         TPUT  WSFMSG1,WSFMSGLN,NOEDIT    DRAW
         BCT   2,LOOP1               LOOP FOR NUMBER OF STEPS
         TPUT  WSFMSG3,2,NOEDIT          DRAW
         TGET   BUFFER,80
         RETURN (14,12),RC=(15)
         DS    0D
STEPS    DC    X'0100'
X1INIT   DC    X'FF80'               X1 CO-ORD OF ANIM START
Y1INIT   DC    X'0080'               Y1 CO-ORD OF ANIM START
X1FINAL  DC    X'0080'               X1 CO-ORD OF ANIM END
Y1FINAL  DC    X'0080'               Y1 CO-ORD OF ANIM END
X1INCR   DS    X'0000'               NUMBER OF MOVEMENTS X
Y1INCR   DC    X'0000'               NUMBER OF Y MOVEMENTS
X2INIT   DC    X'0000'               X2 CO-ORD OF ANIM START
Y2INIT   DC    X'FFF0'               Y2 CO-ORD OF ANIM START
X2FINAL  DC    X'0000'               X2 CO-ORD OF ANIM END
Y2FINAL  DC    X'FFF0'               Y2 CO-ORD OF ANIM END
X2INCR   DC    X'0000'               NUMBER OF MOVEMENTS X
Y2INCR   DC    X'0000'               NUMBER OF Y MOVEMENTS
WSFMSG   DC    X'F3'                 WRITE STRUCTURED FIELD
*
* FIRST STRUCTURED FIELD
*
         DC    X'0004'               LENGTH OF FIRST STRUCTURED FIELD
         DC    X'03'                 ERASE/RESET
         DC    B'00000000'           IMPLICIT PARTITION SIZE - DEFAULT
*
* SECOND STRUCTURED FIELD
*
WSFMSG1  DC    X'F3'                 WRITE STRUCTURED FIELD
WSF2     DC    AL2(WSF2LEN)          LENGTH OF SECOND STRUCTURED FIELD
         DC    X'0F10'               GRAPHIC picture
         DC    X'00'                 PID
         DC    B'11000000'           FLAGS - SPAN : FIRST AND LAST
*                                          - MODE : INTERMEDIATE MODE
         DC    X'00'                 RESERVED
*
         DC    X'70'                 BEGIN segment
         DC    X'0C'                 LENGTH OF FOLLOWING PARAMETERS
         DC    CL4'CH01'             NAME OF PROCEDURE TO BE CREATED
         DC    B'01110100'           VISIBLE   NOHILITE
         DC    B'01101000'           noprol NEW SEG DATA
         DC    AL2(SEGLEN)           LENGTH OF PROCEDURE TO BE CREATED
         DC    X'00000000'           P/S NAME
SEGDATA  DC    X'0A05'               SET COLOR
         DC    X'0C00'               SET MIX
         DC    X'1807'               LINE TYPE
         DC    X'1901'               LINE WIDTH
         DC    X'C1'                 GLINE
         DC    X'08'                 LENGTH
XI       DC    X'0000'               X CO-ORD OF LINE START
YI       DC    X'0000'               Y CO-ORD OF LINE START
XF       DC    X'0000'               X CO-ORD OF LINE END
YF       DC    X'0000'               Y CO-ORD OF LINE END
SEGLEN   EQU   *-SEGDATA
WSF2LEN  EQU   *-WSF2
WSFMSGLN EQU   *-WSFMSG1
*
* THIRD STRUCTURED FIELD
*
WSF3     DC    AL2(WSF3LEN)          LENGTH OF THIRD STRUCTURED FIELD
         DC    X'40'                 OUTBOUND 3270DS
         DC    X'00'                 PARTITION ID (PID)
WSFMSG3  DC    X'F1'                 WRITE COMMAND
         DC    X'C2'                 WCC - RESET KEYBOARD
WSF3LEN  EQU   *-WSF3
* LENGTH OF TPUT DATA
BUFFER   DC    CL80' '
BUFFLEN  EQU   *-BUFFER
         END
