         TITLE 'W E L L P U T -- WYLBUR/TSO I/O INTERFACE FROM PLI'
***********************************************************************
* WELLPUT -                                                           *
* THE PURPOSE OF THIS MODULE IS TO SIMULATE THE I/O ROUTINES TREAD    *
* AND TWRITE USED BY THE ADVENTURE GAME.                              *
*                                                                     *
* CALLING SEQUENCES:                                                  *
*                                                                     *
* TREAD (PROMPT_MESSAGE,PROMPT_LENGTH, MESSAGE_AREA,LENGTH,RTN_CODE)  *
*                                                                     *
* TWRITE (MESSAGE,MESSAGE LENGTH,RETURN CODE)                         *
*                                                                     *
***********************************************************************
TREAD    CSECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING TREAD,R12
***********************************************************************
*  FIRST WE WILL DO A TPUT FOR THE PROMPT MESSAGE (IF IT EXISTS)      *
*  THEN.. ASK FOR THE INPUT.                                          *
*                                                                     *
***********************************************************************
         LR    R5,R1               SAVE INPUT PARM PTR
         L     R1,0(,R5)           GET THE PTR TO THE MSG TEXT
         L     R2,4(,R5)           GET THE LENGTH
         L     R0,0(,R2)           ...
         LTR   R0,R0               IS IT THERE ?
         BZ    TRE0                NO.. JUST DO TGET
         TPUT  (1),(0),R
TRE0     EQU   *
         CLI   TGETFLAG,X'00'      DONE THE GETSIZE?
         BNE   TREGET              YES
         GTSIZE
         LTR   R15,R15             DID IT WORK?
         BNE   TRERTN              NO, RETURN
         LTR   R0,R0               IS IT A VDU?
         BZ    TTY
         MVI   TGETFLAG,X'81'      ASIS TGET FOR VDU
         B     TREGET
TTY      MVI   TGETFLAG,X'80'      EDIT TGET FOR TTY
TREGET   LM    R2,R4,8(R5)         PICK UP THE ADDRESS OF PARAMETERS
         LR    R1,R2               GET THE ADDRESS OF THE INPUT TEXT
         LA    R0,133              HOW MUCH WE CAN STAND
         ICM   R1,B'1000',TGETFLAG SHOW PROPER TGET STUFF
         TGET  (1),(0),R
         SR    R9,R9               ZERO R9
         CLI   TGETFLAG,X'80'      TTY?
         BE    GOTRTN              YES, OK
         NI    0(R2),X'0F'         PREPARE AID BYTE
         IC    R9,0(,R2)           GET PFK NUMBER
         LA    R10,6               R10 = 6
         SR    R1,R10              CORRECT THE MSG LENGTH
         BP    NOTNULL             SOME TEXT TO RETURN
         SR    R1,R1               LENGTH = 0
         B     GOTRTN              RETURN
NOTNULL  MVC   0(127,R2),6(R2)     OVERLAY VDU CONTROL CHARS
GOTRTN   OC    0(133,R2),BLANKS    UPPER CASE THIS SUCKER
         ST    R1,0(,R3)           STORE MESSAGE LENGTH
         ST    R9,0(,R4)           STORE CCODE (PFK #)
TRERTN   RETURN (14,12),RC=(15)    USE TGET RETURN CODE
         DROP  R12
         SPACE 2
TGETFLAG DC    X'00'               FILLED IN AFTER GTSIZE
BLANKS   DC    CL133' '
         LTORG
         EJECT
***********************************************************************
* TWRITE -                                                            *
*                                                                     *
* ISSUE A TPUT FOR THE CALLING PROGRAM..                              *
*                                                                     *
*                                                                     *
***********************************************************************
TWRITE   CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING TWRITE,R12
         LR    R5,R1               SAVE PTR TO INPUT PARMS
         L     R1,0(R5)            PTR TO OUTPUT TEXT
         L     R2,4(R5)            GET OUTPUT LENGTH
         L     R0,0(R2)            ...
         TPUT  (1),(0),R           DO IT
         L     R1,8(R5)            GET THE CCODE PTR
         ST    R15,0(R1)           SAVE IT.
         RETURN (14,12),RC=0
         DROP  R12
         SPACE 2
         LTORG
         TITLE 'RETURN TIME IN TIMER UNITS TO INIT RANDOM NUMBER'
ITIME    CSECT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             ESTAB BASE
         USING ITIME,R12           TELL ASM
         LA    R8,ISAV             GET A(MY SAVE)
         ST    R8,8(R13)           BACK CHAIN
         ST    R13,4(R8)           FORWARD CHAIN
         LR    R13,R8              POINT TO IT
         SPACE 3
         L     R2,0(R1)            R2 -> FULLWORD PARM
         TIME  TU
         N     R0,MASK             TURN TO NUMBER 0-511
         ST    R0,0(R2)            SAVE IN PARM
         L     R13,4(R13)          RELOAD R13
         LM    R14,R12,12(R13)     RESTORE REST OF REGISTERS
         SR    R15,R15             CLEAR RC
         BR    R14                 RETURN
         SPACE
ISAV     DC    18F'0'              SAVE AREA
MASK     DC    X'000001FF'         512-1
         TITLE 'WIZARD WARNING MESSAGE SUBROUTINES'
WARNMSG  CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING WARNMSG,R12
         L     R1,0(0,R1)               R1 --> 16 BYTE PARAMETER
         SR    R4,R4                    CLEAR REG 4
         SR    R5,R5                    CLEAR REG 4
         S     R4,0(0,R1)               DECODE FIRST 4 BYTES
         S     R5,4(0,R1)               DECODE SECOND 4 BYTES
         STM   R4,R5,WIZ                & STORE
         MVI   WIZ+7,C' '               BLANK LAST BYTE OF USERID
         MVC   USR(8),8(R1)             LAST 8 BYTES = USER'S USERID
         CLI   7(R1),C'A'               IF EIGHTH BYTE = C'A' ==>
         BE    AUTHMSG                               AUTHORIZED USER
         TPUT  UNAUTH,42,EDIT,USERIDL=WIZ
         B     WMRTN
AUTHMSG  MVI   AUTH,C'A'                ADJUST MESAGE TEXT
         TPUT  AUTH,40,EDIT,USERIDL=WIZ
WMRTN    MVC   WIZ,USR                  ERASE TARGET USERID
         RETURN (14,12),RC=(15)         USE RETURN CODE FROM TPUT
         DROP  R12
         DS    0F
WIZ      DC    CL8' '
UNAUTH   DC    C'Un'
AUTH     DC    C'authorized access of Program by '
USR      DC    CL8' '
         LTORG
         TITLE 'SUPPLY PASSWORD TYPING AREA'
WHISPER  CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING WHISPER,R12
         TPUT  BUFFER,11,FULLSCR
RESTORE  RETURN (14,12),RC=(15)
         DROP  R12
BUFFER   DC    X'C11D0C13'
         DC    CL5' '
         DC    X'1D28'
         LTORG
         TITLE 'RETURN CURRENT YYDDD IN FIXED DEC(5,0) TO PL/1 PGM'
DECDATE  CSECT
         STM   R14,R12,12(R13)
         L     R3,0(,R1)
         TIME
         ST    R15,16(,R13)      STORE RETURN CODE
         STCM  R1,7,0(R3)        PARAMETER SHOULD BE FIXED DEC(5,0)
         LM    R14,R12,12(R13)
         BR    R14
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
