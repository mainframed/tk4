         TITLE 'SKJ$LC04 - $SPACE/$LISTC TERMINATION ROUTINE'
SKJ$LC04 START 0
         EJECT
         SKJ$LCPD
         EJECT
         IKJDAPL
         EJECT
         IKJEFFMT MTDSECT=YES
         EJECT
         IKJDAP18
         EJECT
         CVT   DSECT=YES
         EJECT
         PRINT NOGEN
         SPACE 2
         DCBD  DSORG=PS
         SPACE 2
         PRINT GEN
         EJECT
***********************************************************************
*                                                                     *
*  EQUATES                                                            *
*                                                                     *
***********************************************************************
         SPACE 1
R0       EQU   0                  WORK REGISTER
R1       EQU   1                  PARAMETER LIST ADDRESS REGISTER
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  INTERNAL LINK REGISTER
R9       EQU   9                  DAPB BASE REGISTER
R10      EQU   10                 DCB BASE REGISTER
R11      EQU   11                 COMMON AREA BASE REGISTER
R12      EQU   12                 CSECT BASE REGISTER
R13      EQU   13                 SAVE REGISTER
R14      EQU   14                 LINK REGISTER
R15      EQU   15                 ENTRY POINT AND RETURN CODE REGISTER
         EJECT
***********************************************************************
*                                                                     *
*  HOUSEKEEPING                                                       *
*                                                                     *
***********************************************************************
         SPACE 1
SKJ$LC04 CSECT
SKJ$LC04 AMODE 31
SKJ$LC04 RMODE 24
         BC    15,14(,R15)
         DC    X'08'
         DC    CL8'SKJ$LC04'
         DS    0H
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LR    R12,R15
         USING SKJ$LC04,R12            ESTABLISH CSECT BASE REGISTER
         ST    R13,SAVE+4
         LR    R2,R13
         LA    R13,SAVE                ESTABLISH LOCAL SAVE AREA
         ST    R13,8(,R2)
         L     R11,0(,R1)              PICK UP ADDRESS OF COMMON AREA
         USING SKJ$LCPD,R11            ADDRESSABILITY TO COMMON AREA
         L     R10,$LCODCB             PICK UP ADDRESS OF OUTPUT DCB
         USING IHADCB,R10              ADDRESSABILITY TO DCB
         SPACE 1
***********************************************************************
*                                                                     *
*  $LCSPF ENTRY - REPOSITION SPF TABLE                                *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    $LCFLGS,$LCSPFF         $LCSPF ENTRY?
         BZ    NOTSPF                  NO  - WRITE TERMINATION MESSAGES
         TM    $LCFLGS1,$LCERR         HAS AN ERROR OCCURRED?
         BO    RETURN                  YES - RETURN IMMEDIATELY
         LA    R2,$LCTABL              ADDRESS OF ISPF TABLE NAME
         ST    R2,ISPPARM+4            STORE IN ISPLINK PARM LIST
         MVI   ISPPARM+4,X'80'         TURN HIGH-ORDER BIT ON
         LA    R1,ISPPARM              ADDRESS OF ISPLINK PARM LIST
         L     R15,VISPLINK            ADDRESS OF ISPLINK ROUTINE
         BALR  R14,R15                 INVOKE ISPF TBTOP SERVICE
         B     RETURN                  RETURN
         SPACE 1
***********************************************************************
*                                                                     *
*  WRITE TERMINATION MESSAGES                                         *
*                                                                     *
***********************************************************************
         SPACE 1
NOTSPF   TM    $LCFLGS1,$LCERR         HAS AN ERROR OCCURRED?
         BO    CLOSE                   YES - BYPASS WRITING MESSAGES
         TM    $LCFLGS1,$LCLISTC       $LISTC ENTRY?
         BO    TERM                    YES - WRITE TERMINATION MESSAGE
         L     R2,$LCTRKAL             PICK UP TOTAL TRACKS ALLOCATED
         CVD   R2,DOUBLE               CONVERT TO DECIMAL
         ED    TOTLIN+3(6),DOUBLE+5    EDIT INTO TOTAL LINE
         L     R2,$LCKBAL              PICK UP TOTAL KB ALLOCATED
         CVD   R2,DOUBLE               CONVERT TO DECIMAL
         MVO   DOUBLE+4(4),DOUBLE+4(4) PREPARE FOR EDIT
         ED    TOTLIN+15(7),DOUBLE+4   EDIT INTO TOTAL LINE
         L     R2,$LCTRKUS             PICK UP TOTAL TRACKS USED
         CVD   R2,DOUBLE               CONVERT TO DECIMAL
         ED    TOTLIN+9(6),DOUBLE+5    EDIT INTO TOTAL LINE
         L     R2,$LCKBUS              PICK UP TOTAL KB USED
         CVD   R2,DOUBLE               CONVERT TO DECIMAL
         MVO   DOUBLE+4(4),DOUBLE+4(4) PREPARE FOR EDIT
         ED    TOTLIN+22(7),DOUBLE+4   EDIT INTO TOTAL LINE
         L     R2,INSAD1               LOAD INSERT ADDRESS AND LENGTH
         BAL   R5,WRTMSG               GO TO WRITE MESSAGE
         SPACE 1
TERM     TM    $LCFLGS1,$LCSPACE       $SPACE ENTRY?
         BZ    WRTTERM                 NO - GO TO WRITE END MESSAGE
         MVC   ENDMSG+4(6),$SPACEA     PREPARE END MESSAGE
WRTTERM  L     R2,INSAD2               LOAD INSERT ADDRESS AND LENGTH
         BAL   R5,WRTMSG               GO TO WRITE MESSAGE
         SPACE 1
***********************************************************************
*                                                                     *
*  CLOSE AND DE-ALLOCATE OUTPUT DATA SET                              *
*                                                                     *
***********************************************************************
         SPACE 1
CLOSE    TM    $LCFLGS1,$LCODS         OUTDATASET/ODS SPECIFIED?
         BZ    RETURN                  NO - RETURN
         SPACE 1
         TM    $LCFLGS2,$LCOPEN        WAS OUTPUT DATA SET OPENED?
         BZ    FREE                    NO - BYPASS CLOSE
         CLOSE ((10)),MODE=31          CLOSE OUTPUT DCB
         SPACE 1
FREE     TM    $LCFLGS2,$LCALLOC       WAS OUTPUT DATA SET ALLOCATED?
         BZ    RETURN                  NO - BYPASS DEALLOCATION
         LA    R1,$LCDAPL              ADDRESS OF DAIR PARAMETER LIST
         USING DAPL,R1                 ADDRESSABILITY TO DAPL
         LM    R2,R4,$LCCPPL+4
         ST    R2,DAPLUPT              SET UP
         ST    R3,DAPLPSCB               DAIR
         ST    R4,DAPLECT                  PARAMETER
         LA    R2,$LCECB                     LIST
         ST    R2,DAPLECB
         SPACE 1
         LA    R9,$LCDA18B             ADDRESS OF DAIR PARAMETER BLOCK
         USING DAPB18,R9               ADDRESSABILITY TO DAPB
         MVC   DA18DDN,DCBDDNAM        MOVE DDNAME TO DAPB
         SPACE 1
         CALLTSSR EP=IKJDAIR,MF=(E,(1))     INVOKE IKJDAIR
         SPACE 1
***********************************************************************
*                                                                     *
*  RETURN                                                             *
*                                                                     *
***********************************************************************
         SPACE 1
RETURN   L     R13,SAVE+4              RESTORE REGISTER 13
         RETURN (14,12)                RETURN
         EJECT
***********************************************************************
*                                                                     *
*  ROUTINE TO WRITE REPORT LINE VIA IKJEFF02 OR QSAM PUT LOCATE       *
*                                                                     *
***********************************************************************
         SPACE 1
WRTMSG   TM    $LCFLGS1,$LCODS         OUTPUT DATASET SPECIFIED?
         BO    PUTLINE                 YES - GO TO ISSUE QSAM PUT
         SPACE 1
         LA    R1,$LCMTPL              ADDRESS OF IKJEFF02 PARM LIST
         USING MTDSECTD,R1             ADDRESSABILITY TO PARM LIST
         MVC   MTMSGID,MTPUT           SET MESSAGE NUMBER (TPUT)
         ST    R2,MTINSRTS             SET INSERT LENGTH AND ADDRESS
         CALLTSSR EP=IKJEFF02,MF=(E,(1))   WRITE REPORT LINE
         BR    R5                      RETURN
         DROP  R1
         SPACE 1
PUTLINE  PUT   (10)                    LOCATE OUTPUT AREA
         SLL   R2,8                    CLEAR HIGH-ORDER BYTE
         SRL   R2,8                      FOR 24-BIT ADDRESS
         MVC   0(80,R1),0(R2)          MOVE REPORT LINE TO OUTPUT AREA
         BR    R5                      RETURN
         EJECT
***********************************************************************
*                                                                     *
*  STORAGE DECLARATIONS                                               *
*                                                                     *
***********************************************************************
         SPACE 1
DOUBLE   DS    D                  DOUBLEWORD FOR CVD INSTRUCTIONS
SAVE     DS    18F                LOCAL SAVE AREA
VISPLINK DC    V(ISPLINK)         ADDRESS OF ISPF INTERFACE ROUTINE
ISPPARM  DC    A(TBTOP)           ISPLINK PARAMETER LIST
INSAD1   DC    AL1(35),AL3(TOTLIN)     IKJEFF02 INSERT ADDRESS
INSAD2   DC    AL1(10),AL3(ENDMSG)     IKJEFF02 INSERT ADDRESS
TBTOP    DC    CL8'TBTOP'
MTPUT    DC    CL4'TPUT'          MESSAGE NUMBER (EQUIVALENT TO TPUT)
ENDMSG   DC    CL80'END $LISTC'   TERMINATION MESSAGE ($LISTC)
$SPACEA  DC    CL6'$SPACE'
TOTLIN   DC    CL3'   '           $SPACE TOTAL LINE
         DC    XL12'402020202120402020202120'
         DC    XL14'4020202020212040202020202120'
         DC    CL51' TOTAL'
         SPACE 1
         DS    0F
$LCDAPL  DS    A                   )
         DS    A                   ) DAIR
         DS    A                   ) PARAMETER
         DS    A                   ) LIST
         DC    A($LCDA18B)         )
         SPACE 1
$LCDA18B DC    X'0018'             )
         DC    X'0000'             )
         DC    X'0000'             )
         DC    X'0000'             ) DAIR
         DC    A(0)                ) PARAMETER
         DC    CL8' '              ) BLOCK
         DC    CL8' '              ) (FREE)
         DC    CL2' '              )
         DC    X'00'               )
         DC    X'00'               )
         DC    XL8'00'             )
         SPACE 2
         END   SKJ$LC04
