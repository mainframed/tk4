SMFJBTIM TITLE 'REPORT JES2 PHASE TIMES BY CLASS FROM TYPE 26 RECORDS'
***********************************************************************
*                                                                     *
*        SMFJBTIM GATHERS STATISTICS FROM SMF TYPE 26 RECORDS.        *
*    THE TYPE 26 RECORD DSECT WILL NEED SOME ADJUSTMENT FOR JES3.     *
*    THE MAIN INFORMATION IS THE AVERAGE ELAPSED TIME SPENT IN EACH   *
*    JES PHASE QUEUE BY EXECUTION CLASS.                              *
*                                                                     *
*        SMFJBTIM WAS WRITTEN BY GREG PRICE (PRYCROFT SIX PTY LTD)    *
*                             ON 7 MARCH 1989.                        *
*                                                                     *
*        SMFJBTIM IS NOT REENTRANT NOR SERIALLY REUSABLE.             *
*        SMFJBTIM RUNS AMODE AND RMODE 24.                            *
*        SMFJBTIM USES MACROS FROM SYS1.MACLIB.                       *
*        SMFJBTIM DOES NOT ISSUE ANY GETMAINS OR FREEMAINS.           *
*        SMFJBTIM USES QSAM WITH BFTEK=A TO READ THE SMF FILE.        *
*                                                                     *
*           FILES -                                                   *
*                 SYSIN    - INPUT CONTROL OF 0 OR 1 CARD IMAGE.      *
*                 SYSPRINT - OUTPUT REPORT PRINT FILE.                *
*                 SMFDATA  - INPUT SMF RECORD FILE.                   *
*                                                                     *
***********************************************************************
         EJECT
SMFJBTIM CSECT
         STM   R14,R12,12(R13)        PERFORM STANDARD INITIALIZATION
         LR    R11,R15
         LA    R12,2048(,R11)
         LA    R12,2048(,R12)
         USING SMFJBTIM,R11,R12
         LA    R0,SAVEAREA
         ST    R0,8(,R13)
         ST    R13,SAVEAREA+4
         LR    R13,R0
         SPACE
         OPEN (SYSIN,INPUT)           GET SPECIFIED RESTRICTIONS
         GET   SYSIN,CARD
ENDOFCRD CLOSE SYSIN
         CLI   INSYSID,C' '           ANY SYSTEM-ID SPECIFIED?
         BNH   GOTSYSID               NO
         MVC   HEADSID,INSYSID        YES, COPY IT
GOTSYSID PACK  WORK,INSDATE           GET THE STARTING DATE
         TM    WORK+7,X'0F'           NUMERIC?
         BNO   GOTSDATE               NO
         MVC   FRSTDATE,WORK+4        YES, USE IT.
GOTSDATE PACK  WORK,INEDATE           GET THE ENDING DATE
         TM    WORK+7,X'0F'           NUMERIC?
         BNO   GOTEDATE               NO
         MVC   LASTDATE,WORK+4        YES, USE IT.
GOTEDATE LA    R1,INSTIME             POINT TO SPECIFIED STARTING TIME
         LA    R15,FRSTTIME           POINT TO STARTING TIME TARGET
         BAL   R14,HHMM2BIN           CONVERT TO CENTISECONDS
         LA    R1,INETIME             POINT TO SPECIFIED STARTING TIME
         LA    R15,LASTTIME           POINT TO STARTING TIME TARGET
         BAL   R14,HHMM2BIN           CONVERT TO CENTISECONDS
         L     R10,=V(SMFJBCOM)
         LR    R1,R10                 POINT TO THE COMMON AREA
         LA    R0,36                  GET NUMBER OF 256-BYTE CHUNKS
ZEROLOOP XC    0(256,R1),0(R1)        ZERO IT
         LA    R1,256(,R1)            POINT TO NEXT CHUNK
         BCT   R0,ZEROLOOP            END OF INITIALIZATION
         SPACE
         OPEN (SMFDATA,INPUT)         COMMENCE INPUT PHASE
         B     GETLOOP                GO GET THE FIRST RECORD
REJECTED LA    R0,1
         A     R0,REJECTCT            INCREMENT THE REJECTED 26 COUNTER
         ST    R0,REJECTCT
GETLOOP  GET   SMFDATA
         USING SMFRCD26,R1
         LA    R0,1
         A     R0,GETCNTR             INCREMENT THE INPUT RECORD COUNT
         ST    R0,GETCNTR
         CLI   HEADSID,C' '           SYSID CHECKING?
         BE    SYSIDOK                NO
         CLC   SMF26SID,HEADSID       YES, CORRECT SYSTEM?
         BNE   GETLOOP                NO, IGNORE RECORD
SYSIDOK  CLI   SMF26RTY,SMFJ26        TYPE 26 SMF RECORD?
         BNE   GETLOOP                NO, IGNORE IT
         TM    SMF26IND,SMF26DES+SMF26EVT+SMF26ATU
         BNO   REJECTED               NOT ALL REQUIRED SECTIONS PRESENT
         TM    SMF26IN2,SMF26BCH+SMF26FTS+SMF26STK
         BZ    REJECTED               NOT JOB/TSU/STC FROM THIS SYSTEM
         CLC   ZEROTIME,SMF26RPT      READ IN ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26CST      CONVERTED ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26CPT      CONVERTED ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26XST      EXECUTED ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26XPT      EXECUTED ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26OST      OUTPUT ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   ZEROTIME,SMF26OPT      OUTPUT ON THIS SYSTEM?
         BNL   REJECTED               NO, CANCELLED OR ROUTED VIA NJE
         CLC   SMF26RSD,FRSTDATE      BEFORE FIRST REQUESTED DATE?
         BL    REJECTED               YES
         CLC   SMF26RSD,LASTDATE      AFTER LAST REQUESTED DATE?
         BH    REJECTED               YES
         CLC   SMF26RST,FRSTTIME      BEFORE FIRST REQUESTED TIME?
         BL    REJECTED               YES
         CLC   SMF26RST,LASTTIME      AFTER LAST REQUESTED TIME?
         BH    REJECTED               YES
         CLC   LODATE,SMF26RSD        NEW LOWEST DATE?
         BL    LOWOKAY                NO
         BH    NEWLOW                 YES
         CLC   LOTIME,SMF26RST        NEW LOWEST TIME?
         BL    LOWOKAY                NO
NEWLOW   MVC   LOTIME,SMF26RST        SAVE NEW LOWEST TIME
         MVC   LODATE,SMF26RSD        SAVE NEW LOWEST DATE
LOWOKAY  CLC   HIDATE,SMF26RSD        NEW HIGHEST DATE?
         BH    HIGHOKAY               NO
         BL    NEWHIGH                YES
         CLC   HITIME,SMF26RST        NEW HIGHEST TIME?
         BH    HIGHOKAY               NO
NEWHIGH  MVC   HITIME,SMF26RST        SAVE NEW HIGHEST TIME
         MVC   HIDATE,SMF26RSD        SAVE NEW HIGHEST DATE
HIGHOKAY TM    SMF26IN2,SMF26SCN+SMF26CPY
         BNZ   REJECTED               IGNORE TYPRUN=SCAN/COPY JOBS
         TM    SMF26IN2,SMF26BCH      BATCH JOB?
         BZ    BATCHOK                NO
         LA    R0,1
         A     R0,JEXECCNT            YES, INCREMENT COUNTER
         ST    R0,JEXECCNT
         ICM   R0,B'1111',SMF26ICD    ALSO TOTAL UP INPUT CARD COUNT
         A     R0,JCARDCNT
         ST    R0,JCARDCNT
BATCHOK  TM    SMF26INF,SMF26TRH+SMF26OPC
         BNZ   REJECTED               IGNORE TYPRUN=HOLD/CANCELLED JOBS
         SLR   R2,R2
         IC    R2,SMF26CLS            LOAD THE EXECUTION CLASS
         MH    R2,THRTYSIX            BY 36 FOR JOBSTATS INDEX
         ALR   R2,R10                 POINT TO FIGURES FOR THIS CLASS
         LA    R0,1
         A     R0,0(,R2)              INCREMENT CLASS JOB COUNTER
         ST    R0,0(,R2)
         LA    R2,4(,R2)              POINT TO FIRST TIME ACCUMULATOR
         LA    R3,SMF26RST            POINT TO FIRST "OLD" TIME STAMP
         LA    R4,SMF26RPT            POINT TO FIRST "NEW" TIME STAMP
         LA    R0,7                   GET NUMBER OF TIME STAMPS IN LIST
TIMELOOP BAL   R14,TIMEDIFF           ACCUMULATE CENTISECOND DIFFERENCE
         LA    R2,4(,R2)              POINT TO NEXT ACCUMULATOR
         LR    R3,R4                  POINT TO NEXT "OLD" TIME STAMP
         LA    R4,8(,R4)              POINT TO NEXT "NEW" TIME STAMP
         BCT   R0,TIMELOOP            PROCESS NEXT TIME STAMP
         LA    R4,SMF26TME            POINT TO FINAL "NEW" TIME STAMP
         BAL   R14,TIMEDIFF           ACCUMULATE CENTISECOND DIFFERENCE
         B     GETLOOP                PROCESS NEXT SMF RECORD
         DROP  R1
         SPACE
ENDOFSMF DS    0H
         SPACE
         CLOSE SMFDATA                END OF INPUT PHASE
         SPACE
         OPEN (SYSPRINT,OUTPUT)       COMMENCE REPORTING PHASE
         SLR   R2,R2
         L     R3,LOTIME
         D     R2,SIXGRAND            CONVERT TO MINUTES
         SLR   R2,R2
         LA    R0,60                  GET MINUTES PER HOUR
         DR    R2,R0                  CONVERT TO HOURS AND MINUTES
         CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  HEADLOHH,WORK+6(2)
         CVD   R2,WORK
         OI    WORK+7,X'0F'
         UNPK  HEADLOMM,WORK+6(2)
         MVC   HEADLOYD,DATEEDIT
         ED    HEADLOYD,LODATE+1
         SLR   R2,R2
         L     R3,HITIME
         D     R2,SIXGRAND            CONVERT TO MINUTES
         SLR   R2,R2
         LA    R0,60                  GET MINUTES PER HOUR
         DR    R2,R0                  CONVERT TO HOURS AND MINUTES
         CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  HEADHIHH,WORK+6(2)
         CVD   R2,WORK
         OI    WORK+7,X'0F'
         UNPK  HEADHIMM,WORK+6(2)
         MVC   HEADHIYD,DATEEDIT
         ED    HEADHIYD,HIDATE+1
         PUT   SYSPRINT,HEADING
         L     R2,GETCNTR
         CVD   R2,WORK
         MVC   GETTOTAL,ED9
         ED    GETTOTAL,WORK+3
         L     R2,REJECTCT
         CVD   R2,WORK
         MVC   REJTOTAL,ED9
         ED    REJTOTAL,WORK+3
         PUT   SYSPRINT,TOTALS
         PUT   SYSPRINT,SUBHEAD
         SPACE
         USING JOBSTATS,R10
         LA    R2,256                 LOAD NUMBER OF POSSIBLE CLASSES
JCLSLOOP LA    R0,256
         SR    R0,R2                  GET CURRENT CLASS
         MVC   CLASS-1(3),CLASS-2     ERASE PREVIOUS CLASS
         STC   R0,CLASS
         CLI   CLASS,C'\'             TIME SHARING USER CLASS?
         BNE   NOTTSU                 NO
         MVC   CLASS-1(3),=C'TSU'     YES, INTERPRET \
         B     NOTSTC
NOTTSU   CLI   CLASS,C'}'             STARTED TASK CLASS?
         BNE   NOTSTC                 NO
         MVC   CLASS-1(3),=C'STC'     YES, INTERPRET }
NOTSTC   ICM   R9,B'1111',JOBCOUNT    GET JOBS PURGED IN THIS CLASS
         BZ    NXTCLASS               NONE SO SKIP THIS CLASS
         CVD   R9,WORK
         MVC   JOBCNTR,ED9
         ED    JOBCNTR,WORK+3
         LA    R0,6                   1000/6000 = 1/6
         SLR   R4,R4
         L     R5,RDRTIME
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   RDRT,ED4PNT3
         ED    RDRT,WORK+4
         SLR   R4,R4
         L     R5,CONVWAIT
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   CONVW,ED4PNT3
         ED    CONVW,WORK+4
         SLR   R4,R4
         L     R5,CONVTIME
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   CONVT,ED4PNT3
         ED    CONVT,WORK+4
         SLR   R4,R4
         L     R5,EXECWAIT
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   EXECW,ED4PNT3
         ED    EXECW,WORK+4
         SLR   R4,R4
         L     R5,EXECTIME
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   EXECT,ED4PNT3
         ED    EXECT,WORK+4
         SLR   R4,R4
         L     R5,OUTWAIT
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   OUTW,ED4PNT3
         ED    OUTW,WORK+4
         SLR   R4,R4
         L     R5,OUTTIME
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   OUTT,ED4PNT3
         ED    OUTT,WORK+4
         SLR   R4,R4
         L     R5,PURGWAIT
         DR    R4,R9                  GET TIME PER JOB
         SLR   R4,R4
         DR    R4,R0                  GET TIME IN MILLIMINUTES
         CVD   R5,WORK
         MVC   PURGEW,ED4PNT3
         ED    PURGEW,WORK+4
         PUT   SYSPRINT,DETAIL
NXTCLASS LA    R10,36(,R10)           POINT TO STATS OF NEXT JOB CLASS
         BCT   R2,JCLSLOOP            PROCESS NEXT CLASS
         DROP  R10
         SPACE
         L     R0,JEXECCNT
         CVD   R0,WORK                REPORT TOTAL BATCH JOB COUNT
         MVC   JOBTOTAL,ED9           (EXCEPT TYPRUN=SCAN/TYPRUN=COPY)
         ED    JOBTOTAL,WORK+3
         L     R0,JCARDCNT
         CVD   R0,WORK                REPORT CARD IMAGE COUNT FOR THESE
         MVC   CRDTOTAL,ED9
         ED    CRDTOTAL,WORK+3
         PUT   SYSPRINT,JTOTALIN
         PUT   SYSPRINT,LEGEND1       FINISH REPORT WITH HEADING LEGEND
         PUT   SYSPRINT,LEGEND2
         PUT   SYSPRINT,LEGEND3
         PUT   SYSPRINT,LEGEND4
         PUT   SYSPRINT,LEGEND5
         PUT   SYSPRINT,LEGEND6
         PUT   SYSPRINT,LEGEND7
         PUT   SYSPRINT,LEGEND8
         PUT   SYSPRINT,LEGEND9
         PUT   SYSPRINT,LEGENDA
         CLOSE SYSPRINT               END OF REPORTING PHASE
         L     R13,4(,R13)            POINT TO PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)        RESTORE REGISTER
         SLR   R15,R15                ZERO THE RETURN CODE
         BR    R14                    EXIT
         TITLE 'SUBROUTINES'
HHMM2BIN STM   R14,R12,12(R13)        SAVE REGISTERS
         CLI   0(R1),C'0'             VALIDATE INPUT HHMM AS NUMERIC
         BLR   R14                    RETURN WITHOUT UPDATE IF INVALID
         CLI   1(R1),C'0'
         BLR   R14
         CLI   3(R1),C'0'             SKIP OVER :
         BLR   R14
         CLI   4(R1),C'0'
         BLR   R14
         CLC   0(2,R1),=C'23'
         BHR   R14
         CLC   3(2,R1),=C'59'
         BHR   R14
         PACK  WORK,0(2,R1)
         CVB   R3,WORK
         LA    R0,60                  GET MINUTES PER HOUR
         MR    R2,R0
         PACK  WORK,3(2,R1)
         CVB   R2,WORK
         AR    R3,R2                  GET MINUTES IN HH:MM
         M     R2,SIXGRAND            CONVERT MINUTES TO CENTISECONDS
         ST    R3,0(,R15)             SAVE THE RESULT
         LM    R14,R12,12(R13)        RESTORE REGISTERS
         BR    R14                    RETURN TO CALLER
         SPACE 2
TIMEDIFF STM   R14,R12,12(R13)        SAVE REGISTERS
         ICM   R5,B'1111',0(R3)       GET "OLD" TIME
         ICM   R15,B'1111',0(R4)      GET "NEW" TIME
         SR    R15,R5                 GET TIME-OF-DAY DIFFERENCE
         ZAP   WORK,4(4,R4)           GET "NEW" DATE
         CLC   4(2,R3),4(R4)          "OLD" & "NEW" DATES IN SAME YEAR?
         BE    SAMEYEAR               YES, DAY DIFFERENCE IS SIMPLE
         XC    WORK(6),WORK           USE "NEW" DDD AS DAY DIFFERENCE
         B     YEAROKAY
SAMEYEAR SP    WORK,4(4,R3)           GET DAY DIFFERENCE
YEAROKAY CVB   R7,WORK
         M     R6,ADAY
         AR    R15,R7                 ADD DAYS ON TO TOD DIFFERENCE
         BNM   DIFNTNEG               ZERO OR POSITIVE IS OKAY
         SLR   R15,R15                ZERO NEGATIVE DIFFERENCE
DIFNTNEG AL    R15,0(,R2)             ACCUMULATE RESULT
         ST    R15,0(,R2)
         LM    R14,R12,12(R13)        RESTORE REGISTERS
         BR    R14                    RETURN TO CALLER
         TITLE 'VARIABLES AND CONSTANTS'
WORK     DC    D'0'
ZEROTIME DC    FL8'15'
SAVEAREA DC    18F'0'
REJECTCT DC    F'0'
GETCNTR  DC    F'0'
JEXECCNT DC    F'0'
JCARDCNT DC    F'0'
SIXGRAND DC    F'6000'                CENTISECONDS PER MINUTE
ADAY     DC    F'8640000'             CENTISECONDS PER DAY
LOTIME   DC    F'8640000'             LOWEST TIME IN LOWEST DATE
LODATE   DC    PL4'9999999'           LOWEST DATE ENCOUNTERED
HITIME   DC    F'0'                   HIGHEST TIME IN HIGHEST DATE
HIDATE   DC    PL4'0'                 HIGHEST DATE ENCOUNTERED
FRSTDATE DC    PL4'0'                 LOWEST ACCEPTABLE READER DATE
LASTDATE DC    PL4'9999999'           LATEST ACCEPTABLE READER DATE
FRSTTIME DC    F'0'                   LOWEST ACCEPTABLE READER TIME
LASTTIME DC    F'9999999'             LATEST ACCEPTABLE READER TIME
CARD     DS    0CL80
         DC    CL6'SYSID='
INSYSID  DC    CL4' '
         DC    CL9' RDRDATE='
INSDATE  DC    CL5'YYDDD'
         DC    CL1'-'
INEDATE  DC    CL5'YYDDD'
         DC    CL9' RDRTIME='
INSTIME  DC    CL5'HH:MM'
         DC    CL1'-'
INETIME  DC    CL5'HH:MM'
         DC    CL30' '
THRTYSIX DC    H'36'                  LENGTH OF JOBSTATS ENTRY
DATEEDIT DC    XL7'4021204B202020'
ED9      DC    XL10'40202020202020202120'
ED4PNT3  DC    XL9'40202021204B202020'
HEADING  DC    CL133'1  AVERAGE JES PHASE TIMES BY EXECUTION CLASS  -  +
               SUBMIT TIMESPAN FROM HH:MM ON YY.DDD TO HH:MM ON YY.DDD +
                   SYSID= ALL '
HEADLOHH EQU   HEADING+71,2
HEADLOMM EQU   HEADING+74,2
HEADLOYD EQU   HEADING+79,7
HEADHIHH EQU   HEADING+90,2
HEADHIMM EQU   HEADING+93,2
HEADHIYD EQU   HEADING+98,7
HEADSID  EQU   HEADING+116,4
TOTALS   DC    CL133'- 123456789 RECORDS READ    123456789 SMF TYPE 26 +
               RECORDS REJECTED DUE TO TIMESTAMP, SYSTEM-ID, TYPRUN, CA+
               NCELLATION, ETC. '
GETTOTAL EQU   TOTALS+1,10
REJTOTAL EQU   TOTALS+27,10
SUBHEAD  DC    CL133'-  CLASS   JOB-COUNT   RDR-TIME  CONV-WAIT  CONV-T+
               IME  EXEC-WAIT  EXEC-TIME   OUT-WAIT   OUT-TIME   PPP-WA+
               IT  (TIMES ARE IN MINUTES) '
DETAIL   DC    CL133'0 '
JTOTALIN DC    CL133'- 123456789 BATCH JOBS WERE SUBMITTED FOR EXECUTIO+
               N ON 123456789 CARD IMAGES '
JOBTOTAL EQU   JTOTALIN+1,10
CRDTOTAL EQU   JTOTALIN+54,10
LEGEND1  DC    CL133'1      CLASS = JOB EXECUTION CLASS  (''TSU'' FOR T+
               SO USERS AND ''STC'' FOR STARTED TASKS) '
LEGEND2  DC    CL133'-  JOB-COUNT = NUMBER OF JOBS IN THIS CLASS THAT W+
               ERE PURGED '
LEGEND3  DC    CL133'-   RDR-TIME = READER-STOP-TIME MINUS READER-START+
               -TIME  (ELAPSED TIME OF READING IN JOB) '
LEGEND4  DC    CL133'-  CONV-WAIT = CONVERTER-START-TIME MINUS READER-S+
               TOP-TIME  (ELAPSED TIME WAITING FOR JCL CONVERSION) '
LEGEND5  DC    CL133'-  CONV-TIME = CONVERTER-STOP-TIME MINUS CONVERTER+
               -START-TIME  (ELAPSED TIME OF JCL CONVERSION) '
LEGEND6  DC    CL133'-  EXEC-WAIT = EXECUTION-START-TIME MINUS CONVERTE+
               R-STOP-TIME  (ELAPSED TIME WAITING FOR JOB EXECUTION) '
LEGEND7  DC    CL133'-  EXEC-TIME = EXECUTION-STOP-TIME MINUS EXECUTION+
               -START-TIME  (ELAPSED TIME OF JOB EXECUTION) '
LEGEND8  DC    CL133'-   OUT-WAIT = OUTPUT-START-TIME MINUS EXECUTION-S+
               TOP-TIME  (ELAPSED TIME WAITING FOR OUTPUT PROCESSING) '
LEGEND9  DC    CL133'-   OUT-TIME = OUTPUT-STOP-TIME MINUS OUTPUT-START+
               -TIME  (ELAPSED TIME OF GENERATING OUTPUT ELEMENTS FOR T+
               HE PRINT/PUNCH QUEUE) '
LEGENDA  DC    CL133'-   PPP-WAIT = JOB-PURGE-TIME MINUS OUTPUT-STOP-TI+
               ME  (ELAPSED TIME ON THE PRINT/PUNCH QUEUE BEFORE BEING +
               PURGED FROM THE SPOOL) '
CLASS    EQU   DETAIL+5,1
JOBCNTR  EQU   DETAIL+10,10
RDRT     EQU   DETAIL+22,9
CONVW    EQU   DETAIL+33,9
CONVT    EQU   DETAIL+44,9
EXECW    EQU   DETAIL+55,9
EXECT    EQU   DETAIL+66,9
OUTW     EQU   DETAIL+77,9
OUTT     EQU   DETAIL+88,9
PURGEW   EQU   DETAIL+99,9
         PRINT NOGEN
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      +
               RECFM=FA,LRECL=133,BLKSIZE=133
SMFDATA  DCB   DSORG=PS,MACRF=GL,DDNAME=SMFDATA,BFTEK=A,EODAD=ENDOFSMF
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,EODAD=ENDOFCRD,          +
               RECFM=FB,LRECL=80,BLKSIZE=800,BUFNO=1
         PRINT GEN
         LTORG
         DS    0D                     END OF CSECT
         SPACE 2
SMFJBCOM COM
         DS    0D
         DS    256XL36                JOB CLASS ACCUMULATORS
         TITLE 'DSECTS AND EQUATES'
SMFRCD26 DSECT          START OF SMF TYPE 26 RECORD (JES2 PURGE)
SMF26PTR EQU   *        HEADER SEGMENT  (LGTH 46 WITHOUT RDW)
SMF26LEN DS    BL2      RECORD LENGTH
SMF26SEG DS    BL2      SEGMENT DESCRIPTOR
SMF26FLG DS    BL1      HEADER FLAG BYTE
SMF26RTY DS    BL1      RECORD TYPE 26
SMFJ26   EQU   26          PURGE RECORD TYPE
SMF26TME DS    BL4      TOD FROM TIME MACRO BINARY
SMF26DTE DS    PL4      DATE FROM TIME MACRO
SMF26SID DS    CL4      SYSTEM INDICATOR
SMF26JBN DS    CL8      JOB NAME
SMF26RST DS    BL4      RDR START TIME, TIME JOB CARD 1ST READ
SMF26RSD DS    PL4      READER START DATE
SMF26UIF DS    CL8      USER IDENTIFICATION FIELD
SMF26RSV DS    BL4      RESV
SMF26SBS DS    BL2      SUBSYSTEM GENERATING ID(JES2=2, JES3=5)
SMF26HSP EQU   X'0002'     JES2 ID
SMF26IND DS    BL2      INDICATORS
SMF26DES EQU   X'80'    DESCRIPTIVE SECTION PRESENT
SMF26EVT EQU   X'40'    EVENT SECTION PRESENT
SMF26ATU EQU   X'20'    ACTUALS SECTION PRESENT
SMF26NTW EQU   X'10'    NETWORK SECTION PRESENT
SMF26J2R EQU   X'08'    JES2 ROUTING SECTION PRESENT
SMF26JXP EQU   X'04'    JES PRINTER SECTION  PRESENT
SMF26SPL EQU   X'02'    JES2 SPOOL USE SECTION PRESENT
* BEGINNING OF DESCRIPTIVE SECTION
SMF26LN1 DS    BL2      LGTH OF THIS SECTION INCLUDING SELF
SMF26RV1 DS    BL2      RESV
SMF26IN2 DS    BL1      ADDITIONAL JOB INFORMATION(JES2 ONLY)
SMF26BCH EQU   X'80'    BIT 0 - BACKGROUND BATCH
SMF26FTS EQU   X'40'    1 - FOREGROUND TIME SHARING
SMF26STK EQU   X'20'    2 - SYSTEM TASK
SMF26NOJ EQU   X'10'    3 - NO JOURNAL OPTION
SMF26NOU EQU   X'08'    4 - NO OUTPUT OPTION
SMF26SCN EQU   X'04'    5 - TYPRUN=SCAN
SMF26CPY EQU   X'02'    6 - TYPRUN=COPY
SMF26JBF EQU   X'01'    7 - RESTART=Y
SMF26INF DS    BL1      JOB INFORMATION
SMF26JCP EQU   X'80'    0 - JOB PRIORITY EXTERNALLY ASSIGNED
*                          (JES2-VIA /*PRIORITY STMT)
*                          (JES3-VIA PRTY PARM ON JOB STMT)
SMF26STU EQU    X'40'   1 - SETUP JOB
*                          (JES2-/*SETUP STMT PROCESSED)
*                          (JES3-PROCESSED BY PREEXEC SETUP)
SMF26TRH EQU    X'20'   2 - JOB HELD VIA TYPERUN=HOLD
SMF26NLG EQU    X'10'   3 - JOB REQUESTED NO JES JOB LOG(JES2)
SMF26XBC EQU    X'08'   4 - EXEC BATCHING JOB (JES2 ONLY)
SMF26EIR EQU    X'04'   5 - JOB ENTERED VIA INTERNAL RDR
SMF26MRE EQU    X'02'   6 - JOB WAS RERUN BY JES
SMF26OPC EQU    X'01'   7 - OPER CANCELLED JOB BY JES CMND
SMF26JNM DS    CL4      JES ASSIGNED JOB #
SMF26JID DS    CL8      JOB ID OF FORM XXX0YYYY WHERE YYYY IS
*                       JES ASSIGNED JOB # & XXX IS
*                       'JOB' - NORMAL JES JOB
*                       'STC' - STARTED TASK CONTROL
*                       'TSU' - TSO USER
SMF26NAM DS    CL20     PROGRAMMER'S NAME FROM JOB CARD
SMF26MSG DS    CL1      MESSAGE CLASS FROM JOB CARD
SMF26CLS DS    CL1      JOB CLASS FROM JOB CARD
SMF26XPI DS    BL1      INITIAL JOB PRIORITY
SMF26XPS DS    BL1      SELECTION PRIORITY AT TIME JOB SELECTED
SMF26OPI DS    BL1      INITIAL OUTPUT PRIORITY(JES2 ONLY)
SMF26OPS DS    BL1      OUTPUT PRIORITY AT SELECTION(JES2 ONLY)
SMF26LOC DS    BL2      INPUT ROUTE CODE OR ZERO (JES2 ONLY)
SMF26DEV DS    CL8      LOG INPUT DEV NAME OF WHERE JOB READ
*                       USERID IF TSO SUBMIT
*                       SYSTEM NAME IF NJP
SMF26ACT DS    CL4      PROGRAMMER'S ACCOUNTING # (JES2 ONLY)
SMF26ROM DS    CL4      PROGRAMMER'S ROOM # (JES2 ONLY)
SMF26XTM DS    BL4      ESTIMATED EXECUTION TIME(SEC)
SMF26ELN DS    BL4      ESTIMATED OUTPUT LINES
SMF26EPU DS    BL4      ESTIMATED OUTPUT PUNCHED CARDS
*
SMF26J2D EQU   *        JES2 ONLY DESCRIPTIVE SECTION
SMF26FRM DS    CL4      DEFAULT OUTPUT FORM #
SMF26CYP DS    BL2      PRINT COPY COUNT IF FOR ALL OF JOB
SMF26LIN DS    BL2      LINES PER PAGE
SMF26PRR DS    BL2      DEFAULT PRINT DESTINATION
SMF26PUR DS    BL2      DEFAULT PUNCH DESTINATION
SMF26PDD DS    CL8      JES2 PROC DDNAME FOR JCL CONVERSION
* BEGINNING OF EVENT SECTION
SMF26LN2 DS    BL2      LGTH OF THIS SECTION(INCLUDING SELF   )
SMF26RV2 DS    BL2      RESV
SMF26RPT DS    BL4      READER STOP TIME
SMF26RPD DS    PL4      READER STOP DATE
SMF26CST DS    BL4      CONVERTER START TIME
SMF26CSD DS    PL4      CONVERTER START DATE
SMF26CPT DS    BL4      CONVERTER STOP TIME
SMF26CPD DS    PL4      CONVERTER STOP DATE
SMF26XST DS    BL4      EXECUTION START TIME
SMF26XSD DS    PL4      EXECUTION START DATE
SMF26XPT DS    BL4      EXECUTION STOP TIME
SMF26XPD DS    PL4      EXECUTION STOP DATE
SMF26OST DS    BL4      OUTPUT PROCESSOR START TIME
SMF26OSD DS    PL4      OUTPUT PROCESSOR START DATE
SMF26OPT DS    BL4      OUTPUT PROCESSOR STOP TIME
SMF26OPD DS    PL4      OUTPUT PROCESSOR STOP DATE
* BEGINNING OF ACTUALS SECTION
SMF26LN3 DS    BL2      LGTH OF THIS SECTION(INCLUDING SELF   )
SMF26RV4 DS    BL2      RESV
SMF26ICD DS    BL4      # OF INPUT CARDS (JCL AND DATA)
SMF26XLN DS    BL4      OUTPUT LINES GENERATED TO SPOOL
SMF26XPU DS    BL4      OUTPUT PUNCH CARDS GENERATED TO SPOOL
SMF26RID DS    CL4      INPUT PROCESSOR SYSTEM ID
SMF26CID DS    CL4      JCL CONVERSION PROCESSOR SYSTEM ID
SMF26XID DS    CL4      EXECUTION PROCESSOR SYSTEM ID
SMF26OID DS    CL4      OUTPUT PROCESSOR SYSTEM ID
*      BEGINNING OF NETWORK SECTION
SMF26LN4 DS    BL2      LENGTH OF THIS SECTION(INCLUDING SELF)
SMF26RV5 DS    BL2      RESERVED
SMF26NID DS    CL4      JOB TRANSMITTER SYSTEM IDENTIFIER
SMF26NST DS    BL4      JOB TRANSMITTER START TIME
SMF26NSD DS    PL4      JOB TRANSMITTER START DATE
SMF26NPT DS    BL4      JOB TRANSMITTER STOP TIME
SMF26NPD DS    PL4      JOB TRANSMITTER STOP DATE
SMF26NAC DS    CL8      NETWORK ACCOUNTING NUMBER
SMF26NJB DS    CL8      ORIGIONAL JOB IDENTIFICATION
SMF26NDV DS    CL8      JOB TRANSMITTER DEVICE NAME
SMF26NON DS    CL8      ORIGIONAL NODE NAME
SMF26NXN DS    CL8      EXECUTION NODE NAME
SMF26NNM DS    CL8      NEXT NODE NAME
SMF26NLN DS    CL8      LAST NODE NAME
*      BEGINNING OF  ROUTING SECTION
SMF26LN5 DS    BL2      LGTH OF THIS SECTION
SMF26INR DS    CL4      INPUT ROUTING
SMF26PRD DS    CL4      DEFAULT PRINT DESTINATION
SMF26PUD DS    CL4      DEFAULT PUNCH DESTINATION
*      BEGINNING OF  PRINT SECTION
SMF26LN6 DS    BL2      LGTH OF THIS SECTION
SMF26EBT DS    CL4      ESTIMATED BYTE COUNT
SMF26XBT DS    CL4      ACTUAL BYTE COUNT
SMF26EPG DS    CL4      ESTIMATED PAGE COUNT
SMF26XPG DS    CL4      ACTUAL PAGE COUNT
SMF26EFM DS    CL8      EXPANDED FORMS ID
         EJECT
JOBSTATS DSECT
JOBCOUNT DS    F                      NUMBER OF JOBS IN THIS CLASS
RDRTIME  DS    F                      ACCUMULATED READER TIME
CONVWAIT DS    F                      WAITING FOR CONVERSION TIME
CONVTIME DS    F                      ACCUMULATED CONVERTER TIME
EXECWAIT DS    F                      WAITING FOR EXECUTION TIME
EXECTIME DS    F                      ACCUMULATED EXECUTION TIME
OUTWAIT  DS    F                      WAITING FOR OUTPUT TIME
OUTTIME  DS    F                      ACCUMULATED OUTPUT TIME
PURGWAIT DS    F                      WAITING FOR PURGE TIME
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         END
