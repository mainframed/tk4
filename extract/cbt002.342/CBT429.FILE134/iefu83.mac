IEFU83   TITLE 'SMF RECORD (SVC 83 ENTRY) CONFIRMATION USER EXIT'
***********************************************************************
*                                                                     *
* MODULE: IEFU83 - SMF RECORD WRITE CONFIRMATION EXIT (SVC ENTRY)     *
*                                                                     *
* FUNCTION: TO ALLOW OR SUPPRESS THE WRITING OF SMF RECORDS           *
*           ACCORDING TO USER SPECIFICATION.  IN ADDITION, TO         *
*           PERFORM USER SPECIFIC PROCESSING.                         *
*                                                                     *
* MACROS: FROM SYS1.MACLIB AND SYS1.MODGEN.                           *
*                                                                     *
* ENTRY:  THIS MODULE RECEIVES CONTROL FROM SVC83/IGC0008C BEFORE     *
*         IT WRITES EACH SMF RECORD TO THE SMF DATASET.  NOTE THAT    *
*         THIS ROUTINE DOES NOT RECEIVE CONTROL FOR RECORDS FOR       *
*         WHICH WRITING HAS BEEN SUPPRESSED BY THE SYSTEM SMF OPTIONS *
*         OR RECORDS WHICH ARE WRITTEN VIA THE SMF BRANCH ENTRY.      *
*                                                                     *
*         REGISTERS AT ENTRY:                                         *
*         R1  -  ADDRESS OF SMF RECORD RDW                            *
*         R13 -  ADDRESS OF 72 BYTE SAVEAREA                          *
*         R14 -  RETURN ADDRESS                                       *
*         R15 -  ADDRESS OF IEFU83 (ENTRY POINT)                      *
*                                                                     *
* EXIT:   THIS MODULE MUST RESTORE ALL REGISTERS EXCEPT R15 WHICH     *
*         CONTAINS A RETURN CODE.  THE SMF RECORD MAY BE MODIFIED     *
*         BY THIS EXIT.                                               *
*                                                                     *
*         RETURN CODES:                                               *
*         0   -  WRITE THE SMF RECORD TO THE SMF DATASET.             *
*         4   -  SUPPRESS THE WRITING OF THE SMF RECORD.              *
*                                                                     *
* ATTRIBUTES: REUS,RENT,REFR, KEY 0, ENABLED, AMODE=31, RMODE=ANY.    *
*                                                                     *
* NOTE: GETMAINS DONE FOR SUBPOOL 231 (CSA, TCB'S KEY, FETCH          *
*       PROTECTED, PAGEABLE, NONSWAPABLE) BECAUSE THE REGION MAY BE   *
*       FULL WITH THE USER'S OWN DATA.                                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* WRITTEN BY:  GREG PRICE    FERNTREE COMPUTER SERVICES (MELB. AUST.) *
*              15 OCT 92     MVS/SP4.2.0                              *
*                                                                     *
*              20 OCT 92     DISPLAY 'VTOC FOR VOLSER' INSTEAD OF     *
*                            44X'04' WHEN VTOC IS ACCESSED (14/15).   *
*                                                                     *
*              22 OCT 92     REARRANGE 14/15/64 MESSAGES SO THERE     *
*                            IS ALWAYS ROOM TO SHOW THE BLOCK SIZE.   *
*                                                                     *
*              17 DEC 92     ISSUE MESSAGE FOR TAPE E-O-V AND SHOW    *
*                            VOLUME SERIAL NUMBER FOR TAPE RECORDS.   *
*                            SHOW '<1KB' FOR 1 TO 1023 BYTES RATHER   *
*                            THAN '0KB' WHICH COULD BE MISLEADING.    *
*                                                                     *
*              29 JAN 93     SUPPRESS MESSAGE IF DDNAME IS 'SMPPTS'   *
*                            AND AN INPUT BPAM FILE IS ACCESSED TO    *
*                            VASTLY REDUCE SMP APPLY AND ACCEPT       *
*                            PROCESSING MESSAGE VOLUMES.              *
*                                                                     *
*              09 FEB 93     NEW TAPE PROCESSING: MAKE AN "M" MESSAGE *
*                            FOR EACH TAPE VOLUME (E-O-V AND CLOSE)   *
*                            SHOWING BLOCK COUNT AND BYTE COUNT WHERE *
*                            PRACTICABLE.                             *
*                            ALSO, MAKE A "C" MESSAGE FOR CLOSE WITH  *
*                            THE TOTAL EXCP COUNT FOR THE WHOLE FILE. *
*                                                                     *
*              19 FEB 93     SUPPRESS PROBABLY MISLEADING BYTE        *
*                            COUNT FOR VARIABLE-LENGTH UNBLOCKED      *
*                            RECORDS, AS IS ALSO DONE FOR RECFM=U.    *
*                                                                     *
*              31 MAY 93     SHOW BYTE TRANSFERRED COUNT ESTIMATE     *
*                            IF RECFM=VB BY CORRECTING JFCRFO IN      *
*                            PREVIOUS CHANGE TO JFCRFB.               *
*                                                                     *
*              03 MAY 94     INCLUDE KEY IN BYTE COUNT FOR DISK       *
*                            FILES.  FOR VARIABLE LENGTH RECORDS      *
*                            ONLY ESTIMATE BYTE COUNT WHEN BLOCKED    *
*                            AND ONLY FOR OUTPUT (TYPE 15) BUT NOT    *
*                            IF QSAM LOCATE MODE IS USED.  BYTE       *
*                            COUNT COULD BE WRONG ON INPUT BECAUSE    *
*                            THE FILE MAY HAVE BEEN CREATED WITH      *
*                            QSAM LOCATE MODE.  QSAM LOCATE MODE      *
*                            ON OUTPUT (DSORG=PS,MACRF=PL) WILL       *
*                            ALWAYS PRODUCE SHORT BLOCKS (UNLESS      *
*                            ALL RECORDS HAVE MAXIMUM LENGTH, IN      *
*                            WHICH CASE THERE IS NO BLOCKING).        *
*                            EXPECTED "SHORTNESS" IS BLOCKSIZE        *
*                            MINUS LOGICAL-RECORD-LENGTH BYTES.       *
*                            REPLACE BYTE COUNT ESTIMATE FOR QSAM     *
*                            PUT LOCATE FOR VARIABLE LENGTH RECORDS   *
*                            WITH 'BLKNG=1' WHEN BLKSIZE = LRECL+4    *
*                            AND WITH 'BLKNG<2' WHEN BLKSIZE IS NOT   *
*                            GREATER THAN TWICE THE LRECL.            *
*                                                                     *
*              14 MAY 96     USE 4K AS BLOCK SIZE WHEN CALCULATING    *
*                            PDSE BYTE TRANSFER COUNT.                *
*                            (REASSEMBLE REQUIRED FOR SP5.2.2/DFSMS.) *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*             PERFORMED PROCESSING:                                   *
*                                                                     *
*  TYPE 14  - ISSUE A WTO MESSAGE CONTAINING VARIOUS FILE DETAILS     *
*             INCLUDING THE EXCP COUNT FOR BATCH JOBS ONLY.           *
*                                                                     *
*  TYPE 15  - ISSUE A WTO MESSAGE CONTAINING VARIOUS FILE DETAILS     *
*             INCLUDING THE EXCP COUNT FOR BATCH JOBS ONLY.           *
*                                                                     *
*  TYPE 21  - ISSUE A WTO MESSAGE SIMILAR TO THE MVT IEC209I MESSAGE. *
*                                                                     *
*  TYPE 64  - ISSUE A WTO MESSAGE CONTAINING VARIOUS FILE DETAILS     *
*             INCLUDING THE EXCP COUNT FOR BATCH JOBS ONLY.           *
*                                                                     *
*         MPF CAN BE USED TO ADVANTAGE TO CONTROL THE DISTRIBUTION    *
*         OF MESSAGES ISSUED BY THIS EXIT.                            *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*     IT IS EXPECTED THAT MOST INSTALLATIONS WOULD NOT WANT THE       *
*  MESSAGES GENERATED BY THIS EXIT TO BE DISPLAYED ON SYSTEM OPERATOR *
*  CONSOLES.  MPF IS THE BEST PLACE TO EFFECT THIS.  'MCS=HRDCPY'     *
*  COULD BE SPECIFIED ON THE WTO MACROS, BUT THIS INTRODUCES SOME     *
*  COMPLICATIONS WHEN MESSAGE HANDLING IS FURTHER CUSTOMIZED BY       *
*  INSTALLATION-SUPPLIED MPF EXITS SUCH AS 'MSGNOLOG'.                *
*                                                                     *
*     THE IDEA IS THAT 'U83021I' APPEARS IN THE JOB LOG WITH THE      *
*  TAPE MOUNT AND DEMOUNT MESSAGES.  HENCE, ROUTCDE=(3,5) FOR THIS    *
*  MESSAGE.  THE MPF EXIT 'MSGJOBLG' CAN BE USED TO PREVENT THIS      *
*  MESSAGE APPEARING ON THE SYSTEM LOG.                               *
*                                                                     *
*     IT IS INTENDED THAT THE FILE ACTIVITY MESSAGES APPEAR IN THE    *
*  JOB MESSAGES, BUT NOT THE JOB LOG.  ROUTCDE=(11) IS USED FOR THE   *
*  JOB MESSAGES DATA SET.  THE MPF EXIT 'MSGNOJLG' CAN BE USED TO     *
*  PREVENT THE MESSAGES APPEARING IN THE JOB LOG DATA SET.            *
*                                                                     *
*     IN SUMMARY, MESSAGES ARE ISSUED HERE WITH THE WIDEST DESIRABLE  *
*  DISTRIBUTION, TO BE LIMITED ACCORDING TO INSTALLATION REQUIREMENTS *
*  BY MPF (MESSAGE PROCESSING FACILITY).  THUS, MESSAGE DISTRIBUTION  *
*  CAN BE CUSTOMIZED DYNAMICALLY WITH THE 'SET MPF' MVS COMMAND,      *
*  WITHOUT THE NEED TO RE-INSTALL THIS EXIT OR EVEN RE-IPL.           *
*                                                                     *
*     IF A REQUIRED MESSAGE IS BASED UPON AN SMF RECORD TYPE NOT      *
*  USUALLY COLLECTED BY AN INSTALLATION, THE SMF OPTIONS CAN BE       *
*  ALTERED TO REQUEST THAT THE SMF RECORD TYPE IS TO BE COLLECTED,    *
*  AND THIS EXIT CAN BE CHANGED TO DELETE THE SMF RECORD AFTER THE    *
*  APPROPRIATE MESSAGE HAS BEEN ISSSUED.  THE SOURCE CODE ALTERATION  *
*  FOR THIS IS SIMPLY TO CHANGE THE RELEVANT 'B     WRITEREC'         *
*  INSTRUCTION TO 'B     SUPRSREC'.                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*  REGISTER USAGE                                                     *
*                                                                     *
R0       EQU   0                                                      *
R1       EQU   1                                                      *
R2       EQU   2                    SMF RECORD BASE                   *
R3       EQU   3                    WORK (MESSAGE BUFFER POINTER)     *
R4       EQU   4                    WORK                              *
R5       EQU   5                    WORK                              *
R6       EQU   6                                                      *
R7       EQU   7                                                      *
R8       EQU   8                                                      *
R9       EQU   9                    WORK AREA BASE                    *
R10      EQU   10                                                     *
R11      EQU   11                                                     *
R12      EQU   12                   PROGRAM BASE                      *
R13      EQU   13                   CALLER'S SAVE AREA BASE           *
R14      EQU   14                   RETURN ADDRESS                    *
R15      EQU   15                   ENTRY POINT, WORK, RETURN CODE    *
*                                                                     *
***********************************************************************
         TITLE 'MAINLINE - GLOBAL PROCESSING AND RECORD TYPE CHECK'
IEFU83   CSECT
IEFU83   AMODE 31
IEFU83   RMODE ANY
         USING IEFU83,R15
         B     U83$
         DC    AL1(15),CL15'IEFU83 &SYSDATE'
         DROP  R15                  IEFU83
U83$     STM   R14,R12,12(R13)      SAVE REGISTERS
         LR    R12,R15              R12 - PROGRAM BASE REGISTER
         USING IEFU83,R12
         ICM   R2,15,0(R1)          R2  - SMF RECORD BASE REGISTER
         BZ    WRITEREC             ZERO ADDRESS - CAN'T FIND RECORD
         USING SMFRECD,R2
         SPACE
         CLI   SMF14RTY,14          TYPE 14 RECORD?
         BE    REC1415$             YES
         CLI   SMF14RTY,15          TYPE 15 RECORD?
         BE    REC1415$             YES
         CLI   SMF21RTY,21          TYPE 21 RECORD?
         BE    REC21$               YES
         CLI   SMF64RTY,64          TYPE 64 RECORD?
         BE    REC64$               YES
         SPACE
WRITEREC LM    R14,R12,12(R13)      EXIT AND WRITE RECORD
         SR    R15,R15                       RC=0
         BR    R14
         SPACE
SUPRSREC LM    R14,R12,12(R13)      EXIT AND SUPPRESS RECORD
         LA    R15,4                         RC=4
         BR    R14
         SPACE 2
         USING PSA,0
         TITLE 'SMF RECORD 14 AND 15 - NON-VSAM DATA SET CLOSE/E-O-V'
***********************************************************************
*                                                                     *
* RECORD TYPE 14 AND 15 PROCESSING: FORMAT AND WRITE A U83@##I        *
*  MESSAGE WHERE ## IS '14' OR '15', AS APPROPRIATE, AND @ IS (IN     *
*  ORDER OF PRECEDENCE) 'M' FOR EACH MAGNETIC TAPE VOLUME E-O-V AND   *
*  CLOSE, 'C' FOR CLOSE ON MAGNETIC TAPE DATA SETS, 'I' FOR ISAM      *
*  DATA SETS, 'V' FOR VIO DATA SETS, 'T' FOR OTHER TEMPORARY DATA     *
*  SETS, 'E' FOR PDSE DATA SETS, AND OTHERWISE 'P' FOR OTHER (DISK)   *
*  PERMANENT DATA SETS.                                               *
*                                                                     *
* U83@##I ???PUT ??AM FILE DDDDDDDD CLOSED 123456789 EXCPS FOR        *
*         12345678901234567890123456789012345678901234 BLKSZ=12345    *
*         12345KB                                                     *
***********************************************************************
         SPACE
REC1415$ L     R4,PSAAOLD           POINT TO CURRENT ASCB
         USING ASCB,R4
         ICM   R1,B'1111',ASCBJBNI  BATCH JOB?
         BZ    WRITEREC             NO, DO NOT DISPLAY ANY MESSAGE
         DROP  R4                   ASCB
         TM    SMF14RIN,SMF14EOV+SMF14DAD
         BO    WRITEREC             NO MESSAGE FOR DASD E-O-V RECORDS
         GETMAIN RC,LV=WK14LEN,SP=231,LOC=(ANY)
         LTR   R15,R15              TEST FOR SUCCESSFUL GETMAIN
         BNZ   WRITEREC             FAILURE - JUST WRITE SMF RECORD
         LR    R9,R1                ADDRESS GETMAINED AREA
         USING WK83AREA,R9
         MVC   WTOU83D(WTO1415L),WTO1415S INITIALIZE WTO PARAMETER LIST
         CLI   SMF14RTY,14          TYPE 14 SMF RECORD?
         BE    T1415OK              YES, CORRECT NUMBER IN MESSAGE
         MVI   WTOMSGID+5,C'5'      NO, MUST BE A TYPE 15 SMF RECORD
T1415OK  TM    SMF14RIN,SMF14DAD    MAGNETIC TAPE VOLUME (CLOSE/E-O-V)?
         BZ    DSOR1415             YES, MESSAGE ID AND TEXT IS CORRECT
CLOS1415 MVI   WTOMSGID+3,C'C'      TRY FOR CLOSE ON MAGNETIC TAPE
         TM    SMF14RIN,SMF14DAD    MAGNETIC TAPE DATA SET?
         BZ    ID1415OK             YES, MESSAGE ID IS CORRECT
         MVI   WTOMSGID+3,C'I'      NO, TRY FOR ISAM
         TM    SMF14RIN,SMF14IS+SMF14JIS  ISAM?
         BNZ   ID1415OK             YES, MESSAGE ID IS CORRECT
         MVI   WTOMSGID+3,C'V'      NO, TRY FOR VIO
         TM    SMF14RIN,SMF14VIO    VIO?
         BNZ   ID1415OK             YES, MESSAGE ID IS CORRECT
         MVI   WTOMSGID+3,C'T'      NO, TRY FOR NON-VIO TEMPORARY
         TM    SMF14RIN,SMF14TDS    TEMPORARY?
         BNZ   ID1415OK             YES, MESSAGE ID IS CORRECT
         MVI   WTOMSGID+3,C'E'      NO, TRY FOR PDSE
         TM    SMF14RIN+1,SMF14IPD  PDSE?
         BNZ   ID1415OK             YES, MESSAGE ID IS CORRECT
         MVI   WTOMSGID+3,C'P'      NO, SIGNAL PERMANENT DISK FILE
ID1415OK MVC   WTOAMDES,=C' EXCPS FOR '   LOAD CORRECT DESCRIPTION
DSOR1415 TM    SMFDCBMF,X'80'       EXCP PROCESSING USED?
         BNZ   AM1415OK             YES, MESSAGE IS CORRECT
         TM    SMFDCBOR,JFCORGIS+JFCORGPS+JFCORGDA+JFCORGPO
*                                   KNOWN DATA SET ORGANIZATION?
         BZ    AM1415OK             NO, JUST SAY EXCP
         MVC   WTOAMNAM,=C'ISAM'    YES
         TM    SMFDCBOR,JFCORGIS    DCBDSORG=IS?
         BO    AM1415OK             YES
         MVC   WTOAMNAM,=C'BDAM'    NO
         TM    SMFDCBOR,JFCORGDA    DCBDSORG=DA?
         BO    AM1415OK             YES
         MVC   WTOAMNAM,=C'BPAM'    NO
         TM    SMFDCBOR,JFCORGPO    DCBDSORG=PO?
         BO    PTSCHECK             YES, GO CHECK FOR SMPPTS INPUT
         MVC   WTOAMNAM,=C'BSAM'    NO, DCBDSORG=PS
         LA    R1,SMFDCBMF          POINT TO DCBMACRF
         LA    R0,2                 2 BYTES TO TEST (INPUT AND OUTPUT)
MACR14LP TM    0(R1),X'60'          TEST BASIC AND QUEUED BITS
         BM    PS14TEST             DIFFERENT SO SEE WHICH TYPE
         LA    R1,1(,R1)            POINT TO NEXT BYTE
         BCT   R0,MACR14LP
         B     AM1415OK             SHOULD NOT HAPPEN BUT LEAVE AS BSAM
PTSCHECK TM    SMFDEBOP,X'0F'       BPAM FILE OPENED FOR INPUT?
         BNZ   AM1415OK             NO, PRODUCE THE MESSAGE
         CLC   =CL8'SMPPTS  ',SMFTIOE5  TEST FOR SMP PTS FILE TO
         BE    MS1415FM                 REDUCE APPLY/ACCEPT MSG VOLUMES
         B     AM1415OK             OTHERWISE, PRODUCE THE MESSAGE
PS14TEST TM    0(R1),X'40'          IS THE QUEUED BIT ON?
         BZ    AM1415OK             NO, SO THE BASIC BIT IS ON
         MVC   WTOAMNAM,=C'QSAM'    YES
AM1415OK MVC   WTOAMDDN,SMFTIOE5    SHOW THE DDNAME (LOGICAL FILE NAME)
         TM    SMFDEBOP,X'08'       WAS THE FILE OPENED FOR OUTPUT?
         BO    OPND1415             YES, MESSAGE IS CORRECT
         IC    R1,SMFDEBOP          NO, GET DEBOPATB I/O METHOD FLAGS
         SLL   R1,28                SHIFT OUT DISPOSITION FLAGS
         SRL   R1,28-3              MULTIPLY BY EIGHT FOR INDEX
         LA    R1,OPNTYPES(R1)      POINT TO OPEN TYPE LITERAL
         MVC   WTOAMOPN,0(R1)       SHOW THE OPEN TYPE
OPND1415 TM    SMF14RIN,SMF14DAD    DASD DATA SET?
         BZ    LD1415DS             NO, SHOW DATA SET NAME
         CLI   JFCBDSNM,X'04'       YES, ACCESSING THE VTOC?
         BNE   LD1415DS             NO, SHOW DATA SET NAME
         MVC   WTOAMDSN(7),=C'VTOC OF'  YES, DO NOT SHOW X'04' BYTES
         MVC   WTOAMDSN+8(6),JFCBVOLS        BUT 'VTOC OF VOLSER'
         B     DS1415OK             DATA SET NAME FIELD NOW LOADED
LD1415DS MVC   WTOAMDSN,JFCBDSNM    SHOW THE DSN (PHYSICAL FILE NAME)
         CLI   WTOMSGID+3,C'M'      MAGNETIC TAPE VOLUME MESSAGE?
         BNE   DS1415OK             NO, GO LOOP THROUGH UCB SECTIONS
         MVI   WTOAMSLA,C'/'        YES, SO INDICATE ON THIS TAPE ONLY
         MVC   WTOAMVOL,SMFSRTEV    SHOW TAPE VOLUME WITH THIS EXCP CT
         ICM   R5,15,SMFDCBBL       LOAD VOLUME EXCP COUNT
         B     UN1415EX             DISPLAY THIS AS THE EXCP COUNT
DS1415OK SLR   R5,R5                CLEAR EXCP COUNT ACCUMULATOR
         SLR   R0,R0                CLEAR FOR INSERT
         ICM   R0,1,SMF14NUC        GET THE NUMBER OF UCB SECTIONS
         BZ    UN1415EX             HANDLE BAD DATA
         SLR   R1,R1                CLEAR FOR INSERT
         ICM   R1,1,SMF14SUC        GET THE UCB SECTION SIZE
         BZ    UN1415EX             HANDLE BAD DATA
         LA    R15,SMF14UCB         POINT TO THE FIRST UCB SECTION
         USING SMF14UCB,R15         NO
UN1415LP ICM   R4,15,SMFEXCP        GET THE EXCP COUNT FOR THIS UNIT
         ALR   R5,R4                ADD TO THE ACCUMULATOR
         ALR   R15,R1               POINT TO THE NEXT UCB SECTION
         BCT   R0,UN1415LP          PROCESS NEXT UCB SECTION IF ANY
         DROP  R15                  SMF14UCB
UN1415EX CVD   R5,WORK              GET DECIMAL TOTAL EXCP COUNT
         MVC   WTOAMXCP,ED9         LOAD EDIT MASK
         ED    WTOAMXCP,WORK+3      SHOW EXCP COUNT UP TO (10**9)-1
         SLR   R15,R15              CLEAR FOR INSERT
         ICM   R15,3,JFCBLKSI       GET THE DATA SET BLOCK SIZE
         TM    SMF14RIN+1,SMF14IPD  PDSE?
         BNZ   PDSE1415             YES
         LTR   R15,R15              TEST THE BLOCK SIZE
         BZ    MS1415OK             ZERO, SO SKIP CALCULATION
         TM    JFCRECFM,JFCUND      UNDEFINED RECORD FORMAT?
         BNM   BLSZ1415             YES, JUST SHOW BLOCK SIZE
         TM    JFCRECFM,JFCFIX      FIXED FORMAT?
         BO    NVPL1415             YES, "NOT VARIABLE PUT LOCATE"
         TM    JFCRECFM,JFCRFB      VARIABLE RECORDS, BUT BLOCKED?
         BZ    BLSZ1415             NO, JUST SHOW BLOCK SIZE
         CLI   SMF14RTY,14          OPENED FOR INPUT (OR RDBACK)?
         BE    BLSZ1415             YES, FILE MAY BE FROM PUT LOCATE
         TM    SMFDCBMF+1,X'48'     OUTPUT, BUT IS IT QSAM PUT LOCATE?
         BNO   NVPL1415             NO, "NOT VARIABLE PUT LOCATE"
         SLR   R0,R0
         ICM   R0,3,JFCLRECL        GET THE LOGICAL RECORD LENGTH
         BNP   BLSZ1415             JUST SHOW BLOCK SIZE IF STRANGE
         LR    R14,R15              COPY BLOCK SIZE
         SR    R14,R0               SUBTRACT LRECL
         CR    R14,R0               IS DIFFERENCE BIGGER THAN LRECL?
         BH    BLSZ1415             YES, JUST SHOW BLOCK SIZE
         MVC   WTOAMSIZ+1(7),=C'BLKNG<2'  NO, GETTING SHORT BLOCKS
         LA    R0,4                 GET FOUR
         CR    R14,R0               IS IT TRUE THAT BLKSIZE=LRECL+4?
         BH    BLSZ1415             NO, CAN'T PROVE NO BLOCKING AT ALL
         MVC   WTOAMSCL,=C'=1'      YES, INDICATE ABSOLUTELY UNBLOCKED
         B     BLSZ1415             NOW SHOW THE BLOCK SIZE
PDSE1415 LTR   R5,R5                ANY EXCPS?
         BNZ   PDSEEXCP             YES
         LTR   R15,R15              IS THE BLOCK SIZE ZERO?
         BZ    MS1415OK             YES, SO SKIP ITS DISPLAY
         B     BLSZ1415             NO, SHOW THE BLOCK SIZE
PDSEEXCP SLR   R4,R4                CLEAR FOR SHIFT
         SLDL  R4,12                MULTIPLY EXCP COUNT BY 4096
         B     PDSEBLKS             GO SHOW THE BYTE TRANSFER COUNT
NVPL1415 TM    SMFDCBOR,JFCORGPS    SEQUENTIAL FILE PROCESSING?
         BZ    BLSZ1415             NO, JUST SHOW BLOCK SIZE
         CLI   WTOMSGID+3,C'M'      MAGNETIC TAPE VOLUME MESSAGE?
         BE    SQ1415OK             YES, CAN SHOW BYTE COUNT RELIABLY
         TM    SMFDCBMF,X'80'       EXCP ACCESS METHOD USED?
         BNZ   BLSZ1415             YES, JUST SHOW BLOCK SIZE
         CLI   SMF14RTY,14          OPENED FOR INPUT (OR RDBACK)?
         BNE   PMBR1415             NO
         BCTR  R5,0                 YES, DECREMENT FOR E-O-F (OR TM)
PMBR1415 TM    JFCDSORG,JFCORGPO    SEQUENTIAL ACCESS OF A PDS?
         BZ    SQ1415OK             NO
         LA    R15,256              YES, GET PDS DIRECTORY BLOCK SIZE
         CLC   JFCBELNM,=CL8' '     SEQUENTIAL ACCESS OF A PDS MEMBER?
         BE    SQ1415OK             NO
         BCTR  R5,0                 YES, DECREMENT EXCP COUNT FOR BLDL
         LA    R15,WTOAMDSN+43      POINT TO LAST DSNAME CHARACTER
NVSDSNLP CLI   0(R15),C' '          TRAILING BLANK?
         BNE   NVSMEMNM             NO
         BCT   R15,NVSDSNLP         YES, BACK UP ONE AND TEST AGAIN
NVSMEMNM CLI   11(R15),C' '         ROOM FOR '(*MEMBER*)'?
         BNE   NVSMEMDN             NO, DO NOT SHOW MEMBER NAME
         MVI   1(R15),C'('          YES, SUPPLY LEFT PARENTHESIS
         MVC   2(8,R15),JFCBELNM    LOAD PDS MEMBER NAME
NVSMEMLP CLI   9(R15),C' '          TRAILING BLANK?
         BNE   NVSMEMOK             NO
         BCT   R15,NVSMEMLP         YES, BACK UP ONE AND TEST AGAIN
NVSMEMOK MVI   10(R15),C')'         SUPPLY RIGHT PARENTHESIS
NVSMEMDN SLR   R15,R15              CLEAR FOR INSERT
         ICM   R15,3,JFCBLKSI       GET THE DATA SET BLOCK SIZE
SQ1415OK LTR   R5,R5                POSITIVE EXCP COUNT?
         BNP   BLSZ1415             NO, JUST SHOW BLOCK SIZE
         TM    SMF14RIN,SMF14DAD    DASD DATA SET?
         BZ    SEQKEYOK             NO, CAN'T HAVE A KEY
         SLR   R0,R0                YES
         IC    R0,JFCKEYLE          GET THE KEY LENGTH USED
         AR    R15,R0               ALSO COUNT KEY BYTES TRANSFERRED
SEQKEYOK MR    R4,R15               GET BYTES (BLOCK_COUNT*SIZE)
PDSEBLKS MVC   WTOAMSIZ+4(2),=C'<1' REPLACE '0KB' WITH '<1KB'
         LA    R1,=C'KMGT'          POINT TO KILO/MEGA/GIGA/TERA FLAGS
SZ1415LP SRDA  R4,10                DIVIDE BY 1024
         BZ    SZ1415DN             IF ZERO THEN LESS THAN ONE
         LTR   R4,R4                NUMBER LARGER THAN ONE FULLWORD?
         BNZ   SZ1415BG             YES, TOO BIG
         CL    R5,=F'99999'         NO, IS NUMBER BIGGER THAN 99,999?
         BNH   SZ1415OK             NO, SHOW THE NUMBER
SZ1415BG LA    R1,1(,R1)            POINT TO NEXT SCALE INDICATOR
         B     SZ1415LP             CONTINUE TO SCALE DOWN
SZ1415OK CVD   R5,WORK              SHOW THE APPROXIMATE BYTE COUNT
         MVC   WTOAMSIZ,ED5         LOAD EDIT MASK
         ED    WTOAMSIZ,WORK+5
SZ1415DN MVC   WTOAMSCL(1),0(R1)    SHOW SCALING INDICATOR
         MVI   WTOAMSCL+1,C'B'      INDICATE BYTES
         ICM   R15,3,JFCBLKSI       GET THE DATA SET BLOCK SIZE AGAIN
         BZ    MS1415OK             HANDLE PDSE WITH ZERO BLOCK SIZE
BLSZ1415 CVD   R15,WORK             SHOW BLOCK SIZE
         OI    WORK+7,15            PREPARE FOR UNPACK
         MVC   WTOAMBLK,=C'BLKSZ='
         UNPK  WTOAMBLS,WORK+5(3)   LOAD BLOCK SIZE NUMERICS
BLSZ14LP CLI   WTOAMBLS,C'0'        LEADING ZERO?
         BNE   MS1415OK             NO, FORMATTING COMPLETE
         MVC   WTOAMBLS,WTOAMBLS+1  YES, SHUFFLE UP TO SUPPRESS IT
         B     BLSZ14LP             CONTINUE WITH LEADING ZERO CHECK
MS1415OK DS    0H                   MESSAGE NOW READY FOR DISPLAY
         SPACE
         WTO   MF=(E,WTOU83D)       DISPLAY U83?14I OR U83?15I
         SPACE
         CLI   WTOMSGID+3,C'M'      MAGNETIC TAPE VOLUME MESSAGE?
         BNE   MS1415FM             NO, GO TIDY UP AND EXIT
         TM    SMF14RIN,SMF14EOV    CLOSE OF TAPE FILE?
         BO    MS1415FM             NO, JUST E-O-V FOR THIS VOLUME
         MVI   WTOAMSLA,C' '        YES, PRODUCE EXTRA MESSAGE TO SHOW
         MVC   WTOAMVOL,=C'CLOSED'       EXCP (AS OPPOSED TO BLOCK)
         MVI   WTOAMBLK,C' '             COUNT FOR WHOLE FILE
         MVC   WTOAMBLK+1(18),WTOAMBLK
         B     CLOS1415
         SPACE
         DROP  R9                   WK83AREA
         SPACE
MS1415FM DS    0H                   FREE GETMAINED STORAGE
         FREEMAIN RU,LV=WK14LEN,A=(R9),SP=231
         B     WRITEREC             ALL DONE, GO WRITE TYPE 14/15 REC
         TITLE 'SMF RECORD 21 - ERROR STATISTICS BY VOLUME (TAPE ONLY)'
***********************************************************************
*                                                                     *
* RECORD TYPE 21 PROCESSING:  FORMAT AND WRITE A U83021I MESSAGE      *
*  BASED ON IEC209I FROM OS/MVT.  COUNTERS ARE ONLY SHOWN IF          *
*  NON-ZERO, EXCEPT THE SSCH COUNT WHICH IS ALWAYS SHOWN.             *
*                                                                     *
* U83021I JJJJJJJJ,VVVVVV,DEV,SIO=12345,DEN=1,BLKSIZE=12345,          *
*         TR=123,TW=123,PR=123,PW=123,NB=123,ERG=12345,CLN=12345,     *
*         TRF=12345,TRB=12345,TRW=12345,BR=123456789,BW=123456789     *
*                                                                     *
***********************************************************************
         SPACE
REC21$   GETMAIN RC,LV=WK21LEN,SP=231,LOC=(ANY)
         LTR   R15,R15              TEST FOR SUCCESSFUL GETMAIN
         BNZ   WRITEREC             FAILURE - JUST WRITE SMF RECORD
         LR    R9,R1                ADDRESS GETMAINED AREA
         USING WK83AREA,R9
         MVC   WTOU83D(WTO21L),WTO21S     INITIALIZE WTO PARAMETER LIST
         L     R4,548               POINT TO CURRENT ASCB
         USING ASCB,R4
         ICM   R1,B'1111',ASCBJBNI  POINT TO ADDRESS SPACE NAME
         BNZ   LDJOB21              USE IT IF FOUND
         ICM   R1,B'1111',ASCBJBNS  POINT TO ADDRESS SPACE NAME
         BNZ   LDJOB21              USE IT IF FOUND
         DROP  R4                   ASCB
         LA    R1,=C'STARTING'      IN CASE OF NULL JOB NAME POINTER
LDJOB21  MVC   WTOTEXT(8),0(R1)     LOAD ADDRESS SPACE NAME
         LA    R3,WTOTEXT+7         POINT TO LAST BYTE OF JOB NAME
JOB21LP  CLI   0(R3),C' '           TRAILING BLANK?
         BNE   JOB21OK              NO
         BCT   R3,JOB21LP           YES, BACK UP ONE AND TEST AGAIN
JOB21OK  MVI   1(R3),C','           SUPPLY SEPARATOR
         MVC   2(6,R3),SMF21VOL     LOAD TAPE VOLUME SERIAL NUMBER
VOL21LP  CLI   7(R3),C' '           TRAILING BLANK?
         BNE   VOL21OK              NO
         BCT   R3,VOL21LP           YES, BACK UP ONE AND TEST AGAIN
VOL21OK  MVI   8(R3),C','           SUPPLY SEPARATOR
         UNPK  9(5,R3),SMF21CA(3)   LOAD DEVICE NUMBER
         TR    9(4,R3),HEX-240      MAKE PRINTABLE HEX CHARACTERS
         MVI   13(R3),C','          SUPPLY SEPARATOR
         CLI   9(R3),C'0'           LEADING ZERO IN DEVICE NUMBER?
         BNE   DEV21OK              NO
         MVC   9(5,R3),10(R3)       YES, SUPPRESS IT
         BCTR  R3,0                 ADJUST POINTER
DEV21OK  LA    R3,14(,R3)           POINTER TO FIRST BLANK AFTER COMMA
         SLR   R0,R0                CLEAR FOR INSERTS
         ICM   R0,3,SMF21SIO        LOAD SSCH COUNT
         LA    R1,=C'SIO'           POINT TO LABEL
         BAS   R14,CNTR3FST         SHOW I/O COUNT
         TM    SMF21DEN,X'03'       VALID DENSITY?
         BNO   DEN21OK              NO
         IC    R0,SMF21DEN          YES, GET DENSITY BYTE FOR 2400/3400
         LR    R1,R0                COPY IT
         SLL   R0,24+2
         SRL   R0,24+2+4            GET BITS 2 AND 3 AS A NUMBER
         SLL   R1,6                 GET BITS 0 AND 1 AS A NUMBER
         AR    R1,R0                ADD THE TWO NUMBERS (GET 0 TO 6)
         MVC   0(5,R3),=C',DEN='    SUPPLY DENSITY LABEL
         STC   R1,5(,R3)            SUPPLY DENSITY VALUE
         OI    5(R3),X'F0'          MAKE PRINTABLE (ASSUME < 10)
         LA    R3,6(,R3)            ADJUST DISPLAY LINE POINTER
DEN21OK  DS    0H
         ICM   R0,3,SMF21BLS        LOAD BLOCKSIZE
         BZ    BLS21OK              ZERO SO DON'T SHOW IT
*        LA    R1,=C'BLS'           POINT TO LABEL
         MVC   0(5,R3),=C',BLKS'    WANT 'BLKSIZE=', NOT 'BLS='
         LA    R3,5(,R3)            ADJUST DISPLAY LINE POINTER
         LA    R1,=C'IZE'           POINT TO LABEL
         BAS   R14,CNTR3FST         SHOW BLOCKSIZE
         SPACE
BLS21OK  IC    R0,SMF21TR           LOAD TEMPORARY READ ERROR COUNT
         LA    R1,=C'TR'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW TEMPORARY READ ERROR COUNT
         SPACE
         IC    R0,SMF21TW           LOAD TEMPORARY WRITE ERROR COUNT
         LA    R1,=C'TW'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW TEMPORARY WRITE ERROR COUNT
         SPACE
         IC    R0,SMF21PR           LOAD PERMANENT READ ERROR COUNT
         LA    R1,=C'PR'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW PERMANENT READ ERROR COUNT
         SPACE
         IC    R0,SMF21PW           LOAD PERMANENT WRITE ERROR COUNT
         LA    R1,=C'PW'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW PERMANENT WRITE ERROR COUNT
         SPACE
         IC    R0,SMF21NB           LOAD NOISE BLOCK COUNT
         LA    R1,=C'NB'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW NOISE BLOCK COUNT
         SPACE
         ICM   R0,3,SMF21ERG        LOAD ERASE GAP COUNT
         LA    R1,=C'ERG'           POINT TO LABEL
         BAS   R14,COUNTER3         SHOW ERASE GAP COUNT
         SPACE
         ICM   R0,3,SMF21CLN        LOAD CLEANER ACTION COUNT
         LA    R1,=C'CLN'           POINT TO LABEL
         BAS   R14,COUNTER3         SHOW CLEANER ACTION COUNT
         SPACE
         ICM   R0,3,SMF21TRF        LOAD TEMP READ FORWARD ERROR COUNT
         LA    R1,=C'TRF'           POINT TO LABEL
         BAS   R14,COUNTER3         SHOW TEMP READ FORWARD ERROR COUNT
         SPACE
         ICM   R0,3,SMF21TRB        LOAD TEMP READ BACKWARD ERROR COUNT
         LA    R1,=C'TRB'           POINT TO LABEL
         BAS   R14,COUNTER3         SHOW TEMP READ BACKWARD ERROR COUNT
         SPACE
         ICM   R0,3,SMF21TWF        LOAD TEMPORARY WRITE ERROR COUNT
         LA    R1,=C'TWF'           POINT TO LABEL
         BAS   R14,COUNTER3         SHOW TEMPORARY WRITE ERROR COUNT
         SPACE
         ICM   R0,7,SMF21BR         LOAD 4K BYTES READ COUNT
         LA    R1,=C'BR'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW 4K BYTES READ COUNT
         SPACE
         ICM   R0,7,SMF21BW         LOAD 4K BYTES WRITTEN COUNT
         LA    R1,=C'BW'            POINT TO LABEL
         BAS   R14,COUNTER2         SHOW 4K BYTES WRITTEN COUNT
         SPACE
         WTO   MF=(E,WTOU83D)       DISPLAY U83021I
         SPACE
         FREEMAIN RU,LV=WK21LEN,A=(R9),SP=231
         B     WRITEREC             ALL DONE, GO AND WRITE TYPE 21 REC
         SPACE
COUNTER2 DS    0H                   SHOW COUNTER WITH 2-CHARACTER LABEL
         LTR   R0,R0                NON-ZERO VALUE?
         BZR   R14                  NO, SUPPRESS DISPLAY OF ZERO
         MVI   0(R3),C','           SUPPLY SEPARATOR
         CVD   R0,WORK              GET DECIMAL VALUE
         OI    WORK+7,15            PREPARE FOR UNPACK
         MVC   1(2,R3),0(R1)        LOAD VALUE LABEL (2 CHARACTERS)
         B     CNTRCMN              BRANCH TO COMMON CODE
         SPACE
COUNTER3 DS    0H                   SHOW COUNTER WITH 3-CHARACTER LABEL
         LTR   R0,R0                NON-ZERO VALUE?
         BZR   R14                  NO, SUPPRESS DISPLAY OF ZERO
         MVI   0(R3),C','           SUPPLY SEPARATOR
         LA    R3,1(,R3)            POINT PAST SEPARATOR
CNTR3FST CVD   R0,WORK              GET DECIMAL VALUE
         OI    WORK+7,15            PREPARE FOR UNPACK
         MVC   0(3,R3),0(R1)        LOAD VALUE LABEL (3 CHARACTERS)
CNTRCMN  MVI   3(R3),C'='
         UNPK  4(9,R3),WORK+3(5)    LOAD NUMERIC COUNT
         LA    R0,13                GET LENGTH OF TEXT ADDED
CNTR0LP  CLI   4(R3),C'0'           LEADING ZERO?
         BNE   CNTROKAY             NO
         MVC   4(9,R3),5(R3)        YES, SUPPRESS IT
         BCT   R0,CNTR0LP           CONTINUE LEADING ZERO CHECK
CNTROKAY ALR   R3,R0                POINT PAST ADDED MESSAGE TEXT
         BR    R14                  RETURN TO CALLER
         SPACE
         DROP  R9                   WK83AREA
         TITLE 'SMF RECORD 64 - VSAM DATA SET CLOSE'
***********************************************************************
*                                                                     *
* RECORD TYPE 64: FORMAT AND WRITE A U83064I MESSAGE.                 *
*                                                                     *
* U83064I ???PUT VSAM FILE DDDDDDDD CLOSED 123456789 EXCPS FOR        *
*         12345678901234567890123456789012345678901234 BLKSZ=12345    *
*         12345KB                                                     *
***********************************************************************
         SPACE
REC64$   L     R4,PSAAOLD           POINT TO CURRENT ASCB
         USING ASCB,R4
         ICM   R1,B'1111',ASCBJBNI  BATCH JOB?
         BZ    WRITEREC             NO, DO NOT DISPLAY ANY MESSAGE
         DROP  R4                   ASCB
         TM    SMF64RIN,X'80'       RECORD WRITTEN BY CLOSE?
         BZ    WRITEREC             NO, BY EOV, SO DO NOT MAKE A MSG
         GETMAIN RC,LV=WK64LEN,SP=231,LOC=(ANY)
         LTR   R15,R15              TEST FOR SUCCESSFUL GETMAIN
         BNZ   WRITEREC             FAILURE - JUST WRITE SMF RECORD
         LR    R9,R1                ADDRESS GETMAINED AREA
         USING WK83AREA,R9
         MVC   WTOU83D(WTO64L),WTO64S INITIALIZE WTO PARAMETER LIST
         SLR   R3,R3                CLEAR FOR INSERT
         ICM   R3,3,SMF64ESL        GET EXTENT SECTION LENGTH
         LA    R3,SMF64FEX(R3)      POINT PAST EXTENT SECTION
         USING SMF64STS,R3          STATISTICS SECTION ADDRESSABILITY
         LA    R0,176
         CLM   R0,15,SMF64SLN       STATISTICS SECTION LONG ENOUGH?
         BH    REC64XIT             NO, DO NOT WRITE ANY MESSAGE
         MVC   WTOAMDDN,SMF64DDN    SHOW THE DDNAME (LOGICAL FILE NAME)
         IC    R1,SMF64MC1          GET ACBMACR1 FLAGS
         SLL   R1,29                SHIFT OUT UNWANTED FLAGS
         SRL   R1,30                GET GET/PUT READ/WRITE FLAGS
         SLA   R1,3                 MULTIPLY BY EIGHT FOR INDEX
         BZ    OPENED64             BOTH FLAGS OFF SO LEAVE BLANK
         LA    R1,VOPNTYPS-8(R1)    POINT TO OPEN TYPE LITERAL
         MVC   WTOAMOPN,0(R1)       SHOW THE OPEN TYPE
OPENED64 MVC   WTOAMDSN,SMF64DNM    SHOW THE DSN (PHYSICAL FILE NAME)
         CLI   WTOAMDSN,C'$'        VALID NAME?
         BNL   DSN64OK              POSSIBLY, ASSUME THAT IT IS
         MVC   WTOAMDSN,SMF64CLN    NO, SHOW THE JCL DATA SET NAME
DSN64OK  ICM   R5,15,SMF64DEP       GET THE EXCP COUNT CHANGE
         BM    REC64XIT             DO NOT ISSUE MESSAGE IF NEGATIVE
         CVD   R5,WORK              GET DECIMAL TOTAL EXCP COUNT
         MVC   WTOAMXCP,ED9         LOAD EDIT MASK
         ED    WTOAMXCP,WORK+3      SHOW EXCP COUNT UP TO (10**9)-1
         ICM   R15,15,SMF64DBS      GET THE DATA SET BLOCK SIZE
         BNP   MESG64OK             ZERO SO SKIP CALCULATION
         CVD   R15,WORK             SHOW BLOCK SIZE
         OI    WORK+7,15            PREPARE FOR UNPACK
         MVC   WTOAMBLK,=C'BLKSZ='
         UNPK  WTOAMBLS,WORK+5(3)   LOAD BLOCK SIZE NUMERICS
BLSZ64LP CLI   WTOAMBLS,C'0'        LEADING ZERO?
         BNE   BLSZ64OK             NO, FORMATTING COMPLETE
         MVC   WTOAMBLS,WTOAMBLS+1  YES, SHUFFLE UP TO SUPPRESS IT
         B     BLSZ64LP             CONTINUE WITH LEADING ZERO CHECK
BLSZ64OK LTR   R5,R5                ANY EXCPS?
         BZ    MESG64OK             NO, SUPPRESS DISPLAY OF '0KB'
         MR    R4,R15               YES, GET BYTES (BLOCK COUNT*SIZE)
         MVC   WTOAMSIZ+4(2),=C'<1' REPLACE '0KB' WITH '<1KB'
         LA    R1,=C'KMGT'          POINT TO KILO/MEGA/GIGA/TERA FLAGS
SIZE64LP SRDA  R4,10                DIVIDE BY 1024
         BZ    SIZE64DN             IF ZERO THEN LESS THAN ONE
         LTR   R4,R4                NUMBER LARGER THAN ONE FULLWORD?
         BNZ   SIZE64BG             YES, TOO BIG
         CL    R5,=F'99999'         NO, IS NUMBER BIGGER THAN 99,999?
         BNH   SIZE64OK             NO, SHOW THE NUMBER
SIZE64BG LA    R1,1(,R1)            POINT TO NEXT SCALE INDICATOR
         B     SIZE64LP             CONTINUE TO SCALE DOWN
SIZE64OK CVD   R5,WORK              SHOW THE APPROXIMATE BYTE COUNT
         MVC   WTOAMSIZ,ED5         LOAD EDIT MASK
         ED    WTOAMSIZ,WORK+5
SIZE64DN MVC   WTOAMSCL(1),0(R1)    SHOW SCALING INDICATOR
         MVI   WTOAMSCL+1,C'B'      INDICATE BYTES
MESG64OK DS    0H                   MESSAGE NOW READY FOR DISPLAY
         SPACE
         WTO   MF=(E,WTOU83D)       DISPLAY U83064I
         SPACE
         DROP  R9,R3                WK83AREA, SMF64STS
REC64XIT DS    0H
         SPACE
         FREEMAIN RU,LV=WK64LEN,A=(R9),SP=231
         B     WRITEREC             ALL DONE, GO WRITE TYPE 64 RECORD
         TITLE 'STATIC AREA AND LITERAL POOL'
HEX      DC    C'0123456789ABCDEF'
ED5      DC    XL6'402020202120'
ED9      DC    XL10'40202020202020202120'
*                 OPEN TYPE      DEBOPATB SETTING
OPNTYPES DC    CL8' INPUT  '       0000      0
         DC    CL8'RDBACK  '       0001      1
         DC    CL8'?0010?  '       0010      2
         DC    CL8' INOUT  '       0011      3
         DC    CL8'UPDATE  '       0100      4
         DC    CL8'?0101?  '       0101      5
         DC    CL8'?0110?  '       0110      6
         DC    CL8' OUTIN  '       0111      7
*        DC    CL8'OUTPUT  '       1111      F
*                 OPEN TYPE      ACBMACR1 SETTING
VOPNTYPS DC    CL8'OUTPUT  '       .01.      2
         DC    CL8' INPUT  '       .10.      4
         DC    CL8'UPDATE  '       .11.      6
         SPACE
*        WTO TEMPLATES - 125 CHARS (MSGID + ' ' + UP TO 117 TEXT)
         SPACE
WTO1415S WTO   'U83M14I OUTPUT EXCP FILE *DDNAME* CLOSED 123456789 BLOC+
               KS OF DSNAME                                            +
                             ',ROUTCDE=(11),MF=L
WTO1415L EQU   *-WTO1415S
         SPACE
WTO21S   WTO   'U83021I JOB-NAME                                       +
                                                                       +
                             ',ROUTCDE=(3,5,11),MF=L
WTO21L   EQU   *-WTO21S
         SPACE
WTO64S   WTO   'U83064I        VSAM FILE *DDNAME* CLOSED 123456789 EXCP+
               S FOR 12345678.12345678.12345678.12345678.12345678      +
                             ',ROUTCDE=(11),MF=L
WTO64L   EQU   *-WTO64S
         SPACE 2
         LTORG
         SPACE 2
         DS    0D                   END OF CSECT
         TITLE 'DYNAMIC AREA DEFINITIONS'
WK83AREA DSECT
WORK     DS    D
WTOU83D  WTO   '1234567890123456789012345678901234567890123456789012345+
               67890123456789012345678901234567890123456789012345678901+
               23456789012345',ROUTCDE=(11),MF=L
         ORG   WTOU83D+4
WTOMSGID DS    CL8                  U83###I
WTOTEXT  DS    CL112
         ORG   WTOTEXT
WTOAMOPN DS    CL6                  OPEN TYPE
         DS    CL1                  ' '
WTOAMNAM DS    CL4                  ACCESS METHOD
         DS    CL6                  ' FILE '
WTOAMDDN DS    CL8                  DDNAME
WTOAMSLA DS    CL1                  ' ' OR '/'
WTOAMVOL DS    CL6                  'CLOSED' OR TAPE VOLSER
WTOAMXCP DS    CL10                 EXCP COUNT
WTOAMDES DS    CL11                 ' EXCPS FOR ' OR ' BLOCKS OF '
WTOAMDSN DS    CL44                 DSNAME
         DS    CL1                  ' '
WTOAMBLK DS    CL6                  'BLKSZ='
WTOAMBLS DS    CL5                  DATA SET BLOCK SIZE
WTOAMSIZ DS    CL6                  UPPER LIMIT TO BYTE COUNT
WTOAMSCL DS    CL2                  'KB'/'MB'/'GB'/'TB'
         ORG
         SPACE
         DS    0D
WK14LEN  EQU   *-WK83AREA
WK21LEN  EQU   *-WK83AREA
WK64LEN  EQU   *-WK83AREA
         EJECT
         TITLE 'SMF RECORD STRUCTURES'
SMFRECD  DSECT
         IFASMFR (14)
         SPACE
         ORG   SMFJFCB1
         IEFJFCBN
         EJECT
         ORG   SMFRECD
         IFASMFR (21)
         EJECT
         ORG   SMFRECD
         IFASMFR (64)
         EJECT
         TITLE 'SYSTEM CONTROL BLOCKS'
         IHAPSA
         SPACE 3
         IHAASCB
         SPACE 3
         END   IEFU83
