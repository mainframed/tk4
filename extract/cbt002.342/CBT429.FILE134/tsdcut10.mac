PROC 0
GLOBAL MSG PREF CON KEY RMT
CONTROL MAIN NOFLUSH NOMSG NOPROMPT
ISPEXEC CONTROL ERRORS  RETURN
ISPEXEC VGET (O1   O2   O3   O4   O5   O6   O7   O8   O9   O10   O11 +
            DSN1 DSN2 DSN3 DSN4 DSN5 DSN6 DSN7 DSN8 DSN9 DSN10 DSN11 +
            VOL1 VOL2 VOL3 VOL4 VOL5 VOL6 VOL7 VOL8 VOL9 VOL10 VOL11 +
            MEM1 MEM2 MEM3 MEM4 MEM5 MEM6 MEM7 MEM8 MEM9 MEM10 MEM11 +
            STRG SELT EXCL CON  RMT  )    PROFILE
SET &MSG=&STR()
SET &PARM=COMP
SET &LB='('
SET &LB=&STR(&SUBSTR(2:2,&LB))
SET &RB=')'
SET &RB=&STR(&SUBSTR(2:2,&RB))
SET &LASTCD=0
IF  &CON EQ THEN SET &CON=Y
DESPLAY:+
SET &MSG=
DISPLAY:+
SET &ZCMD=
IF  &LASTCD LE 4 THEN SET CL=Y
ELSE                  SET CL=N
DOSPLAY:+
ISPEXEC CONTROL DISPLAY REFRESH
ISPEXEC DISPLAY PANEL(TSDPUT10) &MSG
IF      &LASTCC GE 8 THEN GOTO FINISH
SET     &MSG=&STR()
SET     &INDEX=1
SET     &DSN=&STR()
SET     &DSNB=&STR()
SET     &DSNC=&STR()
SET     &DSND=&STR()
SET     &VOL=&STR()
SET     &VOLB=&STR()
SET     &SPC=&STR()
INCR:   +
SET     &OPX=&&O&INDEX
SET     &OPT=&OPX
IF      &OPT EQ THEN GOTO SKIP
        IF  &OPT EQ &SPC THEN GOTO SPEC
        IF  &SPC NE      THEN GOTO SKIP
        SET &DSN=&&DSN&INDEX
        SET &VOL=&&VOL&INDEX
        SET &MEM=&&MEM&INDEX
        IF  &MEM NE THEN DO
        IF  &SUBSTR(1:1,&DSN) EQ &STR(') THEN SET &DSNC=+
            &STR(&SUBSTR(1:&LENGTH(&DSN)-1,&DSN)&LB&MEM&RB.')
        ELSE                                 SET &DSNC=+
            &STR(&SUBSTR(1:&LENGTH(&DSN),&DSN)&LB&MEM&RB)
        END
        IF  &VOL NE THEN SET &VOL=&STR(VOLUME(&VOL))
        IF  &CON EQ N OR +
            (&OPT NE D AND &OPT NE Y AND &OPT NE U) THEN GOTO LAB&OPT
        SET &CL=N
        ISPEXEC DISPLAY PANEL(TSDPUT10) MSG(TSDMU022)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        GOTO LAB&OPT
   SPEC:SET &DSNB=&&DSN&INDEX
        SET &VOLB=&&VOL&INDEX
        SET &MEM=&&MEM&INDEX
        IF  &MEM NE THEN DO
        IF  &SUBSTR(1:1,&DSNB) EQ &STR(') THEN SET &DSND=+
            &STR(&SUBSTR(1:&LENGTH(&DSNB)-1,&DSNB)&LB&MEM&RB.')
        ELSE                                 SET &DSND=+
            &STR(&SUBSTR(1:&LENGTH(&DSNB),&DSNB)&LB&MEM&RB)
        END
        IF  &VOLB NE THEN SET &VOLB=&STR(VOLUME(&VOLB))
        GOTO &SPC
SKIP:   +
SET     &INDEX=&INDEX+1
IF      &INDEX LT 12 THEN GOTO INCR
IF      &SPC NE THEN GOTO E&SPC
IF      &STR(&NRSTR(&PREF)) NE THEN DO
          SET &MSG=
          %TSDCUT30
          GOTO DISPLAY
          END
SET     &MSG=&STR(MSG(TSDMU008))
GOTO    DISPLAY
LABC:   SET &SPC=C
        GOTO SKIP
     EC:SET &MSG=&STR(MSG(TSDMU006))
        GOTO DISPLAY
      C:IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF &STR(&DSND) NE THEN SET &DSNB=&STR(&DSND)
        ALLOC F(OLD) DA(&DSN)  &VOL  UNIT(SYSALLDA) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        ALLOC F(NEW) DA(&DSNB) &VOLB UNIT(SYSALLDA) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CLEAR
        INVOKE COMPAREB
        GOTO DISPLAY
LABS:   ISPEXEC DISPLAY PANEL(TSDPUT20) &MSG
        IF &LASTCC GE 8 THEN GOTO DESPLAY
        IF &STR(&STRG) EQ THEN DO
                               SET &MSG=&STR(MSG(TSDMU018))
                               GOTO LABS
                               END
        IF &SELT NE THEN SET &SXXX=&STR(,%SEL,&SELT)
        ELSE             SET &SXXX=&STR()
        IF &EXCL NE THEN SET &EXXX=&STR(,%EXC,&EXCL)
        ELSE             SET &EXXX=&STR()
        ALLOC F(PDSDD) DA(&DSN) &VOL UNIT(SYSALLDA) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CLEAR
        $ SCANX '&STRG&SXXX&EXXX'
        FREE F(PDSDD)
        SET &SXXX=&STR()
        SET &EXXX=&STR()
        GOTO DISPLAY
LABL:   IF &SUBSTR(1:1,&DSN) EQ &STR(') THEN +
             ISPEXEC SELECT PGM($LISTX) +
                                PARM(&SUBSTR(2:&LENGTH(&DSN)-1,&DSN))
        ELSE ISPEXEC SELECT PGM($LISTX) +
                                PARM(&SYSUID..&DSN)
        SET &LASTCD=0
        GOTO DISPLAY
LABM:   ALLOC F(IN)  DA(&DSN) &VOL UNIT(SYSALLDA) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        ALLOC F(OUT) DA(*)    SHR REUS
        $ STATS
        GOTO DISPLAY
LABZ:   TERM SCRSIZE(24,80)
        ZAP &DSN NOLOG &VOL
        GOTO DISPLAY
LABI:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        REVIEW &DSN &VOL
        GOTO DISPLAY
LABF:   CONTROL MSG
        RLSE  &DSN &VOL EXT
        CONTROL NOMSG
        GOTO DISPLAY
LABP:   ISPEXEC SELECT CMD(PDS &DSN &VOL)
    /*  CONTROL MSG        */
    /*  PDSNSPF &DSN &VOL  */
    /*  TERMIN END EN      */
    /*  DATA               */
    /*  END                */
    /*  ENDDATA            */
    /*  CONTROL NOMSG      */
        GOTO DISPLAY
LABK:   SET &SPC=O
        GOTO SKIP
LABO:   SET &SPC=K
        GOTO SKIP
LABR:   SET &SPC=T
        GOTO SKIP
LABT:   SET &SPC=R
        GOTO SKIP
     ET:GOTO ER
     ER:SET &MSG=&STR(MSG(TSDMU007))
        GOTO DISPLAY
     EO:GOTO EK
     EK:SET &MSG=&STR(MSG(TSDMU019))
        GOTO DISPLAY
      R:GOTO T
      T:IF  &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF  &STR(&DSND) NE THEN SET &DSNB=&STR(&DSND)
        IF  &OPT EQ T THEN RENAME   &DSN  &DSNB
        ELSE               RENAME   &DSNB &DSN
        SET &LASTCD=&LASTCC
        IF  &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU002))
        ELSE                  SET &MSG=&STR(MSG(TSDMU004))
        GOTO DISPLAY
      K:GOTO O
      O:IF  &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF  &STR(&DSND) NE THEN SET &DSNB=&STR(&DSND)
        IF  &OPT EQ O THEN COPY     &DSN  &DSNB NONUM
        ELSE               COPY     &DSNB &DSN  NONUM
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU016))
        ELSE                 SET &MSG=&STR(MSG(TSDMU017))
        GOTO DISPLAY
LABA:   SET &DSN=&DSN
        ISPEXEC VGET (VLM BLK LRC REC DSO PRM SEC DIR TYP) PROFILE
        ISPEXEC DISPLAY PANEL(TSDPUT21)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        ISPEXEC VPUT (VLM BLK LRC REC DSO PRM SEC DIR TYP) PROFILE
        IF &VLM NE THEN SET &VOL=&STR(VOL(&VLM))
        FREE F(ATTRTSD)
        ATTR ATTRTSD BLKSIZE(&BLK) LRECL(&LRC) RECFM(&RCF) DSORG(&DSO)
        ALLOC DA(&DSN) NEW SPACE(&PRM,&SEC) &VOL +
              DIR(&DIR) &TYP USING(ATTRTSD) REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        SET &MSG=&STR(MSG(TSDMU012))
        FREE DA(&DSN)
        GOTO DISPLAY
LABY:   PDSNSPF &DSN &VOL FIXPDS RESET
        GOTO DISPLAY
     /* SET &PARM=RESET  (- FOR QUICK COMPRESS) */
LABQ:   ALLOC F(SYSPRINT) DUMMY    REUS
        ALLOC F(COMPRESS) DA(&DSN) REUS &VOL
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CALL 'TSO.LINKLIB(SRCOMPRS)' '&PARM'
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0  THEN SET &MSG=&STR(MSG(TSDMU021))
        ELSE                  SET &MSG=&STR(MSG(TSDMU020))
        SET &PARM=COMP
        ALLOC F(SYSPRINT) DA(*) REUS
        GOTO DISPLAY
LABG:   ALLOC DA(&DSN) &VOL CATALOG
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        FREE DA(&DSN)
        SET &MSG=&STR(MSG(TSDMU015))
        GOTO DISPLAY
LABE:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        ISPEXEC EDREC INIT
LABEREC:ISPEXEC EDREC QUERY
        IF &LASTCC NE 4 THEN GOTO LABEDIT
        ISPEXEC EDREC PROCESS
        GOTO LABEREC
LABEDIT:ISPEXEC  EDIT   DATASET(&DSN) &VOL
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0  THEN SET &MSG=&STR(MSG(TSDMU001))
        ELSE IF &LASTCD EQ 20 THEN SET &MSG=&STR(MSG(TSDMU010))
        GOTO DISPLAY
LABB:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        ISPEXEC  BROWSE DATASET(&DSN) &VOL
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 20 THEN SET &MSG=&STR(MSG(TSDMU011))
        GOTO DISPLAY
LABU:   DELETE &DSN NOSCRATCH
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU013))
        ELSE                 SET &MSG=&STR(MSG(TSDMU014))
        GOTO DISPLAY
LABH:   SHOWDS &DSN EXT DIR &VOL
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN SET &MSG=&STR(MSG(TSDMU025))
        GOTO DISPLAY
LABD:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        DELETE   &DSN
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU003))
        ELSE                 SET &MSG=&STR(MSG(TSDMU005))
        GOTO DISPLAY
LABW:   GOTO LABV
LABV:   ISPEXEC DISPLAY PANEL(TSDPUT20) &MSG
        IF &LASTCC GE 8 THEN GOTO DESPLAY
        IF &KEY NE THEN DO
                   IF &STRG EQ PASSWORD THEN SET &KKK=&STR(P(&KEY))
                                        ELSE SET &KKK=&STR(K(&KEY))
                   END
                   ELSE SET &KKK=&STR()
        IF &DSNC NE THEN SET &DSN=&STR(&DSNC)
        IF &OPT EQ V THEN $ENCRYPT &DSN &KKK
                     ELSE $DECRYPT &DSN &KKK
        GOTO DISPLAY
LABJ:   ISPEXEC DISPLAY PANEL(TSDPUT20) &MSG
        IF &COP EQ THEN SET &COP=1
        IF &LASTCC GE 8 THEN GOTO DESPLAY
        IF &RMT EQ THEN DO
                        SET &MSG=&STR(MSG(TSDMU034))
                        GOTO LABJ
                        END
        IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF &RMT NE THEN SET &REMOTE=&STR(DEST(&RMT))
                   ELSE SET &REMOTE=&STR()
        PRINTOFF &DSN &REMOTE &VOL COPIES(&COP)
        SET &LASTCD=&LASTCC
        GOTO DISPLAY
LABX:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF SUBSTR(1:1,&DSN) NE &STR(') THEN SET &DSN=&STR((&MEM))
        EXEC &DSN
        SET &LASTCD=&LASTCC
        GOTO DISPLAY
ALLOCER:SET &MSG=&STR(MSG(TSDMU009))
        GOTO DISPLAY
FINISH: +
FREE    F(OLD NEW IN OUT PDSDD COMPRESS)
ISPEXEC VPUT (O1   O2   O3   O4   O5   O6   O7   O8   O9   O10   O11 +
            DSN1 DSN2 DSN3 DSN4 DSN5 DSN6 DSN7 DSN8 DSN9 DSN10 DSN11 +
            VOL1 VOL2 VOL3 VOL4 VOL5 VOL6 VOL7 VOL8 VOL9 VOL10 VOL11 +
            MEM1 MEM2 MEM3 MEM4 MEM5 MEM6 MEM7 MEM8 MEM9 MEM10 MEM11 +
            STRG SELT EXCL CON  RMT  )    PROFILE
