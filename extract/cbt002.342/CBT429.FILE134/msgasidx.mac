MSGASIDX TITLE 'MPF EXIT TO ADD HEX ASID TO END OF MESSAGE'
MSGASIDX CSECT
MSGASIDX AMODE 31                 31 BIT ADDRESSING MODE
MSGASIDX RMODE ANY                31 BIT RESIDENCY MODE
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R12,R15            ESTABLISH MODULES BASE REGISTER
         USING MSGASIDX,R12       R12 IS NOW OUR BASE REGISTER
         L     R9,0(R1)           R9==> WTO EXIT PARAMETER LIST
         USING CTXT,R9            ESTABLISH ADDRESSABLITY TO IT
         L     R3,CTXTTXPJ        ESTABLISH ADDRESSABILITY TO
         USING CTXTATTR,R3        THE MESSAGE ATTRIBUTES.
         LH    R0,CTXTTLMX        GET MAXIMUM LENGTH OF MESSAGE
         SH    R0,CTXTTLEN        SUBTRACT ACTUAL LENGTH OF MESSAGE
         CH    R0,MSGXLEN         ROOM FOR EXTENSION?
         BL    EXEUNT             NO, EXIT WITHOUT ACTION
         LH    R2,CTXTTLEN        YES, GET CURRENT MESSAGE LENGTH
         LA    R0,12(,R2)         GET NEW LENGTH
         STH   R0,CTXTTLEN        UPDATE LENGTH FIELD
         LA    R2,CTXTTMSG(R2)    POINT PAST END OF OLD MESSAGE
         L     R4,548             POINT TO THE CURRENT ASCB
         UNPK  0(5,R2),36(3,R4)   UNPACK CURRENT ASID
         MVC   8(4,R2),0(R2)      MOVE ASID TO CORRECT SPOT
         NC    8(4,R2),HEXMASK    CLEAR ZONES FOR TRANSLATE
         TR    8(4,R2),HEX        ENSURE VALID HEX DIGITS
         MVC   0(8,R2),ASIDLBL    LOAD LABEL INTO MESSAGE
         OI    CTXTRFB1,CTXTRCMT  REQUEST CHANGE TO MESSAGE TEXT
         DROP  R12,R9,R3
EXEUNT   LM    R14,R12,12(R13)    RESTORE CALLERS REGISTERS
         BR    R14                CLEAN UP AND RETURN
         SPACE
MSGXLEN  DC    H'12'              LENGTH OF MESSAGE TEXT ENTENSION
ASIDLBL  DC    CL8' - ASID '
HEXMASK  DC    XL4'0F0F0F0F'
HEX      DC    CL16'0123456789ABCDEF'
         DS    0D                 END OF CSECT
         SPACE
         IEZVX100
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         END   MSGASIDX
