         TITLE '  R E V S U B S  '
**********************************************************************
*
*  R32.0  10JAN00 - CREATE REVSUBS SOURCE MEMBER TO REDUCE THE
*                   SIZE OF THE REVIEW SOURCE MEMBER.           GP@P6
*  R32.1  15FEB00 - FIX IMS ACBLIB OFFSET BITROT.
*                 - ADD IMS ACBLIB Y2K WINDOWING.               GP@P6
*  R32.2  27APR00 - DO NOT OVERLAY RMODE24 SIZE WITH 'PDSE'.    GP@P6
*
**********************************************************************
         TITLE '  R E V P O D I R  '
**********************************************************************
*                                                          *         *
*         PARTITIONED ORGANIZATION DIRECTORY HANDLER       * GP@SECV *
*                                                          *  03/86  *
**********************************************************************

REVPODIR CSECT
         ENTRY IDRDATE
         ENTRY CALCPOSI
         B     @PODIR-*(,R15)
         DC    AL1(9),CL9'REVPODIR'
@PODIR   STM   R14,R12,12(R13)
         LR    R10,R15
         LA    R15,1
         LA    R11,4095(R15,R10)
         USING REVPODIR,R10,R11
         MVI   READR,X'80'         RESET UTILITY PROGRAM INDICATOR
DIREFRSH LR    R4,R13              SAVE SAVE AREA POINTER
         LA    R2,DIRGMLIM         POINT TO BOUNDS OF GETMAIN LENGTH
         LA    R3,DOUBLE           POINT TO RESULTS AREA
         MVC   GMVUW(GMVCL),GMVC
         GETMAIN VC,LA=(R2),A=(R3),MF=(E,GMVUW)
         LTR   R15,R15             DID GETMAIN WORK?
         BZ    USEDYN              YES, PROCEED
         LA    R1,DIRRDMSG         NO, POINT TO ERROR MESSAGE
         LA    R0,L'DIRRDMSG-1     GET ERROR MESSAGE LENGTH
         TPUT  (1),(0),R           DISPLAY MESSAGE
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         LA    R15,20              SET RETURN CODE
         BR    R14                 RETURN
USEDYN   L     R12,DOUBLE+4        GET LENGTH OF GETMAINED AREA
         L     R13,DOUBLE          GET ADDRESS OF GETMAINED AREA
         USING @DYNAREA,R13
         ST    R13,8(,R4)
         ST    R4,4(,R13)          CHAIN SAVE AREAS
         STM   R12,R13,FREEMRGS    SAVE FREEMAIN VALUES
         MVC   DIRDCB(DIRDCBL),STDIRDCB
         ALR   R12,R13             GET ADDRESS ABOVE GETMAINED AREA
         SH    R12,=H'60'          TAKE A PUNT
         ST    R12,MAXADDR         SAVE HIGHEST USEABLE ADDRESS
         LA    R1,BUFFSIZE         INITIALIZE REVCMPBF PARAMETER LIST
         ST    R1,CBPRM1           ADDRESS OF BUFFER LENGTH VARIABLE
         LA    R1,SCRNLNES
         ST    R1,CBPRM2           ADDRESS OF LINES-ON-SCREEN VARIABLE
         L     R1,SCRNCOLS         INITIALIZE HOME SCREEN ADDRESS
         LA    R1,13(,R1)
         STCM  R1,3,PRIMLOCN       SAVE PRIMARY INPUT BUFFER LOCATION
         L     R15,=A(CALCPOSI)
         BALR  R14,R15
         STCM  R1,3,PRIMADDR       SAVE PRIMARY INPUT BUFFER ADDRESS
         MVI   WCCSAVE,WCCSTND     INITIALIZE WCC (BELL OFF)
         MVC   DIRMEMCT(26),POMEMVOL    LOAD EDIT MASK AND VIO FLAG
         CLI   $VOLSER,C' '        IS IT A VIO FILE?
         BE    VIOPDSOK            YES
         MVC   POVOLUME,$VOLSER    NO, DISPLAY VOLUME OF FIRST PDS
         TM    STATUS8,ISPF34      WAS VOLUME FROM ZDLVOL?
         BNO   VIOPDSOK            NO, CONTINUE
         MVI   POVOLUME-1,C'-'     YES, LEAVE A SUBTLE HINT
VIOPDSOK MVI   DIRHDG,C'-'         INITIALIZE HEADING STRING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   OFFDDNAM,SYSUT2     SET DEFAULT OUTPUT DD NAME
         LH    R1,$DSNAME          GET DATA SET NAME LENGTH
         EX    R1,GTPDSNAM         LOAD DSN INTO HEADING
         L     R7,=A(READDIR)      POINT TO DIRECTORY PROCESSOR
         BR    R7                  PROCESS DIRECTORY INFORMATION
FORCETOP MVI   PUTCURSR,SBA        INITIALIZE CURSOR POSITIONER
         MVC   PUTCURSR+1(2),PRIMADDR
         MVI   PUTCURSR+3,IC
         TM    STATUS,STPODIR      HAVE WE BEEN HERE BEFORE?
         BO    TESTSORT            YES, ALREADY KNOW ABOUT MEMBER LIST
         OI    STATUS,STPODIR      NO, FLAG MEMBER LIST MODE
TESTSORT TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    FINDMEM             YES, DON'T PERFORM SORT
         TM    STATUS3,DATESORT
         BO    SORTDATE
         TM    STATUS3,SIZESORT
         BO    SORTSIZE
         TM    STATUS3,SSISORT
         BO    SORTSSI
         TM    STATUS3,TTRSORT
         BO    SORTTTR
         TM    STATUS3,USERSORT
         BO    SORTUSER
         ICM   R0,B'0011',ARLRTRVD ANY CONCATENATED JFCBS FETCHED?
         BNZ   SORTNAME            YES, SORT INTO COLLATING SEQUENCE
FINDMEM  LA    R3,DIRENTS          POINT TO FIRST ENTRY
         TM    STATUS7,EDITED      SPF EDITED AND SAVED A MEMBER?
         BZ    TRYUPDTD            NO, SEE IF PDS UPDATED
         MVI   DIRHDG,C'-'         YES, DISPLAY APPROPRIATE MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'SAVEDMSG),SAVEDMSG
         NI    STATUS7,255-EDITED  RESET EDITED AND SAVED FLAG
         B     TRYNOMEM
TRYUPDTD TM    READR,X'80'         PDS UPDATED?
         BO    TRYNOMEM            NO
         MVI   DIRHDG,C'-'         YES, PREPARE FOR APPROPRIATE MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
DELINKOK L     R0,READR            GET COMPLETION CODE
         SRA   R0,12               DID THE UTILITY PROGRAM ABEND?
         BNZ   UPDTABND            YES
         L     R0,READR            NO
         CVD   R0,DOUBLE           GET CONDITION CODE
         OI    DOUBLE+7,X'0F'
         MVC   DIRHDG(L'UTLCCMSG),UTLCCMSG
         UNPK  UTLCCNUM,DOUBLE
         B     WASUPDTD
UPDTABND MVC   DIRHDG(L'UTLABMSG),UTLABMSG
         UNPK  UTLABNUM,READR+1(2)
         OI    UTLABNUM+2,X'F0'
         TR    UTLABNUM,PODIRHEX-C'0'
WASUPDTD MVI   READR,X'80'         RESET UTILITY PROGRAM INDICATOR
         LTR   R3,R3               PROCESSING DELINK COMPLETION?
         BZ    SAMEMEMS            YES, DO NOT SCROLL
TRYNOMEM OC    SELMEMNM,SELMEMNM   NON-ZERO MEMBER NAME TO LOCATE?
         BZ    THISMEM             NO, GO TO TOP
TRYMEMNM CLC   SELMEMNM,0(R3)      UP TO REQUIRED MEMBER?
         BE    THISMEM             YES, FOUND IT
         BH    NEXTMEM             NO, TRY NEXT ENTRY
         TM    STATUS3,SORTMODE    ENTRIES IN COLLATING SEQUENCE?
         BZ    PREVMEM             YES, PASSED IT, SO BACKUP ONE
         C     R3,LASTADDR         NO, REACHED END OF ENTRIES?
         BNL   NOTEXIST            YES, EXACT MEMBER NAME NOT THERE
NEXTMEM  AH    R3,14(,R3)          NOT YET, POINT TO NEXT ENTRY
         B     TRYMEMNM
NOTEXIST CLI   STATUS3,USERSORT    SORTED IN UPDATE USERID SEQUENCE?
         BNE   PUTNEMSG            NO, SHOW "NOT EXIST" MESSAGE
         LA    R3,DIRENTS          YES, POINT TO FIRST ENTRY
TRYUSRID CLI   15(R3),46           SPFD STATISTICS ENTRY?
         BNE   PREVMEM             NO, SHOW PREVIOUS MEMBER AT TOP
         CLC   SELMEMNM(8),36(R3)  UP TO REQUIRED MEMBER?
         BE    THISMEM             YES, FOUND IT
         BL    PREVMEM             NO, PAST IT SO SHOW PREVIOUS MEMBER
         AH    R3,14(,R3)          NOT YET, POINT TO NEXT ENTRY
         B     TRYUSRID
PUTNEMSG MVI   DIRHDG,C'-'         NO DIRECTORY ENTRIES IN PDS TO SORT
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'NOTEXMSG),NOTEXMSG
         L     R1,CBPRM3           POINT TO BUFFER START
         MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
SAMEMEMS L     R3,TOPMEMAD         RESTORE ENTRY POINTER
         B     THISMEM             RESHOW WITHOUT MOVEMENT
PREVMEM  LA    R2,DIRENTS          GET FIRST ENTRY ADDRESS
         CR    R2,R3               AT FIRST ENTRY?
         BE    THISMEM             YES, CAN'T GO BACK ONE
         LR    R2,R3               NO, POINT TO THIS ENTRY
         BCTR  R2,0                POINT TO LAST HALFWORD
         BCTR  R2,0                         OF PREVIOUS ENTRY
         SH    R3,0(,R2)           POINT TO PREVIOUS ENTRY
THISMEM  XC    SELMEMNM,SELMEMNM   CLEAR MEMBER NAME TO BE LOCATED
         LA    R1,SCREENW-4080
         LA    R1,4080(,R1)
         L     R15,=V(SCREEN1)     POINT TO TEMPLATE OF FIRST LINE
         MVC   0(SCREEN1L,R1),0(R15)
         LR    R15,R1              COPY THE BUFFER ADDRESS
         TM    STATUS5,X3270       IN 7-COLOUR MODE?
         BO    GOTPRM3             YES, HAVE CORRECT ADDRESS
         LA    R15,3(,R15)         NO, SKIP OVER SA,COLOUR
GOTPRM3  ST    R15,CBPRM3          SAVE BUFFER ADDRESS FOR REVCMPBF
         LA    R2,SCREEN1L-160(,R1)     POINT PAST INITIAL ORDERS
         A     R2,SCRNCOLS         POINT TO THE SECOND SCREEN LINE
         LA    R0,26
         SLR   R2,R0               BACK UP TWENTY-SIX BYTES
         MVC   0(26,R2),DIRMEMCT   SHOW MEMBER COUNT AND VOLUME
         LA    R2,26(,R2)          POINT TO THE SECOND SCREEN LINE
         MVC   0(20,R2),POLOCPTR   SUPPLY PRIMARY INPUT AREA ATTRIBUTES
         LA    R2,1(,R2)           COUNT FIELD ATTRIBUTE BYTE
         TM    STATUS5,X3270       USING EXTENDED ATTRIBUTES?
         BZ    *+8                 NO, SO DO NOT COUNT THEM
         LA    R2,6(,R2)           YES, COUNT UNDERSCORE AND RED
         XC    13(8,R2),13(R2)     USE NULLS IN PRIMARY INPUT AREA
         MVC   21(8,R2),POKEYATR   SUPPLY KEY LEGEND LINE ATTRIBUTES
         LA    R2,1(,R2)           COUNT FIELD ATTRIBUTE BYTE
         TM    STATUS5,X3270       USING EXTENDED ATTRIBUTES?
         BZ    *+8                 NO, SO DO NOT COUNT THEM
         LA    R2,6(,R2)           YES, COUNT NORMAL AND TURQUOISE
         MVI   22(R2),C' '         BLANK THE REST OF THE SECOND LINE
         MVC   23(137,R2),22(R2)
         MVC   22(L'POKEYKEY,R2),POKEYKEY    SHOW THE PFK LEGEND
         ICM   R0,15,OFFLDCNT      ANY TAGGED MEMBERS?
         BZ    PDSKEYOK            NO, KEY IS CORRECT
         MVC   22(L'POTAGKEY,R2),POTAGKEY    SHOW THE PFK LEGEND
PDSKEYOK A     R2,SCRNCOLS         POINT TO THE THIRD SCREEN LINE
         MVC   0(5,R2),COLHDATR    SUPPLY COLUMN HEADING ATTRIBUTES
         LA    R2,1(,R2)           COUNT FIELD ATTRIBUTE BYTE
         TM    STATUS5,X3270       USING EXTENDED ATTRIBUTES?
         BZ    *+8                 NO, SO DO NOT COUNT THEM
         LA    R2,3(,R2)           YES, COUNT YELLOW
         MVI   1(R2),C' '          BLANK THE THIRD LINE
         MVC   2(158,R2),1(R2)
         MVC   4(4,R2),NAMENAME    LABEL THE MEMBER NAME COLUMN
         MVC   16(SPFHDLEN,R2),SPFHDR    LABEL THE SPF STATS COLUMNS
         TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   PDSHDROK            NO, SO CANNOT BE A LOAD LIBRARY
         MVC   14(RFUHDLEN,R2),RECFMUHD   LABEL THE PROGRAM COLUMNS
PDSHDROK A     R2,SCRNCOLS         POINT TO THE FOURTH SCREEN LINE
         L     R1,CBPRM3           POINT TO START OF DATA STREAM
         SR    R2,R1               COMPUTE SIZE OF SCREEN
         ST    R2,BUFFSIZE
         LA    R1,CBPRM1           POINT TO PARAMETER LIST
         L     R15,=V(REVCMPBF)
         BALR  R14,R15             CALL BUFFER COMPRESS ROUTINE
         L     R2,BUFFSIZE         GET SIZE OF COMPRESSED HEADING LINES
         A     R2,CBPRM3           POINT PAST COMPRESSED HEADING LINES
         L     R1,SCRNLNES         GET NUMBER OF LINES ON SCREEN
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         ST    R3,TOPMEMAD         SAVE TOP MEMBER ENTRY ADDRESS
         XC    SELMEMNM,SELMEMNM   CLEAR MEMBER NAME IN READINESS
         SLR   R0,R0               CLEAR FOR INSERT CHARACTERS
         L     R7,=A(DIRFRMAT)     POINT TO LINE FORMATTING ROUTINE
LODMEMNM CLC   LASTNAME(14),0(R3)  END OF MEMBER LIST?
         BNER  R7                  NO, FORMAT A MEMBER DIRECTORY ENTRY
         STC   R1,LINKAREA+3       SAVE REMAINING LINE COUNT SOMEWHERE
         MVC   0(5,R2),COLHDATR    SUPPLY COLUMN HEADING ATTRIBUTES
         LA    R2,2(,R2)           POINT PAST START FIELD
         TM    STATUS5,X3270       USING EXTENDED ATTRIBUTES?
         BZ    *+8                 NO, SO OVERLAY SET ATTRIBUTE
         LA    R2,3(,R2)           YES, POINT PAST YELLOW
         MVC   0(9,R2),EODIRFLG    INDICATE END OF DIRECTORY ENTRIES
         LA    R2,9(,R2)           POINT PAST **END**
         MVI   0(R2),C' '
         MVC   1(69,R2),0(R2)      BLANK THE REST OF THE LINE
         TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   SPFDEND             NO, SO CANNOT BE A LOAD LIBRARY
         ICM   R1,B'1111',TOTPGMSZ GET TOTAL OF REAL LOAD MODULE SIZES
         BZ    ZEROSIZE            ZERO SO LEAVE IT BLANK
         LA    R0,1023
         ALR   R1,R0               ROUND UP TO NEXT KILOBYTE
         SRL   R1,10               DIVIDE BY 1024
         CVD   R1,DOUBLE
         MVC   1(10,R2),ED9
         ED    1(10,R2),DOUBLE+3
         MVI   11(R2),C'K'         DENOTE KILOBYTES
         UNPK  13(7,R2),TOTPGMSZ+1 SHOW SIZE IN HEX
         TR    13(6,R2),PODIRHEX-C'0'
         MVI   19(R2),C' '         ERASE RUBBISH
         ICM   R0,B'1111',EXTPGMSZ GET PGM SIZE TOTAL WITH RMODE=ANY
         BZ    ZEROSIZE            ZERO SO LEAVE IT BLANK
         L     R1,TOTPGMSZ         GET TOTAL OF REAL LOAD MODULE SIZES
         SR    R1,R0               GET PGM SIZE TOTAL WITH RMODE=24
         LA    R0,1023
         ALR   R1,R0               ROUND UP TO NEXT KILOBYTE
         SRL   R1,10               DIVIDE BY 1024
         CVD   R1,DOUBLE
         MVC   26(6,R2),ED5
         ED    26(6,R2),DOUBLE+5
         MVC   32(6,R2),=CL6'K-R=24'
ZEROSIZE ICM   R0,B'0011',ARLRTRVD ANY CONCATENATED JFCBS FETCHED?
         BNZ   MOVABLOK            YES, SUPPRESS DSCB DETAILS
         UNPK  20(7,R2),MYDSCB-44+98(4) DS1LSTAR
         TR    20(6,R2),PODIRHEX-C'0'
         MVI   26(R2),C' '         ERASE RUBBISH
         TM    MYDSCB-44+93,X'02'  IS THE DATA SET MODIFIED BIT ON?
         BZ    OKMODBIT            NO, DISPLAY IS CORRECT
         MVC   39(8,R2),MODIFIED   YES, INDICATE THIS
OKMODBIT TM    MYDSCB-44+78,X'08'  IS MEMBER LIST FOR A PDSE?
         BZ    PDSEUOK             NO
         MVC   49(4,R2),=C'PDSE'   YES, FLAG THIS
         B     MOVABLOK            ASSUME PDSE CAN'T BE UNMOVEABLE
PDSEUOK  TM    MYDSCB-44+82,X'01'  IS THE DATA SET UNMOVEABLE?
         BZ    MOVABLOK            NO, DISPLAY IS CORRECT
         MVC   49(10,R2),UNMOVABL  YES, INDICATE THIS
MOVABLOK LA    R2,59(,R2)          POINT PAST PROGRAM END STATISTICS
         B     DONENDST
SPFDEND  ICM   R1,B'1111',TOTSPFSZ GET TOTAL OF REAL SPF EDIT LINES
         BZ    ZEROEDIT            ZERO SO LEAVE IT BLANK
         CVD   R1,DOUBLE
         MVC   39(10,R2),ED9
         ED    39(10,R2),DOUBLE+3
ZEROEDIT ICM   R0,3,ARLRTRVD       ANY CONCATENATED JFCBS FETCHED?
         BNZ   MODBITOK            YES, SUPPRESS DSCB DETAILS
         UNPK  5(7,R2),MYDSCB-44+98(4)  DS1LSTAR
         TR    5(6,R2),PODIRHEX-C'0'
         MVI   11(R2),C' '         ERASE RUBBISH
         TM    MYDSCB-44+78,X'08'  IS MEMBER LIST FOR A PDSE?
         BZ    PDSEOKAY            NO
         MVC   12(4,R2),=C'PDSE'   YES, FLAG THIS
PDSEOKAY SLR   R4,R4               DISPLAY CREATION DATE FROM DS1CREDT
         IC    R4,MYDSCB-44+53     GET THE CREATION YEAR IN BINARY
         LA    R4,1900(,R4)        ADD ORIGIN FOR 1900-2155 SUPPORT
         CVD   R4,DOUBLE           LAST 2 YEAR DIGITS OVERWRITTEN LATER
         UNPK  18(4,R2),DOUBLE+5(3)    DISPLAY 4-DIGIT CREATION YEAR
         L     R0,DOUBLE+4         GET THE DECIMAL YEAR
         SRL   R0,4                SHIFT OUT SIGN
         SLL   R0,16               PUT INTO TOP HALFWORD
         ICM   R4,3,MYDSCB-44+54   GET BINARY CREATION JULIAN DAY
         CVD   R4,DOUBLE
         L     R4,DOUBLE+4         GET DECIMAL CREATION JULIAN DAY
         OR    R4,R0               GET YYYYDDD+
         BAL   R14,SPFDATE         CONVERT TO YY/MM/DD
         MVC   20(8,R2),DOUBLE     DISPLAY CREATION YYYY/MM/DD
         SLR   R4,R4
         ICM   R4,3,MYDSCB-44+49   ASM2 OR DMS UPDATE DATE?
         BNP   NOTASM2             NO
         CH    R4,=H'366'          ASM2 OR DMS UPDATE DATE?
         BH    NOTASM2             NO
         CVD   R4,DOUBLE
         L     R4,DOUBLE+4         GET DECIMAL UPDATE JULIAN DAY
         SLR   R0,R0               DISPLAY ASM2-MAINTAINED UPDATE DATE
         IC    R0,MYDSCB-44+48     GET THE UPDATE YEAR IN BINARY
         CVD   R0,DOUBLE
         L     R0,DOUBLE+4         GET THE DECIMAL YEAR
         SRL   R0,4                SHIFT OUT SIGN
         SLL   R0,16               PUT INTO TOP HALFWORD
         OR    R4,R0               GET 00YYDDD+
         BAL   R14,SPFDATE         CONVERT TO YY/MM/DD
         MVC   29(8,R2),DOUBLE     DISPLAY UPDATE YY/MM/DD
         B     SHOWOSCD            SHOW SYSCODE OR ASM2/DMS UPDATE JOB
NOTASM2  MVI   28(R2),C'-'         INDICATE NEXT DATUM IS RELATED
         MVC   29(6,R2),MYDSCB-44+45   SHOW CREATION VOLUME SERIAL
SHOWOSCD MVC   50(13,R2),MYDSCB-44+62  SHOW CREATION SYSTEM/COMPONENT
         L     R14,=V(FULLT)       POINT TO TRANSLATE TABLE
         TR    29(34,R2),0(R14)    HANDLE RUBBISH IN DS1DSSN/DS1SYSCD
         TM    MYDSCB-44+93,X'02'  IS THE DATA SET MODIFIED BIT ON?
         BZ    MODBITOK            NO, DISPLAY IS CORRECT
         MVC   1(3,R2),MODIFIED    YES, INDICATE THIS
MODBITOK LA    R2,69(,R2)          POINT PAST MEMBER END STATISTICS
DONENDST BCTR  R2,0                POINT TO PREVIOUS BYTE
         CLI   0(R2),C' '          UNNECESSARY TRAILING BLANK?
         BE    DONENDST            YES, CHOP IT OFF THE DATA STREAM
SKIPOSCD CLI   LINKAREA+3,3        AT LEAST TWO UNUSED LINES ON SCREEN?
         BL    POHELPOK            NO, SKIP HELP MESSAGE
         MVI   1(R2),RA            SUPPLY REPEAT-TO-ADDRESS ORDER
         L     R1,SCRNLNES
         BCTR  R1,0
         M     R0,SCRNCOLS         GET LOCATION OF LAST LINE
         LA    R1,20(,R1)          GET LOCATION OF MESSAGE
         L     R15,=A(CALCPOSI)
         BALR  R14,R15
         STCM  R1,3,2(R2)          SUPPLY REPEAT-TO-ADDRESS ADDRESS
         MVI   4(R2),C' '          SUPPLY REPEAT-TO-ADDRESS CHARACTER
         MVC   5(5,R2),HLPMSGAT    YES, LOAD ATTRIBUTES
         LA    R2,6(,R2)           POINT PAST TO BYTE OF SF
         TM    STATUS5,X3270       USING EXTENDED ATTRIBUTES?
         BZ    *+8                 NO, SO OVERLAY SET ATTRIBUTE
         LA    R2,3(,R2)           YES, POINT TO LAST BYTE OF BLUE
         MVC   1(POHL,R2),POHLPMSG
         LA    R2,POHL(,R2)        POINT TO LAST BYTE OF MESSAGE
POHELPOK MVC   1(4,R2),BLNKREST    BLANK THE REST OF THE SCREEN
         LA    R2,5(,R2)           POINT PAST END OF DATA STREAM
CMPRSDIR L     R1,CBPRM3           POINT TO START OF DATA STREAM
         SR    R2,R1               COMPUTE SIZE OF SCREEN
         ST    R2,BUFFSIZE
         LA    R1,CBPRM1           POINT TO PARAMETER LIST
         L     R15,=V(REVCMPBF)
         BALR  R14,R15             CALL BUFFER COMPRESS ROUTINE
DSPLYDIR L     R2,BUFFSIZE         GET COMPRESSED BUFFER LENGTH
         L     R4,CBPRM3           GET BUFFER ADDRESS
         MVC   0(1,R4),WCCSAVE     LOAD WRITE CONTROL CHARACTER (WCC)
         LA    R1,SCREENW-4080     LOAD DATA SET NAME OR MESSAGE
         MVC   4080+SCRDSN-SCREEN1(L'DIRHDG,R1),DIRHDG
         AR    R2,R4               POINT PAST END OF COMPRESSED DATA
         MVC   0(4,R2),PUTCURSR    TACK ON CURSOR POSITION
         LA    R1,X'03'            LOAD FULLSCREEN TPUT FLAGS
         SLL   R1,24               SHIFT TO TOP BYTE
         LA    R0,4(,R2)           GET END OF DISPLAY DATA
         OR    R1,R4               PUT INTO CORRECT REGISTER
         SR    R0,R4               GET LENGTH OF DISPLAY DATA
         NI    STATUS4,255-FLAGI   CLEAR ATTENTION FLAG
         TPUT  (1),(0),R           DISPLAY MEMBER SELECTION LIST
         MVC   REPLY+128(128),REPLY   FOR DEBUG
         MVC   TGETREG2(12),TGETREGS  FOR DEBUG
DIRTGET  XC    REPLY(128),REPLY    TO SIMPLIFY DEBUGGING
         LA    R1,X'81'            LOAD TGET FLAGS
         SLL   R1,24               SHIFT TO TOP BYTE
         LA    R0,REPLY            GET INPUT BUFFER ADDRESS
         OR    R1,R0               PUT INTO CORRECT REGISTER
         LA    R0,L'REPLY          GET MAXIMUM INPUT LENGTH
         TGET  (1),(0),R           GET SELECTION
         CLI   REPLY,X'88'         QUERY RESPONSE?
         BE    DIRTGET             YES, SHOULD NOT HAPPEN HERE
         STM   R15,R1,TGETREGS     NO, PROCESS INPUT
         LA    R0,3                GET READ HEADER LENGTH
         CR    R0,R1               PA1, PA2, PA3 OR CLEAR?
         BH    DSPLYDIR            YES, RESHOW SCREEN
         ICM   R0,3,REPLY+1        LOAD ACTUAL CURSOR BUFFER ADDRESS
         TM    REPLY+1,X'40'       12-BIT FORMAT CURSOR ADDRESS?
         BZ    DIRCSRLC            NO, 14-BIT FORMAT WAS SUPPLIED
         SLL   R0,2                SHIFT OUT LOW-ORDER "PARITY" BITS
         ICM   R0,2,REPLY+1        RE-LOAD HIGH-ORDER BYTE
         SLL   R0,18               SHIFT OUT HIGH-ORDER "PARITY" BITS
         SRL   R0,20               CONVERT TO 12-BIT BINARY NUMBER
DIRCSRLC STH   R0,CSRLOCN          REMEMBER LOCATION FOR LATER
         LA    R0,X'0F'            RESET "UNINTERESTING" BITS IN TGET
         NR    R15,R0                 RC - SO X'1C' MAPS TO X'C'
         CH    R15,=H'12'          WAS THE BUFFER TOO SMALL?
         BNE   DIRINQOK            NO, SKIP A SUPERVISOR CALL
         TCLEARQ INPUT             YES, FLUSH EXTRA INPUT
DIRINQOK MVI   DIRHDG,C'-'         RE-INITIALIZE HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVI   WCCSAVE,WCCSTND     RE-INITIALIZE WCC TO TURN BELL OFF
         ICM   R0,B'0011',CONCTOPN ANY CONCATENATED DIRECTORIES READ?
         BZ    POHDGOK1            NO, SUPPLY DATA SET IN HEADING
         LH    R1,$DDNAML          GET DATA DEFINITION NAME LENGTH
         EX    R1,GTPDDNAM         RE-LOAD DDNAME INTO HEADING
         MVC   DIRHDG(L'REVDDMSG),REVDDMSG
         B     POHDGOK2
POHDGOK1 LH    R1,$DSNAME          GET DATA SET NAME LENGTH
         EX    R1,GTPDSNAM         RE-LOAD DSN INTO HEADING
POHDGOK2 MVC   PUTCURSR+1(2),PRIMADDR      AND PUT CURSOR AT HOME
         TM    STATUS7,EDITED      PROMPTING FOR MEMBER NAME?
         BO    GETPRIME            YES, IGNORE PFK
         MVC   KOUNT,TOPMEMAD      SAVE PRE-SCROLL TOP MEMBER ADDRESS
         OI    REPLY,X'70'         FOLD ALL PFKS TO THE FIRST 12
         CLI   REPLY,X'F1'         TOP?
         BE    DIRTOP              YES
         CLI   REPLY,X'F2'         BOTTOM?
         BE    DIRBOTOM            YES
         CLI   REPLY,X'F3'         END?
         BE    DIREXIT             YES
         CLI   REPLY,X'F4'         SORT BY MODIFICATION TIMESTAMP?
         BE    SORTDATE            YES
         CLI   REPLY,X'F5'         REPORT THE NUMBER OF BLOCKS READ?
         BE    PUTBCNTR            YES
         CLI   REPLY,X'F6'         SORT INTO COLLATING SEQUENCE?
         BE    SORTNAME            YES
         CLI   REPLY,X'F7'         SCROLL UP?
         BE    DIRUPCMD            YES
         CLI   REPLY,X'F8'         SCROLL DOWN?
         BE    DIRDNCMD            YES
         CLI   REPLY,X'F9'         SORT INTO DECREASING SIZE SEQUENCE?
         BE    SORTSIZE            YES
         CLI   REPLY,X'7A'         SORT INTO PHYSICAL SEQUENCE?
         BE    SORTTTR             YES
         CLI   REPLY,X'7B'         SORT INTO USERID COLLATING SEQUENCE?
         BE    SORTUSER            YES
         CLI   REPLY,X'7C'         SORT BY SSI/VV.MM?
         BE    SORTSSI             YES
         BH    WASENTER            NO, INVALID KEY?  NO, IT WAS ENTER
         MVI   DIRHDG,C'-'         YES, LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'INVPFMSG),INVPFMSG
BELDSPLY MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         B     SAMEMEMS
PUTBCNTR ICM   R0,15,OFFLDCNT      ANY TAGGED MEMBERS?
         BNZ   FINDTAG             YES, FIND ONE
         MVI   DIRHDG,C'-'         LOAD MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'ED5+L'BLKSREAD),ED5 + BLKSREAD
         L     R1,BLKREADS         GET READ (OR READ GET) COUNTER
         CVD   R1,DOUBLE
         ED    DIRHDG(L'ED5),DOUBLE+5
DIRMSGLP CLI   DIRHDG,C' '         LEADING BLANK?
         BNE   SAMEMEMS            NO, DISPLAY READY
         MVC   DIRHDG(L'ED5+L'BLKSREAD),DIRHDG+1
         CLC   =C'1 ',DIRHDG       JUST ONE DIRECTORY BLOCK?
         BNE   DIRMSGLP            WELL, CHECK IF MORE EDITING NEEDED
         MVC   DIRHDG+17(7),DIRHDG+18    YES, MAKE SINGULAR
         B     SAMEMEMS            DISPLAY READY
WASENTER CLI   REPLY+3,SBA         SBA?
         BE    GETPRIME            YES, CHECK FOR PRIMARY INPUT
LEADRJMP CLI   REPLY,X'7D'         NO, WAS IT REALLY ENTER?
         BNE   SAMEMEMS            NO, PFK ALREADY PROCESSED
         LA    R1,REPLY            YES, POINT TO AID AND CURSOR ADDRESS
         B     SELCODOK            ANALYSE CURSOR LOCATION
GETPRIME LA    R1,REPLY+3          POINT TO SBA
         CLC   1(2,R1),PRIMADDR    PRIMARY INPUT AREA USED?  (12-BIT)
         BE    WASPRIME            YES
         CLC   1(2,R1),PRIMLOCN    PRIMARY INPUT AREA USED?  (14-BIT)
         BNE   SELCDCHK            NO, CHECK MEMBER SELECTION CODE
WASPRIME LA    R5,8                GET MAXIMUM MEMBER NAME LENGTH
         LA    R1,REPLY+6          STARTING POSITION FOR SOURCE
         LA    R7,DOUBLE           STARTING POSITION FOR TARGET
         XC    DOUBLE,DOUBLE       CLEAR ANY PREVIOUS NAME
SBACHECK CLI   0(R1),SBA           SBA FOUND?
         BE    FOLDWORK            YES
         MVC   0(1,R7),0(R1)       LOAD A CHARACTER
         LA    R7,1(,R7)           POINT TO TARGET NEXT BYTE
         LA    R1,1(,R1)           POINT TO SOURCE NEXT BYTE
         BCT   R5,SBACHECK               AND LOOP
FOLDWORK OC    DOUBLE(8),=CL8' '   CONVERT TEXT TO UPPER CASE
         CLC   DOUBLE(8),=CL8' '   BLANK INPUT?
         BNE   LEFTJUST            NO, PROCESS IT
         TM    STATUS7,EDITED      PROMPTING FOR MEMBER NAME?
         BO    EDITMEM0            YES, EDIT REQUEST NOW CANCELLED
         CLI   0(R1),SBA           YES, SBA FOUND?
         BNE   SAMEMEMS            NO, RESHOW SCREEN
         B     SELCDCHK            YES, CHECK MEMBER SELECTION CODE
EDITMEM0 MVI   DIRHDG,C'-'         NO, LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         TM    MODE,OFFLDSW        REALLY PROMPTING FOR FILE NAME?
         BO    REPCNCLD            YES
         MVC   DIRHDG(L'EDIT0MSG),EDIT0MSG
         NI    STATUS7,255-EDITED  RESET MEMBER EDIT PROMPT FLAG
         B     BELDSPLY            GO RING THE BELL
LEFTJUST CLI   DOUBLE,C' '         FIRST CHARACTER BLANK?
         BNE   GOTPRIME            NO, HAVE PRIMARY INPUT FIELD AT LAST
         MVC   DOUBLE(7),DOUBLE+1
         MVI   DOUBLE+7,C' '       SHIFT LEFT ONE BYTE AND ADD A BLANK
         B     LEFTJUST            CHECK FOR ANOTHER LEADING BLANK
GOTPRIME TM    STATUS7,EDITED      PROMPTING FOR MEMBER NAME?
         BZ    USEPRIME            NO, PROCESS PRIMARY INPUT AS USUAL
         NI    STATUS7,255-EDITED  YES, RESET MEMBER EDIT PROMPT FLAG
         TM    MODE,OFFLDSW        REALLY PROMPTING FOR FILE NAME?
         BO    REPOFFDD            YES
         MVC   $MEMBER,DOUBLE      LOAD MEMBER NAME TO EDIT
         MVI   DIRHDG,C'-'         RESET HEADING FOR RETURN FROM EDIT
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         XC    $CONCAT,$CONCAT     USE FIRST DATA SET IN CONCATENATION
         LA    R1,REPLY+3          POINT TO INDICATOR FOR REVEDIT
         B     ISPFISOK            GO CALL REVEDIT
USEPRIME MVC   SELMEMNM(8),DOUBLE  LOAD MEMBER NAME TO SEARCH FOR
         LR    R7,R1               SAVE SBA POINTER FOR TAGFLIP
         LA    R1,REPLY+3          POINT TO PRIMARY INPUT FOR EDITCHEK
         L     R15,=A(DIRCMDTB)    POINT TO SUBCOMMAND TABLE
DIRCMDLP CLC   DOUBLE,0(R15)       FOUND THE SUBCOMMAND?
         BL    NOTPOCMD            NO, HANDLE NON-COMMAND PRIMARY INPUT
         BE    8(,R15)             YES, PERFORM IT
         LA    R15,12(,R15)        NOT YET, POINT TO NEXT ENTRY
         B     DIRCMDLP            CONTINUE SUBCOMMAND TABLE SCAN
NOTPOCMD CLI   REPLY,X'F7'         SCROLLING PFK?
         BL    FINDMEM             NO, GO SEARCH FOR THE MEMBER NAME
         CLI   REPLY,X'F8'         SCROLLING PFK?
         BH    FINDMEM             NO, GO SEARCH FOR THE MEMBER NAME
         B     SAMEMEMS            YES, SCROLL AMOUNT, NOT LOCATE DATA

RDYRFRSH L     1,TOPMEMAD          POINT TO CURRENT TOP MEMBER
         MVC   $MEMBER(10),0(R1)   REMEMBER IT
         B     RFRSHOBT            PERFORM DIRECTORY REFRESH

SELCDCHK TM    STATUS7,EDITED      PROMPTING FOR MEMBER NAME?
         BO    EDITMEM0            YES, EDIT REQUEST NOW CANCELLED
         CLI   3(R1),SBA           ANOTHER SBA CODE?
         BE    NULLSEL             YES
         OI    3(R1),C' '          FOLD SELECTION CODE TO UPPER CASE
         CLI   3(R1),C' '          BLANK CODE?
         BE    BLANKSEL            YES
         CLI   3(R1),C'.'          NO-OP CODE?
         BE    BLANKSEL            YES
         CLI   3(R1),C'S'          CORRECT SELECTION CODE FOR REVIEW?
         BE    SELCODOK            YES
         CLI   3(R1),C'T'          CORRECT SELECTION CODE FOR TAG?
         BE    SELCODOK            YES
         CLI   3(R1),C'R'          CORRECT SELECTION CODE FOR RESET?
         BE    SELCODOK            YES
         TM    STATUS6,HELSW       INVOKED AS FULLSCREEN HELP?
         BO    SELCDBAD            YES, SELECTION CODE NOT ALLOWED
         CLI   3(R1),C'B'          CORRECT SELECTION CODE FOR BROWSE?
         BE    SELCODOK            YES
         CLI   3(R1),C'E'          CORRECT SELECTION CODE FOR EDIT?
         BE    SELCODOK            YES
SELCDBAD MVI   DIRHDG,C'-'         NO, LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'INVCDMSG),INVCDMSG
         MVC   PUTCURSR+1(2),1(R1) POINT TO WHERE OFFENDING CODE WAS
         B     BELDSPLY            GO RING THE BELL
OFFDSNBK LH    R15,$DSNAME         RESTORE DATA SET NAME INTO HEADING
         EX    R15,GTPDSNAM          AFTER 'T' AND 'R' PROCESSING
BLANKSEL CLI   4(R1),SBA           DOES ANOTHER SELECTION FOLLOW?
         BNE   LEADRJMP            NO, CHECK FOR "JUMP FROM LEADER DOT"
         LA    R1,4(,R1)           YES, POINT TO IT
         B     SELCDCHK            PROCESS IT
NULLSEL  LA    R1,3(,R1)           YES, POINT TO IT
         B     SELCDCHK            PROCESS IT

DIRTOP   LA    R3,DIRENTS          POINT TO FIRST ENTRY
         ST    R3,TOPMEMAD         SAVE NEW TOP ENTRY POINTER
         B     WASENTER            PROCESS OTHER INPUT

DIRBOTOM L     R3,LASTADDR         POINT TO **END**
         LA    R3,14(,R3)          POINT TO **END**'S LENGTH INDICATOR
         L     R1,SCRNLNES         GET NUMBER OF LINES ON SCREEN
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         B     DIRCLIMB                  AND SCROLL UP ONE PAGE

DIRUPCMD L     R15,=A(DIRSCROL)    POINT ROUTINE ENTRY POINT
         BALR  R14,R15             EXTRACT ANY SCROLL AMOUNT OVERRIDE
         CR    R1,R3               MAXIMUM REQUESTED?
         BE    DIRTOP              YES
         L     R3,TOPMEMAD         POINT TO CURRENT TOP MEMBER NAME
         BCTR  R3,0                POINT TO LENGTH OF PREVIOUS ENTRY
         BCTR  R3,0
         LTR   R1,R1               SCROLL AMOUNT SPECIFIED?
         BP    DIRCLIMB            YES, PERFORM SCROLLING
         BM    DIRUPCSR            CURSOR WAS REQUESTED
         TM    STATUS4,CSRSW       CURSOR SCROLLING ACTIVE?
         BNO   DIRUPTST            NO
DIRUPCSR LH    R1,CSRLOCN          GET CURSOR LOCATION
         SLR   R0,R0
         D     R0,SCRNCOLS         DETERMINE LINE NUMBER
         BCTR  R1,0
         BCTR  R1,0
         LTR   R0,R1
         BNP   DIRUPTST            UP A PAGE IF CURSOR NOT LOW ENOUGH
         L     R1,SCRNLNES         GET LINES ON SCREEN
         SR    R1,R0               GET LINES TO SCROLL UP
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         B     DIRCLIMB            USE IT
DIRUPTST L     R1,SCRNLNES         GET NUMBER OF LINES ON SCREEN
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         TM    STATUS4,PGSW        FULL PAGE SCROLLING?
         BO    DIRCLIMB            YES
         SRL   R1,1                NO, HALVE IT
         TM    STATUS4,HFSW        HALF PAGE SCROLLING?
         BO    DIRCLIMB            YES
         L     R1,SCROLL           NO, LOAD SCROLL AMOUNT
DIRCLIMB LA    R2,DIRENTS          POINT TO FIRST ENTRY
UPONEMEM CR    R3,R2               POINTING TO BEFORE FIRST ENTRY?
         BL    FNDUPMEM            YES, USE FIRST ENTRY
         SH    R3,0(,R3)           NO, BACK UP ONE ENTRY
         BCT   R1,UPONEMEM
FNDUPMEM LA    R3,2(,R3)           POINT TO START OF ENTRY
         ST    R3,TOPMEMAD         SAVE NEW TOP ENTRY POINTER
         B     WASENTER            PROCESS OTHER INPUT

DIRDNCMD L     R15,=A(DIRSCROL)    POINT ROUTINE ENTRY POINT
         BALR  R14,R15             EXTRACT ANY SCROLL AMOUNT OVERRIDE
         CR    R1,R3               MAXIMUM REQUESTED?
         BE    DIRBOTOM            YES
         L     R3,TOPMEMAD         POINT TO CURRENT TOP MEMBER NAME
         LTR   R1,R1               SCROLL AMOUNT SPECIFIED?
         BP    DOWN1MEM            YES, PERFORM SCROLLING
         BM    DIRDNCSR            CURSOR WAS REQUESTED
         TM    STATUS4,CSRSW       CURSOR SCROLLING ACTIVE?
         BNO   DIRDNTST            NO
DIRDNCSR LH    R1,CSRLOCN          GET CURSOR LOCATION
         SLR   R0,R0
         D     R0,SCRNCOLS         DETERMINE LINE NUMBER
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         BP    DOWN1MEM            USE THIS NUMBER OF LINES IF POSITIVE
DIRDNTST L     R1,SCRNLNES         GET NUMBER OF LINES ON SCREEN
         SH    R1,=H'3'            LESS THREE LINES FOR HEADINGS
         TM    STATUS4,PGSW        FULL PAGE SCROLLING?
         BO    DOWN1MEM            YES
         SRL   R1,1                NO, HALVE IT
         TM    STATUS4,HFSW        HALF PAGE SCROLLING?
         BO    DOWN1MEM            YES
         L     R1,SCROLL           NO, LOAD SCROLL AMOUNT
DOWN1MEM C     R3,LASTADDR         POINTING TO LAST ENTRY?
         BNL   DOWNDONE            YES, USE LAST ENTRY
         AH    R3,14(,R3)          NO, POINT TO NEXT ENTRY
         BCT   R1,DOWN1MEM
DOWNDONE ST    R3,TOPMEMAD         SAVE NEW TOP ENTRY POINTER
         B     WASENTER            PROCESS OTHER INPUT

DIRCAN   NI    STATUS,255-STPROF   TURN OFF PROFILE REWRITE FLAG
DIREXIT  TM    STATUS,STOPEN       IS THE MEMBER DATA DCB STILL OPEN?
         BZ    DIREXITX            NO, PROCEED WITH EXIT
         TM    DCBOFLGS-IHADCB+DYNDCBW,X'10'
         BZ    DIREXITX            NO, PROCEED WITH EXIT
         MVI   CLOSED,X'80'        YES, SO CLOSE IT
         CLOSE (DYNDCBW),MF=(E,CLOSED)
         NI    STATUS,255-STOPEN   CLOSED
DIREXITX LM    R0,R1,FREEMRGS      PREPARE FOR FREEMAIN
         L     R13,4(,R13)         POINT TO CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @DYNAREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SLR   R15,R15
         STH   R15,RC              OVERWRITE PREVIOUS NON-ZERO RC
         BR    R14                 RETURN

SORTDATE TM    STATUS3,TTRSORT     CURRENTLY SORTED INTO TTR ORDER?
         BZ    NOTTTRED            NO, IT IS NOT TTR'D
         MVI   STATUS3,DATESORT    SORT INTO DESCENDING CHANGE DATE
         L     R15,=V(REVSORTE)    GET SORT ENHANCER ENTRY POINT
         BALR  R14,R15             CALL SORT ENHANCER ROUTINE
NOTTTRED MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
BUBLDATE MVI   STATUS3,DATESORT    RESET SORT SWAP FLAG
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTDATE LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALDAT            YES, CHECK FOR FINAL PASS
         CLI   15(R2),34           IMS FORMAT MEMBER WITH TIMESTAMP?
         BE    SDATEOK1            YES
         CLI   15(R2),46           SPFD STATISTICS IN ENTRY USER DATA?
         BNE   NEXTDATE            NO, DON'T PROMOTE IT
SDATEOK1 CLI   15(R1),34           IMS FORMAT MEMBER WITH TIMESTAMP?
         BE    SDATEOK2            YES
         CLI   15(R1),46           SPFD STATISTICS IN ENTRY USER DATA?
         BNE   SWAPDATE            NO, DEMOTE IT
SDATEOK2 CLC   24(6,R1),24(R2)     COMPARE TIMESTAMPS IN SPFD STATS
         BH    NEXTDATE            THESE TWO ARE IN ORDER
         BE    DATETTR             TIMESTAMPS ARE EQUAL - LOOK AT TTR
SWAPDATE BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTDATE            PROCESS NEXT ENTRY
DATETTR  CLC   8(5,R1),8(R2)       COMPARE TTR VALUES
         BNL   NEXTDATE            THESE TWO ARE IN ORDER
         B     SWAPDATE            ASSUME LATER MEMBER HAS HIGHER TTR
FINALDAT TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLDATE            YES, DO ANOTHER PASS
         MVI   DIRHDG,C'-'         NO, LOAD SORTED BY TIMESTAMP MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'DATSTMSG),DATSTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

SORTNAME CLI   DIRMEMCT+4,C' '     FEWER THAN TEN MEMBERS?
         BE    SORTEDNM            YES, PRETEND ALREADY SORTE'D BY NAME
         MVI   STATUS3,0           SORT INTO NAME COLLATING SEQUENCE
         L     R15,=V(REVSORTE)    GET SORT ENHANCER ENTRY POINT
         BALR  R14,R15             CALL SORT ENHANCER ROUTINE
SORTEDNM MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         ST    R2,DOUBLE+4         SAVE ADDRESS OF FIRST ENTRY TO SORT
BUBLNAME MVI   STATUS3,0           RESET SORT SWAP FLAG
         MVC   OPEND,DOUBLE+4      UPDATE START OF BUBBLE PASS LIMIT
         L     R2,OPEND            POINT TO FIRST ENTRY TO SORT
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTNAME LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALNAM            YES, CHECK FOR FINAL PASS
         CLC   0(10,R1),0(R2)      COMPARE MEMBER NAMES (+ CONCAT NO.)
         BNH   NEXTNAME            THESE TWO ARE IN ORDER
         BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTNAME            PROCESS NEXT ENTRY
FINALNAM TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BNO   EXITNAME            NO, EXIT FROM SORT BY NAME
         MVI   STATUS3,0           RESET SORT SWAP FLAG
         L     R1,DOUBLE           YES, POINT PAST ENTRIES TO SORT
         BCTR  R1,0
         BCTR  R1,0                POINT TO TRAILING LENGTH FIELD
         LA    R2,OPEND            POINT TO FIRST ENTRY TO SORT
         CR    R1,R2               BACKED UP TOO FAR?
         BL    EXITNAME            YES, MUST BE ALL SORTED NOW
         SH    R1,0(,R1)           POINT TO BEFORE LAST UNSORTED ENTRY
BACKNAME LR    R2,R1
         SH    R1,0(,R2)           POINT TO PREVIOUS ENTRY FOR COMPARE
         CLC   2(10,R1),2(R2)      COMPARE MEMBER NAMES (+ CONCAT NO.)
         BNH   BAKUPNAM            THESE TWO ARE IN ORDER
         BAL   R14,SWPBKMEM        OUT OF ORDER SO BUBBLE UP ONE
BAKUPNAM C     R1,OPEND            BACK TO START OF ENTRIES TO SORT?
         BNL   BACKNAME            PROCESS PREVIOUS ENTRY
         TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLNAME            YES, DO ANOTHER PASS
EXITNAME CLI   REPLY,X'F6'         CAME HERE FROM PF6/18 REQUEST?
         BNE   FINDMEM             NO, LEAVE POSSIBLE PREVIOUS MESSAGE
         MVI   DIRHDG,C'-'         YES, LOAD SORTED BY NAME MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'NAMSTMSG),NAMSTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

SORTSIZE MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
BUBLSIZE MVI   STATUS3,SIZESORT    RESET SORT SWAP FLAG
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTSIZE LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALSIZ            YES, CHECK FOR FINAL PASS
         CLI   15(R2),22           BASIC OR SSI ONLY ENTRY?
         BNH   NEXTSIZE            YES, DON'T PROMOTE IT
         CLI   15(R1),22           BASIC OR SSI ONLY ENTRY?
         BNH   SWAPSIZE            YES, DEMOTE IT
         TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BO    PGMSIZE             YES, CHECK LOAD MODULE SIZES
         CLC   30(2,R1),30(R2)     COMPARE SIZES IN SPFD STATS
         BNL   NEXTSIZE            THESE TWO ARE IN ORDER
         B     SWAPSIZE            THESE TWO ARE NOT IN ORDER
PGMSIZE  CLC   18(3,R1),18(R2)     COMPARE SIZES OF LOAD MODULES
         BNL   NEXTSIZE            THESE TWO ARE IN ORDER
SWAPSIZE BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTSIZE            PROCESS NEXT ENTRY
FINALSIZ TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLSIZE            YES, DO ANOTHER PASS
         MVI   DIRHDG,C'-'         NO, LOAD SORTED BY SIZE MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'SIZSTMSG),SIZSTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

SORTTTR  CLI   DIRMEMCT+4,C' '     FEWER THAN TEN MEMBERS?
         BE    SRTEDTTR            YES, PRETEND ALREADY SORTE'D BY TTR
*        TM    STATUS3,TTRSORT     ALREADY CALLED REVSORTE?
*        BO    SRTEDTTR            YES, PROBABLY ATTENTION INTERRUPT
         MVI   STATUS3,TTRSORT     SORT INTO PHYSICAL LOCATION ORDER
         L     R15,=V(REVSORTE)    GET SORT ENHANCER ENTRY POINT
         BALR  R14,R15             CALL SORT ENHANCER ROUTINE
SRTEDTTR MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         ST    R2,DOUBLE+4         SAVE ADDRESS OF FIRST ENTRY TO SORT
BUBLTTR  MVI   STATUS3,TTRSORT     RESET SORT SWAP FLAG
         MVC   OPEND,DOUBLE+4      UPDATE START OF BUBBLE PASS LIMIT
         L     R2,OPEND            POINT TO FIRST ENTRY TO SORT
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTTTR  LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALTTR            YES, CHECK FOR FINAL PASS
         CLC   8(6,R1),8(R2)       COMPARE CONCAT#, TTR AND ALIAS FLAG
         BNH   NEXTTTR             THESE TWO ARE IN ORDER
         BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTTTR             PROCESS NEXT ENTRY
FINALTTR TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BNO   EXITTTR             NO, EXIT FROM SORT BY TTR
         MVI   STATUS3,TTRSORT     RESET SORT SWAP FLAG
         L     R1,DOUBLE           YES, POINT PAST ENTRIES TO SORT
         BCTR  R1,0
         BCTR  R1,0                POINT TO TRAILING LENGTH FIELD
         LA    R2,OPEND            POINT TO FIRST ENTRY TO SORT
         CR    R1,R2               BACKED UP TOO FAR?
         BL    EXITTTR             YES, MUST BE ALL SORTED NOW
         SH    R1,0(,R1)           POINT TO BEFORE LAST UNSORTED ENTRY
BACKTTR  LR    R2,R1
         SH    R1,0(,R2)           POINT TO PREVIOUS ENTRY FOR COMPARE
         CLC   10(6,R1),10(R2)     COMPARE CONCAT#, TTR AND ALIAS FLAG
         BNH   BAKUPTTR            THESE TWO ARE IN ORDER
         BAL   R14,SWPBKMEM        OUT OF ORDER SO BUBBLE UP ONE
BAKUPTTR C     R1,OPEND            BACK TO START OF ENTRIES TO SORT?
         BNL   BACKTTR             PROCESS PREVIOUS ENTRY
         TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLTTR             YES, DO ANOTHER PASS
EXITTTR  MVI   DIRHDG,C'-'         NO, LOAD SORTED BY TTR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'TTRSTMSG),TTRSTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

SORTUSER MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
BUBLUSER MVI   STATUS3,USERSORT    RESET SORT MODE AND SWAP FLAGS
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTUSER LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALUSR            YES, CHECK FOR FINAL PASS
         CLI   15(R2),46           SPFD STATISTICS IN ENTRY USER DATA?
         BNE   NEXTUSER            NO, DON'T PROMOTE IT
         CLI   15(R1),46           SPFD STATISTICS IN ENTRY USER DATA?
         BNE   SWAPUSER            NO, DEMOTE IT
         CLC   36(8,R1),36(R2)     COMPARE USERIDS IN SPFD STATS
         BNH   NEXTUSER            THESE TWO ARE IN ORDER
SWAPUSER BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTUSER            PROCESS NEXT ENTRY
FINALUSR TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLUSER            YES, DO ANOTHER PASS
         MVI   DIRHDG,C'-'         NO, LOAD SORTED BY USERID MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'USRSTMSG),USRSTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

SORTSSI  MVC   DOUBLE(4),LASTADDR  LOAD FIRST BUBBLE PASS LIMIT
BUBLSSI  MVI   STATUS3,SSISORT     RESET SORT MODE AND SWAP FLAGS
         LA    R2,DIRENTS          POINT TO FIRST ENTRY
         C     R2,DOUBLE           ANY MEMBERS IN THIS PDS?
         BE    NULLSORT            NO
         MVC   DOUBLE+4(4),DOUBLE  UPDATE END OF BUBBLE PASS LIMIT
NEXTSSI  LR    R1,R2
         AH    R2,14(,R1)          POINT TO SECOND ENTRY FOR COMPARE
         C     R2,DOUBLE+4         REACHED END OF PASS?
         BE    FINALSSI            YES, CHECK FOR FINAL PASS
         LA    R15,30(,R2)         POINT TO THE PROGRAM SSI
         CLI   15(R2),36           PROGRAM WITH SSI?
         BE    SSIENTY1            YES, HAVE THE RIGHT SORT FIELD
         CLI   15(R2),44           PROGRAM ALIAS WITH SSI?
         BE    SSIENTY1            YES, HAVE THE RIGHT SORT FIELD
         CLI   15(R2),16           ANY IN-CORE USER DATA?
         BE    NEXTSSI             NO, DON'T PROMOTE IT
         CLI   15(R2),32           PROGRAM WITHOUT SSI?
         BE    NEXTSSI             YES, DON'T PROMOTE IT
         CLI   15(R2),40           PROGRAM ALIAS WITHOUT SSI?
         BE    NEXTSSI             YES, DON'T PROMOTE IT
         LA    R15,16(,R2)         POINT TO THE START OF THE USER DATA
SSIENTY1 LA    R14,30(,R1)         POINT TO THE PROGRAM SSI
         CLI   15(R1),36           PROGRAM WITH SSI?
         BE    SSIENTY2            YES, HAVE THE RIGHT SORT FIELD
         CLI   15(R1),44           PROGRAM ALIAS WITH SSI?
         BE    SSIENTY2            YES, HAVE THE RIGHT SORT FIELD
         CLI   15(R1),16           ANY IN-CORE USER DATA?
         BE    SWAPSSI             NO, DEMOTE IT
         CLI   15(R1),32           PROGRAM WITHOUT SSI?
         BE    SWAPSSI             YES, DEMOTE IT
         CLI   15(R1),40           PROGRAM ALIAS WITHOUT SSI?
         BE    SWAPSSI             YES, DEMOTE IT
         LA    R14,16(,R1)         POINT TO THE START OF THE USER DATA
SSIENTY2 CLC   0(4,R14),0(R15)     COMPARE FIRST 2 USER DATA HALFWORDS
         BNH   NEXTSSI             THESE TWO ARE IN ORDER
SWAPSSI  BAL   R14,SWAPMEMS        OUT OF ORDER SO BUBBLE DOWN ONE
         B     NEXTSSI             PROCESS NEXT ENTRY
FINALSSI TM    STATUS3,SORTSWAP    ANY MEMBERS BUBBLED IN THIS PASS?
         BO    BUBLSSI             YES, DO ANOTHER PASS
         MVI   DIRHDG,C'-'         NO, LOAD SORTED BY SSI MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'SSISTMSG),SSISTMSG
         B     FINDMEM             RESHOW FROM REVIEWED MEMBER OR TOP

NULLSORT MVI   DIRHDG,C'-'         NO DIRECTORY ENTRIES IN PDS TO SORT
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'NULSTMSG),NULSTMSG
         MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         B     DIRTOP              RESHOW FROM TOP

SWAPMEMS TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    SORTATTN            YES, TERMINATE SORT
         LH    R3,14(,R1)          GET LENGTH OF FIRST ENTRY
         LH    R4,14(,R2)          GET LENGTH OF SECOND
         BCTR  R3,0                PREPARE FOR EXECUTE
         BCTR  R4,0                PREPARE FOR EXECUTE
         EX    R3,SAVEFRST         SAVE THE FIRST ENTRY
         EX    R4,MOVESCND         PROMOTE THE SECOND ENTRY
         LR    R2,R1
         AH    R2,14(,R1)          POINT TO NEW LOCATION OF FIRST
         EX    R3,LOADFRST         DEMOTE WHAT WAS THE FIRST ENTRY
         OI    STATUS3,SORTSWAP    INDICATE MOVEMENT IN THIS PASS
         ST    R2,DOUBLE           UPDATE END OF BUBBLE PASS LIMIT
         BR    R14                 RETURN TO BUBBLE SORT MAINLINE
SAVEFRST MVC   REPLY+8(0),0(R1)    <<< EXECUTED >>>
MOVESCND MVC   0(0,R1),0(R2)       <<< EXECUTED >>>
LOADFRST MVC   0(0,R2),REPLY+8     <<< EXECUTED >>>
SWPBKMEM TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    SORTATTN            YES, TERMINATE SORT
         LH    R3,16(,R1)          GET LENGTH OF FIRST ENTRY
         LH    R4,16(,R2)          GET LENGTH OF SECOND
         BCTR  R3,0                PREPARE FOR EXECUTE
         BCTR  R4,0                PREPARE FOR EXECUTE
         EX    R3,BSAVFRST         SAVE THE FIRST ENTRY
         EX    R4,BMOVSCND         PROMOTE THE SECOND ENTRY
         LR    R2,R1
         AH    R2,16(,R1)          POINT TO NEW LOCATION OF FIRST
         EX    R3,BLODFRST         DEMOTE WHAT WAS THE FIRST ENTRY
         OI    STATUS3,SORTSWAP    INDICATE MOVEMENT IN THIS PASS
         LA    R2,2(,R2)           POINT TO ACTUAL START OF 2ND ENTRY
         ST    R2,DOUBLE+4         UPDATE START OF BUBBLE PASS LIMIT
         BR    R14                 RETURN TO BUBBLE SORT MAINLINE
BSAVFRST MVC   REPLY+8(0),2(R1)    <<< EXECUTED >>>
BMOVSCND MVC   2(0,R1),2(R2)       <<< EXECUTED >>>
BLODFRST MVC   2(0,R2),REPLY+8     <<< EXECUTED >>>
SORTATTN MVI   DIRHDG,C'-'         LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'INRPTMSG),INRPTMSG
         MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         B     DIRTOP              ATTENTION HIT SO STOP SORTING

GTPDSNAM MVC   DIRHDG(0),$DSNAME+2 <<< EXECUTED >>>
GTPDDNAM MVC   DIRHDG+13(0),$DDNAME-4 <<< EXECUTED >>>

SELCODOK SLR   R5,R5               PREPARE FOR INSERT
         ICM   R5,3,1(R1)          LOAD SCREEN ADDRESS BYTES
         TM    1(R1),X'40'         12-BIT ADDRESS?
         BZ    SELCODAD            NO, 14-BIT ADDRESS
         IC    R5,2(,R1)           GET LOW ORDER SCREEN ADDRESS BYTE
         SLL   R5,2                SHIFT OUT "PARITY" BITS
         ICM   R5,2,1(R1)          GET HIGH ORDER SCREEN ADDRESS BYTE
         SLL   R5,18               SHIFT OUT "PARITY" BITS
         SRL   R5,20               MAKE BINARY INTEGER
SELCODAD SLR   R4,R4               PREPARE FOR DIVIDE
         D     R4,SCRNCOLS         DETERMINE LINE NUMBER
         SH    R5,=H'3'            ADJUST FOR THREE HEADING LINES
         BNM   ESVALID             IF NEGATIVE THEN EITHER IT IS A
         CLI   0(R1),X'7D'            CHECK ON THE CURSOR POSITION
         BE    SAMEMEMS                   = OR =
         MVI   DIRHDG,C'-'            THE SCREEN HAD BEEN CLEARED
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'SCRERMSG),SCRERMSG
         B     BELDSPLY            GO RING THE BELL
ESVALID  LR    R0,R4               SAVE COLUMN REMAINDER
         L     R4,KOUNT            GET PRE-SCROLL TOP ENTRY ADDRESS
         BZ    SELDMEM             IN CASE FIRST SHOWN MEMBER CHOSEN
ESLOOP   AH    R4,14(,R4)          POINT TO NEXT ENTRY
         C     R4,LASTADDR         REACHED END OF DIRECTORY?
         BNL   SAMEMEMS            YES, CURSOR WAS BELOW MEMBER LIST
         BCT   R5,ESLOOP           NO
SELDMEM  CLI   3(R1),C'T'          MEMBER TAG SELECTION?
         BE    OFFLDMEM            YES, GO FLAG MEMBER FOR LATER
         CLI   3(R1),C'R'          MEMBER TAG SELECTION RESET?
         BE    OFFLDRES            YES, GO UNFLAG MEMBER
         MVC   $MEMBER(10),0(R4)   MOVE IN SELECTED MEMBER NAME
         CLI   3(R1),C'B'          SPF BROWSE SELECTION?
         BE    BRWSCHEK            YES, CHECK VALIDITY
         CLI   3(R1),C'E'          SPF EDIT SELECTION?
         BE    EDITCHEK            YES, CHECK VALIDITY
         CLI   3(R1),C'S'          EXPLICIT REVIEW SELECTION?
         BE    DOXTRACT            YES, PROCEED
         LA    R15,1               NO, REVIEW IMPLIED BY CURSOR
         CR    R15,R0              WAS THE CURSOR IN COLUMN 2?
         BNE   SAMEMEMS            NO, NOT "JUMPING FROM LEADER DOTS"
DOXTRACT L     R15,=A(POXTRACT)    GET ROUTINE ADDRESS
         BALR  R14,R15             EXTRACT DIRECTORY ENTRY FOR REVIEW2
         LA    R15,POSAVE2         POINT TO NEW SAVE AREA
         ST    R15,8(,R13)         LINK SAVE AREAS
         ST    R13,4(,R15)
         L     R15,=V(REVIEW2)     GET ADDRESS OF PHASE 2
         STM   R14,R12,12(R13)     SAVE ALL REGISTERS
         L     R13,8(,R13)         POINT TO NEW SAVE AREA
         BALR  R14,R15             CALL REVIEW PHASE 2
         L     R13,4(,R13)         POINT TO OLD SAVE AREA
         LM    R0,R12,20(R13)      RESTORE ALL EXCEPT BALR REGISTERS
         TM    STATUS,STPODIR      STILL IN MEMBER SELECT MODE?
         BZ    DIREXIT             NO, EXIT WAS REQUESTED
         MVC   SELMEMNM,$MEMBER    YES, LOAD MEMBER NAME TO SEARCH FOR
         MVI   READR,X'80'         RESET UTILITY PROGRAM INDICATOR
         B     BROWSEOK            REDISPLAY MEMBER LIST


ZEDITCHK TM    STATUS6,HELSW       INVOKED AS FULLSCREEN HELP?
         BO    FINDMEM             YES, ISPF EDIT NOT ALLOWED
BRWSCHEK EQU   *                   BROWSE PRE-REQS NOW SAME AS EDIT
EDITCHEK MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         CLI   $VOLSER,C' '        IS THE PDS ON VIO?
         BNE   EDITPREP            NO
         MVC   DIRHDG(L'VIOPOMSG),VIOPOMSG
         B     BELDSPLY            GO RING THE BELL
EDITPREP CLI   3(R1),C'='          IS THIS AN '=EDITMEM' REQUEST?
         BNE   ISPFISOK            NO
         OI    STATUS7,EDITED      FLAG MEMBER NAME PROMPT IS ACTIVE
         MVC   DIRHDG(L'EDITMMSG),EDITMMSG
         B     BELDSPLY            GO RING THE BELL
ISPFISOK MVC   SELMEMNM,$MEMBER    LOAD MEMBER NAME TO SEARCH FOR
         L     R15,=V(REVEDIT)     EDIT IS A VALID SELECTION
         BALR  R14,R15             CALL REVEDIT
         CLI   3(R1),C'B'          WAS MEMBER BROWSE REQUESTED?
         BE    EOBROWSE            YES
         LTR   R15,R15             WAS THE MEMBER SAVED?
         BNZ   MEMNTSAV            NO
         OI    STATUS7,EDITED      YES, FLAG THIS
RFRSHOBT ICM   R5,8,MYDSCB-44+59   SAVE PREVIOUS EXTENT COUNT
         LA    R1,OBTAINW          GET THE LATEST DSCB DETAILS
         OBTAIN (1)                    FROM THE VTOC
         CLM   R5,8,MYDSCB-44+59   HAS THE NUMBER OF EXTENTS CHANGED?
         BE    REREFRSH            NO
         TM    STATUS,STOPEN       IS THE MEMBER DATA DCB OPEN?
         BZ    REREFRSH            NO, NO DEB TO DELETE WITH A CLOSE
         TM    DCBOFLGS-IHADCB+DYNDCBW,X'10'
         BZ    REREFRSH            NO, NO DEB TO DELETE WITH A CLOSE
         MVI   CLOSED,X'80'        YES, SO CLOSE IT
         CLOSE (DYNDCBW),MF=(E,CLOSED)
         NI    STATUS,255-STOPEN   CLOSED - OPEN WILL FIND NEW EXTENTS
REREFRSH LM    R0,R1,FREEMRGS      PREPARE FOR FREEMAIN
         L     R13,4(,R13)         POINT TO CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @DYNAREA
         B     DIREFRSH            GET CURRENT DIRECTORY INFORMATION
*
*        CHECK THE VTOC AFTER BROWSING A MEMBER.  IF THE NUMBER OF
*   EXTENTS HAS CHANGED THEN CLOSE THE BPAM DCB SO THAT IT WILL HAVE
*   TO BE OPENED AGAIN IF NEEDED, AND WILL THEREFORE HAVE ACCESS TO
*   THE NEW EXTENT(S).  ALSO, IF DS1LSTAR HAS CHANGED THEN REFRESH THE
*   DIRECTORY DATA FROM DISK.  IF DS1LSTAR HAS NOT CHANGED BUT AN
*   UNCONCATENATED PDSE IS BEING PROCESSED WITH THE MEMBER LIST IN
*   MEMBER NAME COLLATING SEQUENCE, THEN PERFORM A REFRESH ANYWAY
*   BECAUSE DS1LSTAR HAS THE MAXIMUM TTR VALUE REGARDLESS OF DATA SET
*   FULLNESS.  IF A PDSE HAS GONE TO A NEW EXTENT THEN ITS DS1LSTAR
*   WILL ALSO BE UPDATED.  IN THE ABSENCE OF PROOF OF ANY DATA SET
*   UPDATE, DO NOT DO A REFRESH WHICH WOULD CAUSE A MEMBER SORT
*   PROCESS, BECAUSE THIS WOULD (NEEDLESSLY) DEGRADE RESPONSE TIME.
*
EOBROWSE LTR   R15,R15             WAS THE BROWSE SUCCESSFUL?
         BZ    BROWSEOK            YES, RE-DISPLAY MEMBER LIST QUICKLY
MEMNTSAV CVD   R15,DOUBLE          STORE DECIMAL NON-ZERO RETURN CODE
         ST    R15,DOUBLE          STORE BINARY NON-ZERO RETURN CODE
         CLI   DOUBLE+3,4          WAS THERE AN ERROR?
         BNE   EDITFAIL            YES
BROWSEOK ICM   R5,7,MYDSCB-44+98   SAVE PREVIOUS DS1LSTAR VALUE
         ICM   R5,8,MYDSCB-44+59   SAVE PREVIOUS EXTENT COUNT
         LA    R1,OBTAINW          GET THE LATEST DSCB DETAILS
         OBTAIN (1)                    FROM THE VTOC
         CLM   R5,8,MYDSCB-44+59   HAS THE NUMBER OF EXTENTS CHANGED?
         BE    PDSEXTOK            NO
         TM    STATUS,STOPEN       IS THE MEMBER DATA DCB OPEN?
         BZ    PDSEXTOK            NO, NO DEB TO DELETE WITH A CLOSE
         TM    DCBOFLGS-IHADCB+DYNDCBW,X'10'
         BZ    PDSEXTOK            NO, NO DEB TO DELETE WITH A CLOSE
         MVI   CLOSED,X'80'        YES, SO CLOSE IT
         CLOSE (DYNDCBW),MF=(E,CLOSED)
         NI    STATUS,255-STOPEN   CLOSED - OPEN WILL FIND NEW EXTENTS
PDSEXTOK CLM   R5,7,MYDSCB-44+98   HAS DS1LSTAR CHANGED?
         BNE   REREFRSH            YES, REFRESH ENTIRE DIRECTORY
         ICM   R0,B'0011',CONCTOPN ANY CONCATENATED DIRECTORIES READ?
         BNZ   POHDGOK4            YES, SUPPLY DDNAME IN HEADING
         TM    MYDSCB-44+78,X'08'  LOOKING AT A SINGLE PDSE?
         BZ    POHDGOK3            NO, SKIP DIRECTORY REFRESH
         TM    STATUS3,SORTMODE    ENTRIES IN COLLATING SEQUENCE?
         BZ    REREFRSH            YES, SNEAK IN A REFRESH FOR A PDSE
POHDGOK3 LH    R1,$DSNAME          GET DATA SET NAME LENGTH
         EX    R1,GTPDSNAM         RE-LOAD DSN INTO HEADING
         B     FINDMEM             RESHOW LIST FROM REQUESTED MEMBER
POHDGOK4 LH    R1,$DDNAML          GET DATA DEFINITION NAME LENGTH
         EX    R1,GTPDDNAM         RE-LOAD DDNAME INTO HEADING
         MVC   DIRHDG(L'REVDDMSG),REVDDMSG
         B     FINDMEM             RESHOW LIST FROM REQUESTED MEMBER
EDITFAIL CLI   DOUBLE+3,16         WAS THE MEMBER FOUND?
         BE    RFRSHOBT            NO, MAGICALLY REFRESH MEMBER LIST
         MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         CLI   DOUBLE+3,14         WAS THE MEMBER IN USE OR NOT FOUND?
         BNE   NOTSPF14            NO
         MVC   DIRHDG(L'INUSEMSG),INUSEMSG
         B     FINDMEM             RESHOW LIST FROM THE MEMBER
NOTSPF14 CLI   DOUBLE+3,12         WAS THE MEMBER EMPTY?
         BNE   NOTSPF12            NO
         MVC   DIRHDG(L'EMPTYMSG),EMPTYMSG
         B     FINDMEM             RESHOW LIST FROM THE MEMBER
NOTSPF12 MVC   DIRHDG(L'SPFERMSG),SPFERMSG
         OI    DOUBLE+7,X'0F'      SHOW DECIMAL RETURN CODE
         UNPK  DIRHDG+11(2),DOUBLE+6(2)
         B     FINDMEM             RESHOW LIST FROM REQUESTED MEMBER


**********************************************************************
*                                                          *         *
*         PROCESS CHANGE OUTPUT DDNAME REQUEST             *  GP@P6  *
*                                                          *  02/99  *
**********************************************************************

SETOFFDD OI    STATUS7,EDITED      FLAG PROMPT IS ACTIVE
         OI    MODE,OFFLDSW        FLAG FILE, NOT MEMBER NAME PROMPT
         MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'SETDDMSG),SETDDMSG
         B     BELDSPLY            GO RING THE BELL
REPOFFDD NI    MODE,255-OFFLDSW    RESET PROMPT TYPE FLAG
         MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         CLI   DOUBLE,C'$'         VALID FILE NAME?
         BL    REPCNCLD            NO
         CLI   DOUBLE,C'Z'         VALID FILE NAME?
         BH    REPCNCLD            NO
         MVC   DIRHDG(L'REPDDMSG),REPDDMSG
         MVC   DIRHDG(8),OFFDDNAM  SHOW OLD FILE NAME
         MVC   OFFDDNAM,DOUBLE     HOPE SO, SAVE NEW DD NAME
GOTOFFDD MVC   REPDDDDN,OFFDDNAM   SHOW NEW FILE NAME
         B     SAMEMEMS            UPDATE THE SCREEN
REPCNCLD NI    MODE,255-OFFLDSW    RESET PROMPT TYPE FLAG
         MVC   DIRHDG(L'NOREPMSG),NOREPMSG
         MVC   NOREPDDN,OFFDDNAM   SHOW OUTPUT FILE NAME
         LA    R15,NOREPDDN+7      POINT TO LAST BYTE OF FILE NAME
BNOREPLP CLI   0(R15),C' '         TRAILING BLANK?
         BNE   BELDSPLY            NO, GO RING THE BELL
         MVI   1(R15),C'-'         YES, SUPPRESS IT
         BCT   R15,BNOREPLP        BACK UP A BYTE


**********************************************************************
*                                                          *         *
*         PROCESS OFFLOAD REQUEST                          *  GP@P6  *
*                                                          *  02/90  *
**********************************************************************

SEQLDCHK MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         TM    MYDSCB-44+84,X'C0'  IS IT FIXED OR VARIABLE FORMAT?
         BM    OFFLFORV            YES, CHECK LOGICAL RECORD LENGTH
         MVC   DIRHDG(OFFINVLN),SYSUTMSG
         MVC   DIRHDG+OFFINVLN(L'RECFMMSG),RECFMMSG
         B     BELDSPLY            GO RING THE BELL
OFFLDCHK TM    MYDSCB-44+78,X'08'  PDSE?
         BNZ   SEQLDCHK            YES, CANNOT OFFLOAD PROGRAM OBJECTS
         MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
OFFLFORV LA    R15,DIRENTS         POINT TO FIRST ENTRY
         C     R15,LASTADDR        ANY MEMBERS IN THIS PDS?
         BL    OFFSMTHG            YES, SOMETHING IS THERE TO OFFLOAD
PONOMEMS MVC   DIRHDG(L'NULOFMSG),NULOFMSG
         B     BELDSPLY            GO RING THE BELL
OFFSMTHG SLR   R0,R0
         L     R15,540             GET POINTER TO CURRENT TCB
         L     R15,12(,R15)        POINT TO TIOT
         LA    R15,24(,R15)        POINT TO TIOELNGH
OFFLOADD CLC   OFFDDNAM,4(R15)     FOUND FILE TO USE?
         BE    OFFLDTRY            YES, FILE EXISTS SO GO AND USE IT
         IC    R0,0(,R15)          GET TIOT ENTRY LENGTH
         AR    R15,R0              POINT TO NEXT TIOT ENTRY
         CLI   0(R15),0            ZERO LENGTH ENTRY?
         BNE   OFFLOADD            NO, CHECK OUT THIS ENTRY
         MVC   DIRHDG(L'SYSUTMSG),SYSUTMSG
         MVC   SYSUTDDN,OFFDDNAM   SHOW CORRECT DDNAME
         LA    R15,SYSUTDDN+7      POINT TO LAST BYTE OF DDNAME
         CLI   DOUBLE+1,C'D'       =DELINK SUCOMMAND?
         BNE   BSYSUTLP            NO
         MVC   DIRHDG(L'SYSUTMSG),DIRHDG+1
         MVC   DIRHDG(6),DLORGMSG  CHANGE OFFLOAD TO DELINK
         BCTR  R15,0
BSYSUTLP CLI   0(R15),C' '         TRAILING BLANK?
         BNE   BELDSPLY            NO, GO RING THE BELL
         MVC   0(20,R15),1(R15)    YES, SUPPRESS IT
         BCT   R15,BSYSUTLP        BACK UP ONE BYTE
OFFLDTRY CLI   DOUBLE+1,C'D'       =DELINK SUCOMMAND?
         BE    DODELINK            YES
         ICM   R0,B'1111',OFFLDCNT FULL OR PARTIAL OFFLOAD?
         BZ    OFFLDALL            FULL
         MVC   DIRHDG(20),OFFSLMSG
         MVC   DIRHDG+20(8),POMEMSTR
         CLI   STATUS3,TTRSORT     ALIASES BY ./ ALIAS STATEMENT?
         BNE   OFFLDCAL            NO, CALL THE OFFLOADER
         MVC   DIRHDG(L'OFFSLMSG),OFFSLMSG
         B     OFFLDCAL            CALL THE OFFLOADER
OFFLDALL MVC   DIRHDG(15),OFFALMSG
         MVC   DIRHDG+15(8),POMEMSTR
         CLI   STATUS3,TTRSORT     ALIASES BY ./ ALIAS STATEMENT?
         BNE   OFFLDCAL            NO, CALL THE OFFLOADER
         MVC   DIRHDG(L'OFFALMSG),OFFALMSG
OFFLDCAL LA    R1,SCREENW-4000     LOAD MESSAGE AND POINT PAST END
         MVC   4000+SCRDSN-SCREEN1(L'DIRHDG,R1),DIRHDG
         LA    R0,4000+SCRDSN-SCREEN1+L'DIRHDG(,R1)
         L     R1,CBPRM3           POINT TO BUFFER START
         SR    R0,R1               GET DATA STREAM LENGTH
         LA    R15,X'03'           LOAD FULLSCREEN TPUT FLAGS
         SLL   R15,24              SHIFT TO TOP BYTE
         OR    R1,R15              PUT INTO CORRECT REGISTER
         TPUT  (1),(0),R           NOTIFY USER OF PROCESSING
         MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE AFTER RETURN
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         L     R15,=V(REVOFFLD)    POINT TO OFFLOAD PROCESSOR ROUTINE
         BALR  R14,R15             CALL OFFLOAD PROCESSOR
         XC    SELMEMNM,SELMEMNM   CLEAR LOCATE MEMBER NAME
         MVI   READR,X'80'         RESET UTILITY PROGRAM INDICATOR
         MVC   DIRHDG(L'OFFLDMSG),OFFLDMSG
         MVC   OFFLDDDN,OFFDDNAM   SHOW CORRECT DDNAME
         LA    R15,OFFLDDDN+7      POINT TO LAST BYTE OF FILE NAME
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BZ    BOFFNDLP            NO, PDS NOW OFFLOADED
         MVC   DIRHDG(L'OFFINMSG),OFFINMSG
         MVC   OFFINDDN,OFFDDNAM   SHOW CORRECT DDNAME
         LA    R15,OFFINDDN+7      POINT TO LAST BYTE OF FILE NAME
BOFFNDLP CLI   0(R15),C' '         TRAILING BLANK?
         BNE   SAMEMEMS            NO, SHOW OFFLOAD TERMINATION MESSAGE
         MVC   0(9,R15),1(R15)     YES, SUPPRESS IT
         BCT   R15,BOFFNDLP        BACK UP A BYTE
OFFLDMEM MVI   REPLY,0             DESTROY ENTER AID TO INHIBIT JUMP
         TM    13(R4),X'40'        ALREADY SELECTED?
         BNZ   OFFDSNBK            YES, TAKE NO ACTION
         OI    13(R4),X'40'        NO, FLAG OFFLOAD SELECTION
         LA    R0,1
         A     R0,OFFLDCNT         INCREMENT OFFLOAD SELECTION COUNTER
         ST    R0,OFFLDCNT
         B     OFFDSNBK
OFFLDRES MVI   REPLY,0             DESTROY ENTER AID TO INHIBIT JUMP
         TM    13(R4),X'40'        IS THE MEMBER FLAGGED FOR OFFLOAD?
         BZ    OFFDSNBK            NO, TAKE NO ACTION
         NI    13(R4),X'FF'-X'40'  RESET OFFLOAD SELECTION FLAG
         ICM   R0,B'1111',OFFLDCNT GET CURRENT OFFLOAD SELECTION COUNT
         BZ    OFFDSNBK            ALREADY ZERO SO SKIP DECREMENT
         BCTR  R0,0                DECREMENT OFFLOAD SELECTION COUNTER
         ST    R0,OFFLDCNT
         B     OFFDSNBK


**********************************************************************
*                                                          *         *
*         PROCESS SEARCH REQUEST                           *  GP@FT  *
*                                                          *  08/96  *
**********************************************************************

POSEARCH MVI   DIRHDG,C'-'         INITIALIZE HEADING STRING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         LA    R15,DIRENTS         POINT TO FIRST ENTRY
         C     R15,LASTADDR        ANY MEMBERS IN THIS PDS?
         BNL   PONOMEMS            NO, NOTHING TO DO
         L     R15,=V(REVSRCH)     POINT TO SEARCH PROCESSOR ROUTINE
         BALR  R14,R15             CALL SEARCH PROCESSOR
         MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         MVI   READR,X'80'         RESET UTILITY PROGRAM INDICATOR
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    SRCHATTN            YES, NOTIFY USER
         MVC   DIRHDG(L'SRCHCMSG),SRCHCMSG
         CLI   REPLY,X'7D'         ENTER AID PRESENT?
         BNE   SAMEMEMS            NO, SEARCH HAS BEEN CANCELLED
         L     R0,OFFLDCNT         GET MATCHING MEMBER COUNT
         CVD   R0,DOUBLE
         MVC   DIRHDG(L'SRCHDMSG),SRCHDMSG
         MVC   DIRHDG+17(10),ED9
         ED    DIRHDG+17(10),DOUBLE+3
SRCHDMLP CLI   DIRHDG+18,C' '      LEADING BLANK?
         BNE   SAMEMEMS            NO, MESSAGE FORMATTED
         MVC   DIRHDG+18(37),DIRHDG+19
         B     SRCHDMLP            REPEAT LEADING BLANK CHECK
SRCHATTN MVC   DIRHDG(L'SRCHIMSG),SRCHIMSG
         B     SAMEMEMS            NOTIFY USER OF ATTENTION INTERRUPT


**********************************************************************
*                                                          *         *
*         PROCESS PDS UPDATE REQUEST                       *  GP@P6  *
*                                                          *  03/99  *
**********************************************************************

PDSUPDTE L     R15,=V(REVLOAD)     POINT TO UPDATE PROCESSOR ROUTINE
         BALR  R14,R15             CALL UPDATE PROCESSOR
         TM    READR,X'80'         WAS UPDATE UTILITY INVOKED?
         BO    SAMEMEMS            NO, DO NOT REFRESH FROM DISK
         B     RDYRFRSH            REVLOAD SHOULD PUT CODE INTO READR


**********************************************************************
*                                                          *         *
*         PROCESS DELINK REQUEST                           *  GP@P6  *
*                                                          *  05/99  *
**********************************************************************

PODELINK MVI   DIRHDG,C'-'         PREPARE FOR MESSAGE
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         TM    MYDSCB-44+84,X'C0'  RECFM=U?
         BNO   DLORGBAD            NO, CAN'T BE LOAD MODULE PDS
         TM    MYDSCB-44+78,X'08'  PDSE?
         BNZ   DLORGBAD            YES, CAN'T BE LOAD MODULE PDS
         B     OFFLFORV            CHECK FOR EMPTY PDS, MISSING DD
DODELINK L     R15,=V(REVDLNK)     POINT TO DELINK PROCESSOR ROUTINE
         BALR  R14,R15             CALL DELINK PROCESSOR
         SLR   R3,R3               FLAG DELINK COMPLETION PROCESSING
         TM    READR,X'80'         WAS DELINK UTILITY INVOKED?
         BZ    DELINKOK            YES, SHOW TASK COMPLETION CODE
         MVC   DIRHDG(L'DLALCMSG),DLALCMSG
         B     SAMEMEMS            DELINK WAS NOT RUN
DLORGBAD MVC   DIRHDG(L'DLORGMSG),DLORGMSG
         B     BELDSPLY            GO RING THE BELL


**********************************************************************
*                                                          *         *
*         TOGGLE TAG FLAG FOR ALL MEMBERS                  *  GP@FT  *
*                                                          *  10/96  *
**********************************************************************

TAGFLIP  SLR   R14,R14             FLIP ALL TAG BITS
         ST    R14,OFFLDCNT        RESET TAGGED MEMBER COUNTER
         LR    R1,R7               ALLOW HANDLING OF MEMBER CODES
         LA    R15,DIRENTS         POINT TO FIRST ENTRY
TAGLOOP  C     R15,LASTADDR        REACHED END OF MEMBER LIST?
         BNL   OFFDSNBK            YES, NOW LOOK AT MEMBER SELECTION(S)
         XI    13(R15),X'40'       NO, TOGGLE TAG FLAG
         TM    13(R15),X'40'       IS THE MEMBER NOW TAGGED?
         BZ    TAGCNTOK            NO, NOW UNTAGGED SO DO NOT COUNT
         LA    R14,1(,R14)         YES, INCREMENT COUNTER
         ST    R14,OFFLDCNT        UPDATE COUNTER IN CASE LAST TAG
TAGCNTOK AH    R15,14(,R15)        POINT TO NEXT MEMBER
         B     TAGLOOP             PROCESS NEXT MEMBER


**********************************************************************
*                                                          *         *
*         DISPLAY FROM THE NEXT TAGGED MEMBER              *  GP@FT  *
*                                                          *  10/96  *
**********************************************************************

FINDTAG  L     R3,TOPMEMAD         POINT TO THE CURRENT TOP ENTRY
         C     R3,LASTADDR         IS IT END-OF-DIRECTORY?
         BL    FNDTAGLP            NO
         LA    R3,DIRENTS          YES, POINT TO THE FIRST ENTRY
         B     FINDTAG1            HANDLE IF FIRST MEMBER TAGGED
FNDTAGLP AH    R3,14(,R3)          NO, POINT TO NEXT ENTRY
FINDTAG1 C     R3,LASTADDR         REACHED END OF DIRECTORY?
         BNL   THISMEM             YES, END CURRENT SCAN
         TM    13(R3),X'40'        IS THIS MEMBER TAGGED?
         BO    THISMEM             YES, TAGGED MEMBER FOUND
         B     FNDTAGLP            CONTINUE SCAN


**********************************************************************
*                                                          *         *
*         UNTAG ALL MEMBERS WITHOUT REFRESH FROM FILE      *  GP@FT  *
*                                                          *  10/96  *
**********************************************************************

TAGRESET SLR   R0,R0               RESET ALL TAG FLAGS
         ST    R0,OFFLDCNT         NO MEMBERS WILL BE TAGGED
         LA    R3,DIRENTS          POINT TO THE FIRST ENTRY
TAGRESLP C     R3,LASTADDR         END OF MEMBER LIST?
         BNL   SAMEMEMS            YES
         NI    13(R3),255-X'40'    RESET MEMBER TAG FLAG
         AH    R3,14(,R3)          NO, POINT TO NEXT ENTRY
         B     TAGRESLP            CONTINUE PASS


**********************************************************************
*                                                          *         *
*         DISPLAY DIRECTORY HELP PANEL                     *  GP@FT  *
*                                                          *  11/96  *
**********************************************************************

DIRHELP  LA    R0,2048
         SLL   R0,1                GET TPUT BUFFER SIZE
         ST    R0,LINKAREA         SAVE IT
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,LINKAREA+4       SAVE THE ADDRESS
         MVI   DIRFLAG,0
DIRHLPLP L     R5,LINKAREA+4       POINT TO THE TPUT BUFFER
         LA    R3,PODIRHLN         GET THE HELP SCREEN TEMPLATE SIZE
         L     R4,=A(PODIRHLP)     POINT TO HELP SCREEN TEMPLATE
         CLI   DIRFLAG,0           FIRST HELP PANEL?
         BE    HELPOTLP            YES
         LA    R3,POSELHLN         GET THE HELP SCREEN TEMPLATE SIZE
         L     R4,=A(POSELHLP)     POINT TO HELP SCREEN TEMPLATE
HELPOTLP CLI   0(R4),SBA           SBA ORDER?
         BE    HELPOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),RA            RA ORDER?
         BE    HELPOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),SA            SA ORDER?
         BE    HELPOTSA            YES, HANDLE EXTENDED DATA STREAM
         MVC   0(1,R5),0(R4)       COPY A BYTE TO THE TPUT BUFFER
         LA    R4,1(,R4)           INCREMENT SOURCE POINTER
         LA    R5,1(,R5)           INCREMENT TARGET POINTER
         BCT   R3,HELPOTLP         PROCESS NEXT BYTE
HELPREDO LR    R0,R5               POINT PAST END OF DATA STREAM
         L     R1,LINKAREA+4       POINT TO START OF DATA STREAM
         SR    R0,R1               GET LENGTH OF DATA STREAM
HELPTPUT ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           SHOW DATA ENTRY PANEL
         LA    R1,X'81'            LOAD TGET FLAGS
         SLL   R1,24               SHIFT TO TOP BYTE
         LA    R0,REPLY            GET INPUT BUFFER ADDRESS
         OR    R1,R0               PUT INTO CORRECT REGISTER
         LA    R0,L'REPLY          GET MAXIMUM INPUT LENGTH
         TGET  (1),(0),R           GET SELECTION
         CLI   REPLY,X'6E'         PA2 PRESSED OR OTHER RESHOW REQUEST?
         BE    HELPREDO            YES, SHOW SAME HELP PANEL AGAIN
         XI    DIRFLAG,1           FLAG NEXT HELP PANEL TO SHOW
         CLI   REPLY,X'7D'         ENTER PRESSED?
         BE    DIRHLPLP            YES, SHOW OTHER HELP PANEL
         LM    R0,R1,LINKAREA      LOAD FREEMAIN DETAILS
         FREEMAIN R,LV=(0),A=(1)   FREE THE TPUT BUFFER
         XC    LINKAREA(8),LINKAREA   ZERO LINK MACRO DCB ADDRESS
         B     SAMEMEMS            RETURN TO MEMBER LIST DISPLAY
HELPOTAD MVC   0(1,R5),0(R4)       COPY ORDER
         SLR   R1,R1
         IC    R1,1(,R4)           GET THE ROW NUMBER
         BCTR  R1,0                MAKE RELATIVE
         M     R0,SCRNCOLS         GET BYTES ON PRECEDING LINES
         IC    R0,2(,R4)           GET THE COLUMN NUMBER
         BCTR  R0,0                MAKE RELATIVE
         AR    R1,R0               GET SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,1(R5)          SUPPLY ADDRESS FOR THIS TERMINAL
         LA    R0,3
HELPADJS ALR   R4,R0               ADJUST SOURCE POINTER
         ALR   R5,R0               ADJUST TARGET POINTER
         SR    R3,R0               ADJUST BYTES TO GO
         B     HELPOTLP            HOPE R3 IS STILL POSITIVE
HELPOTSA MVC   0(3,R5),0(R4)       COPY COMPLETE SA ORDER STREAM
         LA    R0,3                GET BYTE COUNT INVOLVED
         SR    R3,R0               ADJUST BYTES TO GO
         ALR   R4,R0               ADJUST SOURCE POINTER
         TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    HELPOTLP            NO, DON'T COUNT THIS ORDER
         ALR   R5,R0               YES, ADJUST TARGET POINTER
         B     HELPOTLP            HOPE R3 IS STILL POSITIVE


         DROP  R10,R11             REVPODIR

         TITLE '  R E V P O D I R   -   S T A T I C   A R E A  '
LASTNAME DC    XL8'FFFFFFFFFFFFFFFF',3AL2(0),AL2(16)
PODIRHEX DC    C'0123456789ABCDEF'
REALUNKN DC    C'????????'
MODIFIED DC    C'MODIFIED'
UNMOVABL DC    C'UNMOVEABLE'
HWCNTMSG DC    C'HALFWORDS OF USER DATA'
INVCDMSG DC    C'INVALID SELECTION CODE '
INVPFMSG DC    C'INVALID PROGRAM FUNCTION KEY '
SCRERMSG DC    C'SCREEN INPUT ERROR - INPUT IGNORED '
DIRRDMSG DC    C'REGION FULL - DIRECTORY READ TERMINATED '
INRPTMSG DC    C'INTERRUPT - DIRECTORY SORT TERMINATED '
NOTEXMSG DC    C'EXACT MEMBER NAME NOT FOUND '
DATSTMSG DC    C'SORTED BY MODIFICATION DATE/TIME '
NAMSTMSG DC    C'SORTED BY NAME '
SIZSTMSG DC    C'SORTED BY SIZE '
SSISTMSG DC    C'SORTED BY SSI/VV.MM '
TTRSTMSG DC    C'SORTED BY TTR '
USRSTMSG DC    C'SORTED BY USERID '
NULSTMSG DC    C'NO ENTRIES TO SORT '
RECFMMSG DC    C'RECORD FORMAT NOT FIXED OR VARIABLE '
VIOPOMSG DC    C'EDIT INVALID - VIO LIBRARIES ARE NOT SUPPORTED '
OFFINVLN EQU   18                  LENGTH OF 'OFFLOAD INVALID - '
SYSUTMSG DC    C'OFFLOAD INVALID - FILE SYSUT2?? NOT PRE-ALLOCATED '
SYSUTDDN EQU   DIRHDG+23,8
NULOFMSG DC    C'NO MEMBERS TO PROCESS '
POMEMSTR EQU   NULOFMSG+3,8        C'MEMBERS '
OFFALMSG DC    C'OFFLOADING ALL REAL MEMBERS AND ALIASES '
OFFSLMSG DC    C'OFFLOADING SELECTED REAL MEMBERS AND THEIR ALIASES '
OFFLDMSG DC    C'OFFLOAD COMPLETE - SYSUT2   CLOSED '
OFFLDDDN EQU   DIRHDG+19,8
OFFINMSG DC    C'OFFLOAD INTERRUPTED - SYSUT2   CLOSED '
OFFINDDN EQU   DIRHDG+22,8
SRCHDMSG DC    C'SEARCH COMPLETE - 123456789 MEMBERS MATCHED '
SRCHIMSG DC    C'SEARCH INTERRUPTED '
SRCHCMSG DC    C'SEARCH CANCELLED '
EDITMMSG DC    C'USE LOCATE FIELD TO SUPPLY MEMBER NAME FOR EDIT '
EDIT0MSG DC    C'REQUEST FOR MEMBER EDIT NOW CANCELLED '
SAVEDMSG DC    C'MEMBER SAVED '
EMPTYMSG DC    C'EMPTY MEMBER '
INUSEMSG DC    C'MEMBER NOT FOUND (BROWSE) OR IN-USE (EDIT) '
SPFERMSG DC    C'ISPF ERROR ## '
REVDDMSG DC    C'REVIEW OF DDNAME '
SETDDMSG DC    C'USE LOCATE FIELD TO SUPPLY NEW OUTPUT DDNAME '
REPDDMSG DC    C'SYSUT2   REPLACED BY NEWDDNAM FOR OUTPUT '
REPDDDDN EQU   DIRHDG+21,8
NOREPMSG DC    C'CANCELLED - OUTPUT FILE IS STILL SYSUT2?? '
NOREPDDN EQU   DIRHDG+33,8
UTLCCMSG DC    C'UTILITY PROGRAM COND CODE #### '
UTLCCNUM EQU   DIRHDG+26,4
UTLABMSG DC    C'UTILITY PROGRAM ABENDED - S### '
UTLABNUM EQU   DIRHDG+27,3
DLORGMSG DC    C'DELINK REQUIRES NON-PDSE PDS WITH RECFM=U '
DLALCMSG DC    C'DELINK NOT RUN - ALLOCATION FAILED '
EODIRFLG DC    CL9'  **END**'
BLNKREST DCS   RTA,(1,1),C' '
ED5      DC    X'402020202120'
BLKSREAD DC    C' DIRECTORY BLOCKS READ '
ED9      DC    X'40202020202020202120'
DIRGMLIM DC    F'8192',F'786432'
GMVC     GETMAIN VC,MF=L
GMVCL    EQU   *-GMVC
SYSUT2   DC    CL8'SYSUT2  '

         LTORG

         PRINT NOGEN

         DS    0F
DIREXLST DC    X'05',AL3(DIROPNEX)
         DC    X'11',AL3(DIRABEND)
         DC    X'94',AL3(UDA)
STDIRDCB DCB   DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=256,                  +
               DDNAME=DYNAM,BUFNO=16,EODAD=EOPDSDIR,EXLST=DIREXLST
*                        (4K OF BUFFERS)
DIRDCBL  EQU   *-STDIRDCB

         PRINT GEN


HLPMSGAT DCS   SF,PROLO,SA,COLOUR,BLUE
COLHDATR DCS   SF,PROHI,SA,COLOUR,YELLOW
NAMENAME DC    CL4'NAME'
SPFHDR   DC    C'TTR     '
         DC    C'VV.MM  CREATED  LAST MODIFIED  SIZE  INIT   MOD   ID'
SPFHDLEN EQU   *-SPFHDR
RECFMUHD DC    C'SSI       SIZE   TTR   ALIAS-OF AC -- -- -- -ATTRIBUTE+
               S -- -- ---'
RFUHDLEN EQU   *-RECFMUHD
POMEMVOL DC    X'6020202020202120',C' MEMBERS ON VIO --'
MEMVOLLN EQU   *-POMEMVOL
POLOCPTR DC    CL12' LOCATE ===>'
         DCS   SF,UNPHI,SA,COLOUR,RED,SA,HILITE,USCORE
POKEYATR DCS   SF,PROLO,SA,HILITE,NORMAL,SA,COLOUR,TURQ
POKEYKEY DC    C'1-TOP 2-BOT 3-END 4-SD 6-SN 7-UP 8-DN 9-SSZ 10-STTR 11+
               -SID 12-SSSI/VV.MM 5-SHOW-DIRECTORY-SCANNED-BLOCK-COUNT'
POTAGKEY DC    C'1-TOP 2-BOT 3-END 4-SD 5-FIND-NEXT-TAGGED 6-SN 7-UP 8-+
               DOWN 9-SORT/SIZE 10-SORT/TTR 11-SORT/ID 12-SORT/SSIV.M'
POHLPMSG DC    C'ENTER "=HELP" IN LOCATE FIELD FOR HELP'
POHL     EQU   *-POHLPMSG
         TITLE '  R E V P O D I R  '
**********************************************************************
*                                                          *         *
*         CONVERT DATE FROM P'YYDDD' TO C'YY/MM/DD'        * GP@SECV *
*                                                          *  03/86  *
**********************************************************************

SPFDATE  BALR  R15,0                    (FROM TAPESCAN DATE SUBROUTINE)
         USING IDRDATE,R15
IDRDATE  ST    R4,MSG+100+4                STORE INPUT DATE FOR TESTING
         ST    R4,MSG+100+8                  AND CONVERSION
         UNPK  MSG+100+12(3),MSG+100+5(2)  FORMAT YEAR
         XC    MSG+100(6),MSG+100          GET JULIAN DAY IN DECIMAL
         CVB   R4,MSG+100                  GET JULIAN DAY IN BINARY
         LA    R5,MNTHTBL-4                ADJUST FOR FIRST INCREMENT
         TM    MSG+100+9,X'01'             IF ODD THEN
         BO    NOTLEAP                                 NOT LEAP YEAR
         TM    MSG+100+9,X'12'             TEST FOR LEAP (OK TILL 2099)
         BNM   NEXTMNTH                       IF MIXED NOT LEAP YEAR
NOTLEAP  CH    R4,=H'60'                   DDD AFTER 28TH FEBRUARY?
         BL    NEXTMNTH                    NO, LEAP YEAR IRRELEVANT
         LA    R4,1(,R4)                   YES, FUDGE DDD ACCORDINGLY
NEXTMNTH LA    R5,4(,R5)                   INCREMENT THRU MONTH TABLE
         SH    R4,0(,R5)                   DECREASE NUMBER OF DAYS
         BP    NEXTMNTH                    NOT YET, TRY NEXT MONTH
         AH    R4,0(,R5)                   FOUND THE MONTH
         CVD   R4,MSG+100                  GET DAY OF MONTH
         OI    MSG+100+7,X'0F'             MAKE UNPK RESULT PRINTABLE
         UNPK  MSG+100+18(2),MSG+100+6(2)  FORMAT DAY OF MONTH
         MVI   MSG+100+14,C'/'             INSERT THE SLASH
         MVC   MSG+100+15(2),2(R5)         MOVE IN MONTH NUMBER
         MVI   MSG+100+17,C'/'             INSERT THE SLASH
         MVC   DOUBLE,MSG+100+12           RETURN THE ANSWER
         BR    R14                         RETURN

         DROP  R15                         IDRDATE

MNTHTBL  DC    H'31'                       JAN
         DC    C'01'
         DC    H'29'                       FEB
         DC    C'02'
         DC    H'31'                       MAR
         DC    C'03'
         DC    H'30'                       APR
         DC    C'04'
         DC    H'31'                       MAY
         DC    C'05'
         DC    H'30'                       JUN
         DC    C'06'
         DC    H'31'                       JUL
         DC    C'07'
         DC    H'31'                       AUG
         DC    C'08'
         DC    H'30'                       SEP
         DC    C'09'
         DC    H'31'                       OCT
         DC    C'10'
         DC    H'30'                       NOV
         DC    C'11'
         DC    H'255'                      DEC (ALLOW FOR STUPID DDD)
         DC    C'12'


**********************************************************************
*                                                          *         *
*         DIRECTORY OPEN ABEND EXIT                        *  GP@FT  *
*                                                          *  01/96  *
**********************************************************************

DIRABEND DS    0H
         TM    3(R1),B'00001110'   ANY PROCESSING ALLOWED?
         BZR   R14                 NO
         OI    STATUS,STABEND      INDICATE OPEN ABENDED
         MVC   BLKREADS,0(R1)      COPY ABEND CODE
         MVI   3(R1),4             IGNORE THE ABEND
         BR    R14


**********************************************************************
*                                                          *         *
*         ENCODE SCREEN LOCATION TO 3270 BUFFER ADDRESS    *  GP@FT  *
*                                                          *  01/93  *
**********************************************************************

*              INPUT - R1 = RELATIVE BYTE LOCATION
*             OUTPUT - R1 = TWO-BYTE DATA STREAM ADDRESS (LOW H/W)

         USING CALCPOSI,R15
CALCPOSI DS    0H
         CLM   R1,X'3',MAX12BIT    LOCATION GREATER THAN 4K (12 BITS)?
         BHR   R14                 YES, NO CONVERSION TO BE DONE
         STC   R1,DOUBLE+1         NO, DO ORIGINAL 3270 ADDRESSING
         NI    DOUBLE+1,X'3F'      GET LOW-ORDER SIX-BIT NUMBER
         SRL   R1,6
         STC   R1,DOUBLE           GET HIGH-ORDER SIX-BIT NUMBER
         TR    DOUBLE(2),TABLE     CONVERT TO 3270 DATA STREAM CHARS
         ICM   R1,B'0011',DOUBLE   SAVE IN BOTTOM TWO BYTES OF REGISTER
         BR    R14                 RETURN TO CALLER
         DROP  R15                 CALCPOSI

MAX12BIT DC    H'4095'             MAXIMUM 12-BIT ADDRESSING LOCATION
TABLE    DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'


**********************************************************************
*                                                          *         *
*         DETERMINE SCROLL AMOUNT OVERRIDE                 *  GP@FT  *
*                                                          *  09/96  *
**********************************************************************

*             OUTPUT - R1 = SCROLL AMOUNT OVERRIDE (-VE FOR CSR)

         USING DIRSCROL,R15
DIRSCROL DS    0H
         LA    R0,3                GET LENGTH OF WHOLE SBA ORDER
         L     R1,TGETREGS+4       GET THE INPUT DATA STREAM LENGTH
         LA    R3,REPLY            POINT TO INPUT BUFFER
         B     DIRSCRNF            SKIP AID AND CURSOR LOCATION
DIRSCRLP CLI   0(R3),SBA           SET BUFFER ADDRESS ORDER?
         BE    DIRSCRNF            YES, NEW INPUT FIELD FOUND
         CLI   0(R3),C' '          LEADING BLANK?
         BNE   DIRSCRAM            NO, SEE IF SCROLL AMOUNT
         LA    R3,1(,R3)           YES, POINT PAST BLANK
         BCT   R1,DIRSCRLP         CONTINUE SCAN FOR NON-BLANK TEXT
         BR    R14                 RETURN IF NO TEXT PRESENT
DIRSCRNF ALR   R3,R0               POINT PAST SBA ORDER
         SR    R1,R0               REDUCE LENGTH-TO-GO
         BP    DIRSCRLP            CONTINUE SCAN FOR NON-BLANK TEXT
         BR    R14                 RETURN IF NO TEXT PRESENT
DIRSCRAM CLI   0(R3),C'0'          NUMERIC TEXT?
         BNL   DIRSCRNM            YES
         OI    0(R3),C' '          NO, ENSURE ANY LETTER IN UPPER CASE
         LR    R1,R3               SET SPECIAL CASE INDICATOR
         CLI   0(R3),C'M'          MAXMIMUM SCROLLING?
         BER   R14                 YES, RETURN
         LNR   R1,R3               SET NEGATIVE
         CLI   0(R3),C'C'          CURSOR SCROLLING?
         BER   R14                 YES, RETURN
         L     R1,SCRNLNES         GET SCREEN LINE COUNT
         SR    R1,R0               ACCOUNT FOR HEADINGS
         CLI   0(R3),C'P'          PAGE SCROLLING?
         BER   R14                 YES, RETURN
         SRA   R1,1                HALVE IT
         CLI   0(R3),C'H'          HALF-PAGE SCROLLING?
         BER   R14                 YES, RETURN
         SLR   R1,R1               IRRELEVANT TEXT SO IGNORE
         BR    R14                 RETURN TO CALLER
DIRSCRNM SLR   R1,R1               CLEAR ACCUMULATOR
DIRSCRNL CLI   0(R3),C'0'          NUMERIC CHARACTER?
         BLR   R14                 NO, END OF NUMBER
         IC    R0,0(,R3)           YES, GET DIGIT
         MH    R1,DIRSCR10         PROMOTE PREVIOUS DIGITS
         SLL   R0,28
         SRL   R0,28               SHIFT OUT ZONE
         AR    R1,R0               ADD TO ACCUMULTOR
         LA    R3,1(,R3)           POINT TO NEXT BYTE IN INPUT BUFFER
         B     DIRSCRNL            CONTINUE MEMBER SCROLL NUMBER LOOP
         DROP  R15                 DIRSCROL

DIRSCR10 DC    H'10'               TEN
         EJECT
**********************************************************************
*                                                          *         *
*         FORMAT MEMBER DIRECTORY ENTRY SCREEN LINE        * GP@SECV *
*                                                          *  03/86  *
**********************************************************************

         USING REVPODIR,R10,R11
         USING DIRFRMAT,R7
DIRFRMAT DS    0H                  FORMAT A MEMBER DIRECTORY ENTRY
         TM    STATUS5,X3270       USING 3270 EXTENSIONS?
         BZ    LEADEROK            NO
         MVC   0(3,R2),=X'2842F1'  YES, BLUE FOR "LEADER DOTS"
         LA    R2,3(,R2)           COUNT EXTRA ATTRIBUTE BYTES
LEADEROK MVC   0(8,R2),MEMATLST    LOAD 3270 ATTRIBUTES TEMPLATE
         TM    13(R3),X'40'        IS THIS MEMBER TAGGED?
         BZ    LOADMEMN            NO, LEAVE AS LOW INTENSITY
         MVI   4(R2),PROHI         YES, MAKE IT HIGH INTENSITY
         OI    7(R2),GREEN         ADD GREEN SO PINK->WHITE RED->YELLOW
LOADMEMN TM    STATUS5,X3270       USING 3270 EXTENSIONS?
         BZ    MEMATTRS            NO
         LA    R2,3(,R2)           YES, COUNT EXTRA ATTRIBUTE BYTES
MEMATTRS LA    R2,2(,R2)           COUNT FIELD ATTRIBUTE BYTES
         MVC   3(8,R2),0(R3)       LOAD MEMBER NAME
         MVI   11(R2),C' '
         MVC   12(149,R2),11(R2)   BLANK REST OF LINE
         TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   ALIASTST            NO, SO CANNOT BE A LOAD LIBRARY
         UNPK  30(7,R2),10(4,R3)   SHOW TTR IN HEX
         TR    30(6,R2),PODIRHEX-C'0'
         MVI   36(R2),C' '         ERASE RUBBISH
         CLI   15(R3),24           IMS FORMAT LIBRARY MEMBER?
         BE    PROGSIZE            YES, SHOW THE BYTE COUNT
         CLI   15(R3),34           IMS FORMAT LIBRARY MEMBER?
         BE    PROGSIZE            YES, SHOW THE BYTE COUNT
         CLI   15(R3),32           STANDARD LOAD MODULE ENTRY?
         BE    REALPGM             YES, USE IT
         CLI   15(R3),44           ALIAS LOAD MODULE ENTRY WITH SSI?
         BE    PGMSSI              YES, USE IT
         CLI   15(R3),36           SSI LOAD MODULE ENTRY?
         BNE   ALIASTST            NO, CHECK FOR ALIAS
PGMSSI   UNPK  13(9,R2),30(5,R3)   YES, FORMAT THE SSI
         TR    13(8,R2),PODIRHEX-C'0'
         MVI   21(R2),C' '         ERASE RUBBISH
         CLI   15(R3),36           SSI LOAD MODULE ENTRY?
         BE    REALPGM             YES, PROCEED
         MVC   37(8,R2),34(R3)     NO, LOAD ORIGINAL REAL MEMBER NAME
         B     ALIASPGM            CONTINUE WITH ALIAS PROCESSING
REALPGM  TM    27(R3),X'03'        TEST MEMBER ADDRESSING MODE
         BZ    FMTPGM              DEFAULT, NO AMODE SPECIFIED
         MVC   76(3,R2),=C'ANY'
         BO    FMTPGM              AMODE ANY SPECIFIED
         MVC   77(2,R2),=C'31'
         TM    27(R3),X'02'
         BO    FMTPGM              AMODE 31 SPECIFIED
         MVC   77(2,R2),=C'24'     AMODE 24 SPECIFIED
         B     FMTPGM
ALIASTST TM    13(R3),X'80'        IS THIS MEMBER AN ALIAS?
         BZ    MEMNOTAL            NO
         CLI   15(R3),40           ALIAS WITH LOAD MODULE DATA?
         BNE   ALIASMEM            NO, BUT IT IS STILL AN ALIAS
         MVC   37(8,R2),30(R3)     YES, LOAD ORIGINAL REAL MEMBER NAME
ALIASPGM TM    27(R3),X'0C'        TEST ALIAS ADDRESSING MODE
         BZ    FMTPGM              DEFAULT, NO AMODE SPECIFIED
         MVC   76(3,R2),=C'ANY'
         BO    FMTPGM              AMODE ANY SPECIFIED
         MVC   77(2,R2),=C'31'
         TM    27(R3),X'08'
         BO    FMTPGM              AMODE 31 SPECIFIED
         MVC   77(2,R2),=C'24'     AMODE 24 SPECIFIED
FMTPGM   TM    26(R3),X'41'        OSIV/F4 AE PROGRAM?
         BZ    NOTAEPGM            NO, DO NOT OVERWRITE AMODE DETAILS
         MVC   76(3,R2),=C'AE '    YES, INDICATE THIS
NOTAEPGM CLI   15(R3),36           SSI LOAD MODULE ENTRY?
         BE    HEXSIZE             YES, SHOW SSI, NOT DECIMAL SIZE
         CLI   15(R3),44           ALIAS LOAD MODULE ENTRY WITH SSI?
         BE    HEXSIZE             YES, SHOW SSI, NOT DECIMAL SIZE
PROGSIZE SLR   R6,R6               CLEAR FOR INSERT
         ICM   R6,B'0111',18(R3)   GET LOAD MODULE SIZE
         LA    R6,1023(,R6)        ROUND UP TO NEXT KILOBYTE
         SRL   R6,10               DIVIDE BY 1024
         CVD   R6,DOUBLE
         MVC   15(6,R2),ED5
         ED    15(6,R2),DOUBLE+5
         MVI   21(R2),C'K'
HEXSIZE  UNPK  23(7,R2),18(4,R3)   SHOW SIZE IN HEX
         TR    23(6,R2),PODIRHEX-C'0'
         MVI   29(R2),C' '         ERASE RUBBISH
         CLI   15(R3),24           IMS FORMAT LIBRARY MEMBER?
         BE    MEMLODED            YES, THAT IS ALL THE DATA TO SHOW
         CLI   15(R3),34           IMS FORMAT LIBRARY MEMBER?
         BE    CREATIME            YES, SHOW CREATION TIMESTAMP
         TM    26(R3),X'80'        PROCESSED BY OS/VS LINKAGE EDITOR?
         BZ    DONEAUTH            NO, OS LOAD MODULE
         TM    26(R3),X'08'        IS APF DATA VALID?
         BZ    DONEAUTH            NO
         MVC   46(2,R2),REALUNKN   DENOTE INVALID APF DATA
         CLI   28(R3),1            IS APF DATA LENGTH ONE?
         BNE   DONEAUTH            NO, INVALID
         UNPK  46(3,R2),29(2,R3)   NO, SHOW AUTHORIZATION CODE IN HEX
         TR    46(2,R2),PODIRHEX-C'0'
         MVI   48(R2),C' '         ERASE RUBBISH
DONEAUTH MVC   49(2,R2),=C'DC'     DOWNWARD COMPATIBLE
         TM    17(R3),X'80'
         BZ    *+10
         MVC   49(2,R2),=C'FO'     F-LEVEL LINKAGE EDITOR ONLY
         TM    17(R3),X'08'
         BZ    *+10
         MVC   52(2,R2),=C'NE'     NOT EDITABLE
         TM    16(R3),X'02'
         BO    *+10
         MVC   52(2,R2),=C'NX'     NOT EXECUTABLE
         TM    16(R3),X'08'
         BZ    *+10
         MVC   55(2,R2),=C'OL'     ONLY LOADABLE
         TM    26(R3),X'20'
         BZ    *+10
         MVC   58(2,R2),=C'PG'     PAGE ALIGNMENT
         TM    16(R3),X'20'
         BZ    *+10
         MVC   58(2,R2),=C'OV'     OVERLAY STRUCTURE
         TM    17(R3),X'01'
         BZ    *+10
         MVC   61(2,R2),=C'RF'     REFRESHABLE
         TM    16(R3),X'80'
         BZ    *+10
         MVC   64(2,R2),=C'RN'     REENTERABLE
         TM    16(R3),X'40'
         BZ    *+10
         MVC   67(2,R2),=C'RU'     REUSABLE
         TM    16(R3),X'04'
         BZ    *+10
         MVC   70(2,R2),=C'SC'     SCATTER FORMAT
         TM    17(R3),X'04'
         BZ    *+10
         MVC   73(2,R2),=C'TS'     TEST SYMBOL CARDS
         TM    17(R3),X'20'        ENTRY POINT AT OFFSET ZERO?
         BZ    GETPGMEP            NO
         CLC   52(4,R2),=CL8' '    YES, GOT 4 BLANKS?
         BNE   GOTPGMEP            NO, DON'T SHOW ENTRY POINT DATA
         MVC   52(3,R2),=C'EP0'    YES
         B     GOTPGMEP
GETPGMEP CLC   52(6,R2),=CL8' '    GOT 6 BLANKS?
         BNE   GOTPGMEP            NO, DON'T SHOW ENTRY POINT DATA
         LA    R0,4095
         CLM   R0,7,23(R3)         ENTRY POINT OFFSET LESS THAN 4096?
         BL    GETBIGEP            NO
         MVC   52(2,R2),=C'EP0'    YES (REUSE LITERAL)
         UNPK  DOUBLE(5),24(3,R3)  SHOW ENTRY POINT OFFSET
         MVC   54(3,R2),DOUBLE+1
         TR    54(3,R2),PODIRHEX-C'0'
         B     GOTPGMEP
GETBIGEP CLC   52(8,R2),=CL8' '    GOT 8 BLANKS?
         BNE   GOTPGMEP            NO, DON'T SHOW ENTRY POINT DATA
         MVC   52(2,R2),=C'EP0'    YES (REUSE LITERAL)
         UNPK  54(7,R2),23(4,R3)   SHOW ENTRY POINT OFFSET
         TR    54(6,R2),PODIRHEX-C'0'
         MVI   60(R2),C' '
GOTPGMEP TM    27(R3),X'10'        RESIDENCE MODE ANY?
         BZ    MEMLODED            NO, END OF LOAD MODULE DATA CODE
         IC    R0,2(,R2)           SAVE A COLOUR CODE MAYBE
         ICM   R14,15,OFFLDCNT     ANY MEMBERS TAGGED?
         BNZ   *+8                 YES, HIGHLIGHT TAGGED MEMBERS ONLY
         MVI   2(R2),PROHI         MAKE IT HIGH INTENSITY
         TM    STATUS5,X3270       USING 3270 EXTENSIONS?
         BZ    MEMLODED            NO, END OF LOAD MODULE DATA CODE
         STC   R0,2(,R2)           RESTORE THE COLOUR CODE
         NI    2(R2),X'FE'         NO BLUE SO PINK->RED & WHITE->YELLOW
         LTR   R14,R14             ANY MEMBERS TAGGED?
         BNZ   MEMLODED            YES, HIGHLIGHT TAGGED MEMBERS ONLY
         BCTR  R2,0                POINT TO ATTRIBUTE BYTE
         MVI   0(R2),PROHI         MAKE IT HIGH INTENSITY
         LA    R2,1(,R2)           RESTORE POINTER
         B     MEMLODED            END OF LOAD MODULE DATA CODE
MEMNOTAL CLI   15(R3),46           DOES MEMBER HAVE SPFD STATISTICS?
         BE    SHOWTTR             YES, FORMAT THEM
         TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BO    USERDATA            YES, ALREADY FORMATTED THE TTR
SHOWTTR  UNPK  15(7,R2),10(4,R3)   SHOW TTR IN HEX
         TR    15(6,R2),PODIRHEX-C'0'
         MVI   21(R2),C' '         ERASE RUBBISH
         CLI   9(R3),0             MEMBER FROM CONCATENATION?
         BE    USERDATA            NO, PERFORM COMMON USERDATA LOGIC
         UNPK  12(3,R2),9(2,R3)    YES, SHOW RELATIVE NUMBER IN HEX
         TR    12(2,R2),PODIRHEX-C'0'
         MVI   14(R2),C'+'         ERASE RUBBISH AND SHOW CONCAT'N
         B     USERDATA            PERFORM COMMON USERDATA LOGIC
ALIASMEM MVC   14(5,R2),=C'ALIAS'  NO, DENOTE ALIAS
USERDATA TM    13(R3),B'00011111'  DOES THE MEMBER HAVE ANY USER DATA?
         BZ    MEMLODED            NO, NOTHING ELSE TO FORMAT
         CLI   15(R3),46           DOES MEMBER HAVE SPFD STATISTICS?
         BE    SPFDMEM             YES, FORMAT THEM
         CLI   15(R3),22           DOES MEMBER HAVE SSI?
         BE    SSIMEM              YES, DISPLAY IT
         MVC   DOUBLE(1),13(R3)    NO, GET USER DATA LENGTH CODE BYTE
         NI    DOUBLE,B'00011111'  TURN OFF ALIAS AND TTR COUNT BITS
         IC    R0,DOUBLE           LOAD USER DATA HALFWORD COUNT
         CVD   R0,DOUBLE
         MVC   38(4,R2),ED5
         ED    38(4,R2),DOUBLE+6   DISPLAY (NON-ZERO) HALFWORD COUNT
         MVC   43(L'HWCNTMSG,R2),HWCNTMSG
         B     MEMLODED            NOTHING ELSE TO FORMAT
SSIMEM   MVC   40(4,R2),SSILABEL
         UNPK  45(9,R2),16(5,R3)   SSI IS FOUR BYTES LONG
         TR    45(8,R2),PODIRHEX-C'0'
         MVI   53(R2),C' '         ERASE RUBBISH
         TM    13(R3),B'00011110'  WAS THE USER DATA HALFWORD COUNT 1?
         BNZ   MEMLODED            NO, MUST HAVE BEEN 2 SO NORMAL SSI
         MVC   49(4,R2),53(R2)     YES, ERASE DATA OF SECOND HALFWORD
         B     MEMLODED            NOTHING ELSE TO FORMAT
SPFDMEM  IC    R0,16(,R3)          SHOW VERSION NUMBER
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  24(2,R2),DOUBLE+6(2)
         MVI   26(R2),C'.'
         IC    R0,17(,R3)          SHOW MODIFICATION NUMBER
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  27(2,R2),DOUBLE+6(2)
         TM    18(R3),X'80'        SCLM FLAG ON?
         BZ    SCLMOK              NO
         MVI   22(R2),C'='         YES, DENOTE THIS
SCLMOK   ICM   R4,B'1111',20(R3)   GET DATE FULLWORD
         BAL   R14,SPFDATE         CONVERT TO YY/MM/DD
         MVC   30(8,R2),DOUBLE     SHOW CREATION DATE
         ICM   R4,B'1111',24(R3)   GET DATE FULLWORD
         BAL   R14,SPFDATE         CONVERT TO YY/MM/DD
         MVC   39(8,R2),DOUBLE     SHOW LAST MODIFIED DATE
         UNPK  48(3,R2),28(2,R3)   SHOW LAST MODIFIED HOURS
         MVI   50(R2),C':'
         UNPK  51(3,R2),29(2,R3)   SHOW LAST MODIFIED MINUTES
         LA    R5,53(,R2)          LOAD DISPLAY LINE POINTER
         LA    R6,30(,R3)          LOAD USER DATA POINTER
         LA    R15,3               DISPLAY SIZE, INIT, AND MOD
         SLR   R8,R8               HANDLE COUNTS OVER 32767
ED5LOOP  MVC   0(6,R5),ED5         LOAD EDIT PATTERN
         ICM   R8,B'0011',0(R6)    GET RECORD COUNT TO BE DISPLAYED
         CVD   R8,DOUBLE           MAKE IT PACKED DECIMAL
         ED    0(6,R5),DOUBLE+5    MAKE IT NUMERIC CHARACTERS
         LA    R5,6(,R5)           POINT TO NEXT DISPLAY AREA
         LA    R6,2(,R6)           POINT TO NEXT RECORD COUNTER
         BCT   R15,ED5LOOP         MAKE R15 ZERO FOR LATER INSERT
         MVC   72(8,R2),36(R3)     SHOW USERID
         CLI   16(R3),100          PHONY VERSION NUMBER FOR IMS?
         BNE   MEMLODED            NO, GENUINE STATS
         CLI   72(R2),C'('         AS EXPECTED?
         BNE   MEMLODED            NO
         CLI   79(R2),C')'         AS EXPECTED?
         BNE   MEMLODED            NO
         CLI   17(R3),5            AS EXPECTED?
         BH    MEMLODED            NO
         IC    R15,17(,R3)         GET REFERAL MEMBER TYPE BYTE
         SLL   R15,2               MULTIPLY BY 4 TO GET INDEX
         LA    R15,RFRLTYPE(R15)   POINT TO DESCRIPTION
         MVC   67(4,R2),0(R15)     SUPPLY REFERAL MEMBER TYPE TEXT
         CLI   17(R3),3            FORMAT?
         BE    CLRREFID            YES, DON'T SHOW MEMBER NAME AGAIN
         CLI   73(R2),C' '         BLANKS?
         BNE   MEMLODED            NO, DISPLAY USEFUL INFORMATION
CLRREFID MVC   72(8,R2),71(R2)     YES, BLANK OUT USELESS "USERID"
MEMLODED L     R14,=V(FULLT)       POINT TO TRANSLATE TABLE
         TR    3(77,R2),0(R14)     HANDLE STRANGE MEMBER NAMES/USERIDS
         A     R2,SCRNCOLS         POINT TO NEXT DETAIL SCREEN LINE
         AH    R3,14(,R3)          POINT TO NEXT MEMBER NAME
         BCT   R1,LODMEMNM
         B     CMPRSDIR
CREATIME ICM   R4,B'1111',24(R3)   GET DATE FULLWORD
         BAL   R14,SPFDATE         CONVERT TO YY/MM/DD
         MVC   49(8,R2),DOUBLE     SHOW LAST MODIFIED DATE
         UNPK  58(3,R2),28(2,R3)   SHOW LAST MODIFIED HOURS
         MVI   60(R2),C':'
         UNPK  61(3,R2),29(2,R3)   SHOW LAST MODIFIED MINUTES
         MVI   63(R2),C' '         ERASE GARBAGE
         TM    13(R3),14           ACBLIB MEMBER?
         BNO   MEMLODED            NO, THAT IS ALL THE DATA TO SHOW
         ICM   R4,8,21(R3)         YES, LOAD FLAG BYTE
         SRL   R4,4                CONVERT
         STCM  R4,8,DOUBLE              B'00XX0XXX'
         SLL   R4,1                     TO
         ICM   R4,8,DOUBLE              B'XXXXX000'
         SLL   R4,6
         LA    R0,5                MAXIMUM OF FIVE BITS TO TEST
         LA    R5,ACBFLAGS         POINT TO FIRST DISPLAY FLAG
ACBFLGLP LTR   R4,R4               IS SIGN BIT ON?
         BM    ACBFLGOK            YES
         SLL   R4,1                NO, SHIFT IT OUT
         LA    R5,4(,R5)           POINT TO NEXT DISPLAY FLAG
         BCT   R0,ACBFLGLP         TEST NEXT BIT
ACBFLGOK MVC   64(4,R2),0(R5)      DISPLAY ACBLIB MEMBER TYPE
         B     MEMLODED            THAT IS ALL THE DATA TO SHOW

         DROP  R7                  DIRFRMAT


MEMATLST DCS   SF,UNPHI,C'.',SF,PROLO,SA,COLOUR,PINK
RFRLTYPE DC    C' NEW TBL DFM FMTMSGIMSGO'
ACBFLAGS DC    C'DEDBMSDBSHR1SHR DMB PSB '
SSILABEL DC    C'SSI:'

         LTORG
         EJECT
**********************************************************************
*                                                          *         *
*         DIRECTORY DISPLAY SUBCOMMAND TABLE               *  GP@FT  *
*                                                          *  10/96  *
**********************************************************************

DIRCMDTB DS    0H                 ENTRIES MUST BE IN COLLATING SEQUENCE
         DC    CL8'=?      '
         B     DIRHELP
         DC    CL8'=C      '
         B     DIRCAN
         DC    CL8'=CAN    '
         B     DIRCAN
         DC    CL8'=CANCEL '
         B     DIRCAN
         DC    CL8'=DELINK '
         B     PODELINK
         DC    CL8'=DL     '
         B     PODELINK
         DC    CL8'=E      '
         B     ZEDITCHK
         DC    CL8'=EDIT   '
         B     ZEDITCHK
         DC    CL8'=EDITMEM'
         B     ZEDITCHK
         DC    CL8'=END    '
         B     DIREXIT
         DC    CL8'=EXIT   '
         B     DIREXIT
         DC    CL8'=F      '
         B     POSEARCH
         DC    CL8'=FIND   '
         B     POSEARCH
         DC    CL8'=HELP   '
         B     DIRHELP
         DC    CL8'=OFF    '
         B     OFFLDCHK
         DC    CL8'=OFFLOAD'
         B     OFFLDCHK
         DC    CL8'=PDS    '
         B     PDSUPDTE
         DC    CL8'=PDSLOAD'
         B     PDSUPDTE
         DC    CL8'=REF    '
         B     RDYRFRSH
         DC    CL8'=REFRESH'
         B     RDYRFRSH
         DC    CL8'=RES    '
         B     TAGRESET
         DC    CL8'=RESET  '
         B     TAGRESET
         DC    CL8'=SEARCH '
         B     POSEARCH
         DC    CL8'=SEQ    '
         B     SEQLDCHK
         DC    CL8'=SEQLOAD'
         B     SEQLDCHK
         DC    CL8'=SETFILE'
         B     SETOFFDD
         DC    CL8'=SF     '
         B     SETOFFDD
         DC    CL8'=TAGFLIP'
         B     TAGFLIP
         DC    CL8'=TF     '
         B     TAGFLIP
         DC    CL8'=X      '
         B     DIREXIT
         DC    H'-1'               END OF TABLE
         EJECT
**********************************************************************
*                                                          *         *
*         READ AND STORE DIRECTORY ENTRY DATA              * GP@SECV *
*                                                          *  03/86  *
**********************************************************************

         USING READDIR,R7
         USING IHADCB,DIRDCB
READDIR  DS    0H                  PROCESS PDS DIRECTORIES
         MVC   DCBDDNAM,$DDNAME    LOAD FILE NAME INTO DCB
         SLR   R8,R8               INITIALIZE MEMBER COUNTER
         ST    R8,OFFLDCNT         INITIALIZE OFFLOAD FLAG COUNT
         LA    R3,DIRENTS          POINT TO FIRST ENTRY
         ST    R3,TOPMEMAD         IN CASE OF SORT FOR CONCATENATED MEM
         SLR   R6,R6               CLEAR WORK REGISTER
         ST    R6,EXTPGMSZ         INITIALIZE PROGRAM SIZE ACCUMULATOR
         ST    R6,TOTPGMSZ         INITIALIZE PROGRAM SIZE ACCUMULATOR
         ST    R6,TOTSPFSZ         INITIALIZE EDIT RECORDS ACCUMULATOR
         ST    R6,BLKREADS         INITIALIZE BLOCK READ COUNTER
         STH   R6,CONCTOPN         ZERO CONCATENATED FILE OPEN COUNT
OPENDIR  MVI   OPEND,X'80'         OPEN PDS DIRECTORY
         OPEN  (DIRDCB,INPUT),MF=(E,OPEND)
         BAS   R14,OPENDIRX        CHECK THE SUCCESS OF THE OPEN
READBLK  GET   DIRDCB,REPLY        READ A DIRECTORY BLOCK
         LA    R2,1
         A     R2,BLKREADS         INCREMENT BLOCK READ COUNTER
         ST    R2,BLKREADS         SAVE NEW BLOCK READ COUNTER VALUE
         LH    R2,REPLY            GET BLOCK USED-BYTE-COUNT
         CH    R2,=H'14'           USED BYTE COUNT LESS THAN MINIMUM?
         BL    READBLK             YES, IGNORE BLOCK (BUG WITH PDSE)
         CH    R2,=H'256'          USED BYTE COUNT MORE THAN MAXIMUM?
         BH    READBLK             YES, IGNORE BLOCK (BUG WITH PDSE)
         BCTR  R2,0
         BCTR  R2,0                SUBTRACT 2 FOR COUNTER HALFWORD
         LA    R4,REPLY+2          POINT TO FIRST MEMBER IN BLOCK
NXTMEMBR CLC   LASTNAME(11),0(R4)  END OF DIRECTORY?
         BE    EOPDSDIR            YES
         LA    R8,1(,R8)           NO, INCREMENT MEMBER COUNTER
         MVC   0(8,R3),0(R4)       COPY MEMBER NAME
         MVC   8(2,R3),CONCTOPN    COPY CONCATENATION NUMBER
         MVC   10(4,R3),8(R4)      COPY TTR AND ALIAS FLAG
         LA    R5,16               IN CORE ENTRY IS SIXTEEN BYTES LONG
         MVC   DOUBLE(1),11(R4)    MOVE USER DATA LENGTH TO WORK AREA
         NI    DOUBLE,X'7F'        TURN OFF ALIAS BIT
         BZ    DONENTRY            NO USER DATA AT ALL
         CLI   DOUBLE,32+14        IMS ACB MEMBER WITH A USER TTR?
         BE    ACBSTORE            YES, STORE STUFF IN-CORE
         CLI   DOUBLE,15           HAS THIS MEMBER SPF STATS?
         BE    SPFSTORE            YES, STORE THEM IN-CORE
         CLI   DOUBLE,14           IMS ACB MEMBER OR PFD STATS?
         BE    ACBORPFD            YES, FIND OUT WHICH
         CLI   DOUBLE,9            IS THIS AN IMS REFERAL MEMBER?
         BE    REFSTORE            YES, STORE STUFF IN-CORE
         CLI   DOUBLE,6            IS THIS AN IMS FORMAT MEMBER?
         BE    FMTSTORE            YES, STORE STUFF IN-CORE
         CLI   DOUBLE,2            HAS THIS MEMBER SSI?
         BE    SSISTORE            YES, STORE IT IN-CORE
         CLI   DOUBLE,1            ONE HALF-WORD?
         BE    LENSTORE            YES, STORE IT IN-CORE
CHKFAILD TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   DONENTRY            NO, SO CANNOT BE A LOAD LIBRARY
         TM    13(R3),X'60'        AT LEAST ONE TTR IN ENTRY?
         BZ    DONENTRY            NO, NOT A LOAD MODULE
         OI    13(R3),X'20'        YES, FLAG THIS
         MVC   16(12,R3),20(R4)    LOAD USUAL LOAD MODULE DATA
         LA    R5,16(,R5)          TOTAL ENTRY LENGTH NOW 32 BYTES
         STH   R5,30(,R3)          SAVE LENGTH AT END OF ENTRY
         LA    R1,33(,R4)          POINT TO END OF SECTION
         TM    20(R4),X'04'        IS THIS A SCATTER PROGRAM?
         BZ    *+8                 NO
         LA    R1,8(,R1)           YES, ADD LENGTH OF SCTR SECTION
         TM    30(R4),X'41'        F4 ADDRESSING EXTENSION?
         BZ    NOTF4AE             NO
         TM    31(R4),X'C0'        F4 AE EXPANSION PRESENT?
         BZ    NOTF4AE             NO
         LA    R1,8(,R1)           YES, ADD LENGTH OF AE SECTION
NOTF4AE  SLR   R6,R6
         ICM   R6,B'0111',22(R4)   GET LENGTH OF LOAD MODULE
         TM    11(R4),X'80'        IS THIS AN ALIAS?
         BZ    ADDPGMSZ            NO, GET AUTHORIZATION CODE DETAILS
         MVC   30(8,R3),REALUNKN   YES, PRIME FOR INVALID
         NI    DOUBLE,X'1F'        TURN OFF USER TTR BITS
         CLI   DOUBLE,16           AT LEAST 16 HALFWORDS OF USER DATA?
         BL    DNREALNM            NO, REAL NAME CAN'T BE THERE
         MVC   30(8,R3),3(R1)      YES, COPY THE REAL NAME
DNREALNM MVC   DOUBLE,3(R1)        SAVE REAL NAME FOR A BIT
         LA    R5,8(,R5)           TOTAL ENTRY LENGTH NOW 40 BYTES
         STH   R5,38(,R3)          SAVE LENGTH AT END OF ENTRY
         LA    R1,11(,R1)          POINT PAST ALIAS SECTION
         SLR   R6,R6               DON'T COUNT SIZE OF ALIAS
ADDPGMSZ LR    R0,R6               COPY PROGRAM SIZE
         A     R6,TOTPGMSZ         ACCUMULATE PROGRAM SIZES SO FAR
         ST    R6,TOTPGMSZ         SAVE NEW VALUE
         TM    31(R4),X'10'        RESIDENCY MODE OF ANY?
         BZ    CHECKSSI            NO
         A     R0,EXTPGMSZ         YES, ACCUMULATE SIZES OVER 16MB LINE
         ST    R0,EXTPGMSZ         SAVE NEW VALUE
CHECKSSI TM    30(R4),X'10'        IS THERE SSI DATA?
         BZ    GETATHCD            NO
         LA    R1,1(,R1)           YES, CATER FOR SSI SECTION ALIGNMENT
         SRL   R1,1
         SLL   R1,1
         MVC   30(4,R3),0(R1)      COPY THE SSI
         LA    R1,4(,R1)           POINT PAST SSI SECTION
         LA    R5,4(,R5)           TOTAL ENTRY LENGTH NOW 36 BYTES
         STH   R5,34(,R3)          SAVE LENGTH AT END OF ENTRY
         TM    11(R4),X'80'        IS THIS AN ALIAS?
         BZ    GETATHCD            NO, PROCEED
         MVC   34(8,R3),DOUBLE     YES, RELOAD THE REAL NAME
         STH   R5,42(,R3)          SAVE LENGTH (44) AT END OF ENTRY
GETATHCD MVC   28(2,R3),0(R1)      GET AUTHORIZATION DETAILS
         B     DONENTRY            HAVE NOW STORED ALL DETAILS
ACBORPFD TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   SPFSTORE            NO, ASSUME PFD STATISTICS
         USING ACBNTRY,R4
ACBSTORE SLR   R0,R0               CLEAR HIGH BYTE
         ICM   R0,B'0111',ACBDATE  LOAD CREATION DATE
         BAL   R14,CHKDATE         VERIFY EXPECTED DATE FORMAT
         MVC   25(5,R3),ACBDATE    COPY DATE AND TIME LIKE SPF
         MVC   21(1,R3),ACBFLAG    COPY FLAG BYTE
         MVI   24(R3),0            ZERO DATE HIGH BYTE
         CLI   25(R3),65           BEFORE 1966?
         BH    *+8                 NO
         MVI   24(R3),1            YES, REALLY A CENTURY LATER
         SLR   R0,R0
         ICM   R0,B'0011',ACBSIZE  GET IMS ACBLIB MEMBER'S SIZE
         SLL   R0,3                CONVERT FROM DOUBLEWORDS TO BYTES
         STCM  R0,7,18(R3)         SAVE SIZE IN LOAD MODULE SIZE SLOT
         LA    R5,18(,R5)          TOTAL ENTRY LENGTH NOW 34 BYTES
         STH   R5,32(,R3)          SAVE IN LAST HALFWORD OF ENTRY
         TM    11(R4),X'80'        IS THIS MEMBER AN ALIAS?
         BO    DONENTRY            YES, DON'T COUNT ITS SIZE
         A     R0,TOTPGMSZ         ACCUMULATE BYTE COUNTS SO FAR
         ST    R0,TOTPGMSZ         SAVE NEW VALUE
         B     DONENTRY            HAVE NOW STORED ALL DETAILS
         DROP  R4                  ACBNTRY
FMTSTORE ICM   R0,B'1111',16(R4)   LOAD CREATION DATE
         BAL   R14,CHKDATE         VERIFY EXPECTED DATE FORMAT
         MVC   18(3,R3),13(R4)     COPY SIZE LIKE PGM (DROP HIGH BYTE)
         MVC   24(6,R3),16(R4)     COPY DATE AND TIME LIKE SPF
         LA    R5,18(,R5)          TOTAL ENTRY LENGTH NOW 34 BYTES
         STH   R5,32(,R3)          SAVE IN LAST HALFWORD OF ENTRY
         TM    11(R4),X'80'        IS THIS MEMBER AN ALIAS?
         BO    DONENTRY            YES, DON'T COUNT ITS SIZE
         SLR   R0,R0               (IMS FMT, DIF, DOF, MID OR MOD?)
         ICM   R0,B'1111',12(R4)   GET IMS FORMAT MEMBER'S BYTE COUNT
         A     R0,TOTPGMSZ         ACCUMULATE BYTE COUNTS SO FAR
         ST    R0,TOTPGMSZ         SAVE NEW VALUE
         B     DONENTRY            HAVE NOW STORED ALL DETAILS
LENSTORE TM    MYDSCB-44+84,X'C0'  IS IT UNDEFINED RECORD FORMAT?
         BNO   SSISTORE            NO, CALL IT SSI AND WASTE A CORE HW
         XC    16(6,R3),16(R3)     YES, ASSUME AN IMS BYTE COUNT
         MVC   19(2,R3),14(R4)     LOAD IT AT PROGRAM SIZE OFFSET
         LA    R5,8(,R5)           TOTAL ENTRY LENGTH NOW 24 BYTES
         STH   R5,22(,R3)          SAVE IN LAST HALFWORD OF ENTRY
         TM    11(R4),X'80'        IS THIS MEMBER AN ALIAS?
         BO    DONENTRY            YES, DON'T COUNT ITS SIZE
         SLR   R0,R0               (IMS FMT, DIF, DOF, MID OR MOD?)
         ICM   R0,B'0011',12(R4)   GET IMS FORMAT MEMBER'S BYTE COUNT
         A     R0,TOTPGMSZ         ACCUMULATE BYTE COUNTS SO FAR
         ST    R0,TOTPGMSZ         SAVE NEW VALUE
         B     DONENTRY            HAVE NOW STORED ALL DETAILS
SSISTORE MVC   16(4,R3),12(R4)     SAVE SSI DATA
         LA    R5,6(,R5)           TOTAL ENTRY LENGTH NOW 22 BYTES
         STH   R5,20(,R3)          SAVE IN LAST HALFWORD OF ENTRY
         CLI   DOUBLE,2            WAS IT FOUR-BYTE SSI?
         BE    DONENTRY            YES, HAVE NOW STORED ALL DETAILS
         STCM  R5,B'1100',18(R3)   NO, TWO-BYTE, ZERO OTHER 2 FOR SORT
         B     DONENTRY            HAVE NOW STORED ALL DETAILS
REFSTORE ICM   R0,B'1111',16(R4)   LOAD CREATION DATE
         BAL   R14,CHKDATE         VERIFY EXPECTED DATE FORMAT
         MVC   20(4,R3),16(R4)     COPY CREATION DATE
         MVC   24(6,R3),16(R4)     ALSO CALL IT MODIFICATION DATE/TIME
         MVI   16(R3),100          SUPPLY VERSION TO DENOTE IMS REFERAL
         MVC   17(1,R3),12(R4)     COPY TYPE BYTE AS MODIFICATION LEVEL
         MVC   30(2,R3),14(R4)     COPY SIZE (HOPE HI-ORDER BYTE IS 0)
         MVC   32(2,R3),14(R4)     COPY SIZE (HOPE HI-ORDER BYTE IS 0)
         TM    11(R4),X'80'        IS THIS MEMBER AN ALIAS?
         BO    REFALIAS            YES, DON'T COUNT ITS SIZE
         SLR   R0,R0
         ICM   R0,B'0111',13(R4)   GET REFERAL MEMBER'S BYTE COUNT
         A     R0,TOTSPFSZ         ACCUMULATE BYTE COUNTS SO FAR
         ST    R0,TOTSPFSZ         SAVE NEW VALUE
REFALIAS STCM  R5,B'1100',34(R3)   ZERO SPF MODIFIED RECORD COUNT
         MVI   36(R3),C'('         INDICATE FAKE USERID
         MVC   37(6,R3),24(R4)     COPY THE ROOT NAME
         MVI   43(R3),C')'         INDICATE FAKE USERID
         CLI   37(R3),C' '         WAS SOMETHING COPIED?
         BNE   SPFDFAKE            YES, DONE FAKING SPF STATS DETAILS
         CLI   17(R3),3            IS THIS A FORMAT?
         BNE   SPFDFAKE            NO, DONE FAKING SPF STATS DETAILS
         MVC   37(6,R3),0(R4)      YES, COPY MEMBER NAME FOR SORTING
         B     SPFDFAKE            FINISHED FAKING SPF STATS DETAILS
SPFSTORE ICM   R0,B'1111',16(R4)   LOAD CREATION DATE
         BAL   R14,CHKDATE         VERIFY EXPECTED DATE FORMAT
         ICM   R0,B'1111',20(R4)   LOAD MODIFICATION DATE
         BAL   R14,CHKDATE         VERIFY EXPECTED DATE FORMAT
         TM    11(R4),X'80'        IS THIS MEMBER AN ALIAS?
         BO    SPFALIAS            YES, DON'T COUNT ITS LINES
         SLR   R0,R0
         ICM   R0,B'0011',26(R4)   GET SPF LINE COUNT
         A     R0,TOTSPFSZ         ACCUMULATE LINE COUNTS SO FAR
         ST    R0,TOTSPFSZ         SAVE NEW VALUE
SPFALIAS MVC   16(28,R3),12(R4)    SAVE SPF STATISTICS
SPFDFAKE LA    R5,30(,R5)          TOTAL ENTRY LENGTH NOW 46 BYTES
         STH   R5,44(,R3)          SAVE IN LAST HALFWORD OF ENTRY
DONENTRY STH   R5,14(,R3)          SAVE IN SEVENTH HALFWORD OF ENTRY
         NI    13(R3),X'FF'-X'40'  TURN OFF MEMBER OFFLOAD/SEARCH FLAG
         AR    R3,R5               POINT TO NEXT IN-CORE TABLE ENTRY
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    READATTN            YES, TERMINATE READ
         C     R3,MAXADDR          IS IN-CORE TABLE ALMOST EXHAUSTED?
         BNH   MAKENTRY            NO, CREATE THIS ENTRY
         MVI   DIRHDG,C'-'         YES, LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(L'DIRRDMSG),DIRRDMSG
         B     READBELL            CORE FULL SO STOP READING
READATTN MVI   DIRHDG,C'-'         LOAD ERROR MESSAGE INTO HEADING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         MVC   DIRHDG(9),INRPTMSG
         MVC   DIRHDG+9(L'DIRRDMSG-11),DIRRDMSG+11
READBELL MVI   WCCSAVE,WCCBELL     SUPPLY WCC TO SOUND THE BELL
         B     EOPDSDIR            ATTENTION HIT SO STOP READING
MAKENTRY IC    R5,11(,R4)          GET USER DATA HALFWORD COUNT
         SLL   R5,27               SHIFT OUT NON-COUNT BITS
         SRL   R5,26               CONVERT TO NUMBER OF USER BYTES
         LA    R5,12(,R5)          GET TOTAL BYTES USED BY ENTRY
         SR    R2,R5               UPDATE BYTES-TO-DO-IN-THIS-BLOCK
         BNP   READBLK             IF ZERO GET ANOTHER BLOCK
         LA    R4,0(R5,R4)         POINT TO NEXT MEMBER NAME
         B     NXTMEMBR
EOPDSDIR MVI   CLOSED,X'80'        CLOSE PDS DIRECTORY
         CLOSE (DIRDCB),MF=(E,CLOSED)
         L     R1,CBPRM3           POINT TO BUFFER START
         CLI   WCCSAVE,WCCBELL     GOING TO SOUND THE BELL?
         BE    POCONRET            YES, SKIP FURTHER DATA SETS
         ICM   R0,B'0011',ARLRTRVD ANY CONCATENATED JFCBS RETRIEVED?
         BNZ   POCONCAT            YES, PROCESS NEXT DATA SET
POCONRET MVC   0(16,R3),LASTNAME   DENOTE END OF MEMBER LIST
         ST    R3,LASTADDR         SAVE ADDRESS OF LAST ENTRY
         LA    R3,16+7(,R3)        POINT PAST LAST USED DOUBLEWORD
         SRL   R3,3
         SLL   R3,3                ENSURE DOUBLEWORD BOUNDARY
         LR    R2,R3               ADDRESS OF UNUSED DYNAMIC AREA
         SR    R2,R13              DETERMINE SIZE OF USED DYNAMIC AREA
         LR    R1,R3               POINT TO START OF UNUSED DYN AREA
         L     R0,FREEMRG0         GET TOTAL SIZE OF DYNAMIC AREA
         AR    R0,R13              POINT PAST END OF DYNAMIC AREA
         SR    R0,R1               GET SIZE OF UNUSED DYNAMIC AREA
         BNP   AREAFULL            IF ZERO THEN BYPASS FREEMAIN
         FREEMAIN R,LV=(0),A=(1)   FREE UNUSED PORTION OF DYNAMIC AREA
         ST    R2,FREEMRG0         SAVE NEW SIZE OF DYNAREA
AREAFULL CVD   R8,DOUBLE           GET NUMBER OF MEMBERS IN PDS
         LA    R1,DIRMEMCT+7       POINT TO LAST DIGIT
         EDMK  DIRMEMCT,DOUBLE+4   SHOW UP TO 9999999 MEMBERS CORRECTLY
         BCTR  R1,0                POINT TO BEFORE FIRST DIGIT
         MVI   0(R1),C' '          TIDY UP HEADING
         ST    R8,MEMBRCNT         SAVE COUNT OF MEMBERS IN LIST
         XC    SELMEMNM,SELMEMNM   CLEAR MEMBER NAME TO SEARCH FOR
         TM    STATUS,STNOMEM      ANY USEFUL MEMBER NOMINATED?
         BO    FORCETOP            NO, RETURN TO MAINLINE
         MVC   SELMEMNM,$MEMBER    YES, LOAD MEMBER NAME TO SEARCH FOR
         B     FORCETOP            RETURN TO MAINLINE


**********************************************************************
*                                                          *         *
*         PROCESS CONCATENATED PDS DIRECTORIES             *  GP@P6  *
*                                                          *  06/89  *
**********************************************************************

POCONCAT CLC   DCBDDNAM,$DDNAME    TEMPORARY FILE ALREADY ALLOCATED?
         BE    CONCATDD            NO, ALLOCATE FIRST CONCATENATION
         LA    R15,MYDAPB          YES, FREE IT
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,DCBDDNAM    SUPPLY DDNAME TO BE FREED
         MVC   DA18MNM(8),=CL8' '
         MVC   DA18SCLS(2),=CL8' '
         DROP  R15                 DAPB18
         LA    R1,MYDAPL           POINT TO DAIR PARAMETER LIST
         L     R15,16              MUST BE MVS/SP 2.2 OR LATER, SO
         L     R15,732(,R15)       CVTDAIR MUST EXIST AND BE FILLED IN
         BALR  R14,R15             CALL IKJDAIR
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    EOCONDIR            YES, TERMINATE DIRECTORY READS
         CLC   CONCTOPN,ARLRTRVD   PROCESSED LAST CONCATENATION?
         BL    CONCATDD            NO, PROCESS NEXT CONCATENATION
         MVI   DIRHDG,C'-'         INITIALIZE HEADING STRING
         MVC   DIRHDG+1(L'DIRHDG-1),DIRHDG
         LH    R1,$DDNAML          GET DATA DEFINITION NAME LENGTH
         EX    R1,GTPDDNAM         LOAD DDNAME INTO HEADING
         MVC   DIRHDG(L'REVDDMSG),REVDDMSG
EOCONDIR MVC   POVOLUME-3(9),=CL9'DISPLAYED'
         B     POCONRET            RETURN TO DISPLAY MEMBER LIST
CONCATDD TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    EOCONDIR            YES, TERMINATE DIRECTORY READS
         LA    R0,1
         AH    R0,CONCTOPN         INCREMENT OPENED CONCATENATION COUNT
         STH   R0,CONCTOPN
         L     R1,ARLAREA          POINT TO FIRST CONCATENATED JFCB
CONCTJFL BCT   R0,CONCTJFN         CHAIN THROUGH JFCBS
         B     CONCTJFC            NOW HAVE CORRECT JFCB
CONCTJFN AH    R1,0(,R1)           POINT TO NEXT CONCATENATED JFCB
         B     CONCTJFL
CONCTJFC LA    R15,MYDAPB
         USING DAPB08,R15
         XC    0(84,R15),0(R15)
         MVI   DA08CD+1,X'08'
         MVC   CMDAREA+2(44),4(R1) GET DATA SET NAME FROM JFCB
         LA    R0,44               CHEAT AND TAKE THE LAZY WAY OUT
         STH   R0,CMDAREA
         LA    R0,CMDAREA
         ST    R0,DA08PDSN
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8'SYSALLDA'
         MVC   DA08SER,=CL8' '
         MVC   DA08SER(6),122(R1)  GET VOLUME FROM JFCB
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,$PASSWRD
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP

         LA    R1,MYDAPL           POINT TO PARAMETER LIST
         L     R15,16              MUST BE MVS/SP 2.2 OR LATER, SO
         L     R15,732(,R15)     __CVTDAIR MUST EXIST AND BE FILLED IN
*                               /  (THIS IS TRUE IF THE JFCB DATA WAS
*                              /   SUPPLIED BY RDJFCB, BUT IF THE DATA
*      COMPATIBILITY NOTE: ----    WAS DUMMIED UP BY 'REVIEW' DOING
*                              \   ALL THE WORK ITSELF FOR BACK-LEVEL
*                               \__OS'S THEN THIS MAY NOT BE TRUE!)
         BALR  R14,R15             CALL IKJDAIR
         LTR   R15,R15             SUCCESSFUL ALLOCATION?
         BNZ   CONCATDD            NO, TRY THE NEXT ONE
         LA    R15,MYDAPB
         MVC   DCBDDNAM,DA08DDN    SUPPLY DDNAME TO BE OPENED
         B     OPENDIR             READ THE CONCATENATED DIRECTORY
         DROP  R15                 DAPB08


**********************************************************************
*                                                          *         *
*         VERIFY DECIMAL JULIAN DATE FORMAT - AVOID S0C7   *  GP@P6  *
*                                                          *  03/87  *
**********************************************************************

CHKDATE  ST    R0,DOUBLE           STORE DATE FOR EXAMINATION
         TM    DOUBLE+3,X'0F'      SIGN AS EXPECTED?
         BNO   CHKFAILD            NO, REJECT IT
         NI    DOUBLE+3,X'F0'      YES, ZERO IT
         CLI   DOUBLE+2,X'36'      DDD OVER 369?
         BH    CHKFAILD            YES, REJECT IT
         LA    R0,4                FOUR BYTES TO CHECK
         LA    R1,DOUBLE           POINT TO FIRST BYTE
DECVFYLP CLI   0(R1),X'A0'         IS THE HIGH-ORDER DIGIT DECIMAL?
         BNL   CHKFAILD            NO, REJECT IT
         NI    0(R1),X'0F'         YES, ZERO IT
         CLI   0(R1),X'0A'         IS THE LOW-ORDER DIGIT DECIMAL?
         BNL   CHKFAILD            NO, REJECT IT
         LA    R1,1(,R1)           THIS BYTE OKAY, POINT TO THE NEXT
         BCT   R0,DECVFYLP         PROCESS NEXT BYTE
         BR    R14                 00YYDDD+ OR YYYYDDD+ OR OTHER PL4


**********************************************************************
*                                                          *         *
*         CHECK DIRECTORY OPEN FOR ABNORMAL END            *  GP@FT  *
*                                                          *  01/96  *
**********************************************************************

OPENDIRX TM    STATUS,STABEND      DID OPEN ABEND?
         BZR   R14                 NO, PROCESS DIRECTORY
         MVC   REPLY(50),MSGDIR    LOAD MESSAGE TEXT
         UNPK  REPLY+50(7),BLKREADS
         TR    REPLY+50(6),PODIRHEX-C'0'
         ICM   R1,7,REPLY+53       MAKE A GAP BETWEEN ABEND CODE AND RC
         STCM  R1,7,REPLY+54
         MVI   REPLY+53,C'-'       SHOW SXXX-XXX
         LA    R1,REPLY            POINT TO MESSAGE
         LA    R0,57               GET MESSAGE LENGTH
         TPUT  (1),(0),R           ISSUE MESSAGE
         B     DIREXIT             TERMINATE

         DROP  R7,R10,R11          READDIR, REVPODIR

MSGDIR   DC    CL50'"REVIEW" UNABLE TO SHOW MEMBER LIST DUE TO ABEND S'

         LTORG
         EJECT
**********************************************************************
*                                                          *         *
*         DIRECTORY OPEN USAGE EXIT                        *  GP@GE  *
*                                                          *  11/97  *
**********************************************************************

         IHAUDA DSECT=NO
         ORG   UDAFLAG3
         DC    AL1(UDANORFU)       NO REFERENCE DATE UPDATE
         ORG
         EJECT
**********************************************************************
*                                                          *         *
*         RECREATE DIRECTORY ENTRY FOR CONCATENATION CHECK *  GP@FT  *
*                                                          *  01/97  *
**********************************************************************

*  MSGDSN WILL BE LOADED WITH TTRKZC (BLDL FORMAT) PLUS SPF STATISTICS
*  OR LOAD MODULE ATTRIBUTES IF APPLICABLE.  THIS IS PASSED TO REVIEW2
*  WHICH WILL SEARCH FOR THE SELECTED MEMBER.  IF FOUND, THE CONCATEN-
*  ATION CODE IS CHECKED TO SEE IF THE FOUND MEMBER IS IN THE SELECTED
*  MEMBER'S DATA SET.  IF SO, THEN PROCEED WITH REVIEW.  IF FOUND IN A
*  LATER DATA SET THEN SAY MEMBER NOT FOUND.  IF FOUND IN AN EARLIER
*  DATA SET THEN USE RECREATED ENTRY IN MSGDSN TO REVIEW DATA CONTAINED
*  IN THE NON-FIRST OCCURRENCE OF A MEMBER NAME.  REVIEW2 USES ISPF/PFD
*  STATISTICS TO SHOW THE VV.MM IN THE HEADING, AND LOAD MODULE ATTRIB-
*  UTES TO SHOW WHETHER CSECT FLAGS ARE RELATED TO XA OR OVERLAYS.
*  THE 'MEMBER' SUBCOMMAND DOES NOT CAUSE THIS TO HAPPEN, AND CAN ONLY
*  ACCESS THE FIRST OCCURRENCE OF A MEMBER NAME IN A CONCATENATION.

         USING POXTRACT,R15
POXTRACT OI    STATUS8,SCODE       FLAG 'S' SELECTION CODE
         XC    MSGDSN,MSGDSN       PASS DIRECTORY ENTRY TO REVIEW2
         MVC   MSGDSN(3),10(R4)    COPY TTR
         MVC   MSGDSN+3(1),9(R4)   COPY CONCATENATION CODE
         MVC   MSGDSN+5(1),13(R4)  COPY ALIAS FLAG AND USER H/W COUNT
         CLI   15(R4),46           ISPF/PFD ENTRY?
         BNE   POPGMCHK            NO, CHECK FOR PROGRAM ENTRY
         MVC   MSGDSN+6(28),16(R4) YES, COPY SPF STATISTICS
         BR    R14                 ENTRY RESTORED
POPGMCHK CLI   15(R4),32           LOAD MODULE ENTRY?
         BLR   R14                 NO, NO EXTRA DATA TO RESTORE
         CLI   15(R4),34
         BER   R14                 NO, NO EXTRA DATA TO RESTORE
         MVC   MSGDSN+14(12),16(R4)    COPY LOAD MODULE ATTRIBUTES
         BR    R14                 RETURN TO CALLER

         DROP  R15                 POXTRACT

         DROP  R13                 @DYNAREA


**********************************************************************
*                                                          *         *
*         DIRECTORY OPEN EXIT                              *  GP@P6  *
*                                                          *  06/99  *
**********************************************************************

         USING IHADCB,R1
DIROPNEX DS    0H
         ICM   R0,3,DCBLRECL       IS LRECL ZERO?
         BZR   R14                 YES, OKAY FOR RECFM=F SO RETURN
         MVC   DCBLRECL,DCBBLKSI   NO, SO SET IT TO BLKSIZE (256)
         BR    R14                 S013-60 NOW AVOIDED
         DROP  R1                  IHADCB
         EJECT
**********************************************************************
*                                                          *         *
*         DIRECTORY DISPLAY SUBCOMMAND HELP PANEL          *  GP@FT  *
*                                                          *  11/96  *
**********************************************************************

PODIRHLP DC    AL1(WCCSTND,SBA,1,1,IC,SF,PROHIS,SA,COLR,WHITE)
         DC    AL1(RA,1,26),C' '             '
         DC    CL30'REVIEW DIRECTORY DISPLAY HELP '
         DC    AL1(RA,1,76),C' '             '
         DC    CL5'R&REL'
         DC    AL1(RA,3,1),C' '
         DC    AL1(SF,PROLOS,SA,COLR,PINK)
         DC    C'SUBCMD   ALIASES  FUNCTION'
         DC    AL1(RA,4,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=CANCEL  =C =CAN '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'TERMINATE REVIEW WITHOUT UPDATING YOUR PROFILE.'
         DC    AL1(RA,5,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=DELINK  =DL     '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'DELINK LOAD MODULES AND PRODUCE OBJECT DECKS.'
         DC    AL1(RA,6,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=EDITMEM =E =EDIT'
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'REQUEST PROMPT FOR MEMBER NAME TO EDIT WITH ISPF.'
         DC    AL1(RA,7,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=END     =X =EXIT'
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'TERMINATE REVIEW.'
         DC    AL1(RA,8,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=HELP    =?      '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'DISPLAY THESE HELP PANELS.'
         DC    AL1(RA,9,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=OFFLOAD =OFF    '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'COPY MEMBERS TO A SEQUENTIAL FILE WITH ./ CONTROL RECO+
               RDS.'
         DC    AL1(RA,10,20),C' '
         DC    C'SORT INTO TTR ORDER FIRST TO PRESERVE ALIASES.'
         DC    AL1(RA,11,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=PDSLOAD =PDS    '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'WRITE NEW MEMBER CONTENTS FROM SEQUENTIAL INPUT.'
         DC    AL1(RA,12,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=REFRESH =REF    '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'REFRESH MEMBER LIST WITH THE LATEST DATA FROM DISK.'
         DC    AL1(RA,13,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=RESET   =RES    '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'UNTAG ALL MEMBERS WITHOUT REFRESHING THE MEMBER LIST.'
         DC    AL1(RA,14,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=SEARCH  =F =FIND'
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'SEARCH MEMBERS FOR SPECIFIC DATA.'
         DC    AL1(RA,15,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=SEQLOAD =SEQ    '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'COPY MEMBER CONTENTS TO A SEQUENTIAL FILE.'
         DC    AL1(RA,16,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=SETFILE =SF     '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'SPECIFY OUTPUT SEQUENTIAL FILE DDNAME - INITIALLY SYSU+
               T2.'
         DC    AL1(RA,17,1),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'=TAGFLIP =TF     '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'TOGGLE THE TAG STATUS OF ALL MEMBERS.'
         DC    AL1(RA,19,2),C' '
         DC    AL1(SA,COLR,GREEN)
         DC    C'MEMBERS ARE PROCESSED BY SUBCOMMANDS IN THE CURRENT DI+
               SPLAY LIST ORDER.'
         DC    AL1(RA,23,2),C' '
         DC    AL1(SA,COLR,BLUE),C'PRESS'
         DC    AL1(SF,PROHIS,SA,COLR,RED),C'ENTER'
         DC    AL1(SF,PROLOS,SA,COLR,BLUE),C'FOR OTHER HELP PAGE.'
         DC    AL1(RA,24,2),C' '
         DC    AL1(SA,COLR,BLUE),C'PRESS'
         DC    AL1(SF,PROHIS,SA,COLR,RED),C'ANY PF KEY'
         DC    AL1(SF,PROLOS,SA,COLR,BLUE),C'TO RETURN TO MEMBER LIST.'
         DC    AL1(RA,1,1),C' '
PODIRHLN EQU   *-PODIRHLP
         EJECT
**********************************************************************
*                                                          *         *
*         DIRECTORY DISPLAY SELECTION CODE HELP PANEL      *  GP@P6  *
*                                                          *  03/99  *
**********************************************************************

POSELHLP DC    AL1(WCCSTND,SBA,1,1,IC,SF,PROHIS,SA,COLR,WHITE)
         DC    AL1(RA,1,26),C' '             '
         DC    CL30'REVIEW DIRECTORY DISPLAY HELP '
         DC    AL1(RA,1,76),C' '             '
         DC    CL5'R&REL'
         DC    AL1(RA,4,19),C' '
         DC    AL1(SF,PROLOS,SA,COLR,PINK)
         DC    C'MEMBER SELECTION CODE FUNCTIONS'
         DC    AL1(RA,6,16),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'S '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'BROWSE THE MEMBER WITH REVIEW.'
         DC    AL1(RA,7,16),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'B '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'BROWSE THE MEMBER WITH ISPF BROWSE.'
         DC    AL1(RA,8,16),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'E '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'EDIT THE MEMBER WITH ISPF EDIT.'
         DC    AL1(RA,9,16),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'R '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'RESET OR UNTAG THE MEMBER.'
         DC    AL1(RA,10,16),C' '
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'T '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'TAG THE MEMBER FOR DISPLAY CONTROL PURPOSES,'
         DC    AL1(RA,11,20),C' '
         DC    C'OR FOR LATER PROCESSING BY SEARCH, OFFLOAD'
         DC    AL1(RA,12,20),C' '
         DC    C'OR DELINK.'
         DC    AL1(RA,23,2),C' '
         DC    AL1(SA,COLR,BLUE),C'PRESS'
         DC    AL1(SF,PROHIS,SA,COLR,RED),C'ENTER'
         DC    AL1(SF,PROLOS,SA,COLR,BLUE),C'FOR OTHER HELP PAGE.'
         DC    AL1(RA,24,2),C' '
         DC    AL1(SA,COLR,BLUE),C'PRESS'
         DC    AL1(SF,PROHIS,SA,COLR,RED),C'ANY PF KEY'
         DC    AL1(SF,PROLOS,SA,COLR,BLUE),C'TO RETURN TO MEMBER LIST.'
         DC    AL1(RA,1,1),C' '
POSELHLN EQU   *-POSELHLP

         DS    0D                  END OF CSECT
         TITLE '  R E V P O D I R   -   D Y N A M I C   A R E A  '
@DYNAREA DSECT
         DS    18F                 WORKING SAVE AREA
POSAVE2  DS    18F                 SAVE AREA FOR CALLING REVIEW2
FREEMRGS EQU   *,8                 FREEMAIN REGISTER VALUES
FREEMRG0 DS    F                   FREEMAIN REGISTER 0 VALUE
FREEMRG1 DS    F                   FREEMAIN REGISTER 1 VALUE
TOPMEMAD DS    F                   TOP MEMBER ENTRY ADDRESS SAVE AREA
LASTADDR DS    F                   LAST MEMBER ENTRY ADDRESS SAVE AREA
MAXADDR  DS    F                   TOP USABLE ADDRESS OF DIRENTS
BLKREADS DS    F                   DIRECTORY BLOCK READ COUNTER
EXTPGMSZ DS    F                   SIZE OF REAL PROGRAMS WITH RMODE=ANY
TOTPGMSZ DS    F                   TOTAL SIZE OF REAL PROGRAMS
TOTSPFSZ DS    F                   TOTAL LINES OF REAL SPFD MEMBERS
MEMBRCNT DS    F                   TOTAL NUMBER OF MEMBERS IN LIST
PUTCURSR DS    XL4                 CURSOR POSITION TRAILER
SELMEMNM DS    CL10                USER SELECTED MEMBER + CONCAT'N NO.
CONCTOPN DS    H                   NO. OF CONCATENATED DIRECTORIES READ
OFFDDNAM DS    CL8                 DD NAME FOR OFFLOAD/SEQUENTIALIZING
DIRDCB   DS    0F,XL(DIRDCBL)      PDS DIRECTORY DCB
PRIMADDR DS    XL2                 PRIMARY INPUT AREA BUFFER ADDRESS
PRIMLOCN DS    XL2                 PRIMARY INPUT AREA BUFFER LOCATION
WCCSAVE  DS    CL1                 SAVE AREA FOR WCC
DIRHDG   DS    CL55                DATA SET NAME OR MESSAGE
DIRMEMCT DS    XL8
         DS    CL12
POVOLUME DS    CL6
DIRFLAG  DS    X                   FLAG BYTE
DIRENTS  DS    0D                  IN-CORE MEMBER LIST STARTS HERE
         TITLE ' I M S   A C B L I B   D I R E C T O R Y   E N T R Y '
ACBNTRY  DSECT
ACBNAME  DS    CL8                 MEMBER NAME
ACBTTR   DS    XL3                 TTR OF MEMBER
ACBC     DS    X                   FLAG BYTE
*                                   BIT 0,  0 - MEMBER NAME
*                                           1 - ALIAS
*                                       1-2 NUMBER OF TTRN FIELDS IN
*                                           THE USER DATA FIELD
*                                       3-7 NUMBER OF HALFWORDS IN THE
*                                           USER DATA FIELD = 14
ACBALIS  EQU   X'80'                ALIAS, IF ON
ACBNUTT  EQU   X'20'                NBR OF USER TTRS, NEVER>1
ACBTTRI  DS    XL3                 TTR OF PSB INTENT OR DMB
*                                   SEQUENT LIST
ACBN     DS    X                   RESERVED
ACBFLAG  DS    X                   FLAG BYTE
ACBDMB   EQU   X'01'                 DMB, IF ON - PSB IF OFF
ACBGRP   EQU   X'02'                 PART OF A SHARED INDEX
ACB1GRP  EQU   X'04'                 FIRST DMB IN A SHARED INDX
ACBMSDB  EQU   X'10'               MSDB DMB IF ON
ACBDEDB  EQU   X'20'               DEDB DMB IF ON
ACBDATE  DS    PL3                 PACKED JULIAN DATE
ACBTIME  DS    XL4                 TIME MEMBER WRITTEN
*                                  NOTE - ALL SIZES IN DBL WORD
ACBSIZE  DS    XL2                 SIZE OF MEMBER IN DBL WORDS
*                                   FOR PSB = PSB W/O INTENT
ACBBFR   DS    XL2                 STORAGE SIZE OF MEMBER
*                                   FOR PSB = PSB + INTENT
ACBLSIZ  DS    0XL2                FOR DMB, DMB SEQ LIST SIZE
*                                   IN DOUBLE WORDS
ACBDMOP  DS    XL4                 DATA MANAGEMENT OPTIONS
ACBXWA   DS    XL2                 TOTAL SIZE OF PSB WORK AREA
ACBMBSZ  DS    XL2                 SIZE OF MSG RGN MAILBOX
ACBRLVL  DS    XL1                 RELEASE LEVEL IN WHICH PSB
*                                  OR DMB WAS LAST CHANGED
ACBRPLV  EQU   X'50' PSB RELEASE LEVEL  3B TO 50 5/93 FOR DEDBS
ACBRDLV  EQU   X'50' DMB RELEASE LEVEL  32 TO 50 5/93 FOR DEDBS
*              CHANGE EQUATES FOR PSB OR DMB WHEN DL/I INTERNAL
*              PSB OR DMB CHANGES.  REASSEMBLE MODULES
*              REFERENCING THESE LABELS.
*                    IMS/VS RELEASE 1.2 = X'17'
*                    IMS/VS RELEASE 1.3 = X'18'
*                    IMS/VS RELEASE 2.2 = X'19', X'20'
*                    IMS/VS RELEASE 3.1 = X'31'  OCT 87
*                    IMS/VS RELEASE 3.2 = X'32'  DEC 89
*                    IMS    RELEASE 5.0 = X'50'  MAY 93 DMB & PSB
         DS    XL1                 RESERVED
ACBPCBS  DS    XL2                 SIZE OF CSA PART OF PSB
ACBDSLN  EQU   *-ACBNTRY
ACBEND   EQU   *                   END OF DSECT
         TITLE ' I N T E R N A L   D I R E C T O R Y   E N T R I E S '
**********************************************************************
*                                                          *         *
*         FORMAT FOR REVIEW IN-CORE DIRECTORY ENTRIES      *  GP@P6  *
*                                                          *         *
**********************************************************************

*        COMMON BASE SECTION
*        +0    MEMBER NAME
*        +8    RELATIVE CONCATENATION NUMBER OF PDS CONTAINING MEMBER
*        +A    TTR OF FIRST BLOCK OF MEMBER
*        +D    ALIAS FLAG AND USER DATA LENGTH OF DISK DIRECTORY ENTRY
*        +E    TOTAL LENGTH OF THIS ENTRY (H'16' FOR STANDARD ENTRY)
*       +10    END OF STANDARD INTERNAL ENTRY

*        SYSTEM STATUS INFORMATION ONLY SECTION
*       +10    SSI DATA
*       +14    TOTAL LENGTH OF THIS ENTRY (H'22')
*       +16    END OF SSI INTERNAL ENTRY

*        SPF/PFD STATISTICS SECTION
*       +10    VERSION      (100 FOR IMS REFERAL DETAILS)
*       +11    MODIFICATION LEVEL   (IMS REFERAL MEMBER TYPE FLAG)
*       +12    SCLM FLAGS
*       +14    CREATION DATE
*       +18    LAST MODIFICATION DATE
*       +1C    LAST MODIFICATION TIME
*       +1E    CURRENT SIZE
*       +20    INITIAL SIZE
*       +22    MODIFIED RECORD COUNT
*       +24    MODIFYING USERID     (RELATED IMS FORMAT IF MSG OR FMT)
*       +2C    TOTAL LENGTH OF THIS ENTRY (H'46')
*       +2E    END OF SPF/PFD STATISTICS ENTRY

*        LOAD MODULE COMMON SECTION
*       +10    LOAD MODULE ATTRIBUTES
*       +1C    LENGTH OF PROGRAM AUTHORIZATION CODE
*       +1D    PROGRAM AUTHORIZATION CODE
*       +1E    END OF LOAD MODULE COMMON SECTION

*        LOAD MODULE SECTION
*       +1E    TOTAL LENGTH OF THIS ENTRY (H'32')
*       +20    END OF LOAD MODULE ENTRY

*        LOAD MODULE SYSTEM STATUS INFORMATION SECTION
*       +1E    SSI DATA
*       +22    TOTAL LENGTH OF THIS ENTRY (H'36')
*       +24    END OF LOAD MODULE SSI ENTRY

*        LOAD MODULE ALIAS SECTION
*       +1E    LOAD MODULE NAME OF WHICH THIS ENTRY IS AN ALIAS
*       +26    TOTAL LENGTH OF THIS ENTRY (H'40')
*       +28    END OF LOAD MODULE ALIAS ENTRY

*        LOAD MODULE SYSTEM STATUS INFORMATION ALIAS SECTION
*       +1E    SSI DATA
*       +22    LOAD MODULE NAME OF WHICH THIS ENTRY IS AN ALIAS
*       +2A    TOTAL LENGTH OF THIS ENTRY (H'44')
*       +2C    END OF LOAD MODULE SSI ENTRY

*        IMS COMMON SECTION
*       +12    MEMBER SIZE

*        IMS SIZE ONLY SECTION
*       +16    TOTAL LENGTH OF THIS ENTRY (H'24')
*       +18    END OF IMS SIZE ONLY ENTRY

*        IMS ACB ONLY SECTION
*       +15    ACBLIB MEMBER TYPE FLAG BYTE

*        IMS FORMAT/ACB COMMON SECTION
*       +18    CREATION DATE
*       +1C    CREATION TIME
*       +1E    UNUSED
*       +20    TOTAL LENGTH OF THIS ENTRY (H'34')
*       +22    END OF IMS FORMAT/ACB ENTRY


*        SORTING OF PDS DIRECTORY ENTRIES WAS INSPIRED BY THE
*        'FLIST' CMS COMMAND (FL, FILEL, FILELIST AND OTHER
*        SIMILAR COMMANDS).  IT WAS IMPLEMENTED A COUPLE YEARS
*        BEFORE ISPF PDS DIRECTORY SORTS ARRIVED ON THE SCENE.
         TITLE '  R E V S O R T E  '
**********************************************************************
*                                                          *         *
*         REVIEW ENTRY REVERSE SORT ENHANCER               *  GP@P6  *
*                                                          *  10/87  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF SPEEDING UP      *
*        REVPODIR INTERNAL ENTRY SORTS BY MOVING ENTRIES MORE        *
*        THAN THE ONE SLOT DISPLACEMENT PERFORMED BY A BUBBLE        *
*        SORT "COMPARE-AND-CONDITIONALLY-SWAP" CYCLE.  ENTRIES       *
*        TO BE COMPARED ARE NOT ADJACENT BUT EQUI-DISTANTLY          *
*        OPPOSITE THE CENTRE OF THE INTERNAL DIRECTORY.  HENCE,      *
*        FIRST IS COMPARED WITH LAST, SECOND WITH SECOND LAST,       *
*        AND SO ON.  THUS, IN ONE PASS A WORST CASE MAY BE           *
*        CHANGED INTO A BEST CASE BEFORE BUBBLE SORT PROCESSING.     *
*           INTERNAL ENTRIES MAY HAVE DIFFERENT SIZES PREVENTING     *
*        IN-PLACE SWAPS BEING DONE.  THEREFORE, A DYNAMIC AREA       *
*        IS GETMAINED, ITS SIZE THE SAME AS THAT OF THE INTERNAL     *
*        DIRECTORY TO BE SORTED.  IF THE GETMAIN FAILS THEN THIS     *
*        PRE-SORTING IS NOT PERFORMED; CONTROL IS RETURNED TO        *
*        REVPODIR WHERE BUBBLE SORTING WILL ACCOMPLISH THE ENTIRE    *
*        SORT WITHOUT LOSS OF FUNCTION.                              *
*           THE RETURN CODE, THOUGH AVAILABLE, IS NOT EXAMINED       *
*        UPON RETURN BECAUSE THIS ROUTINE PROVIDES ONLY PERFORMANCE  *
*        AND NOT FUNCTIONAL BENEFITS.  'DOUBLE' IS AVAILABLE AS      *
*        A WORK VARIABLE HERE IF REQUIRED FOR FUTURE EXPANSIONS.     *
*        SIMILARLY, THE DYNAMIC PREFIX MAY BE EXPANDED IF NECESSARY. *
*                                                                    *
**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA                                    *
*                     R13-> @DYNAREA                                 *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVSORTE                                 *
*                                                                    *
* DURING PROCESSING   R3 -> GETMAINED AREA                           *
*                                                                    *
* RETURN CODES        R15 = 0  PROCESSING COMPLETE - NOTHING DONE    *
*                     R15 = 2  PROCESSING COMPLETE - SOMETHING DONE  *
*                     R15 = 4  GETMAIN FAILED - NOTHING DONE         *
*                                                                    *
**********************************************************************

         USING @DYNAREA,R13
REVSORTE CSECT
         B     @SORTE-*(,R15)
         DC    AL1(9),CL9'REVSORTE'
@SORTE   STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVSORTE,R11
         L     R2,FREEMRG0         GET SIZE OF @DYNAREA
         LA    R5,DIRENTS          POINT TO IN-CORE ENTRIES
         L     R4,LASTADDR         POINT TO LAST IN-CORE ENTRY
         SR    R4,R5               GET SIZE OF DIRECTORY DATA
         LA    R15,@DYNAREA
         SR    R5,R15              GET SIZE OF @DYNAREA NOT IN DIRENTS
         SR    R2,R5               GET SIZE OF DIRENTS
         LA    R2,16(,R2)          GET SIZE OF NEW DYNAMIC AREA
         LR    R0,R2               COPY IT
         GETMAIN RC,LV=(0)         GET MAIN STORAGE
         LTR   R15,R15             WAS THE GETMAIN SUCCESSFUL?
         BZ    SHELLIT             YES SO PROCEED
         L     R14,12(,R13)        NO, SO LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE OTHER REGISTERS
         BR    R14                 RETURN WITH GETMAIN RETURN CODE

SHELLIT  ST    R15,DOUBLE          CLEAR A WORD FOR FLAGS
         LR    R3,R1               COPY NEW AREA START ADDRESS
         STM   R2,R4,0(R3)         SAVE FREEMAIN VALUES AND DIR SIZE
         LA    R4,DIRENTS          POINT TO CURRENT FIRST ENTRY
         L     R5,LASTADDR         POINT TO CURRENT LAST ENTRY
         LA    R6,16(,R3)          POINT TO FUTURE FIRST ENTRY
         LR    R7,R6
         A     R7,8(,R3)           POINT PAST FUTURE LAST DATA ENTRY
         BCTR  R5,0
         BCTR  R5,0                POINT TO LAST DATA LENGTH COUNT

SHELLOOP SH    R5,0(,R5)           BACK UP AN ENTRY
         LA    R8,2(,R5)           ASSUME POSITION SWAP TO BE DONE
         LR    R10,R4
         LA    R1,2                INITIALIZE "SHELL SWAPPED" FLAG
         TM    STATUS3,DATESORT    SORT BY DATE AFTER SORT BY TTR?
         BO    LOADSHEL            YES, JUST REVERSE POSITIONS
         TM    STATUS3,TTRSORT     SORTING BY TTR?
         BO    TTRSHELL            YES, GO TEST TTRS
         CLC   0(10,R4),2(R5)      NO, COMPARE MEMBER NAMES + CONCAT NO
         BH    LOADSHEL            OUT OF ORDER SO SWAP THEM
KEEPSHEL XR    R8,R10              MEMBERS ARE IN ORDER
         XR    R10,R8                      SO PRESERVE
         XR    R8,R10                      THEIR POSITIONS
         SLR   R1,R1               RESET "SHELL SWAPPED FLAG"
         B     LOADSHEL            GO COPY THIS SHELL'S TWO ENTRIES

LOADSHL1 MVC   0(0,R6),0(R8)       <<< EXECUTED >>>
LOADSHL2 MVC   0(0,R7),0(R10)      <<< EXECUTED >>>
SETSHLRC OI    DOUBLE+3,0          <<< EXECUTED >>>

TTRSHELL CLC   8(6,R4),10(R5)      COMPARE MEMBER CONCAT NO.,TTR,ALIAS
         BNH   KEEPSHEL            CORRECT ORDER SO DO NOT SWAP THEM
LOADSHEL EX    R1,SETSHLRC         PERHAPS SET "SOMETHING DONE" FLAG
         LH    R1,14(,R8)          GET LENGTH OF FUTURE EARLIER ENTRY
         BCTR  R1,0                GET LENGTH CODE
         EX    R1,LOADSHL1         COPY THE EARLIER ENTRY
         LA    R6,1(R1,R6)         POINT PAST THIS ENTRY
         CR    R6,R7               COMPARE UPPER AND LOWER POINTERS
         BE    RSTRSHLS            DONE EVERY MEMBER SO COPY THEM BACK
         BH    SHLABEND            *** LOGIC ERROR ***
         LH    R1,14(,R10)         GET LENGTH OF FUTURE LATER ENTRY
         SR    R7,R1               POINT TO ITS NEW SLOT
         BCTR  R1,0                GET LENGTH CODE
         EX    R1,LOADSHL2         COPY THE LATER ENTRY
         CR    R6,R7               COMPARE UPPER AND LOWER POINTERS
         BE    RSTRSHLS            DONE EVERY MEMBER SO COPY THEM BACK
         AH    R4,14(,R4)          ADJUST EARLIER SOURCE POINTER
         B     SHELLOOP            PROCESS NEXT SHELL

RSTRSHLS CLI   DOUBLE+3,0          ANY SHELL SWAPPED AT ALL?
         BE    SHLOADED            NO, SO DON'T RESTORE IDENTICAL DATA
         L     R4,8(,R3)           GET LENGTH OF ALL ENTRIES TO RESTORE
         LA    R6,DIRENTS          GET RESTORE TARGET ADDRESS
         LA    R5,16(,R3)          GET RESTORE SOURCE ADDRESS
         LA    R1,256              GET MAXIMUM MVC INSTRUCTION CAPACITY
SHELLDLP CR    R4,R1               LESS THAN 256 BYTES TO MOVE?
         BL    SHLOADND            YES, JUST LOAD THE END
         MVC   0(256,R6),0(R5)     LOAD 256 BYTES
         AR    R5,R1               ADJUST SOURCE POINTER
         AR    R6,R1               ADJUST TARGET POINTER
         SR    R4,R1               ADJUST REMAINING DATA COUNTER
         BM    SHLABEND            *** LOGIC ERROR ***
         BZ    SHLOADED            THAT'S THE END (MULTIPLE OF 256)
         B     SHELLDLP            DO ANOTHER LOAD LOOP
SHELDEND MVC   0(0,R6),0(R5)       <<< EXECUTED >>>
SHLOADND BCTR  R4,0                GET LENGTH CODE OF END OF DATA
         EX    R4,SHELDEND         LOAD THE END OF THE ENTRIES

SHLOADED LM    R0,R1,0(R3)         LOAD FREEMAIN REGISTER VALUES
         FREEMAIN RU,LV=(0),A=(1)  FREE THE TEMPORARY HOLD AREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         L     R15,DOUBLE          EVERYTHING IS HUNKY-DORY SO SET RC
         BR    R14                 TIME FOR A BUBBLE BATH
SHLABEND DC    H'193'              X'00C1' FOR INTENTIONAL S0C1 ABEND

         DROP  R11,R13             REVSORTE, @DYNAREA

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V S O R T E   -   D Y N A M I C   A R E A  '
**********************************************************************
*                                                          *         *
*         FORMAT FOR REVSORTE DYNAMIC HOLD AREA            *  GP@P6  *
*                                                          *         *
**********************************************************************

*        +0    SIZE OF THIS GETMAINED AREA  (USED BY FREEMAIN)
*        +4    ADDRESS OF THIS GETMAINED AREA  (USED BY FREEMAIN)
*        +8    SIZE OF DIRENTS DATA ENTRIES (BEFORE LASTADDR)
*        +C    SPARE (FOR DOUBLEWORD ALIGNMENT)
*       +10    END OF REVSORTE FIXED LENGTH DYNAMIC PREFIX
*       +10    REORDERED INTERNAL ENTRIES
*              +10    FIRST REORDERED INTERNAL ENTRY MEMBER NAME
*              +18    FIRST REORDERED INTERNAL ENTRY REL. CONCAT. NO.
*              +1A    FIRST REORDERED INTERNAL ENTRY TTR
*              +1D    FIRST REORDERED INTERNAL ENTRY ALIAS FLAG + COUNT
*              +1E    FIRST REORDERED INTERNAL ENTRY LENGTH FIELD


*         PERFORMANCE COMPARISON TEST RESULTS
*
*  THE FOLLOWING TEST WAS DONE FOR SEVERAL PARTITIONED DATA SETS:
*  TTR-SORT, SIZE-SORT, SSI/VV.MM-SORT, TTR-SORT, DATE-SORT, NAME-SORT
*
*  "NEW" = REVIEW USING REVSORTE.   "OLD" = REVIEW WITHOUT REVSORTE.
*
*  DATA-TYPE MEMBER-COUNT NEW-TCB-TIME/OLD-TCB-TIME
*  --------- ------------ -------------------------
*   LINKLIB      2041          82.9%
*   FORMAT       2491          74.8%
*   ACBLIB        661          59.2%
*   REFERAL       460          51.9%
*   SPFEDIT        10          85.7%  (NOT INVOKED WITH < 10 MEMBERS)
*  **TOTAL**                   75.9%  (176.89/232.98 4381 CPU SECS)
         TITLE '  R E V O F F L D  '
**********************************************************************
*                                                          *         *
*         REVIEW PDS MEMBER OFFLOAD PROCESSOR              *  GP@P6  *
*                                                          *  02/90  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF OFFLOADING       *
*        ALL OR SOME MEMBERS OF THE PDS BEING REVIEWED TO THE        *
*        SEQUENTIAL FILE SPECIFIED BY THE SYSUT2 DDNAME.  SYSUT2     *
*        MUST BE PRE-ALLOCATED BEFORE OFFLOADING IS ALLOWED.         *
*           THE OFFLOAD OUTPUT IS IN IEBUPDTE-LIKE FORMAT WITH       *
*        './ ADD ' STATEMENTS PRECEEDING EACH MEMBER'S DATA.  ONLY   *
*        FIXED LENGTH OR VARIABLE LENGTH RECORD FORMAT FILES CAN     *
*        BE OFFLOADED.  THE LOGICAL RECORD LENGTH OF SYSUT2 WILL     *
*        BE THAT OF THE INPUT PDS OR 80, WHICHEVER IS GREATER.       *
*        OFFLOAD SUPPORTS ALL ALLOWABLE LRECL VALUES GREATER THAN    *
*        ZERO AND LESS THAN 32K.                                     *
*           STANDARD 4-BYTE SSI INFORMATION FROM MEMBER USER-DATA    *
*        IS SUPPLIED ON THE CONTROL STATEMENTS, AS IS SPF STATS      *
*        IN "PDSLOAD" FORMAT.  ANY OTHER FORM OF USER DATA IS NOT    *
*        OFFLOADED.                                                  *
*           MEMBERS ARE OFFLOADED IN MEMBER SELECTION LIST ORDER.    *
*        WHEN OFFLOADING MEMBERS SORTED IN TTR ORDER './ ALIAS '     *
*        STATEMENTS WILL BE GENERATED FOR ALIASES OF OFFLOADED       *
*        REAL MEMBERS.  FOR CONCATENATED FILES WITH DUPLICATELY      *
*        NAMED MEMBERS, THE FIRST OCCURRENCE OF A MEMBER NAME        *
*        WILL BE OFFLOADED FOR EVERY OCCURRENCE OF THE MEMBER IN     *
*        THE CONCATENATION EXCEPT WHEN MEMBERS ARE SORTED IN         *
*        COLLATING SEQUENCE ORDER, WHEN A GIVEN MEMBER NAME WILL     *
*        ONLY BE OFFLOADED ONCE.                                     *
*           OFFLOAD OUTPUT CAN BE USED AS INPUT TO IEBUPDTE ONLY     *
*        TO RECREATE MEMBERS FROM FIXED LENGTH RECORD PDS'S WITH     *
*        RECORD LENGTHS IN THE RANGE OF 1 TO 80.  IEBUPDTE WILL      *
*        NOT RECREATE SPF STATISTICS.  IN ORDER TO RECREATE SPF      *
*        STATISTICS AND/OR VARIABLE LENGTH RECORDS AND/OR FIXED      *
*        LENGTH RECORDS WITH MORE THAN 80 BYTES, A USER DEVELOPED    *
*        UTILITY SUCH AS PDSLOAD OR UPDTE MUST BE USED.              *
*           WHENEVER THE CHARACTERS './' ARE ENCOUNTERED AS THE      *
*        FIRST TWO DATA BYTES OF A RECORD, THEY WILL BE REPLACED     *
*        BY THE CHARACTERS '><' IN THE OUTPUT FILE.                  *
*           ATTENTION INTERRUPTS CAN PREMATURELY TERMINATE THE       *
*        OFFLOAD PROCESS, BUT ONLY AT THE END OF A MEMBER.  ONCE     *
*        PROCESSING THE LAST MEMBER HAS STARTED THE OFFLOAD WILL     *
*        RUN TO COMPLETION.  THE '*INT*' STRING IN THE './ ENDUP '   *
*        CARD INDICATES TERMINATION DUE TO ATTENTION INTERRUPT.      *
*           IF '=SEQLOAD' IS THE COMMAND (AND NOT '=OFFLOAD')        *
*        THEN THE SAME PROCESSING OCCURS EXCEPT THAT THE IEBUPDTE-   *
*        LIKE CONTROL STATEMENTS ARE NOT GENERATED, AND DATA         *
*        RECORDS BEGINNING WITH './' WILL NOT HAVE THEIR FIRST TWO   *
*        DATA BYTES CHANGED TO '><' UPON OUTPUT TO SYSUT2.  THE      *
*        OUTPUT LRECL WILL BE SET TO THAT OF THE PDS EVEN IF < 80.   *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA         (INPUT COMMAND IN 'DOUBLE' *
*                     R13-> @DYNAREA                    UPON ENTRY)  *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVOFFLD                                 *
*                                                                    *
* DURING PROCESSING   R3 -> @DYNAREA                                 *
*                     R5 -> SYSUT2 OUTPUT DCB                        *
*                     R13-> @OFFLDWK (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 = 0  PROCESSING COMPLETE                   *
*                                                                    *
**********************************************************************

REVOFFLD CSECT
         ENTRY STOFFDCB
         B     @OFFLD-*(,R15)
         DC    AL1(9),CL9'REVOFFLD'
@OFFLD   STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVOFFLD,R11
         LA    R0,@OFFWKLN
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R3,R13              POINT TO @DYNAREA
         USING @DYNAREA,R3
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @OFFLDWK,R13
         XC    OFFPRVWA,OFFPRVWA   ZERO PREVIOUS MEMBER DETAILS
         SLR   R0,R0
         ST    R0,OFFVREC          ZERO RDW
         MVC   OFFDCB(OFFDCBLN),STOFFDCB    INITIALIZE OUTPUT DCB
         LA    R5,OFFDCB
         USING IHADCB,R5           (LET USER OR DFP CHOOSE BLKSIZE)
         MVC   DCBDDNAM,OFFDDNAM   SUPPLY FILE NAME TO USE
         MVC   DCBRECFM,MYDSCB-44+84
         TM    DCBRECFM,X'C0'      PROGRAM LIBRARY?
         BM    NOPGMOFF            NO
         MVI   DCBRECFM,X'90'      YES, SET RECFM=FB
         SLR   R1,R1               ENSURE LRECL=80
         B     USEUPDTE            ENSURE CONTROL STATEMENTS WRITTEN
NOPGMOFF NI    DCBRECFM,X'C0'      MAKE RECFM=F OR RECFM=V
         OI    DCBRECFM,X'10'      MAKE RECFB=FB OR RECFM=VB
         LH    R1,MYDSCB-44+88     LOAD THE LOGICAL RECORD LENGTH
         STH   R1,DCBLRECL         SUPPLY OUTPUT RECORD LENGTH
         CLI   DOUBLE+1,C'O'       WAS THIS AN =OFFLOAD?
         BNE   OFFLDOPN            NO, MUST BE =SEQLOAD
USEUPDTE OI    STATUS3,UPDTECTL    YES, REMEMBER TO MAKE CONTROL CARDS
         LA    R0,80               GET MINIMUM OUTPUT RECORD LENGTH
         CR    R1,R0               RECORD LENGTH LONG ENOUGH?
         BNL   OFFLDOPN            YES
         STH   R0,DCBLRECL         NO, MAKE RECORD LENGTH THE MINIMUM

OFFLDOPN MVI   OPEND,X'80'         OPEN THE OUTPUT DCB
         OPEN  ((R5),OUTPUT),MF=(E,OPEND)
         LA    R4,DIRENTS          POINT TO FIRST INTERNAL ENTRY
         OI    MODE,OFFLDSW        NOW IN OFFLOAD MODE

OFFMEMLP C     R4,LASTADDR         END OF INTERNAL DIRECTORY ENTRIES?
         BNL   OFFENDUP            YES, END UP
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    OFFATTN             YES, TERMINATE OFFLOAD
         BAL   R14,OFFALIAS        CHECK ./ ALIAS STUFF
         ICM   R14,15,OFFLDCNT     OFFLOADING EVERYTHING?
         BZ    OFFITALL            YES
         TM    13(R4),X'40'        NO, OFFLOADING THIS ONE?
         BZ    OFFNXTMM            NO, TRY THE NEXT
OFFITALL TM    STATUS3,SORTMODE    ARE ENTRIES IN NAME ORDER?
         BNZ   OFFNEWNM            NO, DON'T CHECK FOR DUPLICATES
         CLC   MEMMEMNM,0(R4)      YES, IDENTICAL MEMBER NAMES?
         BNE   OFFNEWNM            NO, PROCEED WITH OFFLOAD
         MVC   OFFFREC(L'DUPMEMSG),DUPMEMSG
         MVC   OFFFREC+20(8),0(4)  NOTIFY USER OF DUPLICATE MEMBER
         LA    R1,OFFFREC
         LA    R0,L'DUPMEMSG
         TPUT  (1),(0),R           LINE MODE I/O SHOULD GET NOTICED
         B     OFFNXTMM            GO GET NEXT MEMBER TO PROCESS
OFFNEWNM TM    MYDSCB-44+84,X'C0'  RECFM=U?
         BNO   OFFNOTOV            NO, CAN'T BE PROGRAM PDS
         TM    13(R4),X'20'        ANY TTRS NOTED IN USERDATA?
         BZ    OFFNOTOV            NO, CAN'T BE A PROGRAM
         TM    16(R4),X'20'        OVERLAY PROGRAM?
         BZ    OFFNOTOV            NO, HOPE FOR NO NOTE LIST
         MVC   OFFFREC(L'OVRLYMSG),OVRLYMSG
         MVC   OFFFREC+L'OVRLYMSG(8),0(4)
         LA    R1,OFFFREC
         LA    R0,L'OVRLYMSG+8
         TPUT  (1),(0),R           LINE MODE I/O SHOULD GET NOTICED
         B     OFFNXTMM            GO GET NEXT MEMBER TO PROCESS
OFFNOTOV MVI   OFFFREC,C' '        BLANK THE CONTROL STATEMENT RECORD
         MVC   OFFFREC+1(255),OFFFREC
         MVC   OFFFREC(L'ADDPREF),ADDPREF
         MVC   OFFFREC+L'ADDPREF(8),0(R4)
         LA    R1,OFFFREC+L'ADDPREF+8
OFFTRLP1 BCTR  R1,0                BACK UP ONE BYTE
         CLI   0(R1),C' '          TRAILING BLANK?
         BE    OFFTRLP1            YES
         BAL   R14,OFFDATA         APPEND ANY RELEVANT USER DATA
         BAL   R14,OFFSTMT         OUTPUT THE ADD CONTROL STATEMENT
OFFCALL2 MVC   $MEMBER,0(R4)       SUPPLY THE MEMBER NAME TO PHASE 2
         SLR   R0,R0
         STH   R0,RC               ZERO ANY PREVIOUS BLDL FAILURE
         L     R0,CBPRM3           SAVE REVPODIR TPUT BUFFER ADDRESS
         ST    R5,CALLPARM         SAVE THE SYSUT2 DCB ADDRESS
         LA    R15,OFFFREC+80      POINT TO EIGHTY BLANKS
         ST    R15,CALLPARM+4      PASS IT TO PHASE 2
         LA    R15,POSAVE2         POINT TO NEW SAVE AREA
         ST    R15,8(,R13)         LINK SAVE AREAS
         ST    R13,4(,R15)
         L     R15,=V(REVIEW2)     GET ADDRESS OF PHASE 2
         STM   R14,R12,12(R13)     SAVE ALL REGISTERS
         L     R13,8(,R13)         POINT TO NEW SAVE AREA
         BALR  R14,R15             CALL REVIEW PHASE 2
         L     R13,4(,R13)         POINT TO OLD SAVE AREA
         LM    R0,R12,20(R13)      RESTORE ALL EXCEPT BALR REGISTERS
         ST    R0,CBPRM3           RESTORE REVPODIR TPUT BUFFER ADDRESS
         MVC   OFFLDPRV,0(R4)      SAVE LATEST OFFLOADED MEMBER'S STUFF
         LH    R15,RC              GET BLDL RETURN CODE
         LTR   R15,R15             WAS THE MEMBER FOUND?
         BZ    OFFGOTMM            YES, CONTINUE
         XC    OFFLDPRV,OFFLDPRV   INVALIDATE PREVIOUS MEMBER DETAILS
         MVI   OFFFREC,C' '        BLANK THE CONTROL STATEMENT RECORD
         MVC   OFFFREC+1(255),OFFFREC
         MVC   OFFFREC(L'BDBLDMSG),BDBLDMSG
         MVC   OFFFREC+L'BDBLDMSG(8),0(R4)
         LA    R1,OFFFREC
         LA    R0,L'BDBLDMSG+8
         STH   R0,OFFVREC          UPDATE RDW
         TPUT  (1),(0),R           LINE MODE I/O SHOULD GET NOTICED
         BAL   R14,OFFSTMT         SUPPLY DUMMY DATA FOR IEBUPDTE
         B     OFFNXTMM
OFFGOTMM TM    MYDSCB-44+84,X'C0'  RECFM=U?
         BNO   OFFNXTMM            NO, MEMBER CAN'T BE A LOAD MODULE
         IC    R1,BLDLINDC
         LA    R0,31
         NR    R1,R0               GET THE USERDATA HALFWORD COUNT
         SLA   R1,1                GET THE USERDATA BYTE COUNT
         MVI   OFFFREC,C' '        BLANK THE CONTROL STATEMENT RECORD
         MVC   OFFFREC+1(255),OFFFREC
         MVC   OFFFREC(L'STOWPREF),STOWPREF
         LA    R15,OFFFREC+L'STOWPREF
         EX    R1,OFFLDSTW         SAVE STOW DATA
         BAL   R14,OFFSTMT         WRITE STOW DATA
OFFNXTMM MVC   MEMBRPRV,0(R4)      SAVE THIS MEMBER'S DETAILS
OFFNXTAL AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         B     OFFMEMLP            PROCESS THE NEXT DIRECTORY ENTRY
OFFLDSTW MVC   0(0,R15),BLDLINDC   <<< EXECUTED >>>

OFFENDUP NI    STATUS4,255-FLAGI   RESET ATTENTION FLAG
OFFATTN  MVI   OFFFREC,C' '        BLANK THE CONTROL STATEMENT RECORD
         MVC   OFFFREC+1(255),OFFFREC
         MVC   OFFFREC(L'ENDUPREF),ENDUPREF
         TIME  DEC
         ST    R0,DOUBLE           SHOW THE CURRENT TIME (HH:MM)
         UNPK  OFFFREC+L'ENDUPREF+1(3),DOUBLE(2)     HH
         MVI   OFFFREC+L'ENDUPREF+3,C':'
         UNPK  OFFFREC+L'ENDUPREF+4(3),DOUBLE+1(2)   MM
         MVC   OFFFREC+L'ENDUPREF+6(3),=C' ON'
         LR    R6,R4               SAVE IDRDATE WORK REGISTERS
         LR    R7,R5
         LR    R4,R1               COPY THE CURRENT DATE
         L     R15,=V(IDRDATE)     SHOW THE CURRENT DATE (YY/MM/DD)
         BALR  R14,R15
         LR    R4,R6               RESTORE IDRDATE WORK REGISTERS
         LR    R5,R7
         MVC   OFFFREC+L'ENDUPREF+10(8),DOUBLE
         LA    R1,OFFFREC+L'ENDUPREF+18
         BAL   R14,OFFDATAL        UPDATE RDW
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BZ    OFFFINAL            NO, CONTINUE
         MVC   OFFFREC+9(5),=C'*INT*'  YES, FLAG INTERRUPTION
OFFFINAL BAL   R14,OFFSTMT         OUTPUT THE ENDUP CONTROL STATEMENT
         MVI   CLOSED,X'80'         CLOSE THE OUTPUT DCB
         CLOSE ((R5)),MF=(E,CLOSED)
         NI    MODE,255-OFFLDSW       NO LONGER IN OFFLOAD MODE
         NI    STATUS3,255-UPDTECTL   CLEAN UP STATUS3
         LA    R0,@OFFWKLN
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @OFFLDWK
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE OTHER REGISTERS
         BR    R14                 RETURN WITH FREEMAIN RETURN CODE


OFFALIAS TM    13(R4),X'80'        ALIAS MEMBER?
         BZR   R14                 NO, PROCESS AS USUAL
         TM    STATUS3,TTRSORT     ARE ENTRIES IN TTR ORDER?
         BZR   R14                 NO, PROCESS ALIAS AS REAL MEMBER
         CLC   8(5,R4),OFFCONCT    YES, IS THIS ALIAS OF OFFLOADED MEM?
         BNE   OFFORFAN            NO, CHECK FOR ORPHAN ALIAS
         MVI   OFFFREC,C' '        BLANK THE CONTROL STATEMENT RECORD
         MVC   OFFFREC+1(255),OFFFREC
         MVC   OFFFREC(L'ALIASPRF),ALIASPRF
         MVC   OFFFREC+L'ALIASPRF(8),0(R4)
         LA    R0,L'ALIASPRF+8+4
         STH   R0,OFFVREC          SET THE RDW
         BAL   R14,OFFSTMT         OUTPUT THE ALIAS CONTROL STATEMENT
         TM    MYDSCB-44+84,X'C0'  RECFM=U?
         BO    OFFCALL2            YES, GET DIRECTORY ENTRY USERDATA
         B     OFFNXTAL            ALIAS SUCCESSFULLY PROCESSED
OFFORFAN CLC   8(5,R4),MEMCONCT    IS THIS AN ALIAS OF CURRENT MEMBER?
         BE    OFFNXTAL            YES, SO IT IS NOT AN ORPHAN
         MVC   OFFFREC(L'ORPHNMSG),ORPHNMSG
         MVC   OFFFREC+16(8),0(4)  NOTIFY USER OF ORPHAN ALIAS
         LA    R1,OFFFREC
         LA    R0,L'ORPHNMSG
         TPUT  (1),(0),R           LINE MODE I/O SHOULD GET NOTICED
         B     OFFNXTAL            ALIAS SUCCESSFULLY IGNORED


OFFDATA  CLI   15(R4),22           AN SSI INTERNAL ENTRY?
         BNE   OFFNTSSI            NO
         MVC   1(5,R1),=C',SSI='   YES
         UNPK  6(9,R1),16(5,R4)    SUPPLY 8 HEX SSI DIGITS
         TR    6(8,R1),OFFLDHEX-C'0'
         MVI   14(R1),C' '         ERASE GARBAGE
         LA    R1,14(,R1)          POINT PAST END OF RECORD DATA
         B     OFFDATAL            UPDATE RDW FOR VARIABLE LENGTHS
OFFNTSSI LA    R1,1(,R1)           POINT TO FIRST BLANK
         CLI   15(R4),46           AN SPF STATISTICS INTERNAL ENTRY?
         BNE   OFFDATAL            NO
         SLR   R0,R0
         IC    R0,16(,R4)                 MODIFICATION VERSION
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  OFFFREC+21(2),DOUBLE+6(2)
         IC    R0,17(,R4)                 MODIFICATION LEVEL
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  OFFFREC+23(2),DOUBLE+6(2)
         MVI   OFFFREC+25,C'-'
         UNPK  OFFFREC+26(5),21(3,R4)         CREATION DATE
         MVI   OFFFREC+31,C'-'
         UNPK  OFFFREC+32(5),25(3,R4)     MODIFICATION DATE
         MVI   OFFFREC+37,C'-'
         UNPK  OFFFREC+38(5),28(3,R4)     MODIFICATION TIME
         MVI   OFFFREC+42,C'-'
         ICM   R0,B'0011',30(R4)               CURRENT SIZE
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  OFFFREC+43(5),DOUBLE+5(3)
         MVI   OFFFREC+48,C'-'
         ICM   R0,B'0011',32(R4)               INITIAL SIZE
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  OFFFREC+49(5),DOUBLE+5(3)
         MVI   OFFFREC+54,C'-'
         ICM   R0,B'0011',34(R4)              MODIFIED COUNT
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  OFFFREC+55(5),DOUBLE+5(3)
         MVI   OFFFREC+60,C'-'
         MVC   OFFFREC+61(8),36(R4)          MODIFYING USERID
         LA    R1,OFFFREC+69
OFFDATAL LA    R0,OFFVREC          POINT TO START OF RECORD
         SR    R1,R0               GET DATA BYTE COUNT
         STH   R1,OFFVREC          SUPPLY RDW
         BR    R14


OFFSTMT  TM    STATUS3,UPDTECTL    IEBUPDTE CONTROL CARDS REQUIRED?
         BZR   R14                 NO, RETURN WITHOUT ACTION
         ST    R14,DOUBLE          YES, SAVE RETURN ADDRESS
         LR    R1,R5               POINT TO DCB
         LA    R0,OFFVREC
         TM    DCBRECFM,X'80'      FIXED LENGTH RECORDS?
         BZ    OFFSTMTX            NO, MUST BE VARIABLE
         LA    R0,OFFFREC          YES
         LA    R15,256
         CH    R15,DCBBLKSI        RECORD LONGER THAN 256?
         BL    OFFLDBIG            YES
OFFSTMTX PUT   (1),(0)             WRITE A RECORD
         L     R14,DOUBLE          RESTORE RETURN ADDRESS
         BR    R14
OFFLDBIG LH    R15,DCBBLKSI        YES, LOAD IT
         LA    R0,7(,R15)
         SRL   R0,3
         SLL   R0,3                ROUND UP TO DOUBLEWORD BOUNDARY
         LR    R6,R0               COPY SIZE
         GETMAIN R,LV=(0)          GET SOME STORAGE FOR THIS RECORD
         LR    R7,R1               COPY ITS ADDRESS
         LR    R0,R7               TARGET ADDRESS
         LR    R1,R6               TARGET SIZE
         LA    R14,OFFFREC         SOURCE ADDRESS
         LA    R15,256             SOURCE SIZE
         ICM   R15,8,OFFFREC+2     BLANK PAD BYTE
         MVCL  R0,R14              CREATE FULL SIZE OUTPUT RECORD
         LR    R1,R5               POINT TO DCB
         LR    R0,R7               POINT TO RECORD
         PUT   (1),(0)             WRITE A RECORD
         LR    R0,R6               LOAD RECORD AREA LENGTH
         LR    R1,R7               LOAD RECORD AREA ADDRESS
         FREEMAIN R,LV=(0),A=(1)   FREE TEMPORARY RECORD AREA
         L     R14,DOUBLE          RESTORE RETURN ADDRESS
         BR    R14


         DROP  R11,R3,R13,R5       REVOFFLD, @DYNAREA, @OFFLDWK, IHADCB

         TITLE '  R E V O F F L D   -   S T A T I C   A R E A  '
OFFLDHEX DC    C'0123456789ABCDEF'
         PRINT NOGEN

STOFFDCB DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2
OFFDCBLN EQU   *-STOFFDCB

         PRINT GEN
ADDPREF  DC    C'./ ADD NAME='
ALIASPRF DC    C'./ ALIAS NAME='
ENDUPREF DC    C'./ ENDUP       "REVIEW" PDS MEMBER OFFLOAD AT'
STOWPREF DC    C'./ STOW DATA='
BDBLDMSG DC    C'** BLDL FAILED DURING OFFLOAD FOR MEMBER '
ORPHNMSG DC    C'** ORPHAN ALIAS ???????? NOT OFFLOADED'
DUPMEMSG DC    C'** DUPLICATE MEMBER ???????? NOT OFFLOADED'
OVRLYMSG DC    C'** CANNOT OFFLOAD OVERLAY PROGRAM '
OFFTMSTP DC    X'4021207A20204020204B202020'

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V O F F L D   -   D Y N A M I C   A R E A  '
@OFFLDWK DSECT
OFFSAVE  DS    18F
OFFPRVWA DS    0XL32               OFFLOAD PREVIOUS MEMBER WORK AREA
OFFLDPRV DS    0XL16               INTERNAL ENTRY COMMON BASE SECTION
OFFMEMNM DS    CL8                 MEMBER NAME
OFFCONCT DS    H                   RELATIVE CONCATENATION NUMBER
OFFLDTTR DS    XL3                 TTR OF FIRST BLOCK OF MEMBER
OFFLDFLG DS    X                   ALIAS FLAG AND USER DATA H/W COUNT
OFFENTLN DS    H                   BYTE COUNT OF WHOLE INTERNAL ENTRY
MEMBRPRV DS    0XL16               INTERNAL ENTRY COMMON BASE SECTION
MEMMEMNM DS    CL8                 MEMBER NAME
MEMCONCT DS    H                   RELATIVE CONCATENATION NUMBER
MEMBRTTR DS    XL3                 TTR OF FIRST BLOCK OF MEMBER
MEMBRFLG DS    X                   ALIAS FLAG AND USER DATA H/W COUNT
MEMENTLN DS    H                   BYTE COUNT OF WHOLE INTERNAL ENTRY
OFFDCB   DS    0F,XL(OFFDCBLN)     OFFLOAD OUTPUT DCB
OFFVREC  DS    F                   VARIABLE LENGTH RECORD RDW
OFFFREC  DS    CL256               IEBUPDTE-TYPE CONTROL STATEMENT
         DS    0D                  END OF DSECT
@OFFWKLN EQU   *-@OFFLDWK
         TITLE '  R E V S R C H  '
**********************************************************************
*                                                          *         *
*         REVIEW PDS MEMBER GLOBAL FIND PROCESSOR          *  GP@FT  *
*                                                          *  07/96  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF SEARCHING        *
*        PDS MEMBERS FOR SELECTED CHARACTER STRINGS.  MORE THAN      *
*        ONE MEMBER CAN BE SEARCHED IN ONE INVOCATION.  MEMBERS      *
*        CONTAINING THE SPECIFIED DATA ARE HIGHLIGHTED IN THE        *
*        MEMBER LIST, AND BECOME THE MEMBERS SELECTED FOR OFFLOAD    *
*        IF AN OFFLOAD OPERATION WERE TO BE STARTED.                 *
*                                                                    *
**********************************************************************

**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA         (INPUT COMMAND IN 'DOUBLE' *
*                     R13-> @DYNAREA                    UPON ENTRY)  *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVSRCH                                  *
*                                                                    *
* DURING PROCESSING   R3 -> @DYNAREA                                 *
*                     R5 -> SYSUT2 OUTPUT DCB                        *
*                     R13-> @SCHWORK (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 = 0  PROCESSING COMPLETE                   *
*                                                                    *
**********************************************************************

REVSRCH  CSECT
         B     @SEARCH-*(,R15)
         DC    AL1(7),CL7'REVSRCH'
@SEARCH  STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVSRCH,R11
         LA    R0,@SCHWKLN
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R3,R13              POINT TO @DYNAREA
         USING @DYNAREA,R3
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @SCHWORK,R13
         MVC   SRCHLAST,LASTADDR   COPY ADDRESS OF LAST INTERNAL ENTRY
         LA    R0,DIRENTS          POINT TO FIRST INTERNAL ENTRY
         ST    R0,SRCHMEM1         SAVE ITS ADDRESS
         LA    R0,POSAVE2          POINT TO NEW SAVE AREA
         ST    R0,SRCHSADR         SAVE ITS ADDRESS
         DROP  R3                  @DYNAREA
         MVC   SRCHMCNT,OFFLDCNT   COPY HIGHLIGHTED MEMBER COUNT
         MVC   SRCHFCNT,SRCHMCNT   PRESERVE STATUS IF SEARCH CANCELLED
         OC    RECSIZE,RECSIZE     RECSIZE INITIALIZED?
         BNZ   RECSIZED            YES
         LH    R0,MYDSCB-44+86     GET BLKSIZE FROM VTOC
         TM    MYDSCB-44+84,X'C0'  UNDEFINED RECORD FORMAT?
         BNM   RECSIZOK            YES, USE BLKSIZE
         LH    R0,MYDSCB-44+88     GET LRECL FROM VTOC
         TM    MYDSCB-44+84,X'80'  FIXED LENGTH RECORDS?
         BO    RECSIZOK            YES, USE LRECL
         SH    R0,=H'4'            NO, ALLOW FOR RDW
         BNP   RECSIZED            JUST IN CASE OF FUNNIES
RECSIZOK STH   R0,RECSIZE          SET MAXIMUM DISPLAY COLUMN NUMBER
RECSIZED MVC   SRCHMMSK,SRCHSTAR   INITIALIZE MEMBER NAME PATTERN MASK
         LA    R3,SRCHSCRL         GET THE PROMPTING SCREEN SIZE
         LA    R4,SRCHSCRN         POINT TO PROMPTING SCREEN SOURCE
         LA    R5,SRCHSDYN         POINT TO THE TPUT BUFFER
         LA    R6,SRCHTYPE         POINT TO FIRST VARIABLE SOURCE
SRCHOTLP CLI   0(R4),SBA           SBA ORDER?
         BE    SRCHOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),RA            RA ORDER?
         BE    SRCHOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),SA            SA ORDER?
         BE    SRCHOTSA            YES, HANDLE EXTENDED DATA STREAM
         CR    R4,R6               REACHED A VARIABLE LOCATION?
         BE    SRCHVARI            YES, GO LOAD VARIABLE DATA
SRCHTYOK MVC   0(1,R5),0(R4)       COPY A BYTE TO THE TPUT BUFFER
         LA    R4,1(,R4)           INCREMENT SOURCE POINTER
         LA    R5,1(,R5)           INCREMENT TARGET POINTER
         BCT   R3,SRCHOTLP         PROCESS NEXT BYTE
         B     SRCHOTOK            TRAILER STUFF SHOULD STOP FALL-THRU
SRCHOTAD MVC   0(1,R5),0(R4)       COPY ORDER
         SLR   R1,R1
         IC    R1,1(,R4)           GET THE ROW NUMBER
         BCTR  R1,0                MAKE RELATIVE
         M     R0,SCRNCOLS         GET BYTES ON PRECEDING LINES
         IC    R0,2(,R4)           GET THE COLUMN NUMBER
         BCTR  R0,0                MAKE RELATIVE
         AR    R1,R0               GET SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,1(R5)          SUPPLY ADDRESS FOR THIS TERMINAL
         LA    R0,3
SRCHADJS ALR   R4,R0               ADJUST SOURCE POINTER
         ALR   R5,R0               ADJUST TARGET POINTER
         SR    R3,R0               ADJUST BYTES TO GO
         B     SRCHOTLP            HOPE R3 IS STILL POSITIVE
SRCHOTSA MVC   0(3,R5),0(R4)       COPY COMPLETE SA ORDER STREAM
         LA    R0,3                GET BYTE COUNT INVOLVED
         SR    R3,R0               ADJUST BYTES TO GO
         BNP   SRCHOTOK            SHOULDN'T HAPPEN
         ALR   R4,R0               ADJUST SOURCE POINTER
         TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    SRCHOTLP            NO, DON'T COUNT THIS ORDER
         ALR   R5,R0               YES, ADJUST TARGET POINTER
         B     SRCHOTLP            R3 IS STILL POSITIVE
SRCHVARI LA    R0,SRCHMASK         POINT TO SECOND-LAST VARIABLE
         CR    R6,R0               WHICH VARIABLE?
         BH    SRCHOTCT            MEMBER COUNT
         BE    SRCHOTMS            SEARCH MEMBER NAME PATTERN MASK
         LA    R0,SRCHSPEC         POINT TO SECOND VARIABLE
         CR    R6,R0               WHICH VARIABLE?
         BH    SRCHOTCL            SEARCH COLUMN NUMBER
         BL    SRCHDOTY            SEARCH TYPE
         LA    R6,SRCHCOLM         SET NEXT VARIABLE ADDRESS
         MVC   0(64,R5),0(R4)      COPY NULLS
         LA    R0,64               GET THE LENGTH TO ADJUST
         CLI   STRINGL,0           ANY STRING PRESPECIFIED?
         BNE   SRCHADJS            NO
         LH    R14,STRINGL         GET CURRENT FIND DATA LENGTH CODE
         EX    R14,SRCHTXLD        YES, LOAD IT
         L     R15,SRCHTDYN        POINT TO SEARCH TYPE IN BUFFER
         TM    STATUS8,FPIC        PICTURE STRING SEARCH?
         BO    SRCHTPIC            YES
         TM    STATUS8,FUOL        CASE INSENSITIVE SEARCH?
         BO    SRCHTXOK            YES, PRIMED VALUE IS CORRECT
         MVI   0(R15),C'3'         NO, MAKE IT 'FINDCHR'
         EX    R14,SRCHTHEX        ANY UNPRINTABLES?
         BZ    SRCHTXOK            NO
         MVI   0(R15),C'5'         YES, MAKE IT 'FINDHEX'
         LA    R0,1(,R14)          GET SEARCH BYTE COUNT
         LA    R14,STRING          POINT TO SEARCH DATA
         LR    R7,R5               COPY CURRENT BUFFER POINTER
SRCHEXLP UNPK  0(3,R7),0(2,R14)    UNPACK A BYTE
         TR    0(2,R7),HEXS-C'0'   ENSURE HEX DIGIT
         MVI   2(R7),0             CLEAR GARBAGE
         LA    R7,2(,R7)           ADJUST TARGET POINTER
         LA    R14,1(,R14)         ADJUST SOURCE POINTER
         BCT   R0,SRCHEXLP
         B     SRCHTXOK            HEX DIGITS NOW FORMATTED
SRCHTPIC MVI   0(R15),C'7'         MAKE IT 'FINDPIC'
SRCHTXOK LA    R0,64               GET THE LENGTH TO ADJUST
         TM    STATUS7,FNOT        FINDNOT THE GO?
         BNO   SRCHADJS            NO
         IC    R1,0(,R15)          GET THE SEARCH TYPE CODE
         LA    R1,1(,R1)           ADD 1 FOR 'NOT'
         STC   R1,0(,R15)          SAVE THE NEW SEARCH TYPE CODE
         B     SRCHADJS            PERFORM THE ADJUSTMENTS
SRCHTXLD MVC   0(0,R5),STRING      <<< EXECUTED >>>
SRCHTLOW TRT   0(0,R5),LOWCSTST    <<< EXECUTED >>>
SRCHTHEX TRT   0(0,R5),KEYBDTST    <<< EXECUTED >>>
SRCHDOTY LA    R6,SRCHSPEC         SET NEXT VARIABLE ADDRESS
         ST    R5,SRCHTDYN         SAVE ADDRESS OF TYPE IN BUFFER
         B     SRCHTYOK            PERFORM THE ADJUSTMENTS
SRCHOTCL LA    R6,SRCHMASK         SET NEXT VARIABLE ADDRESS
         MVC   0(5,R5),0(R4)       COPY NULLS
         LA    R0,5                GET THE LENGTH TO ADJUST
         LH    R1,FINDCOL          GET CURRENT FIND COLUMN
         LTR   R1,R1               ANY IS USE?
         BNP   SRCHADJS            NO
         CVD   R1,DOUBLE           YES
         OI    DOUBLE+7,X'0F'
         UNPK  0(5,R5),DOUBLE+5(3) SHOW SEARCH COLUMN NUMBER
         B     SRCHADJS            PERFORM THE ADJUSTMENTS
SRCHOTMS LA    R6,SRCHNMBR         SET NEXT VARIABLE ADDRESS
         MVC   0(8,R5),0(R4)       COPY MEMBER NAME PATTERN MASK
         LA    R0,8                GET THE LENGTH TO ADJUST
         B     SRCHADJS            PERFORM THE ADJUSTMENTS
SRCHOTCT MVC   0(11,R5),0(R4)      COPY MEMBER COUNT TEMPLATE
         ICM   R3,15,SRCHMCNT      ANY HIGHLIGHTED MEMBERS?
         BZ    SRCHOTRL            NO, LEAVE "ALL" MEMBERS IN TEXT
         CVD   R3,DOUBLE           YES
         MVC   0(8,R5),SRCHED7     LOAD EDIT MASK
         ED    0(8,R5),DOUBLE+4    SHOW HIGHLIGHTED MEMBER COUNT
SRCHOTRL TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    *+8                 NO
         LA    R5,3(,R5)           YES, COUNT GREEN ORDER
         MVC   8(SRCHTRLL,R5),SRCHTRLR  COPY NON-EDS SCREEN TRAILER
         LA    R5,8+SRCHTRLL(,R5)  ADJUST BUFFER POINTER
SRCHOTOK LR    R0,R5               POINT PAST END OF DATA STREAM
         LA    R1,SRCHSDYN         POINT TO START OF DATA STREAM
         SR    R0,R1               GET LENGTH OF DATA STREAM
SRCHTPUT ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           SHOW DATA ENTRY PANEL
         XC    REPLY,REPLY         CLEAR RESIDUAL DATA
         LA    R0,256              GET BUFFER SIZE
         LA    R1,REPLY            GET BUFFER ADDRESS
         ICM   R1,8,=X'81'         LOAD FLAGS FOR TGET ASIS,WAIT
         TGET  (1),(0),R           GET USER RESPONSE
         STM   R15,R1,TGETREGS
         LA    R0,X'0F'            RESET "UNINTERESTING" BITS
         NR    R15,R0                 IN TGET RETURN CODE
         CH    R15,=H'12'          WAS REPLY AREA LONG ENOUGH?
         BNE   SRCHTGOT            YES, BRANCH
         TCLEARQ INPUT             NO, FLUSH QUEUED INPUT
SRCHTGOT TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    SRCHEXIT            YES, ABORT THIS REQUEST
         LA    R0,3                GET READ HEADER DATA STREAM LENGTH
         C     R0,TGETREGS+8       PA KEY?  (SHOULDN'T GET CLEAR)
         BH    SRCHOTOK            YES, RESHOW SCREEN
         CLI   REPLY,X'7D'         ENTER KEY?
         BNE   SRCHEXIT            NO, TERMINATE SEARCH IF PF KEY
         LA    R1,REPLY+3          POINT PAST READ HEADER
*
*               PROCESS FIELDS IN TGET BUFFER
*
PARSNXTF LR    R15,R1              POINT TO NEXT FIELD TO PROCESS
         LA    R1,REPLY            POINT TO INPUT BUFFER
         LR    R0,R15              POINT TO SBA OF CURRENT FIELD
         SR    R0,R1               GET LENGTH ALREADY PROCESSED
         L     R14,TGETREGS+8      GET TOTAL LENGTH
         SR    R14,R0              GET LENGTH STILL TO PROCESS
         BNP   SRCHINDN            NOTHING LEFT
         CLI   0(R15),SBA          DOES FIELD START WITH SBA ORDER?
         BNE   SRCHOTOK            NO, UNEXPECTED DATA SO RESEND PANEL
         SLR   R3,R3               CLEAR FOR INSERT
         ICM   R3,3,1(R15)         GET ADDRESS OF FIELD
         TM    1(R15),X'40'        12-BIT ADDRESS?
         BZ    SRCH14AD            NO, 14-BIT ADDRESS
         SLL   R3,2                YES, SHIFT 2 BITS OUT OF LOW BYTE
         ICM   R3,2,1(R15)         RELOAD HIGH BYTE
         SLL   R3,18               SHIFT OUT 2 BITS OF HIGH BYTE
         SRL   R3,20               NOW HAVE 14-BIT ADDRESS
SRCH14AD SLR   R2,R2               CLEAR FOR DIVIDE
         D     R2,SCRNCOLS         GET RELATIVE LINE AND COLUMN
         LTR   R3,R3               INPUT FROM TOP LINE OF SCREEN?
         BNP   SRCHOTOK            YES, USER MUST HAVE HIT CLEAR
         LA    R1,0(R14,R15)       POINT PAST THE INPUT DATA
         L     R2,=V(FINDSBA)      POINT TO SBA TRANSLATE TABLE
         SH    R14,=H'3'           IGNORE FIRST SBA ORDER
         EX    R14,PARSRTRT        FIND THE NEXT SBA
         LR    R14,R1              GET NEW SBA ADDRESS
         SR    R14,R15             GET FIELD LENGTH
         SH    R14,=H'3'           GET DATA LENGTH
         CH    R3,=H'5'            FIELD FROM RELATIVE LINE 5?
         BE    PARSTYPE            YES, GET SEARCH TYPE
         CH    R3,=H'11'           FIELD FROM RELATIVE LINE 11?
         BE    PARSDATA            YES, GET SEARCH DATA
         CH    R3,=H'14'           FIELD FROM RELATIVE LINE 14?
         BE    PARSCOLM            YES, GET SEARCH COLUMN
         CH    R3,=H'17'           FIELD FROM RELATIVE LINE 17?
         BE    PARSMASK            YES, GET SEARCH MEMBER NAME MASK
         B     SRCHOTOK            UNEXPECTED LINE SO RESEND PANEL
PARSRTRT TRT   3(0,R15),0(R2)      <<< EXECUTED >>>
PARSTYPE LTR   R14,R14             ANY DATA?
         BNP   PARSNXTF            NO, DO NOTHING FOR THIS FIELD
         CLI   3(R15),C'1'         VALID TYPE?
         BL    PARSINBD            NO
         CLI   3(R15),C'8'         VALID TYPE?
         BH    PARSINBD            NO
         L     R3,SRCHTDYN         YES, POINT TO TYPE SETTING
         MVC   0(1,R3),3(R15)      SAVE THE REQUESTED SETTING
         B     PARSNXTF            CONTINUE PARSE
PARSDATA L     R3,SRCHTDYN         YES, POINT TO TYPE SETTING
         IC    R0,0(,R3)           GET THE SEARCH TYPE
         BCTR  R0,0                DECREMENT FOR BIT TESTING
         STC   R0,DOUBLE           PUT IT BACK IN STORAGE
         NI    STATUS7,255-FNOT    RESET "FINDNOT" FLAG
         TM    DOUBLE,X'01'        A "FINDNOT" REQUEST?
         BNO   *+8                 NO
         OI    STATUS7,FNOT        YES, FLAG THIS
         NI    DOUBLE,X'FE'        RESET "NOT" BIT
         LTR   R14,R14             ANY DATA SPECIFIED?
         BNP   PARSNULL            NO, CHECK FOR MISSING DATA
         XC    STRING,STRING       ERASE OLD STRING
         NI    STATUS8,255-FUOL    SET CASE INSENSITIVE
         BCTR  R14,0               GET DATA LENGTH CODE
         CLI   DOUBLE,X'F2'        A "FINDCHR" TYPE REQUEST?
         BE    PARSTEXT            YES, LOAD DATA WITHOUT FOLDING
         L     R2,=V(KAPS)         NO, POINT TO TRANSLATE TABLE
         EX    R14,PARSTRAN        FOLD TO UPPER CASE
         CLI   DOUBLE,X'F4'        A HEXADECIMAL DATA REQUEST?
         BNE   PARSTEXT            NO, MUST BE "FIND" OR "FINDPIC" TYPE
         MVI   STRINGL,X'FF'       INVALIDATE 'FIND' STRING
         LA    R2,3(,R15)          POINT TO FIRST DATA BYTE
         LA    R14,1(,R14)         RESTORE LENGTH VALUE
PARSXLP1 CLI   0(R2),C' '          LEADING BLANK?
         BNE   PARSCHKX            NO, CHECK FOR HEXADECIMAL
         LA    R2,1(,R2)           YES, POINT PAST IT
         BCT   R14,PARSXLP1        LOOK FOR NON-BLANK
         B     PARSINBD            INPUT WAS ALL BLANK BUT NEED DATA
PARSCHKX LR    R4,R2               POINT TO START OF DATA
PARSXLP2 CLI   0(R2),C'A'          FOUND NON-HEX?
         BL    PARSXTRL            YES, GO HANDLE TRAILING BLANKS
         CLI   0(R2),C'9'          FOUND NON-HEX?
         BH    PARSINBD            YES, HOW WAS THAT DONE?
         CLI   0(R2),C'0'          FOUND DECIMAL HEX?
         BNL   PARSXNUM            YES, PROCEED
         CLI   0(R2),C'F'          FOUND NON-HEX?
         BH    PARSINBD            YES, INVALID
PARSXNUM LA    R2,1(,R2)           POINT TO NEXT POSSIBLE DIGIT
         BCT   R14,PARSXLP2        VALIDATE NEXT HEX DIGIT
PARSXTRL LR    R3,R2               POINT PAST LAST HEX DIGIT
         LTR   R14,R14             REACHED END OF INPUT FIELD?
         BNP   PARSXTXT            YES, END OF DIGITS SO GET DATA
PARSXLP3 CLI   0(R2),C' '          IS TRAILING CHARACTER A BLANK?
         BNE   PARSINBD            NO, INVALID HEXADECIMAL STRING
         LA    R2,1(,R2)           YES, POINT PAST IT
         BCT   R14,PARSXLP3        DROP THROUGH TRAILING BLANKS
PARSXTXT SR    R3,R4               GET COUNT OF HEX SEARCH DATA
         STC   R3,DOUBLE+1
         TM    DOUBLE+1,X'01'      IS IT AN ODD NUMBER OF HEX DIGITS?
         BO    PARSINBD            YES, NOT A WHOLE NUMBER OF BYTES
         LR    R14,R3              NO, COPY HEX DIGIT COUNT
         BCTR  R14,0               DECREMENT FOR EXECUTE
         L     R2,=V(HEXDATA)      POINT TO TRANSLATE TABLE
         EX    R14,PARSTRAN        CONVERT TO ZONED HEXADECIMAL
         SRA   R14,1               GET SEARCH DATA LENGTH CODE
         STH   R14,STRINGL         SET SEARCH DATA LENGTH
         LA    R14,STRING          POINT TO TARGET STRING AREA
PARSXLP4 PACK  0(2,R14),0(3,R4)    PACK A BYTE
         MVI   1(R14),0            ERASE GARBAGE
         LA    R14,1(,R14)         POINT TO NEXT TARGET BYTE
         LA    R4,2(,R4)           POINT TO NEXT HEX DIGIT PAIR
         BCT   R3,PARSXLP4         PACK NEXT SEARCH BYTE
         B     PARSNXTF            CONTINUE PARSE
PARSTEXT STH   R14,STRINGL         SET SEARCH DATA LENGTH CODE
         EX    R14,PARSTRNG        LOAD THE NEW DATA STRING
         CLI   DOUBLE,X'F2'        FINDCHR?
         BE    PARSNXTF            YES
         BH    PARSFPIC            HANDLE PICTURE STRING SEARCH
         L     R2,=V(TABALPHA)     POINT TO TRANSLATE TABLE
         LR    R0,R1               SAVE SBA/FIELD POINTER
         EX    R14,PARSTEST        ANY ALPHABETICS?
         LR    R1,R0               RESTORE TRT REGISTER
         BZ    PARSNXTF            NO, SAME AS FINDCHR FOR SPEED
         OI    STATUS8,FUOL        SET CASE INSENSITIVE
         B     PARSNXTF            CONTINUE PARSE
PARSFPIC OI    STATUS8,FUOL+FPIC   SET FLAGS FOR FINDPIC
         B     PARSNXTF            CONTINUE PARSE
PARSTRNG MVC   STRING(0),3(R15)    <<< EXECUTED >>>
PARSTRAN TR    3(0,R15),0(R2)      <<< EXECUTED >>>
PARSTEST TRT   3(0,R15),0(R2)      <<< EXECUTED >>>
PARSNULL CLI   STRINGL,0           ANY STRING PRESPECIFIED?
         BE    PARSNXTF            YES, CONTINUE PARSE
         B     PARSINBD            NO, MISSING SEARCH DATA
PARSCOLM SLR   R0,R0               CLEAR ACCUMULATOR
         LTR   R14,R14             ANY DATA IN FIELD?
         BNP   PARSCLOK            NO, CLEAR COLUMN NUMBER
         LA    R2,3(,R15)          POINT TO FIRST DATA BYTE
PARSCLP1 CLI   0(R2),C' '          LEADING BLANK?
         BNE   PARSCHK#            NO, CHECK FOR NUMERIC
         LA    R2,1(,R2)           YES, POINT PAST IT
         BCT   R14,PARSCLP1        LOOK FOR NON-BLANK
         B     PARSCLOK            INPUT WAS ALL BLANK
PARSCHK# LR    R4,R2               POINT TO START OF NUMBER
PARSCLP2 CLI   0(R2),C'0'          FOUND NON-NUMERIC?
         BL    PARSCTRL            YES, GO HANDLE TRAILING BLANKS
         CLI   0(R2),C'9'          FOUND NON-NUMERIC?
         BH    PARSINBD            YES, HOW WAS THAT DONE?
         LA    R2,1(,R2)           POINT TO NEXT POSSIBLE DIGIT
         BCT   R14,PARSCLP2        VALIDATE NEXT DIGIT
PARSCTRL LR    R3,R2               POINT PAST LAST DIGIT
         LTR   R14,R14             ANY TRAILING BYTES TO CHECK?
         BZ    PARSCOL#            NO, END OF DATA SO GET NUMBER
PARSCLP3 CLI   0(R2),C' '          IS TRAILING CHARACTER A BLANK?
         BNE   PARSINBD            NO, INVALID COLUMN NUMBER
         LA    R2,1(,R2)           YES, POINT PAST IT
         BCT   R14,PARSCLP3        DROP THROUGH TRAILING BLANKS
PARSCOL# SR    R3,R4               GET DIGIT COUNT OF COLUMN NUMBER
         BCTR  R3,0                DECREMENT FOR EXECUTE
         EX    R3,PARSPACK         GET DECIMAL NUMBER
         CVB   R0,DOUBLE           GET BINARY NUMBER
         AH    R0,STRINGL          ADD SEARCH DATA LENGTH CODE
         CH    R0,RECSIZE          IS END COLUMN BEYOND RECORD LENGTH?
         BH    PARSINBD            YES, SO IT IS INVALID
         SH    R0,STRINGL          NO, RESTORE SPECIFIED COLUMN NUMBER
PARSCLOK STH   R0,FINDCOL          SAVE THE SEARCH COLUMN
         B     PARSNXTF            CONTINUE PARSE
PARSPACK PACK  DOUBLE,0(0,R4)      <<< EXECUTED >>>
PARSMASK MVC   SRCHMMSK,SRCHSTAR   INITIALIZE MEMBER NAME PATTERN MASK
         LTR   R14,R14             ANY DATA TYPE IN?
         BNP   PARSNXTF            NO, FIELD JUST ERASED
         BCTR  R14,0               YES, GET ITS LENGTH CODE
         EX    R14,PARSMVMS        SET THE NEW MASK
         L     R2,=V(KAPS)         POINT TO TRANSLATE TABLE
         TR    SRCHMMSK,0(R2)      FOLD TO UPPER CASE
         B     PARSNXTF            CONTINUE PARSE
PARSMVMS MVC   SRCHMMSK(0),3(R15)  <<< EXECUTED >>>
PARSINBD ICM   R0,3,1(R15)         GET BUFFER ADDRESS OF FIELD
         MVC   REPLY(SRCHFIXL),SRCHFIX
         STCM  R0,3,REPLY+2        PLACE CURSOR ON BAD FIELD
         L     R1,SCRNCOLS         GET SCREEN WIDTH
         LA    R1,11(R1,R1)        GET BUFFER LOCATION FOR MESSAGE
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,REPLY+6        SUPPLY ADDRESS FOR THIS TERMINAL
         LA    R1,REPLY            POINT TO OUTPUT BUFFER
         LA    R0,SRCHFIXL         GET OUTPUT DATA LENGTH
         B     SRCHTPUT            ISSUE NEW PROMPT
*
*               PROCESS INTERNAL MEMBER DIRECTORY ENTRIES
*
SRCHINDN SLR   R0,R0               REQUEST OKAY SO PROCESS MEMBERS
         ST    R0,SRCHFCNT         NO MEMBERS MATCHED YET
         ST    R0,SRCHMDON         NO MEMBERS SEARCHED YET
         ST    R0,SRCHMTOT         NO MEMBERS TO BE SEARCHED YET
         ST    R0,SRCHPCT1         ZERO PERCENT DONE
         ST    R0,SRCHPCT2         ZERO PERCENT DONE
         L     R15,SRCHMCNT        GET TAGGED MEMBER COUNT
         L     R4,SRCHMEM1         POINT TO FIRST INTERNAL ENTRY
SCHPRPLP C     R4,SRCHLAST         END OF INTERNAL DIRECTORY ENTRIES?
         BNL   SCHPRPDN            YES, PREPARATION ALL DONE
         TM    13(R4),X'40'        IS THIS ENTRY TAGGED?
         BO    TRYMMASK            YES, INVESTIGATE NAME MASK
         LTR   R15,R15             ONLY PROCESS TAGGED MEMBER ENTRIES?
         BNZ   SCHDSCRD            YES, DISCARD THIS MEMBER
TRYMMASK LA    R0,8                GET MEMBER NAME LENGTH
         LA    R1,SRCHMMSK         POINT TO SELECTION MASK
         LR    R2,R4               POINT TO MEMBER NAME
SCHMSKLP CLI   0(R1),C'*'          GENERIC CHARACTER?
         BE    SCHMSKEQ            YES, NO MISMATCH YET
         CLC   0(1,R1),0(R2)       NO, COMPARE A BYTE
         BNE   SCHDSCRD            MISMATCH SO DISCARD MEMBER
SCHMSKEQ LA    R1,1(,R1)           POINT TO NEXT CHARACTER
         LA    R2,1(,R2)           POINT TO NEXT CHARACTER
         BCT   R0,SCHMSKLP         GO CHECK NEXT CHARACTER, IF ANY
         LA    R0,1                MEMBER PASSED MASK PATTERN CHECK
         A     R0,SRCHMTOT         INCREMENT TOTAL SEARCH MEMBER COUNT
         ST    R0,SRCHMTOT
         OI    13(R4),X'40'        TAG MEMBER FOR SEARCH
         AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         B     SCHPRPLP
SCHDSCRD NI    13(R4),255-X'40'    DO NOT SEARCH THIS MEMBER
         AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         B     SCHPRPLP
SCHPRPDN ICM   R0,15,SRCHMTOT      ANY MEMBERS TO SEARCH?
         BZ    SRCHEXIT            NO, RETURN
         L     R4,SRCHMEM1         POINT TO FIRST INTERNAL ENTRY
         XC    SCHHLDNM,SCHHLDNM   CLEAR MEMBER NAME HOLDING AREA
         XC    SCHPRVMM,SCHPRVMM   CLEAR PREVIOUS MEMBER HOLDING AREA
         OI    MODE,OFFLDSW        NOW IN OFFLOAD MODE
         OI    STATUS7,FALL        EMPLOY "FIND ALL" LOGIC
         NI    MODE,255-ASCIISW    ENSURE ASCII IS OFF
         MVC   SCHLATST,NOMMATCH   NO MEMBER MATCHED YET
         MVC   SRCHLINE(PCNTLNHL+PCNTLNTL),PCNTLNHD
         MVC   SRCHLINE+PCSRCHOF+1(7),SRCHLINE+PCSRCHOF
         MVI   SRCHLINE+PCSRCHOF+7,C'0' ASSUME NO MEMBERS PROCESSED
         MVC   SRCHLINE+PCMTCHOF(8),SRCHLINE+PCSRCHOF
         LA    R1,19               GET RELATIVE LINE NUMBER FOR STATS
         MH    R1,SCRNCOLS+2
         LR    R2,R1               SAVE BYTE COUNT OF PRECEDING LINES
         LA    R1,5(,R1)           GET STARTING SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,SRCHLINE+2     SUPPLY ADDRESS FOR THIS TERMINAL
         STH   R1,SRCHSSBA         ALSO SAVE FOR LATER
         AL    R2,SCRNCOLS         GET BYTES BEFORE BAR LINE
         LA    R1,12(,R2)          GET BAR STARTING SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,SRCHLINE+PCBRLNOF SET ADDRESS FOR THIS TERMINAL
         STH   R1,SRCHPSBA         ALSO SAVE FOR LATER
         LA    R1,65(,R2)          GET BAR END SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,SRCHLINE+PCNTLNHL+1
         LA    R1,SRCHLINE         POINT TO START OF DATA STREAM
         LA    R0,PCNTLNHL+PCNTLNTL GET LENGTH OF DATA STREAM
         ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           SHOW INITIAL STATUS BAR
         LA    R2,15(,R2)          GET LOCATION OF ACTUAL BAR START
         STH   R2,SRCHPBAR         SAVE IT
         TIME  BIN                 GET THE TIME
         ST    R0,SCHBARTM         SAVE IT

SCHMEMLP C     R4,SRCHLAST         END OF INTERNAL DIRECTORY ENTRIES?
         BNL   SRCHEXIT            YES, END UP
         CLI   SRCHPCT1+3,50       SHOWN 100% STATUS BAR?
         BE    SRCHEXIT            YES, TERMINATE SEARCH
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BNO   SRCHDOIT            NO, PROCEED WITH SEARCH
         NI    13(R4),255-X'40'    RESET MEMBER TAG FLAG
         AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         B     SCHMEMLP            RESET REMAINING MEMBERS
SRCHDOIT TM    13(R4),X'40'        IS THIS ENTRY TAGGED?
         BZ    SCHNXTMM            NO, IGNORE UNTAGGED ENTRY
         LA    R15,1               YES
         A     R15,SRCHMDON        INCREMENT SEARCHED MEMBER COUNT
         ST    R15,SRCHMDON
         M     R14,=F'50'
         D     R14,SRCHMTOT        GET HALF THE PERCENTAGE DONE
         ST    R15,SRCHPCT2        SAVE IT
         CLC   SCHHLDNM,0(R4)      IDENTICAL MEMBER NAMES?
         BNE   SCHNEWNM            NO, PROCEED WITH SEARCH
         NI    13(R4),255-X'40'    YES, UNTAG MEMBER
         MVI   SRCHLINE,WCCNULL    SUPPLY WCC
         MVI   SRCHLINE+1,SBA      SUPPLY SBA ORDER CODE
         L     R1,SCRNCOLS         GET SCREEN WIDTH
         LA    R1,17(,R1)          GET LOCATION FOR MESSAGE
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,SRCHLINE+2     SET MESSAGE 3270 BUFFER ADDRESS
         MVC   SRCHLINE+4(L'DUPMEMS),DUPMEMS
         MVC   SRCHLINE+25(8),0(4) NOTIFY USER OF DUPLICATE MEMBER
         LA    R1,SRCHLINE
         LA    R0,L'DUPMEMS+4
         ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           SHOW MESSAGE ON SECOND SCREEN LINE
         B     SCHNXTMM            GO GET NEXT MEMBER TO PROCESS
SCHNEWNM CLC   8(5,R4),SCHPRVMM+8  ALIAS OF SAME DATA JUST SEARCHED?
         BNE   SCHMEMBR            NO, JUST HAVE TO DO SEARCH
         TM    SCHPRVMM+13,X'40'   YES, DID THE SEARCH MATCH?
         BZ    SCHALINO            NO, UNTAG THIS MEMBER
         B     SCHALIAS            YES, TAG AND COUNT
SCHMEMBR MVC   $MEMBER,0(R4)       SUPPLY THE MEMBER NAME TO PHASE 2
         SLR   R0,R0
         STH   R0,RC               ZERO ANY PREVIOUS BLDL FAILURE
         ST    R0,CALLPARM         ZERO THE SIGNAL AREA
         L     R0,CBPRM3           SAVE REVPODIR TPUT BUFFER ADDRESS
         L     R15,SRCHSADR        POINT TO NEW SAVE AREA
         ST    R15,8(,R13)         LINK SAVE AREAS
         ST    R13,4(,R15)
         L     R15,=V(REVIEW2)     GET ADDRESS OF PHASE 2
         STM   R14,R12,12(R13)     SAVE ALL REGISTERS
         L     R13,8(,R13)         POINT TO NEW SAVE AREA
         BALR  R14,R15             CALL REVIEW PHASE 2
         L     R13,4(,R13)         POINT TO OLD SAVE AREA
         LM    R0,R12,20(R13)      RESTORE ALL EXCEPT BALR REGISTERS
         ST    R0,CBPRM3           RESTORE REVPODIR TPUT BUFFER ADDRESS
         LH    R15,RC              GET BLDL RETURN CODE
         TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    SCHMEMLP            YES, ABORT SEARCH
         LTR   R15,R15             WAS THE MEMBER FOUND?
         BNZ   SCHNOMEM            NO, ISSUE MESSAGE
         MVC   SCHPRVMM,0(R4)      YES, SAVE THIS MEMBER'S DETAILS
         CLI   CALLPARM,0          SEARCH MATCH?
         BE    SCHMEMNO            NO, UNTAG MEMBER
         OI    SCHPRVMM+13,X'40'
SCHALIAS OI    13(R4),X'40'        YES, TAG THIS MEMBER
         LA    R0,1
         A     R0,SRCHFCNT         INCREMENT TAGGED MEMBER COUNT
         ST    R0,SRCHFCNT
         MVC   SCHLATST,0(R4)      UPDATE LATEST MATCH MEMBER NAME
         B     SCHMATCH            LEAVE MEMBER TAGGED
SCHNOMEM MVC   SRCHLINE(L'BDBLDMS),BDBLDMS
         MVC   SRCHLINE+L'BDBLDMS(8),0(R4)
         LA    R1,SRCHLINE
         LA    R0,L'BDBLDMS+8
         TPUT  (1),(0),R           LINE MODE I/O SHOULD GET NOTICED
         XC    SCHPRVMM,SCHPRVMM   CLEAR PREVIOUS MEMBER HOLDING AREA
SCHMEMNO NI    SCHPRVMM+13,255-X'40'
SCHALINO NI    13(R4),255-X'40'    RESET MEMBER TAG FLAG
SCHMATCH MVC   SCHHLDNM,0(R4)      REMEMBER THE LATEST MEMBER NAME
SCHNXTMM AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         CLI   SRCHPCT2+3,50       HAS THE LAST MEMBER BEEN PROCESSED?
         BE    SCHDOBAR            YES, SHOW 100% BEFORE EXITING
         TIME  BIN                 NO, GET THE TIME
         LA    R1,100
         A     R1,SCHBARTM
         CR    R1,R0               AT LEAST 1 SECOND SINCE LAST UPDATE?
         BH    SCHMEMLP            NO, SKIP UPDATE AT THIS TIME
         CLC   SRCHPCT1,SRCHPCT2   DOES THE STATUS BAR NEED AN UPDATE?
         BNE   SCHDOBAR            YES, UPDATE THE SCREEN
         LA    R1,300              SAME PERCENTAGE,
         A     R1,SCHBARTM              BUT HAVE THREE SECONDS
         CR    R1,R0                    ELAPSED SINCE LAST UPDATE?
         BH    SCHMEMLP            NO, SKIP UPDATE AT THIS TIME
SCHDOBAR MVC   SRCHPCT1,SRCHPCT2   YES, PROCEED WITH UPDATE
         ST    R0,SCHBARTM         SAVE CURRENT TIME
         MVC   SRCHLINE(PCNTLNHL),PCNTLNHD
         L     R0,SRCHMDON         SHOW NUMBER OF MEMBERS SEARCHED
         CVD   R0,DOUBLE
         ED    SRCHLINE+PCSRCHOF(8),DOUBLE+4
         L     R0,SRCHFCNT         SHOW NUMBER OF MEMBERS MATCHED
         CVD   R0,DOUBLE
         ED    SRCHLINE+PCMTCHOF(8),DOUBLE+4
         MVC   SRCHLINE+PCLATEOF(8),SCHLATST
         L     R14,=V(FULLT)       HANDLE BAD MEMBER NAME CODE POINTS
         TR    SRCHLINE+PCLATEOF(8),0(R14)
         MVC   SRCHLINE+2(2),SRCHSSBA  LOAD 3270 BUFFER ADDRESSES
         MVC   SRCHLINE+PCBRLNOF(2),SRCHPSBA
         LA    R2,SRCHLINE+PCNTLNHL
         TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    SCHVIDOK            NO, DON'T USE REVERSE VIDEO
         MVC   0(3,R2),SRCHRVRS    SET REVERSE VIDEO
         LA    R2,3(,R2)           UPDATE POINTER
SCHVIDOK CLI   SRCHPCT2+3,1        JUST ONE BYTE IN STATUS BAR?
         BE    SCHRTAOK            YES, SKIP REPEAT-TO-ADDRESS
         BL    SCHLENOK            ZERO BYTES SO FORGET BAR DISPLAY
         MVI   0(R2),RA
         L     R1,SRCHPCT2         GET HALF OF NEW PERCENT TO SHOW
         AH    R1,SRCHPBAR         GET FINAL LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,1(R2)          SET ENDING 3270 BUFFER ADDRESS
         LA    R2,3(,R2)           POINT TO STATUS BAR CHARACTER
SCHRTAOK MVI   0(R2),C'X'          LOAD CHARACTER FOR BAR
         TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    *+8                 NO, LEAVE THE 'X'
         MVI   0(R2),C' '          YES, USE REVERSE VIDEO BLANKS
         CLI   SACHAR,X'0E'        FUJITSU TERMINAL HARDWARE?
         BNE   SCHBAROK            NO, TERMINAL WILL RESET TO NORMAL
         MVC   1(3,R2),SRCHNORM    RESET TO NORMAL VIDEO FOR NEXT WRITE
         LA    R2,3(,R2)           UPDATE POINTER
SCHBAROK LA    R2,1(,R2)           UPDATE POINTER
SCHLENOK LR    R0,R2               POINT PAST END OF DATA STREAM
         LA    R1,SRCHLINE         POINT TO START OF DATA STREAM
         SR    R0,R1               GET LENGTH OF DATA STREAM
         ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           UPDATE THE STATUS BAR
         B     SCHMEMLP            PROCESS THE NEXT DIRECTORY ENTRY

SRCHEXIT NI    MODE,255-OFFLDSW    ENSURE SEARCH FLAGS ARE RESET
         NI    STATUS7,255-FALL
         MVC   OFFLDCNT,SRCHFCNT   SET HIGHLIGHTED MEMBER COUNT
         LA    R0,@SCHWKLN
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         DROP  R13                 @SCHWORK
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @SCHWORK
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE OTHER REGISTERS
         BR    R14                 RETURN WITH FREEMAIN RETURN CODE

         DROP  R11                 REVSRCH
         TITLE '  R E V S R C H   -   S T A T I C   A R E A  '
HEXS     DC    C'0123456789ABCDEF' HEXADECIMAL DIGIT TRANSLATE TABLE
SRCHED7  DC    X'6E20202020202120' EDIT MASK
SRCHSTAR DC    CL8'********'       ASTERISKS
BDBLDMS  DC    C'** BLDL FAILED DURING SEARCH OF MEMBER '
DUPMEMS  DC    C'*** DUPLICATE MEMBER ???????? NOT SEARCHED ***'
PCNTLNHD DC    AL1(WCCNULL,SBA,20,6)
PCSRCHOF EQU   *-PCNTLNHD
         DC    X'4020202020202120' EDIT MASK FOR MEMBERS SEARCHED
         DC    C' MEMBERS SEARCHED  '
PCMTCHOF EQU   *-PCNTLNHD
         DC    X'4020202020202120' EDIT MASK FOR MEMBERS MATCHED
         DC    C' MEMBERS MATCHED - LATEST: '
PCLATEOF EQU   *-PCNTLNHD
NOMMATCH DC    C'**NONE**'
PCBRLNOF EQU   *-PCNTLNHD+1        OFFSET FOR BAR BUFFER ADDRESS
         DC    AL1(SBA,21,13),C'0% '
PCNTLNHL EQU   *-PCNTLNHD
PCNTLNTR DC    AL1(RA,21,66),C'_ 100%'
PCNTLNTL EQU   *-PCNTLNTR
SRCHRVRS DC    AL1(SA,HILT,RVRSE)
SRCHNORM DC    AL1(SA,HILT,NULL)

*         TRANSLATE TABLES
LOWCSTST EQU   *,256               TRT TABLE FOR LOWER CASE LETTERS
         DC    129X'00'            00-80
         DC    9X'FF'              LOWER CASE A-I
         DC    7X'00'              8A-90
         DC    9X'FF'              LOWER CASE J-R
         DC    8X'00'              9A-A1
         DC    8X'FF'              LOWER CASE S-Z
         DC    86X'00'             EA-FF
KEYBDTST EQU   *,256               TRT TABLE FOR UNDISPLAYABLES
         DC    64X'FF',X'00'       BLANK
         DC    9X'FF',6X'00'       CENT,PERIOD,LESS,LPAREN,PLUS,BAR
         DC    X'00',9X'4B'        AMPERSAND
         DC    6X'00'              EXCL,$,ASTERISK,RPAREN,SEMI,NOT
         DC    X'0000',8X'4B'      HYPHEN,SLASH
         DC    6X'00'              WHAT,COMMA,PERCENT,UNDLN,GT,QM
         DC    9X'4B',X'00'        70-78              BACK-QUOTE
         DC    6X'00'              COLON,POUND,AT,APOST,EQ,DBLQUOTE
         DC    X'4B'               80
         DC    9X'00',7X'4B'       81-90
         DC    9X'00',7X'4B'       91-A0
         DC    9X'00',22X'4B'      A1-BF       TILDE
         DC    10X'00',6X'4B'      LEFT BRACE  A-I
         DC    10X'00',6X'4B'      RIGHT BRACE J-R
         DC    X'004B',8X'00',6X'4B' BACK SLASH  S-Z
         DC    10X'00',6X'4B'      0-9

SRCHFIX  DC    X'C411000013110000'
         DC    C'>>> INPUT FIELD HAS BAD OR MISSING DATA - PLEASE FIX <+
               <<'
SRCHFIXL EQU   *-SRCHFIX

SRCHSCRN DC    AL1(WCCSTND,SBA,1,1,SA,COLR,WHITE)
         DC    C'REVIEW'
         DC    AL1(RA,1,26),C' '
         DC    CL30'PDS SEARCH REQUEST ENTRY PANEL'
         DC    AL1(RA,1,76),C' '
         DC    CL5'R&REL'
         DC    AL1(RA,5,1),C' '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    AL1(RA,6,1),C' '
         DC    C'SEARCH TYPE'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SF,UNPHI,SA,COLR,RED,SA,HILT,USCR)
SRCHTYPE DC    C'1'
         DC    AL1(SA,HILT,NULL,SF,PROLOS,SA,COLR,GREEN)
         DC    CL2' '
         DC    C'1. FIND        2. FN (FINDNOT)'
         DC    AL1(SA,COLR,BLUE,RA,6,55),C' '
         DC    C'(CASE INSENSITIVE)'
         DC    AL1(SA,COLR,GREEN,RA,7,21),C' '
         DC    C'3. FINDCHR     4. FCN'
         DC    AL1(SA,COLR,BLUE,RA,7,55),C' '
         DC    C'(CASE SENSITIVE)'
         DC    AL1(SA,COLR,GREEN,RA,8,21),C' '
         DC    C'5. FINDHEX     6. FXN'
         DC    AL1(SA,COLR,GREEN,RA,9,21),C' '
         DC    C'7. FINDPIC     8. FPN'
         DC    AL1(SA,COLR,BLUE,RA,9,45),C' '
         DC    C'(MASK CHARACTERS: = @ # $ ^ . - < >)'
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    AL1(RA,12,1),C' '
         DC    C'SEARCH DATA'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SA,COLR,RED,SA,HILT,USCR,SF,UNPHIM,IC)    MDT ON
SRCHSPEC DC    XL64'00'
         DC    AL1(SA,HILT,NULL,SF,PROLOS,SA,COLR,TURQOISE)
         DC    AL1(RA,15,1),C' '
         DC    C'OPTIONAL SEARCH COLUMN'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SF,UNPHI,SA,COLR,RED,SA,HILT,USCR)
SRCHCOLM DC    XL5'00'
         DC    AL1(SA,HILT,NULL,SF,PROLOS,SA,COLR,TURQOISE)
         DC    AL1(RA,18,1),C' '
         DC    C'OPTIONAL MEMBER NAME MASK'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SF,UNPHI,SA,COLR,RED,SA,HILT,USCR)
SRCHMASK DC    CL8'********'
         DC    AL1(SA,HILT,NULL,SF,PROLOS)
         DC    AL1(RA,23,5),C' '
         DC    AL1(SF,PROHIS,SA,COLR,PINK)
SRCHNMBR DC    C' **ALL**'
         DC    AL1(SA,COLR,GREEN)
SRCHTRLR DC    AL1(SF,PROLOS)
         DC    C'MEMBERS WILL BE SEARCHED SUBJECT TO MEMBER NAME MASK M+
               ATCHING'
         DCS   SF,PROHIS,RTA,(1,1),C' '
SRCHTRLL EQU   *-SRCHTRLR
SRCHSCRL EQU   *-SRCHSCRN

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V S R C H   -   D Y N A M I C   A R E A  '
@SCHWORK DSECT
SRCHSAVE DS    18F
SRCHMMSK DS    CL8                 MEMBER NAME SEARCH MASK
SRCHMCNT DS    F                   HIGHLIGHTED MEMBER COUNT - BEFORE
SRCHFCNT DS    F                   HIGHLIGHTED MEMBER COUNT - AFTER
SRCHTDYN DS    F                   ADDRESS OF FIND TYPE SETTING
SRCHSADR DS    F                   POINTER TO PHASE 2 SAVE AREA
SRCHMEM1 DS    F                   FIRST INTERNAL MEMBER ENTRY
SRCHLAST DS    F                   LAST INTERNAL MEMBER ENTRY
SRCHMTOT DS    F                   TOTAL MEMBER ENTRY COUNT TO SEARCH
SRCHMDON DS    F                   SEARCHED MEMBER ENTRY COUNT
SRCHPCT1 DS    F                   SEARCH % DONE BEFORE CURRENT MEMBER
SRCHPCT2 DS    F                   SEARCH % DONE AFTER CURRENT MEMBER
SCHBARTM DS    F                   TIME OF LAST STATUS BAR UPDATE
SRCHSSBA DS    H                   3270 BUFFER ADDRESS OF STATS LINE
SRCHPSBA DS    H                   3270 BUFFER ADDRESS OF % BAR
SRCHPBAR DS    H                   LOCATION OF ACTUAL BAR START
         DS    0D                  REST ARE MULTIPLES OF 8 BYTES
SCHLATST DS    CL8                 LATEST MATCHED MEMBER NAME
SCHHLDNM DS    CL8                 PREVIOUS MEMBER NAME IN LIST
SCHPRVMM DS    CL16                PREVIOUS MEMBER SEARCHED
SRCHLINE DS    CL160               MESSAGE WORK AREA
SRCHSDYN DS    256D                TPUT/TGET BUFFER
         DS    0D                  END OF DSECT
@SCHWKLN EQU   *-@SCHWORK
         TITLE '  R E V E D I T   -   C A L L   I S P F   E D I T O R '
**********************************************************************
*                                                          *         *
*         ISPF EDIT INTERFACE                              * AW@IDAPS*
*                                                          *  06/86  *
**********************************************************************

**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R15-> REVEDIT                                  *
*                     R14-> RETURN ADDRESS                           *
*                     R9 -> @DATA                                    *
*                     R1 -> SELECTION DATA STREAM                    *
*                                                                    *
* DURING PROCESSING   R13-> @EDWORK  (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 = 0      MEMBER SAVED                      *
*                     R15 = 4      MEMBER NOT SAVED                  *
*                     R15 = 14     MEMBER IN USE                     *
*                     R15 = 16     MEMBER NOT FOUND                  *
*                     R15 = 20     SEVERE ERROR - UNABLE TO CONTINUE *
*                     R15 = 30     ISPF NOT ACTIVE                   *
*                     R15 = 40     GETMAIN FAILED                    *
*                     R15 = 50     ISPLINK NOT IN SYSTEM             *
*                                                                    *
**********************************************************************

REVEDIT  CSECT
         B     @EDIT-*(,R15)
         DC    AL1(7),CL7'REVEDIT'
@EDIT    STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVEDIT,R11
         LR    R4,R13                  COPY @DYNAREA POINTER
         LR    R5,R1                   COPY SELECTION STREAM POINTER
         SLR   R0,R0
         LA    R0,@EDWKLEN             LOAD LENGTH OF DSA
         GETMAIN RC,LV=(0)             GET THE STORAGE
         LTR   R15,R15
         BZ    EDGETOK
         LM    R14,R12,12(R13)         RESTORE CALLERS REGISTERS
         LA    R15,40                  SET RETURN CODE TO 40
         BR    R14                     RETURN TO CALLER
EDGETOK  DS    0H
         LR    R13,R1                  SAVE THE ADDRESS IN R13
         USING @EDWORK,R13
         ST    R13,8(,R4)              STORE FORWARD AND
         ST    R4,4(,R13)              BACKCHAIN POINTERS
         LA    R15,50                  PREPARE BAD RETURN CODE OF 50
         ST    R15,16(R4)              STORE RETURN CODE
         MVC   DOUBLE,ISPLNKNM         LOAD EXPECTED ENTRY POINT NAME
         TM    OSBITS,X'13'            IS THIS OSIV/F4 OR MSP?
         BO    ISPFITIS                NO, SHOULD BE SOME SORT OF MVS
         MVC   DOUBLE,PFDLNKNM         YES, ISPLINK IS ALIAS OF PFDLINK
ISPFITIS DS    0H                           BUT NEED ENTRY PFDLINKX
         LOAD  EPLOC=DOUBLE,ERRET=EDITEXIT  PRE-LOAD REENTRANT MODULE
         ST    R0,ISPLNKAD             SAVE ADDRESS OF ISPLINK
         MVI   EDATASET,QUOTE          MOVE IN FIRST QUOTE
         LH    R3,$DSNAME              GET DATA SET NAME LENGTH
         LA    R1,$DSNAME-2            POINT 4 BYTES BEHIND DSN START
         LA    R7,$VOLSER              POINT TO FIRST/ONLY VOLUME
         TM    MYDSCB-44+78,X'80'      SMS-MANAGED?
         BZ    SPFVOLOK                NO, SUPPLY THE VOLUME EXPLICITY
         LA    R7,=CL6' '              YES, ALLOW RECALL TO ANY DISK BY
SPFVOLOK SLR   R0,R0                        USING THE CATALOG ENTRY
         ICM   R0,B'0011',$CONCAT      LOAD CONCAT'N NUMBER
         BZ    GETEDDSN                ZERO SO USE $DSNAME
         L     R1,ARLAREA              POINT TO FIRST RETRIEVED JFCB
EDITJFCB BCT   R0,EDJFLOOP             CHAIN THROUGH JFCBS
         B     GOTEJFCB                NOW POINTING TO CORRECT JFCB
EDJFLOOP AH    R1,0(,R1)               POINT TO NEXT JFCB
         B     EDITJFCB                PROCESS IT
GOTEJFCB LA    R7,122(,R1)             POINT TO VOLUME IN JFCB
         LA    R3,44                   GET MAXIMUM DSN LENGTH
         LA    R15,47(,R1)             POINT TO LAST DSN CHARACTER
CNCTEDLP CLI   0(R15),C' '             TRAILING BLANK?
         BNE   GETEDDSN                NO, NOW HAVE DSNAME LENGTH
         BCTR  R15,0                   YES, POINT TO PREVIOUS CHARACTER
         BCT   R3,CNCTEDLP             DECREMENT LEN AND TEST PREVIOUS
GETEDDSN LA    R2,EDATASET+1           POINT R2 AT ISPF DATASET NAME
         EX    R3,MDSN                 MOVE DATASET NAME
         AR    R2,R3                   INCREMENT DATASET POINTER
         MVI   0(R2),C'('              MOVE LEFT BRACKET
         LA    R2,1(,R2)               INCREMENT DATASET POINTER
         MVC   0(8,R2),$MEMBER         MOVE IN THE MEMBER NAME
         LA    R6,8
BLNKLOP  DS    0H                      LOOP AND SEARCH FOR THE BLANK
         LA    R2,1(,R2)                 INCREMENT POINTER
         CLI   0(R2),C' '                IS IT A BLANK?
         BE    MOVRB                     YES!
         BCT   R6,BLNKLOP                NO BRANCH BACK
MOVRB    DS    0H
         MVC   0(3,R2),RIGHTBQ         MOVE RIGHT BRACKET AND QUOTE
         LA    R1,ISPCDRPL             POINT TO ISPLINK PLIST
         L     R15,ISPLNKAD            LOAD ISPLINK ENTRY POINT
         BALR  R14,R15                 CALL ISPLINK
         LTR   R15,R15                 TEST RETURN CODE
         BZ    EDITIT                  OK. THEN GO DO IT
         LA    R15,30                  SET RETCODE TO 30
         L     R4,4(,R13)              POINT TO CALLERS SAVEAREA
         ST    R15,16(R4)              STORE RETURN CODE
         B     EDRETURN                 AND RETURN
EDITIT   DS    0H                      CALL ISPF
         CLI   SACHAR,X'0E'            FUJITSU TERMINAL HARDWARE?
         BNE   EDSCRNOK                NO, COLOUR IS NOT A PROBLEM
         LA    R1,PFDEDEWA             POINT TO ERASE/WRITE ALTERNATE
         CLI   SCRNLNES+3,24           STAYING WITH ALTERNATE SIZE?
         BH    EDERASOK                YES
         CLI   SCRNCOLS+3,80           STAYING WITH ALTERNATE SIZE?
         BH    EDERASOK                YES
         LA    R1,PFDEDEW              NO, SCREEN TO CLEAR IS 24 BY 80
EDERASOK LA    R0,L'PFDEDEW            LOAD DATA STREAM LENGTH
         ICM   R1,8,=X'03'             TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R               RESET CHARACTER ATTRIBUTES
EDSCRNOK DS    0H
         TM    OSBITS,X'13'            OSIV/F4 E40, MSP E10 OR MSP E20?
         BNO   CALLSPFD                YES, CALL "SPFD" (SPF OR PFD)
*        UNDER PFD, MEMBERS SHOULD ONLY BE SELECTED WITH A 'B' OR 'E'
*        FROM OPTION "6".  ELSEWHERE FROM PFD IS ALRIGHT UNTIL YOU EXIT
*        COMPLETELY FROM REVIEW WHEREUPON A PFD MAIN TASK ABEND OCCURS.
*        A 'CONTROL ERRORS RETURN' WRECKS EVEN THIS CAPABILITY.
*        (NOTE THAT PFD DOES NOT SUPPORT RECURSIVE BROWSE OR EDIT.)

         LA    R1,ISPCERPL             POINT TO ISPLINK PLIST
         L     R15,ISPLNKAD            LOAD ISPLINK ENTRY POINT
         BALR  R14,R15                 CALL ISPLINK
CALLSPFD LA    R1,EDITCMD
         CLI   3(R5),C'B'              CAN'T INTEREST YOU IN BROWSE?
         BNE   EDITITIS                NO
         LA    R1,BRWSCMD
EDITITIS ST    R1,EPLIST
         LA    R1,EDATASET
         ST    R1,EPLIST+4
         ST    R7,EPLIST+8             POINT TO VOLUME SERIAL NUMBER
         OI    EPLIST+8,X'80'          INDICATE LAST PARAMETER
         LA    R1,EPLIST               POINT TO ISPLINK PLIST
         L     R15,ISPLNKAD            LOAD ISPLINK ENTRY POINT
         BALR  R14,R15                 CALL ISPLINK
         L     R4,4(,R13)              POINT TO CALLERS SAVEAREA
         ST    R15,16(R4)              STORE RETURN CODE

         LA    R1,ISPCDRPL             POINT TO ISPLINK PLIST
         L     R15,ISPLNKAD            LOAD ISPLINK ENTRY POINT
         BALR  R14,R15                 CALL ISPLINK
EDRETURN DS    0H
         DELETE EPLOC=DOUBLE
EDITEXIT DS    0H
         NI    STATUS4,255-FLAGI       RESET ATTENTION FLAG
         LA    R0,@EDWKLEN             LOAD LENGTH OF DSA
         LR    R1,R13                  PREPARE FOR FREEMAIN
         L     R13,4(,R13)             POINT TO CALLERS SAVEAREA
         FREEMAIN R,LV=(0),A=(1)       FREE DSA
         LM    R14,R12,12(R13)         RESTORE CALLERS REGISTERS
         BR    R14                     RETURN TO CALLER
         TITLE '  R E V E D I T   -   S T A T I C   A R E A  '
MDSN     MVC   EDATASET+1(0),4(R1)     <<< EXECUTED >>>
ISPCDRPL DC    A(CONTROL)
         DC    A(DISPLAY)
         DC    A(REFRESH+X'80000000')
ISPCERPL DC    A(CONTROL)
         DC    A(ERRORS)
         DC    A(RETURN+X'80000000')
ISPLNKNM DC    CL8'ISPLINK '
PFDLNKNM DC    CL8'PFDLINKX'
BRWSCMD  DC    CL8'BROWSE  '
EDITCMD  DC    CL8'EDIT    '
CONTROL  DC    CL8'CONTROL '
DISPLAY  DC    CL8'DISPLAY '
REFRESH  DC    CL8'REFRESH '
ERRORS   DC    CL8'ERRORS  '
RETURN   DC    CL8'RETURN  '
RIGHTBQ  DC    CL3')'' '
PFDEDEW  DC    X'27F540'               ESCAPE + ERASE/WRITE + WCC
PFDEDEWA DC    X'277E40'               ESCAPE + ERASE/WRITE ALT + WCC

         LTORG

         DS    0D                      END OF CSECT
         TITLE '  R E V E D I T   -   D Y N A M I C   A R E A  '
@EDWORK  DSECT
EDSAVE   DS    18F
ISPLNKAD DS    A
EPLIST   DS    A
         DS    A
         DS    A
EDATASET DS    CL57
         DS    0D                      END OF DSECT
@EDWKLEN EQU   *-@EDWORK
         TITLE '  R E V L O A D  '
**********************************************************************
*                                                          *         *
*         REVIEW PDS MEMBER LOAD FROM SEQUENTIAL FILE      *  GP@P6  *
*                                                          *  03/99  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF ADDING OR        *
*        REPLACING PDS MEMBERS FROM A SEQUENTIAL FILE CONTAINING     *
*        AN IEBUPDTE-LIKE INPUT STREAM CONSISTING OF CONTROL         *
*        STATEMENTS AND DATA.                                        *
*                                                                    *
*           AN ENTRY PANEL IS DISPLAYED WHERE THE USER SPECIFIES     *
*        THE INPUT SEQUENTIAL FILE.  IT IS ALLOCATED, AS ARE OTHER   *
*        FILES NECESSARY FOR THE UTILITY.  THE UTILITY PROGRAM IS    *
*        ATTACHED WITH PROGRAM PARAMETERS ALSO SPECIFYING THE DD     *
*        NAMES TO USE.  BECAUSE THE UTILITY IS ATTACHED, REVIEW      *
*        IS PROTECTED TO SOME EXTENT FROM ABENDS, THUS GIVING THE    *
*        USER THE OPPORTUNITY TO RETRY WITH CORRECTED INPUT IN       *
*        SOME CASES.                                                 *
*                                                                    *
*           THE UTILITY PROGRAM ATTACHED IS EITHER 'PDSLOAD' FROM    *
*        CBT FILE 93, OR 'REVLMOD' FOR UNDEFINED RECORD FORMAT       *
*        LIBRARIES.  REVIEW MEMBER OFFLOAD OPERATIONS CREATE FILES   *
*        SUITABLE FOR PROCESSING BY THESE UTILITIES.                 *
*                                                                    *
*           THE IBM UTILITY DDNAME SLOT CONVENTION GOES SOMETHING    *
*        LIKE THIS:                                                  *
*           01  SYSLIN                                               *
*           02  OUTPUT MEMBER NAME                                   *
*           03  SYSLMOD                                              *
*           04  SYSLIB                                               *
*           05  SYSIN                                                *
*           06  SYSPRINT      (SYSLOUT)                              *
*           07  SYSPUNCH                                             *
*           08  SYSUT1                                               *
*           09  SYSUT2                                               *
*           10  SYSUT3                                               *
*           11  SYSUT4                                               *
*           12  SYSTERM                                              *
*           13  SYSUT5                                               *
*           14  SYSUT6        (SYSCIN FOR PL/I COMPILER)             *
*           15  SYSUT7                                               *
*           16  SYSADATA                                             *
*           17  SYSIDL                                               *
*                                                                    *
**********************************************************************

**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA         (INPUT COMMAND IN 'DOUBLE' *
*                     R13-> @DYNAREA                    UPON ENTRY)  *
*                                                                    *
* DURING PROCESSING   R13-> @PDSLDWK (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 = 0  PROCESSING COMPLETE                   *
*                     READR = SUBTASK COMPLETION CODE                *
*                                                                    *
**********************************************************************
         EJECT
REVLOAD  CSECT
         B     @PDSLOAD-*(,R15)
         DC    AL1(7),CL7'REVLOAD'
@PDSLOAD STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVLOAD,R11
         LA    R0,@PDSWKLN
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @PDSLDWK,R13
         MVI   GENSTATS,C'N'       DO NOT MANUFACTURE MEMBER STATISTICS
         LA    R3,LDATTCHM         POINT TO REVLMOD ATTACH
         TM    MYDSCB-44+84,X'C0'  DOES PDS HAVE RECFM=U?
         BO    *+8                 YES, HAVE CORRECT ATTACH LIST
         LA    R3,LDATTCHS         NO, POINT TO PDSLOAD ATTACH
         MVC   LDATTCHD(LDATTCHL),0(R3)
         XC    LDDDLEN(90),LDDDLEN CLEAR DD OVERRIDE SLOTS
         MVI   LDDDLEN+1,88        SET LENGTH FOR 11 SLOTS
         MVC   LDOUTDDN,$DDNAME    SET TARGET FILE NAME
         LA    R3,LDSCRNLN         GET THE PROMPTING SCREEN SIZE
         LA    R4,LOADSCRN         POINT TO PROMPTING SCREEN SOURCE
         LA    R5,LOADSDYN         POINT TO THE TPUT BUFFER
         LA    R6,LDNOTPGM         POINT TO VARIABLE SOURCE DATA
         TM    MYDSCB-44+84,X'C0'  DOES PDS HAVE RECFM=U?
         BO    LOADOTLP            YES, SUPPRESS STATISTICS QUESTION
         SLR   R6,R6               NO, DO NOT EDIT SCREEN PANEL
LOADOTLP CR    R4,R6               REACHED A VARIABLE LOCATION?
         BE    LOADVARI            YES, GO LOAD VARIABLE DATA
         CLI   0(R4),SBA           SBA ORDER?
         BE    LOADOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),RA            RA ORDER?
         BE    LOADOTAD            YES, HANDLE BUFFER ADDRESS
         CLI   0(R4),SA            SA ORDER?
         BE    LOADOTSA            YES, HANDLE EXTENDED DATA STREAM
LOADTYOK MVC   0(1,R5),0(R4)       COPY A BYTE TO THE TPUT BUFFER
         LA    R4,1(,R4)           INCREMENT SOURCE POINTER
         LA    R5,1(,R5)           INCREMENT TARGET POINTER
         BCT   R3,LOADOTLP         PROCESS NEXT BYTE
         B     LOADOTOK            OUTPUT DATA STREAM NOW CONSTRUCTED
LOADOTAD MVC   0(1,R5),0(R4)       COPY ORDER
         SLR   R1,R1
         IC    R1,1(,R4)           GET THE ROW NUMBER
         BCTR  R1,0                MAKE RELATIVE
         M     R0,SCRNCOLS         GET BYTES ON PRECEDING LINES
         IC    R0,2(,R4)           GET THE COLUMN NUMBER
         BCTR  R0,0                MAKE RELATIVE
         AR    R1,R0               GET SCREEN LOCATION
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,1(R5)          SUPPLY ADDRESS FOR THIS TERMINAL
         LA    R0,3
LOADADJS ALR   R4,R0               ADJUST SOURCE POINTER
         ALR   R5,R0               ADJUST TARGET POINTER
         SR    R3,R0               ADJUST BYTES TO GO
         B     LOADOTLP            HOPE R3 IS STILL POSITIVE
LOADOTSA MVC   0(3,R5),0(R4)       COPY COMPLETE SA ORDER STREAM
         LA    R0,3                GET BYTE COUNT INVOLVED
         SR    R3,R0               ADJUST BYTES TO GO
         BNP   LOADOTOK            SHOULDN'T HAPPEN
         ALR   R4,R0               ADJUST SOURCE POINTER
         TM    STATUS5,X3270       USING EXTENDED DATA STREAM ORDERS?
         BZ    LOADOTLP            NO, DON'T COUNT THIS ORDER
         ALR   R5,R0               YES, ADJUST TARGET POINTER
         B     LOADOTLP            R3 IS STILL POSITIVE
LOADVARI LA    R3,LDALTRLL         GET TRAILER LENGTH
         LA    R4,LDALLTRL         POINT TO THE TRAILER
         B     LOADOTLP            FINISH UP PANEL BUILD
LOADOTOK MVC   LOADMMSK,LOADSTAR   INITIALIZE MEMBER NAME PATTERN MASK
         LR    R0,R5               POINT PAST END OF DATA STREAM
         LA    R1,LOADSDYN         POINT TO START OF DATA STREAM
         SR    R0,R1               GET LENGTH OF DATA STREAM
LOADTPUT ICM   R1,8,=X'0B'         LOAD TPUT FLAGS (HOLD, FULLSCREEN)
         TPUT  (1),(0),R           SHOW DATA ENTRY PANEL
         TM    MODE,FSMODE         IN FULLSCREEN MODE?
         BO    LDFSMDOK            YES, GOOD
         STFSMODE ON               NO, RESTORE IT
         OI    MODE,FSMODE         SET FLAG
*   THIS IS A CHEAT TO GET THE ERROR MESSAGE FROM DAIRFAIL ON THE
*   SCREEN IN FULLSCREEN MODE WITHOUT EXTRACTING AND HANDLING IT IN
*   THIS ROUTINE.  IF THE ALLOCATION FAILS THEN FULLSCREEN MODE IS
*   TURNED OFF JUST BEFORE CALLING DAIRFAIL, WHICH IS INSTRUCTED TO
*   USE PUTLINE TO ISSUE THE MESSAGE.  REVIEW'S "BAD OR MISSING DATA"
*   MESSAGE IS ISSUED USING A HOLD TPUT, SO THAT THE TPUT COMPLETES
*   BEFORE FULLSCREEN MODE IS TURNED ON.  THE FIRST "FULLSCREEN" I/O
*   IS THEN THE FOLLOWING TGET, SO VTAM PAGE PROTECTION (IE. THE 3
*   ASTERISKS) IS NOT TRIGGERED, AND THE USER DOES NOT HAVE TO PRESS
*   ENTER TO RETURN TO FULLSCREEN MODE.  BY SETTING THE NON-FULLSCREEN
*   MODE CURRENT LINE TO 4 (BELOW), THE SCREEN IS NOT CLEARED (AS IT
*   WOULD BE FOR LINE 1) AND SO THE MESSAGE LINE(S) APPEAR IN A "NICE"
*   SPOT FOR THE FULLSCREEN DISPLAY IMAGE.
LDFSMDOK XC    REPLY,REPLY         CLEAR RESIDUAL DATA
         LA    R0,256              GET BUFFER SIZE
         LA    R1,REPLY            GET BUFFER ADDRESS
         ICM   R1,8,=X'81'         LOAD FLAGS FOR TGET ASIS,WAIT
         TGET  (1),(0),R           GET USER RESPONSE
         STM   R15,R1,TGETREGS
         LA    R0,X'0F'            RESET "UNINTERESTING" BITS
         NR    R15,R0                 IN TGET RETURN CODE
         CH    R15,=H'12'          WAS REPLY AREA LONG ENOUGH?
         BNE   LOADTGOT            YES, BRANCH
         TCLEARQ INPUT             NO, FLUSH QUEUED INPUT
LOADTGOT TM    STATUS4,FLAGI       ATTENTION INTERRUPT?
         BO    LOADEXIT            YES, ABORT THIS REQUEST
         LA    R0,3                GET READ HEADER DATA STREAM LENGTH
         C     R0,TGETREGS+8       PA KEY?  (SHOULDN'T GET CLEAR)
         BNL   LOADOTOK            YES, RESHOW SCREEN
         CLI   REPLY,X'7D'         ENTER KEY?
         BNE   LOADEXIT            NO, TERMINATE UPDATE IF PF KEY
         MVI   LDINDSN,WCCNULL     USE LDINDSN AS TEMPORARY TPUT BUFFER
         MVI   LDINDSN+1,SBA
         L     R1,SCRNCOLS
         AR    R1,R1               ROW 3 COLUMN 1
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,LDINDSN+2      SUPPLY ADDRESS FOR THIS TERMINAL
         MVI   LDINDSN+4,RA
         LA    R1,7
         M     R0,SCRNCOLS         ROW 8 COLUMN 1
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,LDINDSN+5      SUPPLY ADDRESS FOR THIS TERMINAL
         MVI   LDINDSN+7,C' '
         LA    R1,LDINDSN          POINT TO START OF DATA STREAM
         LA    R0,8                GET LENGTH OF DATA STREAM
         ICM   R1,8,=X'03'         LOAD TPUT FLAGS (FULLSCREEN)
         TPUT  (1),(0),R           ERASE ANY DAIRFAIL MESSAGE
         LA    R1,REPLY+3          POINT PAST READ HEADER
         ST    R1,FLDPTR           POINT TO FIRST INPUT FIELD
*
*               PROCESS FIELDS IN TGET BUFFER
*
LDPARSEF L     R7,FLDPTR           POINT TO NEXT FIELD TO PROCESS
         LA    R1,REPLY            POINT TO INPUT BUFFER
         LR    R0,R7               POINT TO SBA OF CURRENT FIELD
         SR    R0,R1               GET LENGTH ALREADY PROCESSED
         L     R14,TGETREGS+8      GET TOTAL LENGTH
         SR    R14,R0              GET LENGTH STILL TO PROCESS
         BNP   LDINDONE            NOTHING LEFT
         CLI   0(R7),SBA           DOES FIELD START WITH SBA ORDER?
         BNE   LOADOTOK            NO, UNEXPECTED DATA SO RESEND PANEL
         SLR   R3,R3               CLEAR FOR INSERT
         ICM   R3,3,1(R7)          GET ADDRESS OF FIELD
         TM    1(R7),X'40'         12-BIT ADDRESS?
         BZ    LOAD14AD            NO, 14-BIT ADDRESS
         SLL   R3,2                YES, SHIFT 2 BITS OUT OF LOW BYTE
         ICM   R3,2,1(R7)          RELOAD HIGH BYTE
         SLL   R3,18               SHIFT OUT 2 BITS OF HIGH BYTE
         SRL   R3,20               NOW HAVE 14-BIT ADDRESS
LOAD14AD SLR   R2,R2               CLEAR FOR DIVIDE
         D     R2,SCRNCOLS         GET RELATIVE LINE AND COLUMN
         LTR   R3,R3               INPUT FROM TOP LINE OF SCREEN?
         BNP   LOADOTOK            YES, USER MUST HAVE HIT CLEAR
         LA    R1,0(R14,R7)        POINT PAST THE INPUT DATA
         L     R2,=V(FINDSBA)      POINT TO SBA TRANSLATE TABLE
         SH    R14,=H'3'           IGNORE FIRST SBA ORDER
         EX    R14,LDPRSTRT        FIND THE NEXT SBA
         ST    R1,FLDPTR           SAVE NEXT INPUT FIELD ADDRESS
         LR    R14,R1              GET NEW SBA ADDRESS
         SR    R14,R7              GET FIELD LENGTH
         SH    R14,=H'3'           GET DATA LENGTH
         CH    R3,=H'8'            FIELD FROM RELATIVE LINE 8?
         BE    LDPRSDSN            YES, GET INPUT DATA SET NAME
         CH    R3,=H'14'           FIELD FROM RELATIVE LINE 14?
         BE    LDPRSMSK            YES, GET LOAD MEMBER NAME MASK
         CH    R3,=H'18'           FIELD FROM RELATIVE LINE 18?
         BE    LDPRSTAT            YES, GET STATISTICS OPTION
         B     LOADOTOK            UNEXPECTED LINE SO RESEND PANEL
LDPRSTRT TRT   3(0,R7),0(R2)       <<< EXECUTED >>>
LDPRSDSN LTR   R14,R14             ANY DATA TYPE IN?
         BNP   LDPARSBD            NO, FIELD JUST ERASED
         LA    R3,3(,R7)           POINT TO START OF DSNAME
LDSNBLNK CLI   0(R3),C' '          LEADING BLANK?
         BNE   LDSNSTRT            NO, HAVE DSNAME START
         LA    R3,1(,R3)           YES, POINT TO NEXT CHARACTER
         BCT   R14,LDSNBLNK        CONTINUE SEARCH FOR FIRST NON-BLANK
         B     LDPARSBD            INPUT WAS JUST BLANK
LDSNSTRT MVI   LDINDSN,C' '        BLANK OUT DATA SET NAME
         MVC   LDINDSN+1(L'LDINDSN-1),LDINDSN
         CLI   0(R3),QUOTE         QUOTED DATA SET NAME?
         BE    LDSNQUOT            YES
         MVI   LDSNFLAG,0          NO, FLAG NOT QUOTED
         L     R2,REVUPT           POINT TO USER PROFILE TABLE
         USING UPT,R2
         MVC   LDINDSN(7),UPTPREFX COPY THE PREFIX
         SLR   R0,R0
         IC    R0,UPTPREFL         GET THE PREFIX LENGTH
         DROP  R2                  UPT
         LR    R2,R0               COPY THE PREFIX LENGTH
         LA    R2,LDINDSN(R2)      POINT PAST PREFIX
         MVI   0(R2),C'.'          SUPPLY PERIOD
         LA    R2,1(,R2)           POINT PAST PERIOD
         B     LDSNREST            TIME TO FINISH LOADING THE DSNAME
LDSNQUOT MVI   LDSNFLAG,1          FLAG QUOTED
         LA    R3,1(,R3)           POINT PAST THE QUOTE
         LA    R2,LDINDSN          POINT TO TARGET DATA SET NAME FIELD
         BCT   R14,LDSNREST        DECREASE LENGTH
         B     LDPARSBD            INPUT WAS JUST A QUOTE
LDSNREST LA    R4,0(R14,R3)        POINT PAST DSNAME
         BCTR  R4,0                POINT TO LAST BYTE OF DSNAME
LDSNNDLP CLI   0(R4),C' '          TRAILING BLANK?
         BNE   LDSNEND             NO, FOUND THE END OF THE DSNAME
         BCTR  R4,0                YES, BACK UP A BYTE
         BCT   R14,LDSNNDLP        DECREASE LENGTH
         B     LDPARSBD            INPUT WAS JUST A QUOTE AND BLANK(S)
LDSNEND  CLI   0(R4),QUOTE         TRAILING QUOTE?
         BNE   LDSNQUAL            NO, QUALIFICATION NOW COMPLETE
         CLI   LDSNFLAG,0          QUOTED DATA SET NAME?
         BE    LDPARSBD            NO
         BCTR  R4,0                YES, QUOTE ALLOWED, SO BACK UP ONE
         BCT   R14,LDSNQUAL        ADJUST LENGTH
         B     LDPARSBD            INPUT WAS JUST QUOTES
LDSNQUAL MVC   LDINMEM,=CL8' '     CLEAR MEMBER NAME
         L     R6,=V(DSNCHARS)     POINT TO TRANSLATE TABLE
         CLI   0(R4),C')'          MEMBER NAME PRESENT?
         BNE   LDSNFILL            NO
         LA    R0,9                YES, GET OPEN BRACKET SEARCH SIZE
         BCTR  R4,0                BACK UP A CHARACTER
         BCT   R14,LDOPENLP        ADJUST LENGTH
         B     LDPARSBD            INPUT WAS A BRACKET
LDOPENLP CLI   0(R4),C'('          START OF MEMBER NAME?
         BE    LDMEMNAM            YES
         CLI   0(R4),C' '          IMBEDDED BLANK?
         BE    LDPARSBD            YES
         CLI   0(R4),C'.'          IMBEDDED PERIOD?
         BE    LDPARSBD            YES
         BCTR  R4,0                NO, BACK UP A CHARACTER
         BCTR  R14,0               ADJUST LENGTH
         BCT   R0,LDOPENLP         CONTINUE SEARCH FOR OPEN BRACKET
         B     LDPARSBD            MEMBER NAME IS TOO LONG
LDMEMNAM BCTR  R14,0               GET LENGTH OF DSNAME PROPER
         LA    R15,9
         SR    R15,R0              GET THE SUPPLIED MEMBER NAME LENGTH
         BNP   LDPARSBD            NULL MEMBER NAME
         BCTR  R15,0               DECREMENT FOR EXECUTE
         EX    R15,LDSETMEM        COPY THE MEMBER NAME
         EX    R15,LDTRTMEM        ONLY VALID CHARACTERS FOR MEMBER?
         BNZ   LDPARSBD            NO   (R2 MODIFIED BY TRT)
         CLI   LDINMEM,C'0'        NAME STARTS WITH ALPHANUMERIC?
         BNL   LDPARSBD            NO
LDSNFILL LTR   R14,R14             STILL HAVE A VALID LENGTH?
         BNP   LDPARSBD            NO, TOO SHORT
         CH    R14,=H'44'
         BH    LDPARSBD            NO, TOO LONG
         BCTR  R14,0               DECREMENT FOR EXECUTE
         EX    R14,LDSETDSN        COPY THE DATA SET NAME
         EX    R14,LDTRTDSN        ONLY VALID CHARACTERS IN DSNAME?
         BNZ   LDPARSBD            NO
         L     R6,=V(KAPS)         POINT TO TRANSLATE TABLE
         TR    LDINMEM,0(R6)       FOLD MEMBER NAME TO UPPER CASE
         TR    LDINDSN,0(R6)       FOLD DATA SET NAME TO UPPER CASE
         LA    R4,1(R14,R2)        POINT PAST ULTIMATE DSNAME
         LA    R0,LDINDSN          POINT TO START OF ULTIMATE DSNAME
         SR    R4,R0               GET LENGTH OF ULTIMATE DSNAME
         STH   R4,LDINDSNL         SAVE IT
         LA    R6,MYDAPB
         USING DAPB08,R6
         XC    0(84,R6),0(R6)
         MVI   DA08CD+1,X'08'
         LA    R0,LDINDSNL
         ST    R0,DA08PDSN
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,LDINMEM
         MVC   DA08PSWD,=CL8' '
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15
         LTR   R2,R15
         BNZ   LDDAFAIL            DYNAMIC ALLOCATION FAILED
         MVI   LDSNFLAG,2          REMEMBER ALLOCATION
         MVI   LDSNFLAG,0          ACTUALLY...
         MVC   LDINDDN,DA08DDN     COPY GENERATED DD NAME
         B     LDPARSEF            INPUT FILE SUCCESSFULLY ALLOCATED
         DROP  R6                  DAPB08
LDDAFAIL STLINENO LINE=4,MODE=OFF  LET PUTLINE WRITE TO SCREEN
         NI    MODE,255-FSMODE     FULL SCREEN MODE NOW OFF
         LA    R1,MYDFPARM
*        USING DFDSECTD,R1         MAPPED BY IKJEFFDF DFDSECT=YES MACRO
         ST    R2,MYDFRC           IKJDAIR RETURN CODE
         LA    R15,MYDFRC
         ST    R15,4(,R1)          DFRCP
         LA    R15,MYDAPL
         ST    R15,0(,R1)          DFDAPLP
         SLR   R15,R15
         ST    R15,MYJEFF02
         LA    R15,MYJEFF02
         ST    R15,8(,R1)          DFJEFF02
         LA    R15,1               DFDAIR               (DFSVC99 IS 50)
         STH   R15,MYDFID
         LA    R15,MYDFID
         ST    R15,12(,R1)         DFIDP
         MVC   16(4,R1),CPPLPTR    DFCPPLP
*        DROP  R1                  DFDSECTD
         LINK  EP=IKJEFF18,SF=(E,LINKAREA)
         B     LDPARSBD            REPROMPT FOR DATA SET NAME
LDSETMEM MVC   LDINMEM(0),1(R4)    <<< EXECUTED >>>
LDSETDSN MVC   0(0,R2),0(R3)       <<< EXECUTED >>>
LDTRTMEM TRT   LDINMEM(0),0(R6)    <<< EXECUTED >>>
LDTRTDSN TRT   LDINDSN(0),0(R6)    <<< EXECUTED >>>
*
LDPRSMSK MVC   LOADMMSK,LOADSTAR   INITIALIZE MEMBER NAME PATTERN MASK
         LTR   R14,R14             ANY DATA TYPE IN?
         BNP   LDPARSEF            NO, FIELD JUST ERASED
         BCTR  R14,0               YES, GET ITS LENGTH CODE
         EX    R14,LDPARSMV        SET THE NEW MASK
         L     R2,=V(KAPS)         POINT TO TRANSLATE TABLE
         TR    LOADMMSK,0(R2)      FOLD TO UPPER CASE
         B     LDPARSEF            CONTINUE PARSE
LDPARSMV MVC   LOADMMSK(0),3(R7)   <<< EXECUTED >>>
*
LDPRSTAT LTR   R14,R14             ANY DATA TYPE IN?
         BNP   LDPARSBD            NO, FIELD JUST ERASED
         OI    3(R7),X'40'         ENSURE UPPER CASE CHARACTER
         MVI   GENSTATS,C'N'       DO NOT MANUFACTURE MEMBER STATISTICS
         CLI   3(R7),C'N'          IS THIS CORRECT?
         BE    LDPARSEF            YES, CONTINUE PARSE
         CLI   3(R7),C'Y'          VALID INPUT?
         BNE   LDPARSBD            NO
         MVI   GENSTATS,C'Y'       DO MANUFACTURE MEMBER STATISTICS
         B     LDPARSEF            CONTINUE PARSE
LDPARSBD ICM   R0,3,1(R7)          GET BUFFER ADDRESS OF FIELD
         MVC   REPLY(LOADFIXL),LOADFIX
         STCM  R0,3,REPLY+2        PLACE CURSOR ON BAD FIELD
         L     R1,SCRNCOLS         GET SCREEN WIDTH
         LA    R1,11(R1,R1)        GET BUFFER LOCATION FOR MESSAGE
         L     R15,=V(CALCPOSI)    GET CONVERSION ROUTINE ENTRY POINT
         BALR  R14,R15             CONVERT FOR 3270 DATA STREAM
         STCM  R1,3,REPLY+6        SUPPLY ADDRESS FOR THIS TERMINAL
         LA    R1,REPLY            POINT TO OUTPUT BUFFER
         LA    R0,LOADFIXL         GET OUTPUT DATA LENGTH
         B     LOADTPUT            ISSUE NEW PROMPT
*
*               PREPARE PARAMETERS AND ATTACH UTILITY PROGRAM
*
LDINDONE LA    R6,MYDAPB           ALLOCATE A SYSOUT DATA SET
         USING DAPB30,R6
         XC    0(84,R6),0(R6)
         MVI   DA30CD+1,X'30'
         MVC   DA30DDN(8),=CL8' '
         MVC   DA30UNIT,=CL8' '
         MVC   DA30SER,=CL8' '
         MVC   DA30PGNM,=CL8' '
         MVC   DA30FORM,=CL8' '    (4-BYTE FIELD)
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15
         LTR   R2,R15
         BNZ   LDATTACH            DYNAMIC ALLOCATION FAILED
         OI    LDSNFLAG,4          REMEMBER ALLOCATION
         MVC   LDPRTDDN,DA30DDN    COPY GENERATED DD NAME
         DROP  R6                  DAPB30
LDATTACH LA    R1,LDDDLEN
         ST    R1,LDPARM2          SET DD OVERRIDE PARAMETER ADDRESS
         OI    LDPARM2,X'80'       LAST ENTRY IN PLIST
         MVC   LOADSDYN(31),LDP1TEMP    LOAD PARAMETER TEMPLATE
         MVC   LOADSDYN+12(8),LOADMMSK  LOAD MEMBER NAME SELECTION MASK
         CLI   GENSTATS,C'Y'       SPF STATISTICS GENERATION REQUIRED?
         BE    LDPLIST             YES, PROGRAM PARAMETER COMPLETE
         MVI   LOADSDYN+1,25       NO, REMOVE 'SPF,' FROM PARAMETER
         MVC   LOADSDYN+6(21),LOADSDYN+10
LDPLIST  LA    R1,LOADSDYN
         ST    R1,LDPARM1          SET NORMAL PROGRAM PARAMETER ADDRESS
         SLR   R1,R1
         ST    R1,READR            CLEAR ECB
         LA    R1,LDPARM1          POINT TO PLIST FOR UTILITY
         ATTACH ECB=READR,SF=(E,LDATTCHD)
         ST    R1,LDTCBADR         SAVE TCB ADDRESS
         WAIT  ECB=READR
         MVI   READR,0             RESET FLAG BYTE OF ECB
         DETACH LDTCBADR           DETACH UTILITY TCB
         LA    R15,MYDAPB
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,LDINDDN
         MVC   DA18MNM,=CL8' '
         MVC   DA18SCLS,=CL8' '    (2-BYTE FIELD)
         DROP  R15                 DAPB18
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15             FREE INPUT SEQUENTIAL FILE
         TM    LDSNFLAG,4          REPORT FILE OVERRIDE ALLOCATED?
         BZ    LOADEXIT            NO, NO NEED TO FREE IT
         LA    R15,MYDAPB          YES
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,LDPRTDDN
         MVC   DA18MNM,=CL8' '
         MVC   DA18SCLS,=CL8' '    (2-BYTE FIELD)
         DROP  R15                 DAPB18
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15             FREE OUTPUT REPORT FILE

LOADEXIT DS    0H
         NI    STATUS4,255-FLAGI   RESET ATTENTION FLAG
         LA    R0,@PDSWKLN
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         DROP  R13                 @PDSLDWK
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @PDSLDWK
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE OTHER REGISTERS
         BR    R14                 RETURN WITH FREEMAIN RETURN CODE

         DROP  R11                 REVLOAD
         TITLE '  R E V L O A D   -   S T A T I C   A R E A  '
HEXL     DC    C'0123456789ABCDEF' HEXADECIMAL DIGIT TRANSLATE TABLE
LDATTCHS ATTACH EP=PDSLOAD,ECB=,SF=L
LDATTCHL EQU   *-LDATTCHS
LDATTCHM ATTACH EP=REVLMOD,ECB=,SF=L

LDP1TEMP DC    AL2(29),C'NEW,SPF,S(********),UPDTE(><)'
LOADSTAR EQU   LDP1TEMP+12,8       ASTERISKS

LOADFIX  DC    X'C411000013110000'
         DC    C'>>> INPUT FIELD HAS BAD OR MISSING DATA - PLEASE FIX <+
               <<'
LOADFIXL EQU   *-LOADFIX

LOADSCRN DC    AL1(WCCSTND,SBA,1,1,SF,PROHIS,SA,COLR,WHITE)
         DC    C'REVIEW'
         DC    AL1(RA,1,26),C' '
         DC    CL30'PDS UPDATE REQUEST ENTRY PANEL'
         DC    AL1(RA,1,76),C' '
         DC    CL5'R&REL'
         DC    AL1(RA,8,1),C' '
         DC    AL1(SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'SPECIFY INPUT SEQUENTIAL DATA SET NAME'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    AL1(RA,9,10),C' '
         DC    C'==>'
         DC    AL1(SF,UNPHIM,SA,COLR,RED,SA,HILT,USCR)          MDT ON
         DC    AL1(IC,RA,9,70,NULL)                           56 NULLS
         DC    AL1(SA,HILT,NULL,SF,PROLOS,SA,COLR,TURQOISE)
         DC    AL1(RA,15,2),C' '
         DC    C'MEMBER NAME PROCESSING FILTER MASK'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SF,UNPHI,SA,COLR,RED,SA,HILT,USCR)
         DC    C'********'
         DC    AL1(SA,HILT,NULL)
         DC    AL1(SF,PROLOS,SA,COLR,BLUE)
         DC    C' (*, % AND ? CAN MASK 1 CHAR)'
LDNOTPGM DC    AL1(SA,COLR,TURQOISE)
         DC    AL1(RA,19,2),C' '
         DC    C'GENERATE/CORRECT MEMBER STATISTICS?'
         DC    AL1(SF,PROHIS,SA,COLR,YELLOW)
         DC    C'==>'
         DC    AL1(SF,UNPHI,SA,COLR,RED,SA,HILT,USCR)
         DC    C'N'
         DC    AL1(SA,HILT,NULL,SF,PROLOS,SA,COLR,TURQOISE)
         DC    C'(Y/N)  '
         DC    AL1(SA,COLR,BLUE)
         DC    C'(SSI WILL BE DISCARDED IF Y)'
LDALLTRL DC    AL1(SF,PROHIS,RA,1,1),C' '
LDALTRLL EQU   *-LDALLTRL
LDSCRNLN EQU   *-LOADSCRN

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V L O A D   -   D Y N A M I C   A R E A  '
@PDSLDWK DSECT
         DS    18F                 REGISTER SAVE AREA
LOADMMSK DS    CL8                 MEMBER NAME LOAD MASK
LDINMEM  DS    CL8                 INPUT MEMBER NAME
LDINDSNL DS    H                   INPUT DSNAME LENGTH
LDINDSN  DS    CL70                INPUT DSNAME
LDTCBADR DS    A                   ATTACHED TCB ADDRESS
LDATTCHD DS    XL(LDATTCHL)        ATTACH CONTROL PARAMETER LIST
LDPARM1  DS    A                   POINTER TO USUAL PROGRAM PARAMETER
LDPARM2  DS    A                   POINTER TO DD OVERRIDE PARAMETER
GENSTATS DS    C
LDSNFLAG DS    X
LDDDLEN  DS    H                   LENGTH OF DD SLOT LIST
         DS    CL8                 SYSLIN SLOT
         DS    CL8                 OUTPUT MEMBER NAME SLOT
         DS    CL8                 SYSLMOD SLOT
         DS    CL8                 SYSLIB SLOT
LDINDDN  DS    CL8                 SYSIN SLOT
LDPRTDDN DS    CL8                 SYSPRINT SLOT
         DS    CL8                 SYSPUNCH SLOT
         DS    CL8                 SYSUT1 SLOT
LDOUTDDN DS    CL8                 SYSUT2 SLOT
         DS    CL8                 SYSUT3 SLOT
         DS    CL8                 SYSUT4 SLOT
LOADSDYN DS    256D                TPUT/TGET BUFFER
         DS    0D                  END OF DSECT
@PDSWKLN EQU   *-@PDSLDWK
         TITLE '  R E V D L N K  '
**********************************************************************
*                                                          *         *
*         DELINK LOAD MODULES AND WRITE OBJECT DECKS       *  GP@P6  *
*                                                          *  05/99  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF DELINKING        *
*        LOAD MODULES AND WRITING OBJECT DECKS TO A SEQUENTIAL       *
*        OUTPUT FILE.  PROGRAM OBJECTS ARE NOT SUPPORTED.  A         *
*        PARTITIONED DATA SET WITH RECFM=U WHICH IS NOT A PDSE       *
*        MUST BE BEING REVIEWED.                                     *
*                                                                    *
*           DAVID NOON'S DELINKER WRITTEN IN PL/I FROM CBT FILE      *
*        90 WILL BE ATTACHED BY REVIEW TO PERFORM THE DELINKING      *
*        PROCESS.  'DELINKI' USES THE FOLLOWING FILES:               *
*           SYSLIB   - LOAD MODULE INPUT.                            *
*                      THIS IS THE PDS BEING REVIEWED.               *
*           SYSIN    - CONTROL STATEMENT INPUT.                      *
*                      THIS IS ALLOCATED AND PRIMED BY REVDLNK.      *
*           SYSLIN   - OBJECT DECK OUTPUT FILE.                      *
*                      MUST BE PRE-ALLOCATED BY THE USER.            *
*           LINKCNTL - USER-SUPPLIED LINKAGE EDITOR STATEMENTS.      *
*                      A DUMMY FILE IS ALLOCATED BY REVDLNK.         *
*           SYSPRINT - REPORT OUTPUT FILE.                           *
*                      A DUMMY FILE IS ALLOCATED BY REVDLNK.         *
*                                                                    *
*           NOTE THAT THE ABOVE ARE DEFAULT DDNAMES.  REVDLNK        *
*        USES THE OPTIONAL 40-BYTE PROGRAM PARAMETER TO SPECIFY      *
*        THE ACTUAL 5 DDNAMES (IN THE ORDER ABOVE) TO BE USED BY     *
*        DELINKI.                                                    *
*                                                                    *
*           DELINKI REQUIRES THAT SYSIN, SYSLIN AND LINKCNTL         *
*        HAVE FIXED-LENGTH 80-BYTE RECORDS.  THE FILES ALLOCATED     *
*        FOR SYSIN, LINKCNTL AND SYSPRINT ARE DELETED AFTER          *
*        DELINKI TERMINATES.                                         *
*                                                                    *
*           THE OUTPUT FILE USED IS SYSUT2, OR WHATEVER THE          *
*        LATEST DDNAME SPECIFIED BY AN =SETFILE COMMAND IS.          *
*                                                                    *
*           IF ANY MEMBERS ARE TAGGED, THEN ONLY TAGGED MEMBERS      *
*        ARE DELINKED.  IF NO MEMBERS ARE TAGGED THEN ALL MEMBERS    *
*        ARE DELINKED.                                               *
*                                                                    *
**********************************************************************

**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA         (INPUT COMMAND IN 'DOUBLE' *
*                     R13-> @DYNAREA                    UPON ENTRY)  *
*                                                                    *
* DURING PROCESSING   R3 -> @DYNAREA                                 *
*                     R13-> @DLNKWRK (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 SET FROM IKJDAIR FILE ALLOCATION           *
*                           0 IF ATTACH ISSUED (ALLOCATIONS OKAY)    *
*                           OTHERWISE ALLOCATION FAILED SO DELINKI   *
*                                 WAS NOT ATTACHED                   *
*                     READR = SUBTASK COMPLETION CODE                *
*                             (HIGH BYTE ZERO IF ATTACH ISSUED)      *
*                                                                    *
**********************************************************************
         EJECT
REVDLNK  CSECT
         B     @DELINK-*(,R15)
         DC    AL1(7),CL7'REVDLNK'
@DELINK  STM   R14,R12,12(R13)
         LR    R11,R15
         USING REVDLNK,R11
         LA    R0,@DLNKWKL
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R3,R13              POINT TO @DYNAREA FROM REVPODIR
         USING @DYNAREA,R3
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @DLNKWRK,R13
         MVC   DLSYSIND(DLSYSINL),DLSYSINS
         MVC   DLATTCHD(DLATTCHL),DLATTCHS
         USING IHADCB,DLSYSIND


************************************************************
*                                                          *
*         ALLOCATE FILE AND SET PROGRAM PARAMETER          *
*                                                          *
************************************************************

         LA    R1,DLPARML
         ST    R1,DLPARMAD         SET PROGRAM PARAMETER ADDRESS
         OI    DLPARMAD,X'80'      ONE PARAMETER IN PLIST
         XC    DLPARML(42),DLPARML CLEAR RESIDUAL DATA
         MVI   DLPARML+1,40        SET PROGRAM PARAMETER LENGTH
         MVC   DLSYSLIB,$DDNAME    SET LOAD MODULE PDS DDNAME
         MVC   DLSYSLIN,OFFDDNAM   SET OBJECT DECK OUTPUT DDNAME
         LA    R6,MYDAPB
         USING DAPB08,R6

         XC    0(84,R6),0(R6)      ALLOCATE "SYSIN"
         MVI   DA08CD+1,X'08'
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08DSP1,DA08NEW
         MVI   DA08DPS2,DA08DELE
         MVI   DA08DPS3,DA08DEL
         MVI   DA08CTL,DA08TRKS    SPACE=(TRK,(1,3))
         MVI   DA08PQTY+3,1
         MVI   DA08SQTY+3,3
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15
         LTR   R2,R15
         BNZ   DLDAFAIL            DYNAMIC ALLOCATION FAILED
         MVC   DLSYSIN,DA08DDN     COPY GENERATED DD NAME

         XC    0(84,R6),0(R6)      ALLOCATE "LINKCNTL"
         MVI   DA08CD+1,X'08'
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08CTL,DA08DMMY
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15
         ST    R15,16(,R3)         SET REVDLNK RETURN CODE
         LTR   R2,R15
         BNZ   DLDAFAIL            DYNAMIC ALLOCATION FAILED
         MVC   DLLNKCTL,DA08DDN    COPY GENERATED DD NAME
         MVC   DCBDDNAM,DLLNKCTL   SET "LINKCNTL" DDNAME IN DCB
         MVI   OPEND,X'80'         OPEN THE OUTPUT DCB
         OPEN  (DLSYSIND,OUTPUT),MF=(E,OPEND)
         MVI   CLOSED,X'80'        DCB VALUES NOW SET IN JFCB
         CLOSE (DLSYSIND),MF=(E,CLOSED)

         XC    0(84,R6),0(R6)      ALLOCATE "SYSPRINT"
         MVI   DA08CD+1,X'08'
         MVC   DA08DDN(8),=CL8' '
         MVC   DA08UNIT,=CL8' '
         MVC   DA08SER,=CL8' '
         MVC   DA08MNM,=CL8' '
         MVC   DA08PSWD,=CL8' '
         MVI   DA08CTL,DA08DMMY
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15
         ST    R15,16(,R3)         SET REVDLNK RETURN CODE
         LTR   R2,R15
         BNZ   DLDAFAIL            DYNAMIC ALLOCATION FAILED
         MVC   DLSYSPRT,DA08DDN    COPY GENERATED DD NAME

         DROP  R6                  DAPB08


************************************************************
*                                                          *
*         PRIME "SYSIN" WITH DELINKI CONTROL STATEMENTS    *
*                                                          *
************************************************************

         MVC   DCBDDNAM,DLSYSIN    SET "SYSIN" DDNAME IN DCB
         MVI   OPEND,X'80'         OPEN THE OUTPUT DCB
         OPEN  (DLSYSIND,OUTPUT),MF=(E,OPEND)
         MVI   DLNKREC,C' '        SETUP OUTPUT RECORD BUFFER
         MVC   DLNKREC+1(79),DLNKREC
         MVC   DLNKREC+1(7),=C'MEMBER('

         LA    R4,DIRENTS          POINT TO FIRST INTERNAL ENTRY
DLKMEMLP C     R4,LASTADDR         END OF INTERNAL DIRECTORY ENTRIES?
         BNL   DLKCTLOK            YES, END UP
         ICM   R14,15,OFFLDCNT     DELINKING EVERYTHING?
         BZ    DLKITALL            YES
         TM    13(R4),X'40'        NO, OFFLOADING THIS ONE?
         BZ    DLKNXTMM            NO, TRY THE NEXT
DLKITALL MVC   DLNKREC+8(8),0(R4)  COPY MEMBER NAME
         LA    R1,DLNKREC+15       POINT TO LAST BYTE OF MEMBER NAME
         MVI   1(R1),C')'          COMPLETE CONTROL STATEMENT
         MVI   2(R1),C'.'
DLKBLKLP CLI   0(R1),C' '          TRAILING BLANK IN MEMBER NAME?
         BNE   DLKPUTIT            NO, OUTPUT RECORD READY TO WRITE
         MVC   0(3,R1),1(R1)       YES, SHUFFLE UP TO OVERLAY
         BCT   R1,DLKBLKLP         BACK UP A BYTE AND CHECK AGAIN
DLKPUTIT PUT   DLSYSIND,DLNKREC    WRITE CONTROL STATEMENT
DLKNXTMM AH    R4,14(,R4)          POINT TO NEXT INTERNAL ENTRY
         B     DLKMEMLP            PROCESS THE NEXT DIRECTORY ENTRY

DLKCTLOK MVI   CLOSED,X'80'        CLOSE THE OUTPUT DCB
         CLOSE (DLSYSIND),MF=(E,CLOSED)


************************************************************
*                                                          *
*         ATTACH DELINKI                                   *
*                                                          *
************************************************************

         SLR   R1,R1
         ST    R1,READR            CLEAR ECB
         LA    R1,DLPARMAD         POINT TO PLIST FOR UTILITY
         ATTACH ECB=READR,SF=(E,DLATTCHD)
         ST    R1,DLTCBADR         SAVE TCB ADDRESS
         WAIT  ECB=READR
         MVI   READR,0             RESET FLAG BYTE OF ECB
         DETACH DLTCBADR           DETACH UTILITY TCB


************************************************************
*                                                          *
*         FREE FILES AND TERMINATE                         *
*                                                          *
************************************************************

DLNKTERM LA    R2,DLSYSIN
         BAL   R4,DLFREEDD         FREE "SYSIN"
         LA    R2,DLLNKCTL
         BAL   R4,DLFREEDD         FREE "LINKCNTL"
         LA    R2,DLSYSPRT
         BAL   R4,DLFREEDD         FREE "SYSPRINT"

         LA    R0,@DLNKWKL
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         DROP  R13,R3              @DLNKWRK, @DYNAREA
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @DLNKWRK
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN WITH DA08 RETURN CODE


************************************************************
*                                                          *
*         FILE DEALLOCATION SUBROUTINE                     *
*                                                          *
************************************************************
*                                                          *
* REGISTERS ON ENTRY  R2 -> DDNAME TO BE DEALLOCATED       *
*                     R4 -> CALLER'S RETURN POINT          *
*                                                          *
* ON EXIT R15 HAS IKJDAIR RETURN CODE WHICH IS NOT TESTED. *
* DEALLOCATIONS ARE ASSUMED TO BE SUCCESSFUL.              *
*                                                          *
************************************************************

DLFREEDD SLR   R15,R15             NO ERROR IF NO DDNAME
         CLI   0(R2),0             NULL DDNAME?
         BER   R4                  YES, NOTHING TO FREE
         LA    R15,MYDAPB
         USING DAPB18,R15
         XC    0(40,R15),0(R15)
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,0(R2)       LOAD DDNAME TO DEALLOCATE
         MVC   DA18MNM,=CL8' '
         MVC   DA18SCLS,=CL8' '    (2-BYTE FIELD)
         DROP  R15                 DAPB18
         LA    R1,MYDAPL
         L     R15,16              CVT
         L     R15,732(,R15)       CVTDAIR
         BALR  R14,R15             CALL IKJDAIR
         BR    R4                  RETURN TO CALLER


************************************************************
*                                                          *
*         PROCESS FILE ALLOCATION FAILURE                  *
*                                                          *
************************************************************

DLDAFAIL LA    R1,MYDFPARM
*        USING DFDSECTD,R1         MAPPED BY IKJEFFDF DFDSECT=YES MACRO
         ST    R2,MYDFRC           IKJDAIR RETURN CODE
         LA    R15,MYDFRC
         ST    R15,4(,R1)          DFRCP
         LA    R15,MYDAPL
         ST    R15,0(,R1)          DFDAPLP
         SLR   R15,R15
         ST    R15,MYJEFF02
         LA    R15,MYJEFF02
         ST    R15,8(,R1)          DFJEFF02
         LA    R15,1               DFDAIR               (DFSVC99 IS 50)
         STH   R15,MYDFID
         LA    R15,MYDFID
         ST    R15,12(,R1)         DFIDP
         MVC   16(4,R1),CPPLPTR    DFCPPLP
*        DROP  R1                  DFDSECTD
         LINK  EP=IKJEFF18,SF=(E,LINKAREA)
         B     DLNKTERM            TERMINATE REVDLNK


         DROP  R11                 REVDLNK

         TITLE '  R E V D L N K   -   S T A T I C   A R E A  '
         PRINT NOGEN
DLATTCHS ATTACH EP=DELINKI,ECB=,SF=L
DLATTCHL EQU   *-DLATTCHS

DLSYSINS DCB   DSORG=PS,MACRF=PM,DDNAME=DL-SYSIN,                      +
               RECFM=FB,LRECL=80,BLKSIZE=3120
DLSYSINL EQU   *-DLSYSINS
         PRINT GEN

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V D L N K   -   D Y N A M I C   A R E A  '
@DLNKWRK DSECT
         DS    18F                 REGISTER SAVE AREA
DLNKREC  DS    80C                 CONTROL STATEMENT BUFFER
DLATTCHD DS    XL(DLATTCHL)        ATTACH CONTROL PARAMETER LIST
DLSYSIND DS    XL(DLSYSINL)        DCB FOR DELINKI'S "SYSIN" FILE
DLTCBADR DS    A                   ATTACHED TCB ADDRESS
DLPARMAD DS    A                   POINTER TO PROGRAM PARAMETER
DLPARML  DS    H                   LENGTH OF PROGRAM PARAMTER
DLSYSLIB DS    CL8                 SYSLIB SLOT
DLSYSIN  DS    CL8                 SYSIN SLOT
DLSYSLIN DS    CL8                 SYSLIN SLOT
DLLNKCTL DS    CL8                 LINKCNTL SLOT
DLSYSPRT DS    CL8                 SYSPRINT SLOT
         DS    0D                  END OF DSECT
@DLNKWKL EQU   *-@DLNKWRK
         TITLE '  R E V D I V  '
**********************************************************************
*                                                          *         *
*         "DATA-IN-VIRTUAL" STORAGE MANAGER                *  GP@P6  *
*                                                          *  10/88  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE PROVIDES THE CAPABILITY OF SPEEDING UP      *
*        ACCESS TO REVIEWED DATA BY SAVING PREVIOUSLY READ DATA      *
*        IN VIRTUAL STORAGE.  IT IS ONLY USED ON MVS AND MSP         *
*        SYSTEMS WITH 31-BIT ADDRESSING SO THAT DATA STORED          *
*        RESIDES ABOVE THE 16 MEGABYTE LINE.  ACCESS TO DATA         *
*        BEING READ FOR THE FIRST TIME IS NOT IMPROVED.  SINCE       *
*        THIS MEANS THAT OUT-OF-DATE DATA MAY BE BROWSED IT          *
*        IS ONLY USED FOR PARTITIONED DATA SET MEMBERS BECAUSE       *
*        PDS'S ARE RARELY OPENED FOR UPDATE-IN-PLACE PROCESSING.     *
*        (THE LATEST VERSION OF THE MEMBER BEING REVIEWED CAN BE     *
*        ACCESSED VIA THE 'NEWTOP' COMMAND.)                         *
*           THE GETMAINED AREA SIZE IS SIX MEGABYTES ALTHOUGH        *
*        AS LITTLE AS TWO MEGABYTES WILL BE ACCEPTABLE TO REVDIV.    *
*        IF THE AREA IS BELOW THE LINE IT IS FREED AGAIN TO PREVENT  *
*        EXHAUSTING ALL USER REGION STORAGE.  PAGE ALIGNMENT IS      *
*        SPECIFIED FOR THE GETMAIN TO OPTIMIZE PAGE RELEASE          *
*        PROCESSING.                                                 *
*           AN INDEX METHOD OF STORAGE IS USED FOR FIXED- OR         *
*        VARIABLE-LENGTH RECORDS BY RESERVING SPACE FOR THE MAXIMUM  *
*        POSSIBLE RECORD LENGTH FOR EACH RECORD.  FOR VARIABLE-      *
*        LENGTH RECORD FILES THIS IS WASTFUL OF SPACE BUT ENABLES    *
*        DIRECT ACCESS TO THE DATA BASED ON RELATIVE RECORD NUMBER   *
*        WITHOUT CHAINING THROUGH THE STORAGE AREA, THUS KEEPING     *
*        RESULTANT PAGING ACTIVITY TO A MINIMUM.                     *
*           FOR UNDEFINED LENGTH RECORDS, ONLY THE SPACE NEEDED      *
*        FOR EACH RECORD (ROUNDED UP TO THE NEXT DOUBLEWORD          *
*        BOUNDARY) IS USED, WHICH MEANS THAT CHAINING FROM THE       *
*        FIRST RECORD SLOT IS DONE FREQUENTLY, BUT LOAD MODULE       *
*        MEMBERS RARELY HAVE THOUSANDS OR EVEN HUNDREDS OF           *
*        PHYSICAL RECORDS.  SINCE EACH LOGICAL RECORD IS ALSO A      *
*        PHYSICAL RECORD, ANY RESULTANT PAGING WILL CAUSE LESS       *
*        I/O THAN THE FILE I/O AVOIDED WITH "DIV" CACHING.           *
*                                                                    *
*           REVDIV EXECUTES IN 31-BIT ADDRESSABILITY MODE.           *
*                                                                    *
*           SPACE TRIMMING CAN BE PERFORMED.  THE GETMAINED AREA     *
*        SHOULD BE ON PAGE BOUNDARIES AND SO SHOULD BE ALL ZEROS     *
*        UPON FIRST ACCESS.  A X'DA' IS PLACED IN THE SECOND BYTE    *
*        OF THE WHOLE AREA STRAIGHT AFTER THE GETMAIN BEFORE PHASE   *
*        2 HAS OPENED THE FILE.  (THE FIRST FULLWORD IS THE LENGTH   *
*        INDICATOR OF THE FIRST RECORD SO THIS BYTE SHOULD ALWAYS    *
*        BE ZERO ANYWAY EXCEPT FOR AN EMPTY MEMBER OR FILE WHEN IT   *
*        WILL BE X'FF'.)  WHEN A RECORD IS STORED THIS BYTE IS       *
*        EXAMINED.  IT SHOULD ONLY BE X'DA' WHEN STORING THE FIRST   *
*        RECORD FOR THE FIRST MEMBER SELECTED.  AT RECORD STORE      *
*        TIME THE FILE IS OPEN AND THE DEB IS EXAMINED TO COUNT      *
*        THE NUMBER OF TRACKS IN THE DATA SET.  (TRIMMING IS ONLY    *
*        ATTEMPTED FOR DISK FILES.)  BASED ON THE TRACK COUNT, THE   *
*        DISK TYPE, THE RECORD FORMAT AND A COUPLE OF ARBITRARY      *
*        FUDGE FACTORS AN UPPER LIMIT TO THE "DATA-IN-VIRTUAL" SIZE  *
*        REQUIREMENT IS DETERMINED.  (REMEMBER, A 20 CYLINDER PDS    *
*        COULD HAVE ONE MEMBER WITH 20 CYLINDERS OF DATA.)  IF THIS  *
*        IS SMALLER THAN THE SIZE ALREADY ACQUIRED THEN THE EXCESS   *
*        IS FREED SO THAT A RECURSIVE 'REVIEW' WITH A DIV NEED, OR   *
*        EVEN ANOTHER PROGRAM, HAS MORE STORAGE AVAILABLE FOR USE.   *
*        THE AREA TO BE RETAINED IS ROUNDED UP TO THE NEXT PAGE      *
*        BOUNDARY.  THE WHOLE IDEA IS TO NOT DENY ACCESS TO MEGA-    *
*        BYTES OF VIRTUAL STORAGE BY OTHER PROCESSES JUST BECAUSE    *
*        OF A 'REVIEW' OF A THREE TRACK DATA SET.                    *
*           NOTE THAT GETMAINING A FEW MEGABYTES OF VIRTUAL STORAGE  *
*        AND NOT USING IT DOES NOT REALLY DEGRADE PERFORMANCE BY     *
*        ITSELF, BUT BECAUSE OF THE USUAL REGION SIZE LIMITATIONS    *
*        IT MEANS THAT OTHER PROCESSES COULD BE DENIED ACCESS TO     *
*        OPTIMAL STORAGE CAPACITIES.  (THESE "OTHER PROCESSES"       *
*        COULD ONLY PROBABLY BE INITIATED BY THE 'TSO' SUBCOMMAND.)  *
*                                                                    *
**********************************************************************
*                                                                    *
*                   COUNT = RELATIVE RECORD NUMBER                   *
*                                                                    *
* REGISTERS ON ENTRY  R15-> REVDIV                                   *
*                     R14-> RETURN ADDRESS                           *
*                     R9 -> @DATA                                    *
*                     R4 -> DCB                                      *
*                     R0  = ENTRY CODE                               *
*                           0 = GETMAIN DIV STORAGE                  *
*                           4 = STORE A RECORD                       *
*                             R1 = RECORD LENGTH ON ENTRY            *
*                             R2 = RECORD ADDRESS ON ENTRY           *
*                           8 = RETRIEVE A RECORD                    *
*                             R1 = RECORD LENGTH ON EXIT             *
*                             R2 = RECORD ADDRESS ON EXIT            *
*                          12 = RELEASE DIV SYSTEM STORAGE           *
*                          16 = FREEMAIN DIV STORAGE                 *
*                                                                    *
* RETURN CODES        R15 = 0         PROCESSING COMPLETE            *
*                     R15 = 4         PROCESSING INCOMPLETE          *
*                                                                    *
**********************************************************************

REVDIV   CSECT
REVDIV   AMODE 31                  CACHE DATA IN EXTENDED REGION
         B     @DIV-*(,R15)
         DC    AL1(7),CL7'REVDIV'
@DIV     STM   R14,R12,12(R13)
         LR    R10,R15
         LA    R15,1
         LA    R11,4095(R15,R10)
         USING REVDIV,R10,R11
         LR    R15,R0              GET ENTRY CODE
         B     DIVENTYS(R15)       BRANCH TO ENTRY POINT
DIVENTYS B     DIVGETMN        0 - CREATE DIV VIRTUAL STORAGE AREA
         B     DIVSTORE        4 - STORE A RECORD
         B     DIVRETRV        8 - RETRIEVE A RECORD
         B     DIVPGREL       12 - RELEASE DIV REAL & AUXILIARY STORAGE
         B     DIVFREMN       16 - FREE DIV VIRTUAL STORAGE AREA

DIVGETMN DS    0H                  DIV ENTRY CODE 0
         XC    DIVCHKPT,DIVCHKPT   RESET DIV CHECKPOINT
         ICM   R1,B'1111',DIVREG1  POINT TO DIV AREA
         BM    DIVISBAD            DIV SETUP FAILED PREVIOUSLY
         BP    GOTDIV              DIV SETUP WORKED PREVIOUSLY
         ICM   R0,B'1111',DIVSZREQ ANY SPECIFIC SIZE REQUESTED?
         BNP   DIVUSUAL            NO, DO THE USUAL VARIABLE THING
         GETMAIN RC,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE
         L     R0,DIVSZREQ         RELOAD SIZE REQUEST
         XC    DIVSZREQ,DIVSZREQ   CLEAR SPECIFIC REQUEST SIZE
         B     DIVGOTMN            GO TEST RETURN CODE
DIVUSUAL GETMAIN VRC,LV=(6291456,2097152),LOC=(ANY,ANY),BNDRY=PAGE
DIVGOTMN LTR   R15,R15             DID GETMAIN WORK?
         BZ    USEDIV              YES, PROCEED
DIVBELOW MVI   DIVREG1,X'FF'       NO, FLAG FAILURE
DIVISBAD LM    R14,R12,12(R13)     RESTORE REGISTERS
         LA    R15,4               LOAD NON-ZERO RETURN CODE
         BSM   0,R14               RETURN WITH FREEMAIN RETURN CODE
USEDIV   STM   R0,R1,DIVREGS       SAVE FREEMAIN VALUES
         CLI   DIVREG1,0           IS DIV AREA ABOVE THE LINE?
         BNE   GOTDIV              YES, JOLLY GOOD SO CONTINUE
         FREEMAIN RU,A=(1),LV=(0)  NO, BAD SHOW, FREE IT AGAIN
         XC    DIVREGS,DIVREGS     FLAG FREE DIV STORAGE
         B     DIVBELOW            ONLY DIV WAS BELOW THE LINE
GOTDIV   TM    STATUS8,ZIPD+ZIPF   PROCESSING A ZIP FILE?
         BNZ   *+8                 YES, DO NOT TRIM ON DISK SPACE BASIS
         MVI   1(R1),X'DA'         FLAG DATA AREA TRIMMING REQUIREMENT
         NI    STATUS8,255-DIVFULL NO RECORD STORE HAS FAILED YET
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SLR   R15,R15             GETMAIN WAS SUCCESSFUL
         BSM   0,R14               RETURN FOR ENTRY CODE 0

DIVSTORE DS    0H                  DIV ENTRY CODE 4
         ICM   R5,B'1111',DIVREG1  POINT TO THE DIV HOLD AREA
         BNP   DIVSTBAD            NO DIV STORAGE SO RETURN
         CLI   1(R5),X'DA'         DATA AREA TRIMMING FLAGGED?
         BNE   DIVFFLAG            NO, NOT FIRST STORE AFTER GETMAIN
         MVI   1(R5),0             YES, REMOVE FLAG FOR FUTURE
         ICM   R3,B'1111',$UCBAD   POINT TO THE UCB
         BZ    DIVFFLAG            THERE IS NONE
         CLI   18(R3),X'20'        DASD CLASS?
         BNE   DIVFFLAG            NO, HOW LONG IS A PIECE OF TAPE?
         SLR   R15,R15             YES, REMEMBER TO CHECK IT OUT
DIVFFLAG TM    STATUS10,UZ80       UNZIPPING FIXED-LENGTH RECORDS?
         BO    DIVSTIDX            YES, USE THE INDEX STORAGE METHOD
         TM    STATUS8,ZIPD+ZIPF   PROCESSING A ZIP FILE?
         BNZ   DIVSTZIP            YES, MAKE OPTIMAL USE OF STORAGE
         TM    TSTRECFM,X'C0'      FIXED OR VARIABLE LENGTH RECORDS?
         BM    DIVSTIDX            YES, USE THE INDEX STORAGE METHOD
DIVSTZIP ICM   R7,15,DIVCPADR      PREVIOUS ENTRY'S DETAILS AVAILABLE?
         BNP   DIVSTSLO            NO, CHAIN THROUGH FROM START
         L     R0,COUNT            YES, GET NEW RELATIVE RECORD NUMBER
         S     R0,DIVCPCNT         GET NUMBER OF LINKS TO CHAIN
         BNM   DIVSTLNK            NOT GOING BACKWARDS SO USE CHKPT
DIVSTSLO LR    R7,R5               NO, USE CHAIN STORAGE METHOD
         L     R0,COUNT            GET RELATIVE RECORD INDEX (1ST IS 0)
DIVSTLNK A     R5,DIVREG0          POINT PAST END OF DIV AREA
         LTR   R0,R0               ANY CHAINING TO DO?
         BZ    DIVSTREC            NO, GO STORE RECORD
DIVSTCHN A     R7,0(,R7)           ADD THE LENGTH OF CHAINED RECORD -
*                                  ASSUME NO IMBEDDED E-O-F (LENGTH=-1)
         LA    R7,DIVOVHD+7(,R7)   ALLOW FOR RECORD OVERHEAD
         SRL   R7,3                ROUND TO DOUBLEWORD BOUNDARY
         SLL   R7,3
         CR    R7,R5               STILL IN DIV AREA?
         BNL   DIVSTBAD            NO, THIS RECORD CANNOT BE STORED
         BCT   R0,DIVSTCHN         YES, CONTINUE TO FOLLOW CHAIN
         LTR   R6,R1               COPY NEW RECORD LENGTH
         BNM   DIVCHNLN
         SLR   R6,R6               NO DATA IF E-O-F
DIVCHNLN LA    R0,DIVOVHD(R6,R7)   POINT PAST NEW RECORD STORAGE AREA
         CR    R0,R5               WILL THIS RECORD FIT?
         BNH   DIVSTREC            YES, GO STORE THE RECORD HERE
DIVSTBAD OI    STATUS8,DIVFULL     DIV STORAGE AREA IS "CHOCKERS"
         B     DIVISBAD            RETURN TO CALLER
DIVSTIDX L     R7,COUNT            GET RECORD INDEX (FIRST IS 0, ETC.)
         LA    R8,80
         TM    STATUS10,UZ80       UNZIPPING CARD IMAGES?
         BO    *+8                 YES
         L     R8,SAVLRECL         GET ROUNDED MAX LRECL
         LA    R8,DIVOVHD(,R8)     ADD OVERHEAD
         MR    R6,R8               GET OFFSET INTO STORAGE AREA
         AR    R7,R8               ADD SIZE OF ANOTHER RECORD
         C     R7,DIVREG0          DOES IT FIT INTO STORAGE AREA?
         BH    DIVSTBAD            NO, SORRY
         SR    R7,R8               YES, POINT BACK TO REQUIRED RECORD
         AR    R7,R5               POINT TO SPOT IN DIV AREA
DIVSTREC ST    R1,0(,R7)           SAVE RECORD LENGTH
         CLC   DIVHICNT,COUNT      IS THIS A LATER RECORD?
         BNL   SAMEBLOK            NO
         MVC   DIVHICNT,COUNT      YES, SAVE THE NEW HIGH RECORD COUNT
         MVC   DIVHITTR,TTR             SAVE THE NEW HIGH TTR
SAMEBLOK MVC   4(4,R7),TTR         SAVE THE TTR OF THIS RECORD
         MVC   DIVCPCNT,COUNT      SAVE THE RELATIVE RECORD NUMBER
         ST    R7,DIVCPADR         SAVE CORRESPONDING ENTRY ADDRESS
         LTR   R1,R1               IS THE LENGTH POSITIVE?
         BZ    DIVSTORD            NO, IT IS A NULL RECORD
         BM    DIVENDOF            NO, IT IS END-OF-FILE
DIVSTRLP CH    R1,=H'256'          LENGTH LEFT LESS THAN 256
         BL    DIVSTRIT            YES, MOVE THE REST OF IT
         MVC   8(256,R7),0(R2)     NO, MOVE 256 BYTES OF IT
         LA    R2,256(,R2)         ADJUST SOURCE POINTER
         LA    R7,256(,R7)         ADJUST TARGET POINTER
         SH    R1,=H'256'          ADJUST REMAINING-DATA COUNT
         BZ    DIVSTORD            (MULTIPLE OF 256)
         B     DIVSTRLP            PROCESS NEXT BIT
DIVENDOF MVI   DIVHITTR,X'FF'      WILL NEVER GET A HIGHER TTR THAN EOF
         LA    R1,DIVOVHD(,R7)     NEED OVERHEAD FOR THE E-O-F RECORD
         S     R1,DIVREG1          GET REQUIRED STORAGE CAPACITY
         TM    STATUS8,ZIPD+ZIPF   PROCESSING A ZIP FILE?
         BNZ   DIVSTRIM            YES, TRIM UNNECESSARY STORAGE
         ICM   R0,B'1111',$UCBAD   READING FROM I/O DEVICE?
         BNZ   DIVSTORD            YES, SO PROCEED AS NORMAL
         B     DIVSTRIM            TRY TO TRIM UNNECESSARY STORAGE
DIVSTRTL MVC   8(0,R7),0(R2)       <<< EXECUTED >>>
DIVSTRIT BCTR  R1,0                LESS 1 FOR EXECUTE
         EX    R1,DIVSTRTL         STORE THE TAIL OF THE RECORD
DIVSTORD LTR   R15,R15             NON-ZERO ENTRY CODE STILL THERE?
         BNZ   DIVSTORX            YES, NOT TRIMMING GETMAINED AREA NOW
         SLR   R0,R0               ERASE (UCB ADDRESS) FOR INSERT
         IC    R0,MYDSCB-44+59     GET DS1NOEPV (NUMBER OF EXTENTS)
         USING IHADCB,R4
         L     R1,DCBDEBAD         POINT TO DATA EXTENT BLOCK
         DROP  R4                  IHADCB
         LA    R15,32(,R1)         POINT TO DEB DASD SECTION
         SLR   R1,R1               ZERO TRACK COUNTER
         SLR   R14,R14             (CAN'T USE AH IF OVER 32K TRK XTNT)
DEBLOOPD ICM   R14,B'0011',14(R15) GET TRACKS IN THIS EXTENT
         AR    R1,R14              ACCUMULATE TRACK COUNT
         LA    R15,16(,R15)        POINT TO SECTION FOR NEXT EXTENT
         BCT   R0,DEBLOOPD         PROCESS NEXT EXTENT IF IT EXISTS
         L     R15,$UCBAD          POINT TO THE (DASD) UCB
         SLR   R3,R3
         CLI   19(R15),X'85'       FUJITSU 6421?
         BE    DIVDSKOK            YES, USE ZERO INDEX
         CLI   19(R15),X'0F'       KNOWN DISK TYPE?
         BH    DIVSTORX            NO, SKIP WHOLE TRIMMING EXERCISE
         IC    R3,19(,R15)         LOAD DASD MODEL BYTE
         SLL   R3,2                MULTIPLY BY 4 FOR INDEX
DIVDSKOK L     R15,DIVTRKTB(R3)    LOAD MAXIMUM TRACK CAPACITY
         LTR   R15,R15             KNOWN SIZE?
         BNP   DIVSTORX            NO, SKIP WHOLE TRIMMING EXERCISE
         MR    R0,R15              GET ALLEGED MAXIMUM FILE DATA SIZE
         LTR   R0,R0               REALLY BIG FILE/MEMBER?
         BNZ   DIVSTORX            YES, DROP THE WHOLE IDEA
         LTR   R1,R1               REALLY BIG FILE/MEMBER?
         BNP   DIVSTORX            YES, DROP THE WHOLE IDEA
         SLA   R1,1                ADD EXTRAVAGANT OVERHEAD ALLOWANCE
         BO    DIVSTORX            OVERFLOW SO DROP THE WHOLE IDEA
         TM    TSTRECFM,X'40'      VARIABLE-LENGTH RECORDS?
         BZ    DIVSTRIM            NO, THIS SHOULD DO
         SLA   R1,1                YES, ADD SHORT RECORD ALLOWANCE
         BO    DIVSTORX            OVERFLOW SO DROP THE WHOLE IDEA
DIVSTRIM LA    R1,4095(,R1)
         SRL   R1,12
         SLL   R1,12               ENSURE WHOLE NUMBER OF 4K PAGES
         L     R0,DIVREG0          GET THE ORIGINAL SIZE
         CLR   R1,R0               SMALLER THAN ACTUAL SIZE ANYWAY?
         BNL   DIVSTORX            NO, FORGET ABOUT TRIMMING
         ST    R1,DIVREG0          SAVE THE NEW SIZE
         SR    R0,R1               GET THE SIZE OF THE AREA TO BE FREED
         A     R1,DIVREG1          GET ITS ADDRESS
         FREEMAIN RU,A=(1),LV=(0)  FREE STORAGE FOR RECURSIVE 'REVIEW'
DIVSTORX LM    R14,R12,12(R13)     RESTORE REGISTERS
         SLR   R15,R15             RECORD STORAGE WAS SUCCESSFUL
         BSM   0,R14               RETURN FOR ENTRY CODE 4

DIVRETRV DS    0H                  DIV ENTRY CODE 8
         ICM   R1,B'1111',DIVREG1  POINT TO THE DIV HOLD AREA
         BNP   DIVISBAD            NO DIV STORAGE SO RETURN
         CLI   1(R1),0             ANY DATA STORED BY DIV?
         BNE   DIVISBAD            NO, NO DATA TO FETCH (INVALID LEN)
         CLC   COUNT,DIVHICNT      STORED THIS RECORD BEFORE?
         BH    DIVISBAD            NO, THIS RECORD WAS NOT STORED
         TM    STATUS10,UZ80       UNZIPPING FIXED-LENGTH RECORDS?
         BO    DIVRTIDX            YES, USING INDEX STORAGE METHOD
         TM    STATUS8,ZIPD+ZIPF   PROCESSING A ZIP FILE?
         BNZ   DIVRTZIP            YES, MAKING OPTIMAL USE OF STORAGE
         TM    TSTRECFM,X'C0'      FIXED OR VARIABLE LENGTH RECORDS?
         BM    DIVRTIDX            YES, USING INDEX STORAGE METHOD
DIVRTZIP L     R5,DIVREG1          NO, USING CHAIN STORAGE METHOD
         ICM   R7,15,DIVCPADR      PREVIOUS ENTRY'S DETAILS AVAILABLE?
         BNP   DIVRTSLO            NO, CHAIN THROUGH FROM START
         L     R0,COUNT            YES, GET NEW RELATIVE RECORD NUMBER
         S     R0,DIVCPCNT         GET NUMBER OF LINKS TO CHAIN
         BNM   DIVRTLNK            NOT GOING BACKWARDS SO USE CHKPT
DIVRTSLO LR    R7,R5               POINT TO DIV STORAGE AREA
         L     R0,COUNT            GET RELATIVE RECORD INDEX (1ST IS 0)
DIVRTLNK A     R5,DIVREG0          POINT PAST END OF DIV AREA
         LTR   R0,R0               ANY CHAINING TO DO?
         BZ    DIVRTREC            NO, GO FETCH RECORD
DIVRTCHN A     R7,0(,R7)           ADD THE LENGTH OF CHAINED RECORD -
*                                  ASSUME NO IMBEDDED E-O-F (LENGTH=-1)
         LA    R7,DIVOVHD+7(,R7)   ALLOW FOR RECORD OVERHEAD
         SRL   R7,3                ROUND TO DOUBLEWORD BOUNDARY
         SLL   R7,3
         CR    R7,R5               STILL IN DIV AREA?
         BNL   DIVISBAD            NO, THIS RECORD WAS NOT STORED
         BCT   R0,DIVRTCHN         YES, CONTINUE TO FOLLOW CHAIN
*        THE NEXT TWO LINES SHOULD NEVER CAUSE A BRANCH BECAUSE
*        DIVHICNT ONLY GETS INCREMENTED WHEN A RECORD IS STORED:
         OC    0(8,R7),0(R7) __   ANY DATA HERE? (R7<R5 SO >= 1 DBLWD)
         BZ    DIVISBAD _______   NO, THIS IS FIRST RECORD NOT TO FIT
         B     DIVRTREC            YES, GO FETCH THE RECORD
DIVRTIDX L     R7,COUNT            GET RECORD INDEX (FIRST IS 0, ETC.)
         LA    R8,80
         TM    STATUS10,UZ80       UNZIPPING CARD IMAGES?
         BO    *+8                 YES
         L     R8,SAVLRECL         GET ROUNDED MAX LRECL
         LA    R8,DIVOVHD(,R8)     ADD OVERHEAD
         MR    R6,R8               GET OFFSET INTO STORAGE AREA
         AR    R7,R8               ADD SIZE OF RECORD
         C     R7,DIVREG0          DID IT FIT INTO STORAGE AREA?
         BH    DIVISBAD            NO, SORRY, HAVEN'T GOT THE RECORD,
*                                  BUT AT LEAST CHECKPOINTS ARE CLOSE
*                                  TOGETHER NEAR THE END OF THE FILE
         SR    R7,R8               YES, POINT BACK TO REQUIRED RECORD
         A     R7,DIVREG1          POINT TO SPOT IN DIV AREA
DIVRTREC MVC   DIVCPCNT,COUNT      SAVE THE RELATIVE RECORD NUMBER
         ST    R7,DIVCPADR         SAVE CORRESPONDING ENTRY ADDRESS
         SLR   R2,R2               PREPARE FOR END-OF-FILE
         ICM   R1,B'1111',0(R7)    GET RECORD LENGTH
         BM    DIVRETVD            BRANCH IF END-OF-FILE
         ICM   R0,B'1111',$UCBAD   TALKING DIRECTLY TO PHYSICAL DEVICE?
         BNP   DIVPNTLS            NO, A POINT WOULD BE POINTLESS
         TM    STATUS8,ZIPD+ZIPF   PROCESSING A ZIP FILE?
         BNZ   DIVPNTLS            YES, SO NOT TALKING TO I/O DEVICE

*               (A NULL UCB ADDRESS IMPLIES A SUBSYSTEM DATA SET SO
*         NOTE/POINT PROCESSING IS NOT ALLOWED.  "DIV" IS ONLY ASKED TO
*         RETRIEVE A RECORD IF IT HAS BEEN PREVIOUSLY READ, BUT "DIV"
*         MAY NOT HAVE HAD ROOM TO STORE IT WHEN IT WAS FIRST READ.
*         HENCE, IT MAY HAVE TO BE RE-READ FROM THE STORAGE MEDIUM, IN
*         WHICH CASE THE PHYSICAL ADDRESS IN THE DCB WILL HAVE TO BE
*         RESYNCHRONIZED WITH LOGICAL PROCESSING SINCE OTHER LOGICAL
*         RECORDS MAY HAVE BEEN RETRIEVED WITHOUT PHYSICAL I/O (THANKS
*         TO "DIV").  FOR SUBSYSTEM DATA SETS, ALL RECORDS MUST BE
*         STORED BY "DIV" BECAUSE REVIEW CAN'T GO BACK TO RE-READ THEM
*         (IE. ACCESS IS QSAM-LIKE IN ITS SEQUENTIAL NATURE).  SO IF
*         THERE IS NO ROOM TO STORE A RECORD FROM A SUBSYSTEM DATA SET
*         THEN A PREMATURE END-OF-FILE SHOULD BE FORCED (AS FAR AS
*         REVIEWABLE DATA IS CONCERNED).  IF A REAL END-OF-FILE IS
*         ENCOUNTERED THEN ANY UNUSED "DIV" SPACE CAN BE FREED WITH
*         IMPUNITY BECAUSE THE USER CANNOT 'NEWTOP' TO ANY LARGER
*         SOURCE OF DATA AS IS POSSIBLE WITH WITH NORMAL DISK FILES.)
*

         MVC   TTR,4(R7)           LOAD BLOCK LOCATION FOR MAINLINE
         TM    STATUS8,DIVFULL     STORING ALL THE DATA SO FAR?
         BZ    DIVPNTLS            YES, AVOID UNNEEDED FILE INTERACTION
         CLC   DIVHITTR,4(R7)      RECORD FROM LAST BLOCK READ?
         BE    DIVFIXIT            YES, GO REPAIR DIALOG WITH DISK
DIVPNTLS L     R2,BLOCKPTR         POINT TO PHYSICAL BLOCK BUFFER
         ICM   R5,B'1111',SAVSPANL SPANNED RECORD FILE?
         BZ    DIVRETRC            NO, THIS IS THE RIGHT WORK AREA
         L     R2,SPANPTR          YES, POINT TO A LARGER AREA
DIVRETRC LR    R4,R2               COPY TARGET RECORD ADDRESS
         LTR   R3,R1               COPY RECORD LENGTH
         BZ    DIVRETVD            BRANCH IF NULL RECORD
DIVRETLP CH    R3,=H'256'          LENGTH LEFT LESS THAN 256
         BL    DIVRETIT            YES, MOVE THE REST OF IT
         MVC   0(256,R4),8(R7)     NO, MOVE 256 BYTES OF IT
         LA    R7,256(,R7)         ADJUST SOURCE POINTER
         LA    R4,256(,R4)         ADJUST TARGET POINTER
         SH    R3,=H'256'          ADJUST REMAINING-DATA COUNT
         BZ    DIVRETVD            (MULTIPLE OF 256)
         B     DIVRETLP            PROCESS NEXT BIT
DIVRETTL MVC   0(0,R4),8(R7)       <<< EXECUTED >>>
DIVRETIT BCTR  R3,0                LESS 1 FOR EXECUTE
         EX    R3,DIVRETTL         RETRIEVE THE TAIL OF THE RECORD
DIVRETVD L     R14,12(,R13)        RESTORE RETURN REGISTER
         LM    R3,R12,32(R13)      RESTORE OTHER REGISTERS
         SLR   R15,R15             RECORD RETRIEVAL WAS SUCCESSFUL
         BSM   0,R14               RETURN FOR ENTRY CODE 8
DIVFIXIT LA    R1,DIVPOINT         RE-SYNC REVIEW WITH DISK MEDIA
         ST    R13,CALLSAVE+4      CHAIN SAVEAREAS FOR POINT
         LA    R13,CALLSAVE
         ST    R13,@DATA+8
         BSM   0,R1                REVERT TO 24-BIT AMODE FOR I/O
DIVPOINT POINT (R4),DIVHITTR       DIV FETCH DOESN'T ADJUST DCB BLK PTR
         L     R13,4(,R13)
         XC    8(4,R13),8(R13)
         B     DIVISBAD            NOW REVIEW CAN RE-READ LAST BLK READ

DIVPGREL DS    0H                  DIV ENTRY CODE 12
         XC    DIVCHKPT,DIVCHKPT   RESET DIV CHECKPOINT
         ICM   R1,B'1111',DIVREG1  POINT TO THE DIV HOLD AREA
         BNP   DIVISBAD            NO DIV STORAGE SO RETURN
         CLI   1(R1),X'DA'         DATA AREA TRIMMING FLAGGED?
         BE    DIVPGRLD            YES, ONLY FIRST PAGE EXISTS ANYWAY
         ICM   R0,B'1111',0(R1)    NO, IS LENGTH OF FIRST RECORD ZERO?
         BZ    DIVPGRLD            YES, STORAGE IS IN RELEASED STATUS
         TM    OSBITS,X'93'        XA OR LATER VERSION OF MVS?
         BO    DIVPGSER            YES, USE PGSER MACRO
         L     R0,DIVREG0          GET STORAGE SIZE
         FREEMAIN RU,A=(1),LV=(0)  FREE THE DIV STORAGE AREA
         L     R0,DIVREG0          GET STORAGE SIZE
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE
         LA    R1,0(,R1)           ENSURE SIGN BIT IS OFF
         ST    R1,DIVREG1          SHOULD BE THE SAME ADDRESS
         B     DIVPGRLD            PAGES NOW EFFECTIVELY RELEASED
DIVPGSER L     R15,DIVREG0         GET STORAGE SIZE
         AR    R15,R1              POINT PAST DIV AREA
         BCTR  R15,0               POINT TO LAST BYTE IN DIV AREA
*                                  RELEASE STORAGE RESOURCES WITHOUT
         PGSER R,RELEASE,A=(1),EA=(15)     FREEMAIN/GETMAIN OVERHEAD
         L     R1,DIVREG1          POINT TO THE DIV STORAGE AREA
DIVPGRLD MVI   1(R1),X'D0'         FLAG DATA AREA HAS NO DATA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SLR   R15,R15             STORAGE RELEASE WAS SUCCESSFUL
         ST    R15,DIVHITTR        ZERO HIGHEST TTR ENCOUNTERED
         ST    R15,DIVHICNT        ZERO HIGHEST RECORD NUMBER
         NI    STATUS8,255-DIVFULL RESTART STORAGE WITH A CLEAN SLATE
         BSM   0,R14               RETURN FOR ENTRY CODE 12

DIVFREMN DS    0H                  DIV ENTRY CODE 16
         XC    DIVCHKPT,DIVCHKPT   RESET DIV CHECKPOINT
         LM    R0,R1,DIVREGS       LOAD REGISTERS FOR FREEMAIN
         LTR   R0,R0               ANYTHING TO FREE?
         BZ    DIVFREED            NO
         FREEMAIN RU,A=(1),LV=(0)  FREE THE DIV STORAGE AREA
DIVFREED XC    DIVREGS,DIVREGS     FLAG FREEING OF DIV STORAGE
         OI    STATUS8,DIVFULL     DIV WILL NO LONGER SAVE FILE I/O
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SLR   R15,R15             STORAGE FREEING WAS SUCCESSFUL
         ST    R15,DIVHITTR        ZERO HIGHEST TTR ENCOUNTERED
         ST    R15,DIVHICNT        ZERO HIGHEST RECORD NUMBER
         BSM   0,R14               RETURN FOR ENTRY CODE 16
         TITLE '  R E V D I V   -   S T A T I C   A R E A  '
DIVOVHD  EQU   8                   STORAGE OVERHEAD PER RECORD
DIVTRKTB DS    0F                  TRACK SIZE TABLE
         DC    F'26793'            X'2085' F6421
         DC    F'65535'   ????     X'2001'  2311
         DC    F'65535'   ????     X'2002'  2301
         DC    F'65535'   ????     X'2003'  2303
         DC    F'46456'            X'2004'  9345   (WAS 2302)
         DC    F'65535'   ????     X'2005'  2321
         DC    F'14136'            X'2006'  2305-1
         DC    F'14660'            X'2007'  2305-2
         DC    F'7294'             X'2008'  2314   (2319)
         DC    F'13030'            X'2009'  3330   (F478)
         DC    F'8368'             X'200A'  3340
         DC    F'19069'            X'200B'  3350   (F493)
         DC    F'35616'            X'200C'  3375
         DC    F'13030'            X'200D'  3330-1 (F479)
         DC    F'47476'            X'200E'  3380  (F6425,F6427)
         DC    F'56664'            X'200F'  3390
         LTORG

         DS    0D                  END OF CSECT

         DROP  R10,R11             REVDIV
         TITLE ' REVIEW POINT-AND-SHOOT FRONT-END FOR TSO '
**********************************************************************
*                                                          *         *
*         PROCESS POINT-AND-SHOOT REQUEST                  *  GP@FT  *
*                                                          *  11/96  *
**********************************************************************
*                                                                    *
* REGISTERS ON ENTRY  R9 -> @DATA                                    *
*                     R13-> SAVE AREA                                *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVPNS                                   *
*                                                                    *
* DURING PROCESSING   R13-> @PNSDYN  (LOCAL GETMAINED AREA)          *
*                                                                    *
* RETURN CODES        R15 = 0  ZERO RESULT - NOTHING DONE            *
*                     R15 = 2  POSITIVE RESULT - $DSNAME LOADED      *
*                                                                    *
**********************************************************************

REVPNS   CSECT
         ENTRY DSNCHARS
         B     @PNSTART-*(,R15)
         DC    AL1(7),CL7'REVPNS '
@PNSSIZE DC    AL4(@PNSDYNL)
@PNSTART STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             BASE REGISTERS
         LA    R15,1
         LA    R11,4095(R15,R12)
         LA    R10,4095(R15,R11)
         USING REVPNS,R12,R11,R10
         L     R0,@PNSSIZE         GET DYNAMIC AREA SIZE
         GETMAIN RU,LV=(0),LOC=BELOW,SP=1
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R13,R1              POINT TO DYNAMIC AREA
         USING @PNSDYN,R13
         GTSIZE
         LTR   R15,R15             SUCCESS?
         BNZ   PNSNOGO             NO, SCREEN ERROR
         STM   R0,R1,SCRNLNES      SAVE CURRENT SCREEN DIMENSIONS
         CH    R0,=H'24'           AT LEAST 24 LINES?
         BL    PNSNOGO             NO, NOT BIG ENOUGH
         CH    R1,=H'80'           AT LEAST 80 COLUMNS?
         BL    PNSNOGO             NO, NOT BIG ENOUGH
         TM    MODE,RECURS         IS THIS A RECURSIVE 'REVIEW'?
         BO    PNSRCRS             YES, COPY DATA INTERNALLY W/O RDBUF
         CLI   OSBITS,X'9B'        MVS/ESA OR OS/390?
         BNE   PNSFSMD             NO, SKIP CONTROL BLOCK CHASE
         L     R15,540             POINT TO THE CURRENT TCB
         L     R15,132(,R15)       POINT TO ORIGINATING TCB
         ICM   R1,15,112(R15)      GET TCBFSA
         BZ    PNSFSMD             NOT ISPF IF ZERO
         ICM   R1,15,24(R1)        GET SAVED R1
         BNP   PNSFSMD             NO GOOD
         CLI   0(R1),0             HIGH ORDER BYTE NULL?
         BNE   PNSFSMD             NO, PROCESS NEXT TCB
         L     R1,0(,R1)           GET POSSIBLE TLD ADDRESS
         CLC   =C'TLD',0(R1)       TLD?
         BNE   PNSFSMD             NO, NOT ISPF
         LH    R15,166(,R1)        LOAD CURSOR OFFSET
         LTR   R15,R15             NEGATIVE CURSOR OFFSET?
         BM    PNSFSMD             YES, INVALID
         ICM   R3,15,96(R1)        POINT TO ISPF SCREEN BUFFER
         BNP   PNSFSMD             REJECT RIDICULOUS POINTER
         LR    R0,R3               COPY SCREEN BUFFER POINTER
         AR    R3,R15              POINT TO CURSOR LOCATION
         B     ATDSN               LOOK FOR DATA SET NAME

PNSRCRS  OI    MODE,FSMODE         RECURSIVE MUST BE IN FS MODE ALREADY
         L     R1,4(,R9)           POINT TO REVIEW'S CALLER'S SAVE AREA
         L     R1,56(,R1)          POINT TO PREVIOUS @DATA (R9 VALUE)
         DROP  R9                  @DATA
         USING @DATA,R1
         TM    MODE,MODEX          IN HEXADECIMAL DISPLAY MODE?
         BO    PNSTPG              YES, THIS SHOULD BE INTERESTING
         TM    MODE,ASCIISW        IN ASCII MODE?
         BO    PNSTPG              YES, CHARACTERS EXIST ONLY ON SCREEN
         TM    MODE,FMTSW          FORMATTING ON?
         BNZ   PNSTPG              YES, BETTER ACCESS SCREEN DATA
         LH    R15,CSRLOCN         GET CURSOR LOCATION
         LTR   R15,R15             NON-ZERO VALUE?
         BZ    PNSTPG              NO, BETTER ACCESS SCREEN DATA
         SLR   R14,R14
         D     R14,SCRNCOLS        GET CURSOR COORDINATES
         SH    R15,=H'4'           GET SCREEN DATA RECORD NUMBER
         BM    PNSTPG              SHOULD NOT HAPPEN
         L     R7,HOLDTOP          POINT TO FIRST RECORD
         BZ    PNSRVREC            HAVE REVIEW INTERNAL RECORD
PNSRCSLP C     R7,HOLDEND          LAST INTERNAL RECORD?
         BE    PNSTPG              YES, PROBABLY NOTHING THERE
         L     R7,DIRNXT(,R7)      NO, POINT TO NEXT RECORD
         BCT   R15,PNSRCSLP
PNSRVREC TM    DIRLEN(R7),X'80'    IS RECORD END-OF-FILE?
         BO    PNSTPG              YES, PROBABLY NOTHING THERE
         LH    R3,OFFSET           GET SCROLL RIGHT AMOUNT
         A     R3,DIRREC(,R7)      POINT TO DATA ON SCREEN
         LR    R0,R3               DO NOT USE DATA TO LEFT OF SCREEN
         AR    R3,R14              POINT TO CURSOR DATA
         B     ATDSN               LOOK FOR DATA SET NAME
         DROP  R1                  @DATA
         USING @DATA,R9

PNSFSMD  STFSMODE ON,INITIAL=YES,NOEDIT=YES  TURN ON FULLSCREEN MODE
         OI    MODE,FSMODE         FLAG FOR FUTURE REFERENCE
PNSTPG   MVC   PNSTPGD(PNSTPGL),PNSTPGS
         TPG   MF=(E,PNSTPGD)      ISSUE READ BUFFER
         L     R0,@PNSSIZE         GET DYNAMIC AREA SIZE
         LA    R1,RDBUFBUF-@PNSDYN
         SR    R0,R1               GET INPUT BUFFER SIZE
         LA    R1,RDBUFBUF         POINT TO INPUT BUFFER
         ICM   R1,8,=X'81'         LOAD FLAGS FOR ASIS,WAIT TGET
         TGET  (1),(0),R           READ TERMINAL INPUT
         LR    R2,R1               SAVE INPUT DATA LENGTH
         STFSMODE ON,NOEDIT=NO     TURN OFF NOEDIT INPUT MODE
         LTR   R1,R2               ANY DATA RETURNED?
         BNP   PNSNOGO             NO, SOMETHING WENT WRONG
         SLR   R2,R2
         ICM   R2,3,RDBUFBUF+1     GET CURSOR BUFFER ADDRESS
         TM    RDBUFBUF+1,X'40'    14-BIT ADDRESS?
         BZ    CSRLOCOK            YES
         SLL   R2,2                NO, IT IS A 12-BIT ADDRESS
         ICM   R2,2,RDBUFBUF+1
         SLL   R2,18               SHIFT OUT UNWANTED BITS
         SRL   R2,20               CONVERT TO 14-BIT ADDRESS FORMAT
CSRLOCOK LA    R3,RDBUFBUF+3       POINT TO START OF SCREEN CONTENTS
*-XLINE  LR    R15,R2              COPY CURSOR LOCATION
BUFRADOK LR    R0,R3               REMEMBER BUFFER IMAGE START
         LTR   R2,R2               WAS CURSOR AT LOCATION ZERO?
         BNP   ATDSN               YES
BUFRLOOP CLI   0(R3),SF            START FIELD?
         BE    SKIP1               YES
         CLI   0(R3),GE            GRAPHIC ESCAPE?
         BE    SKIP1               YES
         CLI   0(R3),SA            SET ATTRIBUTE?
         BE    SKIP3               YES
         CLI   0(R3),SFE           START FIELD EXTENDED?
         BE    SKIPMANY            YES
DONESKIP LA    R3,1(,R3)           POINT TO NEXT BUFFER LOCATION
         BCT   R2,BUFRLOOP
ATDSN    TRT   0(1,R3),DSNCHARS    VALID DSN CHARACTER?
         BNZ   PNSNOGO             NO, INVALID DATA SET NAME
         LR    R4,R3               COPY POINTER
*-XLINE  SLR   R14,R14             CLEAR FOR DIVIDE
*-XLINE  D     R14,SCRNCOLS        GET CURSOR COORDINATES
*-XLINE  LA    R6,1(,R14)          GET CURSOR COLUMN NUMBER
BACKLOOP CR    R4,R0               BACK AT START OF IMAGE DATA?
         BNH   STRTLOOP            YES, STOP GOING BACKWARDS
         BCTR  R4,0                NO, BACK UP A BYTE
         TRT   0(1,R4),DSNCHARS    VALID DSN CHARACTER?
         BZ    BACKLOOP            YES       (REMOVE FOR XLINE CHECK)
*-XLINE  BNZ   GETSTART            NO, DETERMINE THE START OF DSNAME
*-XLINE  BCT   R6,BACKLOOP         YES, DECREMENT COLUMN NUMBER
*-XLINE  LA    R6,1                DON'T CROSS LINES - START AT COL 1
GETSTART CLI   0(R4),SF            START FIELD?
         BNE   *+8                 NO
         LA    R4,1(,R4)           YES, SKIP ATTRIBUTE BYTE
         LA    R4,1(,R4)           POINT TO FIRST SUITABLE CHARACTER
STRTLOOP CR    R4,R3               IS FIRST CHARACTER PAST POINT?
         BH    PNSNOGO             YES, INVALID DATA SET NAME
         TRT   0(1,R4),DSNCHAR1    VALID FIRST DSN CHARACTER?
         BZ    GOTSTART            YES, FOUND DATA SET NAME START
         LA    R4,1(,R4)           NO, POINT TO NEXT CHARACTER
*-XLINE  LA    R6,1(,R6)           INCREMENT COLUMN NUMBER
*-XLINE  C     R6,SCRNCOLS         PAST THE LINE END?
*-XLINE  BNH   STRTLOOP            NO, KEEP LOOKING
*-XLINE  B     PNSNOGO             YES, INVALID DSNAME ON THIS LINE
         B     STRTLOOP            KEEP LOOKING
GOTSTART LA    R1,44(,R4)          POINT PAST DATA SET NAME
*-XLINE  BCTR  R6,0                CONVERT COLUMN TO OFFSET
*-XLINE  LA    R6,44(,R6)          GET LAST POSSIBLE COLUMN NUMBER
*-XLINE  S     R6,SCRNCOLS         PAST END OF THIS SCREEN LINE?
*-XLINE  BNP   *+6                 NO
*-XLINE  SR    R1,R6               YES, REDUCE MAXIMUM DSNAME LENGTH
         LR    R6,R1               POINT PAST DSNAME
         SR    R6,R4               GET DSNAME LENGTH
         BCTR  R6,0                DECREMENT FOR EXECUTE
         EX    R6,PNSTRT           VALIDATE DATA SET NAME
STOPLOOP BCTR  R1,0                POINT TO LAST BYTE OF DSNAME
         CLI   0(R1),C'.'          FOUND TRAILING PERIOD?
         BE    STOPLOOP            YES
         LA    R5,1(,R1)           POINT PAST END OF DSNAME
         SR    R5,R4               GET LENGTH OF DSNAME
         STH   R5,$DSNAME          SAVE IT
         MVI   $DSNAME+2,C' '      BLANK OUT DATA SET NAME FIELD
         MVC   $DSNAME+3(43),$DSNAME+2
         BCTR  R5,0                DECREMENT FOR EXECUTE
         EX    R5,SHOT$DSN         LOAD DATA SET NAME IN UPPER CASE
         LA    R15,2               INDICATE POSITIVE OUTCOME
         TM    MODE,RECURS         IS THIS A RECURSIVE 'REVIEW'?
         BNO   PNSEXIT             NO, TIDY UP AND RETURN
         L     R1,4(,R9)           POINT TO REVIEW'S CALLER'S SAVE AREA
         L     R1,56(,R1)          POINT TO PREVIOUS @DATA (R9 VALUE)
         CLI   DSORGTYP-@DATA(R1),X'DD'     CALLED FROM REVIEW OF VTOC?
         BNE   PNSEXIT             NO, TIDY UP AND RETURN
         MVC   $VOLSER,$VOLSER-@DATA(R1)    SUPPLY CORRECT VOLUME
         MVC   $UNIT,=CL8'SYSALLDA'         SUPPLY CORRECT UNIT
         B     PNSEXIT             TIDY UP AND RETURN
PNSTRT   TRT   0(0,R4),DSNCHARS    <<< EXECUTED >>>
SHOT$DSN OC    $DSNAME+2(0),0(R4)  <<< EXECUTED >>>
SKIP1    LA    R3,1(,R3)
         B     DONESKIP
SKIP3    LA    R3,3(,R3)
         B     DONESKIP
SKIPMANY SLR   R15,R15
         IC    R15,1(,R3)
         LA    R15,1(,R15)
         SLL   R15,1
         ALR   R3,R15
         B     DONESKIP
PNSNOGO  TM    MODE,RECURS         IS THIS A RECURSIVE 'REVIEW'?
         BZ    PNSLINE             NO, DO LINE MODE PROMPTING
         SLR   R15,R15             YES, DO NOT PROCEED WITH PROMPTING
         BCTR  R15,0               SET NEGATIVE RETURN CODE
         B     PNSEXIT
PNSLINE  TM    MODE,FSMODE         CURRENTLY IN FULLSCREEN MODE?
         BZ    PNSLINE0            NO
         STFSMODE OFF              YES, REVERT TO LINE MODE
         NI    MODE,255-FSMODE     FULL SCREEN MODE NOW OFF
PNSLINE0 SLR   R15,R15             ZERO RETURN CODE FOR NOTHING DONE
PNSEXIT  LR    R1,R13              POINT TO DYNAMIC AREA
         L     R13,4(,R13)         POINT TO CALLER'S SAVE AREA
         ST    R15,16(,R13)        SAVE RETURN CODE
         L     R0,@PNSSIZE         GET SIZE OF DYNAMIC AREA
         FREEMAIN RU,A=(1),LV=(0),SP=1    FREE DYNAMIC AREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER

         DROP  R12,R11,R10         REVPNS
         TITLE ' POINT-AND-SHOOT TRANSLATE TABLES AND CONSTANTS '

DSNCHARS DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'404142434445464748494A004C4D4E4F'
         DC    X'505152535455565758595A005C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A00007D7E7F'
         DC    X'800000000000000000008A8B8C8D8E8F'
         DC    X'900000000000000000009A9B9C9D9E9F'
         DC    X'A0A10000000000000000AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0000000000000000000CACBCCCDCECF'
         DC    X'D0000000000000000000DADBDCDDDEDF'
         DC    X'E0E10000000000000000EAEBECEDEEEF'
         DC    X'00000000000000000000FAFBFCFDFEFF'

DSNCHAR1 DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A005C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A00007D7E7F'
         DC    X'800000000000000000008A8B8C8D8E8F'
         DC    X'900000000000000000009A9B9C9D9E9F'
         DC    X'A0A10000000000000000AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0000000000000000000CACBCCCDCECF'
         DC    X'D0000000000000000000DADBDCDDDEDF'
         DC    X'E0E10000000000000000EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'

RDBUF    EQU   DSNCHAR1+X'F2',1    READ BUFFER COMMAND

PNSTPGS  TPG   RDBUF,1,NOEDIT,WAIT,MF=L
PNSTPGL  EQU   *-PNSTPGS

         LTORG

         DS    0D                  END OF CSECT
         TITLE ' POINT-AND-SHOOT WORK AREAS AND BUFFERS '
@PNSDYN  DSECT                     WORK AREAS AND BUFFERS
         DS    18F
PNSTPGD  TPG   RDBUF,1,NOEDIT,WAIT,MF=L
RDBUFBUF DS    0D                  BUFFER FOR READ BUFFER
         ORG   RDBUFBUF+20480
@PNSDYNL EQU *-@PNSDYN
         TITLE '  R E V I M A G E  '
**********************************************************************
*                                                          *         *
*         DISPLAY PICTURE IMAGE WITH RASTER GRAPHICS       *  GP@P6  *
*                                                          *  11/99  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE WILL PROCESS PICTURE DATA PREVIOUSLY        *
*        STORED DURING INITIALIZATION.  THE PICTURE WILL BE          *
*        RENDERED AND WRITTEN TO THE TERMINAL BY THIS ROUTINE.       *
*        THE "STANDARD" DATA STREAM CONSTRUCTED BY THE MAINLINE      *
*        WILL BE LARGELY DISCARDED, AND UPON RETURN THE MAINLINE     *
*        WILL ONLY WRITE THE TOP TWO LINES OF THE SCREEN.            *
*                                                                    *
*           BLACK-AND-WHITE PICTURES WILL USE ALL SINGLE-PLANE       *
*        AND TRIPLE-PLANE LOADABLE SYMBOL-SETS BEFORE REUSING A      *
*        SYMBOL-SET.  COLOUR PICTURES WILL ONLY USE TRIPLE-PLANE     *
*        LOADABLE SYMBOL-SETS.                                       *
*                                                                    *
*           WHENEVER VECTOR GRAPHICS RATHER THAN RASTER GRAPHICS     *
*        ARE TO BE EMPLOYED REVIMAGE TRANSFERS CONTROL TO REVGDDM    *
*        WHICH WILL RENDER THE PICTURE WITH GDDM CALLS.  BOTH        *
*        ROUTINES RUN WITH AMODE=31.                                 *
*                                                                    *
*           REVIMAGE AND REVGDDM CAN PROCESS THE REVIEW PICTURE      *
*        BYTE/PIXEL ARRAY AND RENDER THE PICTURE ON A 3270 GRAPHICS  *
*        TERMINAL.  THIS PROCESS IS INDEPENDENT OF FORMAT OF THE     *
*        PICTURE FILE BEING REVIEWED.  CURRENTLY, THIS ARRAY IS ONLY *
*        CREATED BY THE PCXCHECK ROUTINE WHICH CAN PROCESS ".PCX"    *
*        SINGLE-PLANE PICTURE FILES WITH 1-, 4- OR 8-BIT COLOUR.     *
*                                                                    *
**********************************************************************

**********************************************************************
*                                                                    *
* ON ENTRY      HSEACTIV -> PICTURE DATA INTERPRETED FROM FILE       *
*                     R9 -> @DATA                                    *
*                     R13-> REGISTER SAVE AREA                       *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVIMAGE                                 *
*                                                                    *
* DURING PROCESSING   R13-> @IMGWORK (LOCAL GETMAINED AREA)          *
*                                                                    *
* ON EXIT             R6 -> END OF DATA STREAM FOR REVIEW2 TO WRITE  *
*                                                                    *
**********************************************************************

REVIMAGE CSECT
REVIMAGE AMODE 31
         USING REVIMAGE,R15
         B     @IMAGE
         DC    AL1(9),CL9'REVIMAGE'
@IMAGE   TM    STATUS6,HELSW       INVOKED AS FULLSCREEN HELP?
         BO    IMAGENOP            YES, DO NOTHING HERE
         OC    HSEACTIV,HSEACTIV   IS ANY PICTURE DATA PRESENT?
         BZ    IMAGENOP            NO, DO NOTHING HERE
         TM    STATUS4,DBUGSW+HELPSW      DEBUG OR PFK DISPLAY?
         BNZ   IMAGENOP            YES, DO NOT OVERLAY IT
         TM    VECFLAGS,VCTR       VECTOR GRAPHICS AVAILABLE?
         BNO   IMGDOSYM            NO, HAVE TO LOAD SYMBOLS
         TM    PICFLAGS,PICOVR     HAS LPS OVERFLOW OCCURRED?
         BO    IMGDOVEC            YES, USE VECTOR FROM NOW ON
         CLI   SYMSTGF,0           ANY LOADABLE SYMBOL SETS?
         BE    IMGDOVEC            NO, HAVE TO USE VECTOR GRAPHICS
         CLC   MATRIXSZ,=X'0910'   NINE BY SIXTEEN CHARACTER MATRIX?
         BNE   IMGDOVEC            NO, USE VECTOR GRAPHICS
         CLC   Y_PELS,=F'320'      MORE THAN 20 LINES' WORTH?
         BH    IMGDOVEC            YES, USE VECTOR GRAPHICS
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BE    IMGDOSYM            YES, USE LOADABLE SYMBOLS
         CLI   SYMSTG3,0           CAN COLOURED SYMBOLS BE LOADED?
         BNE   IMGDOSYM            YES, USE LOADABLE SYMBOLS
IMGDOVEC TM    PICFLAGS,PICOK      PICTURE NEED REDRAWING?
         BO    IMAGENOP            NO, SAVE A LOT OF WORK FOR NOTHING
         L     R15,=V(REVGDDM)     YES, GET GDDM CALL ROUTINE ADDRESS
         BR    R15                 RENDER THE PICTURE WITH GDDM
IMGDOSYM L     R6,SCREENF          DON'T OVERLAY PICTURE WITH FILE DATA
         TM    PICFLAGS,PICOK      PICTURE NEED REDRAWING?
         BO    IMAGENOP            NO, SAVE A LOT OF WORK FOR NOTHING
         DROP  R15                 REVIMAGE
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R11,R15             SET BASE
         USING REVIMAGE,R11
         L     R0,@IMGWKSZ
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @IMGWORK,R13
         MVC   SYMDCB,SYMDCBS      LOAD DCB TEMPLATE
         USING IHADCB,SYMDCB
         MVC   DCBDDNAM,PICSRCDD   LOAD DCB DDNAME
         MVC   IMGTPUT,IMGTPUTS    LOAD TPUT TEMPLATE
         NI    PICFLAGS,255-PICOVR NO SYMBOL SETS LOADED YET
         A     R1,IMGBFOFF         POINT TO FULLSCREEN TPUT BUFFER
         ST    R1,IMGBFADR         SAVE FOR LATER USE
         LA    R0,SYMSTG3          POINT TO FIRST COLOUR RWS ID
         ST    R0,IMGCLRPT         SAVE ADDRESS FOR LATER
         SLR   R5,R5
         ST    R5,IMGROWS          FLAG NO GRAPHICS LINES NEEDED (YET)
         ST    R5,IMGLFTPD
         MVI   0(R1),WCCNULL       USE NO-ACTION WCC
         MVI   1(R1),SBA           SET BUFFER ADDRESS
         L     R0,SCRNCOLS         FIRST GRAPHICS WRITE SO POSITION
         SLL   R0,1                      TO START OF THIRD LINE
         STCM  R0,3,2(R1)          HOPE 14-BIT ADDRESSING IS OKAY
         LA    R8,4(,R1)           ADJUST POINTER
         MVC   0(3,R8),=X'2842F5'  TURQUOISE FOR DETAILS TEXT
         MVI   3(R8),C' '          BLANK OUT TEXT AREA
         MVC   4(48,R8),3(R8)
         MVC   5(1,R8),PICOLRES+3  SHOW COLOUR BIT DEPTH
         OI    5(R8),X'F0'         MAKE NUMERIC CHARACTER
         MVC   6(10,R8),=CL10'-BIT COLOR'
         L     R0,X_PELS
         LA    R15,18(,R8)         POINT TO DISPLAY AREA
         BAL   R14,IMGEDNUM        FORMAT NUMBER
         MVC   7(9,R15),=CL9'PELS WIDE'
         L     R0,Y_PELS
         LA    R15,36(,R8)         POINT TO DISPLAY AREA
         BAL   R14,IMGEDNUM        FORMAT NUMBER
         MVC   7(9,R15),=CL9'PELS DEEP'
         MVC   52(4,R8),=X'3C000040'
         LH    R0,X_BYTES          GET SYMBOLS PER LINE
         AH    R0,X_OFFSET         ADD LEADING BLANKS
         SH    R0,OFFSET           SUBTRACT COLUMNS LEFT OF SCREEN
         BP    IMGISPIC            SHOW SOME PICTURE
         MVC   0(56,R6),0(R8)      COPY TEXT TO MAINLINE SCREEN BUFFER
         LA    R6,56(,R6)          ADJUST MAINLINE BUFFER POINTER
         L     R15,4(,R13)         POINT TO CALLER'S SAVE AREA
         ST    R6,44(,R15)         UPDATE THE R6 SLOT FOR RESTORE
         B     IMAGEXIT            LET MAINLINE WRITE WHOLE SCREEN

IMGISPIC CH    R0,X_BYTES          MORE THAN PICTURE WIDTH?
         BNH   *+8                 NO
         LH    R0,X_BYTES          YES, REDUCE TO PICTURE WIDTH
         C     R0,SCRNCOLS         PICTURE WIDER THAN SCREEN?
         BNH   *+8                 NO
         L     R0,SCRNCOLS         YES, CAN ONLY SHOW SCREEN WIDTH
         ST    R0,IMGCOLS          REMEMBER COLUMNS-TO-LOAD COUNT
         LA    R0,1
         AH    R0,PAGESIZE         GET MAXIMUM LINES AVAILABLE
         CH    R0,Y_BYTES          IS PICTURE SHORTER?
         BNH   *+8                 YES
         LH    R0,Y_BYTES          NO, REDUCE LINE COUNT TO HEIGHT
         ST    R0,IMGROWS          REMEMBER LINES-TO-LOAD COUNT
         LH    R0,OFFSET           GET SCROLL RIGHT AMOUNT
         SH    R0,X_OFFSET         SUBTRACT LEADING BLANKS PADDING
         BNM   IMGHOUSE            FIRST COLUMN WILL HAVE PICTURE
         LPR   R0,R0               GET LEADING BLANK COUNT
         ST    R0,IMGLFTPD         SAVE LEFT PADDING COLUMN COUNT
         SLR   R0,R0               USE PICTURE'S LEFT-MOST DATA
IMGHOUSE MH    R0,=H'9'            CONVERT COLUMNS TO PIXELS
         A     R0,HSEACTIV         POINT TO FIRST ARRAY BYTE TO USE
         ST    R0,IMGHOME          SAVE FIRST PIXEL TO LOAD POINTER
         LA    R1,3                COUNT HEADING, INPUT, STATS LINES
         AH    R1,Y_OFFSET         ADD PADDING LINE COUNT
         M     R0,SCRNCOLS
         A     R1,IMGLFTPD         GO STRAIGHT TO FIRST PICTURE BYTE
         STCM  R1,3,53(R8)
         LA    R8,56(,R8)          ADJUST POINTER
         ST    R1,IMGLNLOC         SAVE LINE SCREEN LOCATION
         MVI   LPSWSF,X'F3'        SET WRITE STRUCTURED FIELD COMMAND
         MVI   LPSID,X'06'         SET LPS STRUCTURED FIELD TYPE
         MVI   LPSFLAGS,X'41'      BASIC(0), CLEAR(1), FORMAT TYPE 1
         MVI   LPSCHAR,X'41'       FIRST CODE POINT
         MVC   LPSRWS,SYMSTGF      ASSUME MONOCHROME FOR THE MOMENT
         LA    R0,LPSBASIC         POINT TO SYMBOL DATA START
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   IMGHDROK            YES, READY TO LOAD DATA
         MVI   LPSFLAGS,X'C1'      NO, NEED EXTENSION FOR COLOUR
         MVC   LPSRWS,SYMSTG3      LOAD TRIPLE-PLANE STORAGE NUMBER
         MVI   LPSLENE,6           SIX BYTES IN EXTENDED HEADER
         MVI   LPSFLAGE,0          SET EXTENDED FLAGS TO DEFAULTS
         MVI   LPSLW,9             FORMAT-1 IS 9 CELLS WIDE
         MVI   LPSLH,16            FORMAT-1 IS 16 CELLS DEEP
         MVI   LPSSUBSN,0          NOT USING 2-BYTE CODED DATA
         MVI   LPSCOLOR,1          START DATA WITH BLUE PLANE
         LA    R0,LPSDATAE         POINT TO SYMBOL DATA START
IMGHDROK ST    R0,IMGSYMAD         SAVE IT FOR DATA LOADING
         MVC   0(2,R8),=X'2842'    SET COLOUR FOR PICTURE
         MVC   2(1,R8),PICCOLOR    INITIALLY NEUTRAL/WHITE
         LA    R8,3(,R8)           ADJUST POINTER
         L     R2,IMGHOME          POINT TO FIRST PIXEL TO LOAD
         L     R5,IMGROWS          GET PICTURE SCREEN LINES
         L     R0,HSEACTIV         POINT TO PICTURE DATA
         A     R0,X_PELS           POINT PAST FIRST LINE'S PIXELS
         ST    R0,IMGEOLIN         SAVE END OF PIXEL LINE POINTER
         SLR   R3,R3               START WITH NEW LINE
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   IMGSETLP            NO

         MVI   OPEND,X'80'         YES, OPEN OUTFILE FILE
         OPEN  (SYMDCB,(OUTPUT)),MF=(E,OPEND)
         MVI   SYMSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   SYMSTMT+1(79),SYMSTMT
         MVC   SYMSTMT(30),=C'* REVIEW &REL  SYMBOL DATA FOR'
         LH    R1,$DSNAME
         BCTR  R1,0
         EX    R1,LDSYMDSN
         CLI   $MEMBER,C' '        MEMBER NAME TO BE DISPLAYED?
         BE    SYMDSNOK            NO
         LA    R1,SYMSTMT+32(R1)
         MVI   0(R1),C'('
         MVC   1(8,R1),$MEMBER
SYMMEMLP CLI   8(R1),C' '          TRAILING BLANK?
         BNE   SYMMEMOK            NO
         BCT   R1,SYMMEMLP
SYMMEMOK MVI   9(R1),C')'
SYMDSNOK PUT   SYMDCB,SYMSTMT      WRITE HEADING
         B     IMGSETLP
LDSYMDSN MVC   SYMSTMT+31(0),$DSNAME+2   <<< EXECUTED >>>

IMGSTGLP EQU   *                   START LOADING TERMINAL'S STORAGE
         L     R8,IMGBFADR         POINT TO SCREEN IMAGE BUFFER
         MVI   0(R8),WCCNULL       USE NO-ACTION WCC
         MVC   1(2,R8),=X'2842'    SET COLOUR FOR PICTURE
         MVC   3(1,R8),PICCOLOR    INITIALLY NEUTRAL/WHITE
         LA    R8,4(,R8)           ADJUST POINTER
IMGSETLP EQU   *                   START LOADING THE SYMBOL SET
         MVI   IMGPELBT,X'01'      TEST LOW BIT IN ARRAY BYTES
         MVI   LPSLCID,X'B0'       SET BROWSER LOCAL CHARACTER SET
         OC    LPSLCID,LPSRWS          ID TO BE USED IN 3270 ORDERS
         MVC   0(2,R8),=X'2843'    SPECIFY SYMBOL SET TO RENDER DATA
         MVC   2(1,R8),LPSLCID     LOADED SYMBOL SET
         LA    R8,3(,R8)           ADJUST POINTER
         L     R0,IMGLNORG         GET SCREEN LINE ARRAY ORIGIN
         L     R1,IMGEOLIN         GET END OF PEL LINE AT SYMSET START
*              R2 <==              PICTURE PEL ARRAY ELEMENT ADDRESS
*              R3 <==              COLUMNS STILL TO PROCESS FOR LINE
         LA    R4,CODEPTS          POINT TO FIRST CODE POINT TO USE
*              R5 <==              NUMBER OF ROWS STILL TO WRITE
         L     R6,IMGSYMAD         POINT TO SYMBOL BITS TARGET
         STM   R0,R5,IMGSSSSV      SAVE SYMBOL SET START STATUS
IMGPLNLP EQU   *                   START LOADING THE SYMBOL SET PLANE
         LM    R0,R5,IMGSSSSV      RESTORE SYMBOL SET START STATUS
         ST    R0,IMGLNORG         RESTORE SCREEN LINE ARRAY ORIGIN
         ST    R1,IMGEOLIN         RESTORE END OF PEL LINE
         IC    R15,IMGPELBT        GET BIT TO TEST IN ARRAY BYTES
         XC    IMGBLOKS,IMGBLOKS   CLEAR COLOUR BLOCK CODE POINTS
         MVI   IMGBLOKS,C' '       USE BLANK FOR SOLID BLACK BLOCK
         LTR   R3,R3               CHECK FOR ALREADY PROCESSING A LINE
         BP    IMGSYMLP            KEEP SYMBOL SET START LINE STATUS
IMGLINLP EQU   *                   START LOADING SYMBOLS FOR A LINE
         ST    R2,IMGLNORG         SAVE SCREEN LINE ARRAY ORIGIN
         L     R3,IMGCOLS          GET PICTURE SCREEN COLUMNS
IMGSYMLP EQU   *                   START LOADING A SYMBOL
         CLI   0(R4),X'FF'         END OF VALID CODE POINTS?
         BE    IMGLDSYM            YES, LOAD SYMBOLS GOT SO FAR
         LA    R0,16               GET BIT COUNT OF VERTICAL SLICES
         SLR   R14,R14             CLEAR PIXEL BIT ACCUMULATOR
         LR    R1,R2               POINT TO TOP LEFT MATRIX CORNER
         MVI   DOUBLE+4,0          RESET A BYTE
         MVI   DOUBLE+5,255        SET A BYTE
IMGVRTLP SLL   R14,1               MAKE ROOM FOR NEW BIT ACCUMULATION
         C     R1,PICENDAD         PAST END OF PICTURE DATA?
         BNL   IMGDONE1            YES, DO NOT ATTEMPT TO ACCESS BYTE
         OC    DOUBLE+4(1),0(R1)   DETECT NON-BLANK
         NC    DOUBLE+5(1),0(R1)   DETECT SOLID COLOUR BLOCK
         EX    R15,PELTEST         TEST BIT IN PICTURE ARRAY BYTE
         BNO   IMGDONE1            BIT WAS ZERO
         LA    R14,1(,R14)         BIT WAS ONE SO SET ACCUMULATOR BIT
IMGDONE1 A     R1,X_PELS           GO DOWN ONE PICTURE SCAN LINE
         BCT   R0,IMGVRTLP         LOOP THROUGH VERTICAL SLICE PELS
         STCM  R14,3,0(R6)         SAVE VERTICAL SLICE PELS
         LA    R6,2(,R6)           POINT PAST STORED SYMBOL DATA
         LA    R7,16               GET NUMBER OF HORIZONTAL SLICES
         LR    R1,R2               POINT TO TOP LEFT MATRIX CORNER
         L     R12,IMGEOLIN        POINT TO END OF SCAN LINE
IMGHRZLP C     R12,PICENDAD        GOING TO OVERSHOOT PICTURE ARRAY?
         BNH   *+8                 NO
         L     R12,PICENDAD        YES, REDUCE MAXIMUM ACCESS ADDRESS
         LA    R0,8                GET BIT COUNT OF A HORIZONTAL SLICE
         ST    R1,DOUBLE           SAVE THE ADDRESS OF SLICE_START-1
         SLR   R14,R14             CLEAR PIXEL BIT ACCUMULATOR
IMGPELLP LA    R1,1(,R1)           POINT TO NEXT SLOT IN SLICE
         SLL   R14,1               MAKE ROOM FOR NEW BIT ACCUMULATION
         CR    R1,R12              PAST END OF CURRENT SCAN LINE?
         BL    IMGHTEST            NO, GO PROCESS PICTURE ELEMENT
         MVI   DOUBLE+5,0          TREAT SPACE BEYOND PICTURE AS BLACK
         B     IMGDONE2            CANNOT USE COLOUR BLOCK
IMGHTEST OC    DOUBLE+4(1),0(R1)   DETECT NON-BLANK
         NC    DOUBLE+5(1),0(R1)   DETECT SOLID COLOUR BLOCK
         EX    R15,PELTEST         TEST BIT IN PICTURE ARRAY BYTE
         BNO   IMGDONE2            BIT WAS ZERO
         LA    R14,1(,R14)         BIT WAS ONE SO SET ACCUMULATOR BIT
IMGDONE2 BCT   R0,IMGPELLP         LOOP THROUGH HORIZONTAL SLICE PELS
         STC   R14,0(,R6)          SAVE PELS OF A HORIZONTAL SLICE
         LA    R6,1(,R6)           POINT PAST STORED SYMBOL DATA
         L     R1,DOUBLE           RESTORE SLICE_START-1 POINTER
         A     R1,X_PELS           ADJUST FOR NEXT HORIZONTAL SLICE
         A     R12,X_PELS          GET SCAN LINE END POINTER
         BCT   R7,IMGHRZLP         PROCESS NEXT HORIZONTAL SLICE
         LA    R2,9(,R2)           GET NEXT SYMBOL TOP LEFT ARRAY CNR
         LA    R0,18               GET BYTES PER LPS SYMBOL
         CLI   PICOLRES+3,1        COLOUR PICTURE?
         BH    IMGBLOCK            YES, CHECK FOR A BLOCK OF COLOUR
         MVI   0(R8),C' '          NO, PREPARE FOR BLANK
         CLI   DOUBLE+4,0          BLANK CHARACTER?
         BE    IMGDSCRD            YES, SAVE A CODE POINT
IMGNTBLK CLI   IMGPELBT,X'01'      SCANNING FIRST OR ONLY PLANE?
         BNE   *+10                NO, DO NOT EDIT TPUT BUFFER
         MVC   0(1,R8),0(R4)       YES, LOAD NEW CODE POINT
         LA    R4,1(,R4)           POINT TO NEXT AVAILABLE CODE POINT
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BH    IMGNXSYM            NO, END OF OPTIMISATION
         LR    R14,R6              YES. COPY LPS BUFFER CURSOR
         SR    R14,R0              POINT TO SYMBOL PIXELS JUST CODED
         L     R10,IMGSYMAD        POINT TO FIRST SYMBOL OF SYMBOL SET
         LA    R12,CODEPTS         NO, POINT TO FIRST CODE POINT
IMGOPTLP CR    R14,R10             UP TO CURRENT SYMBOL?
         BNH   IMGNXSYM            YES, NO REPEATS IN SYMBOL SET
         CLC   0(18,R10),0(R14)    REPEAT PIXEL PATTERN FOR SYMBOL?
         BE    IMGREUSE            YES, REUSE PREVIOUS SYMBOL
         LA    R10,18(,R10)        POINT TO NEXT SYMBOL'S PIXEL SLICES
         LA    R12,1(,R12)         POINT TO CORRESPONDING CODE POINT
         B     IMGOPTLP            CONTINUE SEARCH FOR REPEATS
IMGBLOCK CLC   DOUBLE+4(1),DOUBLE+5   BLOCK OF COLOUR?
         BNE   IMGNTBLK            NO, USE A NEW CODE POINT
         SLR   R12,R12             YES
         IC    R12,DOUBLE+4        GET COLOUR CODE
         LA    R12,IMGBLOKS(R12)   POINT TO BLOCK CODE POINT SLOT
         CLI   0(R12),0            FIRST BLOCK FOR THIS COLOUR?
         BE    IMGBLOK1            YES
         CLI   IMGPELBT,X'01'      SCANNING FIRST PLANE?
         BNE   IMGDSCRD            NO, DO NOT EDIT TPUT BUFFER
         B     IMGREBLK            YES, REUSE CODE POINT
IMGBLOK1 MVC   0(1,R12),0(R4)      REMEMBER CODE POINT FOR NEXT TIME
         B     IMGNTBLK            PROCEED WITH USING CODE POINT
IMGREUSE BCTR  R4,0                REUSE CURRENT CODE POINT NEXT
IMGREBLK MVC   0(1,R8),0(R12)      REUSE OLD CODE POINT
IMGDSCRD SR    R6,R0               DISCARD PIXEL SLICES JUST CODED
IMGNXSYM CLI   IMGPELBT,X'01'      SCANNING FIRST OR ONLY PLANE?
         BNE   *+8                 NO, DO NOT EDIT TPUT BUFFER
         LA    R8,1(,R8)           ADJUST TPUT BUFFER POINTER
         BCT   R3,IMGSYMLP         PROCESS NEXT SYMBOL
         L     R2,X_PELS           GET PICTURE PIXEL WIDTH
         SLL   R2,4                GET PIXELS PROCESSED IN SCREEN LINE
         A     R2,IMGLNORG         GET LINE ARRAY ORIGIN FOR NEXT LINE
         CLI   IMGPELBT,X'01'      SCANNING FIRST PLANE?
         BNE   IMGNXLIN            NO, DO NOT EDIT TPUT BUFFER
         L     R0,SCRNCOLS         GET SCREEN WIDTH
         LR    R1,R0
         A     R1,IMGLNLOC
         ST    R1,IMGLNLOC         UPDATE LINE LOCATION
         S     R0,IMGLFTPD
         S     R0,IMGCOLS          GET COLUMNS TILL END OF LINE
         BZ    IMGNXLIN            LINE IS FULL, SKIP RA ORDER
         CH    R5,=H'1'            LAST LINE?
         BH    *+6                 NO
         SLR   R1,R1               YES, CHANGE LOCATION TO ZERO
         MVI   0(R8),RA            REPEAT TO ADDRESS
         STCM  R1,3,1(R8)          BUFFER ADDRESS
         MVI   3(R8),C' '          BLANKS
         LA    R8,4(,R8)           ADJUST BUFFER POINTER
IMGNXLIN L     R0,X_PELS           GET LENGTH OF SCAN LINE
         SLL   R0,4                GET PIXELS PROCESSED IN SCREEN LINE
         A     R0,IMGEOLIN         UPDATE END OF SCAN LINE POINTER
         ST    R0,IMGEOLIN
         BCT   R5,IMGLINLP         PROCESS NEXT SCREEN LINE

IMGLDSYM CLI   IMGPELBT,X'04'      LAST COLOUR PLANE?
         BE    IMGDOWSF            YES, READY TO ISSUE WSF
         CLI   IMGPELBT,X'02'      NON-FIRST COLOUR PLANE?
         BE    IMGNXPLN            YES, READY TO PROCESS NEXT PLANE

         LA    R0,LPSLEN           POINT TO START OF STRUCTURED FIELD
         LR    R1,R6               GET CURRENT LPS BUFFER POINTER
         SR    R1,R0               GET LENGTH
         STH   R1,LPSLEN           SAVE IT
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   IMGDOWSF            YES, READY TO ISSUE WSF

IMGNXPLN IC    R0,IMGPELBT         GET PLANE BIT TEST FLAG
         SLL   R0,1                PROMOTE IT FOR THE NEXT PLANE
         STC   R0,IMGPELBT         SAVE IT FOR USE
         MVC   0(LPSHDRLN,R6),LPSLEN
         STC   R0,LPSHDRLN-1(,R6)  PREPARE HEADER FOR NEXT PLANE
         LA    R6,LPSHDRLN(,R6)
         B     IMGPLNLP            GO SCAN NEXT PLANE'S PIXELS

IMGDOWSF LA    R15,LPSWSF          POINT TO START OF DATA STREAM
         SR    R6,R15              GET LENGTH OF DATA STREAM
         TPUT  (R15),(R6),NOEDIT,WAIT,MF=(E,IMGTPUT)

         BAL   R14,WRITESYM        INVOKE WRITE SYMBOL ROUTINE

         LTR   R5,R5               END OF PICTURE?
         BNP   IMGWRITE            YES, WRITE IT TO THE TERMINAL
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BH    IMGCLRSS            NO, CHECK COLOUR SYMBOL SETS
         IC    R1,LPSRWS           YES, GET READ/WRITE STORAGE ID USED
         MVC   LPSRWS,SYMSTGF      PREPARE FOR REUSING FIRST SYMBOL SET
         CLM   R1,1,SYMSTGL        USING LAST ONE?
         BNL   IMGWRITE            YES, WRITE OUT PICTURE SO FAR
         LA    R1,1(,R1)           NO, INCREMENT STORAGE ID
         STC   R1,LPSRWS           USE NEXT ONE FOR NEXT SYMBOL BATCH
         B     IMGSETLP            LOAD NEXT SYMBOL SET
IMGCLRSS L     R1,IMGCLRPT         POINT TO COLOUR RWS ID
         LA    R1,1(,R1)           POINT TO NEXT ONE
         ST    R1,IMGCLRPT         SAVE UPDATED POINTER
         ICM   R1,1,0(R1)          GET NEW RWS ID
         STC   R1,LPSRWS           SET NEW ONE IN DATA STREAM
         BNZ   IMGSETLP            NON-ZERO RWS ID SO USE IT
         LA    R1,SYMSTG3          POINT TO FIRST COLOUR RWS ID
         ST    R1,IMGCLRPT         SAVE ADDRESS FOR LATER
         MVC   LPSRWS,0(R1)        SET COLOUR SYMBOL SET

IMGWRITE LTR   R5,R5               LAST WRITE FOR PICTURE?
         BP    IMGOVRFL            NO, SYMBOL SET OVERFLOW
         TM    PICFLAGS,PICOVR     HAS SYMBOL SET OVERFLOW OCCURRED?
         BO    IMGPUTIT            YES, WRITE OUT LAST PICTURE PIECE
         L     R1,IMGBFADR         POINT TO TPUT BUFFER
         LA    R0,4(,R1)           POINT PAST WCC, SBA
         LR    R1,R8               COPY BUFFER POINTER
         SR    R1,R0               GET DATA STREAM LENGTH
         L     R14,SCREENF         POINT TO MAINLINE'S BUFFER
         LR    R15,R1              COPY LENGTH
         MVCL  R14,R0              LET MAINLINE HANDLE WRITE/RESHOW
         L     R15,4(,R13)         POINT TO CALLER'S SAVE AREA
         ST    R14,44(,R15)        UPDATE THE R6 SLOT FOR RESTORE
         B     IMGFINIS            TIDY UP AND TERMINATE
IMGOVRFL OI    PICFLAGS,PICOVR     ALL USABLE SYMBOL SETS FULL
         MVI   0(R8),IC            UPDATE CURRENT LOCATION TO RESUME
         LA    R8,1(,R8)
IMGPUTIT LR    R0,R8               COPY BUFFER POINTER
         L     R1,IMGBFADR         POINT TO TPUT BUFFER
         SR    R0,R1               GET DATA STREAM LENGTH
         ICM   R1,8,=X'03'         LOAD FULLSCREEN FLAGS
         TPUT  (1),(0),R           WRITE PICTURE SYMBOL LINES

IMGFINIS BAL   R14,WRITEPTS        INVOKE WRITE CODE-POINT ROUTINE

         LTR   R5,R5               WHOLE PICTURE SCREEN NOW WRITTEN?
         BP    IMGSTGLP            NO, REUSE TERMINAL'S STORAGE

         OI    PICFLAGS,PICOK      FLAG PICTURE NOW RENDERED
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   IMAGEXIT            NO
         MVI   CLOSED,X'80'        YES, CLOSE THE OUTPUT DCB
         CLOSE (SYMDCB),MF=(E,CLOSED)
         MVI   PICSRCDD,0          CLEAR SOURCE CODE REQUEST FLAG
IMAGEXIT L     R0,@IMGWKSZ         GET DYNAMIC STORAGE SIZE
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @IMGWORK
         LM    R14,R12,12(R13)     RESTORE REGISTERS
IMAGENOP BSM   0,R14               RETURN WITH CALLER'S AMODE

PELTEST  TM    0(R1),X'00'         <<< EXECUTED >>>
LDPICSRC MVC   0(0,R1),0(R15)      <<< EXECUTED >>>

IMGEDNUM CVD   R0,DOUBLE
         MVC   0(6,R15),=XL6'402020202120'
         ED    0(6,R15),DOUBLE+5
         BR    R14

**********************************************************************
*                                                          *         *
*         WRITE SOURCE OF PROGRAMMABLE SYMBOLS             *  GP@P6  *
*                                                          *  12/99  *
**********************************************************************

WRITESYM DS    0H                  DFSMS REQUIRED BECAUSE AMODE=31
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNHR  R14                 NO, RETURN WITHOUT ACTION
         STM   R0,R15,IMGSYMSV     SAVE ALL REGISTERS
         L     R2,IMGSYMAD         INITIALIZE SYMBOL POINTER
         SLR   R3,R3
         IC    R3,0(,R4)           GET WHAT WOULD HAVE BEEN NEXT SYMBOL
         LA    R5,X'41'            GET FIRST SYMBOL CODE POINT VALUE
         SR    R3,R5               GET NUMBER OF SYMBOLS TO WRITE
         ST    R3,SYMWRTCT         SAVE THE COUNT
         MVI   DOUBLE,X'04'        FLAG LAST PLANE
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   SYMSTART            YES, READY TO WRITE DATA
         MVI   DOUBLE,X'01'        FLAG FIRST PLANE
SYMPLNLP MVI   SYMSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   SYMSTMT+1(79),SYMSTMT
         MVC   SYMSTMT(13),=CL13'*  BLUE PLANE'
         CLI   DOUBLE,X'01'        CORRECT?
         BE    SYMPLNHD            YES
         MVC   SYMSTMT+3(4),=CL4' RED'
         CLI   DOUBLE,X'02'        CORRECT?
         BE    SYMPLNHD            YES
         MVC   SYMSTMT+2(5),=CL5'GREEN'
SYMPLNHD PUT   SYMDCB,SYMSTMT      PRODUCE SOURCE CARD FOR SYMBOL
         L     R3,SYMWRTCT         LOAD THE SYMBOL COUNT
SYMSTART LA    R5,CODEPTS          GET FIRST SYMBOL CODE POINT VALUE
SYMLOOP  MVC   SYMSTMT,SYMCARD     LOAD DC STATEMENT TEMPLATE
         LA    R7,SYMSTMT          POINT TO OUTPUT CARD-IMAGE
         UNPK  SYMSTMT+17(13),0(7,R2)
         UNPK  SYMSTMT+29(13),6(7,R2)
         UNPK  SYMSTMT+41(13),12(7,R2)
         TR    SYMSTMT+17(36),IMGHEX-C'0'
         MVI   SYMSTMT+53,QUOTE    CLEAN UP DECLARE CONSTANT STATEMENT
         UNPK  SYMSTMT+59(3),0(2,R5)
         TR    SYMSTMT+59(2),IMGHEX-C'0'
         MVI   SYMSTMT+61,QUOTE    SHOW SYMBOL CODE POINT VALUE
         PUT   SYMDCB,SYMSTMT      PRODUCE SOURCE CARD FOR SYMBOL
         LA    R2,18(,R2)          POINT TO NEXT SYMBOL'S DATA
         LA    R5,1(,R5)           INCREMENT CODE POINT VALUE
         BCT   R3,SYMLOOP          PROCESS NEXT SYMBOL
         CLI   DOUBLE,X'02'        LAST PLANE?
         BH    WRTSYMX             YES, RETURN
         MVC   DOUBLE(1),LPSCOLOR-LPSLEN(R2)
         LA    R2,LPSHDRLN(,R2)    POINT TO NEXT PLANE'S SYMBOL DATA
         B     SYMPLNLP            WRITE NEXT PLANE'S SYMBOL DATA
WRTSYMX  LM    R0,R15,IMGSYMSV     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER

**********************************************************************
*                                                          *         *
*         WRITE SOURCE OF PICTURE CODE POINTS              *  GP@P6  *
*                                                          *  12/99  *
**********************************************************************

WRITEPTS DS    0H                  DFSMS REQUIRED BECAUSE AMODE=31
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNHR  R14                 NO, RETURN WITHOUT ACTION
         STM   R0,R15,IMGSYMSV     SAVE ALL REGISTERS
         L     R2,IMGBFADR         INITIALIZE BUFFER POINTER
PTSBFLP1 CR    R2,R8               REACHED END OF BUFFER?
         BNL   WRTPTSX             YES
         CLI   0(R2),SA            SEARCH FOR START OF PICTURE DATA
         LA    R2,1(,R2)
         BNE   PTSBFLP1
         CLI   0(R2),X'43'
         BNE   PTSBFLP1
         TM    1(R2),X'F0'         IGNORE DEFAULT AND APL CHARACTERS
         BNM   PTSBFLP1
         LA    R2,2(,R2)           POINT PAST ORDER
         LA    R3,SYMSTMT+17       POINT TO FIRST UNPACK TARGET
         LA    R4,SYMSTMT+55       POINT TO LAST UNPACK TARGET
         STM   R3,R4,DOUBLE        SAVE FOR FUTURE REFERENCE
PTSCRDLP MVI   SYMSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   SYMSTMT+1(79),SYMSTMT
         MVC   SYMSTMT(17),SYMCARD COPY TEMPLATE
         L     R3,DOUBLE           POINT TO FIRST UNPACK TARGET
PTSBFLP2 CR    R2,R8               REACHED END OF BUFFER?
         BNL   PTSEOBFR            YES
         CLI   0(R2),X'40'         CONTROL CHARACTER?
         BL    PTSCCHAR            YES
         UNPK  0(3,R3),0(2,R2)     NO, UNPACK A CODE POINT
         TR    0(2,R3),IMGHEX-C'0'
         LA    R3,2(,R3)           POINT PAST LATEST HEX
         MVI   0(R3),QUOTE         PREPARE FOR END OF RECORD'S DATA
         LA    R2,1(,R2)           POINT TO NEXT CODE POINT
         C     R3,DOUBLE+4         FILLED CARD IMAGE?
         BNH   PTSBFLP2            NO, LOOP THROUGH BUFFER
         PUT   SYMDCB,SYMSTMT      PRODUCE CODE POINT DATA RECORD
         B     PTSCRDLP            CONTINUE ON NEW CARD IMAGE
PTSCCHAR C     R3,DOUBLE           ANY DATA IN CARD IMAGE SO FAR?
         BNH   PTSNOCRD            NO, DO NOT WRITE IT
         PUT   SYMDCB,SYMSTMT      PRODUCE CODE POINT DATA RECORD
PTSNOCRD CLI   0(R2),SA
         BE    PTSJUMP3
         CLI   0(R2),SBA
         BE    PTSJUMP3
         CLI   0(R2),SF
         BE    PTSJUMP2
         CLI   0(R2),RA
         BNE   PTSJUMP1
         LA    R2,1(,R2)
PTSJUMP3 LA    R2,1(,R2)
PTSJUMP2 LA    R2,1(,R2)
PTSJUMP1 LA    R2,1(,R2)
         B     PTSCRDLP
PTSEOBFR C     R3,DOUBLE           ANY DATA IN CARD IMAGE SO FAR?
         BNH   WRTPTSX             NO, DO NOT WRITE IT
         PUT   SYMDCB,SYMSTMT      PRODUCE CODE POINT DATA RECORD
WRTPTSX  LM    R0,R15,IMGSYMSV     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER

         DROP  R13                 @IMGWORK
         DROP  R11                 REVIMAGE
         TITLE '  R E V I M A G E   -   S T A T I C   A R E A  '
@IMGWKSZ DC    A(@IMGWKLN)         LOCAL WORKING STORAGE SIZE
IMGBFOFF DC    A(IMGWRTBF-@IMGWORK)
IMGTPUTS TPUT  0,0,NOEDIT,WAIT,MF=L
IMGTPUTL EQU   *-IMGTPUTS
IMGHEX   DC    C'0123456789ABCDEF' HEXADECIMAL DIGIT TRANSLATE TABLE
SYMCARD  DC    CL80'         DC    X''000000000000000000000000000000000+
               000''   X''##''                  '
         PRINT NOGEN
SYMDCBS  DCB   DSORG=PS,DDNAME=$,MACRF=PM,RECFM=FB,LRECL=80
         PRINT GEN
SYMDCBL  EQU   *-SYMDCBS
CODEPTS  DC    191AL1(*-CODEPTS+X'41')

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V I M A G E   -   D Y N A M I C   A R E A  '
@IMGWORK DSECT
         DS    18F
IMGSYMSV DS    16F
SYMDCB   DS    XL(SYMDCBL)         SYMBOL SOURCE OUTPUT DCB
SYMSTMT  DS    CL80                SYMBOL SOURCE OUTPUT RECORD
SYMWRTCT DS    F                   COUNT OF SYMBOLS IN SYMBOL SET
IMGTPUT  DS    XL(IMGTPUTL)        TPUT PARAMETER LIST
IMGROWS  DS    F                   NUMBER OF PICTURE LINES TO LOAD
IMGCOLS  DS    F                   NUMBER OF PICTURE COLUMNS TO LOAD
IMGLFTPD DS    F                   NUMBER OF PADDING BLANKS ON LEFT
IMGBFADR DS    F                   ADDRESS OF FULLSCREEN TPUT BUFFER
IMGSYMAD DS    F                   ADDRESS OF LPS PEL BITS
IMGHOME  DS    F                   ADDRESS OF FIRST PIXEL TO LOAD
IMGCLRPT DS    F                   ADDRESS OF COLOUR RWS ID
IMGLNORG DS    F                   PICTURE SCREEN LINE ORIGIN
IMGEOLIN DS    F                   POINT PAST END OF CURRENT SCAN LINE
IMGLNLOC DS    F                   PICTURE LINE START SCREEN LOCATION
IMGSSSSV DS    6F                  SYMBOL SET START STATUS SAVE AREA
IMGBLOKS DS    XL8                 SOLID COLOUR BLOCK CODE POINTS
IMGPELBT DS    X                   BIT TO TEST IN ARRAY ELEMENTS
LPSWSF   DS    X                   WRITE STRUCTURED FIELD COMMAND
LPSLEN   DS    H                   LENGTH OF THIS STRUCTURED FIELD
LPSID    DS    X                   LOAD PROGRAMMED SYMBOLS ID
LPSFLAGS DS    X                   BASIC/EXT, CLEAR, SKIP, TYPE:5-BITS
LPSLCID  DS    X                   LOCAL CHARACTER SET ID
LPSCHAR  DS    X                   FIRST CODE POINT TO BE LOADED
LPSRWS   DS    X                   READ/WRITE STORAGE NUMBER
LPSBASIC EQU   *                   SYMBOL DATA START FOR BASIC FORM
LPSLENE  DS    X                   LENGTH OF EXTENDED DATA
LPSFLAGE DS    X                   EXTENDED FLAGS
LPSLW    DS    X                   CHARACTER CELL MATRIX WIDTH
LPSLH    DS    X                   CHARACTER CELL MATRIX HEIGHT
LPSSUBSN DS    X                   SUBSECTION ID
LPSCOLOR DS    X                   COLOR PLANE TO LOAD
LPSHDRLN EQU   *-LPSLEN            COLOR PLANE FIELD HEADER LENGTH
LPSDATAE DS    190XL18             ROOM FOR WHOLE SYMBOL SET
LPSPLNLN EQU   *-LPSLEN            MAXIMUM LENGTH FOR ONE PLANE
         DS    2XL(LPSPLNLN)       ROOM FOR TWO MORE PLANES
IMGWRTBF DS    43XL132             FULLSCREEN GRAPHICS TPUT BUFFER
         DS    0D                  END OF DSECT
@IMGWKLN EQU   *-@IMGWORK
         TITLE '  R E V G D D M  '
**********************************************************************
*                                                          *         *
*         DISPLAY PICTURE IMAGE WITH VECTOR GRAPHICS       *  GP@P6  *
*                                                          *  01/00  *
**********************************************************************

**********************************************************************
*                                                                    *
*           THIS ROUTINE WILL PROCESS PICTURE DATA PREVIOUSLY        *
*        STORED DURING INITIALIZATION.  THE PICTURE WILL BE          *
*        RENDERED AND WRITTEN TO THE TERMINAL BY THIS ROUTINE        *
*        USING GDDM CALLS.                                           *
*                                                                    *
*           GDDM WILL USE THE WHOLE SCREEN, AND "NORMAL" INTERACTION *
*        WITH REVIEW WILL RESUME AFTER THIS ROUTINE TERMINATES.      *
*                                                                    *
*           THIS ROUTINE IS GIVEN CONTROL BY REVIMAGE AFTER IT       *
*        HAS BEEN DECIDED TO EMPLOY VECTOR GRAPHICS, WHICH IT IS     *
*        ASSUMED GDDM WILL USE.                                      *
*                                                                    *
*           REVIMAGE AND REVGDDM CAN PROCESS THE REVIEW PICTURE      *
*        BYTE/PIXEL ARRAY AND RENDER THE PICTURE ON A 3270 GRAPHICS  *
*        TERMINAL.  THIS PROCESS IS INDEPENDENT OF FORMAT OF THE     *
*        PICTURE FILE BEING REVIEWED.  CURRENTLY, THIS ARRAY IS ONLY *
*        CREATED BY THE PCXCHECK ROUTINE WHICH CAN PROCESS ".PCX"    *
*        SINGLE-PLANE PICTURE FILES WITH 1-, 4- OR 8-BIT COLOUR.     *
*                                                                    *
**********************************************************************

**********************************************************************
*                                                                    *
* ON ENTRY      HSEACTIV -> PICTURE DATA INTERPRETED FROM FILE       *
*                     R9 -> @DATA                                    *
*                     R13-> REGISTER SAVE AREA                       *
*                     R14-> RETURN ADDRESS                           *
*                     R15-> REVGDDM                                  *
*                                                                    *
* DURING PROCESSING   R13-> @VECWORK (LOCAL GETMAINED AREA)          *
*                                                                    *
* ON EXIT             R6 -> END OF DATA STREAM FOR REVIEW2 TO WRITE  *
*                                                                    *
**********************************************************************

REVGDDM  CSECT
REVGDDM  AMODE 31
         USING REVGDDM,R15
         B     @GDDM
         DC    AL1(7),CL7'REVGDDM'
         DROP  R15                 REVGDDM
@GDDM    STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R11,R15             SET BASE
         USING REVGDDM,R11
         L     R0,@VECWKSZ
         GETMAIN R,LV=(0)          GET MAIN STORAGE
         ST    R1,8(,R13)          CHAIN SAVE AREAS
         ST    R13,4(,R1)
         LR    R13,R1              POINT TO GETMAINED AREA
         USING @VECWORK,R13
         MVC   VECDCB,VECDCBS      LOAD DCB TEMPLATE
         USING IHADCB,VECDCB
         MVC   DCBDDNAM,PICSRCDD   LOAD DCB DDNAME
         ICM   R0,15,ASPLTADR      HAS ADMASPLT BEEN LOADED?
         BNZ   VECASPOK            YES
         LOAD  EPLOC=ADMASPLT      NO, LOAD IT
         ST    R0,ASPLTADR         SAVE THE LOAD POINT
VECASPOK L     R1,X_PELS           GET PICTURE WIDTH
         LA    R1,7(,R1)
         SRL   R1,3                ROUND UP TO BYTE BOUNDARY
         M     R0,Y_PELS           GET GDDM BITMAP SIZE
         ST    R1,V_LENGTH         SAVE IT
         LA    R0,7(,R1)
         SRL   R0,3                ROUND UP TO DOUBLEWORD BOUNDARY
         SLL   R0,3
         ST    R0,VECBMPLN         SAVE BITMAP STORAGE SIZE
         GETMAIN RU,LV=(0),LOC=ANY,SP=63
         ST    R1,VECBMPAD         SAVE BITMAP STORAGE ADDRESS
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   VECOPNOK            NO

         MVI   OPEND,X'80'         YES, OPEN OUTFILE FILE
         OPEN  (VECDCB,(OUTPUT)),MF=(E,OPEND)
         MVI   VECSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   VECSTMT+1(79),VECSTMT
         MVC   VECSTMT(30),=C'* REVIEW &REL  BITMAP DATA FOR'
         LH    R1,$DSNAME
         BCTR  R1,0
         EX    R1,LDVECDSN
         CLI   $MEMBER,C' '        MEMBER NAME TO BE DISPLAYED?
         BE    VECDSNOK            NO
         LA    R1,VECSTMT+32(R1)
         MVI   0(R1),C'('
         MVC   1(8,R1),$MEMBER
VECMEMLP CLI   8(R1),C' '          TRAILING BLANK?
         BNE   VECMEMOK            NO
         BCT   R1,VECMEMLP
VECMEMOK MVI   9(R1),C')'
VECDSNOK PUT   VECDCB,VECSTMT      WRITE HEADING
         B     VECOPNOK
LDVECDSN MVC   VECSTMT+31(0),$DSNAME+2   <<< EXECUTED >>>

VECOPNOK LA    R1,AAB
         ST    R1,AABPTR
         LA    R1,SPINIT
         ST    R1,RCPPTR
         LA    R1,SPIB
         ST    R1,V_PARM1
         OI    V_PARM1,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             SPINIT

         LA    R1,GSMIX
         ST    R1,RCPPTR
         LA    R1,GMIXXOR
         ST    R1,V_PARM1
         OI    V_PARM1,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSMIX (XOR)

         MVI   VECSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   VECSTMT+1(79),VECSTMT
         MVI   VECSTMT,C'*'        FLAG COMMENT
         LA    R8,VECSTMT+5
         MVC   5(1,R8),PICOLRES+3  SHOW COLOUR BIT DEPTH
         OI    5(R8),X'F0'         MAKE NUMERIC CHARACTER
         MVC   6(10,R8),=CL10'-BIT COLOR'
         L     R0,X_PELS
         LA    R15,18(,R8)         POINT TO DISPLAY AREA
         BAL   R14,VECEDNUM        FORMAT NUMBER
         MVC   7(9,R15),=CL9'PELS WIDE'
         L     R0,Y_PELS
         LA    R15,36(,R8)         POINT TO DISPLAY AREA
         BAL   R14,VECEDNUM        FORMAT NUMBER
         MVC   7(9,R15),=CL9'PELS DEEP'
         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   VECTXTOK            NO
         PUT   VECDCB,VECSTMT      YES, WRITE STATISTICS

VECTXTOK LA    R1,GSCOL
         ST    R1,RCPPTR
         LA    R1,GCOLTURQ
         ST    R1,V_PARM1
         OI    V_PARM1,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSCOL (TURQUOISE)

         LA    R1,GSCHAR
         ST    R1,RCPPTR
         LA    R1,GPOSMIN
         ST    R1,V_PARM1          X
         ST    R1,V_PARM2          Y
         LA    R1,GTXTLEN
         ST    R1,V_PARM3
         LA    R1,VECSTMT+10
         ST    R1,V_PARM4
         OI    V_PARM4,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSCHAR (FROM BOTTOM LEFT)

         LA    R1,GSCP
         ST    R1,RCPPTR
         LA    R1,GPOSMIN
         ST    R1,V_PARM1          X
         LA    R1,GPOSMAX
         ST    R1,V_PARM2          Y
         OI    V_PARM2,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSCP (TO TOP LEFT)

         XC    DOUBLE,DOUBLE       SET UP FOR FIRST OR ONLY PLANE
         MVI   DOUBLE+3,X'01'      SET BIT TO TEST IN ARRAY BYTES

VECPLNLP LA    R1,GSCOL
         ST    R1,RCPPTR
         LA    R1,DOUBLE
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   VECMONO             YES, GET PICTURE COLOUR TO USE
         L     R0,DOUBLE           NO, SAVE COLOUR PLANE INDICATOR
         NC    DOUBLE+3(1),PICCOLOR
         BNZ   VECOLROK            THIS PLANE IS IN PICTURE COLOUR
         ST    R0,DOUBLE           RESTORE COLOUR PLANE INDICATOR
         B     VECPLNOK            SKIP THIS COLOUR PLANE
VECMONO  MVC   DOUBLE+7(1),PICCOLOR     COPY COLOUR TO USE
         NI    DOUBLE+7,X'07'
         LA    R1,DOUBLE+4
VECOLROK ST    R1,V_PARM1
         OI    V_PARM1,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSCOL

         L     R15,DOUBLE          GET PIXEL BYTE MASK
         L     R1,HSEACTIV         POINT TO PIXEL/BYTE ARRAY
         L     R2,VECBMPAD         POINT TO BITMAP STORAGE
         L     R3,Y_PELS           GET SCAN LINES IN PICTURE
VECLOOP1 L     R4,X_PELS           GET PIXEL WIDTH OF PICTURE
VECLOOP2 LA    R0,8                GET BITS PER BYTE
         SLR   R14,R14             CLEAR ACCUMULATOR
VECLOOP3 SLL   R14,1               PROMOTE ACCUMULATED BITS
         LTR   R4,R4               FINISHED LINE'S PIXELS?
         BNP   VECBITOK            YES, DO NOT TEST BYTE
         EX    R15,TESTPEL         TEST IF PIXEL BIT IS ON
         LA    R1,1(,R1)           POINT TO NEXT ARRAY BYTE
         BCTR  R4,0                DECREMENT PIXELS IN LINE TO GO
         BZ    VECBITOK            PIXEL BIT IS OFF
         LA    R14,1(,R14)         PIXEL BIT IS ON
VECBITOK BCT   R0,VECLOOP3         PROCESS NEXT BIT OF BYTE
         STC   R14,0(,R2)          STORE EIGHT BITMAP PIXELS
         LA    R2,1(,R2)           POINT TO NEXT BITMAP BYTE
         LTR   R4,R4               FINISHED THIS LINE'S PIXELS?
         BP    VECLOOP2            NO, CONTINUE PACKING THIS SCAN LINE
         BCT   R3,VECLOOP1         YES, COMMENCE PACKING NEXT SCAN LINE

         LA    R1,GSIMG
         ST    R1,RCPPTR
         LA    R1,GIMGTYPE
         ST    R1,V_PARM1          IMAGE TYPE
         LA    R1,X_PELS
         ST    R1,V_PARM2          IMAGE WIDTH
         LA    R1,Y_PELS
         ST    R1,V_PARM3          IMAGE DEPTH
         LA    R1,V_LENGTH
         ST    R1,V_PARM4          IMAGE LENGTH
         L     R1,VECBMPAD
         ST    R1,V_PARM5          IMAGE DATA
         OI    V_PARM5,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             GSIMG (CURRENT POSITION UNCHANGED)

         CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   VECPLNOK            NO
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   VECUNPAK            YES, GET ON WITH IT
         MVI   VECSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   VECSTMT+1(79),VECSTMT
         MVC   VECSTMT(13),=CL13'*  BLUE PLANE'
         CLI   DOUBLE+3,X'01'      CORRECT?
         BE    VECPLNHD            YES
         MVC   VECSTMT+3(4),=CL4' RED'
         CLI   DOUBLE+3,X'02'      CORRECT?
         BE    VECPLNHD            YES
         MVC   VECSTMT+2(5),=CL5'GREEN'
VECPLNHD PUT   VECDCB,VECSTMT
VECUNPAK L     R2,VECBMPAD         INITIALIZE BUFFER POINTER
         LA    R3,VECSTMT+17       POINT TO FIRST UNPACK TARGET
         LA    R4,VECSTMT+55       POINT TO LAST UNPACK TARGET
         L     R5,Y_PELS           GET NUMBER OF SCAN LINES
VECLINLP L     R6,X_PELS           GET PICTURE PIXEL WIDTH
         LA    R6,7(,R6)
         SRL   R6,3                GET BYTES PER SCAN LINE
VECCRDLP MVI   VECSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   VECSTMT+1(79),VECSTMT
         MVC   VECSTMT(17),VECCARD COPY TEMPLATE
         LR    R7,R3               POINT TO FIRST UNPACK TARGET
VECBYTLP UNPK  0(3,R7),0(2,R2)     UNPACK A BITMAP BYTE
         TR    0(2,R7),VECHEX-C'0'
         LA    R7,2(,R7)           POINT PAST LATEST HEX
         MVI   0(R7),QUOTE         PREPARE FOR END OF RECORD'S DATA
         LA    R2,1(,R2)           POINT TO BITMAP BYTE
         CR    R7,R4               FILLED CARD IMAGE?
         BH    VECPUTIT            YES, WRITE OUTPUT RECORD
         BCT   R6,VECBYTLP         NO, GO UNPACK ANOTHER BYTE
         LA    R6,1                END OF LINE SO WRITE RECORD
VECPUTIT PUT   VECDCB,VECSTMT      PRODUCE BITMAP DATA RECORD
         BCT   R6,VECCRDLP         GO WRITE ANOTHER CARD IMAGE
         BCT   R5,VECLINLP         GO PROCESS NEXT SCAN LINE
         MVI   VECSTMT,C' '        CLEAR OUTPUT RECORD
         MVC   VECSTMT+1(79),VECSTMT
         L     R0,V_LENGTH         SHOW LENGTH OF BITMAP
         CVD   R0,24(,R13)
         MVI   VECSTMT,C'*'        FLAG COMMENT
         MVC   VECSTMT+10(8),=X'4020202020202120'
         ED    VECSTMT+10(8),28(R13)
         MVC   VECSTMT+19(21),=C'BYTES IN IMAGE BITMAP'
         CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   VECPUTSZ            YES
         MVC   VECSTMT+28(5),=C'PLANE'
VECPUTSZ PUT   VECDCB,VECSTMT

VECPLNOK CLI   PICOLRES+3,1        BLACK-AND-WHITE PICTURE?
         BNH   VECIMGDN            YES, PICTURE NOW RENDERED
         CLI   DOUBLE+3,X'02'      LAST PLANE PROCESSED?
         BH    VECIMGDN            YES, PICTURE NOW RENDERED
         L     R0,DOUBLE           NO, GET COLOUR PLANE NUMBER
         SLL   R0,1                PROMOTE BIT TO NEXT PLANE
         ST    R0,DOUBLE           SAVE NEXT COLOUR PLANE NUMBER
         B     VECPLNLP            PROCESS NEXT COLOUR PLANE

VECIMGDN CLI   PICSRCDD,C' '       WRITING PICTURE SOURCE?
         BNH   VECLOSOK            NO
         MVI   CLOSED,X'80'        YES, CLOSE THE OUTPUT DCB
         CLOSE (VECDCB),MF=(E,CLOSED)
         MVI   PICSRCDD,0          CLEAR SOURCE CODE REQUEST FLAG
VECLOSOK LM    R0,R1,VECBMPLN      FREE BITMAP STORAGE
         FREEMAIN RU,LV=(0),A=(1),SP=63

         LA    R1,ASREAD
         ST    R1,RCPPTR
         LA    R1,ATTTYPE
         ST    R1,V_PARM1
         LA    R1,ATTVALUE
         ST    R1,V_PARM2
         LA    R1,ATTCOUNT
         ST    R1,V_PARM3
         OI    V_PARM3,X'80'       FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             ASREAD (FINISH SEND, WAIT FOR INPUT)

         LA    R1,FSTERM
         ST    R1,RCPPTR
         OI    RCPPTR,X'80'        FLAG LAST PARAMETER
         LA    R1,VECPARMS         POINT TO GDDM PARAMETER LIST
         L     R15,ASPLTADR
         BALR  R14,R15             FSTERM

         L     R0,@VECWKSZ         GET DYNAMIC STORAGE SIZE
         LR    R1,R13              POINT TO DYNAMIC AREA TO BE FREED
         DROP  R13                 @VECWORK
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE @VECWORK
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BSM   0,R14               RETURN WITH CALLER'S AMODE

TESTPEL  TM    0(R1),X'00'         <<< EXECUTED >>>

VECEDNUM CVD   R0,DOUBLE
         MVC   0(6,R15),=XL6'402020202120'
         ED    0(6,R15),DOUBLE+5
         BR    R14

         DROP  R11                 REVGDDM
         TITLE '  R E V G D D M   -   S T A T I C   A R E A  '
@VECWKSZ DC    A(@VECWKLN)         LOCAL WORKING STORAGE SIZE
ADMASPLT DC    CL8'ADMASPLT'       GDDM DYNAMIC SPI FOR TSO
SPINIT   DC    A(QQSPINIT)         INITIALIZE GDDM WITH SPIB
GSMIX    DC    A(QQGSMIX)          SET FOREGROUND COLOUR-MIXING MODE
GSCOL    DC    A(QQGSCOL)          SET GRAPHIC COLOUR
GSCHAR   DC    A(QQGSCHAR)         DRAW A CHARACTER STRING
GSCP     DC    A(QQGSCP)           SET CURRENT POSITION
GSIMG    DC    A(QQGSIMG)          DRAW A GRAPHICS IMAGE
ASREAD   DC    A(QQASREAD)         PERFORM A TERMINAL READ
FSTERM   DC    A(QQFSTERM)         TERMINATE GDDM
GMIXXOR  DC    F'4'
GCOLTURQ DC    F'5'
GTXTLEN  DC    F'47'
GIMGTYPE DC    F'0'
GPOSMIN  DC    E'0'
GPOSMAX  DC    E'100'
SPIB     DS    0F                  SYSTEM PROGRAMMER INTERFACE BLOCK
SPIBHEAD DC    CL4'SPIB'           NOT USED
SPIBLENG DC    F'16'               LENGTH OF SPIB - 16 IS MINIMUM
SPIBUDSL DC    F'0'
SPIBUDSP DC    F'0'
VECHEX   DC    C'0123456789ABCDEF' HEXADECIMAL DIGIT TRANSLATE TABLE
VECCARD  DC    CL17'         DC    X'''
         PRINT NOGEN
VECDCBS  DCB   DSORG=PS,DDNAME=$,MACRF=PM,RECFM=FB,LRECL=80
         PRINT GEN
VECDCBL  EQU   *-VECDCBS

         LTORG

         DS    0D                  END OF CSECT
         TITLE '  R E V G D D M   -   D Y N A M I C   A R E A  '
@VECWORK DSECT
         DS    18F
VECBMPLN DS    F            _____/ BITMAP STORAGE LENGTH
VECBMPAD DS    F                 \ BITMAP STORAGE ADDRESS
VECDCB   DS    XL(VECDCBL)         BITMAP SOURCE OUTPUT DCB
VECSTMT  DS    CL80                BITMAP SOURCE OUTPUT RECORD
AAB      DS    0F                  APPLICATION ANCHOR BLOCK
AABFC    DS    0F                  GDDM FEEDBACK CODE
AABSC    DS    H                   GDDM SEVERITY CODE
AABEC    DS    H                   GDDM ERROR CODE
AABAP    DS    A                   GDDM ANCHOR POINTER
RCP      DS    F                   REQUEST CONTROL PARAMETER
V_LENGTH DS    F                   LENGTH OF THE IMAGE BITMAP
VECPARMS DS    0F
AABPTR   DS    A                   APPLICATION ANCHOR BLOCK ADDRESS
RCPPTR   DS    A                   REQUEST CONTROL PARAMETER ADDRESS
V_PARM1  DS    A
V_PARM2  DS    A
V_PARM3  DS    A
V_PARM4  DS    A
V_PARM5  DS    A
ATTTYPE  DS    A
ATTVALUE DS    A
ATTCOUNT DS    A
         DS    0D                  END OF DSECT
@VECWKLN EQU   *-@VECWORK
         TITLE '  G D D M   -   R C P   C O D E S  '
*                                COPIED FROM GDDM.SADMSAM(ADMURCPB)
* GROUP - ADMMCQA
QQFSTRCE EQU   X'00020000'       TRACE
QQFSEXIT EQU   X'00030000'       SET ERROR EXIT
QQFSQERR EQU   X'00040000'       QUERY LAST ERROR
QQSPINIT EQU   X'00050000'       SPI SPECIAL INIT
QQFSQSYS EQU   X'00060000'       QUERY SYSTEMS ENVIRONS
QQESSUDS EQU   X'00070000'       SPECIFY SOURCE UDS
QQESEUDS EQU   X'00080000'       SPECIFY ENCODED UDS
QQESACRT EQU   X'000A0000'       CREATE APP GROUP
QQESADEL EQU   X'000B0000'       DELETE APP GROUP
QQESAQRY EQU   X'000C0000'       QUERY CURRENT APP GRP
QQESASEL EQU   X'000D0000'       SELECT APP GROUP
QQFSTRAN EQU   X'000F0000'       TRANSLATE CHAR STRING
QQESQCPG EQU   X'00100000'       QUERY CODE PAGE
QQESSCPG EQU   X'00110000'       SET CODE PAGE
QQESQEUD EQU   X'00120000'       QUERY ENCODED UDS
QQESQUNL EQU   X'00130000'       QRY NICKNAME INFO LEN
QQESQUNS EQU   X'00140000'       QUERY NICKNAME INFO
* GROUP - ADMMCQEE
QQESLIB  EQU   X'08142000'       DEFINE LIBRARY
QQESQOBJ EQU   X'08142400'       QUERY OBJECT EXISTENCE
QQESPCB  EQU   X'081C1000'       DEFINE PCB
* GROUP - ADMMCQDS
QQFSTERM EQU   X'0C000000'       TERMINATION
QQFSINIT EQU   X'0C000001'       INITIALISATION
QQFSRNIT EQU   X'0C000002'       RE-INITIALISATION
QQDSOPEN EQU   X'0C000200'       OPEN DEVICE
QQDSCLS  EQU   X'0C000201'       CLOSE DEVICE
QQDSUSE  EQU   X'0C000202'       DEVICE USAGE
QQDSDROP EQU   X'0C000203'       DROP DEVICE
QQDSQUID EQU   X'0C000204'       QUERY UNIQUE DEVICE ID
QQDSQUSE EQU   X'0C000205'       QUERY USAGE
QQDSQDEV EQU   X'0C000206'       QUERY DEVICE CHRISTCS
QQDSRNIT EQU   X'0C000207'       RE-INIT DEVICE
* GROUP - ADMMCQDC
QQFSPCRT EQU   X'0C040000'       PAGE CREATION
QQFSPSEL EQU   X'0C040001'       PAGE SELECTION
QQFSPDEL EQU   X'0C040002'       PAGE DELETION
QQFSPCLR EQU   X'0C040003'       PAGE CLEAR
QQFSPQRY EQU   X'0C040004'       QUERY PAGE
QQFSQCPG EQU   X'0C040005'       QUERY CURRENT PAGE
QQMSPQRY EQU   X'0C040006'       QUERY MAPPED PAGE
QQSSQF   EQU   X'0C040100'       QUERY S SETS ON FILE
QQPSQSS  EQU   X'0C040101'       QUERY DEVICE PS STORES
QQGSQNSS EQU   X'0C040102'       QUERY NO LOADED GRA SS
QQGSQSS  EQU   X'0C040103'       QUERY LOADED GRPHCS SS
QQPSLSS  EQU   X'0C040200'       LOAD DEV PS FROM STOR
QQPSLSSC EQU   X'0C040201'       COND LOAD DEV PS STORE
QQPSDSS  EQU   X'0C040202'       LOAD DEV PS FROM PROG
QQPSRSV  EQU   X'0C040203'       RESERVE DEVICE PS STOR
QQGSLSS  EQU   X'0C040300'       LOAD GRPH SYMBOL SET
QQGSDSS  EQU   X'0C040301'       DEFINE GRPH SYMBOL SET
QQPSRSS  EQU   X'0C040400'       RELEASE SS FROM DEV PS
QQGSRSS  EQU   X'0C040401'       RELEASE GRPH SYM SET
QQFSQDEV EQU   X'0C040500'       QUERY DEVICE
QQFSQURY EQU   X'0C040501'       EXTENDED QUERY DEVICE
QQFSQUPG EQU   X'0C040900'       QUERY UNIQUE PAGE ID
QQSSREAD EQU   X'0C040B00'       READ SYMBOL SET
QQSSWRT  EQU   X'0C040B01'       WRITE SYMBOL SET
QQFSPWIN EQU   X'0C040C00'       PAGE WINDOW
QQFSQWIN EQU   X'0C040C01'       PAGE WINDOW QUERY
QQGSCPG  EQU   X'0C040D00'       SET CURRENT CODE PAGE
QQGSQCPG EQU   X'0C040D01'       QUERY CODE PAGE
QQFSENAB EQU   X'0C040E00'       ENABLE DEVICE INPUT
* GROUP - ADMMCQDA
QQFSALRM EQU   X'0C080000'       SOUND ALARM
QQASFCUR EQU   X'0C080100'       MOVE CURSOR
QQASDFLT EQU   X'0C080200'       SET DEFAULT FLD ATTRS
QQASDTRN EQU   X'0C080300'       DEFINE XLATE TABLE
QQASFCLR EQU   X'0C080400'       CLEAR ALPHA FIELDS
QQASFTYP EQU   X'0C080500'       SET FIELD TYPE
QQASFINT EQU   X'0C080501'       SET FIELD INTENSITY
QQASFCOL EQU   X'0C080502'       SET FIELD COLOR
QQASFPSS EQU   X'0C080503'       SET FIELD PRIMARY SS
QQASFHLT EQU   X'0C080504'       SET FIELD HILITE
QQASFEND EQU   X'0C080505'       SET END FLD ATTRIBUTE
QQASFOUT EQU   X'0C080506'       SET FIELD O/P NULLS
QQASFIN  EQU   X'0C080507'       SET FIELD I/P BLANKS
QQASFTRN EQU   X'0C080508'       SET FLD XLATE TABLE
QQASFTRA EQU   X'0C080509'       SET FIELD TRANSPARENCY
QQASFSEN EQU   X'0C08050A'       SET SO/SI ENABLEMENT
QQASFBDY EQU   X'0C08050B'       SET FIELD OUTLINING
QQASCHLT EQU   X'0C080600'       SET CHARACTER HILITES
QQASCCOL EQU   X'0C080601'       SET CHARACTER COLORS
QQASCSS  EQU   X'0C080602'       SET CHAR SYMBOL SETS
QQASCPUT EQU   X'0C080603'       SET CHARACTER CODES
QQASDFLD EQU   X'0C080700'       DEFINE ALPHA FIELD
QQASRFMT EQU   X'0C080800'       REDEFINE ALPHA FIELDS
QQASDFMT EQU   X'0C080801'       DEFINE ALPHA FIELDS
QQASRATT EQU   X'0C080802'       REDEFINE FIELD ATTR.S
QQASQHLT EQU   X'0C080900'       QUERY CHAR HILITES
QQASQCOL EQU   X'0C080901'       QUERY CHARACTER COLORS
QQASQSS  EQU   X'0C080902'       QUERY CHAR SYMBOL SETS
QQASCGET EQU   X'0C080903'       QUERY CHARACTER CODES
QQASQFLD EQU   X'0C080A00'       QUERY FIELD ATTRIBUTES
QQASQMOD EQU   X'0C080B00'       QUERY MODIFIED FIELDS
QQFSREST EQU   X'0C080C00'       RESTORE SCREEN
QQDSCMF  EQU   X'0C080C01'       USER CONTROL FUNCTION
QQDSQCMF EQU   X'0C080C02'       QUERY USER CONTROL FTN
QQASMODE EQU   X'0C080D00'       SET REPLY MODE
QQASQMAX EQU   X'0C080E00'       QUERY NO. OF FIELDS
QQASQNMF EQU   X'0C080E01'       QUERY NO. MOD. FIELDS
QQASQCUR EQU   X'0C080F00'       QUERY CURSOR
QQASFMOD EQU   X'0C081100'       MODIFY FIELD
QQASTYPE EQU   X'0C081300'       SET TERMINAL TYPE
QQASGPUT EQU   X'0C081503'       SET DUAL-CHARACTERS
QQASGGET EQU   X'0C081603'       QUERY DUAL-CHARACTERS
QQASQLEN EQU   X'0C081800'       QUERY FIELD LENGTH
* GROUP - ADMMCQDG
QQGSFLD  EQU   X'0C0C0000'       GRAPHICS FIELD
QQGSPS   EQU   X'0C0C0001'       PICTURE SPACE
QQGSWIN  EQU   X'0C0C0002'       SPECIFY WINDOW
QQGSVIEW EQU   X'0C0C0003'       SPECIFY VIEWPORT
QQGSQPS  EQU   X'0C0C0004'       QUERY PICTURE SPACE
QQGSQVIE EQU   X'0C0C0005'       QUERY VIEWPORT
QQGSQWIN EQU   X'0C0C0006'       QUERY WINDOW
QQGSUWIN EQU   X'0C0C0007'       DEFINE UNIFORM WINDOW
QQGSQFLD EQU   X'0C0C000A'       QUERY GRAPHICS FIELD
QQGSARCC EQU   X'0C0C000B'       ASPECT RATIO CONTROL
QQGSBND  EQU   X'0C0C000D'       SPECIFY DATA BOUNDARY
QQGSQBND EQU   X'0C0C000E'       QUERY DATA BOUNDARY
QQGSQMAX EQU   X'0C0C0100'       QUERY SEGMENT RANGE
QQGSQCUR EQU   X'0C0C0101'       QUERY CURSOR
QQGSQSSD EQU   X'0C0C0102'       QUERY SYMBOL SET DATA
QQGSQCEL EQU   X'0C0C0202'       QUERY CELL SIZE
QQGSCLP  EQU   X'0C0C0203'       SPECIFY CLIPPING
QQGSQCLP EQU   X'0C0C0204'       QUERY CLIPPING
QQGSSEG  EQU   X'0C0C0300'       CREATE SEGMENT
QQGSSCLS EQU   X'0C0C0301'       CLOSE SEGMENT
QQGSSDEL EQU   X'0C0C0302'       DELETE SEGMENT
QQGSCLR  EQU   X'0C0C0303'       CLEAR GRAPHICS FIELD
QQGSSATI EQU   X'0C0C0309'       SET INITIAL ATTRIBUTES
QQGSQATI EQU   X'0C0C030A'       QUERY INITIAL ATTS
QQGSSATS EQU   X'0C0C030B'       SET SEGMENT ATTRIBUTES
QQGSQATS EQU   X'0C0C030C'       QUERY SEGMENT ATTS
QQGSSPOS EQU   X'0C0C030D'       SET SEGMENT POSITION
QQGSQPOS EQU   X'0C0C030E'       QUERY SEGMENT POSITION
QQGSSORG EQU   X'0C0C0311'       SET SEGMENT ORIGIN
QQGSSPRI EQU   X'0C0C0312'       SET SEGMENT PRIORITY
QQGSQPRI EQU   X'0C0C0313'       QUERY SEGMENT PRIORITY
QQGSQORG EQU   X'0C0C0316'       QUERY SEGMENT ORIGIN
QQGSMOVE EQU   X'0C0C0400'       MOVE TO
QQGSLINE EQU   X'0C0C0401'       LINE TO
QQGSPLNE EQU   X'0C0C0402'       POLYLINE TO
QQGSMARK EQU   X'0C0C0406'       MARKER AT
QQGSMRKS EQU   X'0C0C0407'       POLY MARKER AT
QQGSAREA EQU   X'0C0C0408'       BEGIN AREA
QQGSENDA EQU   X'0C0C0409'       END AREA
QQGSVECM EQU   X'0C0C040A'       VECTOR
QQGSCHAR EQU   X'0C0C0500'       CHARACTER STRING AT
QQGSCHAP EQU   X'0C0C0501'       CHARACTER STRING
QQGSQTB  EQU   X'0C0C0502'       QUERY THE TEXT BOX
QQGSARC  EQU   X'0C0C0600'       ARC
QQGSELPS EQU   X'0C0C0601'       ELLIPSE
QQGSPFLT EQU   X'0C0C0602'       FILLET
QQGSQCP  EQU   X'0C0C0700'       QUERY CURRENT POSN
QQGSCOL  EQU   X'0C0C0701'       SET COLOR
QQGSMIX  EQU   X'0C0C0702'       SET MIX MODE
QQGSLT   EQU   X'0C0C0703'       SET LINE TYPE
QQGSLW   EQU   X'0C0C0704'       SET LINE WIDTH
QQGSCM   EQU   X'0C0C0705'       SET CHARACTER MODE
QQGSCS   EQU   X'0C0C0706'       SET CHARACTER SET
QQGSCB   EQU   X'0C0C0707'       SET CHARACTER BOX
QQGSCA   EQU   X'0C0C0708'       SET CHARACTER ANGLE
QQGSCD   EQU   X'0C0C0709'       SET CHARACTER DIRCTN
QQGSPAT  EQU   X'0C0C070A'       SET PATTERN
QQGSMS   EQU   X'0C0C070B'       SET MARKER SYMBOL
QQGSCH   EQU   X'0C0C070C'       SET CHARACTER SHEAR
QQGSFLW  EQU   X'0C0C070E'       SET FRACTIONAL WIDTH
QQGSQFLW EQU   X'0C0C070F'       QUERY FRACTIONAL WIDTH
QQGSQCOL EQU   X'0C0C0711'       QUERY COLOR
QQGSQMIX EQU   X'0C0C0712'       QUERY MIX MODE
QQGSQLT  EQU   X'0C0C0713'       QUERY LINE TYPE
QQGSQLW  EQU   X'0C0C0714'       QUERY LINE WIDTH
QQGSQCM  EQU   X'0C0C0715'       QUERY CHARACTER MODE
QQGSQCS  EQU   X'0C0C0716'       QUERY CHARACTER SET
QQGSQCB  EQU   X'0C0C0717'       QUERY CHARACTER BOX
QQGSQCA  EQU   X'0C0C0718'       QUERY CHARACTER ANG
QQGSQCD  EQU   X'0C0C0719'       QUERY CHARACTER DIRN
QQGSQPAT EQU   X'0C0C071A'       QUERY PATTERN
QQGSQMS  EQU   X'0C0C071B'       QUERY MARKER SYMBOL
QQGSQCH  EQU   X'0C0C071C'       QUERY CHARACTER SHEAR
QQGSMSC  EQU   X'0C0C071D'       SET MARKER SCALE
QQGSQMSC EQU   X'0C0C071E'       QUERY MARKER SCALE
QQGSPUT  EQU   X'0C0C0900'       INCLUDE GDF
QQGSIMG  EQU   X'0C0C0A00'       LIMITED IMAGE
QQGSIMGS EQU   X'0C0C0A04'       LIMITED IMAGE (SCALED)
QQGSGETS EQU   X'0C0C0B00'       RETRIEVE GDF
QQGSGETE EQU   X'0C0C0B01'       END GDF RETRIEVAL
QQGSGET  EQU   X'0C0C0B02'       GET GDF BUFFER
QQGSILOC EQU   X'0C0C0C00'       INITIALIZE LOCATOR
QQGSIPIK EQU   X'0C0C0C01'       INITIALIZE PICK DEVICE
QQGSIDVI EQU   X'0C0C0C04'       INITIALIZE DATA VALUE
QQGSIDVF EQU   X'0C0C0C05'       INITIALIZE DATA VALUE
QQGSISTR EQU   X'0C0C0C06'       INIT. STRING DEVICE
QQGSISTK EQU   X'0C0C0C07'       INIT. STROKE DEVICE
QQGSQLID EQU   X'0C0C0C09'       QUERY INPUT DEVICE
QQGSENAB EQU   X'0C0C0D00'       ENABLE INPUT DEVICE
QQGSFLSH EQU   X'0C0C0E00'       FLUSH INPUT QUEUE
QQGSQSIM EQU   X'0C0C0E01'       QUERY INPUT QUEUE
QQGSQCHO EQU   X'0C0C0F00'       QUERY CHOICE DATA
QQGSQLOC EQU   X'0C0C0F01'       QUERY LOCATOR DATA
QQGSQPIK EQU   X'0C0C0F02'       QUERY PICK DATA
QQGSQSTR EQU   X'0C0C0F03'       QUERY STRING DATA
QQGSQSTK EQU   X'0C0C0F04'       QUERY STROKE DATA
QQGSQPKS EQU   X'0C0C0F05'       QUERY PICK STRUCTURE
QQGSTAG  EQU   X'0C0C1000'       SET THE CURRENT TAG
QQGSQTAG EQU   X'0C0C1001'       QUERY THE CURRENT TAG
QQGSSAGA EQU   X'0C0C1102'       SET GEOM ATTRIBUTES
QQGSSTFM EQU   X'0C0C1103'       SET SEGMENT TRANSFORM
QQGSQAGA EQU   X'0C0C1104'       QUERY GEOM ATTRIBUTES
QQGSQTFM EQU   X'0C0C1105'       QUERY SEGMENT TRANSFM
QQGSSCT  EQU   X'0C0C1107'       SET CURRENT TRANSFORM
QQGSSAVE EQU   X'0C0C1200'       SAVE SEGMENT
QQGSLOAD EQU   X'0C0C1201'       LOAD SEGMENT
QQGSMB   EQU   X'0C0C1307'       SET MARKER BOX
QQGSQMB  EQU   X'0C0C1308'       QUERY MARKER BOX
QQGSTA   EQU   X'0C0C130D'       SET TEXT ALIGNMENT
QQGSQTA  EQU   X'0C0C130E'       QUERY TEXT ALIGNMENT
QQGSCBS  EQU   X'0C0C130F'       SET CHARACTER SPACING
QQGSQCBS EQU   X'0C0C1310'       QUERY CHARACTER SPACIN
QQGSAM   EQU   X'0C0C1311'       SET ATTRIBUTE MODE
QQGSQAM  EQU   X'0C0C1312'       QUERY ATTRIBUTE MODE
QQGSPOP  EQU   X'0C0C1313'       RESTORE ATTRIBUTES
QQGSSVL  EQU   X'0C0C1314'       SET SEGMENT VIEW LIMIT
QQGSQSVL EQU   X'0C0C1315'       QUERY SEG VIEW LIMITS
QQGSQBMX EQU   X'0C0C1316'       QUERY BACKGROUND MIX
QQGSBMIX EQU   X'0C0C1317'       SET BACKGROUND MIX
QQGSCP   EQU   X'0C0C1319'       SET CURRENT POSITION
QQGSSCPY EQU   X'0C0C1400'       COPY SEGMENT
QQGSSINC EQU   X'0C0C1401'       INCLUDE SEGMENT
QQGSCALL EQU   X'0C0C1402'       CALL A SEGMENT
QQGSCORR EQU   X'0C0C1500'       EXPLICIT CORRELATE
QQGSCORS EQU   X'0C0C1501'       STRUCTURE CORRELATION
QQGSDEFS EQU   X'0C0C1900'       START DRAWING DEFAULTS
QQGSDEFE EQU   X'0C0C1901'       END DRAWING DEFAULTS
QQFSUPDM EQU   X'0C0C1A00'       SET UPDATE MODE
QQFSQUPD EQU   X'0C0C1A01'       QUERY UPDATE MODE
QQGSSEN  EQU   X'0C0C1B00'       SET MIXED STRING MODE
QQGSQSEN EQU   X'0C0C1B01'       QUERY MIX STRING MODE
QQCGLOAD EQU   X'0C0C1F00'       LOAD CGM
QQCGSAVE EQU   X'0C0C2000'       SAVE CGM
* GROUP - ADMMCQDD
QQASREAD EQU   X'0C100000'       READ
QQFSFRCE EQU   X'0C100001'       FORCE OUTPUT
QQFSCHEK EQU   X'0C100002'       PREPARE PS FOR OUTPUT
QQGSREAD EQU   X'0C100003'       AWAIT GRAPHICS INPUT
QQFSSAVE EQU   X'0C100004'       SAVE SCREEN
QQFSSHOW EQU   X'0C100005'       SHOW SCREEN
QQFSSHOR EQU   X'0C100007'       SHOW WITH REPLY
QQWSIO   EQU   X'0C100008'       WINDOWED DEVICE I/O
QQFSGETS EQU   X'0C100009'       FAM-4 BUFFER START
QQFSGET  EQU   X'0C10000A'       FAM-4 OUTPUT TO BUFFER
QQFSGETE EQU   X'0C10000B'       FSGET TERMINATE
QQDSFRCE EQU   X'0C10000C'       FAM-4 MEMBER TO PDS
* GROUP - ADMMCQDO
QQFSOPEN EQU   X'0C180000'       OPEN PRINTER DESTINTN
QQFSCOPY EQU   X'0C180001'       PAGE COPY
QQGSCOPY EQU   X'0C180002'       GRAPHICS COPY
QQFSLOG  EQU   X'0C180003'       LINE-BY-LINE OUTPUT
QQFSCLS  EQU   X'0C180004'       CLOSE PRINTER DESTINTN
QQFSLOGC EQU   X'0C180005'       LINE-BY-LINE O/P + CC
QQDSCOPY EQU   X'0C180008'       DEVICE COPY
* GROUP - ADMMCQDB
QQPTSCRT EQU   X'0C200000'       CREATE PTN SET.
QQPTSQRY EQU   X'0C200001'       QUERY PTN SET.
QQPTSSEL EQU   X'0C200100'       SELECT PTN SET.
QQPTSDEL EQU   X'0C200101'       DELETE PTN SET.
QQPTSQUN EQU   X'0C200102'       Q. UNIQUE PTN SET ID.
QQPTSSPP EQU   X'0C200300'       SET PARTN. PRIORITIES
QQPTSQPP EQU   X'0C200301'       QUERY PTN PRIORITIES
QQPTSQPI EQU   X'0C200400'       QUERY PARTITION IDS
QQPTSQPN EQU   X'0C200401'       QUERY PARTITION NOS
* GROUP - ADMMCQDE
QQPTNCRT EQU   X'0C240000'       CREATE PARTITION.
QQPTNQRY EQU   X'0C240001'       QUERY PARTITION.
QQPTNMOD EQU   X'0C240002'       MODIFY PARTITION.
QQPTNSEL EQU   X'0C240100'       SELECT PARTITION.
QQPTNDEL EQU   X'0C240101'       DELETE PARTITION.
QQPTNQUN EQU   X'0C240102'       Q. UNIQUE PTN ID.
* GROUP - ADMMCQDM
QQMSREAD EQU   X'0C280000'       READ WITH MAP
QQMSPCRT EQU   X'0C280100'       CREATE MAPPED PAGE
QQMSQGRP EQU   X'0C280300'       QUERY ABOUT A GROUP
QQMSQMAP EQU   X'0C280301'       QUERY ABOUT A MAP
QQMSQADS EQU   X'0C280302'       QUERY ABOUT AN ADS
QQMSQFIT EQU   X'0C280303'       QUERY WHETHER MAP FITS
QQMSQMOD EQU   X'0C280400'       QUERY MODIFIED M-FLDS
QQMSDFLD EQU   X'0C280500'       DEFINE MAPPED-FIELD
QQMSPUT  EQU   X'0C280501'       PUT MAPPED-FIELD DATA
QQMSGET  EQU   X'0C280502'       GET MAPPED-FIELD DATA
QQMSQFLD EQU   X'0C280503'       QUERY MAPPED-FIELD
QQMSCPOS EQU   X'0C280600'       SET CURSOR POSITION
QQMSQPOS EQU   X'0C280601'       QUERY CURSOR POSITION
* GROUP - ADMMCQDN
QQWSCRT  EQU   X'0C2C0000'       CREATE OPERATOR WINDOW
QQWSDEL  EQU   X'0C2C0100'       DELETE OPERATOR WINDOW
QQWSMOD  EQU   X'0C2C0200'       MODIFY OPERATOR WINDOW
QQWSQRY  EQU   X'0C2C0300'       QUERY OPERATOR WINDOW
QQWSQUN  EQU   X'0C2C0400'       QUERY OP WINDOW ID
QQWSQWI  EQU   X'0C2C0500'       QUERY OP WINDOW IDS
QQWSQWN  EQU   X'0C2C0600'       QUERY OP WINDOW NOS
QQWSQWP  EQU   X'0C2C0700'       QUERY WIN PRIORITIES
QQWSSEL  EQU   X'0C2C0800'       SELECT OPERATOR WINDOW
QQWSSWP  EQU   X'0C2C0900'       SET WINDOW PRIORITIES
* GROUP - ADMMCQD3
QQISFLD  EQU   X'0C300000'       DEFINE IMAGE FIELD
QQISQFLD EQU   X'0C300001'       QUERY IMAGE FIELD
QQISCTL  EQU   X'0C300002'       SET IMAGE QUALITY CTRL
QQISXCTL EQU   X'0C300003'       EXT IMAGE QUALITY CTRL
QQISESCA EQU   X'0C300B00'       ECHO SCANNER
QQISLDE  EQU   X'0C300C00'       LOAD EXT. R/O IMAGE
QQISQSCA EQU   X'0C300D00'       QRY IMAGE SCANNER DEV.
QQISQRES EQU   X'0C300E00'       QRY SUPPORTED RES
QQISENAB EQU   X'0C301200'       IMAGE CSR ENAB/DISAB
QQISQLOC EQU   X'0C301300'       QRY IMAGE LOCATOR CSR
QQISILOC EQU   X'0C301400'       IMAGE LOC. CSR. INIT
QQISQBOX EQU   X'0C301500'       IMAGE BOX CSR. QUERY
QQISIBOX EQU   X'0C301600'       IMAGE BOX CSR. INIT
QQISQFOR EQU   X'0C301700'       QUERY IMAGE FORMATS
QQISQCOM EQU   X'0C301800'       QUERY IMAGE COMPRESSNS
* GROUP - ADMMCQDQ
QQAPDEF  EQU   X'0C380000'       DEFINE FIELD LIST
QQAPDEL  EQU   X'0C380100'       DELETE FIELD LIST
QQAPMOD  EQU   X'0C380200'       MODIFY FIELD LIST
QQAPQIDS EQU   X'0C380300'       QUERY FIELD LIST IDS
QQAPQNUM EQU   X'0C380400'       QUERY FIELD LIST NOS
QQAPQRY  EQU   X'0C380500'       QUERY FIELD LIST
QQAPQSIZ EQU   X'0C380600'       QUERY FIELD LIST SIZE
QQAPQUID EQU   X'0C380700'       QUERY UNIQUE FL ID
* GROUP - ADMMCQB
QQCHLC   EQU   X'10020307'       LINE COLORS
* GROUP - ADMMCQIS
QQISSE   EQU   X'18000000'       INVOKE SYMBOL EDITOR
* GROUP - ADMMCQ3I
QQIMACRT EQU   X'3C010001'       CREATE AN IMAGE
QQIMAGID EQU   X'3C010002'       GET UNIQUE IMAGE ID
QQIMAQRY EQU   X'3C010004'       QUERY IMAGE ATTRIBUTES
QQIMARES EQU   X'3C010006'       CONVERT RESOLUTION
QQIMADEL EQU   X'3C010007'       DELETE SPECIFIED IMAGE
QQIMACLR EQU   X'3C010008'       CLEAR IMAGE RECTANGLE
QQIMATRM EQU   X'3C010009'       TRIM IMAGE TO RECTANGL
QQIMASAV EQU   X'3C01000A'       SAVE IMAGE IN LIBRARY
QQIMARST EQU   X'3C01000B'       RESTORE IMAGE
QQIMARF  EQU   X'3C01000C'       CHANGE RESOLUTION FLAG
QQIMAPTS EQU   X'3C010011'       START DATA ENTRY
QQIMAPT  EQU   X'3C010012'       ENTER DATA IN IMAGE
QQIMAPTE EQU   X'3C010013'       END DATA ENTRY
QQIMAGTS EQU   X'3C010014'       START DATA RETRIEVAL
QQIMAGT  EQU   X'3C010015'       GET IMAGE DATA
QQIMAGTE EQU   X'3C010016'       END DATA RETRIEVAL
QQIMXFER EQU   X'3C010017'       IMAGE DATA TRANSFER
* GROUP - ADMMCQ3P
QQIMPGID EQU   X'3C030001'       GET UNIQUE PROJ ID
QQIMPCRT EQU   X'3C030003'       CREATE A PROJECTION
QQIMPDEL EQU   X'3C030004'       DELETE SPECIFIED PROJ
QQIMPSAV EQU   X'3C030005'       SAVE IMAGE PROJECTION
QQIMPRST EQU   X'3C030006'       RESTORE PROJECTION
QQIMREX  EQU   X'3C030101'       DEFINE IMAGE RECTANGLE
QQIMREXR EQU   X'3C030102'       DEFINE IMAGE RECTANGLE
QQIMRPL  EQU   X'3C030103'       DEFINE PLACE POSITION
QQIMRSCL EQU   X'3C030105'       SCALE IMAGE RECTANGLE
QQIMRRAL EQU   X'3C030106'       SET SCALING ALGORITHM
QQIMRORN EQU   X'3C030107'       ORIENTATE SUB-IMAGE
QQIMRREF EQU   X'3C030108'       REFLRCT SUB-IMAGE
QQIMRNEG EQU   X'3C030109'       NEGATE IMAGE PIXELS
QQIMRCVB EQU   X'3C030201'       BI-LEVEL CONVERSION
QQIMRBRI EQU   X'3C030202'       BRIGHTNESS CONVERSION
QQIMRCON EQU   X'3C030203'       CONTRAST CONVERSION
QQIMRPLR EQU   X'3C030204'       DEFINE PLACE POSITION
* GROUP - ADMMCQ4
QQCDPU   EQU   X'40000000'       COMP DOC PRINT UTIL
