SVMDSSU  TITLE '  FRONT END TO ADRDSSU FOR VM VOLUME BACKUPS  '
**********************************************************************
*                                                                    *
*                                                                    *
*    THE PURPOSE OF THIS PROGRAM IS TO ZAP A SELECTED UCB IN THE     *
*    NUCLEUS WHICH CORRESPONDS TO AN ONLINE VM DISK TO ALLOW THE     *
*    DISK TO BE PROCESSED BY OS DISK UTILITIES.  THE UPDATED FIELD   *
*    IS UCBVTOC WHICH CONTAINS THE RELATIVE (FROM START OF VOLUME)   *
*    TRACK ADDRESS OF THE VTOC.  THIS IS CHANGED TO POINT TO THE     *
*    LAST TRACK OF THE VOLUME WHICH SHOULD CONTAIN A PRE-FORMATTED   *
*    ONE TRACK OS VTOC.  THE VOLUME LABEL ON TRACK ZERO WHICH        *
*    CONTAINS THE PERMANENT VTOC POINTER IS NOT ALTERED.  THE        *
*    VOLUME ALLOCATED TO DDNAME 'VMDAUNIT' WILL BE PROCESSED.        *
*    ALLOCATION ENSURES THAT THE VOLUME IS ONLINE AND HAS A          *
*    STANDARD VOLUME LABEL.  THE SELECTED UCB IS CHECKED FOR         *
*    STANDARD FORMAT, DIRECT ACCESS DEVICE CLASS, KNOWN DEVICE       *
*    TYPE, AND A UCBVTOC VALUE OF X'00000500'.  AN OS FORMAT VTOC    *
*    POINTER CAN POINT TO RECORD ONE OF ANY TRACK EXCEPT TRACK ZERO. *
*                                                                    *
*    IF UCBVTOC IS UPDATED IN A SHARED DASD UCB THEN THE SHARED      *
*    BIT (DA2HEX20 OF UCBTBYT2) IS RESET TO PREVENT VM HANGING       *
*    FOR THE DURATION OF A DISK BACKUP.                              *
*                                                                    *
*    THIS PROGRAM IS DESIGNED TO BE SPECIFIED IN JOB STREAMS         *
*    INSTEAD OF ADRDSSU WHENEVER A DISK BACKUP OF A VM VOLUME        *
*    IS TO BE TAKEN USING DATA SET SERVICES UNDER OS/VS.  ONLY       *
*    THE LINK MACRO NEED BE CHANGED FOR THIS PROGRAM TO BECOME       *
*    THE FRONT END OF OTHER OS DISK UTILITIES (IEHDASDR, ETC.).      *
*    IT IS ASSUMED THAT THE VM VOLUME HAS A PHONY BUT VALID OS       *
*    VTOC SITTING UNUSED ON ITS LAST TRACK (FORGETTING ALTERNATES).  *
*    REMEMBER THAT DEVICE SUPPORT FACILTIES UNDER VM USES THE        *
*    FORMAT-4 DSCB ON TRACK ZERO RECORD 5 TO KEEP TRACK (PUN) OF     *
*    ALTERNATE TRACK USAGE.  THE FORMAT-5 DSCB ON TRACK ZERO RECORD  *
*    6 INDICATES THAT THERE IS NO FREE SPACE.                        *
*                                                                    *
*    SAMPLE JCL -                                                    *
*                //BKVMDISK JOB                                      *
*                //STEP1   EXEC PGM=SVMDSSU,REGION=2048K,TIME=20     *
*                //SYSPRINT DD  SYSOUT=*                             *
*                //VMDAUNIT DD  VOL=SER=VMDISK,UNIT=3350,DISP=OLD    *
*                //DDTAPE   DD  DSN=BACKUP,DISP=(,CATLG,DELETE),     *
*                //             UNIT=TAPE                            *
*                //SYSIN    DD  *                                    *
*                  DUMP FULL INDDNAME(VMDAUNIT) OUTDDNAME(DDTAPE) -  *
*                    ALLDATA(*) ALLEXCP OPTIMIZE(3)                  *
*                /*                                                  *
*                //                                                  *
*                                                                    *
*    NOTE - THE DISK DDNAME MUST BE VMDAUNIT.                        *
*                                                                    *
*    WRITTEN - 24 JULY 1985  BY GREG PRICE, SECV.                    *
*                                                                    *
**********************************************************************
         SPACE 3
**********************************************************************
*    SUPPORTED DEVICES ARE 3330, 3330-1, 3350, 3380.                 *
*                                                                    *
*    THIS PROGRAM MUST RUN APF AUTHORIZED BECAUSE KEY ZERO IS        *
*    REQUIRED TO ALTER THE UNIT CONTROL BLOCK.  MODESET IS ISSUED    *
*    AFTER ALL CHECKS HAVE BEEN VERIFIED.  THE UCB IS MODIFIED IN    *
*    KEY ZERO, ADRDSSU IS LINKED IN PROBLEM STATE AND USER KEY,      *
*    THEN THE UCB IS RESTORED IN KEY ZERO.                           *
*                                                                    *
*    RETURN CODES -                                                  *
*                                                                    *
*               0 - ADRDSSU RETURNED A CONDITION CODE OF 0           *
*               4 - ADRDSSU RETURNED A CONDITION CODE OF 4           *
*               8 - ADRDSSU RETURNED A CONDITION CODE OF 8           *
*              12 - ADRDSSU RETURNED A CONDITION CODE OF 12          *
*              16 - ADRDSSU RETURNED A CONDITION CODE OF 16          *
*              20 - INPUT DEVICE TYPE SPECIFIED BY 'VMDAUNIT'        *
*                   IS NOT SUPPORTED                                 *
*              24 - DDNAME 'VMDAUNIT' NOT DEFINED                    *
*                                                                    *
*    PROGRAMMED ABEND S0C1 -                                         *
*              NON-STANDARD UCB ENCOUNTERED, OR                      *
*              BYTE 4 OF UCBVTOC WAS NOT A NULL,                     *
*              OR SOMETHING FUNNY HAPPENED AFTER                     *
*              ADRDSSU RETURNED CONTROL.                             *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
*        INITIALIZE                                                  *
*                                                                    *
**********************************************************************
         SPACE
SVMDSSU  CSECT
         SAVE  (14,12)
         LR    11,15            (YOU KNOW THE ROUTINE.... *SIGH*
         USING SVMDSSU,11
         ST    1,PARMSAVE        SAVE ANY PARAMETER FOR ADRDSSU....
         LA    4,SVMSAVEA
         ST    4,8(0,13)         FORWARD POINTER....
         ST    13,SVMSAVEA+4     BACKWARD POINTER....  *YAWN*
         LA    13,SVMSAVEA       POINT TO NEW SZZZZZZZZZZ....)
         SPACE 2
**********************************************************************
*                                                                    *
*        SEARCH FOR 'VMDAUNIT' DD CARD                               *
*                                                                    *
**********************************************************************
         SPACE
         L     4,X'21C'         GET POINTER TO CURRENT TCB.
         L     4,12(0,4)        POINT TO TIOT.
         LA    4,24(0,4)        POINT TO TIOELNGH.
         SR    5,5              CLEAR REGISTER.
         SPACE
CHKDDNAM CLC   4(8,4),VMDAUNIT  IS THIS THE RIGHT DDNAME?
         BE    CHECKUCB         YES, FILE EXISTS SO VERIFY THE UCB.
         IC    5,0(0,4)         NO, GET TIOT ENTRY LENGTH.
         AR    4,5              POINT TO NEXT TIOT ENTRY.
         CLI   0(4),0           ZERO LENGTH ENTRY?
         BNE   CHKDDNAM         NO, CHECK OUT THIS ENTRY.
         WTO   'SVM001W - VMDAUNIT DDNAME NOT DEFINED - NOTHING DONE', X
               ROUTCDE=(11)
         LA    15,24            RETURN CODE IS 24.
         B     SVMQQUIT         EXIT.
         EJECT
**********************************************************************
*                                                                    *
*        VERIFY UNIT CONTROL BLOCK (DEVICE CLASS AND TYPE)           *
*                                                                    *
**********************************************************************
         SPACE
CHECKUCB ICM   5,7,17(4)        GET UCB ADDRESS.
         CLI   2(5),X'FF'       IS THIS A STANDARD UCB?
         BE    CHEKCLAS         YES.
SVMABEND WTO   'SVM002W - VMDAUNIT IS NON-STANDARD - NOTHING DONE',    X
               ROUTCDE=(11)
         WTO   'SVM003T - ABENDING - R4 => TIOT ENTRY - R5 => UCB',    X
               ROUTCDE=(11)
         DC    H'0'             ABEND S0C1.
*        B     SVMQQUIT         EXIT.
         SPACE
CHEKCLAS CLI   18(5),X'20'      IS THIS A DASD UNIT?
         BE    CHEKTYPE         YES.
         MVC   NOTDAMSG+18(3),13(5)     SHOW UNIT ADDRESS.
NOTDAMSG WTO   'SVM004W - CUA IS NOT A DASD UNIT - NOTHING DONE',      X
               ROUTCDE=(11)
         LA    15,20            RETURN CODE IS 20.
         B     SVMQQUIT         EXIT.
         SPACE
CHEKTYPE MVC   LASTRACK,LAST3380
         CLI   19(5),X'0E'      IS THIS A 3380 UNIT?
         BE    CHEKTTR0         YES.
         MVC   LASTRACK,LAST3350
         CLI   19(5),X'0B'      IS THIS A 3350 UNIT?
         BE    CHEKTTR0         YES.
         MVC   LASTRACK,LAST3331
         CLI   19(5),X'0D'      IS THIS A 3330-1 UNIT?
         BE    CHEKTTR0         YES.
         MVC   LASTRACK,LAST3330
         CLI   19(5),X'09'      IS THIS A 3330 UNIT?
         BE    CHEKTTR0         YES.
         XC    LASTRACK,LASTRACK      JUST TO BE TIDY.
         MVC   UNKNTYPE+18(3),13(5)   SHOW UNIT ADDRESS.
         MVC   UNKNTYPE+22(6),28(5)   SHOW VOLUME SERIAL NUMBER.
         UNPK  UNKNTYPE+34(3),19(2,5) SHOW DASD TYPE (UCBTBYT4).
         TR    UNKNTYPE+34(2),HEX-C'0'
         MVI   UNKNTYPE+36,C' '       ERASE RUBBISH.
UNKNTYPE WTO   'SVM005W - CUA VOLSER TYPE XX NOT KNOWN - NOTHING DONE',X
               ROUTCDE=(11)
         LA    15,20            RETURN CODE IS 20.
         B     SVMQQUIT         EXIT.
         EJECT
**********************************************************************
*                                                                    *
*        VERIFY UNIT CONTROL BLOCK (DASD SPECIFICS)                  *
*                                                                    *
**********************************************************************
         SPACE
CHEKTTR0 EQU   *
         CLI   27(5),0          IS 0 PART OF TTR0 ZERO?
         BNE   SVMABEND         NO, UCB ERROR SO GO AND ABEND.
         SPACE
         ICM   2,3,24(5)        GET TT PART OF TTR0 IN UCBVTOC.
         BZ    NOTOSVOL         VTOC STARTS ON TRACK 0, SO NOT OS VTOC.
         CLI   26(5),1          DOES VTOC BEGIN AT FIRST RECORD OF TRK?
         BNE   NOTVMVOL         NO, SO NOT AN OS VTOC, NOR A VM ONE.
         MVC   NOTVMMSG+18(3),13(5)    SHOW UNIT ADDRESS.
         MVC   NOTVMMSG+22(6),28(5)    SHOW VOLUME SERIAL NUMBER.
NOTVMMSG WTO   'SVM006W - CUA VOLSER NOT RECOGNIZED AS VM - NOTHING DONX
               E',ROUTCDE=(11)
         MVC   OPDSKMSG+18(3),13(5)    SHOW UNIT ADDRESS.
         MVC   OPDSKMSG+22(6),28(5)    SHOW VOLUME SERIAL NUMBER.
OPDSKMSG WTO   'SVM007A - CUA VOLSER APPARENTLY NOT A VM VOLUME.',     X
               ROUTCDE=(4,11)
         WTO   'SVM007A - (CONT) IF IT IS, VARY OFFLINE AND REMOUNT.', X
               ROUTCDE=(4,11)
         LA    15,20            RETURN CODE IS 20.
         B     SVMQQUIT         EXIT.
         SPACE
NOTOSVOL CLI   26(5),5          DOES VTOC BEGIN AT FIFTH RECORD OF TRK?
         BE    ISVMDISK         YES, AS EXPECTED FOR VM VOLUME.
NOTVMVOL MVC   NOTOSMSG+18(3),13(5)    SHOW UNIT ADDRESS.
         MVC   NOTOSMSG+22(6),28(5)    SHOW VOLUME SERIAL NUMBER.
NOTOSMSG WTO   'SVM008U - CUA VOLSER NOT OS NOR VM DISK - NOTHING DONE'X
               ,ROUTCDE=(2,4,6,9,11)   NOW THAT'S A FUNNY THING....
         LA    15,20            RETURN CODE IS 20.
         B     SVMQQUIT         EXIT.
         EJECT
**********************************************************************
*                                                                    *
*        UPDATE UNIT CONTROL BLOCK FOR ADRDSSU PROCESSING            *
*                                                                    *
**********************************************************************
         SPACE
ISVMDISK ST    5,UCBADDR        SAVE UPDATED UCB ADDRESS.
         MODESET KEY=ZERO       GET INTO STORAGE KEY ZERO.
         MVC   24(2,5),LASTRACK REPLACE TT PART OF UCBVTOC.
         MVI   26(5),1          REPLACE R PART OF UCBVTOC.
         TM    17(5),X'20'      IS THIS DASD SHARED?
         BZ    USERKEY          NO.
         SPACE
         XI    17(5),X'20'      RESET SHARED BIT
         MVI   SHARFLAG,X'20'         AND REMEMBER THAT WE IT.
USERKEY  MODESET KEY=NZERO      RETURN TO USER KEY.
         EJECT
**********************************************************************
*                                                                    *
*        LINK TO ADRDSSU                                             *
*                                                                    *
**********************************************************************
         SPACE
         L     1,PARMSAVE       RESTORE ANY PROSPECTIVE PARAMETER.
         LINK  EP=ADRDSSU       CALL DATA SET SERVICES.
         SPACE
         LR    7,15             SAVE RETURN CODE FROM DSS.
         C     5,UCBADDR        ADDRESSIBILITY VALIDITY CHECK.
         BE    RESTRUCB         NO WORRIES, MATE.  SHE'S APPLES.
         SPACE
         WTO   'SVM009U - POST DATA SET SERVICES FATAL ERROR - ABENDINGX
               ',ROUTCDE=(2,4,9,11)         O-OH... CALL OUT THE GUARD.
         LR    15,7             ABEND REASON CODE = ADRDSSU COND CODE.
         DC    H'0'             ABEND S0C1.
         EJECT
**********************************************************************
*                                                                    *
*        RESTORE UNIT CONTROL BLOCK PRIOR TO COMPLETION              *
*                                                                    *
**********************************************************************
         SPACE
RESTRUCB MODESET KEY=ZERO       GET INTO STORAGE KEY ZERO.
         SR    6,6              GET ZERO.
         ST    6,24(0,5)        ZERO UCBVTOC.
         MVI   26(5),5          COMPLETE UCBVTOC RESTORATION.
         OC    17(1,5),SHARFLAG RESTORE SHARED BIT TO ORIGINAL VALUE.
         MODESET KEY=NZERO      RETURN TO USER KEY.
         SPACE 2
**********************************************************************
*                                                                    *
*        RESTORE REGISTERS AND ISSUE COMPLETION CODE                 *
*                                                                    *
**********************************************************************
         SPACE
         LR    15,7             STEP COND CODE = ADRDSSU RETURN CODE.
SVMQQUIT L     13,SVMSAVEA+4    POINT TO CALLER'S SAVE AREA.
         SPACE
         RETURN (14,12),RC=(15) GO AWAY.
         TITLE '  SVMDSSU  -  CONSTANTS AND SAVE AREAS  '
SVMSAVEA DC    18F'0'           SAVE AREA FOR ADRDSSU.
PARMSAVE DC    F'0'             PARAMETER ADDRESS FOR ADRDSSU.
UCBADDR  DC    F'0'             SAVE AREA FOR UCB ADDRESS.
LASTRACK DC    H'0'             TEMPORARY VALUE FOR UCBVTOC (TT PART).
LAST3330 DC    AL2(19*404-1)    LAST TRACK OF A 3330 MODEL 1.
LAST3331 DC    AL2(19*808-1)    LAST TRACK OF A 3330 MODEL 11.
LAST3350 DC    AL2(30*555-1)    LAST TRACK OF A 3350.
LAST3380 DC    AL2(15*885-1)    LAST TRACK OF A 3380.
VMDAUNIT DC    C'VMDAUNIT'      INPUT DDNAME FOR THIS PROGRAM.
HEX      DC    C'0123456789ABCDEF'
SHARFLAG DC    X'00'            SHARED BIT RESET FLAG.
         SPACE 3
         END
