PROC 1 DSN VOLSER(REVIEW) UNIT(TAPE) LBL(BLP) FSN(1) PTF
CONTROL NOFLUSH
/*
/*  CLIST TO SEE WHAT IS ON A TAPE VOLUME.
/*
/*  UNLESS AUTOMATIC VOLUME RECOGNITION IS EXPLOITED, USERS WITHOUT THE
/*  MOUNT ATTRIBUTE WILL NOT GET VERY FAR.  CATALOGUED TAPE DATA SETS
/*  CAN BE REVIEWED DIRECTLY WITHOUT THIS CLIST.  UNCATALOGUED
/*  STANDARD LABELLED DATA SETS CAN ALSO BE REVIEWED DIRECTLY WITHOUT
/*  THIS CLIST BY SPECIFYING UNIT AND VOLUME ON THE COMMAND, ONLY AS
/*  LONG AS THE FILE SEQUENCE NUMBER IS ONE (1).  THIS CLIST CAN BE
/*  USED TO REVIEW TAPE LABELS AS WELL AS TAPE DATA FILES.
/*
/*  IN THE EVENT OF ANY ABNORMAL TERMINATION WHICH FLUSHES THE CLIST,
/*  REVFILE WILL NOT BE FREED, SO PLEASE ENSURE THAT YOU DO NOT
/*  UNNECESSARILY HOG A TAPE DRIVE.
/*
IF '&LBL' = 'SL' OR '&LBL' = 'AL' THEN GOTO NOATTR
IF &PTF =   THEN ATTR REVATTR DSORG(PS) BLKSIZE(32760) RECFM(U)
   ELSE ATTR REVATTR DSORG(PS) BLKSIZE(7200) RECFM(F B) LRECL(80)
ALLOC FILE(REVFILE) DA(&DSN) USING(REVATTR) UNIT(&UNIT) LABEL(&LBL)  -
      VOLUME(&VOLSER) POSITION(&FSN) SHR REUSE
FREE ATTR(REVATTR)
GOTO REVIT
                           /* USE DCB INFORMATION FROM TAPE LABELS. */
NOATTR:  -
ALLOC FILE(REVFILE) DA(&DSN) UNIT(&UNIT) LABEL(&LBL)  -
      VOLUME(&VOLSER) POSITION(&FSN) SHR REUSE
REVIT: REVIEW REVFILE FILE
FREE FILE(REVFILE)
END
