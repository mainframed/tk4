PROC 0 TRACE
GLOBAL MSG PREF CON KEY RMT
IF  &TRACE NE THEN CONTROL SYMLIST CONLIST LIST NOFLUSH NOMSG NOPROMPT
              ELSE CONTROL NOFLUSH NOMSG   NOPROMPT
SET &LASTCD=0
IF &STR(&PREF)=&STR(*) THEN SET &PREF=&SYSUID
ISPEXEC CONTROL ERRORS  RETURN
ISPEXEC TBVCLEAR $LCTABL
ISPEXEC SELECT PGM($LCSPF) PARM(&PREF)
IF &LASTCC GT 0 THEN DO
QZ:SET &SELECT=
   SET &MSG=&STR(MSG(TSDMU029))
   GOTO OUTOFIT
   END
CARRYON: +
SET &MSG=&STR()
SET &PARM=COMP
SET &LB='('
SET &LB=&STR(&SUBSTR(2:2,&LB))
SET &RB=')'
SET &RB=&STR(&SUBSTR(2:2,&RB))
SET &LASTCD=0
SET &LOSTCC=0
SET    MSG=
SET   MSG2=
SET COMMAND=
SET  SELECT=
SET  MSG=
SET MORE=NO
SET $LCVOL=
DISPLAY:+
        SET &SELECT=
        SET &MEMBER=
        ISPEXEC CONTROL DISPLAY REFRESH
        ISPEXEC TBDISPL $LCTABL PANEL(TSDPUT30) &MSG
        SET &LOSTCC=&LASTCC
        IF &LOSTCC GT 8 THEN GOTO QZ
   ELSE IF &LOSTCC EQ 8 THEN GOTO ENDOFIT
   ELSE IF &LOSTCC EQ 4 THEN SET &MORE=YES
   ELSE                      SET &MORE=NO
        IF &STR(&NRSTR(&ZCMD))  NE THEN DO
           SET &SYSDVAL=&STR(&NRSTR(&ZCMD))
           READDVAL COMMAND ARGUMENT
           IF &STR(&COMMAND) EQ L THEN DO
              SET &$LCDSN=&STR(')&STR(&NRSTR(&PREF..&ARGUMENT))&STR(*)
              SET &$LCVOL=
              ISPEXEC TBSCAN $LCTABL ARGLIST($LCDSN)
              IF &LASTCC NE 0 THEN SET &MSG=&STR(MSG(TSDMU028))
                              ELSE SET &MSG=
              END
           GOTO DISPLAY
           END
SET     &MSG=&STR()
SET     &DSNC=&STR()
SET     &DSND=&STR()
SET     &VOLB=&STR()
SET     &LASTCD=0
INCR:   +
IF &STR(&NRSTR(&SELECT)) LT A OR +
   &STR(&NRSTR(&SELECT)) GT Z THEN GOTO DISPLAY
SET     &OPT=&SELECT
IF      &OPT EQ THEN GOTO SKIP
        SET &DSN=&$LCDSN
        SET &DSNB=&$LCDSN
        SET &VOLB=&$LCVOL
        SET &MEM=&MEMBER
        IF  &MEM NE THEN DO
        IF  &SUBSTR(1:1,&DSN) EQ &STR(') THEN SET &DSNC=+
            &STR(&SUBSTR(1:&LENGTH(&DSN)-1,&DSN)&LB&MEM&RB.')
        ELSE                                 SET &DSNC=+
            &STR(&SUBSTR(1:&LENGTH(&DSN),&DSN)&LB&MEM&RB)
        END
        ISPEXEC TBGET $LCTABL
        IF (&OPT EQ D OR &OPT EQ U OR &OPT EQ Y) AND &CON EQ Y +
        THEN DO
             ISPEXEC DISPLAY PANEL(TSDPUT37)
             IF &LASTCC GE 8 THEN GOTO DISPLAY
             END
        GOTO LAB&OPT
SKIP:   +
IF &LASTCD GT 4 THEN GOTO DISPLAY
IF &MORE=NO  THEN GOTO DISPLAY
        ISPEXEC TBDISPL $LCTABL
        SET &LOSTCC=&LASTCC
        IF &LOSTCC EQ 4 THEN SET &MORE=YES
                        ELSE SET &MORE=NO
        GOTO INCR
LABC:   ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        IF  &VOLB NE THEN SET &VOLB=&STR(VOLUME(&VOLB))
        IF  &DSNC NE THEN SET &DSN=&STR(&DSNC)
        ALLOC F(OLD) DA(&DSN)  SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        ALLOC F(NEW) DA(&DSNB) &VOLB UNIT(SYSALLDA) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CLEAR
        INVOKE COMPAREB
        GOTO SKIP
LABS:   ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        IF &STR(&STRG) EQ THEN DO
                               SET &MSG=&STR(MSG(TSDMU018))
                               GOTO DISPLAY
                               END
        IF &SELT NE THEN SET &SXXX=&STR(,%SEL,&SELT)
        ELSE             SET &SXXX=&STR()
        IF &EXCL NE THEN SET &EXXX=&STR(,%EXC,&EXCL)
        ELSE             SET &EXXX=&STR()
        ALLOC F(PDSDD) DA(&DSN) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CLEAR
        $ SCANX '&STRG&SXXX&EXXX'
        FREE F(PDSDD)
        SET &SXXX=&STR()
        SET &EXXX=&STR()
        GOTO SKIP
LABL:   IF &SUBSTR(1:1,&DSN) EQ &STR(') THEN +
             ISPEXEC SELECT PGM($LISTX) +
                                PARM(&SUBSTR(2:&LENGTH(&DSN)-1,&DSN))
        ELSE ISPEXEC SELECT PGM($LISTX) +
                                PARM(&SYSUID..&DSN)
        GOTO SKIP
LABM:   ALLOC F(IN)  DA(&DSN) SHR REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        ALLOC F(OUT) DA(*)    SHR REUS
        $ STATS
        GOTO SKIP
LABZ:   TERM SCRSIZE(24,80)
        ZAP &DSN NOLOG
        GOTO SKIP
LABF:   CONTROL MSG
        RLSE  &DSN
        CONTROL NOMSG
        GOTO SKIP
LABP:   ISPEXEC SELECT CMD(PDS &DSN)
   /*   CONTROL MSG */
   /*   PDSNSPF &DSN */
   /*   TERMIN END EN */
   /*   DATA */
   /*   END */
   /*   ENDDATA */
   /*   CONTROL NOMSG */
        GOTO SKIP
LABR:   ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        IF  &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        RENAME   &DSN  &DSNB
        SET &LASTCD=&LASTCC
        IF  &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU002))
        ELSE                  SET &MSG=&STR(MSG(TSDMU004))
        IF &LASTCD EQ 0 AND &MEM EQ THEN DO
                   ISPEXEC TBDELETE $LCTABL
                   SET &$LCDSN=&DSNB
                   ISPEXEC TBADD $LCTABL
                   END
        GOTO SKIP
LABK:   ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        IF  &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        COPY     &DSN  &DSNB NONUM
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU016))
        ELSE                 SET &MSG=&STR(MSG(TSDMU017))
        IF &LASTCD EQ 0 THEN DO
                   SET &$LCDSN=&DSNB
                   ISPEXEC TBADD  $LCTABL
                   END
        GOTO SKIP
LABA:   ISPEXEC VGET (VLM BLK LRC REC DSO PRM SEC DIR TYP) PROFILE
        ISPEXEC DISPLAY PANEL(TSDPUT36)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        SET &VOLB=
        IF &VLM NE THEN SET &VOLB=&STR(VOL(&VLM))
        IF &DSNB EQ THEN GOTO ALLOCER
        FREE F(ATTRTSD)
        ATTR ATTRTSD BLKSIZE(&BLK) LRECL(&LRC) RECFM(&RCF) DSORG(&DSO)
        ALLOC DA(&DSNB) NEW SPACE(&PRM,&SEC) &VOLB +
              DIR(&DIR) &TYP USING(ATTRTSD) REUS
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN DO
                   SET &$LCDSN=&DSNB
                   ISPEXEC TBADD  $LCTABL
                   END
        ELSE GOTO ALLOCER
        SET &MSG=&STR(MSG(TSDMU012))
        FREE DA(&DSN)
        ISPEXEC VPUT (VLM BLK LRC REC DSO PRM SEC DIR TYP) PROFILE
        GOTO SKIP
LABY:   PDSNSPF &DSN FIXPDS RESET
        GOTO SKIP
     /* SET &PARM=RESET  (- FOR QUICK COMPRESS, THEN DROP THRU TO LABQ)
LABQ:   ALLOC F(SYSPRINT) DUMMY    REUS
        ALLOC F(COMPRESS) DA(&DSN) REUS SHR
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        CALL 'TSO.LINKLIB(SRCOMPRS)' '&PARM'
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0  THEN SET &MSG=&STR(MSG(TSDMU021))
        ELSE                  SET &MSG=&STR(MSG(TSDMU020))
        SET &PARM=COMP
        ALLOC F(SYSPRINT) DA(*) REUS
        GOTO SKIP
LABG:   SET &VOLB=&$LCVOL
        ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        IF  &VOLB NE THEN SET &VOLL=&STR(VOLUME(&VOLB))
                     ELSE SET &VOLL=&STR(VOLUME(&$LCVOL))
        ALLOC DA(&$LCDSN) &VOLL CATALOG
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN GOTO ALLOCER
        IF &VOLB NE &$LCVOL THEN DO
           SET &$LCVOL=&VOLB
           ISPEXEC TBPUT $LCTABL
           END
        FREE DA(&DSN)
        SET &MSG=&STR(MSG(TSDMU015))
        GOTO SKIP
LABE:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        ISPEXEC EDREC INIT
LABEREC:ISPEXEC EDREC QUERY
        IF &LASTCC NE 4 THEN GOTO LABEDIT
        ISPEXEC EDREC PROCESS
        GOTO LABEREC
LABEDIT:ISPEXEC  EDIT   DATASET(&DSN)
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0  THEN SET &MSG=&STR(MSG(TSDMU001))
        ELSE IF &LASTCD EQ 20 THEN SET &MSG=&STR(MSG(TSDMU010))
        GOTO SKIP
LABB:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        ISPEXEC  BROWSE DATASET(&DSN)
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 20 THEN SET &MSG=&STR(MSG(TSDMU011))
        GOTO SKIP
LABU:   DELETE &DSN NOSCRATCH
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU013))
        ELSE                 SET &MSG=&STR(MSG(TSDMU014))
        GOTO SKIP
LABH:   SHOWDS &DSN EXT      /* NO 'DIR' */
        SET &LASTCD=&LASTCC
        IF &LASTCD NE 0 THEN SET &MSG=&STR(MSG(TSDMU025))
        GOTO SKIP
LABT:   ISPEXEC TBDELETE $LCTABL
        GOTO SKIP
LABD:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        DELETE   &DSN
        SET &LASTCD=&LASTCC
        IF &LASTCD EQ 0 THEN SET &MSG=&STR(MSG(TSDMU003))
        ELSE                 SET &MSG=&STR(MSG(TSDMU005))
        IF &LASTCD EQ 0 AND &MEM EQ THEN ISPEXEC TBDELETE $LCTABL
        GOTO SKIP
LABN:   ISPEXEC DISPLAY PANEL(TSDPUT35)
        IF &LASTCC GE 8 THEN GOTO DISPLAY
        SET &$LCDSN=&DSNB
        SET &$LCVOL=&VOLB
        ISPEXEC TBPUT  $LCTABL
        IF &LASTCC NE 0 THEN ISPEXEC TBADD  $LCTABL
        GOTO SKIP
LABO:   GOTO DISPLAY
LABI:   IF &DSNC NE THEN SET &DSN=&STR(&DSNC)
        REVIEW &DSN
        GOTO SKIP
LABJ:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF &RMT NE THEN SET &REMOTE=&STR(DEST(&RMT))
                   ELSE SET &REMOTE=&STR()
        IF  &VOLB NE THEN SET &VOLB=&STR(VOLUME(&VOLB))
        PRINTOFF &DSN &REMOTE &VOLB
        SET &LASTCD=&LASTCC
        GOTO DISPLAY
LABX:   IF &STR(&DSNC) NE THEN SET &DSN=&STR(&DSNC)
        IF SUBSTR(1:1,&DSN) NE &STR(') THEN SET &DSN=&STR((&MEM))
        EXEC &DSN
        SET &LASTCD=&LASTCC
        GOTO DISPLAY
LABW:   GOTO LABV
LABV:   IF &KEY NE THEN SET &KKK=&STR(K(&KEY))
                   ELSE SET &KKK=&STR()
        IF &DSNC NE THEN SET &DSN=&STR(&DSNC)
        IF &OPT EQ V THEN ENCRYPT &DSN &KKK
                     ELSE DECRYPT &DSN &KKK
        GOTO DISPLAY
ALLOCER:SET &MSG=&STR(MSG(TSDMU009))
        GOTO DISPLAY
ENDOFIT:SET &MSG=
OUTOFIT:ISPEXEC TBEND   $LCTABL
        FREE    F(OLD NEW IN OUT PDSDD COMPRESS)
