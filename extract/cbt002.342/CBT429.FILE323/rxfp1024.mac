/*********************************REXX*********************************/
/*                                                                    */
/* Description: Communicate with OPC/ESA address space                */
/*     Package: REXX Function Pack (1)                                */
/*      Author: Gerard J Nicol                                        */
/*                                                                    */
/*********************************REXX*********************************/

parse upper arg ADID
if ADID="" then do
   say "No Application-ID specified as argument"
   exit(10)
end

/* Allocate required OPC/ESA Datasets --------------------------------*/
Address TSO
"ALLOC F(EQQMLIB) DA('OPCESA.INST.SEQQMSG0') SHR REUS"
"ALLOC F(EQQMLOG) Ds(*)  Reus"
"ALLOC F(EQQDUMP) SYSOUT Reus"

/* Set Default Values for PIF ----------------------------------------*/
DAR=Left('00000000'x,4)
ARN=Left(' ',8)
ARV=Left('00000000'x,4)
COM=Left('00000000'x,4)
ORC=Left('00000099'x,4)
OPL="ACT RES DAR ARN ARV COM ORC"

/* Initialize communications with OPC/ESA ----------------------------*/
ACT=Left('INIT',8)
RES=Left('OPCE',8)
Address LINKPGM "EQQYCOM" OPL

/* List OPC/ESA Current Plan Occurences ------------------------------*/
ACT=Left("LIST",8)
RES=Left("CPOC",8)
ARN=Left("ADID",16)
ARV=MALLOC(16)
null=storage(C2X(ARV),16,left(ADID,16))
Address LINKPGM "EQQYCOM" OPL

ACT=Left("SELECT",8)
ARN=Left('NEXT',16)

Say "Application-ID"
Say "----------------"

do I=1 while ORC="00000000"x
    say SegBody(DAR,1,16)
    Address LINKPGM "EQQYCOM" OPL
end

say "Applications that match argument=" I-1

null=MFREE(ARV)
ACT=Left("TERM",8)
Address LINKPGM "EQQYCOM" OPL

exit(0)

/* Functions ---------------------------------------------------------*/
SegHead: return storage(D2X(C2D(Arg(1))+(Arg(2)*16)-16),16)
SegName: return substr(SegHead(Arg(1),Arg(2)),1,8)
SegOSet: return C2D(substr(SegHead(Arg(1),Arg(2)),9,4))
SegBody: return storage(D2X(C2D(arg(1))+SegOSet(arg(1),arg(2))),arg(3))
/* Functions ---------------------------------------------------------*/

exit(0)
