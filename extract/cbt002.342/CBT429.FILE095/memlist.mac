 ISREDIT MACRO (FROM,TO)
 ISREDIT (WORKDSN) = DATASET
 SET &Z9 = Z9999999
 IF &FROM = ? THEN DO
 ISPEXEC DISPLAY PANEL(MEMLIST)
 EXIT
 END
 IF &FROM ^= THEN SET &RT = &STR(RANGE &FROM TO &TO)
 IF &TO = THEN SET &TO = &Z9
 SET &TARGET1 = &FROM
 SET T1 = &LENGTH(&TARGET1)
 IF &FROM =  THEN SET &T1 = 8
 SET &TARGET2 = &TO
 SET T2 = &LENGTH(&TARGET2)
SET  PDSDSN = &WORKDSN


/* INITIALIZE DATASET */
ISPEXEC  LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                ENQ(SHRW)                        +
                ORG(PDSORG)
SET LMRC = &LASTCC
IF &LMRC ^= 0 THEN +
   DO
      WRITE LMINIT FAILED - RETURN CODE &LMRC
      WRITE DATA SET IS NOT ALLOCATED OR NOT PARTITIONED
      EXIT CODE(8)
   END

WRITE LIST OF CURRENT MEMBERS OF:'&WORKDSN'  &RT
/* OPEN DATA SET */
ISPEXEC LMOPEN DATAID(&PDSID) OPTION(INPUT)
SET LMRC = &LASTCC

/* CALL EDIT WITH EACH MEMBER OF THE DATA SET AND SPECIFY THE */
/* INITIAL EDIT MACRO.                                        */
DO WHILE (&LMRC = 0)
  /* BUILD MEMBER LIST AND RETURN NEXT MEMBER */
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER) -
        STATS(NO)
  SET LMRC = &LASTCC
  IF &LMRC = 0 THEN /* NOT END OF MEMBER LIST */ +
     DO
     IF &SUBSTR(1:&T1,&MEMBER) GE &TARGET1 AND +
      &SUBSTR(1:&T2,&MEMBER) LE &TARGET2  THEN DO
       WRITENR &MEMBER
       END
     END
 END
 WRITE
 WRITE
 WRITE
 WRITE
WRITE  END OF SELECTED MEMBER LIST
/* FREE MEMBER LIST */
ISPEXEC LMMLIST DATAID(&PDSID) OPTION(FREE)
/* CLOSE DATA SET */
ISPEXEC LMCLOSE DATAID(&PDSID)
/* FREE DATA ID */
ISPEXEC LMFREE  DATAID(&PDSID)


EXIT CODE(0)
