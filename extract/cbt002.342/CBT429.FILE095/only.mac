ISREDIT MACRO (PARM)
 IF &STR(&PARM) = ? THEN DO
 ISPEXEC DISPLAY PANEL(ONLY)
 EXIT
 END
/**********************************************************************/
/* THIS IS A COMBINATION OF EXCLUDE AND FIND SUCH THAT "ONLY" THE     */
/* LINES CONTAINING THE SEARCH STRING WILL BE DISPLAYED.              */
/*                                                                    */
/* TO RUN:                                                            */
/*    ENTER ONLY FOLLOWED BY ANY VALID FIND COMMAND PARAMETER EXCEPT  */
/*    ALL.                                                            */
/**********************************************************************/
/* IF NO PARAMETER WAS GIVEN, DISPLAY MESSAGE AND EXIT */
IF &STR(&PARM) = &STR() THEN +
  DO
    SET &ZEDSMSG = &STR(NO PARAMETER SPECIFIED)
    SET &ZEDLMSG = &STR(THE ONLY MACRO REQUIRES A SEARCH PARAMETER)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12) /* KEEPS COMMAND ON COMMAND LINE */
  END

/* EXCLUDE ALL LINES AND THEN FIND THE PARM */
ISREDIT EXCLUDE ALL
ISREDIT FIND ALL '&STR(&PARM)'

/* GET THE FIND COUNTS AND REMOVE LEADING ZEROS FROM FINDLINE */
ISREDIT (FINDCNT,FINDLINE) = FIND_COUNTS
SET &FINDLINE = &FINDLINE

/* DISPLAY MESSAGE AND EXIT */
SET ZEDSMSG = &STR(&FINDLINE LINES DISPLAYED)
SET ZEDLMSG = &STR(ONLY &PARM CAUSED &FINDLINE LINES TO BE SHOWN)
ISPEXEC SETMSG MSG(ISRZ000)
EXIT CODE(0)
