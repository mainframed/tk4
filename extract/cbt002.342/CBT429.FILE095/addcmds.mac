PROC 0  CMD(ISPCMDS)
CONTROL NOMSG
 ISPEXEC VPUT (ISPC1 ISPC2) PROFILE
 CLRSCRN
 WRITE                **** WAIT CHECKING CONCATENATION ****
 WRITE                * SEARCHING FOR: ISPTLIB AND ISPTABL*
SET &TARGETDD = ISPTLIB
SET &FLAG2 = ISPTLIB
ISPEXEC VPUT FLAG2 PROFILE
ISPEXEC VPUT TARGETDD PROFILE
FREE F(MSGS1)
   SET &Q = &SUBSTR(1:2,&SYSTIME)
   SET &Q1 = &SUBSTR(4:5,&SYSTIME)
   SET &Q2 = &SUBSTR(7:8,&SYSTIME)
   SET &MSGS = &STR(LS&Q&Q1&Q2)
   SET &MDA = &STR(&SYSPREF..&MSGS)
  FREE ATTRLIST(ISPMSGS)
    ATTRIB ISPMSGS DSORG(PS) RECFM( F B ) +
      LRECL( 80 ) BLKSIZE( 80 )
       ALLOC DA('&MDA') SP (1,1) TRACKS USING (ISPMSGS) +
        F(MSGS1) NEW
 ISPEXEC EDIT DATASET('&MDA')   MACRO(CMDUPDT1)
SET &FLAG2 = TABL
ISPEXEC VPUT FLAG2 PROFILE
SET &TARGETDD = ISPTABL
ISPEXEC VPUT (TARGETDD) PROFILE
 ISPEXEC EDIT DATASET('&MDA')   MACRO(CMDUPDT1)
 DELETE '&MDA'
 ISPEXEC VGET (ISPC1 ISPC2) PROFILE
 CLRSCRN
SET &DATAID = &ISPC1
SET &MEM = &CMD
SET &DSN = &ISPC2
  ISPEXEC  LMINIT DATAID(DD01)  DATASET('&DATAID')  ENQ(SHR)
ISPEXEC  LMINIT DATAID(DD02) DATASET('&DSN') ENQ(SHR) VOLUME(&SYSVOLUME)
 IF &MEM ^= THEN DO
   ISPEXEC  LMCOPY FROMID(&DD01) TOMEM(@@@@CMDS) TRUNC REPLACE +
     TODATAID(&DD02) FROMMEM(&MEM)
     IF &LASTCC =8 THEN DO
           SET ZEDSMSG = &STR(MEMBER NOT FOUND)
             SET ZEDLMSG = &STR(&DATAID(&MEM) NOT FOUND)
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT
          END
 END
SET &RESP1 = ENTER
ISPEXEC VPUT (RESP1) PROFILE
ISPEXEC SELECT PGM(ISPUCM) PARM(@@@@CMDS)
ISPEXEC VGET CMDIN PROFILE
  IF &CMDIN = THEN SET &CMDIN = REFRESH
  IF &SUBSTR(1:3,&CMDIN) = CAN THEN GOTO LCLOSE
CONTROL NOFLUSH
SET &TBNM = @@@@CMDS
ISPEXEC TBOPEN &TBNM WRITE
TABSORT: +
  ISPEXEC  TBSORT  &TBNM   FIELDS(ZCTVERB)
  ISPEXEC  TBCLOSE &TBNM         REPLCOPY
WRITE              ****** COMMAND TABLE SORTED REPLACING ******
WRITE              **** TO ACTIVATE: EXIT AND REENTER ISPF ****
   ISPEXEC  LMCOPY FROMID(&DD02) TOMEM(&MEM) TRUNC REPLACE +
     TODATAID(&DD01) FROMMEM(@@@@CMDS)
     IF &LASTCC =8 THEN DO
           SET ZEDSMSG = &STR(MEMBER NOT FOUND)
             SET ZEDLMSG = &STR(&DATAID(&MEM) NOT FOUND)
           ISPEXEC SETMSG MSG(ISRZ001)
           EXIT
          END
LCLOSE: +
  IF &SUBSTR(1:3,&CMDIN) = CAN THEN DO
  WRITE CANCEL COMMAND ACCEPTED NO UPDATES MADE
  END
ISPEXEC LMCLOSE DATAID(&DD01)
ISPEXEC LMFREE DATAID(&DD01)
ISPEXEC LMCLOSE DATAID(&DD02)
ISPEXEC LMFREE DATAID(&DD02)
SET  PDSDSN = &DSN
ISPEXEC  LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                ENQ(SHRW)                        +
                ORG(PDSORG)
ISPEXEC LMOPEN DATAID(&PDSID) OPTION(OUTPUT)
ISPEXEC LMMDEL DATAID(&PDSID) MEMBER(@@@@CMDS)
ISPEXEC LMCLOSE DATAID(&PDSID)
ISPEXEC LMFREE DATAID(&PDSID)

