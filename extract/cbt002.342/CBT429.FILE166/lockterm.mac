LOCKTERM CSECT
***********************************************************************
*                                                                     *
*  MODULE NAME:              LOCKTERM                                 *
*                                                                     *
*  MODULE TYPE:              TSO COMMAND                              *
*                                                                     *
*  LOAD MODULE NAME:         LOCKTERM                                 *
*                                                                     *
*  LOAD LIBRARY:             SYS2.LINKLIB                             *
*                                                                     *
*  ASSEMBLER ATTRIBUTES:     NONE                                     *
*                                                                     *
*  LKED ATTRIBUTES:          NONE                                     *
*                                                                     *
*  MODULE AUTHORIZATION:     NONE                                     *
*                                                                     *
*  MACROS:                   LOTS                                     *
*                                                                     *
*  MACRO LIBRARIES:          SYS1.MACLIB PTEC220.S.ASM                *
*                                                                     *
*  ABENDS:                   NONE                                     *
*                                                                     *
*  FUNCTION:                 TO LOCK UP A TERMINAL UNTIL THE          *
*                            CORRECT PASSWORD IS ENTERED              *
*                                                                     *
*  PARAMETERS:               NONE                                     *
*                                                                     *
*  INTERNAL TABLES:          NONE                                     *
*                                                                     *
*  CALLED MODULES:           NONE                                     *
*                                                                     *
*  CALLING MODULES:          TMP                                      *
*                                                                     *
*  PROGRAM FLOW:             PROMPT FOR INITIAL PASSWORD              *
*                                                                     *
*                            LOOP UNTIL PASSWORD IS TYPED IN AGAIN    *
*                                                                     *
*  PARAMETER LAYOUTS:        NONE                                     *
*                                                                     *
*                                                                     *
*  COPYRIGHT:                KIRK STICKEN                             *
*                            APRIL 25, 1985                           *
*                                                                     *
*  AUTHOR:                   KIRK STICKEN                             *
*                                                                     *
*                                                                     *
*  MODIFICATION RECORD:      NEW PROGRAM 04-25-85 KIRK STICKEN        *
*                            MODIFY FOR X-A  5/1/86 KIRK STICKEN      *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
LOCKTERM RMODE 24
LOCKTERM AMODE 31
         SPLEVEL SET=2
BLANK    EQU   X'40'
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING LOCKTERM,R12
         LA    R6,SAVE
         ST    R6,8(R13)
         ST    R13,4(R6)
         LR    R13,R6
         STFSMODE ON,INITIAL=YES       . START FULL SCREEN PROGRAM
TPUT1    TPUT  KLEAR,KLEARL,FULLSCR    . CLEAR AND ISSUE WRITE ERASE
         ESTAE EXIT1                   . PREPARE FOR THE WURST
         STTMPMD OFF,KEYS=ALL          . I INTERCEPT ALL FKEYS NOW
INITL    BAL   R9,PUTGET1              . PUT SCREEN AND GET RESPONSE
         CLI   DATA1,X'00'             . DID USER ENTER ANYTHING
         BE    INITL                   .    NOT HE.
         MVC   PWD,DATA1               . SAVE PASSWORD
         BALR  R10,0                   . SET LOOPBACK
LOOP     BAL   R9,PUTGET               . GET USERS DATA
         CLC   PWD,DATA1               . IS IT THE PASSWORD
         BNER  R10                     . NO, LOOP FOREVER
         B     FINAL                   . USER ENTER PWD, GET OUT
EXIT1    DS    0H                      .
         STM   R14,R12,12(R13)         .
         BALR  R11,R0                  .
         USING *,R11                   .
         ESTAE 0                       .
         STTMPMD ON,KEYS=NO            .
         SETRP RC=0                    .
         LM    R14,R12,12(R13)         .
         BR    R14                     .
         DROP  R11                     .
PUTGET   NOPR  R1
RESHOW1  TPUT  WORK,WORKL,FULLSCR
         XC    DATA1(L'PWD),DATA1      . CLEAR PASSWORD AREA
         TGET  PFK,L'PFK,ASIS
         CLI   PFK,RESHOW              .
         BE    RESHOW1                 .
         BR    R9
PUTGET1  NOPR  R1
RESHOW2  TPUT  WORK1,WORK1L,FULLSCR
         XC    DATA1(L'PWD),DATA1      . CLEAR PASSWORD AREA
         TGET  PFK,L'PFK,ASIS
         CLI   PFK,RESHOW              .
         BE    RESHOW2                 .
         BR    R9
FINAL    DS    0H                      .
RETURN   EQU   *
         STTMPMD ON,KEYS=NO            . RESET TMP SCREEN MANAGMENT
         ESTAE 0                       .
TPUT2    TPUT  KLEAR,KLEARL,FULLSCR    . RESET TMP SCREEN MANAGMENT
         STFSMODE OFF                  . RESET TMP SCREEN MANAGMENT
         LH    R15,RETCODE      SET A VALID RETURN CODE
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
SAVE     DS    18F
RETCODE  DC    H'0'
KLEAR    MFORM ESC,WRE,(1,1),IC,(1,2),(24,79,RA),NULL
WORK     MFORM WCCA,(2,00032),A'P','TERMINAL LOCKUP',            XXXXXXX
               (3,00025),(3,29,RA),NULL,                              XX
               (3,00029),'Enter Your',                                XX
               A'HP','Password:',A'O',IC,(3,00050),(3,065,RA),NULL,    X
               (3,00058),A'P'
WORK1    MFORM WCCA,(2,00032),A'P','TERMINAL LOCKUP',              XXXXX
               (3,00025),'Enter Your',                                XX
               A'HP','Initial Password:',A'O',IC,                      X
               (3,00054),(3,00062,RA),NULL,A'P'
PWD      DS    CL8                     .
PFK      DS    CL1920
         ORG   PFK                     .
KEY1     DS    CL1                     .
CURADDR  DS    CL2                     .
SBA1     DS    CL1                     .
DATA1DDR DS    CL2                     .
DATA1    DS    CL(L'PFK-(*-KEY1))      .
         ORG ,                         .
         M3270
         DSECT
         IHASDWA ,                     .
         END   LOCKTERM
