//PTEC2201  JOB (8351,RTSP),CLASS=I,MSGCLASS=X,NOTIFY=PTEC220
/*JOBPARM   R=403
//ASM1      EXEC PGM=IEUASM,PARM='LOAD,NODECK,RENT'
//SYSPRINT    DD SYSOUT=*
//SYSLIB      DD DSN=SYS1.SMPMTS,DISP=SHR,UNIT=SYSDA,VOL=SER=SYSRSA
//            DD DSN=SYS1.SMPSTS,DISP=SHR,UNIT=SYSDA,VOL=SER=SYSRSA
//            DD DSN=SYS1.HASPSRC,DISP=SHR
//            DD DSN=SYS1.MACLIB,DISP=SHR
//            DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSUT2      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSUT3      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSIN       DD *
***********************************************************************
*                                                                     *
*  MODULE NAME:              EXIT1                                    *
*                                                                     *
*  MODULE TYPE:              JES2 MAIN TASK SEP PAGE EXIT             *
*                                                                     *
*  LOAD MODULE NAME:         EXIT1                                    *
*                                                                     *
*  LOAD LIBRARY:             SYS2.LINKLIB                             *
*                                                                     *
*  ASSEMBLER ATTRIBUTES:     RENT                                     *
*                                                                     *
*  LKED ATTRIBUTES:          RENT                                     *
*                                                                     *
*  MODULE AUTHORIZATION:     JES2                                     *
*                                                                     *
*  MACROS:                   REF: JES2 MODS AND MACS                  *
*                                                                     *
*  MACRO LIBRARIES:          SYS1.HASPSRC, SYS1.MACLIB                *
*                                                                     *
*  ABENDS:                   NONE                                     *
*                                                                     *
*  FUNCTION:                 FORMAT AND PRINT A SEP PAGE FOR PRINT    *
*                                                                     *
*  PARAMETERS:               REF: JES2 MODS AND MACS                  *
*                                                                     *
*  INTERNAL TABLES:          NONE                                     *
*                                                                     *
*  CALLED MODULES:           RBDATE: FORMAT DATE/TIME ROUTINE         *
*                                                                     *
*  CALLING MODULES:          REF: JES2 MODS AND MACS                  *
*                                                                     *
*  PROGRAM FLOW:                                                      *
*                                                                     *
*                                                                     *
*  PARAMETER LAYOUTS:        REF: JES2 MODS AND MACS                  *
*                                                                     *
*                                                                     *
*  COPYRIGHT:                REPUBLIC BANK DALLAS                     *
*                            JUNE 15, 1985                            *
*                                                                     *
*  AUTHOR:                   KIRK STICKEN, INFORMATION SERVICES       *
*                            (214)-653-7794                           *
*                            REPUBLIC BANK DALLAS                     *
*                            P.O. BOX 225961                          *
*                            DALLAS ,  TEXAS   75265                  *
*                                                                     *
*                                                                     *
*  MODIFICATION RECORD:      NEW PROGRAM 06-15-85 KIRK STICKEN        *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         COPY  $HASPGBL                .  COPY THOSE HASP GLOBALS
SAMPUEX  $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                  X
               TITLE='JES2 PRINT SEPARATOR PAGE',                      X
               ENVIRON=JES2,                                           X
               PSA,                    .                               X
               CVT,                    .                               X
               SMCA,                   .                               X
               $BUFFER,                .                               X
               $CAT,                   .                               X
               $DCT,                   .                               X
               $HASPEQU,               .                               X
               $HCT,                   .                               X
               $JCT,                   .                               X
               $JOE,                   .                               X
               $JQE,                   .                               X
               $MIT,                   .                               X
               $PCE,                   .                               X
               $PDDB,                  .                               X
               $PPPWORK                .
RBDEXIT  $ENTRY BASE=R12,CSECT=YES     .  ENTER A JES2 USER EXIT
         $SAVE                         .  STD SAVE PROTOCOL
         LR    R12,R15                 .  SET ADDRESS OF THE EXIT
         LR    R8,R1                   . GET THE DCT POINTER
         USING DCTDSECT,R8             .    AND ADDRESS THE DCT ALSO
         SLR   R15,R15                 .  PRESET VALID RETURN CODE
         TM    PCEID,PCEPUSID          .  IS THIS A PUNCH DEVICE?
         BO    RETURN                  .      YOU GOT IT SHERLOCK
         TM    DCTPPSW,DCTPPSWS        . ARE SEP PAGES REQUESTED
         BO    RETURN                  .    NO, GO HOME
         TM    DCTPPSW9,DCT9700        . IS THIS THE 9700
         BO    RETURN                  .    YES, DO NOT REPEAT PAGE
BEGIN    DS    0H                      .
         LR    R5,R0                   . SAVE ENTRY CODE
         L     R3,PCEUSER0             . GET PCEUSER SPECIAL SAVE FLD
         C     R3,$ZEROS               . WERE WE INTERRUPTED?
         BNE   OK                      .    YES WE WERE, CONTINUE
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES . GET A HASP WORK BUFFER
         LR    R3,R1                   . COPY ADDRESS OF BUFFER
         USING BUFDSECT,R3             .      SO WE CAN USE IT HERE
         ST    R3,PCEUSER0             . SAVE FIXED BUFFER PTR
OK       LA    R4,BUFSTART             . REFERENCE OUR WORK AREA
         USING WORK3,R4                . ADDRESS WORKING STORAGE
         TM    MDCTFEAT,DCTPSHDR       . IS THIS SNA TERM ?
         BNO   NOSNA                   .    NO, SKIP PDIR COMMAND
         $SEPPDIR (R3)                 . SEND PDIR IN CASE SNA RMT
NOSNA    DS    0H                      . SO WE FINALLY GOT HERE!!
         LA    R0,JCTJNAME             . GET JOBNAME FROM JCT
         $PBLOCK BUFFER=(R3),DATA=(R0),SLANT=YES,CENTER=YES . BLOCK IT
         MVI   MSG,C' '                . PRINT ONE SPACE
         LA    R1,MSG                  . GET ADDRESS FOR LINE PRINT
         LA    R0,1                    .     AND BUFFER LENGTH
         $PRPUT DATA=(R1),LEN=(R0),COUNT=03,WAIT=YES . CLEAR 3 LINES
         MVC   DCTUSER0(8),=CL8'BIN#'  . CLEAR EIGHT BYTES IN WORK AREA
         MVC   DCTUSER1(4),JCTROOMN    .    AND MOVE IN BIN NUMBER
         LA    R0,DCTUSER0             . PERPARE TO BLOCK BIN NUMBER
         $PBLOCK BUFFER=(R3),DATA=(R0),SLANT=YES,CENTER=YES . BLOCK IT
         MVI   MSG,C' '                . PRINT ONE SPACE
         LA    R1,MSG                  . GET ADDRESS FOR LINE PRINT
         LA    R0,1                    .     AND BUFFER LENGTH
         $PRPUT DATA=(R1),LEN=(R0),COUNT=02,WAIT=YES . CLEAR 2 LINES
         L     R7,PWKJOE               .  ADDRESS THE CURRENT JOE
         USING JOEDSECT,R7             .     AND ADDRESS IT
         MVC   MSG,TEMPLATE            . INIT BUFFER AREA
         MVC   MSG+TJOB-TEMPLATE(L'TJOB),JCTJNAME  . MOVE JOBNAME
         MVC   MSG+TCLASS1-TEMPLATE(L'TCLASS1),JOECURCL  . MOVE CLASS
         MVC   MSG+TCLASS2-TEMPLATE(L'TCLASS2),JOECURCL  . MOVE CLASS
         SLA   R5,1                    . MULT ENTRY CODE BY 2
         LA    R5,TYPES(R5)            . INDEX TO TYPE CODE
         MVC   MSG+TYPE-TEMPLATE(L'TYPE),0(R5)  AND MOVE IT IN
         MVC   MSG+TYPE2-TEMPLATE(L'TYPE),0(R5) AND MOVE IT IN
         MVC   MSG+TJID1-TEMPLATE(L'TJID1),JCTJOBID . MOVE IN JOBID
         MVC   MSG+TJID2-TEMPLATE(L'TJID2),JCTJOBID . MOVE IN JOBID
         MVC   MSG+TNAME-TEMPLATE(L'TNAME),JCTPNAME . MOVE IN PROG NAME
         MVC   MSG+TROOM-TEMPLATE(L'TROOM),JCTROOMN . MOVE IN BIN #
         LA    R15,MSG+TTIME-TEMPLATE  . GET ADDRESS OF TIME
         ST    R15,PLIST               .       PUT IT IN PLIST
         LA    R15,MSG+TDATE-TEMPLATE  . GET ADDRESS OF DATE
         ST    R15,PLIST+4             .       PUT IT IN PLIST
         LA    R15,USERWORK            . GET USER STORAGE AREA
         O     R15,=X'80000000'        .       SAY LAST PARM
         ST    R15,PLIST+8             .           AND PUT IN PLIST
         LA    R1,PLIST                .  STD REG PARMS
         L     R15,=V(RBDATE)          .      AND CALL DATE ROUTINE
         BALR  R14,R15                 .           NOW
         MVC   MSG+TPRID-TEMPLATE(L'TPRID),DCTDEVN . MOVE IN PRINTER NM
         L     R1,16                   . GET CVT IN R1
         L     R1,CVTSMCA-CVT(R1)      . GET SMCA
         MVC   MSG+TSYS-TEMPLATE(L'TSYS),SMCASID-SMCA(R1) . MOVE SYSTEM
DOPUT    DS    0H                      .
         SLR   R9,R9                   .
         IC    R9,$PRIDCT              . GET LOCAL LINES PER SEP PAGE
         TM    DCTDEVTP,DCTPRT         . IS LOCAL PRINTER
         BO    LOCAL                   .
         IC    R9,$TPIDCT              . GET REMOTE LINE COUNT INSTEAD
LOCAL    S     R9,=F'30'               .   MINUS PRINTED LINES +1
         C     R9,$ZEROS               . IS PERHAPS NEGATIVE?
         BH    OKAGAIN                 .    NO, IS OK
         L     R9,$F8                  .   YES, MAKE 8 THE DEFAULT
OKAGAIN  DS    0H                      .
         BALR  R2,R0                   .
LOOP     LA    R0,L'MSG                .
         LA    R1,MSG                  .
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=NO  PRINT A LINE
         BCTR  R9,R2                   . LOOP UNTIL DONE
         LA    R0,L'MSG                .
         LA    R1,MSG                  .
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES PRINT LAST LINE
         $FREEBUF (R3)                 . FREE HASP BFR AND WORK AREA
         SLR   R15,R15                 . CLEAR REGISTER
         ST    R15,PCEUSER0            . RESET OUR WORK POINTER
         DROP  R3,R4                   .
         LA    R15,8                   . DO NOT PRINT JES2 SEPARATOR
RETURN   EQU   *
         $RETURN RC=(R15)              .  GO BACK TO JES WITH RC
TYPES    DC    CL8'START'              .  ENTRY CODE = 0
         DC    CL8'CONT'               .  ENTRY CODE = 4
         DC    CL8'END'                .  ENTRY CODE = 8
TEMPLATE DC    CL132' '                .  INITIAL TEMPLATE DATA
         ORG   TEMPLATE                .
         DC    CL4'****'               .  FIRST BOUNDARY
TCLASS1  DS    CL1                     .  OUTPUT CLASS
         DC    CL2' '                  .
TYPE     DS    CL5                     .  SEPARATOR TYPE(START,END)
         DC    CL2' '                  .
TJID1    DS    CL8                     .  JOB ID
         DC    CL2' '                  .
TJOB     DS    CL8                     .  JOB NAME
         DC    CL2' '                  .
TNAME    DS    CL20                    .  PROGRAMMER NAME
         DC    CL2' '                  .
         DC    CL4'BIN#'               .
TROOM    DS    CL4                     .  ROOM NUMBER
         DC    CL2' '                  .
TTIME    DS    CL12                    .  PRINTER TIME
TDATE    DS    CL10                    .  DATE (DD MMM YY)
         DC    CL2' '                  .
TPRID    DS    CL8                     . PRINTER ID
         DC    CL2' '                  .
         DC    CL4'SYS '               .
TSYS     DS    CL4                     . JES2 SYSTEM CPU ID
         DC    CL2' '                  .
TJID2    DS    CL8                     . JOB ID 2
         DC    CL2' '                  .
TYPE2    DS    CL5                     .
         DC    CL2' '                  .
TCLASS2  DS    CL1                     . SECOND OCCURRENCE OF JOB CLASS
         DC    CL4'****'               .
TPLEN    EQU   *-TEMPLATE              .
         ORG   ,                       .
         LTORG                         .
WORK3    DSECT                         .
MSG      DS    CL(TPLEN)               .
PLIST    DS    5F                      .
         DS    0D                      .
USERWORK DS    CL256                   .
WORKLEN  EQU   *-MSG                   .
         PUSH  USING                   .
RBDATE   CSECT
***********************************************************************
*                                                                     *
*  MODULE NAME:              RBDATE                                   *
*                                                                     *
*  MODULE TYPE:              JES2 EXIT SUBROUTINE                     *
*                                                                     *
*  LOAD MODULE NAME:         RBDATE                                   *
*                                                                     *
*  LOAD LIBRARY:             SYS1.LINKLIB                             *
*                                                                     *
*  ASSEMBLER ATTRIBUTES:     RENT                                     *
*                                                                     *
*  LKED ATTRIBUTES:          RENT                                     *
*                                                                     *
*  MODULE AUTHORIZATION:     JES2                                     *
*                                                                     *
*  MACROS:                   NONE                                     *
*                                                                     *
*  MACRO LIBRARIES:          NONE                                     *
*                                                                     *
*  ABENDS:                   NONE                                     *
*                                                                     *
*  FUNCTION:                 FORMAT DATE AND TIME                     *
*                                                                     *
*  PARAMETERS:               TIME,DATE,WORKAREA                       *
*                                                                     *
*  INTERNAL TABLES:          MONTAB: TABLE OF MONTH NAMES             *
*                                                                     *
*  CALLED MODULES:           NONE                                     *
*                                                                     *
*  CALLING MODULES:          HASPXIT1                                 *
*                                                                     *
*  PROGRAM FLOW:             FORMAT TIME                              *
*                            CONVERT JULIAN TO GREGORIAN DATE         *
*                                                                     *
*  PARAMETER LAYOUTS:        NONE                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*  AUTHOR:                   KIRK STICKEN, INFORMATION SERVICES       *
*                            (214)-653-7794                           *
*                            REPUBLIC BANK DALLAS                     *
*                            P.O. BOX 225961                          *
*                            DALLAS ,  TEXAS   75265                  *
*                                                                     *
*                                                                     *
*  MODIFICATION RECORD:      NEW PROGRAM 05-06-85 KIRK STICKEN        *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         B     START1-RBDATE(R15)      .  BRANCH AROUND CONSTANT
         DC    AL1(L'@ID)              .  DEFINE ID LENGTH
@ID      DC    C'RBDATE'               .  DEFINE PROGRAM ID
         DC    C'_&SYSDATE._&SYSTIME._REPUBLIC_BANK_DALLAS'
*   THE JULIAN DATE CONVERSION ALGORITHM COMES FROM THE CACM.
*   ALGORITHM 398: TABLELESS DATE CONVERSION
*
*   PROCEDURE CALENDAR(Y,N,M,D);
*     VALUE Y,N:  INTEGER Y, N, M, D, T;
*     COMMENT Y IS YEAR, N IS DAY OF YEAR. M IS MONTH, D IS DAY OF
*             MONTH. M AND D ARE CALCULATED FOR YOU;
*     BEGIN
*       T:= IF(Y/4)*4=Y THEN 1 ELSE 0;
*       T:=IF(Y/400)*400=Y OR (Y/100)*100 NOT=Y THEN T ELSE 0;
*       D:=N+(IF N>(59+T) THEN 2-T ELSE 0);
*       M:=((D+91*100)/3055;
*       D:=(D+91) - (M*3055)/100;
*       M:=M-2;
*     END CALENDAR;
*
*
START1   DS    0H                      .
         STM   R14,R12,12(R13)         .  SAVE MASTERS REGISTERS
         LR    R12,R15                 .  SET UP BASE ADDRESS
         USING RBDATE,R12              .  AND USE TO RUN WITH
         USING PARM1,R5                .
         USING PARM2,R6                .
         USING PARM3,R7                .
         LM    R5,R7,0(R1)             .  LOAD 3 PARMS
         LA    R8,SAVE2                .  GET OUR SAVE AREA
         ST    R8,8(R13)               .  STORE OURS THERE
         ST    R13,4(R8)               .  STORE THEIRS HERE
         LR    R13,R8                  .  STANDARD REGISTER CONVENTIONS
         MVI   WTIME,C' '              .
         MVC   WTIME+1(L'WTIME-1),WTIME . BLANK OUT FIELD
         MVI   WDATE,C' '              .
         MVC   WDATE+1(L'WDATE-1),WDATE . BLANK OUT FIELD
TIME1    TIME  DEC                     .  GET SYSTEM DATE AND TIME
         ST    R1,DATE1                .    AND SAVE THE DATE
         O     R0,=F'15'               .  SET PACKED SIGN
         ST    R0,TIME2                .    AND SAVE TIME
         MVC   TYPE1,=C'AM'            .  INIT TYPE TO MORNING
         CP    TIME2,=P'1200000'       .  IS IT IN AFTERNOON
         BL    AMOK                    .      NO, AM IS OK
         MVC   TYPE1,=C'PM'            .    YES, RESET TYPE
         SP    TIME2,=P'1200000'       .  ADJUST FOR AFTERNOON
AMOK     MVC   EWORK,=X'4021204B20204B2020' . INIT EDIT WORK
         ED    EWORK,TIME2             .  EDIT THE TIME
         MVC   WTIME(L'EWORK),EWORK    .
         MVC   WTIME+L'EWORK+1(L'TYPE1),TYPE1
* R1=T,  R2=D,  R3=Y,  R4=M            .
LINE0    MVC   DWORK1+4(4),DATE1       .
         XC    DWORK1(4),DWORK1        .
         CVB   R2,DWORK1               .
         SRDA  R2,32                   .
         D     R2,=F'1000'             .
LINE1    SR    R1,R1                   .
         LR    R14,R3                  .
         SRDA  R14,32                  .
         D     R14,=F'4'               .
         LR    R14,R15                 .
         SRDA  R14,32                  .
         M     R14,=F'4'               .
         CR    R3,R15                  .
         BNE   OKT1                    .
         LA    R1,1                    .
OKT1     DS    0H                      .
LINE2    LR    R14,R3                  .
         SRDA  R14,32                  .
         D     R14,=F'400'             .
         LTR   R14,R14                 .
         BZ    NOMORT                  .
         LR    R14,R3                  .
         SRDA  R14,32                  .
         D     R14,=F'100'             .
         LTR   R14,R14                 .
         BNZ   NOMORT                  .
         SR    R1,R1                   .
NOMORT   DS    0H                      .
LINE3    LA    R15,59(R1)              .
         CR    R2,R15                  .
         BNH   NOMOR3                  .
         LA    R15,2                   .
         SR    R15,R1                  .
         AR    R2,R15                  .
NOMOR3   DS    0H                      .
LINE4    LA    R14,91(R2)              .
         SRDA  R14,32                  .
         M     R14,=F'100'             .
         D     R14,=F'3055'            .
         LR    R4,R15                  .
LINE5    LA    R2,91(R2)               .
         LR    R14,R4                  .
         SRDA  R14,32                  .
         M     R14,=F'3055'            .
         D     R14,=F'100'             .
         SR    R2,R15                  .
LINE6    BCTR  R4,R0                   .
         BCTR  R4,R0                   .
*LINE7    LTR   R1,R1                   .
*         BZ    NOMOR7                  .
*         C     R4,=F'2'                .
*         BNE   NOMOR7                  .
*         C     R2,=F'27'               .
*         BNH   NOMOR7                  .
*         BCTR  R2,R0                   .
*NOMOR7   DS    0H                      .
         CVD   R2,DWORK1               .
         MVC   EWORKD,=X'40202021'     .
         ED    EWORKD,DWORK1+6         .
         CVD   R3,DWORK1               .
         MVC   EWORKY,=X'40202021'     .
         ED    EWORKY,DWORK1+6         .
         LR    R14,R4                  .
         BCTR  R14,R0                  .
         SRDA  R14,32                  .
         M     R14,=F'3'               .
         LA    R15,MONTAB(R15)         .
         MVC   WDATE+4(3),0(R15)       .
         MVC   WDATE(3),EWORKD+1       .
         MVC   WDATE+7(3),EWORKY+1     .
RETURN1  EQU   *
         SR    R15,R15                 .  GET MODULE RETURN CODE
         L     R13,4(R13)              .  GET THEIR SAVE AREA ADDRESS
         L     R14,12(R13)             .  GET RETURN ADDRESS
         LM    R0,R12,20(R13)          .  RESTORE ALL BUT R15
         BR    R14                     .  RETURN TO CALLER
MONTAB   DC    CL3'JAN'                .
         DC    CL3'FEB'                .
         DC    CL3'MAR'                .
         DC    CL3'APR'                .
         DC    CL3'MAY'                .
         DC    CL3'JUN'                .
         DC    CL3'JUL'                .
         DC    CL3'AUG'                .
         DC    CL3'SEP'                .
         DC    CL3'OCT'                .
         DC    CL3'NOV'                .
         DC    CL3'DEC'                .
         LTORG ,                       .
PARM1    DSECT                         .
WTIME    DS    CL12                    .
PARM2    DSECT                         .
WDATE    DS    CL10                    .
PARM3    DSECT                         .
SAVE2    DS    18F
DWORK1   DS    D                       .
TIME2    DS    F                       .
DATE1    DS    F                       .
TYPE1    DS    CL2                     .
EWORK    DS    CL9                     .
EWORKD   DS    CL4                     .
EWORKY   DS    CL4                     .
         POP   USING                   .
RBDEXIT  CSECT                         .
         LTORG
         $MODEND                       .  SOME JES2 HOUSKEEPING
         END
//SYSGO       DD DSN=&&A,SPACE=(TRK,(10,10)),DCB=(LRECL=80,
//             BLKSIZE=800,RECFM=FB),DISP=(,PASS,DELETE),UNIT=SYSDA
//LKED      EXEC PGM=IEWL,REGION=256K,COND=(4,LT,ASM1),
//             PARM=('LIST,LET,MAP,RENT')
//SYSLIB      DD DSN=SYS1.LINKLIB,DISP=SHR
//SYSLIN      DD DSN=&&A,DISP=(OLD,DELETE)
//            DD DDNAME=SYSIN          .
//SYSLMOD     DD DISP=SHR,DSN=PTEC220.TEST.LOAD
//SYSPRINT    DD SYSOUT=*,DCB=(BLKSIZE=121,BUFNO=1)
//SYSUT1      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSIN       DD *                     .
  NAME EXIT1N(R)                       .
/*
//
