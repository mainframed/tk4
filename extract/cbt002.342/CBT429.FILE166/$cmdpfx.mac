$CMDPFX  TITLE '$CMDPFX - TO ASSIGN/DELETE A SYSTEM COMMAND PREFIX'
         PRINT NOGEN
$CMDPFX  CSECT
*
*-------------------------------------------------------------------
*
*        AUTHOR - VINH VU
*
*        PURPOSE - TO ADD/DELETE A COMMAND PREFIX FOR A MEMBER OF
*              THE PARALLEL SYSPLEX.
*
*        DATE WRITTEN - 11/25/96
*
*        REGISTER USAGE:
*              R9       - RETURN CODE
*              R11      - 2ND BASE
*              R10      - COPY OF R1 (JCL PARM POINTER) & RC
*              R12      - BASE
*
*        RETURN CODES:
*
*        ABEND CODES:
*
*        REQUIRED JCL :
*              //         EXEC PGM=$CMDPFX,PARM='',REGION=4M
*
*        PROGRAM USAGE:
*              PARM WILL BE '+PREFIX' OR '-PREFIX'. '+' IS FOR ADDING,
*              '-' IS FOR DELETING.  IF NO PARM FOUND, THE CVTSNAME IS
*              TO BE ADDED AS THE PREFIX.
*
*-------------------------------------------------------------------
*
*-------------------------------------------------------------------
*
$CMDPFX  SET   (R12,R11),DSECT=WKAREA,LV=WKLENG
*
         L     R10,0(R1)           SAVE PARM ADDRESS
*
         MVC   WKNAME,BLANK
         MVC   WTOTEXT,BLANK
*
         LH    R15,0(R10)          GET LENGTH
         LTR   R15,R15             TEST FOR ZERO
         BZ    USECVTNM            IF ZERO THEN ASSUME ADDING CVTNAME
         C     R15,F9              COMPARE AGAINST '9'
         BH    BADPARM             IF HIGH THEN BADPARM
         CLI   2(R10),C'+'         ELSE - IS IT 'ADD' ??
         BE    GOODPARM            YES - THEN SAVE PARM
         CLI   2(R10),C'-'         ELSE - IS IS 'DELETE' ?
         BE    GOODPARM            YES - THEN SAVE PARM
*
BADPARM  EQU   *
         MVC   WTOTEXT(L'MSG1),MSG1       SAY 'INVALID PARM'
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS12              AND THEN GET OUT
*
USECVTNM EQU   *
         L     R1,CVTPTR           OBTAIN CVT ADDRESS
         USING CVTMAP,R1
         MVC   WKNAME,CVTSNAME     USE SYSTEM NAME
         DROP  R1
         B     ADDPREF             GO TO ADD CVTSNAME AS PREFIX
*
*
GOODPARM EQU   *
         C     R15,F1              LENG=1 ? (JUST +/- AND NOTHING ELSE)
         BE    BADPARM             YES - THEN ABORT
*
         BCTR  R15,0               BYPASS FIRST BYTE
         BCTR  R15,0               MINUS 1 FOR EXECUTE INSTRUCTION
         EX    R15,SAVEPARM        NOW SAVE PARM
*
*----------------------------------
*        I'M READY TO PROCESS THE PARM
*----------------------------------
*
PARMDONE EQU   *
         CLI   2(R10),C'+'         IS IS 'ADD' ??
         BE    ADDPREF             YES - THEN DO 'CPF DEFINE'
*
DELPREF  EQU   *
         MVC   WKCPF(MCPFLENG),MCPF
         CPF   REQUEST=DELETE,     DELETE A PREFIX                     X
               PREFIX=WKNAME,      PREFIX IS FROM WKNAME               X
               MF=(E,WKCPF)
         LTR   R15,R15             TEST RETURN CODE
         BZ    DELISOK
         C     R15,F8              IS IT 'NOT FOUND'
         BE    DELNOFND            YES - THEN DISPLAY APPROP MSG
*
DELBAD   EQU   *
         MVC   WTOTEXT(L'MSG4),MSG4       SAY 'DELETE BAD'
         MVC   WTOTEXT+L'MSG4(L'WKNAME),WKNAME    DISPLAY PARM AS WELL
*
         CVD   R15,DWORD
         UNPK  DWORD(5),DWORD+5(3)
         OI    DWORD+4,X'F0'       MAKE EVERYTHING PRINTABLE
         MVC   WTOTEXT+MSG4R15(4),DWORD+1
*
         CVD   R0,DWORD
         UNPK  DWORD(5),DWORD+5(3)
         OI    DWORD+4,X'F0'       MAKE EVERYTHING PRINTABLE
         MVC   WTOTEXT+MSG4R0(4),DWORD+1
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS12
*
*
DELISOK  EQU   *
         MVC   WTOTEXT(L'MSG2),MSG2       SAY 'DELETE IS OK'
         MVC   WTOTEXT+L'MSG2(L'WKNAME),WKNAME    DISPLAY PARM AS WELL
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS0               GET OUT WITH GOOD RC
*
DELNOFND EQU   *
         MVC   WTOTEXT(L'MSG3),MSG3       SAY 'DELETE ERROR'
         MVC   WTOTEXT+L'MSG3(L'WKNAME),WKNAME    DISPLAY PARM AS WELL
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS4               USE '4' AS RC (NOT FND FOR DELETE)
*
*
*
ADDPREF  EQU   *
         MVC   WKCPF(MCPFLENG),MCPF
         CPF   REQUEST=DEFINE,     DEFINE A PREFIX                     X
               PREFIX=WKNAME,      PREFIX IS FROM WKNAME               X
               OWNER=OWNER,        OWNER NAME                          X
               FAILDISP=SYSPURGE,  KEEP UNTIL SYSTEM LEAVES PLEX       X
               REMOVE=YES,         STRIP PFX BEFORE PROCESSING CMD     X
               MF=(E,WKCPF)
         LTR   R15,R15             WAS 'ADD' OK ??
         BZ    ADDCHK              YES - THEN CHECK R0 ALSO FOR '4'
         C     R15,F8              IS IT 'DUPLICATE'
         BE    ADDDUP              YES - THEN DISPLAY APPROP MSG
*
*
ADDBAD   EQU   *
         MVC   WTOTEXT(L'MSG5),MSG5       SAY 'ADD BAD'
*
         CVD   R15,DWORD
         UNPK  DWORD(5),DWORD+5(3)
         OI    DWORD+4,X'F0'       MAKE EVERYTHING PRINTABLE
         MVC   WTOTEXT+MSG5R15(4),DWORD+1
*
         CVD   R0,DWORD
         UNPK  DWORD(5),DWORD+5(3)
         OI    DWORD+4,X'F0'       MAKE EVERYTHING PRINTABLE
         MVC   WTOTEXT+MSG5R0(4),DWORD+1
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS12
*
*
ADDCHK   EQU   *
         LTR   R0,R0               DOES R0 CONTAIN '0' ?
         BZ    ADDISOK             YES - THEN GOOD ADD
         MVC   WTOTEXT(L'MSG8),MSG8       SAY 'ADD IS NOT OK'
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS8               USE '8' AS RC
*
*
ADDISOK  EQU   *
         MVC   WTOTEXT(L'MSG6),MSG6       SAY 'ADD IS OK'
         MVC   WTOTEXT+L'MSG6(L'WKNAME),WKNAME    DISPLAY PARM AS WELL
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS0               GET OUT WITH GOOD RC
*
ADDDUP   EQU   *
         MVC   WTOTEXT(L'MSG7),MSG7       SAY 'ADD DUPLICATE'
         MVC   WTOTEXT+L'MSG7(L'WKNAME),WKNAME    DISPLAY PARM AS WELL
         BAL   R14,DOWTO           PUT OUT MESSAGE
         B     RCIS4               USE '4' AS RC (NOT FND FOR DELETE)
*
*
*
DOWTO    EQU   *
         ST    R14,#DOWTO          SAVE RETURN ADDRESS
         MVC   WKWTO,WTOM          GET THE WTO LIST FORM TO WKAREA
         MVC   WKWTO+4(L'WTOTEXT),WTOTEXT
         WTO   MF=(E,WKWTO)
         L     R14,#DOWTO          RELOAD RETURN ADDRESS
         BR    R14                 BACK TO CALLER
*
*
RCIS0    EQU   *
         XR    R9,R9               USE 0 AS RETURN CODE
         B     GETOUT
*
RCIS4    EQU   *
         LA    R9,4
         B     GETOUT
*
RCIS8    EQU   *
         LA    R9,8
         B     GETOUT
*
RCIS12   EQU   *
         LA    R9,12
         B     GETOUT
*
*
GETOUT   EQU   *
         EXIT  R=R9
*
*
SAVEPARM MVC   WKNAME(0),3(R10)      SAVE PREFIX
*
*
*
         EJECT
*
         LTORG
*
*
*
MSG1     DC    C'PF0001E - PROGRAM ABORTED - INVALID PARM'
MSG2     DC    C'PF0002I - CMD PREFIX SUCESSFULLY DELETED AS '
MSG3     DC    C'PF0003E - DELETED PREFIX NOT FOUND AS '
MSG4     DC    C'PF0004E - DELETE CPF ERROR - R15=XXXX - R0=XXXX'
MSG4R15  EQU   33                  OFFSET TO R15 RC
MSG4R0   EQU   43                  OFFSET TO R0 RC
*
MSG5     DC    C'PF0005E - ADD CPF ERROR - R15=XXXX - R0=XXXX'
MSG5R15  EQU   30                  OFFSET TO R15 RC
MSG5R0   EQU   40                  OFFSET TO R0 RC
*
MSG6     DC    C'PF0006I - CMD PREFIX SUCESSFULLY ADDED AS '
MSG7     DC    C'PF0007E - DUPLICATE PREFIX BEING ADDED AS '
MSG8     DC    C'PF0008E - ADD CPF ERROR - SYSTEM IN XCF-LOCAL MODE'
*
F1       DC    F'1'
F8       DC    F'8'
F9       DC    F'9'
*
MCPF     CPF   MF=(L,OWNCPF)
MCPFLENG EQU   *-MCPF
*
*
WTOM     WTO   '0123456789012345678901234567890123456789012345678901234*
               5678901234567890123456789',ROUTCDE=2,DESC=4,MF=L
WTOL     EQU   *-WTOM
*
*
*
OWNER    DC    CL8'$CMDPFX'
*
*
BLANK    DC    CL80' '
*
*
         EJECT
WKAREA   DSECT
         DS    18F                 SAVE AREA
*
DWORD    DS    D
#DOWTO   DS    F                   RETURN ADDRESS FOR 'DOWTO' ROUTINE
*
*
         DS    0F
WKWTO    DS    CL(WTOL)            WTO LIST FORM
*
*
         CPF   MF=(L,WKCPF)
*
*
WKNAME   DS    CL8                 PREFIX
*
WTOTEXT  DS    CL80
*
RETCDE   DS    F                   RET CODE
REASCODE DS    F                   REASON CODE
*
WKLENG   EQU   *-WKAREA
*
         CVT   DSECT=YES
*
         END
