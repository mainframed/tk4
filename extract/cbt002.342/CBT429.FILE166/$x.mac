 /********************* $X       REXX/CLIST      *********************/
 /* $X                                                               */
 /*                                                                  */
 /*    This is an edit macro.  It reads the member being edited and  */
 /*    executes it as a clist.  Parms can be entered on the command  */
 /*    line as if you're really running this under Option 6 (TSO).   */
 /*    You DO NOT have to save the edited member before running      */
 /*    this macro (that's why it was written in the first place).    */
 /*                                                                  */
 /* Author: Vinh Vu                                                  */
 /* Date written: 8/1997                                             */
 /*                                                                  */
 /* SYNTAX: $X Parms (Optional)                                      */
 /*                                                                  */
 /********************************************************************/

Parse Source . . MACRO_NAME .

Address ISPEXEC
'CONTROL ERRORS RETURN'
'ISREDIT MACRO (PARMS)'
If Rc ^=0 Then Do
   ZEDSMSG = MACRO_NAME××' error'
   ZEDLMSG = MACRO_NAME××' can only run as an ISPF EDIT macro !!!'
   'SETMSG MSG(ISRZ001)'
   Exit
   End

'ISREDIT (OWNNAME) = MEMBER'
If MACRO_NAME = OWNNAME Then Do
   ZEDSMSG = 'Recursive call error'
   ZEDLMSG = MACRO_NAME××' is being called recursively ... abort'
   'SETMSG MSG(ISRZ001)'
   Exit
   End

Address ISPEXEC "VGET (ZTEMPF)"

DATANAME = "'"××ZTEMPF××"'"
FILENAME = '$$'××Random(1000,9999)××'$$'

Address TSO "ALLOC DA("××DATANAME××") FILE("××FILENAME××") SHR"

'ISREDIT (STRTLINE) = LINENUM .ZFIRST'
'ISREDIT (ENDLINE) = LINENUM .ZLAST'
'ISREDIT RECOVERY ON'
'ISREDIT RESET'

NUM_LINES = (ENDLINE-STRTLINE)+1

DATA. = ''
Do I=STRTLINE to ENDLINE
   'ISREDIT (MYINPUT) = LINE' I
   DATA.I = MYINPUT
   End
DATA.0 = NUM_LINES

Address TSO
"EXECIO * DISKW" FILENAME "(STEM DATA. FINIS"
Address ISPEXEC

X = OUTTRAP('O.')
Address TSO "FREE F("FILENAME")"
X = OUTTRAP('OFF')

QUOTES = "'"
"MAKEBUF"

If Index(PARMS,QUOTES) > 0 Then Do
   PARM_PART. = ''
   I = 0
   Do Until Index(PARMS,QUOTES) = 0
      I=I+1
      PARM_PART.I = Substr(PARMS,1,Index(PARMS,QUOTES)-1)
      PARMS = Substr(PARMS,Index(PARMS,QUOTES)+1)
      I=I+1
      PARM_PART.I = QUOTES××QUOTES
      End
   If PARMS ^ = '' Then Do    /* if there is data left after last ' */
      I=I+1
      PARM_PART.I = PARMS
      End
   PARMS = ''                 /* Re-construct the parms             */
   Do J=1 To I
      PARMS = PARMS××PARM_PART.J
      End

   End

If PARMS = '' Then  Address TSO "EX" DATANAME
Else Address TSO "EX" DATANAME "'"PARMS"'"

"DROPBUF"

Exit
