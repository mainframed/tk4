 /********************* $SAVE    REXX/CLIST      *********************/
 /* $SAVE                                                            */
 /*    This exec will do update in place using data from the data    */
 /*    being edited.  This keeps a PDS from filling up due to        */
 /*    wasted space.  The restriction here is that the input and     */
 /*    output need to have the SAME NUMBER OF RECORDS.               */
 /*                                                                  */
 /*    Since this is update in place, the exec does require that     */
 /*    the number of lines (records) not being changed.  Obviously,  */
 /*    adding more lines is impossible.  Keeping the same number of  */
 /*    lines now will make it easier if further updates are needed   */
 /*    at a later time.                                              */
 /*                                                                  */
 /* SYNTAX: $SAVE                                                    */
 /*                                                                  */
 /* Author: Vinh Vu                                                  */
 /*    Chg -  11/6/97 - Newly written                                */
 /*                                                                  */
 /********************************************************************/


Parse Source . . MACRO_NAME .

Address ISPEXEC
'CONTROL ERRORS RETURN'
'ISREDIT MACRO'
If Rc ^=0 Then Do
   ZEDSMSG = ''
   ZEDLMSG = MACRO_NAME××' can only run as an ISPF EDIT macro !!!'
   'SETMSG MSG(ISRZ001)'
   Exit
   End

'ISREDIT (MYDSN) = DATASET'

If DSN_NOGOOD(MYDSN) Then Exit

'ISREDIT (MYMEM) = MEMBER'
'ISREDIT (STRTLINE) = LINENUM .ZFIRST'
'ISREDIT (ENDLINE) = LINENUM .ZLAST'
'ISREDIT RECOVERY ON'
'ISREDIT RESET'

NUM_LINES = (ENDLINE-STRTLINE)+1
DATANAME = "'"××MYDSN××"("××MYMEM××")'"
FILENAME = '##'××Random(1000,9999)××Random(10,99)

Address TSO "ALLOC DA("××DATANAME××") FILE("××FILENAME××") SHR"
OLDLINE. = ''
'NEWSTACK'

Address TSO 'EXECIO * DISKR' FILENAME '(STEM OLDLINE. FINIS'
'DELSTACK'
If OLDLINE.0 ^= NUM_LINES Then Do
   ZEDSMSG = ''
   ZEDLMSG = 'Number of records being written out is different than',
             'what is out there.  Update-in-place IS NOT ALLOWED.',
             'Number of old records='××OLDLINE.0××'. Number of new',
             'records='××NUM_LINES××'.'
   'SETMSG MSG(ISRZ001)'
   Address TSO 'FREE FILE('FILENAME')'
   Exit
   End

NEWLINE. = ''
CNT = 0
Do I=STRTLINE to ENDLINE
   'ISREDIT (MYINPUT) = LINE' I
   CNT = CNT + 1
   NEWLINE.CNT = MYINPUT
   End

CNT = 0
'NEWSTACK'
NOT_THE_END = 1
Do While NOT_THE_END
   Address TSO 'EXECIO 1 DISKRU' FILENAME
   TEMPRC = RC
   If TEMPRC=2 Then NOT_THE_END = 0
   Else Do
      If TEMPRC ^= 0 Then Do
         ZEDSMSG = ''
         ZEDLMSG = 'EXECIO Retcode='××TEMPRC××'.',
                   'Partial data was written out ... process aborted.'
         'SETMSG MSG(ISRZ001)'
         Call CLOSE_FILE
         Exit
         End
      Else Do
         Pull
         CNT = CNT + 1
         Queue NEWLINE.CNT
         Address TSO 'EXECIO 1 DISKW' FILENAME
         End
      End
   End
'DELSTACK'

Call CLOSE_FILE

ZEDSMSG = ''
ZEDLMSG = MYMEM××" has been successfully saved in "××MYDSN××". ",
   "Data being edited was updated in place.  However, the",
   "the ISPF stat (i.e. LAST MODIFIED, ID, etc..) did not change and",
   "therefore is no longer correct."

'SETMSG MSG(ISRZ001)'
'ISREDIT CANCEL'

Exit


CLOSE_FILE:
Address TSO 'EXECIO 0 DISKW' FILENAME '(FINIS'
Address TSO 'FREE FILE('FILENAME')'
Return


DSN_NOGOOD:
Arg CHECK_DSN
CHECK_DSN ="'"××Strip(CHECK_DSN)××"'"
X = LISTDSI(CHECK_DSN 'NODIRECTORY NORECALL')
If X=16 Then Do
   If SYSREASON = 9 Then Do
      ZEDSMSG = 'Data set migrated'
      ZEDLMSG = ''
      'SETMSG MSG(ISRZ001)'
      Return 1
      End
   If SYSREASON = 5 Then Do
      ZEDSMSG = 'Dataset not cataloged'
      ZEDLMSG = ''
      'SETMSG MSG(ISRZ001)'
      Return 1
      End
   Else Do
      ZEDSMSG = 'LISTDSI RC=16'
      ZEDLMSG = 'LISTDSI error RC=16 - SYSREASON=' SYSREASON
      'SETMSG MSG(ISRZ001)'
      Return 1
      End
   End
If SYSRECFM = 'F' × SYSRECFM = 'FB' Then Nop
Else Do
   ZEDSMSG = ''
   ZEDLMSG = 'Invalid RECFM as '××SYSRECFM××'.',
             MACRO_NAME 'can only be used to process either F or FB.'
   'SETMSG MSG(ISRZ001)'
   Return 1
   End
If SYSDSORG = 'PO' Then Return 0
Else Do
   ZEDSMSG = ''
   ZEDLMSG = 'Invalid DSORG as '××SYSDSORG××'.',
             MACRO_NAME 'can only be used to update a PDS member.'
   'SETMSG MSG(ISRZ001)'
   Return 1
   End
