$MPFREPL TITLE '$MPFREPL -- WTOR EXIT TO REPLY TO VARIOUS MESSAGES'
*
*
         MACRO
&LABEL   $MSGTAB &JOBNAME,&TEXT,&REPLY,&TYPE=BODY
*
         AIF   ('&TYPE' EQ 'START').START
         AIF   ('&TYPE' EQ 'BODY').BODY
         AIF   ('&TYPE' EQ 'STOP').STOP
         AGO   .BODY
.START   ANOP
         MNOTE *,'*-----------------------------------------------*'
         MNOTE *,'*                                               *'
         MNOTE *,'*   THIS MACRO WILL SET UP THE MESSAGE TABLE:   *'
         MNOTE *,'*                                               *'
         MNOTE *,'*      JOBNAME = JOBNAME THAT GENERATES THE     *'
         MNOTE *,'*          WTORS (OR *)                         *'
         MNOTE *,'*                                               *'
         MNOTE *,'*      TEXT =  THE MESSAGE TEXT PART OF THE     *'
         MNOTE *,'*          WTOR.                                *'
         MNOTE *,'*                                               *'
         MNOTE *,'*      REPLY = THE REPLY USED FOR THIS WTOR     *'
         MNOTE *,'*          (ASSUMING THE JOBNAME AND THE WTOR   *'
         MNOTE *,'*          TEXT MATCH).                         *'
         MNOTE *,'*                                               *'
         MNOTE *,'*      TYPE = BEGIN, BODY OR END.  USED TO      *'
         MNOTE *,'*      INDICATE THE TABLE ENTRY TYPE.           *'
         MNOTE *,'*                                               *'
         MNOTE *,'*   THE RESULTING TABLE ENTRY WILL BE:          *'
         MNOTE *,'*                                               *'
         MNOTE *,'*      DC  AL1(LENG OF JOBNAME)                 *'
         MNOTE *,'*      DC  C"JOBNAME"                           *'
         MNOTE *,'*      DC  AL1(LENG OF WTOR TEXT)               *'
         MNOTE *,'*      DC  C"WTOR TEXT"                         *'
         MNOTE *,'*      DC  AL1(LENG OF REPLY)                   *'
         MNOTE *,'*      DC  C"REPLY"                             *'
         MNOTE *,'*                                               *'
         MNOTE *,'*                                               *'
         MNOTE *,'*-----------------------------------------------*'
&LABEL   DS    0F
         MEXIT
*
.BODY    ANOP
         LCLA &LENG
&LENG    SETA K'&JOBNAME
         AIF  (&LENG LE 8).CKTEXT
         MNOTE 12,'****** JOBNAME IS > 8 CHARACTERS'
         MEXIT
.CKTEXT  ANOP
&LENG    SETA K'&TEXT
         AIF  (&LENG LE 50).CKREPLY
         MNOTE 12,'****** WTOR TEXT IS > 50 CHARACTERS'
         MEXIT
.CKREPLY ANOP
&LENG    SETA K'&REPLY
         AIF  (&LENG LE 52).ITISOK
         MNOTE 12,'****** WTOR REPLY IS > 50 CHARACTERS'
         MEXIT
.ITISOK  ANOP
             DC    AL1(L'$$J&SYSNDX.@)       LENG OF JOBNAME
$$J&SYSNDX.@ DC    C'&JOBNAME'               JOBNAME
             DC    AL1(L'$$T&SYSNDX.@)       LENG OF WTOR TEXT
$$T&SYSNDX.@ DC    C&TEXT                    WTOR TEXT
             DC    AL1(L'$$R&SYSNDX.@)       LENG OF REPLY
$$R&SYSNDX.@ DC    C&REPLY                   REPLY
         MEXIT
*
.STOP    ANOP
         DC     X'00'              END OF TABLE
*
         MEND
         EJECT
*
*
*
*
         PRINT NOGEN
*
$MPFREPL CSECT
$MPFREPL AMODE 31
$MPFREPL RMODE ANY
*
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     BEGINPGM
         DC    C'$MPFREPL'
         DC    C'&SYSDATE',C' &SYSTIME'
*
BEGINPGM EQU   *
         L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ        LOAD POINTER TO THE MSG ATTR
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR   WTOR?
         BNO   RETURN              NO, FORGET IT
         LA    R14,MSGTABLE
*
VERIFY   EQU   *
         CLI   0(R14),X'00'        IS THIS END-OF-TABLE ??
         BE    RETURN              YES - THEN FORGET THIS ...
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R14)          GET LENG OF JOBNAME FOR COMPARE
         BCTR  R15,0               MINUS 1 FOR EXECUTE
*
         CLI   1(R14),C'*'         ARE ANY JOBNAMES ALLOWED ??
         BE    JOBOKAY             YES - THEN GO ON FOR MORE CHECKIN'
         EX    R15,CLCJOBNM        CHECK FOR MATCHING JOBNAME
         BE    JOBOKAY             IF EQUAL THEN GO ON
         LA    R14,2(R15,R14)      GO PAST JOBNAME
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R14)          GET LENG OF WTOR TEXT
         LA    R14,1(R15,R14)      GO PAST WTOR TEXT
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R14)          GET LENG OF REPLY TEXT
         LA    R14,1(R15,R14)      GO PAST REPLY TEXT
         B     VERIFY              BACK TO VERIFY EVERYTHING
*
CLCJOBNM CLC   CTXTJBNM(0),1(R14)  COMPARE JOBNAMES
CLCTEXT  CLC   CTXTTMSG(0),1(R14)  COMPARE WTOR TEXT
MVCREPLY MVC   3(0,R14),1(R9)      MOVE REPLY TO COMMAND AREA
*
JOBOKAY  EQU   *
         LA    R14,2(R15,R14)      GO PAST JOBNAME
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R14)          GET LENG OF WTOR TEXT
         BCTR  R15,0               MINUS 1 FOR EXECUTE
         EX    R15,CLCTEXT         CHECK FOR MATCHING TEXT
         BE    TEXTOKAY            IF EQUAL THEN GO ON
         LA    R14,2(R15,R14)      GO PAST WTOR TEXT
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R14)          GET LENG OF REPLY TEXT
         LA    R14,1(R15,R14)      GO PAST REPLY TEXT
         B     VERIFY              BACK TO VERIFY EVERYTHING
*
TEXTOKAY EQU   *
         LA    R9,2(R15,R14)       GO PAST WTOR TEXT (SAVE ADDR=R9)
*
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLYD),REPLYD   MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+REPLYFIX(L'CTXTRPYI),CTXTRPYI
         LA    R14,MGCRTEXT+REPLYFIX+L'CTXTRPYI GO TO END OF REPLYID
*
FINDNONB EQU   *
         BCTR  R14,0               SUB 1
         CLI   0(R14),C' '         IS THIS BLANK STILL ??
         BE    FINDNONB            YES - THEN BACK AGAIN
*
         MVI   1(R14),C','
         MVI   2(R14),C''''        MOVE SINGLE QUOTE THERE
         SR    R15,R15             CLEAR R15 FIRST
         IC    R15,0(R9)           GET LENG OF WTOR REPLY
         BCTR  R15,0               MINUS 1 FOR EXECUTE
         EX    R15,MVCREPLY        MOVE REPLY TO CMD AREA
         LA    R14,4(R15,R14)      GO PAST THE END OF REPLY
         MVI   0(R14),C''''        MOVE SINGLE QUOTE (ENDING)
*
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLYD
         STC   R3,MGCRLGTH         SET THE LENGTH OF THE REPLY
         SR    R0,R0
         MGCR  MGCRPL
         L     R10,4(R13)          RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   EQU   *
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
*
*
*
REPLYD   DC    CL80'REPLY '
REPLYFIX EQU   6
*
*
MSGTABLE $MSGTAB TYPE=START
         $MSGTAB DASDMNT,'ICK003D REPLY U TO ALTER VOLUME 0E','U'
         $MSGTAB DASDMNT,'ICK003D REPLY U TO ALTER VOLUME 0F','U'
         $MSGTAB *,'TESTING','U'
         $MSGTAB $VNV1,'FLIDC000I ENTER FLCACHE','QUIT'
         $MSGTAB $VNV1,'FLIDC000I STARTING DASD','NO'
         $MSGTAB VNV1,'FLIDC000I STARTING DASD','YES'
         $MSGTAB TYPE=STOP
*
         LTORG
         PRINT   GEN
*
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
*
         IEZVX100
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END   $MPFREPL
