         MACRO
&NAME    MFORM   &X
         GBLC    &NAMEX
         LCLA    &I,&J,&K
         LCLC    &C
         AIF     ('&NAME' EQ '').GO
&NAME    DS    0C               START OF SCREEN
&NAMEX   SETC    '&NAME'
.GO      AIF     (&I EQ N'&SYSLIST).END
&I       SETA    &I+1
         AIF     (N'&SYSLIST(&I) EQ 2).ADD
         AIF     (N'&SYSLIST(&I) EQ 3).ADD
         AIF     ('&SYSLIST(&I)'(1,2) EQ 'A''').WCC
         AIF     ('&SYSLIST(&I)'(1,2) EQ 'W''').WCC
         AIF     ('&SYSLIST(&I)'(1,1) EQ '''').CON
         AIF     ('&SYSLIST(&I)' EQ 'NEND').MEXIT
         DC    AL1(&SYSLIST(&I))
         AGO     .GO
.CON     DC    C&SYSLIST(&I)
         AGO     .GO
.WCC     ANOP    ,
&J       SETA    1
&K       SETA    0
.SC      AIF     (&J EQ K'&SYSLIST(&I)).GEN
&J       SETA    &J+1
&C       SETC    '&SYSLIST(&I)'(&J,1)
         AIF     ('&C' EQ '''').SC
         AIF     ('&C' EQ ' ').SC
         AIF     ('&C' NE 'R').NOR
&K       SETA    &K+1
         AGO     .SC
.NOR     AIF     ('&C' NE 'K').NOK
&K       SETA    &K+2
         AGO     .SC
.NOK     AIF     ('&C' NE 'A').NOA
&K       SETA    &K+4
         AGO     .SC
.NOA     AIF     ('&C' NE 'P').NOP
&K       SETA    &K+32
         AGO     .SC
.NOP     AIF     ('&C' NE 'S').NOS
&K       SETA    &K+48
         AGO     .SC
.NOS     AIF     ('&C' NE 'N').NON
&K       SETA    &K+16
         AGO     .SC
.NON     AIF     ('&C' NE 'H').NOH
&K       SETA    &K+8
         AGO     .SC
.NOH     AIF     ('&C' NE 'O').NOO
&K       SETA    &K+12
         AGO     .SC
.NOO     AIF     ('&C' NE 'M').NOM
&K       SETA    &K+1
         AGO     .SC
.NOM     MNOTE   8,'INVALID CODE ''&C'''
         AGO     .SC
.GEN     AIF     ('&SYSLIST(&I)' EQ 'W').GWCC
         MSIXBIT &K,,SF
         AGO     .GO
.GWCC    MSIXBIT &K
         AGO     .GO
.ADD     AIF     (&SYSLIST(&I,1) GT 24).AERR1
         AIF     (&SYSLIST(&I,2) GT 80).AERR2
&J       SETA    (&SYSLIST(&I,1)-1)*80+&SYSLIST(&I,2)-1
&K       SETA    &J/64
&J       SETA    &J-&K*64
         AIF     (N'&SYSLIST(&I) EQ 3).SPE
         MSIXBIT &K,&J,SBA
         AGO     .GO
.AERR1   MNOTE   8,'INVALID SCREEN ADDRES ''&SYSLIST(&I,1)'''
         AGO     .GO
.AERR2   MNOTE   8,'INVALID SCREEN ADDRESS ''&SYSLIST(&I,2)'''
         AGO     .GO
.SPE     MSIXBIT &K,&J,&SYSLIST(&I,3)
         AGO     .GO
.END     AIF     ('&NAMEX' EQ '').MEXIT
&NAMEX.L EQU     *-&NAMEX
&NAMEX   SETC    ''
.MEXIT   MEND
