         MACRO
&LABEL   $BLDLIST &PARML,&PARM,&VL
.*
.*-----------------------------------------------------------------
.*
.*       $BLDLIST       VINH VU         09/30/81
.*       BUILDS A PARM LIST FOR REENTRANT CODE
.* PARMS -
.*   &PARML - NAME OF AN AREA TO CONTAIN THE PARMLIST.
.*            SYMBOL OR (REG).
.*   &PARM  - NAMES OF PARMS. THEIR ADDRESSES ARE PLACED IN PARMLIST.
.*            SYMBOL OR (REG).
.*   &VL    - CAUSES LAST PARM ADDRESS TO BE MARKED WITH X'80'.
.*
.*       R14 GETS ALTERED
.*       R1  GETS A(&PARML)
.*
.*       CHG - 2/17/95 - $VNV - CHANGE THE 'MVI' TO 'OI' (XA-SUPPORT)
.*
.*-----------------------------------------------------------------
.*
         LCLA  &SUB,&DISP,&LEN
         LCLC  &CDISP,&REG
&SUB     SETA  1
         AIF   ('&PARML'(1,1) NE '(').NOTREG1
&LEN     SETA  K'&PARML-2
&REG     SETC  '&PARML'(2,&LEN)
&LABEL   LR    1,&REG
         AGO   .LOOP
.NOTREG1 ANOP
&LABEL   LA    1,&PARML
.LOOP    ANOP
&CDISP   SETC  '&DISP'
         AIF   ('&PARM(&SUB)'(1,1) NE '(').NOTREG
&LEN     SETA  K'&PARM(&SUB)-2
&REG     SETC  '&PARM(&SUB)'(2,&LEN)
         ST    &REG,&CDISP.(1)
         AGO   .INCR
.NOTREG  ANOP
         LA    14,&PARM(&SUB)
         ST    14,&CDISP.(1)
.INCR    ANOP
&SUB     SETA  &SUB+1
&DISP    SETA  &DISP+4
         AIF   (&SUB LE N'&PARM).LOOP
         AIF   (T'&VL EQ 'O').CONT
         OI    &CDISP.(1),X'80'
.CONT    MEND
