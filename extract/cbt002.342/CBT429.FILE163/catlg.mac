//CATLG    JOB
//*
//*  CATLG, UNCATLG AND SCRATCH -
//*    THESE TSO COMMANDS DO THE SAME THING THAT THE
//*    CORRESPONDING IEHPROGM CONTROL STATEMENTS DO.
//*    YOU CAN CATALOG AND UNCATALOG DATASETS TO
//*    ARBITRARY VOLUMES AND UNITS, AND SCRATCH DATASETS
//*    ON DIRECT ACCESS VOLUMES WITHOUT REGARD TO THEIR
//*    CATALOG STATUS.
//*
//*  INSTALLATION -
//*    MODIFY THE JOB CARD ABOVE AND THE PROCEDURE DEFAULTS
//*    BELOW TO SUIT YOUR TASTE.  IF YOU WANT TO CHANGE THE
//*    COMMAND NAME AND ALIASES YOU WILL HAVE TO DOCTOR THE
//*    JOBSTREAM.
//*
//INSTALL PROC SOUT=$,                 <=== SYSOUT CLASS
//             LIB='SYS1.CMDLIB',      <=== TARGET LOAD LIBRARY
//             HELP='SYS1.HELP',       <=== HELP LIBRARY
//             SYSTS=SYSALLDA,         <=== UNITNAME FOR WORK DATASETS
//             ASMBLR=IEV90,           <=== NAME OF YOUR ASSEMBLER
//             ALIB='SYS1.LINKLIB',    <=== LOCATION OF YOUR ASSEMBLER
//             SMPMTS='SYS1.SMPMTS',   <=== SMPMTS DATASET NAME
//             MACLIB='SYS1.MACLIB',   <=== MACLIB DATASET NAME
//             AMODGEN='SYS1.AMODGEN', <=== AMODGEN DATASET NAME
//             MODGEN='SYS1.MODGEN'    <=== MODGEN DATASET NAME
//*                                         USE SYS1.SMPMTS FOR MVS-370
//*
//IEBUPDTE EXEC PGM=IEBUPDTE,PARM=NEW
//SYSPRINT DD  SYSOUT=&SOUT
//SYSUT1   DD  DSN=&HELP,DISP=SHR
//SYSUT2   DD  DSN=&HELP,DISP=SHR
//*
//ASM     EXEC PGM=&ASMBLR,REGION=2048K,PARM='NOOBJECT,DECK,NOALIGN'
//STEPLIB  DD  DSN=&ALIB,DISP=SHR
//SYSTERM  DD  SYSOUT=&SOUT
//SYSPRINT DD  SYSOUT=&SOUT
//SYSLIB   DD  DSN=&MODGEN,DISP=SHR
//         DD  DSN=&SMPMTS,DISP=SHR
//         DD  DSN=&MACLIB,DISP=SHR
//         DD  DSN=&AMODGEN,DISP=SHR
//SYSUT1   DD  UNIT=&SYSTS,SPACE=(TRK,(15,1))
//SYSUT2   DD  UNIT=&SYSTS,SPACE=(TRK,(15,1))
//SYSUT3   DD  UNIT=&SYSTS,SPACE=(TRK,(15,1))
//SYSPUNCH DD  DSN=&&SYSLIN,UNIT=&SYSTS,DISP=(,PASS,DELETE),
//             SPACE=(TRK,(5,1),RLSE)
//*
//LKED    EXEC PGM=HEWL,COND=(0,NE),PARM='LIST,MAP,XREF'
//SYSPRINT DD  SYSOUT=&SOUT
//SYSUT1   DD  UNIT=&SYSTS,SPACE=(TRK,5)
//SYSLMOD  DD  DSN=&LIB,DISP=SHR
//SYSLIN   DD  DSN=&&SYSLIN,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//        PEND
//*
//        EXEC INSTALL
//IEBUPDTE.SYSIN DD *
./         ADD    NAME=CATLG,LIST=ALL
./         NUMBER NEW1=1000,INCR=1000
)F FUNCTION -
             THIS PROGRAM PERFORMS CATLG FUNCTIONS SIMILAR TO IEHPROGM.

  AUTHOR -
             DAVE PHILLIPS
             A. DUDA AND SONS, INC.
             P.O. BOX 257
             OVIEDO, FL  32765

)X SYNTAX -
      CATLG  dsname                     {REQUIRED}
             VOL(volser)                {REQUIRED}
             UNIT(unitname)             {OPTIONAL}

)O OPERANDS -
))DSNAME
           - REQUIRED.  DATASET NAME TO BE CATALOGED.  NORMAL TSO
             DATASET NAMING CONVENTIONS APPLY.
))VOL(volser)
           - REQUIRED.  VOLUME SERIAL YOU WANT THE DATASET CATALOGED
             ON.
))UNIT(unitname)
           - OPTIONAL.  IF THE VOLUME SERIAL IS THAT OF A CURRENTLY
             MOUNTED DIRECT-ACCESS VOLUME, THE DEFAULT VALUE IS
             TAKEN FROM THE VOLUME'S UCB.  OTHERWISE THE OPERAND
             IS REQUIRED.
./         ADD    NAME=SCRATCH,LIST=ALL
./         NUMBER NEW1=1000,INCR=1000
./         ALIAS  NAME=SCR
)F FUNCTION -
             THIS PROGRAM PERFORMS SCRATCH FUNCTIONS SIMILAR TO
             IEHPROGM.  THE CATALOG IS NOT AFFECTED.

  AUTHOR -
             DAVE PHILLIPS
             A. DUDA AND SONS, INC.
             P.O. BOX 257
             OVIEDO, FL  32765

)X SYNTAX -
      SCRATCH dsname                     {REQUIRED}
              VOL(volser)                {OPTIONAL}

   ALIAS - SCR

)O OPERANDS -
))DSNAME
           - REQUIRED.  DATASET NAME TO BE SCRATCHED.  NORMAL TSO
             DATASET NAMING CONVENTIONS APPLY.
))VOL(volser)
           - OPTIONAL.  VOLUME SERIAL YOU WANT THE DATASET SCRATCHED
             FROM.  IF NOT SPECIFIED, THE dataset MUST BE CATALOGED,
             AND THE VOLUME SERIAL IS TAKEN FROM THE CATALOG ENTRY.
./         ADD    NAME=UNCATLG,LIST=ALL
./         NUMBER NEW1=1000,INCR=1000
./         ALIAS  NAME=UNCAT
)F FUNCTION -
             THIS PROGRAM PERFORMS UNCATLG FUNCTIONS SIMILAR TO
             IEHPROGM.  THE TARGET DATASET IS NOT SCRATCHED.

  AUTHOR -
             DAVE PHILLIPS
             A. DUDA AND SONS, INC.
             P.O. BOX 257
             OVIEDO, FL  32765

)X SYNTAX -
      UNCATLG dsname                     {REQUIRED}

   ALIAS - UNCAT

)O OPERANDS -
))DSNAME
           - REQUIRED.  DATASET NAME TO BE SCRATCHED.  NORMAL TSO
             DATASET NAMING CONVENTIONS APPLY.
./        ENDUP
/*
//ASM.SYSIN DD *
CATLG    TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          HOUSEKEEPING AN*
               D PARSE COMMAND BUFFER'
*
* FUNCTION -
*            THIS PROGRAM PERFORMS CATLG,UNCATLG AND SCRATCH FUNCTIONS
*            SIMILAR TO IEHPROGM.
*
* AUTHOR -
*            DAVE PHILLIPS
*              A. DUDA AND SONS, INC.
*              P.O. BOX 257
*              OVIEDO, FL  32765
*
* CHANGES OCTOBER 1990 (DAVID ANDREWS) -
*            THE UCB LOOKUP TABLE WENT AWAY IN MVS/XA.  ADDED SOME
*            CODE TO USE THE IOSVSUCB UCB SCAN ROUTINE INSTEAD.
*
* CHANGES NOVEMBER 1990 (DAVID ANDREWS) -
*            OOPS.  DEVICE NAME TABLE WENT AWAY TOO.  ADDED CODE
*            TO USE XA UNIT VERIFICATION SERVICES.  WHATTA PAIN.
*
         SPACE
         ENTRY UNCATLG
         ENTRY UNCAT
         ENTRY SCRATCH
         ENTRY SCR
         SPACE
CATLG    CSECT
         SPACE
         SAVE  (14,12),,CATLG-&SYSDATE-&SYSTIME
         SPACE
         LA    R12,START-CATLG(,R15)      LOAD BASE REGISTER
         USING START,R12
         LA    R2,EPCAT                   REMEMBER ENTRY POINT
         L     R3,=V(CATPARS)             -> PARSE TABLE
         BR    R12                        TO COMMON CODE
         SPACE 2
UNCATLG  SAVE  (14,12),,*
         SPACE
         LA    R12,START-UNCATLG(,R15)    LOAD BASE REGISTER
         LA    R2,EPUCAT                  REMEMBER ENTRY POINT
         L     R3,=V(UCATPARS)            -> PARSE TABLE
         BR    R12                        TO COMMON CODE
UNCAT    EQU   UNCATLG
         SPACE 2
SCRATCH  SAVE  (14,12),,*
         SPACE
         LA    R12,START-SCRATCH(,R15)    LOAD BASE REGISTER
         LA    R2,EPSCR                   REMEMBER ENTRY POINT
         L     R3,=V(SCRPARS)             -> PARSE TABLE
SCR      EQU   SCRATCH
         EJECT
         SPACE
START    LR    R6,R1              SAVE PARM
         USING CPPL,R6
         LA    R0,CORESIZE
         GETMAIN R,LV=(0)         GET A WORK AREA
         ST    R13,4(,R1)         )
         ST    R1,8(,R13)         ) CHAIN SAVE AREAS
         LR    R13,R1             )
         USING CORE,R13
         SPACE 2
* BUILD ALL SERVICE ROUTINE PARAMETER BLOCKS & LISTS
         SPACE
         STC   R2,FLAGS           SET EP TYPE
         LA    R5,PPLSECT         -> PPL
         USING PPL,R5
         ST    R3,PPLPCL          -> PARSE PCL
         L     R1,CPPLUPT         -> UPT
         L     R2,CPPLECT         -> ECT
         SR    R3,R3
         ST    R3,ECB             ZERO ECB
         LA    R3,ECB             -> ECB
         STM   R1,R3,PPLSECT      PUT ADDR OF UPT,ECT,ECB IN PPL
         SPACE
         LA    R2,PARSBACK        -> PARSE ANSWER AREA
         ST    R2,PPLANS          IN PPL
         L     R1,CPPLCBUF        CBUF PTR IN PPL
         ST    R1,PPLCBUF
         ST    R13,PPLUWA         PASS ADDR OF WORKAREA TO VALID CHK
         SPACE
         XC    MTPARML(MTLENMT),MTPARML  ZERO IKJEFF02 PARM LIST
         LA    R1,MTCSECTP        -> MSG DESC SECTION OF PARMLIST
         ST    R1,MTPLPTR         IN PARM LIST
         ST    R6,MTCPPLP         PTR TO CPPL FOR IKJEFF02
         ST    R3,MTECBP          PTR TO ECB  FOR IKJEFF02
         SR    R1,R1              ZERO RESERVED
         ST    R1,MTRESV1         WORD IN IKJEFF02 PARM LIST
         MVI   MTHIGH,X'80'       SET VL BIT IN IKJEFF02 PARM LIST
         SPACE
         L     R1,=V(MSGCSECT)    PTR TO MSG CSECT
         ST    R1,MTCSECTP        IN MSG DESC SECTION
         MVI   MTSW1,MTPUTLSW     ISSUE MSGS WITH PUTLINE
         MVI   MTSW2,0
         SPACE
         DROP  R5,R6
         EJECT
* CALL PARSE SERVICE ROUTINE TO GET PARAMETERS
         SPACE
CALLPARS CALLTSSR EP=IKJPARS,MF=(E,PPLSECT)
         SPACE
         LTR   R15,R15            PARSE OK ?
         BZ    PARSEOK            YES
         SPACE
         CH    R15,=H'4'          USER PROFILE NOPROMPT ?
         BE    RETURN             YES - GO AWAY
         SPACE
         LA    R1,GFPARSE         ROUTINE ID FOR GENERAL FAIL
         B     GNRLFAIL           GET GENERAL FAIL TO DIAGNOSE ERROR
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          PROCESS PARMS R*
               ETURNED BY PARSE'
PARSEOK  TM    FLAGS,EPUCAT       UNCATLG ?
         BO    PARMSET            PARMS ALL SET
         SPACE
         L     R9,PARSBACK        -> PDL AREA
         SPACE
* MAKE SURE VOLSER HAS BEEN SUPPLIED
         SPACE
         TM    FLAGS,SVOL         VOLSER SUPPLIED ?
         BO    GOTVOL             YEP
         TM    FLAGS,EPSCR        SCRATCH ?
         BNO   NEEDVOL            NO - MUST BE CATLG AND IT NEEDS VOL
         SPACE
* ISSUE LOCATE TO FIND VOLUME(S) FOR SCRATCH
         SPACE
         LA    R3,DSNAME          -> DSNAME
         ST    R3,CAMLSTP2
         SR    R3,R3              NO CVOL PARM
         ST    R3,CAMLSTP3
         LA    R3,CAMAREA         -> AREA TO PUT VOLUME LIST
         ST    R3,CAMLSTP4
         MVC   CAMLST,NAMECAM     CAMLST NAME FLAGS
         LOCATE CAMLST            LOCATE DSNAME IN CATALOG
         SPACE
         LTR   R15,R15            LOCATE OK ?
         BNZ   BADLOC             NOPE
         SPACE
         B     SCRPRM             GO FINISH OFF SCRATCH PARAMETERS
         SPACE
NEEDVOL  MVC   MTMSGID,=CL4'V04'  "VOLSER REQUIRED"
         OI    FLAGS,QUIT         NO GO
         LA    LINK,PARMSET
         B     ISSUEMSG
         SPACE
GOTVOL   TM    FLAGS,SUNIT        UNIT SUPPLIED ?
         BNO   UCBLOOK            NO - LOOK IT UP IN UCB
         SPACE
         CLI   DEVTYPE+2,X'20'    DASD DEVICE SPECIFIED ?
         BNE   SUCBLOOK           NOPE - SKIP LOOKUP
         SPACE
* LOOK UP UCB FOR GIVEN VOLSER TO GET DEVICE TYPE
         SPACE
UCBLOOK  L     R3,CVTPTR          -> CVT
         USING CVTMAP,R3
         XC    IOSVSWRK,IOSVSWRK  INITIALIZE IOSVSUCB WORKAREA 10/16/90
         LA    R1,IOSVSWRK        BUILD IOSVSUCB PARM LIST     10/16/90
         ST    R1,IOSVSPRM             "                       10/16/90
         LA    R1,IOSVSDEV             "                       10/16/90
         ST    R1,IOSVSPRM+4           "                       10/16/90
         LA    R1,IOSVSUCB             "                       10/16/90
         ST    R1,IOSVSPRM+8           "                       10/16/90
         OI    IOSVSPRM+8,X'80'        "                       10/16/90
         MVI   IOSVSDEV,UCB3DACC       "                       10/16/90
UCBLOOP  L     R15,CVTUCBSC       -> IOSVSUCB UCB SCAN RTN     10/16/90
         LA    R1,IOSVSPRM        -> IOSVSUCB PARAMETER LIST   10/16/90
         BALR  R14,R15            GET POINTER TO NEXT UCB      10/16/90
         LTR   R15,R15            ANYMORE UCBS?                10/16/90
         BNZ   VOLUNAV            NO, VOLUME NOT MOUNTED       10/16/90
         L     R2,IOSVSUCB        POINT TO UCB                 10/16/90
         USING UCB,R2                  "                       10/16/90
         AGO   .BYPASS1                                        10/16/90
         L     R3,CVTILK2         -> UCB ADDR TABLE
         DROP  R3
         SR    R2,R2
         USING UCB,R2
UCBLOOP  ICM   R2,B'0011',0(R3)   -> UCB
         LA    R3,2(,R3)          -> NEXT UCB PTR
         BZ    UCBLOOP            NEXT UCB IF NULL
         CL    R2,=X'0000FFFF'    END OF TABLE ?
         BE    VOLUNAV            YES - VOLUME NOT MOUNTED
         TM    UCBTYP+2,X'20'     DASD ?
         BNO   UCBLOOP            NO
.BYPASS1 ANOP  ,                                               10/16/90
         CLC   VOLSER,UCBVOLI     THIS THE VOLUME ?
         BNE   UCBLOOP            NO
         DROP  R3                 FORGET ABOUT CVT             10/16/90
         SPACE
GOTUCB   TM    FLAGS,SUNIT        UNIT SUPPLIED ??
         BNO   GOTUCB2            NO - FILL IN DEVTYPE
         CLC   DEVTYPE,UCBTYP     YES - SAME AS MOUNTED VOL ?
         BE    SUCBLOOK           YES - ALL OK
         OI    FLAGS,SKIPOBTN     NOT SAME - BYPASS OBTAIN
         B     SUCBLOOK
         SPACE
GOTUCB2  MVC   DEVTYPE,UCBTYP     SAVE DEVICE TYPE
         B     SUCBLOOK           SKIP ERROR EXIT
         DROP  R2
         SPACE
VOLUNAV  DS    0H                 VOLUME UNAVAILBLE
         TM    FLAGS,SUNIT        UNIT SUPPLIED ?
         BNO   *+12               NO - ISSUE ERROR MSG
         OI    FLAGS,SKIPOBTN     YES - BYPASS OBTAIN
         B     SUCBLOOK
         MVC   MTMSGID,=CL4'V02'  VOLUME NOT MOUNTED MSG
         TM    FLAGS,EPSCR        SCRATCH ?
         BNO   *+10               NOPE - MSGID IS RIGHT
         MVC   MTMSGID,=CL4'V03'  YES - FIX MSGID
         SPACE
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT         PASS TO IKJEFF02
         MVI   MTINSRT,L'VOLSER   LENGTH
         OI    FLAGS,QUIT
         LA    LINK,PARMSET       RETURN ADDR
         B     ISSUEMSG
         SPACE
SUCBLOOK DS    0H                 SKIP UCB LOOKUP
         SPACE
         TM    FLAGS,EPSCR        SCRATCH EP ?
         BO    SCRPRM             YES
         SPACE
* BUILD VOLUME LIST FOR CATALOG MACRO
         SPACE
         USING CATPARM,R9         DSECT
         LA    R8,CAMAREA+2       -> AREA FOR VOLUME LIST
         USING VOLLIST,R8         DESCRIPTION OF VOLUME LIST
         SPACE
VPTR     EQU   5                  VOLUME PDE LIST REGISTER
UPTR     EQU   6                  UNIT PDE LIST REGISTER
FPTR     EQU   7                  FILESEQ PDE LIST REGISTER
         SPACE
         LA    VPTR,CATVOLIN      -> VOLUME PDE LIST
         LA    UPTR,CATUNTIN      -> UNIT TYPE PDE LIST
         LA    FPTR,CATFSQIN      -> FILE SEQUENCE NUMBER PDE LIST
         SR    R4,R4              COUNT NUMBER OF VOLUMES
         SPACE 2
VOLLOOP  MVI   CATVOL,C' '        BLANK OUT VOLSER FIELD
         MVC   CATVOL+1(L'CATVOL-1),CATVOL
         L     R1,0(,VPTR)        -> VOLUME
         LH    R2,4(,VPTR)        LENGTH
         BCTR  R2,0               -1 FOR EX
         LA    R3,CATVOL          -> DESTINATION
         EX    R2,MOVEPARM        MOVE IT IN
*        MVC   0(*-*,R3),0(R1)
         L     VPTR,8(,VPTR)      -> NEXT VOLUME PDE
         SPACE
*                                                              11/16/90
*  I'M NOT SURE WHY THE FOLLOWING CODE IS HERE.  IT CAUSED     11/16/90
*  A MESS WHEN I PUT IN THE IEFEB4UV MODS FOR XA.  DELETE      11/16/90
*  IT AND SEE WHAT HAPPENS...   DBA                            11/16/90
*                                                              11/16/90
*+*      LTR   UPTR,UPTR          ANY UNIT TYPE ?              11/16/90
*+*      BZ    V1UNIT             NO - USE UNIT & DEVTYPE FLDS 11/16/90
*+*      TM    6(UPTR),X'80'      UNIT TYPE SUPPLIED ?         11/16/90
*+*      BNO   V1UNIT             NO - IN UNIT & DEVTYPE FLDS  11/16/90
*+*      SPACE ,                                               11/16/90
*+*      L     R1,0(,UPTR)        -> DEVNAMET ENTRY            11/16/90
*+*      MVC   DEVCODE,8(R1)      GET DEVICE TYPE              11/16/90
*+*      L     UPTR,8(,UPTR)      YES - GET NEXT PDE           11/16/90
*+*      LA    UPTR,0(,UPTR)      CLEAR HIGH BYTE              11/16/90
*+*      B     VFSEQN             HAVE UNIT TYPE NOW           11/16/90
         SPACE
V1UNIT   MVC   DEVCODE,DEVTYPE    GET DEVICE TYPE
         SPACE
VFSEQN   TM    6(FPTR),X'80'      FILE SEQ SUPPLIED ?
         BNO   VNFSEQ             NO - DETERMINE WHAT IT SHOULD BE
         SPACE
         L     R1,0(,FPTR)        -> FILE SEQ NUM
         L     R1,0(,R1)          FILE SEQ NUM
         STH   R1,DSEQNUM         PUT IN LIST
         CLI   8(FPTR),X'FF'      ANY MORE FILE SEQ NUM ??
         BE    NXTVOL             NO - SKIP CHAINING
         L     FPTR,8(,FPTR)      YES - GET IT
         B     NXTVOL             TO NEXT ENTRY
         SPACE
VNFSEQ   SR    R1,R1              ASSUME ZERO FILE SEQ NUMBER
         CLI   DEVCODE+2,X'80'    MAG TAPE DEVICE ?
         BNE   *+8                NOPE
         LA    R1,1               YES - FSEQ NUM OF ONE
         STH   R1,DSEQNUM         PUT IN LIST
         SPACE 2
NXTVOL   LA    R4,1(,R4)          COUNT THEM VOLS
         CH    R4,=Y(#VOLS)       MORE THAN MAX VOLUMES ?
         BH    VOLHIGH            TOO MUCH
         LA    R8,NEXTVOL         -> NEXT VOLUME LIST ENTRY
         LA    VPTR,0(,VPTR)      CLEAR HIGH BYTE
         LTR   VPTR,VPTR          ANY MORE VOLS ?
         BNZ   VOLLOOP            YES - GET THEM
         SPACE
         STH   R4,CAMAREA         STORE VOLUME COUNT
         B     PARMSET            DONE WITH CATLG PARAMETERS
         SPACE
VOLHIGH  BCTR  R4,0               -1
         STH   R4,CAMAREA         STORE VOLUME COUNT
         SPACE
         MVC   MTMSGID,=CL4'V01'        TOO MANY VOLUMES MSG
         LA    LINK,PARMSET             RETURN ADDR
         B     ISSUEMSG                 TELL USER ABOUT IT
         SPACE
         DROP  R8
         EJECT
* PROCESS SCRATCH PARAMETERS
         SPACE
         USING SCRPARM,R9         DSECT
SCRPRM   CLI   SCRPRGKY+1,0       PURGE SPECIFIED ?
         BE    PARMSET            NOPE - ALL DONE
         OI    FLAGS,SCRPURGE     YES - REMEMBER THAT
         SPACE
         DROP  R9                 FINISHED WITH PDL
         SPACE 4
PARMSET  IKJRLSA PARSBACK          RELEASE PARSE STORAGE
         SPACE 2
         TM    FLAGS,QUIT         ANY ERRORS ?
         BO    RETURN             YES - HANG IT UP
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          PROCESSING'
         TM    FLAGS,EPUCAT+EPSCR+SKIPOBTN   SCR,UNCAT OR SKIP OBTN ?
         BNZ   BLDCAML            YEP - BYPASS OBTAIN
         SPACE
         CLI   DEVTYPE+2,X'20'    DASD DEVICE ???
         BNE   BLDCAML            NOPE - BYPASS OBTAIN
         SPACE
* VERIFY DATASET IS ON VOLUME SPECIFIED
         SPACE
         LA    R3,DSNAME          -> DSNAME
         ST    R3,CAMLSTP2
         LA    R3,VOLSER          -> VOLUME ID
         ST    R3,CAMLSTP3
         LA    R3,F1DSCB          -> AREA FOR F1 DSCB
         ST    R3,CAMLSTP4
         MVC   CAMLST,SRCHCAM     CAMLST SEARCH FLAGS
         OBTAIN CAMLST            GET THE DSCB FOR DATASET
         SPACE
         LTR   R15,R15            OBTAIN OK ???
         BNZ   BADOBTN            NOPE
         SPACE 2
         EJECT
* BUILD CAMLST FOR REQUIRED FUNCTION
         SPACE
BLDCAML  TM    FLAGS,EPSCR        SCRATCH EP ?
         BO    SCRCAML            YES - BUILD SCRATCH CAMLST
         SPACE
CATCAML  LA    R3,DSNAME          -> DSNAME
         ST    R3,CAMLSTP2
         SR    R3,R3              NO CVOL PARM
         ST    R3,CAMLSTP3
         LA    R3,CAMAREA         -> VOLUME LIST
         ST    R3,CAMLSTP4
         MVC   CAMLST,CATCAM      ASSUME CATBX CAMLST
         TM    FLAGS,EPCAT        RIGHT ?
         BO    *+10               YES
         MVC   CAMLST,UCATCAM     NO - UNCATALOG
         SPACE
         CATALOG CAMLST           PERFORM THE FUNCTION
         SPACE
         LTR   R15,R15            CATALOG OK ??
         BNZ   BADCAT             NOPE
         SPACE
         B     CONFIRM            GO WRITE CONFIMATION MSG
         SPACE
SCRCAML  TM    FLAGS,SVOL         WAS VOLUME SUPPLIED BY USER ?
         BNO   SCRCAML2           NO - VOL LIST WAS BUILT BY LOCATE
         LA    R1,1               ONE VOLUME
         STH   R1,CAMAREA         IN VOLUME LIST
         LA    R5,CAMAREA+2       -> VOLUME LIST ENTRY
         USING VOLLIST,R5         DSECT
         MVC   DEVCODE,DEVTYPE    DEVICE TYPE
         MVC   CATVOL,VOLSER      VOLSER
         SR    R1,R1              ZERO FILE SEQ NUM
         STH   R1,DSEQNUM         STORE IT
         DROP  R5
         SPACE
SCRCAML2 LA    R3,DSNAME          -> DSNAME
         ST    R3,CAMLSTP2
         SR    R3,R3              NO THIRD PARM
         ST    R3,CAMLSTP3
         LA    R3,CAMAREA         -> VOLUME LIST
         ST    R3,CAMLSTP4
         MVC   CAMLST,SCRCAM      ASSUME NOPURGE CAMLST
         TM    FLAGS,SCRPURGE     RIGHT ?
         BNO   *+10               YES
         MVC   CAMLST,SCRPCAM     NO - SCRATCH PURGE
         SPACE
         SR    R0,R0              DON'T MOUNT ANY VOLUMES
         SCRATCH CAMLST           PERFORM THE FUNCTION
         SPACE
         LTR   R15,R15            SCRATCH OK ??
         BNZ   BADSCR             NOPE
         SPACE
         TM    FLAGS,SVOL         DID USER SUPPLY VOLUME FOR SCRATCH ?
         BO    CONFIRM            YES - ALL DONE WITH SCRATCH THEN
         SPACE
         MVC   CAMLST,UCATCAM     OTHERWISE NEED TO UNCATALOG DS ALSO
         CATALOG CAMLST           DO IT     (PARM LIST SAME AS SCRATCH)
         SPACE
         LTR   R15,R15            UNCATALOG OK ?
         BNZ   BADCAT             NOPE
         EJECT
* WRITE CONFIRMATION MESSAGE TO USER
         SPACE
CONFIRM  DS    0H                 WRITE OUT CONFIRMATION MSG
         SPACE
         LA    R1,DSNAME          -> DSNAME
         ST    R1,MTINSRT         1ST VARIBLE
         MVC   MTINSRT(1),DSNLEN  LENGTH
         SPACE
         TM    FLAGS,EPUCAT       UNCATALOG ?
         BO    CONFUCAT           YES
         SPACE
         TM    FLAGS,EPCAT        CATALOG ?
         BNO   CONFSCR            NO - SCRATCH
         SPACE
         MVC   MTMSGID,=CL4'CAT'  CATALOG CONFIRMATION MSG
         B     CONFMSG
         SPACE
CONFSCR  TM    FLAGS,SVOL         DID USER SUPPLY VOLUME ?
         BNO   CONFSCR2           NO - SO DIFFERENT MSG
         SPACE
         MVC   MTMSGID,=CL4'SCR'  SCRATCH CONFIRMATION MSG
         B     CONFMSG
         SPACE
CONFSCR2 MVC   MTMSGID,=CL4'DEL'  DELETE CONFIRMATION MSG
         B     CONFMSG
         SPACE
CONFUCAT MVC   MTMSGID,=CL4'UCAT' UNCATALOG CONFIRMATION MSG
         SPACE
CONFMSG  BAL   LINK,ISSUEMSG
         SPACE
RETURN   LA    R1,CORE            -> WORKAREA
         LA    R0,CORESIZE        LENGTH
         L     R13,4(,R13)        GET OLD SAVE AREA
         SVC   10                 FREE WORKAREA
         SPACE
         SR    R15,R15            RETURN CODE ZERO
         RETURN (14,12),,RC=(15)  RETURN TO CALLER
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          SUBROUTINES'
         SPACE
ISSUEMSG CALLTSSR EP=IKJEFF02,MF=(E,MTPARML)
         SPACE
         LTR   R15,R15            IKJEFF02 OK ?
         BZR   LINK               YES - RETURN
         SPACE
         CH    R15,=H'76'         IKJEFF02 PARMLIST INVALID ?
         BNE   NEFF02ER           NO
         SPACE
         ABEND 200,DUMP
         SPACE
NEFF02ER LA    R1,GFPUTL          NO - PUTLINE ERROR
         B     GNRLFAIL           GET GENERAL FAIL TO DIAGNOSE ERROR
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          PARSE PCLS'
         PRINT NOGEN
CATPARS  IKJPARM DSECT=CATPARM
CATDSNIN IKJPOSIT DSNAME,USID,PROMPT='DSNAME',VALIDCK=DSNCHK
CATVOLKY IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=CATVOLSF,ALIAS='VOLSER'
CATFSQKY IKJKEYWD
         IKJNAME 'FILESEQUENCE',SUBFLD=CATFSQSF,ALIAS='FSEQN'
CATUNTKY IKJKEYWD
         IKJNAME 'UNIT',SUBFLD=CATUNTSF,ALIAS='DEVTYPE'
         SPACE
CATVOLSF IKJSUBF
CATVOLIN IKJIDENT 'VOLUME',LIST,MAXLNTH=6,PROMPT='VOLUME',             *
               FIRST=ALPHANUM,OTHER=ALPHANUM,VALIDCK=VOLCHK
CATFSQSF IKJSUBF
CATFSQIN IKJIDENT 'FILE SEQUENCE NUMBER',LIST,INTEG,                   *
               PROMPT='FILE SEQUENCE NUMBER'
CATUNTSF IKJSUBF
CATUNTIN IKJIDENT 'UNIT TYPE',LIST,MAXLNTH=8,VALIDCK=UNITCHK,          *
               FIRST=ALPHANUM,OTHER=ALPHANUM,                          *
               PROMPT='UNIT TYPE'
         IKJENDP
         SPACE 4
UCATPARS IKJPARM DSECT=UCATPARM
UCATDSN  IKJPOSIT DSNAME,USID,PROMPT='DSNAME',VALIDCK=DSNCHK
         IKJENDP
         SPACE 4
SCRPARS  IKJPARM DSECT=SCRPARM
SCRDSNIN IKJPOSIT DSNAME,USID,PROMPT='DSNAME',VALIDCK=DSNCHK
SCRVOLKY IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=SCRVOLSF,ALIAS='VOLSER'
SCRPRGKY IKJKEYWD
         IKJNAME 'PURGE'
         SPACE
SCRVOLSF IKJSUBF
SCRVOLIN IKJIDENT 'VOLUME',MAXLNTH=6,PROMPT='VOLUME',                  *
               FIRST=ALPHANUM,OTHER=ALPHANUM,VALIDCK=VOLCHK
         IKJENDP
         SPACE 2
         PRINT GEN
         SPACE 3
         PUSH  USING              SAVE MAINLINE ADDRESSABLITY
         DROP  ,                  DROP MAINLINE ADDRESSABLITY
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          PARSE VALIDITY *
               CHECKING ROUTINES'
DSNCHK   SAVE  (14,6),,*
         SPACE
         LR    R6,R1              SAVE PARM ADDR
         LR    R5,R15             LOAD BASE
         USING DSNCHK,R5
         L     R4,4(,R6)          -> WORKAREA
         USING CORE,R4
         SPACE
         MVI   DSNAME,C' '        BLANK OUT DSNAME FIELD
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         SPACE
         L     R3,0(,R6)          -> PDE FOR DSNAME
         L     R1,0(,R3)          -> DSNAME
         LH    R2,4(,R3)          LENGTH OF DSNAME
         SPACE
         STC   R2,DSNLEN          SAVE DSNAME LENGTH
         LA    R3,DSNAME          -> AREA TO MOVE TO
         BCTR  R2,0               MINUS ONE FOR EX MVC
         EX    R2,MOVEPARM        GET DSN
*        MVC   0(*-*,R3),0(R1)
         SPACE
         SR    R15,R15
         RETURN (14,6),,RC=(15)
         SPACE
         DROP  R5,R4
         EJECT
VOLCHK   SAVE  (14,6),,*
         SPACE
         LR    R6,R1              SAVE PARM ADDR
         LR    R5,R15             LOAD BASE
         USING VOLCHK,R5
         L     R4,4(,R6)          -> WORKAREA
         USING CORE,R4
         SPACE
         TM    FLAGS,SVOL         GOT VOLSER ALREADY ?
         BO    VOLCRET            YES - RETURN
         SPACE
         MVI   VOLSER,C' '        BLANK OUT VOLSER FIELD
         MVC   VOLSER+1(L'VOLSER-1),VOLSER
         OI    FLAGS,SVOL         INDICATE WE HAVE A VOLSER
         SPACE
         L     R3,0(,R6)          -> PDE FOR VOLSER
         L     R1,0(,R3)          -> VOLSER
         LH    R2,4(,R3)          LENGTH OF VOLSER
         SPACE
         LA    R3,VOLSER          -> AREA TO MOVE TO
         BCTR  R2,0               MINUS ONE FOR EX MVC
         EX    R2,MOVEPARM        GET DSN
*        MVC   0(*-*,R3),0(R1)
         SPACE
VOLCRET  SR    R15,R15
         RETURN (14,6),,RC=(15)
         SPACE
         DROP  R5,R4
         EJECT
UNITCHK  SAVE  (14,12),,*
         SPACE
         LR    R12,R1             SAVE PARM ADDR
         LR    R10,R15            LOAD BASE
         USING UNITCHK,R10
         L     R11,4(,R12)        -> WORKAREA
         USING CORE,R11
         SPACE 2
         MVI   UNIT,C' '          BLANK OUT UNIT FIELD
         MVC   UNIT+1(L'UNIT-1),UNIT
         SPACE
         L     R3,0(,R12)         -> PDE FOR UNIT
         L     R1,0(,R3)          -> UNIT
         LH    R2,4(,R3)          LENGTH OF UNIT
         LA    R3,UNIT            -> AREA FOR UNIT
         BCTR  R2,0               MINUS ONE FOR EX
         EX    R2,MOVEPARM
*        MVC   0(*-*,R3),0(R1)
         SPACE
*                                                              11/16/90
*  IF MVS/XA THEN VERIFY THE UNITNAME WITH THE UNIT            11/16/90
*  VERIFICATION SERVICE.  IF MVS-370 THEN GO MANHANDLE         11/16/90
*  DEVNAMET DIRECTLY.                                          11/16/90
*                                                              11/16/90
         L     R3,CVTPTR          -> CVT                       11/16/90
         USING CVT,R3                  "                       11/16/90
         TM    CVTDCB,CVTMVSE     XA SYSTEM?                   11/16/90
         BNO   DEVN370            NO, FIDDLE WITH DEVNAMET     11/16/90
         DROP  R3                                              11/16/90
         SPACE ,                                               11/16/90
*                                                              11/16/90
*  IEFEB4UV NEEDS AN 18-WORD SAVE AREA POINTED TO BY           11/16/90
*  REG13.                                                      11/16/90
*                                                              11/16/90
         ST    R13,UVSRSAVE+4     PUSH OLD SAVE AREA ADDRESS   11/16/90
         LA    R13,UVSRSAVE       POINT TO NEW ONE             11/16/90
         SPACE ,                                               11/16/90
*                                                              11/16/90
*  SETUP TO CALL IEFEB4UV FUNCTIONS #3 AND #8 (SEE MVS/XA      11/16/90
*  SYSTEM MODIFICATIONS SPL).  FUNCTION #3 RETURNS A LIST      11/16/90
*  OF UCBS ASSOCIATED WITH THIS UNITNAME.  FUNCTION #8         11/16/90
*  RETURNS THE ATTRIBUTES OF THE UNITNAME.                     11/16/90
*                                                              11/16/90
         LA    R5,UVSRUTAB        POINT TO UVSR UNIT TABLE     11/16/90
         ST    R5,UVSRPARM             "                       11/16/90
         LA    R5,UVSRFLGS        POINT TO UVSR FLAGS          11/16/90
         ST    R5,UVSRPARM+4           "                       11/16/90
         MVC   UVSRFLGS,=XL4'10800000' SET FNS #3 AND #8       11/16/90
         XC    UVSRUTAB,UVSRUTAB  CLEAR UNIT TABLE             11/16/90
         XC    UVSRATTR,UVSRATTR  CLEAR ATTRIBUTE AREA         11/16/90
         MVI   UVSRATTR,X'0A'     SET LENGTH OF ATTRIBUTE AREA 11/16/90
         MVC   UVSRUTAB(8),UNIT   MOVE IN UNIT NAME            11/16/90
         LA    R1,UVSRATTR        POINT TO ATTRIBUTE AREA      11/16/90
         ST    R1,UVSRUTAB+12          "                       11/16/90
         LA    R1,UVSRPARM        POINT TO PARAMETER LIST      11/16/90
         LINK  EP=IEFEB4UV        VERIFY UNITNAME              11/16/90
         LTR   R15,R15            GO OKAY?                     11/16/90
         BZ    EB4OK              YES, BRANCH                  11/16/90
EB4NOK   L     R13,4(,R13)        RESTORE OLD S/A POINTER      11/16/90
         LA    R15,4              TELL PARSE NAME INVALID      11/16/90
         B     UNITCRET           RETURN                       11/16/90
         SPACE ,                                               11/16/90
*                                                              11/16/90
*  CALL TO IEFEB4UV WENT OKAY.  FUNCTION #3 RETURNED A         11/16/90
*  LIST OF UCBS; LOOK AT THE FIRST ONE AND GET ITS DEVICE      11/16/90
*  TYPE, THEN FREEMAIN THE UCB POINTER LIST.                   11/16/90
*                                                              11/16/90
EB4OK    L     R1,UVSRUTAB+8      POINT TO UCB LIST            11/16/90
         CLC   4(4,R1),=F'0'      AT LEAST ONE ENTRY?          11/16/90
         BNH   EB4NOK             NO, RETURN ERROR TO PARSE    11/16/90
         L     R0,0(,R1)          GET SP/LENGTH OF UCB LIST    11/16/90
         L     R15,8(,R1)         POINT TO FIRST UCB           11/16/90
         USING UCB,R15                 "                       11/16/90
         MVC   DEVTYPE,UCBTYP     MOVE DEVICE TYPE FROM UCB    11/16/90
         DROP  R15                                             11/16/90
         FREEMAIN R,LV=(0),A=(1)  FREE UCB POINTER LIST        11/16/90
         SPACE ,                                               11/16/90
*                                                              11/16/90
*  HAVING RETRIEVED A DEVICE TYPE, MAKE SURE IT'S THE ONLY     11/16/90
*  ONE ASSOCIATED WITH THIS UNITNAME.                          11/16/90
*                                                              11/16/90
         CLI   UVSRATTR+3,X'01'   MORE THAN ONE DEVICE CLASS?  11/16/90
         BH    EB4NOK             YES, FAIL PARSE              11/16/90
         CLC   UVSRATTR+4(4),=F'1' MORE THAN ONE DEVICE TYPE?  11/16/90
         BH    EB4NOK             YES, FAIL IT AGAIN           11/16/90
         OI    FLAGS,SUNIT        INDICATE UNIT IS AVAILABLE   11/16/90
         L     R13,4(,R13)        RESTORE OLD S/A POINTER      11/16/90
         SR    R15,R15            ZERO RC                      11/16/90
         B     UNITCRET           RETURN TO PARSE              11/16/90
         SPACE ,                                               11/16/90
*                                                              11/16/90
*  IN MVS/370, DANCE THROUGH THE DEVICE NAME TABLE             11/16/90
*                                                              11/16/90
DEVN370  L     R5,@DEVNAME        LOAD ADDR OF DEVNAMET
         LTR   R5,R5              GOT IT ?
         BNZ   HAVNAMET           YES - SKIP LPA SEARCH
         SPACE
         L     R3,CVTPTR          GET ADDRESS OF CVT
         L     R15,CVTLPDSR-CVT(,R3) GET ADDR OF LPDE SEARCH ROUTINE
         LM    R0,R1,DEVNAMET     GET 'DEVNAMET' IN R0 & R1
         BALR  R14,R15            CALL IEAVVMSR - DESTROYS R6, R8 & R9
         B     GOTNAMET           +0 - DEVNAMET FOUND                  *
                                  +4 - DEVNAMET NOT FOUND
         LA    R15,12             TELL PARSE WE CAN'T GO ON
         B     UNITCRET           ERROR EXIT
GOTNAMET DS    0H                 DEVNAMET LPDE FOUND
         LR    R1,R0              GET ADDRESS OF DEVNAMET LPDE
         L     R5,LPDENTP-LPDE(,R1) GET EP ADDRESS OF DEVNAMET
         ST    R5,@DEVNAME        SAVE FOR FUTURE USE
HAVNAMET L     R15,0(,R5)         GET NUMBER OF ENTRIES
         LA    R5,4(,R5)          GET ADDRESS OF 1ST ENTRY
         SPACE
DEVTLOOP CLC   UNIT,0(R5)         UNIT NAME MATCH ?
         BE    UNITGOT            YES - UNIT FOUND
         LA    R5,12(,R5)         NO - POINT TO NEXT ENTRY
         BCT   R15,DEVTLOOP       LOOP UNTIL END
         SPACE
         LA    R15,4              TELL PARSE NAME INVALID
         B     UNITCRET           RETURN
         SPACE
UNITGOT  DS    0H                 UNIT NAME FOUND IN DEVNAMET
         CLI   11(R5),X'00'       A DEVICE GROUP NAME ?
         BNE   UNITACT            NO - AN ACTUAL UNIT
         LA    R2,GRPTABLE        -> GROUP TABLE
         LA    R1,GRPCNT          NUMBER OF GROUPS IN TABLE
GRPLOOP  CLC   10(1,R5),0(R2)     DEVICE CLASS MATCH ?
         BE    GOTDEVCL           YES
         LA    R2,9(,R2)          -> NEXT GROUP ENTRY
         BCT   R1,GRPLOOP         LOOP
         SPACE
         LA    R15,4              TELL PARSE BAD UNIT
         B     UNITCRET           RETURN
         SPACE
GOTDEVCL MVC   UNIT,1(R2)         GET ACTUAL UNIT WE WANT
         L     R5,@DEVNAME        -> DEVNAMET
         B     HAVNAMET           GO LOOK UP ACTUAL DEVICE
         SPACE
UNITACT  DS    0H                 ACTUAL UNIT NAME FOUND
         MVC   DEVTYPE,8(R5)      GET DEVICE TYPE FIELD
         SPACE
         OI    FLAGS,SUNIT        HAVE A UNIT
         L     R1,0(,R12)         -> PDE FOR UNIT
         ST    R5,0(,R1)          SAVE DEVNAMET ENTRY @ IN PDE
         SR    R15,R15            ZERO RC
         SPACE
UNITCRET RETURN (14,12),,RC=(15)
         SPACE
         DROP  R10,R11
         SPACE 4
         POP   USING              RESTORE MAINLINE ADDRESSABLITY
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          CONSTANTS'
MOVEPARM MVC   0(*-*,R3),0(R1)    MOVE PARAMTERS FROM PDL
         SPACE
CATCAM   CAMLST CATBX,0,,0        CATALOG A DATASET
         ORG   CATCAM+4
UCATCAM  CAMLST UCATDX,0          UNCATALOG A DATASET
         ORG   UCATCAM+4
SCRCAM   CAMLST SCRATCH,0,,0      SCRATCH A DATASET
         ORG   SCRCAM+4
SCRPCAM  CAMLST SCRATCH,0,,0,,OVRD  SCRATCH A DATASET (PURGE)
         ORG   SCRPCAM+4
NAMECAM  CAMLST NAME,0,,0         LOCATE A DATASET
         ORG   NAMECAM+4
SRCHCAM  CAMLST SEARCH,0,0,0      OBTAIN A DATASET'S DSCB
         ORG   SRCHCAM+4
         SPACE
CAT8LDES DS    0F                 DESCRIPTIONS OF CATALOG ERROR CODES
         DC    AL1(L'ALRCAT),AL3(ALRCAT)        CATALOG R1 = 0
         DC    AL1(L'CVOLCH),AL3(CVOLCH)        CATALOG R1 = 4
         DC    AL1(L'NOQUAL),AL3(NOQUAL)        CATALOG R1 = 8
         DC    AL1(L'NEEDQUAL),AL3(NEEDQUAL)    CATALOG R1 = 12
         DC    AL1(L'OVERQUAL),AL3(OVERQUAL)    CATALOG R1 = 16
         SPACE
DEVNAMET DC    CL8'DEVNAMET'      NAME OF DEVICE TABLE
         SPACE
GRPTABLE DC    X'80',CL8'3400-5'  MAGNETIC TAPE DEVICE CLASS
         DC    X'20',CL8'3350'    DIRECT ACCESS DEVICE CLASS
GRPCNT   EQU   (*-GRPTABLE)/9
         SPACE
ALRCAT   DC    C'QUALIFICATION ALREADY EXISTS'
CVOLCH   DC    C'CLOSED CHAIN OF CVOL POINTERS'
NOQUAL   DC    C'QUALIFICATION DOES NOT EXIST'
NEEDQUAL DC    C'INSUFFICIENT QUALIFICATION'
OVERQUAL DC    C'TOO MUCH QUALIFICATION'
         SPACE 2
         LTORG
         SPACE 2
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          ERROR EXITS'
         SPACE
BADCAT   LA    LINK,RETURN        SET RETURN ADDR FOR ISSUEMSG
         CH    R15,=H'4'
         BL    CATRC
         CH    R15,=H'24'
         BH    CATRC
         B     *(R15)             DETERMINE RETURN CODE
         B     CAT4
         B     CAT8
         B     CATRC
         B     CAT16
         B     CAT20
         B     CAT24
         SPACE 2
CAT4     MVC   MTMSGID,=CL4'C04'  "CATALOG DOES NOT EXIST"
         B     ISSUEMSG
         SPACE
CAT8     LR    R4,R0              SAVE INDEX LEVEL
         LTR   R3,R1              DO WE HAVE A "LOCATE" RC ???
         BNZ   CAT8CVOL           YES - MUST BE A CVOL CATALOG
         SPACE
         CH    R4,=H'56'          USER UNAUTHORIZED ?
         BNE   CAT8VS             NO - MUST BE VSAM CTLG RC
         MVC   MTMSGID,=CL4'C08B' "USER UNAUTHORIZED"
         B     ISSUEMSG
         SPACE
CAT8VS   MVC   MTMSGID,=CL4'C08C' "VSAM CATALOG RETURN CODE"
         ST    R4,DBLWRD          VSAM CTLG RC
         LA    R1,DBLWRD
         ST    R1,MTINSRT         FIRST INSERT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         SPACE
CAT8CVOL MVC   MTMSGID,=CL4'C08'  "OPERATION INCONSISTENT ... "
         LA    R3,0(,R3)          CLEAR HIGH BYTE
         CH    R3,=H'16'
         BH    CAT8LRC
         LA    R1,B'11'           MASK
         NR    R1,R3              MULTIPLE OF 4 ?
         BNZ   CAT8LRC            NOPE
         SPACE
         L     R1,CAT8LDES(R3)    LOAD PTR ENGLISH DESCRIPTION OF RC
         ST    R1,MTINSRT         SET FIRST INSERT
         SPACE
CAT8ILVL ST    R4,DBLWRD          INDEX LEVEL
         LA    R1,DBLWRD
         ST    R1,MTINSRT+4       SECOND INSERT
         MVI   MTINSRT+4,X'84'    CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         SPACE
CAT8LRC  MVC   MTMSGID,=CL4'C08A' 2ND LVL MSG IS "LOCATE RETURN CODE"
         ST    R3,DBLWRD+4        STORE "LOCATE" TYPE RETURN CODE
         LA    R1,DBLWRD+4
         ST    R1,MTINSRT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     CAT8ILVL           GO GET INDEX LEVEL INSERT
         SPACE
CAT16    MVC   MTMSGID,=CL4'C16'  "INDEX STRUCTURE DOES NOT EXIST"
         B     ISSUEMSG
         SPACE
CAT20    MVC   MTMSGID,=CL4'C20'  "CATALOG FULL"
         B     ISSUEMSG
         SPACE
CAT24    MVC   MTMSGID,=CL4'C24'  "CANNOT CATALOG GDG DATASET"
         B     ISSUEMSG
         SPACE
CATRC    MVC   MTMSGID,=CL4'CRC'  "UNRECOVERABLE ERROR"
         ST    R15,DBLWRD         CATALOG RC
         LA    R1,DBLWRD
         ST    R1,MTINSRT         FIRST INSERT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         EJECT
         SPACE
BADLOC   LA    LINK,RETURN        SET RETURN ADDR FOR ISSUEMSG
         CH    R15,=H'4'
         BL    LOCRC
         BE    CAT4               RC = 4 SAME AS CATALOG
         CH    R15,=H'16'
         BH    LOCRC
         LA    R1,B'11'           MASK
         NR    R1,R15             RC MULTIPLE OF 4 ?
         BNZ   LOCRC              NOPE
         SPACE 2
LOCQUAL  MVC   MTMSGID,=CL4'QUAL' "..... - INDEX LEVEL IS ... "
         L     R1,CAT8LDES(R15)   LOAD PTR ENGLISH DESCRIPTION OF RC
         ST    R1,MTINSRT         SET FIRST INSERT
         ST    R0,DBLWRD          INDEX LEVEL
         LA    R1,DBLWRD
         ST    R1,MTINSRT+4       SECOND INSERT
         MVI   MTINSRT+4,X'84'    CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         SPACE
LOCRC    MVC   MTMSGID,=CL4'LOC'  "LOCATE RETURN CODE"
         ST    R15,DBLWRD         LOCATE RC
         LA    R1,DBLWRD
         ST    R1,MTINSRT         FIRST INSERT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         EJECT
BADSCR   LA    LINK,RETURN        SET RETURN ADDR FOR ISSUEMSG
         CH    R15,=H'4'
         BL    SCRRC
         CH    R15,=H'8'
         BH    SCRRC
         B     *(R15)             DETERMINE RETURN CODE
         B     SCRR4
         B     SCRR8
         SPACE 2
SCRR4    MVC   MTMSGID,=CL4'V03'  "VOLUME XXXXXX NOT MOUNTED"
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT         1ST VARIBLE
         MVI   MTINSRT,L'VOLSER   LENGTH
         B     ISSUEMSG
         SPACE
SCRR8    DS    0H                 STATUS CODE RETURNED IN VOL LIST
         LA    R1,DSNAME          MOST MSGS HAVE A DSN AS A VARIBLE
         ST    R1,MTINSRT         1ST VARIBLE
         MVC   MTINSRT(1),DSNLEN  LENGTH
         SR    R15,R15            CLEAR FOR INSERT
         IC    R15,CAMAREA+13     PICK UP SCRATCH STATUS CODE
         CH    R15,=H'1'          CHECK WITHIN BOUNDS
         BL    SCRSC
         CH    R15,=H'9'
         BH    SCRSC
         SLL   R15,2              * 4 TO GET BRANCH TABLE OFFSET
         B     *(R15)             DETERMINE RETURN CODE
         B     SCR1
         B     SCR2
         B     SCR3
         B     SCR4
         B     SCRR4
         B     SCR6
         B     SCR7
         B     SCR8
         B     SCR9
         SPACE
SCR1     MVC   MTMSGID,=CL4'SSC1' "DATASET NOT FOUND"
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT+4       1ST VARIBLE
         MVI   MTINSRT+4,L'VOLSER     LENGTH
         B     ISSUEMSG
         SPACE
SCR2     MVC   MTMSGID,=CL4'SSC2' "INCORRECT PASSWORD SUPPLIED"
         B     ISSUEMSG
         SPACE
SCR3     MVC   MTMSGID,=CL4'SSC3' "DATASET NOT EXPIRED"
         B     ISSUEMSG
         SPACE
SCR4     MVC   MTMSGID,=CL4'SSC4' "I/O ERROR READING VTOC"
         B     ISSUEMSG
         SPACE
SCR6     MVC   MTMSGID,=CL4'SSC6' "UNABLE TO MOUNT VOLUME"
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT         1ST VARIBLE
         MVI   MTINSRT,L'VOLSER   LENGTH
         B     ISSUEMSG
         SPACE
SCR7     MVC   MTMSGID,=CL4'SSC7' "DATASET IN USE"
         B     ISSUEMSG
         SPACE
SCR8     MVC   MTMSGID,=CL4'SSC8' "USER NOT AUTHORIZED TO SCRATCH DS"
         B     ISSUEMSG
         SPACE
SCR9     MVC   MTMSGID,=CL4'SSC9' "UNABLE TO DELETE DATASET FROM RACF"
         B     ISSUEMSG
         SPACE
SCRSC    MVC   MTMSGID,=CL4'SSC'  "SCRATCH STATUS CODE XX"
         B     SCRNUMC
         SPACE
SCRRC    MVC   MTMSGID,=CL4'SRC'  "SCRATCH RETURN CODE XX"
SCRNUMC  ST    R15,DBLWRD         SCRATCH RC
         LA    R1,DBLWRD
         ST    R1,MTINSRT         FIRST INSERT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         EJECT
BADOBTN  LA    LINK,RETURN        RETURN ADDR FOR ISSUEMSG
         CH    R15,=H'4'
         BL    OBTNRC
         CH    R15,=H'12'
         BH    OBTNRC
         B     *(R15)             DETERMINE RETURN CODE
         B     OBTN4      4
         B     OBTN8      8
         B     SCR4      12       "I/O ERROR READING VTOC"
         SPACE 2
OBTN4    MVC   MTMSGID,=CL4'V02'  "VOLUME XXXXXX NOT MOUNTED"
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT         1ST VARIBLE
         MVI   MTINSRT,L'VOLSER   LENGTH
         B     ISSUEMSG
         SPACE
OBTN8    MVC   MTMSGID,=CL4'SSC1' "DATASET NOT FOUND"
         LA    R1,DSNAME          -> DSNAME
         ST    R1,MTINSRT         1ST VARIBLE
         MVC   MTINSRT(1),DSNLEN  LENGTH
         LA    R1,VOLSER          -> VOLUME
         ST    R1,MTINSRT+4       1ST VARIBLE
         MVI   MTINSRT+4,L'VOLSER     LENGTH
         B     ISSUEMSG
         SPACE
OBTNRC   MVC   MTMSGID,=CL4'ORC'  "OBTAIN RETURN CODE XX"
         ST    R15,DBLWRD         OBTAIN RC
         LA    R1,DBLWRD
         ST    R1,MTINSRT         FIRST INSERT
         MVI   MTINSRT,X'84'      CNVT TO DEC,LENGTH OF 4
         B     ISSUEMSG           GO ISSUE MSG
         EJECT
GNRLFAIL DS    0H                 CALL GENERAL FAIL
         SPACE
         XC    GFPARMS(GFLENGF),GFPARMS    INIT GENERAL FAIL PARM LIST
         SPACE
         ST    R15,GFRCODE        RETURN CODE IN PARM LIST
         STH   R1,GFCALLID        FAILING ROUTINE ID
         L     R1,MTCPPLP         GET CPPL FROM IKJEFF02 PARM LIST
         ST    R1,GFCPPLP         -> CPPL FOR GENRL FAIL
         SPACE
         LA    R1,ECB             -> DUMMY ECB
         ST    R1,GFECBP          FOR GENERAL FAIL TO GIVE TO PUTLINE
         SR    R1,R1
         ST    R1,ECB             CLEAR ECB
         SPACE
         LA    R1,GFPARMS         -> GENERAL FAIL PARMS
         ST    R1,GFPARMP         ADDR LIST FOR LINK
         SPACE
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)  DIAGNOSE RETURN CODE
         SPACE
         LTR   R15,R15            GENERAL FAIL EXECUTE OK ?
         BZ    RETURN
         SPACE 2
         ABEND 100,DUMP           WHY ?
         SPACE 3
         LTORG
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          MESSAGES'
MSGCSECT CSECT
         IKJTSMSG (' ',,' CATALOGED'),CAT
         SPACE
         IKJTSMSG (' ',,' UNCATALOGED'),UCAT
         SPACE
         IKJTSMSG (' ',,' SCRATCHED'),SCR
         SPACE
         IKJTSMSG (' ',,' DELETED'),DEL
         SPACE
         IKJTSMSG (' TOO MANY VOLUMES+'),VX01
         IKJTSMSG (' ALL VOLUMES AFTER THE FIRST 20 HAVE BEEN IGNORED')*
               ,V01,VX01
         SPACE
         IKJTSMSG (' VOLUME ',,' NOT MOUNTED+'),VX02
         IKJTSMSG (' YOU MUST SUPPLY THE UNIT TYPE TO CATALOG A DATASET*
                ON A NON-MOUNTED VOLUME'),V02,VX02
         SPACE
         IKJTSMSG (' DASD VOLUME ',,' NOT MOUNTED'),V03
         SPACE
         IKJTSMSG (' VOLUME SERIAL IS REQUIRED'),V04
         SPACE
         IKJTSMSG (' REQUIRED CATALOG DOES NOT EXIST'),C04
         SPACE
         IKJTSMSG (' OPERATION INCONSISTENT WITH EXISTING CATALOG STRUC*
               TURE+'),CX08
         IKJTSMSG (' ',,' - INDEX LEVEL IS ',),C08,CX08
         IKJTSMSG (' VSAM CATALOG RETURN CODE IS ',),C08C,CX08
         SPACE
         IKJTSMSG (' OPERATION CANNOT BE PERFORMED+'),CY08
         IKJTSMSG (' CATALOG MACRO R15 = 8, R1 = ',,', R0 = ',),C08A,CY*
               08
         IKJTSMSG (' USER IS NOT AUTHORIZED TO PERFORM THE OPERATION'),*
               C08B,CY08
         SPACE
         IKJTSMSG (' INDEX STRUCTURE REQUIRED TO CATALOG DATASET DOES N*
               OT EXIST'),C16
         SPACE
         IKJTSMSG (' CATALOG DATASET IS FULL'),C20
         SPACE
         IKJTSMSG (' UNABLE TO CATALOG GENERATION DATASET+'),CX24
         IKJTSMSG ('  GENERATION DATASET HAS IMPROPER NAME OR IS TOO OL*
               D TO FIT IN CATALOG'),C24,CX24
         SPACE
         IKJTSMSG (' UNRECOVERABLE ERROR DURING CATALOG OPERATION+'),CX*
               RC
         IKJTSMSG (' CATALOG MACRO RETURN CODE IS ',),CRC,CXRC
         SPACE
         IKJTSMSG (' ',,' NOT ON VOLUME ',),SSC1
         SPACE
         IKJTSMSG (' INCORRECT PASSWORD SUPPLIED'),SSC2
         SPACE
         IKJTSMSG (' ',,' NOT EXPIRED'),SSC3
         SPACE
         IKJTSMSG (' I/O ERROR READING VTOC - NOTIFY SYSTEMS'),SSC4
         SPACE
         IKJTSMSG (' UNABLE TO MOUNT VOLUME ',),SSC6
         SPACE
         IKJTSMSG (' ',,' IN USE'),SSC7
         SPACE
         IKJTSMSG (' USER NOT AUTHORIZED TO SCRATCH DATASET ',),SSC8
         SPACE
         IKJTSMSG (' UNABLE TO DELETE DATASET FROM RACF'),SSC9
         SPACE
         IKJTSMSG (' SCRATCH STATUS CODE ',),SSC
         SPACE
         IKJTSMSG (' SCRATCH RETURN CODE ',),SRC
         SPACE
         IKJTSMSG (' ',,' - INDEX LEVEL IS ',),QUAL
         SPACE
         IKJTSMSG (' LOCATE RETURN CODE ',),LOC
         SPACE
         IKJTSMSG (' OBTAIN RETURN CODE ',),ORC
         SPACE
         IKJTSMSG
         SPACE
CATLG    CSECT
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          EQUATES AND DSE*
               CTS'
         SPACE
#VOLS    EQU   20                 MAXIMUM NUMBER OF VOLUMES            *
                                  IF CHANGED THEN CHANGE ERROR MSG TOO
         SPACE 2
R0       EQU   0                  WORK REGISTER
R1       EQU   1                  WORK REGISTER
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R6       EQU   6                  WORK REGISTER
R7       EQU   7                  WORK REGISTER
R8       EQU   8                  WORK REGISTER
R9       EQU   9   ) DUAL         WORK REGISTER
LINK     EQU   9   ) DEFINITION   LINKAGE REGISTER FOR SUBROUTINES
R10      EQU   10                 WORK REGISTER
R11      EQU   11                 WORK REGISTER
R12      EQU   12                 BASE REGISTER FOR CATLG CSECT
R13      EQU   13                 BASE REGISTER FOR CORE DSECT
R14      EQU   14                 WORK REGISTER
R15      EQU   15                 WORK REGISTER
         EJECT
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         SPACE 2
         IKJPPL
PPLLEN   EQU   *-PPL
         EJECT
         SPACE
         CVT   DSECT=YES,LIST=NO
         SPACE
         EJECT
         SPACE
         IHALPDE
         SPACE
         EJECT
         SPACE
UCB      DSECT
         IEFUCBOB LIST=NO
         TITLE 'CATLG/UNCATLG/SCRATCH FUNCTION          WORK AREA'
CORE     DSECT
         DS    18F
         SPACE
DBLWRD   DS    D
@DEVNAME DS    A                  -> DEVNAMET MODULE IN LPA
         SPACE
PPLSECT  DS    0F                 PPL AREA
         DS    CL(PPLLEN)
ECB      DS    F                  ECB FOR PARSE
PARSBACK DS    F                  -> PARSE PDL
GFPARMP  DS    F                  -> GENERAL FAIL PARM BLOCK
         IKJEFFGF                 GENERAL FAIL PARM BLOCK
         EJECT
         IKJEFFMT MTNINST=4       IKJEFF02 MSG ISSUER PARM BLOCK
         EJECT
CAMLST   DS    F                  FLAG BYTES INDICATING FUNC OF CAMLST
CAMLSTP2 DS    A                  PARAMETER TWO OF CAMLST
CAMLSTP3 DS    A                  PARAMETER THREE OF CAMLST
CAMLSTP4 DS    A                  PARAMETER FOUR OF CAMLST
         SPACE
         DS    0D
CAMAREA  DS    (#VOLS)CL12        VOLUME LIST AREA
         SPACE
         DS    0D
F1DSCB   DS    CL140              SPACE FOR F1 DSCB
         SPACE
         ORG   CAMAREA
         DS    CL265              MAKE SURE ENOUGH ROOM FOR LOCATE
         ORG   ,                  SET LOC CNTR TO HIGHEST USED ADDR
         SPACE
DSNLEN   DS    AL1                LENGTH OF DSNAME
DSNAME   DS    CL44               DSNAME
VOLSER   DS    CL6                VOLSER OF DATASET
UNIT     DS    CL8                UNIT TYPE
DEVTYPE  DS    XL4                DEVICE TYPE OF ABOVE UNIT
FLAGS    DS    X
EPCAT    EQU   X'80'              CATLG   EP
EPUCAT   EQU   X'40'              UNCATLG EP
EPSCR    EQU   X'20'              SCRATCH EP
SVOL     EQU   X'10'              VOLUME SERIAL SUPPLIED  (SCR & CATLG)
SUNIT    EQU   X'08'              UNIT TYPE SUPPLIED      (SCR & CATLG)
SCRPURGE EQU   X'04'              PURGE SPECIFIED         (SCRATCH)
QUIT     EQU   X'02'              ERROR DURING PARM VERIFICATION
SKIPOBTN EQU   X'01'              DONT ISSUE OBTAIN TO VERIFY DS EXISTS
         SPACE ,                                               10/16/90
IOSVSWRK DC    100X'00'           IOSVSUCB WORKAREA            10/16/90
IOSVSPRM DC    A(IOSVSWRK)         )                           10/16/90
         DC    A(IOSVSDEV)         ) MUST BE KEPT TOGETHER     10/16/90
         DC    X'80',AL3(IOSVSUCB) )                           10/16/90
IOSVSUCB DS    A                  UCB ADDRESS                  10/16/90
IOSVSDEV DC    AL1(UCB3DACC)      DASD DEVICE CLASS            10/16/90
         SPACE ,                                               11/16/90
UVSRSAVE DS    18F                IEFEB4UV SAVE AREA           11/16/90
UVSRPARM DS    2F                 IEFEB4UV PARAMETER LIST      11/16/90
UVSRUTAB DS    4F                 IEFEB4UV UNIT TABLE          11/16/90
UVSRATTR DS    CL10               IEFEB4UV ATTRIBUTE AREA      11/16/90
UVSRFLGS DS    CL2                IEFEB4UV FLAGS               11/16/90
         SPACE
         DS    0D                 ROUND UP TO DOUBLE WORD BOUNDRY
         SPACE
CORESIZE EQU   *-CORE
         SPACE 3
VOLLIST  DSECT                    FORMAT FOR A VOLUME ENTRY ON CATALOG
DEVCODE  DS    XL4                UCB DEVICE TYPE FIELD
CATVOL   DS    CL6                SERIAL OF A VOLUME
DSEQNUM  DS    H                  DATASET SEQUENCE NUMBER
NEXTVOL  EQU   *                  NEXT VOLUME ENTRY
         SPACE 3
         END
/*
//LKED.SYSIN DD *
  ENTRY CATLG
  ENTRY UNCATLG
  ENTRY UNCAT
  ENTRY SCRATCH
  ALIAS UNCATLG
  ALIAS UNCAT
  ALIAS SCRATCH
  ALIAS SCR
  NAME  CATLG(R)
/*
