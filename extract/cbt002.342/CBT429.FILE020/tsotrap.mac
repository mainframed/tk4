/* ---------------------  REXX PROCEDURE  ----------------------
   Name:      TSOTRAP
   Function:  This Rexx procedure will accept any TSO command
              (including clists and other Rexx procedures)
              that output to the terminal and trap the output
              and then display the results using ISPF Browse.
   Syntax:    %TSOTRAP tso-command options
   ------------------------------------------------------------- */
arg command
if length(command) = 0 then do
   say "Error:  Use of TSOTRAP requires the specification of"
   say "        a TSO command whose results will be captured"
   say "        and displayed using ISPF Browse."
   say "        (e.g.  %TSOTRAP LISTC)"
   exit 12
   end
x = outtrap("trap.","*")
command
x= outtrap("off")
Address ISPEXEC "VGET (ZTEMPF)"
dd = "TP"random()
"ALLOC DD("dd") DA('"ztempf"') SHR"
"EXECIO * DISKW" dd "(FINIS STEM trap."
Address ISPEXEC "BROWSE DATASET('"ztempf"')"
"FREE DD("dd")"
