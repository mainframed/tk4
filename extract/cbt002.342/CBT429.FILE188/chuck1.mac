TAPEREAD TITLE 'PROGRAM TO READ SOURCE TAPES AND CREATE INDEX'
*%A PPLINK NOTEST
TAPEREAD CSECT
         REGS
         USING TAPEREAD,R15        ESTABLISH TEMPORARY BASE.
         B     BEGIN               SKIP OVER HEADER.
HEAD0    DC    AL1(HEADL)          EXCLUSIVE LENGTH OF HEADER.
HEAD     DC    C'READ SOURCES, MAKE INDEX, &SYSDATE'
HEADL    EQU   *-HEAD
         DROP  R15                 DROP TEMPORARY BASE.
BEGIN    STM   R14,R12,12(R13)     PUT CALLER'S REGS IN SAVE AREA.
         LR    R12,R15             COPY BASE TO MY BASE REG.
         USING TAPEREAD,R12        TELL ASSEMBLER ABOUT NEW BASE.
         LR    R14,R13             COPY CALLER'S SAVE AREA POINTER.
         LA    R13,SAVE            MAKE R13 POINT TO MY SAVE AREA.
         ST    R13,8(,R14)         CHAIN SAVE AREAS ...
         ST    R14,SAVE+4          ... TO COMPLETE LINKAGE.
         RDJFCB MF=(E,RD1)         READ THE JFCB FOR INPUT
         LTR   15,15               CHECK RETURN CODE
         BNZ   FUBAR               REPORT ON FAILURE
         OPEN  (SYSPRINT,OUTPUT)   OPEN OUTPUT DATA SET ...
         OPEN  (SYSUT2,OUTPUT)     ... PRINTER AND DATA SET
         CLC   =H'0',JFCBFLSQ      TEST FOR FILE ZERO
         BE    FUBAR1              SEQUENCE ZERO IS NOT A TAPE
         LH    R1,JFCBFLSQ         SEQ. NO. INTO REG FOR LIST
         B     MOVNUM              GO TO LOOP END TO INITIALIZE
OPENER   OPEN  (SYSUT1),TYPE=J     ... AND INPUT DATA SET
         SR    R6,R6               CLEAR REG 6 (FILE RECORD CT)
         SR    R7,R7               CLEAR REG 7 (MOD RECORD CT)
READ1    GET   SYSUT1              GET A RECORD FROM THE FILE.
         LA    R6,1(R6)            BUMP RECORD COUNT (FILE)
         LA    R7,1(R7)            BUMP RECORD COUNT (MOD)
         TM    ACTFG,L'ACTFG       IS THIS FIRST READ OF FILE?
         BNZ   DOTCHK              IF NOT CHECK FOR './'
         XI    SYSUT1+84,X'A0'     TURN OFF 'EROPT=ACC'
         CLC   PIDID(LPIDID),0(R1) ON FIRST OF FILE CHECK FOR PID
         BZ    WRAPPER             IF NO MORE DATA LEAVE
DOTCHK   OI    ACTFG,L'ACTFG       MARK ACTIVITY FLAG
         CLC   =C'./',0(R1)        DO WE WANT THIS ONE
         BNZ   READ1               IF NOT GO READ AGAIN
         LH    R2,=H'66'           LENGTH OF ORIGINAL SEARCH
SRCH$N   LR    R3,R1               SAVE START OF RECORD
         EX    R2,EXEC1            SEARCH FOR AN 'N'
* (      TRT   0(66,R3),NTABLE )
         BZ    READ1               IF NONE GO READ AGAIN
         CLC   =C'AME=',1(R1)      SEE IF REALLY 'NAME='
         BZ    GETDEL              IF GOOD GET ENDING DELIMITER
         LH    R2,=H'66'           ORIGINAL SEARCH LENGTH
         AR    R2,R3               ADD START OF SEARCH
         SR    R2,R1               SUBTRACT CURRENT POSITION
         AH    R1,=H'1'            BUMP BEYOND FALSE 'N'
         B     SRCH$N              TRY AGAIN
GETDEL   AH    R2,=H'6'            LENGTH OF LONGEST SEARCH
         AR    R2,R3               COMPUTE LAST CHARS ADDRESS
         SR    R2,R1               SUBTRACT START FOR LENGTH
         LR    R3,R1               SAVE BEGINNING OF FIELD
         EX    R2,EXEC2            EXECUTE TRT WITH VARIABLE LENGTH
* (      TRT   0(0,R1),BTABLE    )
         BNZ   GOTLEN              IF NONE FOUND USE MAX LEN
         LR    R1,R3               MOVE START OF SEARCH INTO R1
         AR    R1,R0               ADD LENGTH FOR SIMULATED END
GOTLEN   TM    FMODFG,L'FMODFG     IS THIS FIRST MOD IN FILE
         BZ    PROCLEN             IF SO PROCESS LENGTH
         CVD   R7,PACKED           MOVE NO. RECS IN FILE TO PACKED
         OI    PACKED+7,X'0F'      FIX SIGN FOR UNPACK
         UNPK  NORPF(6),PACKED+5(3)  UNPACK INTO MESSAGE
         LR    R7,R1               SAVE REGISTER 1 FROM PUT
         PUT   SYSUT2,PLINE        PUT LINE IN OUTPUT
         LR    R1,R7               RECALL AFTER PUT
PROCLEN  XR    R7,R7               CLEAR RECORD COUNT (MOD)
         OI    FMODFG,L'FMODFG     NO LONGER FIRST MOD IN FILE
         SR    R1,R3               SUBTRACT START FOR LEN
         AH    R3,=H'5'            BUMP ADDRESS BEYOND 'NAME='
         SH    R1,=H'5'            SHORTEN LENGTH BY 5 CHARS
         MVI   PLINE,C' '          CLEAR OLD NAME
         MVC   PLINE+1(7),PLINE    PROPOGATE MOVE
         EX    R1,MVC1             EXECUTE MOVE TO PUT NAME IN PLINE
* (      MVC   PLINE(0),0(R3)    ) PUT NAME IN PRINT LINE
         CVD   R6,PACKED           MOVE RECORD # INTO PACKED FIELD
         OI    PACKED+7,X'0F'      FIX SIGN FOR UNPACK
         UNPK  RECNO(6),PACKED+5(3)  UNPACK INTO MESSAGE
         B     READ1               BRANCH TO READ AGAIN.
NEXTFILE CLOSE (SYSUT1,LEAVE)      CLOSE INPUT FILE
         NI    FMODFG,X'FF'-L'FMODFG  TURN ON FIRST MOD IN FILE FLAG
         CVD   R7,PACKED           MOVE NO. RECS IN FILE TO PACKED
         OI    PACKED+7,X'0F'      FIX SIGN FOR UNPACK
         UNPK  NORPF(6),PACKED+5(3)  UNPACK INTO MESSAGE
         PUT   SYSUT2,PLINE        PUT LINE IN OUTPUT
         XR    R7,R7               CLEAR COUNT (MOD)
         CVD   R6,PACKED           MOVE RECORD # INTO PACKED FIELD
         OI    PACKED+7,X'0F'      FIX SIGN FOR UNPACK
         UNPK  TOTRC(6),PACKED+5(3)  UNPACK INTO MESSAGE
         PUT   SYSPRINT,EOFMSG     REPORT END OF FILE
         TM    ACTFG,L'ACTFG       WAS THERE ANY DATA IN THIS FILE?
         BZ    WRAPPER             B IF NOT.  TWO TAPE MARKS.
         XI    SYSUT1+84,X'A0'     TURN ON 'EROPT=ACC'
         LH    R1,JFCBFLSQ         BUMP TAPE FILE NUMBER ...
         LA    R1,1(,R1)           ... IN JFCB BY 1 TO ...
         STH   R1,JFCBFLSQ         ... GET TO NEXT FILE.
MOVNUM   CVD   R1,PACKED           CONVERT TO DECIMAL
         UNPK  FILENO(4),PACKED+6(2)  UNPACK INTO MESSAGE
         OI    FILENO+3,X'F0'      FIX ZONE PUNCH CONFIG
         MVC   TAPFNO(4),FILENO    PUT IT INTO PLINE ALSO
         MVC   TAPLAB(6),JFCBVOLS  PUT TAPE NAME IN BUFFER
         MVC   TAPLAB1(6),JFCBVOLS PUT TAPE NAME IN PRINT LINE
         PUT   SYSPRINT,NFILMSG    NOW PRINT MESSAGE
         NI    ACTFG,X'FF'-L'ACTFG TURN OFF ACTIVITY FLAG.
         B     OPENER              OPEN NEXT FILE.
WRAPPER  PUT   SYSPRINT,NORMALX    REPORT NORMAL EXIT
EXIT     L     R13,SAVE+4          RESTORE
         LM    R14,R12,12(R13)      SAVED
         SR    R15,R15               REGS
         BR    R14                    AND LEAVE.
FUBAR    PUT   SYSPRINT,FUBMSG     REPORT FAILURE
         B     EXIT                BEFORE DEPARTING
FUBAR1   PUT   SYSPRINT,FUBMSG1    REPORT FAILURE
         B     EXIT                SHOW AND SPLIT
         SPACE 1
EXEC1    TRT   0(0,R3),NTABLE      INSTRUCTION FOR 'N' SEARCH
EXEC2    TRT   0(0,R1),BTABLE      INSTRUCTION FOR DELIM SEARCH
MVC1     MVC   PLINE(0),0(R3)      PUT NAME IN PRINT LINE
         SPACE 1
SAVE     DS    9D                  RESERVE FOR SAVE AREA.
RECLEN   DC    AL2(80)             LENGTH OF A RECORD
         SPACE 1
FLAG1    DC    X'00'               BYTE FOR BIT FLAGS
ACTFG    EQU   FLAG1,X'01'         ONE BIT FOR ACTIVITY
FMODFG   EQU   FLAG1,X'02'         ONE BIT FOR FIRST MOD IN FILE
         SPACE 1
NTABLE   DC    XL256'00'           TRT TABLE WITH ONLY 'N' ON
         ORG   NTABLE+C'N'
         DC    X'80'
         ORG   ,                   RESET INSTRUCTION COUNTER
         SPACE 1
BTABLE   DC    XL256'00'           TRT TABLE FOR BLANK AND COMMA
         ORG   BTABLE+C','
         DC    X'80'
         ORG   BTABLE+C' '
         DC    X'80'
         ORG   ,                   RESET INSTRUCTION COUNTER
         SPACE 1
NFILMSG  DC    CL80' '             RESERVE 80 CHAR BUFFER
         ORG   NFILMSG             RE-ORG TO START OF BUFFER
         DC    C'STARTING TO READ FILE NO. '
FILENO   DC    CL4' '              FILE NUMBER GOES HERE
         DC    C' FROM TAPE '
TAPLAB1  DC    CL6' '              TAPE LABEL GOES HERE
         DC    C'.'
         ORG   ,                   CONTINUE WITH NORMAL ALLOCATION
         SPACE 1
         DS    0D                  FORCE ALIGNMENT
PACKED   DC    CL8' '              PLACE FOR PACKED FILE NUMBER
PIDID    DC    X'00',C'PID.STACK'  STRING TO MARK NO MORE DATA
LPIDID   EQU   *-PIDID             LENGTH FOR SEARCH
         SPACE 1
NORMALX  DC    CL80'NORMAL EXIT CONDITION DETECTED. SAYONARA.'
FUBMSG   DC    CL80'READ JFCB MACRO REPORTED AN ERROR. BYE BYE.'
FUBMSG1  DC    CL80'THIS PROGRAM REQUIRES TAPE INPUT. SORRY.'
         SPACE 1
EOFMSG   DC    CL80' '             FOR EOF MESSAGE WITH REC COUNT
         ORG   EOFMSG
         DC    C'     END OF FILE  AFTER '
TOTRC    DC    CL6' '              COUNT GOES HERE
         DC    C' RECORDS.'
         ORG   ,
         SPACE 1
MODBUF   DC    CL80' '             MODULE STATS LINE BUFFER
         ORG   MODBUF              NOW ALLOCATE PARTS
PLINE    DC    CL15' '             FOR MODULE NAME
         DC    C'TAPE='
TAPLAB   DC    CL10' '             FOR TAPE LABEL
         DC    C'FILE='
TAPFNO   DC    CL10' '             FOR FILE NUMBER
         DC    C'START='
RECNO    DC    CL10' '             FOR RECORD NUMBER
         DC    C'LEN='
NORPF    DC    CL10' '             FOR NUMBER RECORDS IN MODULE
         ORG   ,                   RETURN TO ORIGINAL COUNTER
         SPACE 1
SYSPRINT DCB   BLKSIZE=11440,LRECL=80,RECFM=FB,DSORG=PS,               X
               MACRF=PM,DDNAME=SYSPRINT
SYSUT2   DCB   BLKSIZE=11440,LRECL=80,RECFM=FB,DDNAME=SYSUT2,MACRF=PM, X
               DSORG=PS
SYSUT1   DCB   BLKSIZE=32720,DDNAME=SYSUT1,DSORG=PS,EODAD=NEXTFILE,    X
               DEVD=TA,DEN=4,RECFM=FB,LRECL=80,MACRF=GL,EXLST=LSTA,    X
               EROPT=ACC
         SPACE 1
LSTA     DS    0F                  EXIT LIST FOR READ JFCB MACRO
         DC    X'07'
         DC    AL3(INFMJFCB)
         SPACE 1
RD1      RDJFCB (SYSUT1),MF=L      LIST FORM OF READ JFCB MACRO
         IEFJFCBN LIST=YES         JFCB MAPPING MACRO
         END   TAPEREAD            END OF PROG.
