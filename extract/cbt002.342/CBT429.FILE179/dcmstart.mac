         MACRO
&NAME    DCMSTART &A,&LV=80,&SA=SAVEAREA,&REGS=,                       X
               &BASE=12,&IOCB=YES,&IREG=,&CB=NO,                       X
               &BASE2=,                                                $
               &KEEP=PARMADDR,&TEMP=2,                                 X
               &STAE=NO,&XCMD=YES,                                     X
               &PARM=5,&WORK=DCMSWORK
.*.
.*$MACRO=DCMSTART   DATE=09/00/79       SOURCE=SDC
.*
.*  THIS MACRO IS PART OF DCMS.  IT IS USED TO DO THE OS HOUSEKEEPING
.*  AND DCMS INITIALIZATION NEEDED AT ENTRANCE TO ANY ROUTINE.
.*
.*  Updates:
.*    30Jun97  LDW - Move "DROP 15" to keep HLA 1.2 happy
.*    06-25-80 LDW - LOAD "TERMIO" AND SAVE ADDRESS IN "IOCBATIO"
.*.
         COPY  DCMSGBLS
         LCLA  &RNM
         LCLA  &RCT
         LCLA  &RLST
         LCLC  &CNM
         LCLA  &RDSP
&RCT     SETA  0
&RDSP    SETA  0
         AIF   (&LIST).LIST
         AGO   .LISTX
.LIST    ANOP
*                                                                     *
*                                                                     *
*        D C M S T A R T    M A C R O                                 *
*                                                                     *
*                                                                     *
         SPACE 4
*                                                                     *
*        FUNCTION:                                                    *
*                   CREATES INITIAL CODING (CSECT) FOR AN             *
*                   ASSEMBLY LANGUAGE MODULE:                         *
*                        1.   CSECT STATEMENT                         *
*                        2.   REGISTER SAVING                         *
*                        3.   ADDRESSABILITY                          *
*                        4.   NEW SAVE AREA  (GETMAIN)                *
*                                                                     *
*        FORMAT:                                                      *
*                                                                     *
*              NAME  DCMSTART  TYPE,LV=NN
*         OR                                                          *
*              NAME  DCMSTART  TYPE,REGS=X,PARM=Y
*                                                                     *
*                                                                     *
*                                                                     *
*                  -  WHERE  -                                        *
*                                                                     *
*                        NAME  IS THE CSECT NAME                      *
*                                                                     *
*                        TYPE  IS  R                                  *
*                                  RENT                               *
*                                  REENTRANT                          *
*                              FOR REENTERABLE CODE                   *
*                                                                     *
*                              OR  S                                  *
*                                  STD                                *
*                                  REUS                               *
*                                  BLANK                              *
*                              FOR REUSABLE CODE                      *
*                                                                     *
*                        LV    IS  NUMBER OF BYTES IN GETMAIN         *
*                                  GENERATED FOR REENTRANT            *
*                                  MODULES                            *
*                                                                     *
*                                                                     *
*                                                                     *
*                        REGS  IS  THE NUMBER OF REGISTERS TO BE      *
*                                  USED FOR ADDRESSABILITY IN         *
*                                  RE-ENTRANT MODULES.                *
*                                                                     *
*                        PARM  IS  THE REGISTER TO BE USED            *
*                                  TO SAVE R1 IN - RE-ENTRANT         *
*                                  MODULE CODE.                       *
*                                                                     *
*                                                                     *
*              NOTE THAT THIS MACRO MUST BE USED WITH THE             *
*              - DCMSTOP-  MACRO                                      *
*                                                                     *
         MEXIT
.LISTX   ANOP
&LVNUM   SETC  '&LV'
&RENT    SETB  0
&CSECT   SETC  '&NAME'
         AIF   ('&NAME' EQ '').MG1
&NAME    CSECT
         AIF   (&PLG).MG4
&PLG     SETB  1
         SPACE 8
         DCMSREGS
         EJECT
         SPACE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*     D C M S    S T A N D A R D    E N T R Y    L I N K A G E        *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         AIF   ('&A' EQ '').NORENT
         AIF   ('&A' EQ 'R').RENT
         AIF   ('&A' EQ 'S').NORENT
         AIF   ('&A' EQ 'RENT').RENT
         AIF   ('&A' EQ 'REUS').NORENT
         AIF   ('&A' EQ 'STD').NORENT
         AIF   ('&A' EQ 'REENTRANT').RENT
         AGO   .MG3
.NORENT  ANOP
         AGO   .END
.RENT    ANOP
&RENT    SETB  1
         AIF   ('&WORK' EQ '').MG1
&DSECT   SETC  '&WORK'
         SPACE 2
****         ****
*   REENTRANT   *
****         ****
         SPACE 4
&NAME    CSECT
         USING *,R15
         B     DCMSECT$
         DC    AL1(27)
DCMS$NAM DC    CL9'&NAME.'
         DC    CL9'&SYSDATE.'
         DC    CL9'&SYSTIME.'
DCMSECT$ STM   14,12,12(13)             SAVE ENTRY REGISTERS
         LR    &BASE,R15
         DROP  15
         AIF   (T'&BASE2 EQ 'O').BASE1
         LA    &BASE2,2048(,&BASE)
         LA    &BASE2,2048(&BASE2)
         USING &NAME,&BASE,&BASE2
         AGO   .BASECOM
.BASE1   ANOP  ,
         USING &NAME,&BASE
.BASECOM ANOP  ,
         LR    &PARM.,R1                PROTECT PARM LIST
&RNM     SETA  &BASE
&CNM     SETC  '&NAME.'
.*30JUN97  DROP  15
*        GETMAIN R,LV=&LV
         CNOP  0,4
         BAL   1,*+8                    BRANCH AROUND LENGTH
         DC    A(&LV.)                  DEFINE SAVE/WORK AREA LENGTH
         L     0,0(,1)                  LOAD LENGTH
         SVC   10                       GETMAIN SAVE/WORK AREA
         ST    13,4(,1)                 FORWARD CHAIN SAVEAREAS
         ST    1,8(,13)                 BACKWARD CHAIN SAVE AREAS
         LR    13,1
         USING &WORK.,R13
         L     &TEMP,4(,13)
         LM    15,1,16(&TEMP.)
         ST    &PARM.,&KEEP
         AGO   .END
.END     ANOP
         AIF   ('&REGS' EQ '').DFRG
&RCT     SETA  &REGS
.DFRG    ANOP
         AIF   (&RCT LT 1).EXRG
         AIF   (&RCT LT 9).RGEN
         MNOTE 9,'MAXIMUM OF 8 BASE REGISTERS  EXCEEDED'
         MEXIT
.RGEN    ANOP
.*       GENERATION OF ADDITIONAL BASE REGISTERS                     *.
         AIF   (&RCT EQ 0).EXRG
&RLST    SETA  &RNM
&RNM     SETA  &RNM-1
&RCT     SETA  &RCT-1
&RDSP    SETA  &RDSP+4096
         LR    &RNM.,&RLST
         LA    &RNM.,2048(&RNM.)
         LA    &RNM.,2048(&RNM.)
         USING &CNM.+&RDSP.,&RNM
         AGO   .RGEN
.EXRG    ANOP
         AIF   ('&STAE' NE 'YES').X1
         L     &TEMP.,=V(XCMDSTAE)
         ESTAE (&TEMP.),CT              SET ESTAE ENVIRONMENT
.X1      ANOP
         AIF ('&IREG' EQ '').NOIRG
         USING IOCB,&IREG               DEFAULT IOCB USING
         LA    &IREG.,IOCB              SPECIFIED REGISTER
*
*
.NOIRG   ANOP
         AIF ('&IOCB' EQ 'NO').NOIOCB
         MVC   IOCBNAME(8),DCMS$NAM
*        LOAD  EPLOC==CL8'TERMIO'
         LOAD  EPLOC==CL8'TERMIO'
*
         ST    R0,IOCBATIO
.NOIOCB  ANOP
         AIF  ('&CB' EQ '').NOCBS
         AIF  ('&CB' EQ 'NO').NOCBS
         AIF  ('&CB' EQ 'YES').YESCBS
         MNOTE 9,'INVALID -CB- OPERAND SPECIFICATION'
         MEXIT
.YESCBS  ANOP
.NOCBS   ANOP
DCMSOVER L     R1,&KEEP                 RECOVER PARM LIST
.*LDW*** EJECT
.*
.*
.*
.*
.*
.*
.*
.*
         MEXIT
.MG1     ANOP
         MNOTE 9,'NAME FIELD MISSING'
         MEXIT
.MG2     ANOP
         MNOTE 9,'NAME ENTRY MUST BE SEVEN CHARACTERS OR LESS'
         MEXIT
.MG3     ANOP
         MNOTE 9,'INVALID MODE PARAMETER SPECIFIED'
         MEXIT
.MG4     ANOP
         MNOTE 9,'A DCMSTOP MACRO MUST APPEAR BETWEEN TWO SUCCESSIVE'
         MNOTE 9,'DCMSTART MACRO CALLS'
         MEND
