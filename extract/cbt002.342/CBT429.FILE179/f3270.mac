         MACRO
&NFS     F3270 &COL=,&LINE=,&ROW=,&POS=,&INITIAL=,&LENGTH=,&ATTR=,     $
               &RA=,&MODEL=,                                           $
               &$C$=(BLUE,F1,RED,F2,PINK,F3,GREEN,F4,TURQUOIS,F5,      $
               YELLOW,F6,WHITE,F7,NEUTRAL,F7,DEFCOLOR,00),             $
               &$H$=(BLINK,F1,REVERSE,F2,UNDER,F4,DEFHI,00),           $
               &$S$=(12,40,480,24,80,1920,32,80,2560,43,80,3440,       $
               27,132,3564)
.*
.*   INTERNAL MACRO REFERENCES:
.*      $3270 - GENERATE VALID 3270 CONTROL FIELD BIT CONFIG.
.*
.*   GLOBAL SET SYMBOL REFERENCES:
.*      &$3270 - COMMUNICATION WITH $3270 INNER MACRO
.*      &F3270M - THE MODEL OF 3270 WE'RE GENNING FOR
.*      &F3270E - HAVE WE REGALED HIM WITH THE ERROR MESSAGE
.*
.*   GENERATE A 3270 FIELD DEFINITION WITH THE FOLLOWING FEATURES:
.*   1. A SET BUFFER ADDRESS (SBA) SEQUENCE, OR A REPEAT TO ADDRESS
.*      (RA) SEQUENCE.  THE APPROPRIATE KEYWORDS ARE:
.*         ROW= (OR LINE=) AND COL= OF THE BUFFER ADDRESS, OR
.*         POS= ABSOLUTE 3270 BUFFER ADDRESS, OR
.*         POS=(ROW,COL) OF THE BUFFER ADDRESS, AND
.*         RA= CHARACTER TO BE REPEATED (TWO DIGITS OF HEX, OR
.*             C'CHAR'); AND
.*   2. ATTRIBUTE (SF,SFE,SA,MF) SEQUENCE(S):
.*         ATTR=(ORDER,ATTRIBUTES,ORDER,ATTRIBUTES,...) WITH MNEMONIC
.*         NAMES; AND
.*   3. A FIELD OF DATA WITH AN INITIAL VALUE OF SPACES:
.*         LENGTH= DESIRED NUMBER OF SPACES; OR
.*      A FIELD WITH A SPACIFIED INITIAL VALUE:
.*         INITIAL= DESIRED CHARACTER STRING, AND
.*         LENGTH= (OPTIONAL) LENGTH ON DC STATEMENT.
.*
.*   NOTE: IF A LABEL IS SPECIFIED THEN THE DATA FIELD (IF IT IS
.*         GENERATED) WILL BE LABELLED 'LABEL'.
.*
.* 14MAR85 SDM - ADD MODEL=5, CHANGE SA AND MF APPROACH AND ADD SFE.
.*               ELIMINATE TROUBLESOME NUMERIC TESTS.
.* 09MAR81 LDW - ADD DEFCOLOR AND DEFHI EXTENDED ATTRIBUTES.
.* 29JAN81 LDW - ADD SA= AND MA= FOR 3279 HIGHLIGHT AND COLOR.
.* 06SEP79 SDM - CHANGE TO TWO CHAR INTERFACE TO $3270.
.* 05SEP79 SDM - ADD POS=(ROW,COL) OPTION.
.*               ADD ADDRESS RANGE TESTS.
.* 04SEP79 SDM - REWRITTEN TO USE $3270 AND ELIMINATE SBA3270.
.* 26JUL79 LDW - NEW MACRO.
.*
         GBLA  &F3270M
         GBLB  &F3270E
         GBLC  &$3270
         LCLA  &A,&B,&I,&J,&CA,&HA
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B7,&FA,&FAP
         LCLC  &L,&O,&OX
*************************************************F3270**14MAR85**SDM***
         AIF   ('&MODEL' EQ '').MOD3
         AIF   ('&MODEL' LT '1').MOD1
         AIF   ('&MODEL' LE '5').MOD2
.MOD1    MNOTE 8,'MODEL= IS NOT BETWEEN 1 AND 5.  IGNORED.'
         AGO   .MOD3
.MOD2    ANOP
&F3270M  SETA  (&MODEL-1)*3+1
         AGO   .MOD4
.MOD3    AIF   (&F3270M NE 0).MOD4
         MNOTE 0,'MODEL=2 IS ASSUMED'
&F3270M  SETA  (2-1)*3+1
.MOD4    AIF   ('&LINE&ROW&COL&POS' EQ '').NOPOS1
         AIF   ('&POS' NE '').POS1
         AIF   ('&ROW' EQ '').ROW1
&A       SETA  &ROW
         AIF   ('&LINE' EQ '').ROW3
         MNOTE 8,'LINE AND ROW ARE SYNONYMS - ROW WILL BE USED'
         AGO   .ROW3
.ROW1    AIF   ('&LINE' EQ '').ROW2
&A       SETA  &LINE
         AGO   .ROW3
.ROW2    MNOTE 8,'LINE OR ROW INVALID - ROW=1 ASSUMED'
&A       SETA  1
.ROW3    AIF   ('&COL' NE '').COL1
         MNOTE 8,'COL INVALID - COL=1 ASSUMED'
&B       SETA  1
         AGO   .RNG1
.COL1    ANOP
&B       SETA  &COL
         AGO   .RNG1
.POS1    AIF   ('&LINE&ROW&COL' EQ '').POS2
         MNOTE 8,'LINE, ROW, OR COL MAY NOT OCCUR WITH POS - POS VALUE X
               USED'
.POS2    AIF   ('&POS'(1,1) NE '(').POS7
         AIF   ('&POS(1)' NE '').POS3
         MNOTE 8,'POS= ROW INVALID - 1 ASSUMED'
&A       SETA  1
         AGO   .POS4
.POS3    ANOP
&A       SETA  &POS(1)
.POS4    AIF   ('&POS(2)' NE '').POS5
         MNOTE 8,'POS= COLUMN INVALID - 1 ASSUMED'
&B       SETA  1
         AGO   .POS6
.POS5    ANOP
&B       SETA  &POS(2)
.POS6    AIF   (N'&POS LE 2).RNG1
         MNOTE 8,'MORE THAN 2 VALUES TO POS= - EXCESS IGNORED'
.RNG1    AIF   (&A GE 1).RNG2
&A       SETA  1
         AGO   .RNG3
.RNG2    AIF   (&A LE &$S$(&F3270M)).RNG4
&A       SETA  &$S$(&F3270M)
.RNG3    MNOTE 8,'SPECIFIED ROW OUT OF RANGE - &A ASSUMED'
.RNG4    AIF   (&B GE 1).RNG5
&B       SETA  1
         AGO   .RNG6
.RNG5    AIF   (&B LE &$S$(&F3270M+1)).RNG7
&B       SETA  &$S$(&F3270M+1)
.RNG6    MNOTE 8,'SPECIFIED COL OUT OF RANGE - &B ASSUMED'
.RNG7    ANOP
&A       SETA  (&A-1)*&$S$(&F3270M+1)+&B-1
         AGO   .ADDR
.POS7    ANOP
&A       SETA  &POS
         AIF   (&A LT &$S$(&F3270M+2)).ADDR
&A       SETA  &$S$(&F3270M+2)-1
         MNOTE 8,'SPECIFIED POS OUT OF RANGE - &A ASSUMED'
.ADDR    ANOP
&B       SETA  &A/64
&A       SETA  &A-&B*64
         $3270 &B,&A
         AIF   ('&RA' NE '').RA1
         DC    X'11&$3270'              SBA
         AGO   .ATTR1
.RA1     AIF   ('&RA'(1,2) EQ 'C''').RA2
         DC    X'3C&$3270',X'&RA'       RA
         AGO   .ATTR1
.RA2     DC    X'3C&$3270',&RA          RA
         AGO   .ATTR1
.NOPOS1  AIF   ('&RA' EQ '').ATTR1
         MNOTE 8,'RA= INVALID WITHOUT POSITIONING OPERANDS - IGNORED'
.ATTR1   AIF   ('&ATTR' EQ '').ATTR6
&I       SETA  0
.ATTRO1  ANOP
&A       SETA  0
&CA      SETA  0
&FA      SETB  0
&FAP     SETB  0
&HA      SETA  0
.ATTR2   ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&ATTR).OKATTR
         AIF   ('&ATTR(&I)' EQ 'SF').ATTRO2
         AIF   ('&ATTR(&I)' EQ 'SFE').ATTRO2
         AIF   ('&ATTR(&I)' EQ 'SA').ATTRO2
         AIF   ('&ATTR(&I)' EQ 'MF').ATTRO2
         AIF   (&I GT 1).ATTRA
         MNOTE 4,'ATTR= WITHOUT VALID ORDER TYPE - SF ASSUMED'
.ATTRA   AIF   (NOT &FAP).ATTRFN
&FA      SETB  1                        FIELD ATTRIBUTE SPECIFIED
.ATTRFN  ANOP
&FAP     SETB  1                        PROCESSING FIELD ATTRIBUTES
         AIF   ('&ATTR(&I)' EQ 'ASKIP' ).ATTR30
         AIF   ('&ATTR(&I)' EQ 'PROT'  ).ATTR20
         AIF   ('&ATTR(&I)' EQ 'NUM'   ).ATTR10
         AIF   ('&ATTR(&I)' EQ 'UNPROT').ATTR30X
         AIF   ('&ATTR(&I)' EQ 'DARK'  ).ATTR0C
         AIF   ('&ATTR(&I)' EQ 'NDISP' ).ATTR0C
         AIF   ('&ATTR(&I)' EQ 'BRT'   ).ATTR08
         AIF   ('&ATTR(&I)' EQ 'BRIGHT').ATTR08
         AIF   ('&ATTR(&I)' EQ 'SPD'   ).ATTR04
         AIF   ('&ATTR(&I)' EQ 'DET'   ).ATTR04
         AIF   ('&ATTR(&I)' EQ 'NORM'  ).ATTR0CX
         AIF   ('&ATTR(&I)' EQ 'MDT'   ).ATTR01
         AIF   ('&ATTR(&I)' EQ 'MOD'   ).ATTR01
&FAP     SETB  0                        THIS WASN'T A FIELD ATTRIBUTE
&J       SETA  1
.ATTRC1  AIF   (&J GT N'&$C$).ATTRCX
         AIF   ('&ATTR(&I)' EQ '&$C$(&J)').ATTRC2
&J       SETA  &J+2
         AGO   .ATTRC1
.ATTRC2  AIF   (&CA EQ 0).ATTRC3
         MNOTE 8,'MULTIPLE COLOR ATTRIBUTES - &$C$(&CA) IGNORED'
.ATTRC3  ANOP
&CA      SETA  &J
         AGO   .ATTR2
.ATTRCX  ANOP
&J       SETA  1
.ATTRH1  AIF   (&J GT N'&$H$).ATTRHX
         AIF   ('&ATTR(&I)' EQ '&$H$(&J)').ATTRH2
&J       SETA  &J+2
         AGO   .ATTRH1
.ATTRH2  AIF   (&HA EQ 0).ATTRH3
         MNOTE 8,'MULTIPLE HIGHLIGHT ATTRIBUTES - &$H$(&HA) IGNORED'
.ATTRH3  ANOP
&HA      SETA  &J
         AGO   .ATTR2
.ATTRHX  ANOP
         MNOTE 8,'INVALID ATTR - &ATTR(&I) IGNORED'
         AGO   .ATTR4
.***  ASKIP
.ATTR30  AIF   (&B7 OR &B2).ATTR3       ASKIP OR NUMERIC
&B7      SETB  1                        ASKIP HAS BEEN HERE
         AIF   (&B1).ATTR10X            ALREADY PROT, SET NUM
&A       SETA  &A+X'30'
         AGO   .ATTR2
.***  PROT
.ATTR20  AIF   (&B1).ATTR3              PROT
&B1      SETB  1                        PROT HAS BEEN SPECIFIED
         AIF   (&B7).ATTR2              ALREADY ASKIP
&A       SETA  &A+X'20'
         AGO   .ATTR2
.***  NUM
.ATTR10  AIF   (&B2 OR &B7).ATTR3       NUMERIC OR ASKIP
&B2      SETB  1                        DON'T ALLOW NUMERIC AGAIN
.ATTR10X ANOP
&A       SETA  &A+X'10'
         AGO   .ATTR2
.***  UNPROT
.ATTR30X AIF   (&B1 OR &B7).ATTR3       PROT OR ASKIP
&B1      SETB  1                        DON'T ALLOW PROT
&B7      SETB  1                        DON'T ALLOW ASKIP
         AGO   .ATTR2
.***  DARK/NDISP
.ATTR0C  AIF   (&B3 OR &B4).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
&B4      SETB  1                        DON'T ALLOW DET
&A       SETA  &A+X'0C'                 X'08'+X'04'
         AGO   .ATTR2
.***  BRT/BRIGHT
.ATTR08  AIF   (&B3).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
         AIF   (NOT &B5).ATTR08A
&A       SETA  &A-X'04'                 TAKE BACK THE DET ATTR
.ATTR08A ANOP
&A       SETA  &A+X'08'
&B5      SETB  1                        DET HAS BEEN SET
         AGO   .ATTR2
.***  SPD/DET
.ATTR04  AIF   (&B4).ATTR3
&B4      SETB  1                        DON'T ALLOW DET
         AIF   (&B5).ATTR2              BRIGHT HAS BEEN HERE
&B5      SETB  1                        DET HAS BEEN SET
&A       SETA  &A+X'04'
         AGO   .ATTR2
.***  NORM
.ATTR0CX AIF   (&B3).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
         AGO   .ATTR2
.***  MDT/MOD
.ATTR01  AIF   (&B6).ATTR3
&B6      SETB  1
&A       SETA  &A+X'01'
         AGO   .ATTR2
.ATTR3   MNOTE 8,'DUPLICATE OR CONFLICTING ATTR - &ATTR(&I) IGNORED'
.ATTR4   AIF   (&F3270E).ATTR2
&F3270E  SETB  1
         MNOTE *,'TRY THESE ATTRIBUTE NAMES:'
         MNOTE *,'  (FIELD) UNPROT, PROT, ASKIP, NUM, NORM, BRT,'
         MNOTE *,'    BRIGHT, DARK, NDISP, DET, SPD, MDT, MOD;'
         MNOTE *,'  (COLOR) BLUE, RED, PINK, GREEN, TURQUOIS, YELLOW,'
         MNOTE *,'    WHITE, NEUTRAL, DEFCOLOR;'
         MNOTE *,'  (HIGHLIGHT) BLINK, REVERSE, UNDER,'
         MNOTE *,'    NORMHI, DEFHI.'
         AGO   .ATTR2
.*
.ATTRO2  AIF   (&I LE 1).ATTR5
.OKATTR  AIF   ('&O' NE 'SA').ATTRSA4
         AIF   (NOT &FA).ATTRSA1
         MNOTE 8,'FIELD ATTRIBUTES IGNORED FOR ATTR=SA'
.ATTRSA1 AIF   ((&CA EQ 0) AND (&HA EQ 0)).ATTRSA3
         AIF   (&HA EQ 0).ATTRSA2
         DC    X'2841&$H$(&HA+1)'       SA (HIGHLIGHT)
.ATTRSA2 AIF   (&CA EQ 0).ATTR5
         DC    X'2842&$C$(&CA+1)'       SA (COLOR)
         AGO   .ATTR5
.ATTRSA3 DC    X'280000'                SA (RESET)
         AGO   .ATTR5
.ATTRSA4 AIF   (('&O' NE 'SFE') AND ('&O' NE 'MF')).ATTRMF5
&OX      SETC  ''
         AIF   (NOT &FA).ATTRMF1
         $3270 &A
&OX      SETC  'C0&$3270'
.ATTRMF1 AIF   (&HA EQ 0).ATTRMF2
&OX      SETC  '&OX.41&$H$(&HA+1)'
.ATTRMF2 AIF   (&CA EQ 0).ATTRMF3
&OX      SETC  '&OX.42&$C$(&CA+1)'
.ATTRMF3 ANOP
&A       SETA  K'&OX/4
         AIF   ('&O' NE 'SFE').ATTRMF4
         DC    X'290&A&OX'              SFE
         AGO   .ATTR5
.ATTRMF4 DC    X'2C0&A&OX'              MF
         AGO   .ATTR5
.ATTRMF5 $3270 &A
         DC    X'1D&$3270'              SF
         AIF   ((&CA EQ 0) AND (&HA EQ 0)).ATTR5
         MNOTE 8,'COLOR OR HIGHLIGHT ATTRIBUTES IGNORED FOR ATTR=SF'
.ATTR5   AIF   (&I GT N'&ATTR).ATTR6
&O       SETC  '&ATTR(&I)'
         AGO   .ATTRO1
.ATTR6   ANOP
.INIT1   AIF   ('&INITIAL&LENGTH' EQ '').EXIT
         AIF   ('&INITIAL' EQ '').JUSTL
         AIF   ('&LENGTH' EQ '').JUSTI
&NFS     DC    CL&LENGTH&INITIAL        DATA
         AGO   .EXIT
.JUSTL   ANOP
&NFS     DC    CL&LENGTH' '             DATA AREA
         AGO   .EXIT
.JUSTI   ANOP
&NFS     DC    C&INITIAL                DATA
.EXIT    ANOP
***********************************************************************
         SPACE 1
         MEND
