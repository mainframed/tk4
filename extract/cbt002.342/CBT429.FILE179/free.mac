         MACRO
&LABEL   FREE  &DDNAME=,&DSN=,&MEMBER=,&PREFIX=NO,                     $
               &FILE=,&DATASET=,                                       $
               &SYSOUT=,                                               $
               &JOBNAME=,                                              $
               &DISP=,&PERM=YES,                                       $
               &MF=,&SYNAD=
.*.
.*$MACRO=FREE       DATE=04/00/79       SOURCE=UCLA
.*
.*  THIS MACRO WILL FILL IN THE DAIR PARAMETER LIST AND CALL DAIR TO
.*       FREE A DATASET.
.*.
         GBLB  &OMITTED,&DROP
         LCLA  &PERMBIT
&PERMBIT SETA  0                        PERM = X'10'
         AIF   ('&PERM' NE 'YES').NOTPERM
&PERMBIT SETA  16                       =X'10'
.NOTPERM ANOP
&LABEL   DS    0H
         $MF   DAPB18,15,&MF
         XC    DAPB18(DAPB18L),DAPB18   INITIALIZE DAIR BLOCK TO ZERO
         MVI   DA18CD+1,X'18'           DAIR ENTRY CODE FOR FREE
         $RXR  DA18PDSN,&DSN&DATASET,0  POINTER TO DSNAME
         $RXQ  DA18DDN,&DDNAME&FILE,8   DDNAME
         $RXQ  DA18JBNM,&JOBNAME,8      JOBNAME
         $RXQ  DA18SCLS,&SYSOUT,2       SYSOUT CLASS
         AIF   (&OMITTED).MEM
         MVI   DA18SCLS+1,C' '          BLANK 2ND BYTE OF OUTPUT CLASS
.MEM     $RXQ  DA18MNM,&MEMBER,8        MEMBER NAME
         AIF   ('&PREFIX' NE  'YES').NOPREF
         OI    DA18CTL,DA18UID+&PERMBIT PREFIX CHARGENO.USERID TO DSN
         AGO   .DISPOS
.NOPREF  AIF   ('&PERM' NE 'YES').DISPOS FREE PERM DATASET ?
         OI    DA18CTL,&PERMBIT         FREE PERMANENTLY ALLOCATED DS
.DISPOS  AIF   (T'&DISP    EQ 'O').NODISP2
         AIF   ('&DISP'    NE 'KEEP').DELETE2
         MVI   DA18DPS2,DA18KEEP        DISP=(,KEEP)
         AGO   .DISP3
.DELETE2 AIF   ('&DISP'    NE 'DELETE').CATLG2
         MVI   DA18DPS2,DA18DEL         DISP=(,DELETE)
         AGO   .DISP3
.CATLG2  AIF   ('&DISP'    NE 'CATLG').UNCAT2
         MVI   DA18DPS2,DA18CAT         DISP=(,CATLG)
         AGO   .DISP3
.UNCAT2  AIF   ('&DISP'    NE 'UNCATLG').DISPBAD
         MVI   DA18DPS2,DA18UCAT        DISP=(,UNCATLG)
         AGO   .DISP3
.NODISP2 ANOP
*        DISPOSITION SPECIFIED WHEN ALLOCATED WILL BE USED
         AGO   .DISP3
.DISPBAD ANOP
         MNOTE *,'*****************************************'
         MNOTE  12,'INVALID DISPOSITON, DISP= &DISP  '
         MNOTE  *,'*****************************************'
         AGO   .END
.DISP3   AIF   ('&MF(1)' EQ 'B').ENDB
         AIF   ('&MF(1)' EQ 'L').END
.* OTHERWISE MF='E' OR '' OR INVALID IS 'E'
         TSDAIR DAPB=(15),SYNAD=&SYNAD
         AGO   .END
.ENDB    ST    15,DAPLDAPB
         LA    R1,DAPL
.END     AIF    (NOT &DROP).END1
         DROP  15               DROP ADDRESSIBLITY TO DAIR BLOCK
.END1    SPACE 3
         MEND
