         MACRO -- $MVCX -- INTERNAL MACRO FOR MVCP AND MVCS
         $MVCX &M,&O1
.* 5JAN83 WRITTEN BY STEVEN D. MCGINTY
         GBLC  &$MVCXA,&$MVCXR
         LCLA  &I,&I1,&I2
&$MVCXA  SETC  '0'
&$MVCXR  SETC  '0'
.* LOCATE D1 (DELIMITED BY '(' OR EOS)
&I       SETA  1
&I1      SETA  &I
.LD1     AIF   (&I GT K'&O1).D1
         AIF   ('&O1'(&I,1) EQ '(').D1
&I       SETA  &I+1
         AGO   .LD1
.D1      ANOP
&I2      SETA  &I-&I1
         AIF   (&I2 LT 1).ER3
&$MVCXA  SETC  '&O1'(&I1,&I2)
         AIF   (&I GT K'&O1).O2
.* LOCATE R1 (DELIMITED BY ',' OR ')')
&I       SETA  &I+1
&I1      SETA  &I
.LR1     AIF   (&I GT K'&O1).ER4
         AIF   ('&O1'(&I,1) EQ ')').R1
         AIF   ('&O1'(&I,1) EQ ',').R1B
&I       SETA  &I+1
         AGO   .LR1
.R1      AIF   (&I EQ &I1).ERX
.R1B     ANOP
&I2      SETA  &I-&I1
         AIF   (&I2 LT 1).NR1
&$MVCXR  SETC  '&O1'(&I1,&I2)
.NR1     ANOP
         AIF   ('&O1'(&I,1) EQ ')').NB1
.* LOCATE B1 (OPTIONAL) IN FIRST OPERAND (DELIMITED BY ')')
&I       SETA  &I+1
&I1      SETA  &I
.LB1     AIF   (&I GT K'&O1).ER5
         AIF   ('&O1'(&I,1) EQ ')').B1
&I       SETA  &I+1
         AGO   .LB1
.B1      ANOP
&I2      SETA  &I-&I1
         AIF   (&I2 LT 1).ER7
&$MVCXA  SETC  '&$MVCXA.('.'&O1'(&I1,&I2).')'
.NB1     ANOP
         AIF   (&I LT K'&O1).ER8
.O2      ANOP
         MEXIT
.ER3     MNOTE 8,'&M: NO D1 SPECIFIED'
         AGO   .ERF
.ER4     MNOTE 8,'&M: COULD NOT LOCATE DELIMITER AFTER R1'
         AGO   .ERF
.ERX     MNOTE 8,'&M: EXPRESSION INCOMPLETE -- R1 MISSING'
         AGO   .ERF
.ER5     MNOTE 8,'&M: COULD NOT LOCATE PAREN AFTER B1'
         AGO   .ERF
.ER7     MNOTE 8,'&M: EXPRESSION INCOMPLETE -- B1 MISSING'
         AGO   .ERF
.ER8     MNOTE 8,'&M: EXCESS DATA AFTER (R1,B1)'
.ERF     MNOTE *,'INSTRUCTION FORMAT -- D1(R1,B1),D1(B2),R3'
         MEND
