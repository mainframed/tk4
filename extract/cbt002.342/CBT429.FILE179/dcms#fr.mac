         MACRO
&NAME    DCMS#FR &DIR,&N,&IMAGE
.*.
.*$MACRO=DCMS#FR    DATE=03/31/80       SOURCE=LDW
.*$MACRO=#FRAME     DATE=10/23/79       SOURCE=DCMS (DLH@SDC)
.*
.*  THIS MACRO IS PART OF DCMS.
.*
.* UPDATES:
.*    02/24/81 SDM  - MAJOR CHANGE:  FIX FOR STACK SIZE NOT
.*                    MULTIPLE OF FRAME SIZE
.*                  - IMPLEMENT 'TEST' OPERAND
.*.
&NAME    DS    0H
         AIF   ('&DIR' EQ 'FORWARD').FOR
         AIF   ('&DIR' EQ 'F').FOR
         AIF   ('&DIR' EQ 'BACKWARD').BAK
         AIF   ('&DIR' EQ 'B').BAK
         MNOTE 9,'DIRECTION SPECIFICATION MISSING OR INVALID'
         MEXIT
.FOR     AIF   ('&IMAGE' EQ 'TEST').TSTFOR
         L     R0,#NOW&N                CURRENT FRAME (SOURCE)
         L     R1,#END&N                END OF STACK
         SR    R1,R0                    REMAINING STACK LENGTH
         C     R1,#SCR&N                MORE THAN FRAME LENGTH?
         BNH   *+8                      NO, USE FOR SOURCE LENGTH
         L     R1,#SCR&N                YES, USE ONE FRAME
         LA    R14,&IMAGE               TRMIO IMAGE ADDRESS (DEST)
         L     R15,#SCR&N               DEST LENGTH
         MVCL  R14,R0                   MOVE (FILL WITH NULLS)
         L     R1,#NOW&N                POINT TO
         A     R1,#SCR&N                           NEXT FRAME
         C     R1,#END&N                FREEZE
         BNH   *+8                              IF
         L     R1,#END&N                            NECESSARY
         ST    R1,#NOW&N                SAVE NEXT FRAME ADDRESS
         MEXIT
.TSTFOR  SR    R15,R15                  INITIALIZE RETURN CODE
         L     R14,#NOW&N               CURRENT FRAME
         C     R14,#STK&N               ANY MORE DATA?
         BL    *+8                      YES, LEAVE RC=0
         LA    R15,4                    INDICATE NO MORE FRAMES
         MEXIT
.BAK     ANOP
         MEND
