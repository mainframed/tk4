         MACRO
&NAME    PFKEY &TYPE=IOCB,&KEYGEN=NO,                                  X
               &CLEAR=,&ENTER=,&ATTN=,&CNCL=,&DEF=,&RESHOW=,           X
               &K1=,&K2=,&K3=,&K4=,&K5=,&K6=,                          X
               &K7=,&K8=,&K9=,&K10=,&K11=,&K12=
.*.
.*$MACRO=PFKEY      DATE=09/00/79       SOURCE=SDC
.*
.*  THIS MACRO IS PART OF DCMS.  IT IS USED TO SELECT A PROCESSING
.*  ROUTINE BASED ON THE AID.
.*
.*  UPDATES:
.*
.*    02/28/86 LDW - ADD RESHOW= KEYWORD FOR SAME USE AS CNCL=
.*    11/10/81 LDW - MOVE CHECK FOR ENTER TO IN FRONT OF PFKEY TESTS
.*.
         COPY  DCMSGBLS
         LCLC  &N
&N       SETC  '&NAME'
         SPACE
*                                                                     *
*        I N P U T    A N A L Y S I S                                **
*                                                                   ***
*                                            AID / PF KEY SELECTOR ****
*                                                                   ***
*                                                                    **
*                                                                     *
         SPACE
         AIF   ('&KEYGEN' NE '').KK0
.KDFLT   ANOP
*
*        DEFAULT PFK KEYNAMES / FUNCTIONS USED
*
         SPACE
         MEXIT
.KK0     AIF   ('&ENTER' EQ '').KK1
&N       CLI   &TYPE.AID,AIDENT
&N       SETC  ''
         BE    &ENTER                            <=====   E N T E R
.KK1     AIF   ('&K1' EQ '').KK2
&N       CLI   &TYPE.AID,AIDPF1
&N       SETC  ''
         BE    &K1                               <=====   P F K 1
.KK2     AIF   ('&K2' EQ '').KK3
&N       CLI   &TYPE.AID,AIDPF2
&N       SETC  ''
         BE    &K2                               <=====   P F K 2
.KK3     AIF   ('&K3' EQ '').KK4
&N       CLI   &TYPE.AID,AIDPF3
&N       SETC  ''
         BE    &K3                               <=====   P F K 3
.KK4     AIF   ('&K4' EQ '').KK5
&N       CLI   &TYPE.AID,AIDPF4
&N       SETC  ''
         BE    &K4                               <=====   P F K 4
.KK5     AIF   ('&K5' EQ '').KK6
&N       CLI   &TYPE.AID,AIDPF5
&N       SETC  ''
         BE    &K5                               <=====   P F K 5
.KK6     AIF   ('&K6' EQ '').KK7
&N       CLI   &TYPE.AID,AIDPF6
&N       SETC  ''
         BE    &K6                               <=====   P F K 6
.KK7     AIF   ('&K7' EQ '').KK8
&N       CLI   &TYPE.AID,AIDPF7
&N       SETC  ''
         BE    &K7                               <=====   P F K 7
.KK8     AIF   ('&K8' EQ '').KK9
&N       CLI   &TYPE.AID,AIDPF8
&N       SETC  ''
         BE    &K8                               <=====   P F K 8
.KK9     AIF   ('&K9' EQ '').KK10
&N       CLI   &TYPE.AID,AIDPF9
&N       SETC  ''
         BE    &K9                               <=====   P F K 9
.KK10    AIF   ('&K10' EQ '').KK11
&N       CLI   &TYPE.AID,AIDPF10
&N       SETC  ''
         BE    &K10                              <=====   P F K 1 0
.KK11    AIF   ('&K11' EQ '').KK12
&N       CLI   &TYPE.AID,AIDPF11
&N       SETC  ''
         BE    &K11                              <=====   P F K 1 1
.KK12    AIF   ('&K12' EQ '').KEND
&N       CLI   &TYPE.AID,AIDPF12
&N       SETC  ''
         BE    &K12                              <=====   P F K 1 2
.KEND    SPACE
         AIF   ('&CLEAR' EQ '').CNCLR
&N       CLI   &TYPE.AID,AIDCLR
&N       SETC  ''
         BE    &CLEAR                            <=====   C L E A R
.CNCLR   AIF   ('&CNCL&RESHOW' EQ '').ATN
&N       CLI   &TYPE.AID,AIDCAN
&N       SETC  ''
         AIF   ('&CNCL' EQ '').NOCNCL
         BE    &CNCL                             <=====   C A N C L
.NOCNCL  AIF   ('&RESHOW' EQ '').ATN
         BE    &RESHOW                           <=====   R E S H O W
.ATN     AIF   ('&ATTN' EQ '').DEF
&N       CLI   &TYPE.AID,AIDPA1
&N       SETC  ''
         BE    &ATTN                             <=====   A T T N
.DEF     AIF   ('&DEF' EQ '').OUT
         AIF   ('&DEF' EQ 'NO' OR '&DEF' EQ 'YES' OR '&DEF' EQ 'ENTER')$
               .ERROR
         B     &DEF                              <=====   ALL OTHERS
         AGO   .OUT
.ERROR   MNOTE 8,'DEF=&DEF NO LONGER SUPPORTED'
         DC    X'00000000'              ZAPPABLE
.OUT     SPACE 1
*                                       *=============================*
**                                      *                             *
***                                     *  END OF PFK SELECTION CODE  *
**                                      *                             *
*                                       *=============================*
         SPACE 2
         MEND
