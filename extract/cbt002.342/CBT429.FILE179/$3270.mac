         MACRO
         $3270 &C1,&C2
         GBLC  &$3270
         LCLA  &I,&N,&Z
         LCLB  &TWICE
         LCLC  &C
.* $3270 - 09/06/79 SDM
.*
.*   RELATED MACROS:
.*      F3270 - $3270 IS AN INTERNAL REFERENCE IN F3270.
.*
.*   GLOBAL SET SYMBOLS:
.*      &$3270 - COMMUNICATION WITH THE EXTERNAL MACRO
.*
.*   THIS MACRO RECEIVES ANY SELF DEFINING VALUE AS THE FIRST
.*   ARGUMENT AND RETURNS TWO HEXADECIMAL DIGITS REPRESENTING
.*   A VALID 3270 (EBCDIC) CHARACTER IN THE GLOBAL CHARACTER
.*   SETC SYMBOL '&$3270'.  THE LOW ORDER SIX BITS ARE FROM
.*   THE INPUT ARGUMENT, AND THE HIGH ORDER TWO BITS ARE SET
.*   TO MAKE IT VALID.
.*
.* 09/06/79 SDM - ADD SECOND CHARACTER CAPABILITY
.* 09/04/79 SDM - NEW MACRO
.*
&$3270   SETC  ''
&I       SETA  &C1
.LOOP    ANOP
&I       SETA  &I-&I/64*64
&Z       SETA  &I/16
&N       SETA  &I-&Z*16
         AIF   (&N GT 9).SPEC
&C       SETC  '1120'(&Z+1,1)
         AIF   (&N LT &C).SPEC   YES, THIS IS VALID MIXED MODE.
&$3270   SETC  '&$3270'.'CDEF'(&Z+1,1).'0123456789ABCDEF'(&N+1,1)
         AGO   .OKCHAR
.SPEC    ANOP
&$3270   SETC  '&$3270'.'4567'(&Z+1,1).'0123456789ABCDEF'(&N+1,1)
.OKCHAR  AIF   (T'&C2 EQ 'O').LCHAR
         AIF   (&TWICE).LCHAR
&TWICE   SETB  1
&I       SETA  &C2
         AGO   .LOOP
.LCHAR   MEND
