         MACRO
         DCMSERV &FORCE=NO
.*.
.*$MACRO=DCMSERV    DATE=09/00/79       SOURCE=SDC
.*
.*  THIS MACRO IS PART OF DCMS.  IT GENERATES THE DCMS SERVICE
.*  SUBROUTINES.
.*
.*  UPDATES:
.*     12/21/83 SDM  FIX MESSED UP CODE IN @HEX
.*
.*.
         COPY  DCMSGBLS
         AIF   ('&FORCE' NE 'YES').NOSETS
&DCMSCLR SETB  1
&DCMSHEX SETB  1
&DCMSDEC SETB  1
&DCMSWRK SETB  1
&DCMSCST SETB  1
.NOSETS  ANOP
*                                                                     *
*                                                                     *
*        D C M S    S E R V I C E    S U B R O U T I N E S            *
*                                                                     *
*                                                                     *
         SPACE 3
         AIF   (&DCMSCLR).CLR
         AGO   .CLROUT
.CLR     ANOP
&DCMSWRK SETB  1
&DCMSCST SETB  1
         SPACE 2
@CLEAR   STM   R2,R5,@CLEARSA
         LR    R2,R1
         LR    R3,R0
         LH    R3,0(,R3)
         SR    R4,R4
         SR    R5,R5
         ICM   R5,B'1000',BLANKS
         MVCL  R2,R4
         LM    R2,R5,@CLEARSA
         BR    R14
.CLROUT  AIF   (NOT &DCMSDEC).DECOUT
&DCMSWRK SETB  1
&DCMSCST SETB  1
         SPACE 2
@DECIMAL L     R2,DECWORK1
@DECIML2 CVD   R2,DECWORK3
         MVC   DECWORK4(4),EDITMASK
         ED    DECWORK4(4),DECWORK3+6
         XC    DECWORK1(4),DECWORK1
         BR    R14
         SPACE 1
.DECOUT  AIF   (NOT &DCMSHEX).HEXOUT
&DCMSWRK SETB  1
&DCMSCST SETB  1
         SPACE 2
@HEX     UNPK  HEXWORK2(9),HEXWORK1(5)
         MVI   HEXWORK2+8,C' '
         TR    HEXWORK2,TRTBL
         BR    R14
         SPACE 2
.HEXOUT  MEND
