         MACRO
&NFS     $RXR  &TARGET,&SEND,&REG,&H,&A=A
.*.
.*$MACRO=$RXR       DATE=04/00/79       SOURCE=UCLA
.*
.*  THIS MACRO IS USED AS AN INNER MACRO.  IT WILL GET AN ADDRESS INTO
.*  A REGISTER, STORE IT, DOING "L", "LA", "LR" AS APPROPRIATE FOR THE
.*  SOURCE (&SEND), AND "ST" OR "STH" FOR &TARGET.
.*
.*  IF &TARGET IS NOT SPECIFIED THE VALUE WILL BE IN &REG
.*  IF "A=" IS CODED (NULL OPERAND), A LOAD FROM A FULLWORD WILL BE
.*             DONE INSTEAD OF A LOAD ADDRESS
.*  IF "H" IS CODED, A "STH" WILL BE DONE INSTEAD OF "ST"
.*
.*  GBLC &$NFS WILL BE SET TO NULL IF ANY INSTRUCTION IS EXPANDED.
.*.
         GBLB  &OMITTED
         GBLC  &$NFS
&OMITTED SETB  0
         AIF   ('&SEND' EQ '' ).NOSEND  NO SOURCE FIELD
         AIF   ('&SEND'(1,1) EQ '(').REGSEND   SOURCE IS A REGISTER
&NFS     L&A   &REG,&SEND
&$NFS    SETC  ''
         AIF   ('&TARGET' EQ '').END    LEAVE IN REGISTER
         ST&H  &REG,&TARGET
         AGO   .END
.REGSEND AIF   ('&TARGET' NE '').RSTORE
         AIF   ('&SEND(1)' EQ '&REG').END
&NFS     LR    &REG,&SEND(1)
&$NFS    SETC  ''
         AGO   .END
.RSTORE  ANOP
&NFS     ST&H  &SEND(1),&TARGET
&$NFS    SETC  ''
         AGO   .END
.NOSEND  ANOP
&OMITTED SETB  1                        OPERAND WAS OMITTED
.END     MEND
