         MACRO
&NFS     TODAY &LOC,&MONTHS=MONTHS,&DAYS=DAYS,&WORK=DWD,               $
               &PUNCT=YES,&WEEKDAY=,&TIME=,                            $
               &F15==F'15',&F6==F'6',&H2==H'2',&H12==H'12',            $
               &H10==H'10',&H13==H'13',&F5==F'5',&H34==H'34',          $
               &F7==F'7'
.*.
.*$MACRO=TODAY      DATE=06/23/78       SOURCE=LDW
.*
.*  THIS MACRO WILL FORMAT THE DATE AND TIME, INCLUDING COMPUTING THE
.*       DAY OF THE WEEK.
.*.
         LCLC  &LQ,&IHBNX
&LQ      SETC  'L'''
&LQ      SETC  '&LQ&DAYS'
&IHBNX   SETC  'IHB&SYSNDX'
&NFS     L     R1,16                    -> CVT
         UNPK  &LOC+16(5),57(3,R1)      UNPK THE DATE
         OI    &LOC+20,C'0'             FIX THE SIGN
         MVC   &LOC+15(2),&LOC+16       MOVE THE YEAR OVER
         MVI   &LOC+17,C'.'             PUT IN THE DOT
         AIF   ('&PUNCT' NE 'YES').NOP1
         MVI   &LOC+14,C'('
         MVI   &LOC+21,C')'
.NOP1    TM    57(R1),X'01'             LEAP YEAR?
         BO    &IHBNX.N                 NO (ODD YEAR) - NOT LEAP
         TM    57(R1),X'12'             DIVISIBLE BY 4?
         BM    &IHBNX.N                 NO - NOT LEAP YEAR
         MVI   &MONTHS+7,29             FEB HAS 29 DAYS THIS YEAR
         SPACE 1
&IHBNX.N MVC   &WORK+6(2),58(R1)        COPY DDDF
         XC    &WORK.(6),&WORK          CLEAR BEGINNING
         CVB   R0,&WORK                 GET DAY OF YEAR IN BINARY
         LA    R15,&MONTHS              POINT TO TABLE
         SPACE 1
&IHBNX.L SH    R0,0(,R15)               SUB NUMBER OF DAYS THIS MONTH
         BNP   &IHBNX.H                 HAVE MONTH
         LA    R15,6(,R15)              NEXT TABLE ENTRY
         B     &IHBNX.L                 KEEP LOOKING
         SPACE 1
&IHBNX.H MVC   &LOC.(3),2(R15)          MOVE NAME OF MONTH
         AH    R0,0(,R15)               GET BACK CORRECT RESIDUAL
         CVD   R0,&WORK
         UNPK  &LOC+4(2),&WORK+6(2)     DAY OF MONTH
         OI    &LOC+5,C'0'              FIX SIGN
         MVC   &LOC+10(2),&LOC+15       MOVE IN YEAR
         AIF   ('&PUNCT' NE 'YES').NOP2
         MVC   &LOC+6(4),=C', 19'
.NOP2    AIF   ('&WEEKDAY' EQ '').NODAY
         ST    R0,&WORK                 SAVE DAY OF MONTH
.*
.* MONTH=MOD(MONTH+9,12)+1
.* IF (MONTH=11 × MONTH=12) YEAR=YEAR-1  ==> IF MONTH > 10 YEAR=YEAR-1
.* A=(13*MONTH-1)/5
.* B=YEAR/4
.* C=CENTURY/4
.* D=A+B+C+DAY+YEAR-2*CENTURY
.* Z=MOD(D,7)+1
.* WHERE Z=DAY OF WEEK, 1=SUNDAY,... 7=SATURDAY
.*
.* THIS MACRO ASSUMES 1900-1999... IE, CENTURY=19
         SPACE 2
         LA    R14,&MONTHS-6
         SR    R15,R14                  GET MONTH # * 6
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F6                  GET MONTH #
* MONTH=MOD(MONTH+9,12)+1
         SH    R15,&H2
         BP    *+8
         AH    R15,&H12
.***     XR    R14,R14                  CLEAR FOR DIVIDE
.***     LA    R15,9(,R15)              MONTH+9
.***     D     R14,=F'12'               DIVIDE BY 12
.***     LA    R14,1(,R14)
         SPACE 1
         IC    R0,57(,R1)               GET YEAR
         SRL   R0,4
         N     R0,&F15
         MH    R0,&H10
         IC    R1,57(,R1)
         N     R1,&F15
         AR    R1,R0                    = YEAR IN BINARY
         SPACE 1
         CH    R15,&H10                 MONTH = 11 OR 12 ?
         BNH   *+6                      NO - SKIP
         BCTR  R1,0                     -1 FROM YEAR
* A=(13*MONTH-1)/5
         MH    R15,&H13                 MONTH * 13
         BCTR  R15,0                    -1
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F5                  ALL OVER 5 (R5=A)
* B=YEAR/4
         LR    R0,R1                    COPY YEAR
         SRL   R0,2                     YEAR/4
         AR    R15,R0                   D=A+B SO FAR
* C=CENTURY/4 = 19/4 = 4                CENTURY*2 = 19*2 = 38
* +C-2*CENTURY = -34
         SH    R15,&H34                 D=A+B+C-2*CENT NOW
* D=A+B+C+DAY+YEAR-2*CENTURY
         A     R15,&WORK                ADD DAY
         AR    R15,R1                   ADD YEAR
* Z=MOD(D,7)+1
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F7                  GET MOD(D,7) IN R4=Z
         MH    R14,=Y(&LQ)              OFFSET INTO TABLE
         LA    R14,&DAYS.(R14)          POINT TO DAY IN TABLE
         MVC   &WEEKDAY.(&LQ),0(R14)    DAY TO OUTPUT LOCATION
*  SEEMS LIKE A LOT, BUT BETTER THAN AN SVC CALL, AND TRANSPORTABLE!!
.NODAY   AIF   ('&TIME' EQ '').NOTIME
         SPACE 2
*        TIME  DEC                      GET TIME
         TIME  DEC                      GET TIME
         ST    R0,&WORK                 PUT TIME INTO WORK AREA
         OI    &WORK+3,X'0F'            FIX THE SIGN
         MVC   &TIME.(9),=X'4021207A20207A2020'      ' DD:DD:DD'
         ED    &TIME.(9),&WORK
.NOTIME  SPACE 2
         MEND
