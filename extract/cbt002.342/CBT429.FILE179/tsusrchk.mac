         MACRO
&LABEL   TSUSRCHK  &PSCB=
.* FUNCTION:
.*    TEST THE USER TYPE FIELD IN THE PSCB
.* REFERENCES:
.*    MVS PROTECTED STEP CONTROL BLOCK - BIT PSCBACCT
.* FORMAT:
.*    TSUSRCHK  (VALUE,TEST,BRANCH),...,PSCB=
.*    VALUE, TEST, AND BRANCH MAY BE SPECIFIED MULTIPLE TIMES
.*    FOR COMPLEX TESTS.
.* NOTES:
.*    - RELIANCE ON THE CONTENTS OF R0 AND R1 AFTER THIS MACRO
.*      IS ILLEGAL BECAUSE OF COMPATABILITY WITH OTHER VERSIONS.
.*    - THIS VERSION OF THE MACRO SUPPORTS ONLY USER TYPES 'SYSPGMR'
.*      AND 'USER'.  ALL OTHER TESTS WILL TAKE THE 'NO' PATH.
.* OPERANDS:
.*    VALUE -- TYPE OF USER TO BE TESTED
.*       SYSPGMR  -- SYSTEMS PROGRAMMER
.*       PLAYER   -- USER HAS UNRESTRICTED GAMES PRIVILEGES
.*       NOPLAYER -- USER HAS NO GAMES PRIVILEGES
.*       USER     -- NORMAL USER
.*    TEST -- CONDITION ON WHICH THE BRANCH IS TAKEN
.*       NO  -- CURRENT USER IS NOT THE SAME AS VALUE
.*       YES -- CURRENT USER IS THE SAME AS VALUE
.*    BRANCH -- LOCATION TO BRANCH TO -- RX ADDR OR (REG)
.*    PSCB= -- LOCATION OF PSCB (OPTIONAL) -- RX ADDR OR (REG) --
.*             IF NOT SPECIFIED, R1 WILL BE USED TO LOCATE IT
.*
.* UPDATES:
.*    04/28/81 SDM - CREATE HARTFIELD-ZODY'S VERSION
.*
         LCLA  &A1,&TC
         LCLC  &C1,&C2
* TSUSRCHK -- 04/28/81 SDM
         AIF   ('&LABEL' EQ '').NOLAB
&LABEL   DC    0H'0'
.NOLAB   ANOP
.*
.* ADDRESS THE PSCB
&C1      SETC  '16(1)'
         AIF   (T'&PSCB EQ 'O').P4
         AIF   ('&PSCB'(1,1) EQ '(').P1
&C1      SETC  '&PSCB+16'
         AGO   .PX
.P1      ANOP
&C1      SETC  '16(&PSCB(1))'
         AIF   ('&C1' EQ '0').P2
         AIF   ('&C1' EQ 'R0').P2
         AIF   (N'&PSCB EQ 1).PX
.P2      MNOTE 8,'TSUSRCHK - PSCB OPERAND INCORRECTLY SPECIFIED'
         AGO   .PX
.P4      ANOP
         L     1,540                    -> CURRENT TCB
         L     1,180(,1)                -> JSCB
         ICM   1,B'1111',264(1)         -> PSCB
         BZ    *+8                      BRANCH IF BATCH
.PX      ANOP
.*
.* SET THE CONDITION CODE FROM THE PSCBACCT BIT
         TM    &C1,X'40'                PSCBACCT - ACCOUNT PRIVILEGE
.*
.* GENERATE BRANCH INSTRUCTIONS
&TC      SETA  N'&SYSLIST
         AIF   (&TC GE 1).C1
         MNOTE 8,'TSUSRCHK - NO CONDITION TESTS SPECIFIED'
         NOP   0                        PATCH SPACE
         MEXIT
.C1      ANOP
.*
.* LOOP TO GENERATE ACTUAL CONDITION TESTS
&A1      SETA  1
.COND    AIF   (&A1 GT N'&SYSLIST).CONDX
.*
.* CREATE CORRECT BASIC BRANCH OP-CODE AND SAVE IT
         AIF   (N'&SYSLIST(&A1) LT 1).C03
&C1      SETC  '&SYSLIST(&A1,1)'
         AIF   (N'&SYSLIST(&A1) LT 2).C01
&C2      SETC  '&SYSLIST(&A1,2)'
         AIF   ('&C2' EQ 'YES').C02
         AIF   ('&C2' EQ 'NO').C02
.C01     MNOTE 8,'TSUSRCHK - TEST FIELD &A1 IS INCORRECTLY SPECIFIED'
&C1      SETC  'NOP'
         AGO   .C08
.C02     AIF   ('&C1' EQ 'SYSPGMR').C06
         AIF   ('&C1' EQ 'PLAYER').C05
         AIF   ('&C1' EQ 'NOPLAYER').C05
         AIF   ('&C1' EQ 'USER').C04
.C03     MNOTE 8,'TSUSRCHK - USER TYPE FIELD &A1 IS INCORRECTLY SPECIFI+
               ED'
&C1      SETC  'NOP'                    SET NO-OP OP CODE
         AGO   .C08
.* NORMAL USER
.C04     AIF   ('&C2' EQ 'YES').C07Z
         AGO   .C07O
.* FORCE THE 'NO' PATH
.C05     AIF   ('&C2' EQ 'YES').C05N
&C1      SETC  'B'                      SET 'BRANCH' OP CODE
         AGO   .C08
.C05N    ANOP
&C1      SETC  'NOP'                    SET NO-OP OP CODE
         AGO   .C08
.* SYSTEM PROGRAMMER
.C06     AIF   ('&C2' EQ 'YES').C07O
.* SET 'BRANCH ON ZERO' CONDITION
.C07Z    ANOP
&C1      SETC  'BZ'
         AGO   .C08
.* SET 'BRANCH ON ONE' CONDITION
.C07O    ANOP
&C1      SETC  'BO'
.*
.* CREATE CORRECT BRANCH LABEL.  ALSO, MODIFY BRANCH OP-CODE IF
.* REGISTER BRANCH REQUIRED.
.C08     AIF   (N'&SYSLIST(&A1) LT 3).C09
&C2      SETC  '&SYSLIST(&A1,3)'
         AIF   ('&C2'(1,1) NE '(').C10
&C2      SETC  '&C2'(2,K'&C2-2)
         AIF   ('&C2' EQ '0').C09
         AIF   ('&C2' EQ 'R0').C09
         AIF   ('&C2' EQ '1').C09
         AIF   ('&C2' EQ 'R1').C09
&C1      SETC  '&C1.R'
         AGO   .C10
.C09     MNOTE 8,'TSUSRCHK - BRANCH FIELD &A1 IS INCORRECTLY SPECIFIED'
&C2      SETC  '0'
.*
.* ASSEMBLE ACTUAL BRANCH INSTRUCTION
.C10     &C1   &C2
         AIF   (N'&SYSLIST(&A1) LE 3).CONDI
         MNOTE 8,'TSUSRCHK - MORE THAN 3 OPERANDS IN CONDITION &A1'
.CONDI   ANOP
&A1      SETA  &A1+1
         AGO   .COND
.CONDX   MEND
