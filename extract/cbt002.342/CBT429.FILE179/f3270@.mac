         MACRO
&NFS     F3270 &COL=,&LINE=,&ROW=,&POS=,&INITIAL=,&LENGTH=,&ATTR=,     $
               &RA=,&SA=,&MA=,&MODEL=,                                 $
               &$$VAL$1=(12,24,36,43,40,80,80,80,480,1920,2880,3440),  $
               &$$VAL$2=(BLUE,42F1,RED,42F2,PINK,42F3,GREEN,42F4,      $
               TURQOUIS,42F5,YELLOW,42F6,WHITE,42F7,DEFCOLOR,4200,     $
               BLINK,41F1,REVERSE,41F2,UNDER,41F4,DEFHI,4100)
.*
.* F3270 - 03/09/81 LDW
.*
.*   INTERNAL MACRO REFERENCES:
.*      $3270 - GENERATE VALID 3270 CONTROL FIELD BIT CONFIG.
.*
.*   GLOBAL SET SYMBOL REFERENCES:
.*      &$3270 - COMMUNICATION WITH $3270 INNER MACRO
.*      &F3270M - THE MODEL OF 3270 WE'RE GENNING FOR
.*      &F3270E - HAVE WE REGALED HIM WITH THE ERROR MESSAGE
.*
.*   GENERATE A 3270 FIELD DEFINITION WITH THE FOLLOWING FEATURES:
.*   1. A SET BUFFER ADDRESS (SBA) SEQUENCE, OR A REPEAT TO ADDRESS
.*      (RA) SEQUENCE.  THE APPROPRIATE KEYWORDS ARE:
.*         ROW= (OR LINE=) AND COL= OF THE BUFFER ADDRESS, OR
.*         POS= ABSOLUTE 3270 BUFFER ADDRESS, OR
.*         POS=(ROW,COL) OF THE BUFFER ADDRESS, AND
.*         RA= CHARACTER TO BE REPEATED (TWO DIGITS OF HEX, OR
.*             C'CHAR'); AND
.*   2. A START FIELD (SF) SEQUENCE WITH THE DESIRED ATTRIBUTES:
.*         ATTR=(ATTRIBUTES) WITH MNEMONIC NAMES; AND
.*   3. A FIELD OF DATA WITH AN INITIAL VALUE OF SPACES:
.*         LENGTH= DESIRED NUMBER OF SPACES; OR
.*      A FIELD WITH A SPACIFIED INITIAL VALUE:
.*         INITIAL= DESIRED CHARACTER STRING, AND
.*         LENGTH= (OPTIONAL) LENGTH ON DC STATEMENT.
.*
.*   NOTE: IF A LABEL IS SPECIFIED THEN THE SF SEQUENCE ATTRIBUTE
.*      BYTE (IF IT IS GENERATED) WILL BE LABELLED 'LABEL'A AND THE
.*      DATA FIELD (IF IT IS GENERATED) WILL BE LABELLED 'LABEL'.
.*
.* 03/09/81 LDW - ADD DEFCOLOR AND DEFHI EXTENDED ATTRIBUTES
.* 01/29/81 LDW - ADD SA= AND MA= FOR 3279 HIGHLIGHTING AND COLOR
.* 09/06/79 SDM - CHANGE TO TWO CHAR INTERFACE TO $3270
.* 09/05/79 SDM - ADD POS=(ROW,COL) OPTION
.*                ADD ADDRESS RANGE TESTS
.* 09/04/79 SDM - REWRITTEN TO USE $3270 AND ELIMINATE SBA3270
.* 07/26/79 LDW - NEW MACRO
.*
         GBLA  &F3270M
         GBLB  &F3270E
         GBLC  &$3270
         LCLA  &L,&A,&B,&I
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6
         LCLC  &C
************************************************F3270**03/09/81**LDW***
         AIF   (T'&MODEL EQ 'O').MOD3
         AIF   ('&MODEL' LT '1').MOD1
         AIF   ('&MODEL' LE '4').MOD2
.MOD1    MNOTE 4,'MODEL= IS NOT BETWEEN 1 AND 4.  IGNORED.'
         AGO   .MOD3
.MOD2    ANOP
&F3270M  SETA  &MODEL
         AGO   .MOD4
.MOD3    AIF   (&F3270M NE 0).MOD4
         MNOTE 0,'MODEL=2 IS ASSUMED'
&F3270M  SETA  2
.MOD4    AIF   ('&LINE&ROW&COL&POS' EQ '').NOPOS1
         AIF   (T'&POS NE 'O').POS1
         AIF   (T'&ROW EQ 'O').ROW1
         AIF   (T'&ROW NE 'N').ROW2
&A       SETA  &ROW
         AIF   (T'&LINE EQ 'O').ROW3
         MNOTE 4,'LINE AND ROW ARE SYNONYMS - ROW WILL BE USED'
         AGO   .ROW3
.ROW1    AIF   (T'&LINE NE 'N').ROW2
&A       SETA  &LINE
         AGO   .ROW3
.ROW2    MNOTE 4,'LINE OR ROW INVALID - ROW=1 ASSUMED'
&A       SETA  1
.ROW3    AIF   (T'&COL EQ 'N').COL1
         MNOTE 4,'COL INVALID - COL=1 ASSUMED'
&B       SETA  1
         AGO   .COL2
.COL1    ANOP
&B       SETA  &COL
.COL2    ANOP
         AGO   .RNG1
.POS1    AIF   ('&LINE&ROW&COL' EQ '').POS2
         MNOTE 4,'LINE, ROW, OR COL MAY NOT OCCUR WITH POS - POS VALUE X
               USED'
.POS2    AIF   (N'&POS LT 2).POS6
         AIF   (T'&POS(1) EQ 'N').POS3
         MNOTE 4,'POS ROW INVALID - 1 ASSUMED'
&A       SETA  1
         AGO   .POS4
.POS3    ANOP
&A       SETA  &POS(1)
.POS4    AIF   (T'&POS(2) EQ 'N').POS5
         MNOTE 4,'POS COLUMN INVALID - 1 ASSUMED'
&B       SETA  1
         AGO   .RNG1
.POS5    ANOP
&B       SETA  &POS(2)
.RNG1    AIF   (&A GE 1).RNG2
&A       SETA  1
         AGO   .RNG3
.RNG2    AIF   (&A LE &$$VAL$1(&F3270M)).RNG4
&A       SETA  &$$VAL$1(&F3270M)
.RNG3    MNOTE 4,'SPECIFIED ROW OUT OF RANGE - &A ASSUMED'
.RNG4    AIF   (&B GE 1).RNG5
&B       SETA  1
         AGO   .RNG6
.RNG5    AIF   (&B LE &$$VAL$1(&F3270M+4)).RNG7
&B       SETA  &$$VAL$1(&F3270M+4)
.RNG6    MNOTE 4,'SPECIFIED COL OUT OF RANGE - &B ASSUMED'
.RNG7    ANOP
&A       SETA  (&A-1)*80+&B-1
         AGO   .ADDR
.POS6    AIF   (T'&POS EQ 'N').POS7
&A       SETA  0
         AGO   .POS8
.POS7    ANOP
&A       SETA  &POS
         AIF   (&A LT &$$VAL$1(&F3270M+8)).ADDR
&A       SETA  &$$VAL$1(&F3270M+8)-1
.POS8    MNOTE 4,'SPECIFIED POS OUT OF RANGE - &A ASSUMED'
.ADDR    ANOP
&B       SETA  &A/64
&A       SETA  &A-&B*64
         $3270 &B,&A
         AIF   (T'&RA NE 'O').RA1
         DC    X'11&$3270'
         AGO   .ATTR1
.RA1     AIF   ('&RA'(1,2) EQ 'C''').RA2
         DC    X'3C&$3270',X'&RA'
         AGO   .ATTR1
.RA2     DC    X'3C&$3270',&RA
         AGO   .ATTR1
.NOPOS1  AIF   (T'&RA EQ 'O').ATTR1
         MNOTE 4,'RA= INVALID WITHOUT POSITIONING OPERANDS - IGNORED'
.ATTR1   AIF   (T'&ATTR EQ 'O').SA1
&A       SETA  0
.ATTR2   AIF   (&I EQ N'&ATTR).OKATTR
&I       SETA  &I+1
         AIF   ('&ATTR(&I)' EQ 'ASKIP' ).ATTR30
         AIF   ('&ATTR(&I)' EQ 'PROT'  ).ATTR20
         AIF   ('&ATTR(&I)' EQ 'NUM'   ).ATTR10
         AIF   ('&ATTR(&I)' EQ 'UNPROT').ATTR30X
         AIF   ('&ATTR(&I)' EQ 'DARK'  ).ATTR0C
         AIF   ('&ATTR(&I)' EQ 'NDISP' ).ATTR0C
         AIF   ('&ATTR(&I)' EQ 'BRT'   ).ATTR08
         AIF   ('&ATTR(&I)' EQ 'BRIGHT').ATTR08
         AIF   ('&ATTR(&I)' EQ 'SPD'   ).ATTR04
         AIF   ('&ATTR(&I)' EQ 'DET'   ).ATTR04
         AIF   ('&ATTR(&I)' EQ 'NORM'  ).ATTR0CX
         AIF   ('&ATTR(&I)' EQ 'MDT'   ).ATTR01
         AIF   ('&ATTR(&I)' EQ 'MOD'   ).ATTR01
         MNOTE 4,'INVALID ATTR - &ATTR(&I) IGNORED'
         AGO   .ATTR4
.***  ASKIP
.ATTR30  AIF   (&B1 OR &B2).ATTR3
&B1      SETB  1
&B2      SETB  1
&A       SETA  &A+X'30'                 X'20'+X'10'
         AGO   .ATTR2
.***  PROT
.ATTR20  AIF   (&B1).ATTR3
&B1      SETB  1
&A       SETA  &A+X'20'
         AGO   .ATTR2
.***  NUM
.ATTR10  AIF   (&B2).ATTR3
&B2      SETB  1
&A       SETA  &A+X'10'
         AGO   .ATTR2
.***  UNPROT
.ATTR30X AIF   (&B1).ATTR3
&B1      SETB  1
         AGO   .ATTR2
.***  DARK/NDISP
.ATTR0C  AIF   (&B3 OR &B4).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
&B4      SETB  1                        DON'T ALLOW DET
&A       SETA  &A+X'0C'                 X'08'+X'04'
         AGO   .ATTR2
.***  BRT/BRIGHT
.ATTR08  AIF   (&B3).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
         AIF   (NOT &B5).ATTR08A
&A       SETA  &A-X'04'                 TAKE BACK THE DET ATTR
.ATTR08A ANOP
&A       SETA  &A+X'08'
&B5      SETB  1                        DET HAS BEEN SET
         AGO   .ATTR2
.***  SPD/DET
.ATTR04  AIF   (&B4).ATTR3
&B4      SETB  1                        DON'T ALLOW DET
         AIF   (&B5).ATTR2              BRIGHT HAS BEEN HERE
&B5      SETB  1                        DET HAS BEEN SET
&A       SETA  &A+X'04'
         AGO   .ATTR2
.***  NORM
.ATTR0CX AIF   (&B3).ATTR3
&B3      SETB  1                        INTENSITY HAS BEEN SET
         AGO   .ATTR2
.***  MDT/MOD
.ATTR01  AIF   (&B6).ATTR3
&B6      SETB  1
&A       SETA  &A+X'01'
         AGO   .ATTR2
.ATTR3   MNOTE 4,'DUPLICATE OR CONFLICTING ATTR - &ATTR(&I) IGNORED'
.ATTR4   AIF   (&F3270E).ATTR2
&F3270E  SETB  1
         MNOTE *,'TRY ONE OF THESE:  UNPROT, PROT, ASKIP, NUM,'
         MNOTE *,'                   NORM, BRT, BRIGHT, DARK, NDISP,'
         MNOTE *,'                   DET, SPD, MDT, MOD.'
         AGO   .ATTR2
.*
.OKATTR  ANOP
         $3270 &A
         DC    X'1D&$3270'              START FIELD + ATTR BYTE
         AIF   ('&NFS' EQ '').SA1
&NFS.A   EQU   *-1                      ATTRIBUTE BYTE LABEL
.***
.SA1     AIF   (T'&SA EQ 'O').MA1
&I       SETA  1
&C       SETC  ''
.SA2     ANOP
&A       SETA  1
.SA3     AIF   ('&SA(&I)' EQ '&$$VAL$2(&A)').SA4
&A       SETA  &A+2
         AIF   (&A LT N'&$$VAL$2).SA3
         MNOTE 4,'INVALID COLOR OR HIGHLIGHTING - &SA(&I) IGNORED'
         AGO   .SA5
.SA4     ANOP
&C       SETC  '&C.28&$$VAL$2(&A+1)'
.SA5     ANOP
&I       SETA  &I+1
         AIF   (&I LE N'&SA).SA2
         DC    X'&C'                    SET COLOR AND/OR HIGHLIGHTING
.***
.MA1     AIF   (T'&MA EQ 'O').INIT1
&A       SETA  1
.MA3     AIF   ('&MA' EQ '&$$VAL$2(&A)').MA4
&A       SETA  &A+2
         AIF   (&A LT N'&$$VAL$2).MA3
         MNOTE 4,'INVALID COLOR OR HIGHLIGHTING - &MA IGNORED'
         AGO   .INIT1
.MA4     DC    X'2C&$$VAL$2(&A+1)'      MODIFY COLOR OR HIGHLIGHTING
.***
.INIT1   AIF   ('&INITIAL&LENGTH' EQ '').EXIT
         AIF   ('&INITIAL' EQ '').JUSTL
         AIF   ('&LENGTH' EQ '').JUSTI
&NFS     DC    CL&LENGTH&INITIAL        DATA
         AGO   .EXIT
.JUSTL   ANOP
&NFS     DC    CL&LENGTH' '             DATA AREA
         AGO   .EXIT
.JUSTI   ANOP
&NFS     DC    C&INITIAL                DATA
.EXIT    ANOP
***********************************************************************
         SPACE 1
         MEND
