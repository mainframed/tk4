         MACRO
&LBL     TSPARSE &Z,&PCL=,&CBUF=,&USER=,&SYNAD=,&ENTRY=,&MF=I
.*.
.*$MACRO=TSPARSE    DATE=02/07/80       SOURCE=LDW
.*
.*  THE TSPARSE MACRO IS USED TO INVOKE IKJPARS
.*
.*  UPDATES:
.*    03/06/82 LDW  USE CALLTSSR INSTEAD OF LINK
.*.
         GBLC  &$NFS
         LCLC  &IHBNX
&IHBNX   SETC  'IHB&SYSNDX'
&$NFS    SETC  '&LBL'
         AIF  (('&MF' NE 'I') AND ('&MF' NE 'E') AND ('&MF' NE 'L')).E1
        AIF (('&MF' EQ 'I') AND (('&PCL' EQ '') OR ('&CBUF' EQ ''))).E2
&$NFS    $STP  &PCL,PPLPCL
&$NFS    $STP  &CBUF,PPLCBUF
         AIF   ('&USER' EQ '').NO$UWA
&$NFS    $STP  &USER,PPLUWA
.NO$UWA  AIF   ('&MF' EQ 'L').DONE
&$NFS    LA    1,PPL
&$NFS    SETC  ''
         AIF   ('&ENTRY' EQ '').NOENT
         $RXR  ,&ENTRY,15,A=            FROM REGISTER OR FULLWORD
         BALR  14,15
         AGO   .NOLINK
.*  FOLLOWING CHANGE MADE 03/06/82 BY LDW
.*NOENT  LINK  EP=IKJPARS
.NOENT   CALLTSSR  EP=IKJPARS
.NOLINK  AIF   ('&SYNAD' EQ '').DONE
         LTR   15,15
         BZ    &IHBNX+4
         LA    0,&SYNAD
         L     14,&IHBNX
         BR    14
&IHBNX   DC    A(TSPARERR)
.DONE    AIF   ('&$NFS' EQ '').MEND
&$NFS    DS    0H                       DEFINE NAME
.MEND    MEXIT
.E1      MNOTE 12,'MF=&MF IS INVALID'
         AGO   .DONE
.E2      MNOTE 12,'PCL= AND CBUF= REQUIRED WITH MF=I'
         AGO   .DONE
         MEND
