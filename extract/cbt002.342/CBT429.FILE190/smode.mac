         MACRO
&LABL    SMODE  &MODE,&CLEAR=YES
.*
.*       THIS MACRO WILL SWITCH TO 24 OR 31 BIT ADDRESSING MODE IN
.*       AN MVS XA ENVIRONMENT.  THE CURRENT ADDRESSING MODE IS
.*       SAVED IN THE HIGH BIT OF REGISTER 14.
.*
.*       VALID VALUES FOR &MODE MODE ARE:
.*       24  -  SWITCH TO 24 BIT ADDRESSING MODE
.*       31  -  SWITCH TO 31 BIT ADDRESSING MODE
.*       (R0)-(R14) SWITCH TO THE ADDRESSING MODE SPECIFIED IN THE
.*              HIGH BIT OF THE SPECIFIED REGISTER.  OTHER BITS CLEARED
.*              BY THIS MACRO.
.*       CLEAR - IF YES (THE DEFAULT) AND A REGISTER VALUE IS SPECIFIED
.*               FOR MODE, THE LOW ORDER 31 BITS IN THE WORD ARE
.*               CLEARED PRIOR TO ORING IN THE HIGH BIT.  CLEAR SHOULD
.*               REMAIN YES UNLESS THE USER IS SURE OTHER BITS IN THE
.*               REGISTER SPECIFIED BY MODE ARE ZERO
.*
.*       THIS MACRO USES REGISTERS 14 AND 15
.*
         AIF   (('&MODE' EQ '24') OR                                   X
               ('&MODE' EQ '31')  OR                                   X
               ('&MODE'(1,1) EQ '(')).PARMOK
         MNOTE 8,'INVALID MODE SPECIFIED, 24 AND 31 ARE VALID VALUES'
         MEXIT
.PARMOK  ANOP
         AIF   ('&MODE'(1,1) EQ '(').REGPARM
         CNOP  0,4                  MAKE SURE "A" CON IS ALIGNED
         L     15,*+8               LOAD NEW AMODE
         SLR   14,14                CLEAR OLD AMODE REGISTER
         BSM   14,15                BRANCH AROUND CONST AND SET AMODE
         AIF   ('&MODE' EQ '31').MODE31
         DC    A(X'00000000'+(*+4))
         MEXIT
.MODE31  ANOP
         DC    A(X'80000000'+(*+4))
         MEXIT
.REGPARM ANOP
         AIF   ('&CLEAR' NE 'YES').NOCLEAR
         SRL   &MODE(1),31          SHIFT OUT ALL BUT HIGH BYTE
         SLL   &MODE(1),31          SHIFT IN ZEROS FOR THE OR INSTR
.NOCLEAR ANOP
         LA    15,*+10              ADDR JUST PAST BSM INSTR
         OR    15,&MODE(1)          SET HIGH ORDER BIT FROM PARM
         SLR   14,14                CLEAR OLD AMODE REGISTER
         BSM   14,15                BRANCH AROUND CONST AND SET AMODE
         MEND
