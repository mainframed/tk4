         MACRO
&LABL    TESTSYS  &XAADDR,&S370ADDR
.*
.*     THIS MACRO WILL TEST THE SYSTEM ENVIRONMENT TO DETERMINE
.*     IF THE PROGRAM IS RUNNING UNDER MVS XA OR MVS 370.
.*     THE TWO POSITIONAL PARAMETERS ARE THE BRANCH ADDRESS IF
.*     THIS IS AN XA ENVIRONMENT AND THE BRANCH ADDRESS IF THIS
.*     IS A SYSTEM 370 ENVIRONMENT.  EITHER OR BOTH OF THE PARMS
.*     MAY BE OMMITTED.
.*
.*     THIS MACRO USES REGISTER 15
.*
.*     EXAMPLES:
.*********************************************************************
.*               TESTSYS LABELXA,LABEL370
.*                 . THIS POINT WILL NEVER BE REACHED
.*     LABELXA   DS    0H
.*                 .
.*               MVS XA SPECIFIC CODE
.*                 .
.*     LABEL370  DS    0H
.*                 .
.*               MVS 370 SPECIFIC CODE
.*                 .
.*********************************************************************
.*               TESTSYS LABELXA
.*                 .
.*               MVS 370 SPECIFIC CODE
.*                 .
.*     LABELXA   DS    0H
.*                 .
.*               MVS XA SPECIFIC CODE
.*                 .
.*********************************************************************
.*               TESTSYS ,LABEL370
.*                 .
.*               MVS XA SPECIFIC CODE
.*                 .
.*     LABEL370  DS    0H
.*                 .
.*               MVS 370 SPECIFIC CODE
.*                 .
.*********************************************************************
         L    15,16               POINT TO CVT
         TM    CVTDCB-CVT(15),CVTMVSE
         AIF   ('&XAADDR' EQ '').NOXA
         BO    &XAADDR
.NOXA    ANOP
         AIF   ('&S370ADDR' EQ '').NO370
         BZ    &S370ADDR
.NO370   ANOP
         MEND
