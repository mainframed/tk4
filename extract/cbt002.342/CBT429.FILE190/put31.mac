         MACRO
&NAME    PUT31  &AREA24,&DCB,&AREA,&RPL=
.*  DATE OF CREATION    01/08/90
.*  DATE OF LAST CHANGE 01/08/90
.*
.*   THIS MACRO ALLOWS THE EXECUTION OF AN OS PUT MACRO FROM A
.*   PROGRAM RUNING RMODE ANY.  THE TEXT OF THE PUT MACRO IS
.*   COPIED TO THE 24 BIT ADDRESSABLE STORAGE REFERECED BY ADDED
.*   PARM AREA24.
.*
.*   AREA24 MUST POINT TO A 24 BIT ADDRESSABLE AREA OF MINIMUM
.*   LENGTH 20 FOR USE WITH DCB'S AND 34 FOR USE WITH RPL'S.
.*   THE RPL= PARM IS ADDED FOR COMPLETENESS.  RPL'S AND ACB'S CAN
.*   BE USED FROM 31 BIT ADDRESSING MODE.
.*
.*   REGISTERS 0,1,14 AND 15 ARE MODIFIED BY THIS MACRO (JUST LIKE PUT)
.*
.*   WARNING:
.*   THE CALLER MUST INSURE THAT THE DCB AND TARGET AREA ARE 24 BIT
.*   ADDRESSABLE.  (LOCATE MODE DOES NOT USE AN AREA ADDRESS.)
.*
.*   THIS MACRO IS REENTRANT ASSUMING AREA24 IS REENTRANT.
.*
.* SAMPLE:
.*        PUT31  WORK24,MYDCB
.*
.*
&IDX     SETC  '&SYSNDX'
&NAME    MVC   &AREA24.(LEN&IDX),COD&IDX    MOVE 24 BIT CODE
         LA    15,&AREA24                  POINT TO THE AREA
         BASSM 14,15                       CALL SWITCHING AMODE
         B     DON&IDX
         SPACE
*        THE FOLLOWING CODE IS COPIED TO 24 BIT ADDRESSABLE STORAGE
*        AND EXECUTED AS A LINKAGE ASSIST ROUTINE
*--------------------------------------------------------------------
COD&IDX  DS    0H
         ST    14,DAT&IDX-COD&IDX.(,15)
         PUT   &DCB,&AREA,RPL=&RPL
         L     14,DAT&IDX-*(,14)           LOAD REAL RETURN POINT
         BSM   0,14                        RETURN TO INLINE CODE
DAT&IDX  DC    F'0'                        TEMP SAVE AREA
LEN&IDX  EQU   *-COD&IDX
*--------------------------------------------------------------------
DON&IDX  DS    0H
         SPACE
         MEND
