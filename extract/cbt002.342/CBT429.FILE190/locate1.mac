LOCATE   TITLE 'DETERMINE IF DATASET IS EXISTS FOR PL/I'
**********************************************************************
*
* SEGMENT NAME:
*
*    LOCATE -- DETERMINE IF A DATASET EXISTS FOR PL/I
*
* INPUTS (*TO FUNCTIONS):
*
*    DSN -- DATASET NAME (*1,2)
*       THE PARM TO THIS FUNCTION IS THE FULLY QUALIFIED DATASET NAME
*       WHOSE EXISTENCE IS TO BE CHECKED.  QUOTES ARE NOT TO BE
*       SUPPLIED IN THE STRING.
*
* OUTPUTS (*FROM FUNCTIONS):
*
*    RC -- RETURN CODE (*1,2,3)
*       THE RETURNED PARAMETER IS SHOWS THE STATUS OF THE DATASET
*       VALUES: 0  -  DATASET IS CATALOGUED AND EXISTS ON A DISK PACK
*               4  -  DATASET IS CATALOGUED AND EXISTS ON A WORK PACK
*               8  -  DATASET IS NOT CATALOGUED
*               11 -  DATASET IS MIGRATED BY HSM
*               12 -  DATASET IS CATALOGUED BUT DOES NOT EXIST ON
*                     DISK (OBTAIN)
*
* NOTES:
*
*    1. SAMPLE CALL AND DECLARE:
*
*       DECLARE LOCATE  ENTRY(CHAR(44)) RETURNS(FIXED BINARY(15))
*       EXT;
*       DECLARE DSN     CHAR(44)  INIT('LT22505.TSO.CNTL');
*
*       RC = LOCATE(DSN);
*
*    2. ISSUE HISTORY:
*
*       REASON FOR ISSUE 06:
*       ADD CODE TO WORK PACK CHECK.  WORK PACKS NOW START WITH SWK,
*       SMS, AND WRK.
*
**********************************************************************
         EJECT
LOCATE   RENTR BASE=R11,PARM=R10,WORK=(WORKLEN,WORKAREA)
         SPACE
         LM    R9,R10,0(R10)   LOAD THE PARM LIST
         L     R9,0(R9)        POINT TO DSNAME STRING
         SPACE
**********************************************************************
*
* CHECK TO SEE IF THE DATASET IS CATALOGED ON THE VOLUME SPECIFIED
* WHICH THE LOCATE MACRO.  IF IT IS, PUT UP THE CONFIRMATION PANEL.
* IF NOT, GO GETHER THE INFORMATION NECESSARY TO GENERATE THE ZAP
*
**********************************************************************
         SPACE
         MVC   OLDNAME(44),0(R9)    MOVE NAME TO LOCATE PARM AREA
         LA    R8,8                 LOAD NOT CATALOGUED RETURN CODE
         SPACE
         MVC   CATLIST(CATLEN),CONCAT  INITIALIZE CAMLST FOR LOCATE
         LA    R15,OLDNAME             POINT TO NAME PARM
         ST    R15,CATLIST+4
         LA    R15,VOLLIST             POINT TO TARGET AREA
         ST    R15,CATLIST+12
         SPACE
         LOCATE CATLIST       IS THE DATASET CATALOGUED?
         SPACE
         LTR   R15,R15        CHECK FOR FOUND CATALOG ENTRY
         BNZ   RETURN         IF ERROR, DSN IS NOT CATALOGED
         SPACE
         CLC   VOLLIST+6(6),=CL6'MIGRAT'  IS THIS THE HSM MIGRATED VOL
         BNE   DOOBTAIN       IF NOT, KEEP PROCESSING.
         SPACE
         LA    R8,11          LOAD RC, FOR HSM MIGRATED
         B     RETURN
         SPACE 2
**********************************************************************
*
* DO THE OBTAIN TO THE VOLUME SPECIFIED BY THE PANEL.  IF IT IS NOT
* FOUND, SET UP A MESSAGE AND GO BACK TO THE DISPLAY.
*
* THE MESSAGE IS SET UP FROM THE TABLE OF MESSAGE IDS FOR THE OBTAIN.
* THE RC CAN BE 4, 8, 12, OR 16.  THE OFFSETS INTO THE TABLE ARE 0,
* 8, 16, AND 24 RESPECTIVELY.  THUS WE SUBTRACT 4 AND MULTIPLY BY 2.
*
**********************************************************************
         SPACE
DOOBTAIN DS    0H
         MVC    OLDVOL,VOLLIST+6  MOVE VOLUME TO OBTAIN CAMLIST
         LA     R8,12             LOAD RC FOR BAD OBTAIN
         SPACE
         MVC   OBLIST(OBLEN),CONOB     INITIALIZE CAMLST FOR LOCATE
         LA    R15,OLDNAME             POINT TO NAME PARM
         ST    R15,OBLIST+4
         LA    R15,OLDVOL              POINT TO VOLUME SERIAL PARM
         ST    R15,OBLIST+8
         LA    R15,DSCB1               POINT TO TARGET AREA
         ST    R15,OBLIST+12
         SPACE
         OBTAIN OBLIST
         SPACE
         LTR   R15,R15              DID THE OBTAIN WORK?
         BNZ   RETURN               IF NOT, FLAG THE ERROR
         SPACE
         LA    R8,4                 SHOW DSN IS ON A WORK PACK
         SPACE
         CLC   OLDVOL(3),=CL3'WRK'  IS THIS A WORK GTE CSC PACK?
         BE    RETURN               IF SO, SHOW WORK PACK VALUE
         SPACE
         CLC   OLDVOL(3),=CL3'SWK'  IS THIS A WORK GTE CSC PACK?
         BE    RETURN               IF SO, SHOW WORK PACK VALUE
         SPACE
         CLC   OLDVOL(3),=CL3'SMS'  IS THIS A WORK GTE CSC PACK?
         BE    RETURN               IF SO, SHOW WORK PACK VALUE
         SPACE
         SLR   R8,R8                SHOW ZERO RETURN CODE
         EJECT
RETURN   DS    0H
         STH   R8,0(R10)      SAVE IN PL/I PARM
         SPACE
         REXIT
         SPACE 2
CONCAT   CAMLST NAME,DUMMYARG,,DUMMYARG
CATLEN   EQU   *-CONCAT
         SPACE 2
CONOB    CAMLST SEARCH,DUMMYARG,DUMMYARG,DUMMYARG
OBLEN    EQU   *-CONOB
         SPACE
DUMMYARG DC    A(0)
         EJECT
WORKAREA DSECT
         DS    26F            DYNAMIC SAVE AREA
OLDNAME  DS    CL46
OLDVOL   DS    CL6
CATLIST  DS    XL(CATLEN)
OBLIST   DS    XL(OBLEN)
         SPACE
DSCB1    DS    0D
         DS    CL140
         SPACE
VOLLIST  DS    0D
         DS    265X           TWO SIX FIVE
WORKLEN  EQU   *-WORKAREA
         SYMBAR MODE=DEC
         END
