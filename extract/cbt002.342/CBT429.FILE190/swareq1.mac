SWAREQ   TITLE 'SWAREQ_RL_INVOCATION_ROUTINE'
**********************************************************************
*
* SEGMENT NAME:
*
*    SWAREQ -- SWAREQ_RL_INVOCATION_ROUTINE
*
* INPUTS (*TO FUNCTIONS):
*
*    SWATOK -- SWA (SCHEDULER WORK AREA) TOKEN (*1)
*       THE PARM TO THIS ROUTINE IS THE ADDRESS OF THE 3 BYTE TOKEN
*       REPRESENTING THE SWA CONTROL BLOCK.  IN PRE-ESA DAYS, THIS
*       WAS THE THREE BYTE ADDRESS.  NOW, WITH THE SWA ABOVE THE 16
*       MEGABYTE LINE. THIS IS A THREE BYTE TOKEN.  ON ENTRY R1
*       POINTS TO THE 3 BYTE TOKEN.
*
* OUTPUTS (*FROM FUNCTIONS):
*
*    ADDR -- CONTROL BLOCK ADDRESS (*2)
*       THE CONTROL BLOCK ADDRESS IS RETURNED IN REGISTER R0.
*
*    RC -- RETURN CODE (*2)
*       THE RETURN CODE FROM SWAREQ IS RETURNED IN REGISTER R15.
*       VALUES: 00  -  CONVERSION WORKED
*               08  -  INVALID SVA IN THE SWA PREFIX
*               24  -  (HEX) ATTEMPT TO READ A BLOCK NOT YET WRITTEN
*               28  -  (HEX) INVALID POINTER TO THE EPA
*
* NOTES:
*
*    1. THIS ROUTINE SUPPORTS BEING CALLED IN A PL/I ENVIRONMENT, BUT
*       IS DESIGNED TO BE CALLED FROM ASSEMBLER.
*
**********************************************************************
         EJECT
SWAREQ   RENTR  BASE=11,PARM=10,WORK=(WORKLEN,WORKAREA),ENV=BOTH
         MVC   SWALIST(SWALEN),CONSWA   INITIALIZE SWA PARM LIST
         XC    EPAAREA(EPALEN),EPAAREA  ZERO OUT THE EPA
         LA    R3,EPAAREA               POINT TO THE EPA
         ST    R3,EPAPTR                SAVE THE ADDRESS
         USING SWAEPA,R3                ADDRESS THE EPA SYMBOLICLY
         SPACE
         MVC   SWVA(3),0(R10)           COPY THE SWA BLOCK TOKEN
         SPACE
         SWAREQ FCODE=RL,EPA=EPAPTR,UNAUTH=YES,MF=(E,SWALIST)
         SPACE
         L     R0,SWBLKPTR              GET THE 31 BIT ADDR OF THE JFCB
RETURN   DS    0H
         REXIT ENV=BOTH,RETURN=(0),RC=(15)
         DROP  R3,R11
         SPACE
CONSWA   SWAREQ FCODE=RL,MF=L
SWALEN   EQU   *-CONSWA
         LTORG
WORKAREA DSECT
         DS    26F
         DS    0F
EPAPTR   DS    A
SWALIST  DS    XL(SWALEN)
EPAAREA  DS    XL(EPALEN)
         DS    0D
WORKLEN  EQU   *-WORKAREA
EPALEN   EQU   L'SWAEPAX
         SPACE 3
         PRINT NOGEN
         CVT   DSECT=YES
         SPACE
         IEFZB505 LOCEPAX=YES
         SPACE
         IEFJESCT
         SPACE
         SYMBAR MODE=DEC
         END
