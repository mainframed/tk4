         TITLE 'SVC UPDATE PROGRAM'
* CPARM='XREF(SHORT)',LPARM='AC=1,MAP'                        CJYRINIT
*      DD DSN=SYS1.MACLIB,DISP=SHR
*      DD DSN=SYS1.MODGEN,DISP=SHR
         EJECT
CJYRINIT CSECT
*
**********************************************************************
*                                                                    *
*    COPYRIGHT (C) 1991 BY CRAIG J. YASUNA.  ALL RIGHTS RESERVED.    *
*                                                                    *
*    THIS SOFTWARE PRODUCT OR ANY OF ITS COMPONENTS MUST NOT BE      *
*    SOLD, GIVEN, OR OTHERWISE DISTRIBUTED TO ANY OTHER COMPANY      *
*    WITHOUT THE PRIOR WRITTEN PERMISSION OF:                        *
*                                                                    *
*                                  CRAIG J. YASUNA, PRESIDENT        *
*                                  ENTERPRISE SYSTEMS GROUP          *
*                                  2 MARC COURT                      *
*                                  EDISON, NEW JERSEY 08820          *
*                                                                    *
**********************************************************************
*
         SAVE  (14,12),,CJYRINIT_&SYSDATE._&SYSTIME
         USING CJYRINIT,R15            ESTABLISH => PROGRAM EP
         ST    R13,SAVEAREA+4          SAVE HSA
         LA    R11,SAVEAREA            ESTABLISH => SAVEAREA
         ST    R11,8(R13)              SAVE LSA
         LR    R13,R11                 SETUP => OUR SAVEAREA
         USING SAVEAREA,R13            NEW ADDRESSABILITY
         DROP  R15
         B     CONTINUE                BRANCH AROUND SAVEAREA
SAVEAREA DS    18F
CONTINUE DS    0H
*        MODESET MODE=SUP,KEY=ZERO     SETUP FOR SVCUPDATE
         ENQ   (SYSZSVC,TABLE,E,,SYSTEM)  EXCLUSIVE CONTROL OF SVCTABLE
*
         L     R2,=V(IGC00130)      130 BEGINNING
         L     R3,=V(SVC130E)      130 END
         SR    R3,R2               GET LENGTH
         GETMAIN RU,LV=(R3),LOC=BELOW,SP=241 GET CSA
         LR    R5,R1               SAVE EP.
         LR    R6,R1               CSA
         LR    R7,R3               NUMBER OF BYTES
         MVCL  R6,R2               MOVE IT
         SVCUPDTE 130,REPLACE,TYPE=4,EP=(R5)
         LTR   R15,R15             UPDATE SUCCESSFUL?
         BNZ   ABEND               NO, ABEND
*
         L     R2,=V(IGC0013A)     131 BEGINNING
         L     R3,=V(SVC131E)      131 END
         SR    R3,R2               GET LENGTH
         GETMAIN RU,LV=(R3),LOC=BELOW,SP=241 GET CSA
         LR    R5,R1               SAVE EP.
         LR    R6,R1               CSA
         LR    R7,R3               NUMBER OF BYTES
         MVCL  R6,R2               MOVE IT
         SVCUPDTE 131,REPLACE,TYPE=4,EP=(R5)
         LTR   R15,R15                 UPDATE SUCCESSFUL?
         BNZ   ABEND                   NO, ABEND
*
         L     R2,=V(IGC0013C)     133 BEGINNING
         L     R3,=V(SVC133E)      133 END
         SR    R3,R2               GET LENGTH
         GETMAIN RU,LV=(R3),LOC=BELOW,SP=241 GET CSA
         LR    R5,R1               SAVE EP.
         LR    R6,R1               CSA
         LR    R7,R3               NUMBER OF BYTES
         MVCL  R6,R2               MOVE IT
         SVCUPDTE 133,REPLACE,TYPE=4,EP=(R5)
         LTR   R15,R15                 UPDATE SUCCESSFUL?
         BNZ   ABEND                   NO, ABEND
*
         DEQ   (SYSZSVC,TABLE,,SYSTEM)    RELEASE SVCTABLE
*
         L     R2,=V(ICHRTX00)     RTX BEGINNING
         L     R3,=V(RTXEND)       RTX END
         SR    R3,R2               GET LENGTH
         GETMAIN RU,LV=(R3),LOC=BELOW,SP=241 GET CSA
         LR    R5,R1               SAVE EP.
         LR    R6,R1               CSA
         LR    R7,R3               NUMBER OF BYTES
         MVCL  R6,R2               MOVE IT
         L     R2,FLCCVT-PSA(0)    CVT
         ICM   R2,B'1111',CVTSAF-CVT(R2)  SAFV
         BZ    ABEND               NO, ABEND
         ST    R5,SAFVEXIT-SAFV(R2) SAVE IT
         WTO   'CJYRINIT0 SVCS AND ROUTER EXIT INSTALLED'
*
         L     R15,=V(CJYRCVT)     RCVT LOADER
         BALR  R14,R15             CALL IT
*
RETURN   L     R13,SAVEAREA+4
         RETURN (14,12),,RC=0
ABEND    ABEND 100
SYSZSVC  DC    CL8'SYSZSVC'
TABLE    DC    CL8'TABLE'
         YREGS
         CVT   DSECT=YES
         IHAPSA   DSECT=YES
         ICHSAFV  DSECT=YES
         END   CJYRINIT
