         TITLE 'ESG-SECURITY DATA PROFILE PROCESSOR'
* CPARM='XREF(SHORT),OBJ,NODECK',LPARM='AC=1'                 CJYRPROF
*      DD DSN=SYS1.MACLIB,DISP=SHR
*      DD DSN=SYS1.MODGEN,DISP=SHR
*      DD DSN=SYS3.SAF.PARMLIB,DISP=SHR
         EJECT
CJYRPROF CSECT
*
**********************************************************************
*                                                                    *
*    COPYRIGHT (C) 1991 BY CRAIG J. YASUNA.  ALL RIGHTS RESERVED.    *
*                                                                    *
*    THIS SOFTWARE PRODUCT OR ANY OF ITS COMPONENTS MUST NOT BE      *
*    SOLD, GIVEN, OR OTHERWISE DISTRIBUTED TO ANY OTHER COMPANY      *
*    WITHOUT THE PRIOR WRITTEN PERMISSION OF:                        *
*                                                                    *
*                                  CRAIG J. YASUNA, PRESIDENT        *
*                                  ENTERPRISE SYSTEMS GROUP          *
*                                  2 MARC COURT                      *
*                                  EDISON, NEW JERSEY 08820          *
*                                                                    *
**********************************************************************
*
         SAVE  (14,12),,CJYRPROF_&SYSDATE._&SYSTIME._
         LR    R12,R15                 SETUP BASE REG.
         USING CJYRPROF,R12            ESTABLISH ADDRESSABILITY
         LA    R11,SAVEAREA            OUR SAVEAREA
         ST    R13,SAVEAREA+4          SAVE HIGH SAVEAREA
         ST    R11,8(R13)              SAVE LOW SAVEAREA
         LR    R13,R11                 POINT TO OUR SAVEAREA
         L     R8,FLCCVT-PSA(R0)       GET CVT POINTER
         ICM   R8,B'1111',CVTRAC-CVTMAP(R8) IS RCVT POINTER ZERO?
         BZ    NOGOOD                       YES, ABEND
         ICM   R8,B'1111',RCVTISTL-RCVT(R8) IS POINTER TO OUR AREA 0?
         BZ    NOGOOD                       YES, ABEND
*
         USING CJYRCVTD,R8
OK2GO    DS    0H                      NO, CONTINUE
         ENQ   (SECURITY,PROFS,E,,SYSTEM),RET=HAVE
         OPEN  (CJYPDATA,(INPUT))      OPEN INPUT FILE
         MODESET KEY=ZERO              SET KEY FOR GETMAIN
*
READLOOP DS    0H
         MVC   INRECOLD,INRECNEW       SAVE OLD I/P FOR COMPARE
         GET   CJYPDATA,INRECNEW
         CLC   INRECNEW(ACCAUTH-INREC),INRECOLD  NEXT REC. > PREV?
         BNH   OUTOFORD                          NO, NOT SORTED
         CLC   INRECNEW(GROUPID-INREC),INRECOLD  SAME CLASS/ENTITY?
         BE    PERMBLD                 YES, BUILD PERMIT RECORD
         CLC   =CL8' ',GROUPID         IS THIS A UNIVERSAL RECORD?
         BNE   NOUAR                   NO, A PERMIT WITHOUT UAR - ABEND
         GETMAIN RU,LV=RPEL,SP=241
         LR    R10,R1                  ESTABLISH ADDRESSABILITY
         USING RPECBLK,R10               TO RPE
         XC    RPECBLK(RPEL),RPECBLK   INITIALIZE TO BINARY ZEROES
         MVC   RPENEXT,LASTRPE         POINT TO PREVIOUS RPE
         ST    R10,LASTRPE             SAVE -> THIS RPE
         LA    R11,RPEACCPT            SET PERMIT ANCHOR
         LA    R1,L'RPECLASN           DEFAULT LENGTH IF NOTFOUND
         TRT   CLASS,TRTTEST           SEARCH CLASS - BLANKS FOUND?
         BZ    NOBLANKS                NO, LENGTH ALREADY IN R1
         S     R1,=A(CLASS)            CALCULATE LENGTH OF DATA
NOBLANKS DS    0H
         STC   R1,RPECLASL             SAVE LENGTH
         MVC   RPECLASN,CLASS            AND CLASS NAME
         XR    R2,R2                   CLEAR R2 FOR NOTFOUND
         LA    R4,38                   PRESET LENGTH FOR FACILITY
         CLC   =CL8'FACILITY',RPECLASN IS IT FACILITY?
         BE    TESTENT                 YES, TRT
         LA    R4,5                    PRESET LENGTH FOR DASDVOL
         CLC   =CL8'DASDVOL',RPECLASN  IS IT DASDVOL?
         BE    TESTENT                 YES, TRT
         LA    R4,5                    PRESET LENGTH FOR TAPEVOL
         CLC   =CL8'TAPEVOL',RPECLASN  IS IT TAPEVOL?
         BE    TESTENT                 YES, TRT
         LA    R4,43                   PRESET LENGTH FOR DATASET
         CLC   =CL8'DATASET',RPECLASN  IS IT DATASET?
         BE    TESTENT                 YES, TRT
         LA    R4,7                    DEFAULT LENGTH IF NOTFOUND
TESTENT  DS    0H
         LA    R1,1(R4)                DEFAULT LENGTH FOR SUBTRACTION
         EX    R4,TRT                  EXECUTE THE TRT
         B     CONT
TRT      TRT   ENTITY(*-*),TRTTEST     SEARCH ENTITY FOR BLANK/*
CONT     DS    0H
         B     BRANCTBL(R2)            AND WE FOUND...
BRANCTBL DS    0H
         B     NOTFND                  ... NO BLANKS/NO ASTERISKS
         B     ASTRKFND                ... AN ASTERISK
         B     BLANKFND                ... A BLANK
BLANKFND DS    0H
         LA    R1,1(R1)                ADD 1 TO INCLUDE BLANK IN LENGTH
ASTRKFND DS    0H
         S     R1,=A(ENTITY)           CALCULATE LENGTH
NOTFND   DS    0H                      IF NOTFOUND LENGTH ALREADY IN R1
         STC   R1,RPEENTYL             SAVE LENGTH
         MVC   RPEENTYN,ENTITY           AND ENTITY NAME
*
         CLC   =CL8' ',ACCAUTH         IS THERE AN ACCESS AUTHORITY?
         BE    NOACCAUT                NO, BLANK ACCESS AUTH. - ABEND
*
         LA    R1,PATTERN              BEGINNING OF TABLE
         LA    R2,ROWLEN               LENGTH OF A ROW IN THE TABLE
         LA    R3,PATTERN+PATTERNL-1   LAST BYTE OF TABLE
AUTHLP1  DS    0H
         CLC   ACCAUTH,0(R1)           MATCH ON ACCESS AUTHORITY?
         BE    MATCH1                  YES, MOVE IT IN
         BXLE  R1,R2,AUTHLP1           CHECK NEXT
MATCH1   MVC   RPEUACC,8(R1)           MOVE IN BIT PATTERN
         B     READLOOP                SEE IF ANY MORE I/P RECORDS
PERMBLD  DS    0H
         CLC   =CL8' ',GROUPID         IS THERE A GROUP/USERID?
         BE    PERMINVL                NO, INVALID PERMIT RECORD- ABEND
*
         CLC   =CL8' ',ACCAUTH         IS THERE AN ACCESS AUTHORITY?
         BE    NOACCAUT                NO, BLANK ACCESS AUTH. - ABEND
*
         GETMAIN RU,LV=RPEACCLN,SP=241
         LR    R9,R1                   ESTABLISH ADDRESSABILITY
         USING RPEACCLE,R9               TO ACCESS RECORD
         XC    RPEACCLE(RPEACCLN),RPEACCLE INITIALIZE TO B'0'
         ST    R9,0(R11)               ESTABLISH POINTER TO HERE
         MVC   RPEAUSR,GROUPID         SAVE GROUP/USERID OF PERMIT
*
         LA    R1,PATTERN              BEGINNING OF TABLE
         LA    R2,ROWLEN               LENGTH OF A ROW IN THE TABLE
         LA    R3,PATTERN+PATTERNL-1   LAST BYTE OF TABLE
AUTHLP2  DS    0H
         CLC   ACCAUTH,0(R1)           MATCH ON ACCESS AUTHORITY?
         BE    MATCH2                  YES, MOVE IT IN
         BXLE  R1,R2,AUTHLP2           CHECK NEXT
MATCH2   MVC   RPEACS,8(R1)            MOVE IN BIT PATTERN
         LA    R11,RPEANEXT            PROVIDE ANCHOR FOR NEXT PERMIT
         B     READLOOP                SEE IF ANY MORE I/P RECORDS
*
EOFILE   DS    0H
         OC    LASTRPE,LASTRPE         WERE THERE ANY RECORDS I/P?
         BZ    NOREC                   NO, ABEND
         L     R11,CJYPROFS            SAVE ADDRESS OF OLD CHAIN
         MVC   CJYPROFS,LASTRPE        ACTIVATE NEW CHAIN
         DROP  R8
         BAS   R14,MORERPE             START TO FREE RPE'S
*
         DEQ   (SECURITY,PROFS,,SYSTEM)    RELEASE ENQ
         MODESET KEY=NZERO             SET KEY BACK FOR CLOSE
         CLOSE (CJYPDATA)              CLOSE I/P DATASET
         WTO   'CJYRPROF7  **  NORMAL END OF PROGRAM  **'
         L     R13,4(R13)              RESTORE HSA
         RETURN (14,12),,RC=0          END O.K.
FREERPE  DS    0H
         L     R8,RPEACCPT             GET ACCESS LIST POINTER
         B     MOREACC                 SEE IF ANY PERMITS
FREEACC  DS    0H
         L     R8,RPEANEXT             GET -> NEXT ACC
         FREEMAIN RU,LV=RPEACCLN,A=(R9),SP=241    FREE CURRENT RPE
MOREACC  DS    0H
         LTR   R9,R8                   ANY MORE PERMITS?
         BNZ   FREEACC                 YES, DELETE THEM
*
         L     R11,RPENEXT             GET -> NEXT RPE
         FREEMAIN RU,LV=RPEL,A=(R10),SP=241   FREE CURRENT RPE
MORERPE  DS    0H
         LTR   R10,R11                 ANYMORE RPE'S?
         BNZ   FREERPE                 YES, FREE THEM
         BSM   0,R14                   RETURN TO END RUN
*
NOGOOD   DS    0H
         WTO   'CJYRPROF1  RCVT NOT PROPERLY INITIALIZED'
         WTO   'CJYRPROF1  **  PROGRAM TERMINATED  **'
         ABEND 100,,STEP
*
NOREC    DS    0H
         WTO   'CJYRPROF2  NO RECORDS INPUT FROM CJYPDATA'
         WTO   'CJYRPROF2  **  PROGRAM TERMINATED  **'
         ABEND 200,,STEP
*
OUTOFORD DS    0H
         L     R11,LASTRPE             SETUP FOR RPE CLEANUP
         BAS   R14,MORERPE             GO CLEANUP AND RETURN HERE
         BAS   R14,BADMSG              WRITE BADMSG
         WTO   'CJYRPROF3  I/P RECORDS OUT OF ORDER'
         WTO   'CJYRPROF3  **  PROGRAM TERMINATED  **'
         ABEND 300,,STEP
*
NOUAR    DS    0H
         L     R11,LASTRPE             SETUP FOR RPE CLEANUP
         BAS   R14,MORERPE             GO CLEANUP AND RETURN HERE
         BAS   R14,BADMSG              WRITE BADMSG
         WTO   'CJYRPROF4  PERMIT RECORD WITHOUT UNIVERSAL ACCESS RECOR*
               D'
         WTO   'CJYRPROF4  **  PROGRAM TERMINATED  **'
         ABEND 400,,STEP
*
PERMINVL DS    0H
         L     R11,LASTRPE             SETUP FOR RPE CLEANUP
         BAS   R14,MORERPE             GO CLEANUP AND RETURN HERE
         BAS   R14,BADMSG              WRITE BADMSG
         WTO   'CJYRPROF5  PERMIT RECORD WITHOUT GROUPID/USERID'
         WTO   'CJYRPROF5  **  PROGRAM TERMINATED  **'
         ABEND 500,,STEP
*
NOACCAUT DS    0H
         L     R11,LASTRPE             SETUP FOR RPE CLEANUP
         BAS   R14,MORERPE             GO CLEANUP AND RETURN HERE
         BAS   R14,BADMSG              WRITE BADMSG
         WTO   'CJYRPROF6  ENTITY RECORD WITH NO ACCESS AUTHORITY'
         WTO   'CJYRPROF6  **  PROGRAM TERMINATED  **'
         ABEND 500,,STEP
*
BADMSG   DS    0H
         MVC   BADMSGN+18(80),INRECNEW
BADMSGN  WTO   'CJYRPROF8                                              X
                                                  '
         BSM   0,R14
**********************
         PRINT NOGEN
SAVEAREA DS    18F
LASTRPE  DC    F'0'
LASTACC  DC    F'0'
SECURITY DC    CL8'CJYRCVT'
PROFS    DC    CL8'CJYPROFS'
INREC    DS    0CL80
INRECNEW DS    0CL80
CLASS    DC    XL8'00'
ENTITY   DC    XL44'00'
GROUPID  DC    XL8'00'
ACCAUTH  DC    XL8'00'
         DC    XL(80-(*-INREC))'00'
INRECOLD DC    XL80'00'
PATTERN  EQU   *
         DC    CL8'ALTER   ',AL1(RACFALTR)
ROWLEN   EQU   *-PATTERN
         DC    CL8'CONTROL ',AL1(RACFCNTL)
         DC    CL8'UPDATE  ',AL1(RACFUPDT)
         DC    CL8'READ    ',AL1(RACFREAD)
         DC    CL8'EXEC    ',AL1(RACFEXEC)
         DC    CL8'NONE    ',AL1(RACFNONE)
PATTERNL EQU   *-PATTERN
TRTTEST  DC    XL256'00'               TABLE TO FIND BLANK/ASTERISK
         ORG   TRTTEST+C' '            SETUP FLAG FOR BLANK
         DC    X'08'
         ORG   TRTTEST+C'*'            SETUP FLAG FOR ASTERISK
         DC    X'04'
         ORG
CJYPDATA DCB   MACRF=GM,EODAD=EOFILE,DDNAME=INPUT,DSORG=PS
         COPY  CJYRCVTD
         COPY  CJYPCBLK
         IHAPSA DSECT=YES
         CVT    DSECT=YES
         ICHPRCVT
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   CJYRPROF
