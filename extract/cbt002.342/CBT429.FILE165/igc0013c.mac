         TITLE 'ESG-SECURITY  SVC 133'
* CPARM='XREF(SHORT),OBJ,NODECK,RENT',LPARM='RENT'            IGC0013C
*      DD DSN=SYS1.MACLIB,DISP=SHR
*      DD DSN=SYS1.MODGEN,DISP=SHR
         EJECT
IGC0013C CSECT
*
**********************************************************************
*                                                                    *
*    COPYRIGHT (C) 1991 BY CRAIG J. YASUNA.  ALL RIGHTS RESERVED.    *
*                                                                    *
*    THIS SOFTWARE PRODUCT OR ANY OF ITS COMPONENTS MUST NOT BE      *
*    SOLD, GIVEN, OR OTHERWISE DISTRIBUTED TO ANY OTHER COMPANY      *
*    WITHOUT THE PRIOR WRITTEN PERMISSION OF:                        *
*                                                                    *
*                                  CRAIG J. YASUNA, PRESIDENT        *
*                                  ENTERPRISE SYSTEMS GROUP          *
*                                  2 MARC COURT                      *
*                                  EDISON, NEW JERSEY 08820          *
*                                                                    *
**********************************************************************
*
         USING IGC0013C,R6         SVC ENTRY R6 = EP.
         LR    R9,R1               SAVE ENTRY REG.
         LR    R8,R14              SAVE RETURN REG.
         GETMAIN RC,LV=GMLEN       GET STORAGE
         LR    R13,R1              LOAD REGISTER 13 WITH GETMAINED PTR
         USING WORKAREA,R13        SET UP DATA DSECT ADDR.
*
         L     R15,FLCCVT-PSA(0)   ADDRESS CVT
         TM    CVTDCB-CVT(R15),CVTMVSE  X/A?
         BNO   STRTROUT            NO, GO ON
         LA    R15,STRTTMDE        ADDRESS OF INS.
         O     R15,=X'80000000'    SET 31 BIT MODE
         BSM   R0,R15              GO TO INSTRUCTION
*
STRTTMDE L     R15,PSATOLD-PSA(0)  ADDRESS TCB
         L     R15,TCBRBP-TCB(R15) ADDRESS RB
         XR    R14,R14             CLEAR R14
         ICM   R14,B'0111',RBLINKB-RBSECT(R15) PRIOR RB
         TM    RBOPSWA-RBSECT(R14),RBOPSWM 31 BIT MODE?
         BO    STRTROUT            WE WERE 31 BIT, DON'T CLEAR
*
         LR    R10,R9              SAVE ENTRY REG.
         ICM   R10,B'1000',=X'00'  CLEAR HIGH ORDER OF PARM ADDR.
         MVC   RACRGM+RACRLEN-RACDLEN(RACDLEN),0(R10) MOVE PARM
         NC    RACRGM+RACRLEN-RACDLEN+8(28),=7X'00FFFFFF'
         NC    RACRGM+RACRLEN-RACDLEN+40(8),=7X'00FFFFFF'
         B     STRTSAFR
*
STRTROUT MVC   RACRGM+RACRLEN-RACDLEN(RACDLEN),0(R9) MOVE PARM
STRTSAFR LA    R5,SAFWORK          FOR SAF
         RACROUTE REQUEST=DEFINE,WORKA=(R5),MF=(E,RACRGM)
         L     R3,RACRGM           RACDEF RETURN CODE
         L     R4,RACRGM+4         RACDEF REASON CODE
*
         L     R15,FLCCVT-PSA(0)   ADDRESS CVT
         TM    CVTDCB-CVT(R15),CVTMVSE  X/A?
         BNO   RTRNFREE            NO, BYPASS BSM
         LA    R14,RTRNFREE        24 BIT MODE
         BSM   R0,R14              GO TO INSTRUCTION
*
RTRNFREE FREEMAIN RC,LV=GMLEN,A=(R13)
         LR    R15,R3              RETURN CODE
         LR    R0,R4               REASON CODE
         LR    R1,R9               RETURN R1
         BR    R8                  RETURN TO ORIG. R14
         LTORG
RACRREAL RACROUTE REQUEST=DEFINE,MF=L
RACRLEN  EQU   *-RACRREAL
RACDEF   RACDEF MF=L               THIS IS JUST FOR THE LENGTH
RACDLEN  EQU   *-RACDEF
*
WORKAREA DSECT
SAVEAREA DS     18F
SAFWORK  DS     XL512
RACRGM   DS     XL(RACRLEN)
GMLEN    EQU    *-WORKAREA
         PRINT  NOGEN
         YREGS
         CVT     DSECT=YES,LIST=NO
         IHAPSA  DSECT=YES,LIST=NO
         IKJRB   DSECT=YES,LIST=NO
         IKJTCB  DSECT=YES,LIST=NO
IGC0013C CSECT
         DS    D
SVC133E  EQU   *
         ENTRY SVC133E
         END
