         MACRO
         ENTRE# &MODNAME,&SAREA=YES
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GEXCNT                           COUNTER FOR LABELS
         GBLB  &IEXSW#                           IEXIT CONTROL SWITCH
         GBLC  &SAVE#                            SAVE AREA FOR LINK REG
         GBLC  &GLREG                            LINK REGISTER
         GBLC  &EXLIS                            LISTING TYPE INDICATOR
         GBLC  &STARTEX                          FIRST MODULE NAME
         GBLC  &ENTNAME                          CURRENT MOD NAME
.*
.**********************************************************************
.*       CHECK INPUT PARAMETERS                                       *
.**********************************************************************
.*
         AIF   ('&MODNAME' EQ '').ERR01          IS MODNAME SUPPLIED ?
.*
         AIF   ('&SAREA' EQ '').ERR02            IS SAVAREA SUPPLIED ?
         AIF   (N'&SAREA GT 1).ERR02             IS THERE ONE SAREA ?
.*
         AGO   .OK
.ERR01   MNOTE 12,'YS000012-MODULE NAME MISSING'
         MEXIT
.ERR02   MNOTE 12,'YS000013-&&SAREA NOT SUPPLIED OR INVALID'
         MEXIT
.*
.**********************************************************************
.*       SET UP GLOBAL VARIABLES                                      *
.**********************************************************************
.*
.OK      ANOP
.*       SET UP COUNTER INITIAL VALUE FOR LABELS OF EXIT#-MACRO
&GEXCNT  SETA  &GEXCNT+1
         SEQCK ENTRE                             PRED/SUCCESSOR CHECK
.*       CHANGE COUNTER FOR SECOND LABEL
&GEXCNT  SETA  &GEXCNT+1
&IEXSW#  SETB  0                                 TURN OFF IEXIT SWITCH
&SAVE#   SETC  '&SAREA'                          SET LINK REG SAREA
&ENTNAME SETC  '&MODNAME'                        SET CURRENT MOD NAME
.*
.**********************************************************************
.*       GENERATE CODE                                                *
.**********************************************************************
.*
         AIF   ('&EXLIS' EQ 'Y').CODE010         IS PRINT GEN ?
         PUSH  PRINT
         PRINT NOGEN
.CODE010 ANOP
         SPACE
&MODNAME DS    0H                                ROUTINE ENTRY POINT
         AIF   ('&STARTEX' EQ '&ENTNAME').CODE020  IS IT FIRST MODULE ?
.*
         AIF   ('&SAREA' EQ 'NO').CODE020        IS THERE ANY SAREA ?
.*
         AIF   ('&SAREA' NE 'YES').CODE030       IS SAREA STANDARD ?
.*       NEXT STATEMENT USES SECOND LABEL OF EXIT# TO SAVE LINK REG
         ST    &GLREG,ZLEX&GEXCNT                SAVE LINK REGISTER
         AGO   .CODE020
.CODE030 ANOP
         AIF   ('&SAREA'(1,1) NE '(').CODE040    IS SAREA REG ?
         LR    &SAREA(1),&GLREG                  SAVE LINK REGISTER
         AGO   .CODE020
.CODE040 ANOP
         ST    &GLREG,&SAREA                     SAVE LINK REGISTER
.*
.CODE020 ANOP
.*
.*       CHANGE COUNTER BACK FOR FIRST LABEL-IT WILL BE USED IN
.*       SUBSEQUENT MACRO-IEXIT#
&GEXCNT  SETA  &GEXCNT-1
         SPACE
.*
         MEND
