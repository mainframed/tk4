XFER     TITLE 'XFERVOL - GETS XFER TAPE VOLUME NUMBER'
***********************************************************************
*                                                                     *
*                        XFERVOL                                      *
*                                                                     *
*                XFER SDUMPS INDEX SUPPORT                            *
*                                                                     *
* ABSTRACT: GETS VOLSER OF XFER TAPE. TAPE DD-NAME PASSED TO PROGRAM  *
*                                                                     *
* INPUT: R1 -> A(PARM FIELD)                                          *
*              PARM FIELD = XFER TAPE DD-NAME                         *
*                                                                     *
* OUTPUT:             : XFER TAPE VOL=SER=XXXXXX                      *
*                                                                     *
* RETURN CODES:R15=0 - SUCCESSFUL EXECUTION OF PROGRAMM               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*  REGISTERS:   15 - ENTRY ADDRESS & RETURN CODE                      *
*               14 - RETURN ADDRESS                                   *
*               13 - SAVE AREA ADDRESS                                *
*               12 - CSECT BASE                                       *
*               11 - TIOT DSECT BASE                                  *
*               10 - UCB DSECT BASE                                   *
*                9 -                                                  *
*                8 -                                                  *
*                7 -                                                  *
*                6 - WORK REG                                         *
*                5 -                                                  *
*                4 - WORK REG                                         *
*                3 - WORK REG                                         *
*                2 - WORK REG                                         *
*                1 - LIST PARAM ADDRESS                               *
*                0 -                                                  *
***********************************************************************
         EJECT
***********************************************************************
*               XFERVOL CODE                                          *
***********************************************************************
         SPACE 3
*                            ******************************************
*                            *     DSECTS  AREAS.                     *
*                            ******************************************
         SPACE 2
TIODSECT DSECT
         IEFTIOT1                 MAP OF TIOT
         EJECT
         IEFUCBOB LIST=YES        MAP OF UCB
         EJECT
*                            ******************************************
*                            *   XFERVOL CSECT INITIALIZATION.        *
*                            ******************************************
         SPACE 2
         INIT# XFERVOL,BASEREG=(12),                                   X
               EXEC=MAINLINE,REL=02.01.00,                             X
               ID=COPYRIGHT_(C)_HOWARD_M._DEAN_1985,                   X
               EXLIST=YES
         SPACE
         USING UCBCMSEG,RA        UCB DSECT BASE
         USING TIOT1,RB           TIOT DSECT BASE
         EJECT
*                            ******************************************
*                            *    WORK FIELDS.                        *
*                            ******************************************
         SPACE
XFDDNAME DC    CL8' '             INPUT DD-NAME
TESTNAME DC    CL8' '             INPUT DD-NAME
ADDRTIOT DC    A(0)               TIOT ADDRESS
SAVEIN   DC    A(0)
*
TVOLSER  DC    CL6' '             XFER TAPE VOL=SER
*---------------------------------------------------------------------*
*        PROGRAM MESSAGES                                             *
*---------------------------------------------------------------------*
         SPACE
WTO001   WTO   'YS-001 DD-NAME NOT FOUND IN TIOT    ',ROUTCDE=(2,11),  X
               MF=L
         EJECT
*                            ******************************************
*                            * MAINLINE- MAIN ROUTINE OF THE XFERVOL  *
*                            ******************************************
         SPACE 2
         ENTRE# MAINLINE
         SPACE
***********************************************************************
*        GET XFER TAPE DD-NAME FROM INPUT PARM FIELD                  *
***********************************************************************
         SPACE
         L     R4,0(R1)           R3=ADDRESS OF PARM FIELD
         ST    R4,SAVEIN          SAVE INPUT PARM ADDRESS
         MVC   XFDDNAME,0(R4)
         SPACE
*---------------------------------------------------------------------*
*        GET TIOT AND THEN UCB ADDRESS FOR OUR DD-NAME                *
*---------------------------------------------------------------------*
         SPACE
         EXTRACT ADDRTIOT,'S',FIELDS=(TIOT)  LOAD TIOT ADD.IN BASE REG
         L     RB,ADDRTIOT
         LA    RB,TIOENTRY       ADDRESS OF FIRST DD-ENTRY
         USING TIOENTRY,RB
         DOWHILE XFDDNAME,NE,TIOEDDNM,CLC
            IF TIOELNGH,EQ,X'00',CLI
               WTO   MF=(E,WTO001)      DDNAME NOT FOUND
               IEXIT# RC=12
            ENDIF
            SR   R3,R3
            IC   R3,TIOELNGH
            LA   RB,0(R3,RB)
         ENDDO
         ICM   RA,B'0111',TIOEFSRT    LOAD UCB ADDRESS INTO BASE REG
         SPACE
***********************************************************************
*        GET VOL=SER FROM UCB AND RETURN TO CALLER                    *
***********************************************************************
         SPACE
         L     R4,SAVEIN
         MVC   0(8,R4),=CL8' '
         MVC   0(8,R4),UCBVOLI
         SPACE
         EXIT# RC=0
         EJECT
*                            ******************************************
*                            *     LITERALS                           *
*                            ******************************************
         SPACE
         LTORG
         SPACE 3
         END#  XFERVOL
