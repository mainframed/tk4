         MACRO
         IF    &OPND1,&RELA,&OPND2,&OPCD
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GIFCNT,&GIFLVL,&GDOLVL,&GIFSTK(16),&GIFDOST(16)
         GBLB  &IFORSW#                          'IFOR' INDICATOR
         LCLC  &LOC
         LCLC  &LBLYSS
         LCLA  &I,&J,&K                          'AND' LOOP COUNTERS
.*
         SEQCK IF
.*
         SPACE
         AIF   (&IFORSW# EQ 1).L10          IF THERE IS IFOR,SKIP NEXT
&GIFCNT  SETA  &GIFCNT+3
&GIFLVL  SETA  &GIFLVL+1
&GIFSTK(&GIFLVL) SETA &GIFCNT
&GIFDOST(&GIFLVL) SETA &GDOLVL
.*
.L10     ANOP
&LOC     SETC  'ZLIF'.'&GIFCNT'
.*
.**********************************************************************
.*       GENERATE CODE TO TEST CONDITION AND BRANCH                   *
.**********************************************************************
.*
&J       SETA  0                                 FIRST DELIMITER NUMBER
&I       SETA  1                                 FIRST PARM NUMBER
.*
.LOOPAND ANOP                                    LOOP THRU PARMS
&J       SETA  &J+1                        TRY NEXT PARM AS DELIMITER
         AIF   (&J GT N'&SYSLIST).ENDAND         IS IT END OF PARMS ?
         AIF   ('&SYSLIST(&J)' NE '*AND*').LOOPAND  IS IT DELIMITER ?
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN 2 DELIMITERS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND1       MUST BE 4 OR 1 PARM
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND1  ANOP                                    GEN. TEST AND BRANCH
         AIF   (&K EQ 1).GENOP1                  DO WE HAVE 1 PARM ?
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
         AGO   .GENOK
.GENOP1  ANOP                                    GEN CODE WITH 1 PARM
         GENC  &SYSLIST(&I),,,,&LOC
.GENOK   ANOP                                    GEN IS DONE
.*
&I       SETA  &J+1                  SET POINTER TO NEXT SET OF PARMS
         AGO   .LOOPAND                          REPEAT LOOP
.*
.ENDAND  ANOP                                    END OF LOOP
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN LAST DELIMTRS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND2       MUST BE 4 OR 1
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND2  ANOP                                  GEN LAST TEST & BRANCH
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
.*
.**********************************************************************
.*       ANALIZE 'OR' CONDITION                                       *
.**********************************************************************
.*
         AIF   (&IFORSW# EQ 0).CODE020        IF NO IFOR,SKIP NEXT
&GIFCNT  SETA  &GIFCNT-1
&LBLYSS  SETC  'ZLIF&GIFCNT'                  TO GET OPCODE IN COL 10
&LBLYSS  DS    0H                                MAIN BRANCH START
&GIFCNT  SETA  &GIFCNT+1
.*
.CODE020 ANOP
&IFORSW# SETB  0
         SPACE
.*
         MEND
