*---------------------------------------------------------------------*
*                                                                     *
*                          S M F I D                                  *
*                                                                     *
*   THIS SUBROUTINE RETURNS A SINGLE FOUR CHARACTER VARIABLE TO THE   *
*   CALLER CONTAINING THE FOUR CHARACTER SMF SYSTEM ID.               *
*                                                                     *
*   CALLING SEQUENCE:      CALL SMFID ( ID )                          *
*                                                                     *
*   THE SMFID IS LOCATED IN THE SYSTEM MANAGEMENT CONTROL AREA, A     *
*   CONTROL BLOCK POINTED TO BY THE CVT.                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
SMFID    CSECT
SMFID    RMODE 24
SMFID    AMODE 24
R1       EQU   1
R2       EQU   2
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         USING *,R15               DECLARE BASE REGISTER
         SAVE  (14,12),,*          SAVE CALLER'S REGISTERS
         SPACE
         L     R2,0(R1)            LOAD ADDRESS OF RETURN AREA
         L     R1,CVTPTR           LOAD ADDRESS OF CVT
         L     R1,(CVTSMCA-CVT)(R1)  LOAD ADDRESS OF SMCA
         MVC   0(4,R2),(SMCASID-SMCABASE)(R1)   COPY SMF ID TO USER
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15             CLEAR RETURN CODE
         BR    R14                 RETURN TO CALLER
         PRINT NOGEN
         CVT   DSECT=YES           MAP THE CVT
         IEESMCA ,                 MAP THE SMCA
         END   SMFID
