WTO      TITLE 'SEND SYSTSPRT FROM BATCH TMP TO OPERATOR'
*---------------------------------------------------------------------*
*  PROGRAM:  XFERWTO                                                  *
*                                                                     *
*  AUTHOR:   HOWARD DEAN, AMERICAN PRESIDENTAIL LINES                 *
*  MODIFIED: GENE LEW, MCI TELECOMMUNICATIONS CORP., JANUARY 1987     *
*                                                                     *
*  DATE:     JUNE 1985                                                *
*                                                                     *
*  FUNCTION: WRITES THE OUTPUT FROM 'XFER1/XFER2' CLIST TO THE        *
*            THE "PROGRAMMER", WHICH IS THE 'INDEX' DATASET FOR       *
*            'XFERDUMP'.                                              *
*---------------------------------------------------------------------*
         SPACE
XFERWTO  CSECT
XFERWTO  RMODE 24                     SET RESIDENCY MODE
XFERWTO  AMODE 24                     SET ADDRESSING MODE
         SETREG 12,CODE=RENT
         OPEN (SYSIN,INPUT)
         MVI  BUFFER,C'*'
         MVC  BUFFER+1(79),BUFFER
         WTO  MF=(E,WTOLIST)
GETIT    GET  SYSIN,BUFFER
         CLC  =C'READY',BUFFER      IS IT READY?
         BE   GETIT                 BYPASS THIS RECORD
         CLC  =C'END',BUFFER        IS IT END?
         BE   GETIT                 BYPASS THIS RECORD
         WTO  MF=(E,WTOLIST)
         B    GETIT
EOFIN    DS   0H                    END OF FILE/SYSIN
         CLOSE SYSIN
         MVI  BUFFER,C'*'
         MVC  BUFFER+1(79),BUFFER
         WTO  MF=(E,WTOLIST)
         EOJ  RC=0
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,LRECL=80,BLKSIZE=800,RECFM=FB,    X
               BUFNO=2,MACRF=GM,EODAD=EOFIN
         EJECT
WTOLIST  WTO  'A234567890B234567890C234567890D234567890E234567890F23456X
               7890G234567890H234567890',                              X
               ROUTCDE=(2,11),DESC=(4,6),MF=L
BUFFER   EQU   WTOLIST+4
         END   XFERWTO
