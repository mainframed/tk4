         TITLE 'IGC0025B - GET PASSWORD FROM JES2 - TYPE 4 SVC'
***********************************************************************
*                                                                     *
* MODULE NAME: IGC0025B (SVC 252)                                     *
*                                                                     *
* DESCRIPTION: ROUTINE TO GET PASSWORD FROM JCL                       *
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*  1) GET POINTER TO JCT                                              *
*         IF NOT FOUND RETURN WITH CODE 4                             *
*                                                                     *
*  2)     SCAN TABLE FOR DSN MATCH OR BLANK DSN (FOR DEFAULT PW)      *
*             IF NONE FOUND RETURN WITH CODE 4                        *
*                ELSE MOVE PSW AND RETURN WITH CODE 0                 *
*                                                                     *
*  ENTRY POINT:                                                       *
*      IGC0025B CALLED FROM IEAVMXIT                                  *
*                                                                     *
*  PURPOSE:                                                           *
*      TO GET PASSWORD FROM JES2                                      *
*                                                                     *
*  INPUT:                                                             *
*      R0  = ADDRESS OF FIELD IN WHICH PASSWORD IS RETURNED           *
*      R1  = ADDRESS OF DSN                                           *
*      R6  = ENTRY POINT                                              *
*      R7  = ADDRESS OF ASCB                                          *
*      R13 =                                                          *
*      R14 = RETURN ADDRESS                                           *
*                                                                     *
*  OUTPUT:                                                            *
*      IF PASSWORD FOUND                                              *
*          PASSWORD AT ADDRESS PASSED IN R0                           *
*          RETURN CODE 0                                              *
*      ELSE                                                           *
*          RETURN CODE 4                                              *
*                                                                     *
*  REGISTER USEAGE:                                                   *
*       R0  - WORK REGISTER                                           *
*       R1  - WORK REGISTER                                           *
*       R2  - WORK                                                    *
*       R3  - WORK                                                    *
*       R4  - WORK                                                    *
*       R5  - WORK                                                    *
*       R6  - BASE REGISTER FOR PROGRAM                               *
*       R7  - BASE REGISTER FOR ASCB DSECT                            *
*       R8  - UNUSED                                                  *
*       R9  - UNUSED                                                  *
*       R10 - BASE REGISTER FOR JCT DSECT                             *
*       R11 - UNUSED                                                  *
*       R12 - UNUSED                                                  *
*       R13 - UNUSED                                                  *
*       R14 - RETURN ADDRESS                                          *
*       R15 - RETURN CODE                                             *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,NOGEN
         COPY  $HASPGBL
&ANVIRON SETC  'USER'        SET ENVIRONMENT TO USER
         $HASPEQU            GENERATE HASP EQUATES
         $BUFFER             GENERATE HASP BUFFER DSECT
         $CADDR
         $HFAME              GENERATE HASP
         $HCCT               GENERATE HASP SSVT DSECT
         $JCT                GENERATE HASP JCT DSECT
         $MIT
         $PADDR
         $PARMLST
         $PSV
         $SCAT               GENERATE HASP SCAT DSECT
         $SJB                GENERATE HASP SJB DSECT
         $TQE                GENERATE HASP TQE DSECT
         $USERCBS            GENERATE USER CONTROL BLOCKS
         $XECB               GENERATE HASP XECB DSECT
         IFGRPL
         IECDSECS CVT,TCB,JSCB,PSCB,UPT,DSAB,TIOT,UCB,ASCB,            X
               DEB,PSA,JESCT,,                                         X
               SECCORE,(MAIN,(IOB=NO)),WTG,PREFX,EXPAND=YES
         IEFJSCVT
         IEFJSSIB
         PRINT ON,GEN
         TITLE 'IGC0025B - GET PASSWORD FROM JES2 - TYPE 4 SVC'
IGC0025B CSECT
IGC0025B AMODE 31
IGC0025B RMODE ANY
         USING PSA,0
         USING ASCB,R7
         USING IGC0025B,R6     SET ADDRESSABILITY
         B     SKIPHDR         SKIP OVER HEADER
         DC    AL1(L'IEIBALL)  LENGTH OF HEADER
IEIBALL  DC    C'IGC0025B &SYSDATE &SYSTIME'
SKIPHDR  DS    0H
         SPACE 2
***********************************************************************
*                                                                     *
*        SAVE PARAMETERS AND GET STORAGE                              *
*                                                                     *
***********************************************************************
         LR    R4,R0            SAVE PASSWORD RETURN ADDRESS
         LR    R5,R1            SAVE DSN ADDRESS
         GETMAIN RU,LV=DATALEN,SP=252 GET STORAGE
         LR    R13,R1
         USING DATA,R13
         ST    R14,RETADDR
         SPACE 2
***********************************************************************
*                                                                     *
*        GET POINTER TO JCT                                           *
*                                                                     *
***********************************************************************
         L     R1,PSATOLD               CURRENT TCB
         L     R1,TCBJSCB-TCB(,R1)      JSCB
         L     R1,JSCBACT-IEZJSCB(,R1)  SSIB
         ICM   R1,B'1111',JSCBSSIB-IEZJSCB(R1) SSIB
         BZ    RET4
         CLC   SSIBSSNM-SSIB(3,R1),=CL3'JES'
         BNE   RET4
         LA    R3,SSIBSSNM-SSIB(,R1)    POINT TO SUB SYSTEM NAME
         SPACE ,
         L     R1,FLCCVT
         L     R1,CVTJESCT-CVT(,R1)
         L     R1,JESSSCT-JESCT(,R1)
         USING SSCT,R1
SSCTLOOP DS    0H
         LTR   R1,R1
         BZ    RET4
         CLC   SSCTSNAM,0(R3)
         BE    SSCTFND
         L     R1,SSCTSCTA
         B     SSCTLOOP
SSCTFND  DS    0H
         ICM   R11,B'1111',SSCTSUS2
         BZ    RET4
         USING HCCT,R11
         DROP  R1
        $SJBFIND TYPE=LAST
         LTR   R15,R15
         BNZ   RET4
         ICM   R10,B'1111',SJBJCT-SJB(R1) GET POINTER TO JCT
         BZ    RET4                     RETURN CODE 4 IF NO JCT
         USING JCT,R10
         CLC   JCTID,=CL4'JCT'          IS IT A JCT
         BNE   RET4
         SPACE 2
***********************************************************************
*                                                                     *
*        CHECK PASSWORD MODE                                          *
*                                                                     *
***********************************************************************
         TM    JCTUSER0,JCTPW            PASSWORD SUPPLIED IN JCL ?
         BNO   RET4                     NO, RETURN CODE 4
         EJECT ,
***********************************************************************
*                                                                     *
*        PASSWORD PROCESSING ROUTINE                                  *
*                                                                     *
***********************************************************************
         USING PCT,R1
         L     R1,JCTUSER2               PTR TO PASSWORD CONTROL TABLE
         LTR   R1,R1                    IS THERE ONE ?
         BZ    RET4                     NO, RETURN CODE 4
         LH    R0,PCTPWCNT              GET COUNT
         LA    R1,PCTTABL               POINT TO FIRST ENTRY
         DROP  R1
         SPACE ,
PWDSRCH  DS    0H
         XC    8(44,R1),=52X'FF'        UNSCRAMBLE DSN
         CLC   8(44,R1),0(R5)           DATA SET NAMES COMPARE ?
         BE    PWDSRCH1                 YES, BR TO MOVE PW
         CLC   8(44,R1),=CL44' '        ENTRY FOR DEFAULT PASSWORD ?
         BNE   PWDSRCH2                 NO, BRANCH TO CONTINUE
         MVC   0(8,R4),0(R1)            MOVE IN PASSWORD
         XC    0(8,R4),=52X'FF'         UNSCRAMBLE PASSWORD
PWDSRCH2 DS    0H
         XC    8(44,R1),=52X'FF'        RESCRAMBLE DSN
         LA    R1,52(,R1)               BUMP TO NEXT ENTRY
         BCT   R0,PWDSRCH               LOOP TO END OF TABLE
         TM    JCTUSER0,JCTPWDEF        WAS THERE A DEFLT PASSWORD ?
         BO    RET0                     YES, BRANCH RETURN CODE 0
         B     RET4                     NO, BR RET CODE 4 PW NOT FOUND
         SPACE ,
PWDSRCH1 DS    0H
         XC    8(44,R1),=52X'FF'        RESCRAMBLE DSN
         MVC   0(8,R4),0(R1)            MOVE IN PASSWORD
         XC    0(8,R4),=52X'FF'         UNSCRAMBLE PASSWORD
         B     RET0                     BRANCH TO RETURN CODE 0
         EJECT ,
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
RET0     DS    0H
         LA    R2,0                     LOAD RETURN CODE 0
         B     RETURN                   BR TO RETURN
RET4     DS    0H
         LA    R2,4                     LOAD RETURN CODE OF 4
         B     RETURN                   BR TO RETURN
RETURN   DS    0H
         L     R3,RETADDR
         LR    R1,R13
         FREEMAIN RU,LV=DATALEN,A=(1),SP=252 FREE DATA DSECT STORAGE
         LR    R14,R3
         LR    R15,R2
         BR    R14                      RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        LITERALS                                                     *
*                                                                     *
***********************************************************************
         LTORG ,
         EJECT ,
***********************************************************************
*                                                                     *
*        DATA DSECT                                                   *
*                                                                     *
***********************************************************************
DATA     DSECT  ,
SAVEAREA DS     18F
RETADDR  DS     16F
DATALEN  EQU    *-DATA
         END
