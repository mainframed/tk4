         TITLE 'JESEXIT4 - JCL AND JES2 CONTROL STATEMENT SCAN'
***********************************************************************
*                                                                     *
* MODULE NAME: HASPXJ04                                               *
*                                                                     *
* DESCRIPTION: SCAN JES2 CONTROL STAEMENTS FOR /*PASSWORD CARDS AND   *
*              IF FOUND PROCESS THEM                                  *
*                                                                     *
*                                                                     *
*  ENTRY POINT:                                                       *
*      HASPXJ04                                                       *
*         CALLED FROM HASPRDR                                         *
*                                                                     *
*  PURPOSE:                                                           *
*      TO PROCESS /*PASSWORD JES2 CONTROL STAEMENTS                   *
*                                                                     *
*  INPUT:                                                             *
*      R0  = CODE INDICATING WHETHER A JES2 CONTROL STATEMENT OR JCL  *
*            IS BEING PROCESSED.                                      *
*                0 INDICATES JES2 CONTROL STATEMENT                   *
*                4 INDICATES JCL STATEMENT                            *
*      R1  = POINTER TO A 3 WORD PARAMETER LIST WITH FORMAT           *
*            WORD 1 - ADDRESS OF THE CONTROL STATEMENT/JCL IMAGE BUFF *
*            WORD 2 - ADDRESS OF THE EXIT FLAG BYTE, RDWFLAGX, IN PCE *
*            WORD 3 - ADDRESS OF THE JCTXWRK FIELD IN THE JCT         *
*      R10 = ADDRESS OF THE JCT                                       *
*      R11 = ADDRESS OF THE HCT                                       *
*      R13 = ADDRESS OF TE PCE                                        *
*      R14 = RETURN ADDRESS                                           *
*      R15 = ENTRY ADDRESS                                            *
*                                                                     *
*  OUTPUT:                                                            *
*      R0-R14 SAME AS ENTRY                                           *
*      R15   RETURN CODE                                              *
*      RETURN CODES HAVE THE FOLLOWING MEANINGS                       *
*        0 - TELLS JES2 THAT IF THERE ARE ANY ADDITIONAL EXIT ROUTINES*
*            ASSOCIATED WITH THIS EXIT, CALL THE NEXT CONSECUTIVE EXIT*
*            ROUTINE. IF THERE ARE NO ADDITIONAL EXIT ROUTINES ASSOCI-*
*            ATED WITH THIS EXIT, PERFORM STANDARD HASPRDR PROCESSING.*
*        4 - TELLS JES2 TO IGNORE OTHER EXIT ROUTINES ASSOCIATED WITH *
*            THIS EXIT AND TO PERFORM STANDARD HASPRDR PROCESSING.    *
*        8 - FOR JES2 CONTROL STATEMENTS, TELLS JES2 NOT TO PERFORM   *
*            STANDARD HASPRCCS PROCESSING: INSTEAD, IMMEDIATELY       *
*            CONVERT THE STATEMENT TO A COMMENT (//*) WITH THE NULL-ON*
*            -INPUT FLAG SET TO ONE AND WRITE THE STATEMENT TO THE JCL*
*            IMAGES DATA SET. FOR JCL STATEMENTS, TELLS JES2 TO       *
*            PERFORM STANDARD HASPRDR PROCESSING.                     *
*       12 - TELLS JES2 TO CANCEL THE JOB BECAUSE AN ILLEGAL CONTROL  *
*            STATEMENT HAS BEEN DETECTED;                             *
*            OUTPUT (THE INCOMPLETE JCL IMAGE) IS PRODUCED.           *
*       16 - TELLS JES2 TO PURGE THE JOB BECAUSE AN ILLEGAL CONTROL   *
*            STATEMENT HAS BEEN DETECTED; NO OUTPUT IS PRODUCED.      *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,NOGEN
         COPY  $HASPGBL
         EJECT ,
HASPXJ04 $MODULE NOTICE=NONE,                                          C
               TITLE='JESEXIT4 - JCL AND JES2 CONTROL STATEMENT SCAN', C
               RPL,                GENERATE MVS  RPL    DSECT          C
               $BUFFER,            GENERATE HASP BUFFER DSECT          C
               $HASPEQU,           GENERATE HASP EQUATES               C
               $JCT,                                                   C
               $JQE,                                                   C
               $PCE,                                                   C
               $CAT,                                                   C
               $HCT,                                                   C
               $MIT,                                                   C
               $MITETBL,                                               C
               $MODMAP,                                                C
               $RDRWORK,                                               C
               $USERCBS,                                               C
               $XECB,              GENERATE HASP XECB DSECT            C
               $XIT                GENERATE HASP XIT DSECT
         PRINT ON,GEN,NODATA
         SPACE 2
***********************************************************************
*                                                                     *
*        CHECK FOR /*PASSWORD CONTROL CARD                            *
*                                                                     *
***********************************************************************
UEXIT4  $ENTRY CSECT=YES,BASE=R12   EXIT ROUTINE ENTRY POINT
        $SAVE
         USING JCT,R10
         LR    R12,R15         LOAD BASE REGISTER
         LTR   R0,R0           IS IT FOR JES2 CONTROL CARD ?
         BNZ   RET0            NO, RETURN CODE 0
         LM    R2,R4,0(R1)     LOAD POINTERS TO PARAMS
         LTR   R4,R4           IS THERE A JCT ?
         BZ    RET0            NO, RETURN CODE 0
         CLC   0(10,R2),=CL10'/*PASSWORD'  IS IT A PSW CONTROL CARD ?
         BNE   RET0            NO, RETURN CODE 0
         MVC   JCTXWRK,=CL52' ' INITIAL TEMP PCT ENTRY TO BLANKS
         EJECT ,
***********************************************************************
*                                                                     *
*        CHECK FORMAT                                                 *
*                                                                     *
***********************************************************************
         LA    R1,ERRMSG1      POINT TO ERROR MSG 1
         CLI   10(R2),C' '     IS COL 11 A BLANK ?
         BNE   RET12           NO, RETURN CODE 12 WITH MSG1
         CLI   11(R2),C' '     IS COL 12 A BLANK ?
         BE    RET12           YES, RETURN CODE 12 WITH MSG1
         SPACE 2
***********************************************************************
*                                                                     *
*        MOVE DSN TO WORK AREA AND BLANK OUT IN CONTROL CARD          *
*                                                                     *
***********************************************************************
         LA    R1,ERRMSG2      POINT TO ERROR MSG 2
         LA    R5,11(,R2)      LOAD FOR START OF SCAN FOR BLANK
         LA    R6,1            LOAD INCREMENT FOR BXLE
         LA    R7,19(,R2)      LOAD FOR END OF SCAN
         LA    R8,JCTXWRK      LOAD ADDRESS TO MOVE PW
SCNLP1   CLI   0(R5),C' '      FOUND A BLANK ?
         BE    SCNLP1E         YES, EXIT LOOP
         MVC   0(1,R8),0(R5)   MOVE NEXT CHARACTER OF PW
         MVI   0(R5),C' '      BLANK OUT PASSWORD IN CARD
         LA    R8,1(,R8)       BUMP POINTER
         BXLE  R5,R6,SCNLP1    LOOP
         B     RET12           RETURN CODE 12 WITH MSG 2
SCNLP1E  DS    0H
         SPACE 2
***********************************************************************
*                                                                     *
*        FIND FIRST NON-BLANK AFTER PASSWORD                          *
*                                                                     *
***********************************************************************
         LA    R7,68(,R2)      LOAD FOR END OF SCAN
SCNLP2   CLI   0(R5),C' '      FOUND NON-BLANK ?
         BNE   SCNLP2E         YES, EXIT LOOP
         BXLE  R5,R6,SCNLP2    NO, LOOP
         B     SETDEF          BRANCH TO SET DEFAULT PASSWORD
SCNLP2E  DS    0H
         SPACE 2
***********************************************************************
*                                                                     *
*        CHECK FOR DSN= KEYWORD                                       *
*                                                                     *
***********************************************************************
         CLC   0(4,R5),=CL4'DSN=' FOUND DSN= KW ?
         BNE   SETDEF          NO, BRANCH TO SET DEFAULT PASSWORD
         SPACE 2
***********************************************************************
*                                                                     *
*        CHECK FOR DSN AFTER DSN= KEYWORD                             *
*                                                                     *
***********************************************************************
         LA    R1,ERRMSG6      POINT TO ERROR MSG 6
         LA    R7,71(,R2)      POINT TO COL 72
         LA    R5,4(,R5)       POINT PAST KEYWORD
         CR    R5,R7           AT END OF CARD ?
         BNL   RET12           YES, RETURN CODE 12 WITH MSG 6
         CLI   0(R5),C' '      IS THERE A DSN ?
         BE    RET12           NO, RETURN CODE 12 WITH MSG 6
         SPACE 2
***********************************************************************
*                                                                     *
*        MOVE DSN TO WORK AREA                                        *
*                                                                     *
***********************************************************************
         LA    R1,ERRMSG3      POINT TO ERROR MSG 3
         LA    R7,71(,R2)      LOAD FOR END OF SCAN
         LA    R8,JCTXWRK+7    LOAD ADDRESS TO MOVE DSN - 1
         LA    R9,JCTXWRK+52   LOAD END OF TABLE ENTRY
SCNLP4   CLI   0(R5),C' '      FOUND A BLANK ?
         BE    SCNLP4E         YES, EXIT LOOP
         LA    R8,1(,R8)       BUMP POINTER
         CR    R8,R9           MOVED 44 CHARS ALREADY ?
         BE    RET12           YES, RETURN CODE 12 WITH MSG 3
         MVC   0(1,R8),0(R5)   MOVE NEXT CHARACTER OF DSN
         BXLE  R5,R6,SCNLP4    LOOP
SCNLP4E  DS    0H
         B     SETENT1         BRANCH TO SET ENTRY IN PCT
         EJECT ,
***********************************************************************
*                                                                     *
*        SET DEFAULT PASSWORD SUPPLIED FLAG                           *
*                                                                     *
***********************************************************************
SETDEF   DS    0H
         OI    JCTUSER0,JCTPWDEF  SET PW DEFAULT FLAG
         B     SETENT1         BRANCH TO SET ENTRY IN PCT
         EJECT ,
***********************************************************************
*                                                                     *
*        INTIALIZE PCT                                                *
*                                                                     *
***********************************************************************
SETENT1  DS    0H
         TM    JCTUSER0,JCTPW  ALREADY BEEN HERE ONCE ?
         BO    SETENT2         YES, SKIP GETBUF
         OI    JCTUSER0,JCTPW  SET PW FLAG
        $GETBUF WAIT=YES       GET BUFFER
         LR    R2,R1           SAVE ADDRESS IN R2
         USING PCT,R2          ADDRESS PCT
         XC    PCTSTART(PCTTABL-PCTSTART),PCTSTART CLEAR PCT HEADER
         MVC   PCTID,=CL4'PCT' SET PCT ID
         LH    R0,$BUFLENG     SET BLOCK ...
         STH   R0,PCTLENG      ... LENGTH
         MVC   PCTJBKEY,JCTJBKEY   SET JOB KEY
         LA    R9,PCTTABL-PCT       SET INITIAL ...
         ST    R9,PCTENTOF         ... OFFSET
         ST    R2,JCTUSER2     SAVE BUFFER ADDRESS
         EJECT ,
***********************************************************************
*                                                                     *
*        SCAN TABLE FOR DUPLICATE DSN                                 *
*                                                                     *
***********************************************************************
SETENT2  DS    0H
         L     R2,JCTUSER2     LOAD ADDRESS OF PCT
         XC    JCTXWRK,=52X'FF' SCRAMBLE
         LA    R5,PCTTABL      GET POINTER TO START OF TABLE
         L     R7,PCTENTOF     GET OFFSET TO NEXT AVAILABLE ENTRY
         LA    R7,PCT(R7)      POINT TO LAST ENTRY
         CR    R7,R5           DOES TABLE HAVE ANY ENTRIES ?
         BNH   SETENT3         NO, THEN SKIP SEARCH
         LA    R6,52           GET TABLE ENTRY LENGTH
         SR    R7,R6           POINT TO LAST ENTRY
         SR    R5,R6           BACK UP ONE ENTRY LENGTH FOR BXH
SETENT2A DS    0H
         BXH   R5,R6,SETENT3
         CLC   8(44,R5),JCTXWRK+8 DATA SET NAMES COMPARE ?
         BNE   SETENT2A        NO, LOOP
         MVC   0(52,R5),JCTXWRK   MOVE IN TABLE ENTRY
         B     RET8             RET CODE 8 TO TELL JES NOT TO PROCESS
         EJECT ,
***********************************************************************
*                                                                     *
*        CHECK FOR AVAILABLE SPACE IN PCT AND MOVE IN ENTRY           *
*                                                                     *
***********************************************************************
SETENT3  DS    0H
         LH    R9,PCTPWCNT     GET COUNT
         LA    R9,1(,R9)        BUMP COUNT BY ONE
         STH   R9,PCTPWCNT      AND STORE BACK
         SPACE 1
         LA    R1,ERRMSG7       POINT TO ERROR MSG 7
         L     R5,PCTENTOF      GET CURRENT OFFSET
         LA    R0,52(,R5)       ADD 52 FOR OFFSET FOR NEXT ENTRY
         CH    R0,PCTLENG       OVER MAXIMUM LENGTH ?
         BH    RET12            YES, RETURN CODE 12 WITH MESSAGE
         ST    R0,PCTENTOF      SET NEW OFFSET
         SPACE 1
*        MOVE IN PCT ENTRY
         SPACE 1
SETENT3A DS    0H
         LA    R5,PCT(R5)       POINT TO  ENTRY
         MVC   0(52,R5),JCTXWRK MOVE IN TABLE ENTRY
         B     RET8             RET CODE 8 TO TELL JES NOT TO PROCESS
         DROP  R2
         EJECT ,
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
RET0     DS    0H
         LA    R15,0           LOAD RETURN CODE 0
         B     RETURN          BR TO RETURN
         SPACE 2
RET8     DS    0H
         LA    R15,8           LOAD RETURN CODE OF 8
         B     RETURN          BR TO RETURN
         SPACE 2
RET12    DS    0H
         MVC   JCTXWRK(80),0(R1) MOVE IN ERROR MESSAG
         OI    RDWFLAGX,RDWXXSEM FLAG ERROR MESSAGE SUPPLIED
         L     R1,JCTUSER2     GET ADDRESS OF BUFFER
         LTR   R1,R1           IS THERE ONE ?
         BZ    RET12A          NO, SKIP FREEBUF
         XC    JCTUSER2,JCTUSER2 CLEAR PCT ADDRESS
        $FREEBUF (R1)          FREE BUFFER
RET12A   DS    0H
         LA    R15,12          LOAD RETURN CODE OF 12
         B     RETURN          BR TO RETURN
         SPACE 2
RETURN   DS    0H
        $RETURN RC=(R15)       RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        ERROR MESSAGES                                               *
*                                                                     *
***********************************************************************
ERRMSG1  DC    CL80'INVALID PASSWORD CONTROL CARD, INVALID FORMAT'
ERRMSG2  DC    CL80'INVALID PASSWORD CONTROL CARD, PASSWORD IS LONGER TX
               HAN 8 CHARACTERS'
ERRMSG3  DC    CL80'INVALID PASSWORD CONTROL CARD, DATA SET NAME IS LONX
               GER THAN 44 CHARACTERS'
ERRMSG4  DC    CL80'INVALID PASSWORD CONTROL CARD, MULTIPLE DEFAULT PASX
               SWORDS SUPPLIED'
ERRMSG6  DC    CL80'INVALID PASSWORD CONTROL CARD, NO DATA SET NAME FOLX
               LOWING DSN KEYWORD'
ERRMSG7  DC    CL80'NUMBER OF PASSWORD CONTROL CARDS EXCEEDS MAXIMUM'
         EJECT ,
***********************************************************************
*                                                                     *
*        LITERALS                                                     *
*                                                                     *
***********************************************************************
         LTORG ,
        $MODEND
         END
