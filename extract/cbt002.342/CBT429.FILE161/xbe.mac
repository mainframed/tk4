         TITLE '   X B E L 0 1   '
***********************************************************************
*                                                                     *
*        'XBEL01' - AN ISPF DIALOG SERVICES PROGRAM                   *
*         FOR EXECUTING BROWSE OR EDIT.                               *
*                                                                     *
***********************************************************************
         SPACE
* WRITTEN BY BILL GODFREY, PRC INC.
*  (PRC INC. OF MCLEAN, VIRGINIA, WAS FORMERLY PLANNING RESEARCH CORP.)
* CURRENT INSTALLATION:
*  NOAA (NATIONAL OCEANIC AND ATMOSPHERIC ADMINISTRATION),
*  5200 AUTH ROAD, CAMP SPRINGS, MARYLAND 20746
* DATE WRITTEN. NOV 27 1985.
* DATE UPDATED. APRIL 25 1991.
* ATTRIBUTES. RE-ENTRANT.
* DISCLAIMER: NO GUARANTEE; NO WARRANTY; INSTALL/USE AT YOUR OWN RISK.
* REQUIRED PANELS.   NONE, BUT USES PANEL ISREDM02 FOR EDIT RECOVERY.
* REQUIRED MESSAGES. NONE.
* REQUIRED CLISTS.   NONE.
* MODULES LOADED. ISPEXEC.
* DESCRIPTION.
*  THIS SPF DIALOG PROGRAM CALLS BROWSE OR EDIT
*  USING THE DATA SET NAME SPECIFIED IN THE PARM FIELD.
*  THE SAME THING COULD BE DONE WITH A CLIST, BUT BY USING
*  A PROGRAM WE AVOID THE OVERHEAD OF OPENING SYSPROC.
*
*  THE PARM FIELD CONTAINS:
*          1) 'B' OR 'E'
*          2) COMMA
*          3) A FULLY QUALIFIED DATA SET NAME,
*  PLUS, OPTIONALLY,
*          4) COMMA AND OPTIONAL VOLUME,
*          5) COMMA AND OPTIONAL PROFILE,
*          6) COMMA AND OPTIONAL MACRO
*
***********************************************************************
*
*  SPECIAL CASE:
*  IF THE FIRST CHARACTER OF THE DSNAME IS A PERIOD, THE PERIOD
*  WILL BE DROPPED AND THE REST OF THE DSNAME WILL BE PASSED TO
*  BROWSE/EDIT WITHOUT QUOTES, AND BROWSE/EDIT WILL INSERT THE
*  THE TSO USER PREFIX (IF THERE IS ONE) AT THE FRONT OF THE
*  DSNAME.  THIS IS IN CASE YOUR PANEL SPECIFIES:
*          PGM(XBEL01) PARM(E,&ZPREFIX..LIB.CNTL)
*  AND &ZPREFIX IS NULL, WHICH IT IS WHEN PROFILE NOPREFIX.
*
***********************************************************************
*
*    SPECIFYING THE MEMBER NAME IN THE ZCMD FIELD.
*
*    IF THE DSNAME IS A PDS WITH NO MEMBER SPECIFIED, BROWSE/EDIT
*    WILL DISPLAY A MEMBER SELECTION LIST.  YOU CAN, HOWEVER
*    SPECIFY THE MEMBER ANOTHER WAY.  YOUR PANEL )PROC SECTION
*    COULD SPECIFY:
*
*     &ZSEL = TRANS( TRUNC (&ZCMD,'.')
*                   C,'PGM(XBEL01) PARM(E,&ZPREFIX..LIB.CLIST) NOCHECK'
*                   J,'PGM(XBEL01) PARM(E,&ZPREFIX..LIB.CNTL) NOCHECK'
*                 ' ',' '
*                   X,'EXIT'
*                   *,'?' )
*     &ZTRAIL = .TRAIL
*
*    THIS ALLOWS YOU TO SPECIFY THE MEMBER NAME BY APPENDING
*    A PERIOD AND THE MEMBER NAME TO THE SELECTION FIELD
*    REPRESENTED BY &ZCMD.  IN THE ABOVE EXAMPLE, IF YOU SELECTED
*    'J' IN THE &ZCMD FIELD, YOU WOULD GET A MEMBER SELECTION LIST
*    FOR THE PDS LIB.CLIST, BUT IF YOU ENTERED 'J.ABC' INSTEAD
*    YOU WOULD GET MEMBER ABC DIRECTLY.
*
***********************************************************************
*
*  LOG OF CHANGES:
*    06JUN88 - USE ZTRAIL INSTEAD OF XBEMEM
*              FIX BUG IN VOLUME: 1ST BCT AFTER PARMVOL WAS TO PARMDSN
*              ADD EDIT PROFILE AND MACRO.
*    24MAY90 - ADD RECOVERY SUPPORT.
*    25APR91 - NO CODE CHANGES. MINOR COMMENT CHANGES.
*
         SPACE
XBEL01   START
         USING *,R10,R11
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'XBEL01 1.0 '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(R13)       SAVE REGISTERS
         LR    R10,R15             LOAD BASE REGISTER
         LA    R15,1
         LA    R11,4095(R15,R10)
         LR    R2,R1               PARM POINTER
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R9,R1               INITIALIZE WORKAREA POINTER
         SPACE 1
         ST    R13,4(,R9)          CHAIN SAVEAREA
         ST    R9,8(,R13)          CHAIN SAVEAREA
         LR    R13,R9              UPDATE SAVEAREA POINTER
         USING @DATA,R9
         XC    ISPEXEC,ISPEXEC
         MVI   DSNAME,C' '
         MVC   DSNAME+1(55),DSNAME
         MVC   VOLUME,DSNAME
         MVC   BLANKS,DSNAME
         MVC   PROFILE,BLANKS
         MVC   MARCO,BLANKS
         MVC   DSNAME+56(8),DSNAME
         L     R1,0(,R2)           POINT TO PARM
         LH    R0,0(,R1)           GET LENGTH
         LTR   R0,R0               IF LENGTH ZERO
         BZ    EXIT12                 THEN EXIT
         LA    R1,2(,R1)           POINT TO FIRST BYTE OF PARM
         SPACE
***********************************************************************
*                                                                     *
*         GET 'B' OR 'E' FROM PARM                                    *
*                                                                     *
***********************************************************************
         SPACE
         MVC   BORE,=CL8'BROWSE'
         CLI   0(R1),C'B'
         BE    GOTBORE
         CLI   0(R1),C'E'
         BNE   EXIT12
         MVC   BORE,=CL8'EDIT'
GOTBORE  LA    R1,1(,R1)
         BCT   R0,COMMA1
         B     EXIT12              JUST 1 BYTE IN PARM
COMMA1   CLI   0(R1),C','
         BNE   EXIT12
         LA    R1,1(,R1)
         BCT   R0,GETDSN
         B     EXIT12              JUST 2 BYTES IN PARM
         SPACE
***********************************************************************
*                                                                     *
*         GET DSNAME AND VOLUME FROM PARM                             *
*                                                                     *
***********************************************************************
         SPACE
GETDSN   MVI   DSNAME,C''''        LEADING QUOTES
         MVI   QUOTES,C''''        CLOSING QUOTES
         LA    R14,DSNAME+1
         CLI   0(R1),C'.'          IS FIRST CHARACTER A PERIOD
         BNE   QUOTED              NO
         BCTR  R14,0               YES, BACK UP OVER LEADING QUOTES
         MVI   QUOTES,C' '         CLOSING BLANK
         LA    R1,1(,R1)           POINT PAST PERIOD
         BCT   R0,QUOTED           REDUCE LENGTH BY 1 AND BRANCH
         B     EXIT12              PARM WAS JUST A PERIOD
QUOTED   LA    R15,54(,R14)        MAX LENGTH 54
PARMDSN  CLI   0(R1),C','          IT THIS A COMMA
         BE    PARMCOM2            YES, FINISHED WITH DSNAME
         CR    R14,R15             TOO LONG?
         BE    EXIT12              YES, EXIT
         MVC   0(1,R14),0(R1)
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         BCT   R0,PARMDSN
         MVC   0(1,R14),QUOTES     CLOSE QUOTES
         B     PARMX
PARMCOM2 MVC   0(1,R14),QUOTES     CLOSE QUOTES
         LA    R1,1(,R1)           POINT PAST COMMA
         BCT   R0,PARMNXT2
         B     PARMX               NOTHING FOLLOWS COMMA
PARMNXT2 LA    R14,VOLUME
         LA    R15,6(,R14)         MAX LENGTH 6
PARMVOL  CLI   0(R1),C','          COMMA
         BE    PARMCOM3            YES, END OF NAME
         CR    R14,R15             TOO LONG
         BE    EXIT12              YES, EXIT
         MVC   0(1,R14),0(R1)
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         BCT   R0,PARMVOL
         B     PARMX
PARMCOM3 LA    R1,1(,R1)           POINT PAST COMMA
         BCT   R0,PARMNXT3
         B     PARMX               NOTHING FOLLOWS COMMA
PARMNXT3 LA    R14,PROFILE
         LA    R15,8(,R14)         MAX LENGTH 8
PARMPRO  CLI   0(R1),C','          COMMA
         BE    PARMCOM4            YES, END OF NAME
         CR    R14,R15             TOO LONG
         BE    EXIT12              YES, EXIT
         MVC   0(1,R14),0(R1)
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         BCT   R0,PARMPRO
         B     PARMX               NOTHING FOLLOWS COMMA
PARMCOM4 LA    R1,1(,R1)           POINT PAST COMMA
         BCT   R0,PARMNXT4
         B     PARMX
PARMNXT4 LA    R14,MARCO
         LA    R15,8(,R14)         MAX LENGTH 8
PARMMAC  CLI   0(R1),C','          COMMA
         BE    PARMX               YES, END OF NAME
         CR    R14,R15             TOO LONG
         BE    EXIT12              YES, EXIT
         MVC   0(1,R14),0(R1)
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         BCT   R0,PARMMAC
PARMX    CLI   DSNAME+1,C''''      WAS FIRST BYTE OF PARM A COMMA
         BE    EXIT12              YES, ERROR
         CLI   DSNAME+1,C' '       WAS FIRST BYTE OF PARM A COMMA
         BE    EXIT12              YES, ERROR
         SPACE
***********************************************************************
*                                                                     *
*         GET ENTRY POINT OF ISPLINK                                  *
*                                                                     *
***********************************************************************
         SPACE
         LOAD  EP=ISPLINK,ERRET=EXIT12
         ST    R0,ISPEXEC
         SPACE
***********************************************************************
*                                                                     *
*         ISSUE CONTROL-ERRORS-RETURN SO WE WILL MAINTAIN CONTROL     *
*                                                                     *
***********************************************************************
         SPACE
*              THIS KEEPS US IN CONTROL IF BROWSE/EDIT FAILS DUE TO
*              DATA SET NOT FOUND OR MEMBER NOT FOUND (BROWSE).
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'CONTROL'
         ST    R0,0(,R1)
         LA    R0,ERRORS
         MVC   ERRORS,=CL8'ERRORS'
         ST    R0,4(,R1)
         LA    R0,RETURN
         MVC   RETURN,=CL8'RETURN'
         ST    R0,8(,R1)
         OI    8(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         SPACE
***********************************************************************
*                                                                     *
*         GET THE VALUE OF 'ZTRAIL'                                   *
*                                                                     *
***********************************************************************
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'VCOPY'
         ST    R0,0(,R1)
*        LA    R0,=CL10'(XBEMEM)'
         LA    R0,=CL10'(ZTRAIL)'
         ST    R0,4(,R1)
         LA    R0,MEMLEN
         MVC   MEMLEN,=F'8'
         ST    R0,8(,R1)
         LA    R0,MEMNAME
         ST    R0,12(,R1)
         LA    R0,=CL7'MOVE'
         ST    R0,16(,R1)
         OI    16(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         SPACE
***********************************************************************
*                                                                     *
*         IF ZTRAIL CONTAINS A MEMBER NAME, APPEND IT TO THE DSN      *
*                                                                     *
***********************************************************************
         SPACE
         CH    R15,=H'0'
         BNE   NOMEM
         L     R15,MEMLEN
         LTR   R15,R15
         BZ    NOMEM
         LA    R1,DSNAME+56
MEMA01   CLI   0(R1),C' '
         BNE   MEMA02
         BCT   R1,MEMA01
MEMA02   SR    R0,R0
         CLI   0(R1),C''''
         BNE   MEMA03
         BCTR  R0,0                INDICATE QUOTES PRESENT
         BCTR  R1,0                POINT TO CHAR PRECEDING QUOTES
MEMA03   CLI   0(R1),C')'          IS MEMBER SPECIFIED IN PARM
         BE    EXIT12              YES, ERROR
         MVI   1(R1),C'('          ADD OPEN PAREN
         LA    R1,2(,R1)           POINT TO WHERE NAME WILL GO
         BCTR  R15,0               MEMLEN MINUS 1 FOR EX
         B     *+10
         MVC   0(0,R1),MEMNAME     (EXECUTED)
         EX    R15,*-6             MOVE MEMBER NAME
         LA    R1,1(R15,R1)        POINT PAST MEMBER NAME
         MVI   0(R1),C')'          CLOSE PARENS
         LTR   R0,R0               WERE QUOTES PRESENT
         BZ    *+8                 NO
         MVI   1(R1),C''''         YES, PUT QUOTES BACK
NOMEM    EQU   *
         SPACE
***********************************************************************
*                                                                     *
*         IF EDIT, INVOKE EDIT RECOVERY                               *
*            IF RECOVERY PENDING,                                     *
*               DISPLAY PANEL ISREDM02                                *
*               (USER WILL THEN ENTER A CHOICE)                       *
*               COPY ZEDCMD TO CMDNAME                                *
*               DEFER, CANCEL, OR PROCESS, DEPENDING ON CMDNAME       *
*               CHECK FOR MORE RECOVERY PENDING                       *
*                                                                     *
***********************************************************************
         SPACE
         CLI   BORE,C'E'
         BNE   EDRX
EDRLOOP  LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'EDREC'
         ST    R0,0(,R1)
         LA    R0,QUERY
         MVC   QUERY,=CL8'QUERY'
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15             IF NOTHING PENDING
         BZ    EDRX                   GO INVOKE EDIT
         C     R15,=F'4'           IF RC NOT = 4 (PENDING)
         BNE   EXIT12                 SEVERE ERROR
         SPACE
*               DISPLAY PANEL 'ISREDM02' TO GET THE USER'S DECISION
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'DISPLAY'
         ST    R0,0(,R1)
         LA    R0,PANEL
         MVC   PANEL,=CL9'ISREDM02 '
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15
         BNZ   EDRDEF
         SPACE
*              PANEL ISREDM02 COPIES THE USER'S RESPONSE IN ZCMD
*              TO ZEDCMD, THEN ERASES ZCMD.
*              NOW WE CALL ZCOPY TO GET THE VALUE IN ZEDCMD
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'VCOPY'
         ST    R0,0(,R1)
         LA    R0,=CL10'(ZEDCMD)'
         ST    R0,4(,R1)
         LA    R0,CMDLEN
         MVC   CMDLEN,=F'8'
         ST    R0,8(,R1)
         LA    R0,CMDNAME
         ST    R0,12(,R1)
         MVI   CMDNAME,C' '
         LA    R0,=CL7'MOVE'
         ST    R0,16(,R1)
         OI    16(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15             IF USER ENTERED END
         BNZ   EDRDEF                 DEFER
         SPACE
         L     R0,CMDLEN
         LTR   R0,R0
         BNZ   *+8
         MVI   CMDNAME,C' '
         SPACE
         CLI   CMDNAME,C'D'        IF USER REPLIED DEFER
         BE    EDRDEF                 DEFER
         CLI   CMDNAME,C'C'        IF USER REPLIED CANCEL
         BE    EDRCAN                 CANCEL
         CLI   CMDNAME,C' '        IF USER HIT ANYTHING ELSE BUT ENTER
         BNE   EDRDEF                 DEFER
         SPACE
         LA    R1,PARAMS           USER HIT ENTER, SO PROCESS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'EDREC'
         ST    R0,0(,R1)
         LA    R0,PROCESS
         MVC   PROCESS,=CL8'PROCESS '
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15
         BZ    EDRLOOP
         C     R15,=F'4'
         BNE   EXIT12
         B     EDRLOOP
         SPACE
EDRDEF   LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'EDREC'
         ST    R0,0(,R1)
         LA    R0,DEFER
         MVC   DEFER,=CL8'DEFER'
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15             IF NOT SUCCESSFUL
         BNZ   EXIT12                 SEVERE ERROR, GIVE UP
         B     EDRLOOP             QUERY AGAIN IN CASE MULTIPLE PENDING
         SPACE
EDRCAN   LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'EDREC'
         ST    R0,0(,R1)
         LA    R0,CANCEL
         MVC   CANCEL,=CL8'CANCEL '
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15             IF NOT SUCCESSFUL
         BNZ   EXIT12                 SEVERE ERROR, GIVE UP
         B     EDRLOOP             QUERY AGAIN IN CASE MULTIPLE PENDING
EDRX     EQU   *
         SPACE
***********************************************************************
*                                                                     *
*         CALL THE BROWSE OR EDIT SERVICE                             *
*                                                                     *
***********************************************************************
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,BORE
         ST    R0,0(,R1)
         LA    R0,DSNAME
         ST    R0,4(,R1)
         LA    R0,VOLUME
         ST    R0,8(,R1)
         CLI   BORE,C'B'
         BE    ONLY3
*              IF NEITHER PROFILE NOR MACRO ARE SPECIFIED, JUST PASS
*              3 PARAMETERS, IN CASE IT'S AN OLDER ISPF.
         CLI   PROFILE,C' '
         BNE   EMORE
         CLI   MARCO,C' '
         BE    ONLY3
EMORE    LA    R0,BLANKS PSWD
         ST    R0,12(,R1)
         LA    R0,BLANKS PANEL
         ST    R0,16(,R1)
         LA    R0,MARCO
         ST    R0,20(,R1)
         LA    R0,PROFILE
         ST    R0,24(,R1)
         OI    24(R1),X'80'
         B     *+8
ONLY3    OI    8(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15
         BZ    EXIT0
         SPACE
***********************************************************************
*                                                                     *
*         GET THE VALUE OF 'ZERRMSG'                                  *
*                                                                     *
***********************************************************************
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'VCOPY'
         ST    R0,0(,R1)
         LA    R0,=CL10'(ZERRMSG)'
         ST    R0,4(,R1)
         LA    R0,ZERRMSL
         MVC   ZERRMSL,=F'8'
         ST    R0,8(,R1)
         LA    R0,ZERRMSG
         ST    R0,12(,R1)
         LA    R0,=CL7'MOVE'
         ST    R0,16(,R1)
         OI    16(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         LTR   R15,R15
         BNZ   EXIT12
         SPACE
***********************************************************************
*                                                                     *
*         DISPLAY MESSAGE 'ZERRMSG' ON NEXT PANEL                     *
*                                                                     *
***********************************************************************
         SPACE
         LA    R1,PARAMS
         LA    R0,SERVICE
         MVC   SERVICE,=CL8'SETMSG'
         ST    R0,0(,R1)
         LA    R0,ZERRMSG
         ST    R0,4(,R1)
         OI    4(R1),X'80'
         L     R15,ISPEXEC
         BALR  R14,R15
         B     EXIT12
         SPACE
***********************************************************************
*                                                                     *
*         RETURN TO CALLER                                            *
*                                                                     *
***********************************************************************
         SPACE
EXIT0    SR    R15,R15             RETURN CODE ZERO
         B     EXIT
EXIT12   LA    R15,12              RETURN CODE 12
EXIT     LR    R2,R15              SAVE RETURN CODE
         L     R0,ISPEXEC          GET ENTRY POINT OF ISPLINK
         LTR   R0,R0               WAS IT LOADED
         BZ    NODEL               NO, BYPASS DELETE
         DELETE EP=ISPLINK
         XC    ISPEXEC,ISPEXEC
NODEL    EQU   *
         LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
         LM    0,12,20(R13)        RESTORE REGISTERS
         L     14,12(,R13)         LOAD RETURN ADDRESS
         BR    14                  RETURN
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
ISPEXEC  DS    F
PARAMS   DS    7F
SERVICE  DS    CL8
ERRORS   DS    CL8
RETURN   DS    CL8
QUERY    DS    CL8
DEFER    DS    CL8
CANCEL   DS    CL8
PROCESS  DS    CL8
CMDLEN   DS    F
CMDNAME  DS    CL8
PANEL    DS    CL10
BORE     DS    CL8
DSNAME   DS    CL56,CL8
VOLUME   DS    CL6
PROFILE  DS    CL8
MARCO    DS    CL8
BLANKS   DS    CL8
QUOTES   DS    CL1
MEMLEN   DS    F
MEMNAME  DS    CL8
ZERRMSL  DS    F
ZERRMSG  DS    CL8
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
