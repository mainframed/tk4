PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      TRANSFER A FILE TO A VM SYSTEM OVER THE NJE       */
/*      LINE AS PRINT.                                    */
/*--------------------------------------------------------*/
CONTROL  NOLIST NOMSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN +
   CONTROL LIST CONLIST SYMLIST MSG
ISPEXEC CONTROL ERRORS RETURN
/*--------------------------------------------------------*/
/*      SET UP THE VARIABLES                              */
/*--------------------------------------------------------*/
SET &NSY  = &STR(VM1)
SET &VMPRTER = &STR()
SET &NFLE = &STR()
FREE  FI(FILEOUT)
FREE  FI(JCL)
FREE  FI(MYJCL)
DELETE DXQRY.ZVTX
/**********************************************************/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
 SET &LASTCC = 0
 SET &MAXCC  = 0
 FREE FI(F80)
 ISPEXEC DISPLAY PANEL(NPRT#P1) CURSOR(NSY)
 IF &LASTCC = 8 THEN GOTO ALLDONE
 SET &LASTCC = 0
 SET &MAXCC  = 0
 /*--------------------------------------------------------*/
 /*      GET THE INFO FROM THE PANEL                       */
 /*--------------------------------------------------------*/
 ISPEXEC  VGET  (&NSY &VMPRTER &NFLE ZUSER)
 ATTR F80 RECFM(F B) LRECL(80) BLKSIZE(80)
 /*--------------------------------------------------------*/
 /* CHECK TO SEE IF THE FILE EXISTS BEFORE WE TRY TO PRINT */
 /*--------------------------------------------------------*/
 DSNCHECK '&NFLE' NOLIST
 IF &LASTCC EQ 0 THEN +
    DO
    GOTO DOCARD
    END
ELSE +
    DO
    WRITE  ERROR !!!!!!!!!
    WRITE
    WRITE  FILE YOU REQUESTED TO PRINT DOES NOT EXIST OR IS
    WRITE  NOT CATALOGED  OR THE MEMBER OF A FILE YOU REQUESTED
    WRITE  DOES NOT EXIST.  PLEASE RE-SPECIFY WITH CORRECT
    WRITE  INFORMATION.
    WRITE
    GOTO LOOP
    END
END
 /*--------------------------------------------------------*/
 /*      ALLOC THE FILE FOR JOB CARD PGM                   */
 /*--------------------------------------------------------*/
DOCARD: +
 ALLOC FI(FILEOUT) UNIT(VIO) SP(5) TR  NEW DELETE USING(F80)
 DOPROGL JOBCARD
 /*--------------------------------------------------------*/
 /*      GO GET THE JOBCARD  (2 RECORDS)                   */
 /*--------------------------------------------------------*/
 ALLOC FI(JCL) UNIT(SYSDA) SP(5) TR NEW CATALOG USING(F80)  +
          DA(DXQRY.ZVTX)
 OPENFILE FILEOUT INPUT
 OPENFILE JCL     OUTPUT
 GETFILE FILEOUT
 SET &JCL = &FILEOUT
 PUTFILE JCL
 GETFILE FILEOUT
 SET &JCL = &FILEOUT
 PUTFILE JCL
 /*--------------------------------------------------------*/
 /*      GO GET REST OF JCL FROM THE SKELETON              */
 /*--------------------------------------------------------*/
 ISPEXEC FTOPEN TEMP
 ISPEXEC FTINCL NPRT#S
 ISPEXEC FTCLOSE
 ISPEXEC VGET ZTEMPF
 ALLOC FI(MYJCL) DA('&ZTEMPF') OLD
 OPENFILE MYJCL INPUT
 /*--------------------------------------------------------*/
 /*      GO GET  6 JCL CARDS FROM THE SKELETON             */
 /*--------------------------------------------------------*/
 SET &B=1
 DO WHILE &B<7
    GETFILE MYJCL
    SET &JCL = &MYJCL
    PUTFILE JCL
    SET &B = &B + 1
 END
 CLOSFILE MYJCL
 CLOSFILE JCL
 CLOSFILE FILEOUT
 SUBMIT DXQRY.ZVTX
 SET &V = &STR($)
 SET &UJOB = &STR(&ZUSER&V)
 WRITE
 WRITE  &UJOB   SUBMITTED TO MVS TO ROUTE PRINT TO &NSY PRINTER &VMPRTER
 DELETE DXQRY.ZVTX
 FREE  FI(MYJCL,FILEOUT)
 GOTO LOOP
ALLDONE: +
CONTROL  NOLIST MSG PROMPT NOFLUSH
END
