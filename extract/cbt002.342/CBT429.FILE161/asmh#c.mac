PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      INVOKE 'ASMH'    TSO COMMAND                      */
/*--------------------------------------------------------*/
CONTROL  NOLIST   MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN +
   CONTROL LIST CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
ALLOC  FI(SYSTERM)  DA(*)
ISPEXEC VGET (ASMXDSN,ASMXLIB,ASMXRENT) SHARED
IF &ASMXRENT = &STR() THEN +
   DO
   SET  &ASMXRENT = &STR(NORENT)
   END
/**********************************************************/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   ISPEXEC ADDPOP COLUMN(1) ROW(1)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ASMH#P)
   SET &RC = &LASTCC
   ISPEXEC REMPOP
   IF &RC = 8 THEN GOTO ALLDONE
   IF  &MAXCC NE 0  THEN  GOTO ERRMSG
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*   WE WILL BUILD THE CMD AND SAVE THE VARIABLES, WHY ?  */
   /*   SHALL BE BACK AND PROBABLY TO COMPILE THE SAME CODE! */
   /*--------------------------------------------------------*/
   IF &ASMXLIB  =  &Z   THEN  +
      DO
      SET &XALIB = &STR()
      END
   ELSE   +
      DO
      SET &XALIB = &STR(LIB(&ASMXLIB))
      END
   ASMH  &ASMXDSN     +
         NOPRINT      +
         NOXREF       +
         NOESD        +
         NORLD        +
         BATCH        +
         TERM         +
         NOOBJ        +
         NOMSG        +
         &XALIB       +
         &ASMXRENT
   GOTO LOOP
ERRMSG:  +
  WRITE  **  SEVERE  PANEL  ERROR ***
ALLDONE: +
ISPEXEC VPUT (ASMXDSN,ASMXLIB,ASMXRENT) SHARED
FREE  FI(SYSTERM)
CONTROL  NOLIST MSG PROMPT NOFLUSH
END
