PROC 0 DEBUG
/*----------------------------------------------------------------*/
/*      ISPF/PDF CLIST FOR FMS PANELS FOR TSO ACCOUNT FUNCTIONS   */
/*      " DEFINE MASTER CATALOG ALIAS "                           */
/*----------------------------------------------------------------*/
CONTROL  NOLIST MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN   CONTROL LIST CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &AUSER = &STR(S)
SET &TUSER = &STR(T)
SET &USER = &STR(&SUBSTR(1:1,&SYSUID))
IF  &USER NE &AUSER  AND  &USER NE &TUSER THEN GOTO FINISH
SET  &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
SET  &CATNO   = &STR(1)
/*--------------------------------------------------------*/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   ISPEXEC CONTROL DISPLAY REFRESH
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#MP) CURSOR(NEWID)
   IF &LASTCC = 8 THEN GOTO FINISH
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   ADIOS TIME.
       GOTO FINISH
       END
   SET  &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*      DEFINE ALIAS ENTRY IN MASTER CATALOG              */
   /*--------------------------------------------------------*/
   IF  &CATNO =  &STR(1)  THEN  +
       DO
       SET  &CATNAME = &STR(CATALOG.VST1201)
       END
   ELSE  +
       IF  &CATNO = &STR(2)  THEN  +
           DO
           SET  &CATNAME = &STR(CATALOG.VPR1001)
           END
       ELSE  +
           IF  &CATNO = &STR(3)  THEN  +
               DO
               SET  &CATNAME = &STR(CATALOG.VDE1001)
               END
           ELSE  +
               DO
               WRITE
               WRITE    CATALOG NAME ERROR, SEE  TSB !!!!!
               WRITE
               GOTO LOOP
               END
   SET  &LASTCC = 0
   DEFINE  ALIAS   (NAME('&NEWID')  RELATE('&CATNAME'))  +
           CATALOG('&MASTCAT')
   IF  &LASTCC  EQ  0     THEN  +
       DO
       WRITE
       WRITE  *----------------------------------------------------*
       WRITE  *    &NEWID  ALIAS  ENTRY  ADDED.                    *
       WRITE  *----------------------------------------------------*
       WRITE
       END
   ELSE  +
       DO
       WRITE
       WRITE  *----------------------------------------------------*
       WRITE  *    ERROR  ENCOUNTERED  ADDING  ALIAS  ENTRY,  AS   *
       WRITE  *    YOU  SAW.   SORRY !!!!!!                        *
       WRITE  *                                                    *
       WRITE  *----------------------------------------------------*
       WRITE
       END
   /*--------------------------------------------------------*/
   /*      DETERMINE THE CPU WE ARE EXECUTING AND SET THE    */
   /*      "NODEID" TO THE OTHER SYSTEM.  BE CAREFUL!!!      */
   /*--------------------------------------------------------*/
   SYSID
   SET &SYSIDRC  =  &LASTCC
   IF  &SYSIDRC  =  0  THEN  +
       DO
       SET  &NODEID = &STR(N2)
       SET  &ID     = &STR(1)
       END
   ELSE  +
       DO
       SET  &NODEID = &STR(N1)
       SET  &ID     = &STR(2)
       END
   /*--------------------------------------------------------*/
   /*      GET THE ACCOUNT NUMBER FROM OUR TSO ID.           */
   /*--------------------------------------------------------*/
   GETACCT
   ISPEXEC  VGET (ACCT#)  SHARED
   /*--------------------------------------------------------*/
   /*      GET THE VERIFICATION FOR JOB SUBMIT TO OTHER SYS  */
   /*--------------------------------------------------------*/
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#MPB)
   IF &LASTCC = 8 THEN GOTO LOOP
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   JCL STEP 1.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      GET JCL FOR THE JOB1 AND SUBMIT                   */
   /*--------------------------------------------------------*/
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ACC#MSB
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   SUBMIT   '&ZTEMPF'
   GOTO LOOP
FINISH: +
   CONTROL  NOLIST MSG PROMPT NOFLUSH
END
