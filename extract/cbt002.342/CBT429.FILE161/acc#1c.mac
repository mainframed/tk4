PROC 0 DEBUG
/*----------------------------------------------------------------*/
/*      ISPF/PDF CLIST FOR FMS PANELS FOR TSO ACCOUNT FUNCTIONS   */
/*      " ADD TSO USER, TSB SYSPROG "                             */
/*----------------------------------------------------------------*/
CONTROL  NOLIST MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN   CONTROL LIST CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &AUSER = &STR(S)
SET &TUSER = &STR(T)
SET &USER = &STR(&SUBSTR(1:1,&SYSUID))
IF  &USER NE &AUSER  AND  &USER NE &TUSER THEN GOTO FINISH
SET &NEWID  = &STR( )
SET &PASSWD = &STR(TSS)
SET &ACTF   = &STR( )
SET &PROCED = &STR($TSOUSER)
SET &MAXREG = &STR(4096)
SET &ACCT   = &STR(NOACCT)
SET &JBSUB  = &STR(JCL)
SET &MNT    = &STR(NOMOUNT)
SET &OPER   = &STR(OPER)
SET &DESTU  = &STR( )
SET &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
/*--------------------------------------------------------*/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   ISPEXEC CONTROL DISPLAY REFRESH
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#1P) CURSOR(NEWID)
   IF &LASTCC = 8 THEN GOTO FINISH
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   ADIOS TIME.
       GOTO FINISH
       END
   SET &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*      GOT INFO FROM THE PANEL                           */
   /*--------------------------------------------------------*/
   SET &RSIZE = &MAXREG
   IF  &PROCED = &STR($TSODBA)  THEN  SET &RSIZE = &STR(8092)
   IF  &DESTU  = &STR( )  THEN +
       DO
       SET  &RMTDEST =  &STR( )
       END
   ELSE   +
       DO
       SET  &RMTDEST =  &STR(DEST(&DESTU))
       END
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ACCOUNT
      ADD (&NEWID  &PASSWD  &ACTF  &PROCED)  +
      SIZE(&RSIZE)                    +
      MAXSIZE(&MAXREG)                +
      UNIT(SYSALLDA)                  +
      &RMTDEST                        +
      &ACCT                           +
      &JBSUB                          +
      &MNT                            +
      &OPER                           +
      MSGCLASS(A) SYSOUT(A) JOBCLASS(A) HOLD(S) RECOVER
   END
   /*--------------------------------------------------------*/
   /*      DETERMINE THE CPU WE ARE EXECUTING AND SET THE    */
   /*      "NODEID" TO THE OTHER SYSTEM.  BE CAREFUL!!!      */
   /*--------------------------------------------------------*/
   SYSID
   SET &SYSIDRC  =  &LASTCC
   IF  &SYSIDRC  =  0  THEN  +
       DO
       SET  &NODEID = &STR(N2)
       END
   ELSE  +
       DO
       SET  &NODEID = &STR(N1)
       END
   /*--------------------------------------------------------*/
   /*      GET THE ACCOUNT NUMBER FROM OUR TSO ID.           */
   /*--------------------------------------------------------*/
   GETACCT
   ISPEXEC  VGET (ACCT#)  SHARED
   /*--------------------------------------------------------*/
   /*      GET THE VERIFICATION FOR  JOB1                    */
   /*--------------------------------------------------------*/
   JCL1:  +
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#1PA)
   IF &LASTCC = 8 THEN GOTO JCL2
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   JCL STEP 1.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      GET JCL FOR THE JOB1 AND SUBMIT                   */
   /*--------------------------------------------------------*/
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ACC#1SA
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   SUBMIT   '&ZTEMPF'
   /*--------------------------------------------------------*/
   /*      GET THE VERIFICATION FOR  JOB2                    */
   /*--------------------------------------------------------*/
   JCL2:  +
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#1PB)
   IF &LASTCC = 8 THEN GOTO JCL3
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   JCL STEP 2.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      GET JCL FOR THE JOB2 AND SUBMIT                   */
   /*--------------------------------------------------------*/
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ACC#1SB
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   SUBMIT   '&ZTEMPF'
   /*--------------------------------------------------------*/
   /*      GET THE VERIFICATION FOR  JOB3                    */
   /*--------------------------------------------------------*/
   JCL3:  +
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#1PC)
   IF &LASTCC = 8 THEN GOTO LOOP
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   JCL STEP 3.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      GET JCL FOR THE JOB3 AND SUBMIT                   */
   /*--------------------------------------------------------*/
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ACC#1SC
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   SUBMIT   '&ZTEMPF'
   GOTO LOOP
FINISH: +
   CONTROL  NOLIST MSG PROMPT NOFLUSH
END
