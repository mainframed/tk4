         TITLE 'FRONT-END FOR SORT'
*---------------------------------------------------------------------*
*                                                                     *
*       SOMETIMES SYSTEMS PLACE THE SORT MODULE IN A LINKLIST         *
*       LIBRARY. IN THIS CASE WE WANT TO DO A SORT IN TSO AND         *
*       DO NOT WANT TO USE THE TSO 'CALL' COMMAND.  TO USE THE        *
*       CALL COMMAND, YOU NEED TO GIVE THEM SECURITY ACCESS TO        *
*       THE CALLING LIBRARY (NO WAY). SO THIS IS A BETTER             *
*       ALTERNATIVE.                                                  *
*                                                                     *
*                                                                     *
*       JIM MARSHALL, CAPT, USAF (RET)                                *
*       DEPARTMENT OF TREASURY/FINANCIAL MANAGEMENT SERVICE           *
*                                                                     *
* INSTALLED:  05/01/91                                                *
*                                                                     *
* CHANGE LOG: 01/24/98 - JDM1 - ON NEW SYSTEM, DFSORT IS NOW IN LPA.  *
*                               "LINK" WOULD S0C4. NOW USE IKJEFTSR.  *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SP000    EQU   0
         TITLE 'SORTLNK - FRONT END FOR SORTING'
SORTLNK  CSECT
SORTLNK  AMODE 24
SORTLNK  RMODE 24
         SAVE  (14,12),,SORTLNK_&SYSDATE &SYSTIME
         LR    R12,R15
         USING SORTLNK,R12
         LA    R1,SAVE
         ST    R13,SAVE+4
         ST    R1,8(R13)
         LR    R13,R1
         SPACE
*-------------------------------------------------------------------*
*        SETUP FOR GOING OFF TO TSO/E SERVICE ROUTINE IKJEFTSR
*-------------------------------------------------------------------*
         SPACE
         L     R15,CVTPTR
         L     R15,CVTTVT(,R15)
         L     R15,TSVTASF-TSVT(,R15)
         CALL  (15),(FLAGS,PGM,PGMLEN,RETCODE,REASONC,ABENDCD,         X
               PARMLIST),VL
         LTR   R15,R15
         BNZ   ABORT1
         SPACE
*--------------------------------------------------------------------*
*        NOW WE CLEANUP AFTER OURSELVES AND GO HOME.
*--------------------------------------------------------------------*
         SPACE
         L     R13,SAVE+4
         RETURN (14,12),RC=(15)
ABORT1   ABEND 1
         SPACE
         DC    C'SYSTEMS PROG TOOL BOX'
         LTORG
SAVE     DS    18F
FLAGS    DS    0F
         DC    XL2'00'             MUST BE ZEROS
         DC    XL1'00'             ERROR PROCESSING FLAG BYTE
*                                    00 ==> NO DUMP IF ABEND
*                                    01 ==> DUMP TAKEN
         DC    XL1'02'             FUNCTION FLAG BYTE
*                                    01 ==> TSO CMD IS BEING INVOKED
*                                    02 ==> PGM BEING INVOKED
*                                    05 ==> CLIST
PGM      DC    C'SORT'
PGMLEN   DC    F'4'
RETCODE  DS    F
REASONC  DS    F
ABENDCD  DS    F
PGMPARM1 DC    H'00'  18
         DC    CL24'DYNALLOC=(SYSDA,1)'
PARMLIST CALL ,(PGMPARM1),VL,MF=L
         SPACE
CVTPTR   EQU   16
CVTTVT   EQU   X'9C'
         IKJTSVT
         END
