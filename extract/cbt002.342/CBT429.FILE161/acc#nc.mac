PROC 0 DEBUG
/*----------------------------------------------------------------*/
/*      ISPF/PDF CLIST FOR FMS PANELS FOR TSO ACCOUNT FUNCTIONS   */
/*      " DELETE  AN  ALIAS  ENTRY "                              */
/*----------------------------------------------------------------*/
CONTROL  NOLIST MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN   CONTROL LIST CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &AUSER = &STR(S)
SET &TUSER = &STR(T)
SET &USER = &STR(&SUBSTR(1:1,&SYSUID))
IF  &USER NE &AUSER  AND  &USER NE &TUSER THEN GOTO FINISH
SET  &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
/*--------------------------------------------------------*/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   ISPEXEC CONTROL DISPLAY REFRESH
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#NP) CURSOR(NEWID)
   IF &LASTCC = 8 THEN GOTO FINISH
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   ADIOS TIME.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      VERIFY USERS DESIRE TO PROCEED.                   */
   /*--------------------------------------------------------*/
   ISPEXEC DISPLAY PANEL(ACC#NPA)
   IF &LASTCC = 8 THEN GOTO FINISH
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   ADIOS TIME.
       GOTO FINISH
       END
   SET  &MASTCAT = &STR(CATALOG.MVSICFM.VSYACAT)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*      DELETE ALIAS ENTRY IN MASTER CATALOG, PROVIDED    */
   /*      THERE ARE NO CATALOG ENTRIES                      */
   /*--------------------------------------------------------*/
   LISTCAT  LEVEL('&NEWID')
   IF  &LASTCC  NE  0  THEN  +
       DO
       DELETE  '&NEWID'   ALIAS   CATALOG('&MASTCAT')
       END
   ELSE   +
       DO
       WRITE
       WRITE  *--------------------------------------------------*
       WRITE  *                                                  *
       WRITE  *    CATALOG  ENTRIES  EXIST  FOR  THE  ALIAS      *
       WRITE  *    ENTRY  TRYING  TO  BE  DELETED.  SORRY !!!!!  *
       WRITE  *                                                  *
       WRITE  *--------------------------------------------------*
       WRITE
       GOTO  LOOP
       END
   /*--------------------------------------------------------*/
   /*      DETERMINE THE CPU WE ARE EXECUTING AND SET THE    */
   /*      "NODEID" TO THE OTHER SYSTEM.  BE CAREFUL!!!      */
   /*--------------------------------------------------------*/
   SYSID
   SET &SYSIDRC  =  &LASTCC
   IF  &SYSIDRC  =  0  THEN  +
       DO
       SET  &NODEID = &STR(N2)
       SET  &ID     = &STR(1)
       END
   ELSE  +
       DO
       SET  &NODEID = &STR(N1)
       SET  &ID     = &STR(2)
       END
   /*--------------------------------------------------------*/
   /*      GET THE ACCOUNT NUMBER FROM OUR TSO ID.           */
   /*--------------------------------------------------------*/
   GETACCT
   ISPEXEC  VGET (ACCT#)  SHARED
   /*--------------------------------------------------------*/
   /*      GET THE VERIFICATION FOR JOB SUBMIT TO OTHER SYS  */
   /*--------------------------------------------------------*/
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(ACC#NPB)
   IF &LASTCC = 8 THEN GOTO LOOP
   IF &MAXCC  NE 0  THEN +
       DO
       WRITE  ***  SEVERE ERROR  ****   JCL STEP 1.
       GOTO FINISH
       END
   /*--------------------------------------------------------*/
   /*      GET JCL FOR THE JOB1 AND SUBMIT                   */
   /*--------------------------------------------------------*/
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ACC#NSB
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   SUBMIT   '&ZTEMPF'
   GOTO LOOP
FINISH: +
   CONTROL  NOLIST MSG PROMPT NOFLUSH
END
