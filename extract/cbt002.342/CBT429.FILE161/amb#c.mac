PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      INTERACTIVE INVOKING OF IBM UTILITY AMBLIST  JDM  */
/*--------------------------------------------------------*/
CONTROL  NOLIST NOMSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN  CONTROL LIST CONLIST SYMLIST MSG
ISPEXEC CONTROL ERRORS RETURN
/* ------------------------------------------------------ */
/*      GO GET FILE NAME AND PROGRAM NAME.                */
/*--------------------------------------------------------*/
SET &FNAME  =  &STR()
SET &MEMB   =  &STR()
AMB:  +
   SET &CUST   = &STR( )
   ISPEXEC CONTROL DISPLAY SAVE
   ISPEXEC ADDPOP COLUMN(1) ROW(1)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(AMB#P)
   SET &RC = &LASTCC
   ISPEXEC REMPOP
   IF &RC = 8 THEN GOTO CEND
   IF &MAXCC  > 8   THEN +
       DO
       WRITE  ***  SEVERE ERROR IN PANEL "P", ADIOS TIME.
       GOTO CEND
       END
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*      SORT THE FILE INTO JOBNAME SEQUENCE.              */
   /*--------------------------------------------------------*/
   DELETE $PRT$
   FREE  FI(SYSPRINT,SYSIN)
   SET &LASTCC = 0
   IF &SYSDSN(&FNAME) NE OK THEN +
       DO
       WRITE
       WRITE       DSN=&FNAME
       WRITE
       WRITE  ***  THE ABOVE FILE DOES NOT EXIST.  PLEASE
       WRITE       CHECK THE DSN.
       WRITE
       WRITE  ***  SORRY,  TRY  AGAIN.
       GOTO AMB
       END
   ALLOC FI(AMBLIB) DA(&FNAME) SHR  REUSE
   ALLOC FI(SYSPRINT) DA($PRT$)   UNIT(SYSDA)                 +
            TRACKS SPACE(1 1) NEW CATALOG
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL AMB#S1
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   ALLOC FI(SYSIN) DA('&ZTEMPF') SHR
   /*--------------------------------------------------------*/
   /*      INVOKE THE PROGRAM AMBLIST INTERTACTIVELY.        */
   /*--------------------------------------------------------*/
   AMBLINK
   FREE FI(AMBLIB)
   /*--------------------------------------------------------*/
   /*      BROWSE THE FILE.                                  */
   /*--------------------------------------------------------*/
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC BROWSE DATASET($PRT$)
   IF &MAXCC  = 12  THEN +
       DO
       WRITE  ***  SEVERE ERROR IN "BROWSE" FILE.  ADIOS TIME.
       GOTO CEND
       END
   ISPEXEC CONTROL DISPLAY RESTORE
   DEL $PRT$
   GOTO AMB:
CEND:    +
SET ZWINTTL =
DEL $PRT$
ALLOC FI(SYSIN) DA(*) REUSE
ALLOC FI(SYSPRINT) DA(*) REUSE
CONTROL  NOLIST MSG PROMPT NOFLUSH
EXIT CODE(0)
