         MACRO
&NAME    LDROP &W1,&W2,&W3,&W4,&W5,&W6,&W7,&W8
         GBLB  &LUSED
         GBLC  &USENM(50),&USEW(25)
         LCLA  &A,&B,&C,&D,&E
         LCLC  &X1,&COMMA
         AIF   (&LUSED).GO
         AGO   .LDRM000
.GO      ANOP
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H .           DEFINE NAME
.NONAME  ANOP
         AIF   (N'&SYSLIST GT 8).LDRM001
         AIF   (N'&SYSLIST LT 1).LDRM002
&A       SETA  1
&C       SETA  N'&SYSLIST
.OKX     AIF   ('&SYSLIST(&A)' EQ '').LDRM003
&A       SETA  &A+1
         AIF   (&A LE &C).OKX
.GEN     ANOP
&B       SETA  1
.NEXT    ANOP
&A       SETA  1
.FDROP   AIF   ('&USEW(&A)' EQ '&SYSLIST(&B)').HDROP
&A       SETA  &A+1
         AIF   (&A LE 25).FDROP
         AGO   .LDRM004
.HDROP   ANOP
&D       SETA  (&A*2)-1
&E       SETA  &D+1
&X1      SETC  '&X1'.'&COMMA'.'&SYSLIST(&B)'
&COMMA   SETC  ','
.CLOSEUP ANOP
&USEW(&A) SETC '&USEW(&A+1)'
&USENM(&D) SETC '&USENM(&D+2)'
&USENM(&E) SETC '&USENM(&E+2)'
&A       SETA  &A+1
&D       SETA  (&A*2)-1
&E       SETA  &D+1
         AIF   ('&USEW(&A)' EQ '').NEXTB
         AIF   (&A LE 25).CLOSEUP
.NEXTB   ANOP
&B       SETA  &B+1
         AIF   (&B LE &C).NEXT
.GENUSE  DROP  &X1            GEN DROP STMT
         MEXIT
.LDRM000 MNOTE 12,'LDROP ERROR - LUSE NEVER USED TO ESTABLISH BASE REG'
         MEXIT
.LDRM001 MNOTE 12,'OPERAND ERROR - TOO MANY OPERANDS'
         AGO   .OPFIELD
.LDRM002 MNOTE 12,'OPERAND ERROR - AT LEAST 1 REGISTER NUMBER REQUIRED'
         AGO   .OPFIELD
.LDRM003 MNOTE 12,'OPERAND ERROR - NULL OPERAND'
         AGO   .OPFIELD
.LDRM004 MNOTE 12,'LDROP ERROR - REGISTER &SYSLIST(&B) NEVER LUSED'
         AGO   .OPFIELD
.OPFIELD MNOTE *,'OPERAND LIST FOLLOWS - &SYSLIST(1),&SYSLIST(2),&SYSLI*
               ST(3),&SYSLIST(4),&SYSLIST(5),&SYSLIST(6),&SYSLIST(7),&S*
               YSLIST(8)'
         MEND
