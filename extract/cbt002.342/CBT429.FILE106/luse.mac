         MACRO
&NAME    LUSE  &WHERE,&W1,&W2
         GBLB  &LUSED
         GBLC  &USENM(50),&USEW(25)
         LCLA  &A,&B,&C,&D,&E
         LCLC  &X1,&X2
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H .           DEFINE NAME
.NONAME  ANOP
         AIF   (N'&SYSLIST GT 8).LUSM001
         AIF   (N'&SYSLIST LT 2).LUSM002
&A       SETA  1
&C       SETA  N'&SYSLIST
.OKX     AIF   ('&SYSLIST(&A)' EQ '').LUSM003
&A       SETA  &A+1
         AIF   (&A LE &C).OKX
&E       SETA  K'&WHERE
         AIF   (&E GT 8).LUSM004
&X1      SETC  '&WHERE'
.GEN     ANOP
&B       SETA  2
&E       SETA  0
.NEXT    ANOP
&A       SETA  1
&D       SETA  1
.FNULL   AIF   ('&USEW(&A)' EQ '').HNULL
         AIF   ('&USEW(&A)' EQ '&SYSLIST(&B)').MATCH
&A       SETA  &A+1
&D       SETA  (&A*2)-1
         AIF   (&A LE 25).FNULL
         AGO   .LUSM005
.MATCH   MNOTE 0,'** WARNING **  REGISTER &USEW(&A) LUSED AS BASE FOR &*
               USENM(&D)&USENM(&D+1) -'
         MNOTE 0,'               NEW USING CREATED'
.HNULL   ANOP
&USENM(&D)   SETC '&X1'
&USENM(&D+1) SETC '&X2'
&USEW(&A)    SETC '&SYSLIST(&B)'
         AIF   (&LUSED).GENUSE
         SPACE 1
*------> LUSE ENVIRONMENT ESTABLISHED <------
         SPACE 1
&LUSED   SETB  1
.GENUSE  ANOP
         USING &X1&X2,&SYSLIST(&B) .   GEN USING STMT
&B       SETA  &B+1
&E       SETA  &E+4096
&X2      SETC  '+'.'&E'
         AIF   (&B LE &C).NEXT
         MEXIT
.LUSM001 MNOTE 12,'OPERAND ERROR - TOO MANY OPERANDS'
         AGO   .OPFIELD
.LUSM002 MNOTE 12,'OPERAND ERROR - AT LEAST 2 OPERANDS REQUIRED'
         AGO   .OPFIELD
.LUSM003 MNOTE 12,'OPERAND ERROR - OPERAND &A IS NULL'
         AGO   .OPFIELD
.LUSM004 MNOTE 12,'OPERAND ERROR - OPERAND 1 IS LONGER THAN 8 CHARS'
         AGO   .OPFIELD
.LUSM005 MNOTE 12,'LUSE USER ERROR - TOO MANY LUSE WITHOUT LDROP'
         INUSE
         MEXIT
.OPFIELD MNOTE *,'OPERAND LIST FOLLOWS - &SYSLIST(1),&SYSLIST(2),&SYSLI*
               ST(3),&SYSLIST(4),&SYSLIST(5),&SYSLIST(6),&SYSLIST(7),&S*
               YSLIST(8),&SYSLIST(9)'
         MEND
