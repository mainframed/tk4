//HPZ$B   JOB ,ZBIEGIEN,MSGCLASS=W,CLASS=3
/*JOBPARM ROOM=3426
//* TAKE THE SAS RASTER FILE AND BUILD THE CHARACTER SET AGAIN
//* THE DD NAME OF THE SAS FILE IS CHAR
//A EXEC SAS@
//OUT DD SYSOUT=W
//* A MODEL CHARACTER SET MUST BE INPUT, USUALLY THE ONE YOU ARE
//* CHANGING OR MODIFYING
//CHARX DD DSN=SYS1.FONTLIB(C1S0CE12),DISP=SHR
//NEWCHAR DD DSN=&&TEMP,DISP=(,PASS,DELETE),UNIT=DISK,
// DCB=(RECFM=VBA,BLKSIZE=8209,LRECL=8205),SPACE=(TRK,(5,5),RLSE)
//* ONCE NO ERRORS CHANGE THE NEWCAR DD TO GO TO A PERMANENT FILE
//* WHICH THEN IS COPIED INTO YOUR "FONTLIB" DATA SET THAT PSF USES
//CHAR  DD DSN=HPZ.SAS.CHARSET,DISP=OLD
//ABC DD *   YOU MUST NAME THE NEW CHARACTER SET
C1D0HPZ1
//SYSIN DD *
OPTIONS GEN=0;
  /* RASTER MUST HAVE #RECORDS = (HEIGHT+1) */
  /*        MUST HAVE BIT PATTER IN XPATTRN */;
DATA CHARDAT2(KEEP=CHARID CINCR CWIDTH CHEIGTH ASPACE BSPACE
              CSPACE ADD PBYTES FBYTES)
     CHARHDR(KEEP= TOTALBY CHARS);
RETAIN TOTALBY 0 CHARS 0 ADD 0 CNT 0;
  /* READ THE SAS FILES AND CALCULATE SOME GLOBAL DATA */;
MERGE CHAR.RASTER
      CHAR.CHARDAT1 END=EODAT;
 BY CHARID;
IF _N_ = 1 THEN ADD=0;
CNT+1;
IF LAST.CHARID THEN DO;
 CHARS+1;
 FBYTES=(CWIDTH+1)/8*CNT;
 CNT=0;
 PBYTES=CEIL(FBYTES/8)*8; /* MOVE UP TO AN 8 BYTE BOUNDARY */;
 FBYTES=PBYTES-FBYTES; /* FILLER BYTES */;
 WORK=ADD+PBYTES;
 IF INT(ADD/2048) NE INT(WORK/2048) THEN /* OVERLAYS 2048 BOUNDARY */
    ADD=INT(WORK/2048)*2048;
 OUTPUT CHARDAT2;
 ADD=ADD+PBYTES;
END;
IF EODAT THEN DO;
 TOTALBY=ADD;
 OUTPUT CHARHDR;
END;

 /*PROC PRINT DATA=CHARDAT2 */;
 /*PROC PRINT DATA=CHARHDR */;

DATA  _NULL_;  /* BUILD THE CHARACTER SET */;
FILE NEWCHAR LINESIZE=8201 NOPRINT;
INFILE CHARX STOPOVER;
INPUT @4 ID $CHAR3. @ /* READ ID */;
IF ID ='D3A889'X THEN DO /* PROCESS BEGIN FONT */;
 PUT _INFILE_;  /* COPY */;
 RETURN;
END;
IF ID='D3EEEE'X THEN DO /* NOOP RECORD */;
 T1=TODAY();
 T2=TIME();
 INFILE ABC;
 INPUT SETNAME $8.;
 INFILE CHARX STOPOVER;
 PUT _INFILE_ @10 'HPZ CHANGED' T1 MMDDYY8.    /* COPY AN MODIFY */
 T2 TIME8. @36 SETNAME $8.  ;
 RETURN;
END;
IF ID='D3A689'X THEN DO /* FONT DESCRIPTOR */;
 PUT _INFILE_; /* COPY */;
 RETURN;
END;
IF ID='D3AC89'X THEN DO /* FONT CHARACTER POSITION */;
 PUT _INFILE_; /* COPY */;
 RETURN;
END;
IF ID='D3A789'X THEN DO /* FONT CONTROL */;
 SET CHARHDR;
 PUT _INFILE_ @27 TOTALBY IB3.;  /* COPY BUT MODIFIED */;
 RETURN;
END;
IF ID='D3AE89'X THEN DO /* FONT ORIENTATON */;
 PUT _INFILE_; /* COPY */;
 RETURN;
END;
IF ID='D38C89'X THEN DO /* FONT INDEX */;
 WORK=24*CHARS+8;
 PUT @1 '5A'X  @4 'D38C89000007'X  @2 WORK IB2. @;
 DO I =0 TO (CHARS-1);
  SET CHARDAT2;
  PT=10+I*24;
  WORK=ADD/8;
  PUT @PT CHARID $CHAR8. CINCR IB2. CWIDTH IB2. CHEIGTH IB2.
      WORK IB4. ASPACE IB2. BSPACE IB2. CSPACE IB2. @;
 END;
 PUT @1 '5A'X;
 RETURN;
END;
IF ID='D3EE89'X THEN DO /* FONT PATTERN RECORDS */;
 RETURN;  /*DROP ORIGINAL RECORDS */;
END;
RETAIN SEQ 8;
RETAIN PREV 10;
LENGTH XC $ 1;
IF ID='D3A989'X THEN DO /* END FONT */;
 DO I=0 TO (CHARS-1); /* OUTPUT RASTER PATTERN */;
  SET CHARDAT2 END=ENDCHAR;
  IF MOD(ADD,8192) EQ 0 AND ADD NE 0 THEN DO;
   DO J=PREV TO (8192+10-1);
    PUT @J '00'X  @;
   END;
   PUT @1 '5A2008D3EE8900'X  SEQ IB2.;
   SEQ=SEQ+1;
   PREV=10;
  END;
  WORK=MOD(ADD,8192);
  DO J=PREV   TO WORK+10-1; /* FILL OTHER ON 2048 BOUNDARY */;
   PUT @J '00'X  @;
  END;
  PT=10+WORK;
  W=((CWIDTH+1)/8)-1;
  DO M=0 TO CHEIGTH;
   SET CHAR.RASTER;
   DO K=0 TO W;
    XC=SUBSTR(XPATTRN,(K+1),1);
    PUT @PT XC $CHAR1. @;
    PT=PT+1;
   END;
  END;
  DO J=PT TO PT+FBYTES-1; /* FILL FILLER BYTES */;
   PUT @J '00'X  @;
   PT=PT+1;
  END;
  PREV=PT;
  IF ENDCHAR     THEN DO;
   WORK=PT+8-10;
   PUT @1 '5A'X  WORK IB2. 'D3EE8900'X  SEQ IB2.;
   SEQ=SEQ+1;
  END;
 END;
END;
IF ID='D3A989'X THEN    /* END FONT RECORD */;
  PUT _INFILE_ @8 SEQ IB2.;
//  EXEC AGIBMCOM,REPO=W UTILITY TO COMPARE TWO FILES
OPT
//OLD DD DSN=SYS1.FONTLIB(C1S0CE12),DISP=SHR
//NEW DD DSN=&&TEMP,DISP=(OLD,PASS,DELETE)
// EXEC AGRPRINT,REPO=W,COND=(0,NE) UTILITY TO PRINT FILE
PRTBV
//INPUT DD DSN=&&TEMP,DISP=(OLD,DELETE,DELETE)
