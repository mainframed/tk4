//HPZ$CAL   JOB ,ZBIEGIEN,TIME=3,CLASS=3,MSGCLASS=W
/*JOBPARM ROOM=3426
//COMP EXEC AGASMCL,LIBRARY='HPZ.TESTLIB(CAL)'
//ASM.SYSIN DD *
CAL      SUBENTRY BASES=(R13,R12)       SET BASES
         L     R2,0(R1)                 GET ADDRESS OF PARM
         MVC   YEARX,2(R2)              MOVE YEAR
         PACK  WORK,YEARX               PACK
         CVB   R2,WORK
         STH   R2,YEAR                  SAVE
         LA    R15,19                   SET COUNTER
         LA    R14,FIRSTL+1             GET LOCATION
LOOP1    MVC   0(4,R14),YEARX           MOVE YEAR
         LA    R14,7(R14)               UP LOCATION
         BCT   R15,LOOP1                BRANCH
         LH    R3,YEAR
         SR    R2,R2
         D     R2,=F'4'                 DIVIDE BY 4
         C     R2,=F'0'                 SEE IF ZERO
         BNE   NOTLEAP
         MVC   LEAP,=H'1'              SET TO ONE
NOTLEAP  LA    R1,365
         AH    R1,LEAP                 SET COUNTER
LOOP2    BAL   R14,GEN                 GEN TABLE
         BCT   R1,LOOP2
         OPEN (PRINTER,(OUTPUT))
         LA    R4,44
         LA    R3,FIRSTL
LOOP3    PUT   PRINTER,(R3)
         LA    R3,133(R3)
         BCT   R4,LOOP3
         CLOSE (PRINTER)
         SUBEXIT
GEN      TAG
         LH    R2,NNN                  GET JULIAN DAY
         AH    R2,=H'1'                UP ONE
         STH   R2,NNN                  SAVE
         LH    R5,YEAR                 GET YEAR
         M     R4,=F'5'                TIMES 5
         D     R4,=F'4'                DIVIDE BY 4
         AR    R5,R2
         SH    R5,LEAP
         SH    R5,=H'2'
         SR    R4,R4
         D     R4,=F'7'
         STH   R4,DOW
         LA    R7,59
         AH    R7,LEAP
         CR    R2,R7
         BNH   CONT1
         LA    R2,2(R2)
         SH    R2,LEAP
CONT1    LA    R2,91(R2)
         LR    R5,R2
         M     R4,=F'100'
         D     R4,=F'3055'
         STH   R5,MM
         M     R4,=F'3055'
         D     R4,=F'100'
         SR    R2,R5
         STH   R2,DD
         CLC   MM,OLDMM                SEE IF MONTH CHANGED
         BE    CONT2                   NO
         MVC   OFFSET,=F'0'            RESET COUNT
CONT2    LH    R2,DOW                  CALC ADD TO PUT
         MVC   OLDMM,MM
         MH    R2,=H'4'                EACH ENTRY 4 BXTES
         A     R2,OFFSET               FOR WEEK
         LA    R3,MONTHTB              ADDRESS OF MONTH OFFSETS
         LH    R4,MM                   MONTH *4
         S     R4,=F'3'
         MH    R4,=H'4'                =WORD OFFSET
         AR    R3,R4
         A     R2,0(R3)                ADD OFFSET
         BCTR  R2,0                    DOWN ONE
         LH    R3,DD
         CVD   R3,WORK
         IC    R6,0(R2)                 SAVE CC
         MVC   0(4,R2),=X'40202020'
         ED    0(4,R2),WORK+6          MOVE DAY OF MONTH
         STC   R6,0(R2)
         LH    R3,NNN
         CVD   R3,WORK
         IC    R6,133(R2)               SAVE CC
         MVC   133(4,R2),=X'40202020'
         ED    133(4,R2),WORK+6        MOVE DAY OF YEAR
         STC   R6,133(R2)               SAVE CC
         CLC   DOW,=H'6'
         BNER  R14
         L     R2,OFFSET
         LA    R2,266(R2)
         ST    R2,OFFSET
         BR    R14
         LTORG
         EQUREGS
PRINTER  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=133
YEARX    DC    CL4' '                  YEAR
YEAR     DC    H'0'                    YEAR
LEAP     DC    H'0'                    1=LEAP
NNN      DC    H'0'                    DAY OF YEAR
DOW      DC    H'0'                    DAY OF WEEK 0=SUN
MM       DC    H'0'                    MONTH
DD       DC    H'0'                    DAY
OLDMM    DC    H'0'
OFFSET   DC    F'0'
WORK     DC    D'0'
MONTHTB    DC  A(FIRSTL+533)
           DC  A(FIRSTL+567)
           DC  A(FIRSTL+601)
           DC  A(FIRSTL+635)
           DC  A(FIRSTL+2395)
           DC  A(FIRSTL+2429)
           DC  A(FIRSTL+2463)
           DC  A(FIRSTL+2497)
           DC  A(FIRSTL+4257)
           DC  A(FIRSTL+4291)
           DC  A(FIRSTL+4325)
           DC  A(FIRSTL+4359)
FIRSTL   DC    CL133'1'
         DC    CL133'-'
         DC    CL11'-',C'JANUARY',CL27' ',C'FEBRUARY'
         DC    CL27' ',C'MARCH',CL29' ',C'APRIL',CL14' '
         DC    C'0'
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL3' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL13'-',C'MAY',CL31' ',C'JUNE',CL30' '
         DC    C'JULY',CL29' ',C'AUGUST',CL13' '
         DC    C'0'
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL3' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL10'-',C'SEPTEMBER',CL26' ',C'OCTOBER'
         DC    CL27' ',C'NOVEMBER',CL26' ',C'DECEMBER',CL12' '
         DC    C'0'
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL7' '
         DC    C'SUN MON TUE WED THU FRI SAT'
         DC    CL3' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         DC    CL133'0'
         DC    CL133' '
         END   CAL
//TEST EXEC  PGM=CAL,PARM=1980
//STEPLIB  DD DSN=HPZ.TESTLIB,DISP=SHR
//SYSPRINT DD SYSOUT=W,DCB=BLKSIZE=1729
//SYSUDUMP DD SYSOUT=W
