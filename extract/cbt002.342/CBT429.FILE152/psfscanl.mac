//HPZ$L   JOB ,ZBIEGIEN,MSGCLASS=W,CLASS=3
/*JOBPARM ROOM=3426
//* TAKE THE SAS CODE PAGE AND BUILD THE REAL THING
//* THE DD NAME OF THE SAS FILE IS CHAR
//A EXEC SAS@
//OUT DD SYSOUT=W
//NEWCHAR DD DSN=&&TEMP,DISP=(,PASS,DELETE),UNIT=DISK,
// DCB=(RECFM=VBA,BLKSIZE=8209,LRECL=8205),SPACE=(TRK,(5,5),RLSE)
//* WHEN READY CHANGE THE NEWCHAR DD TO WRITE TO A FILE AND THEN COPY
//* THE FILE INTO THE "FONTLIB" PDS THAT IS USED BY PSF
//CHAR  DD DSN=HPZ.SAS.CHARSET,DISP=OLD
//ABC DD *  PUT NAME OF THE CODE PAGE HERE
T1D0HPZ1
//SYSIN DD *
OPTIONS GEN=0;
DATA  _NULL_;
FILE NEWCHAR LINESIZE=8201 NOPRINT;
INFILE ABC;
INPUT TRANNAME $ ;
PUT '5A0008D3A887000001'X; /* BEGIN CODE PAGE */;
T1=TIME();
T2=DATE();
M=-1;
WORK=63;
PUT '5A'X @4 'D3EEEE000002'X @2 WORK IB2. /* NOOP RECORD */
    @10 'BUILT BY HPZ' @36 TRANNAME $8. T1 TIME. T2 MMDDYY8. @ 64 'X';
SET CHAR.TRANSHDR;
PUT '5A0032D3A687000003'X      /* CODE PAGE DESCRIPTOR */
    'DCF REL 2 COMPATIBILITY HPZ'
    @42 '0008000000'X    CDPOINTS IB1.    'FFFF0000'X;
FLAG=0;
IF DVALID='N' THEN FLAG=128;
IF DPRINT='N' THEN FLAG=FLAG+64;
IF DINCR='N' THEN FLAG=FLAG+32;
FLG2=0;
IF VARBSPAC='N' THEN FLG2=8 ;
PUT '5A0015D3A787000004'X     /* CODE PAGE CONTROL */
    DEFAULTG $8.    FLAG PIB1.
    '0A4040'X    FLG2 IB1.;
DO I=0 TO (CDPOINTS-1);   /* CODE PAGE INDEX */;
 SET CHAR.TRANSCHR;
 FLAG=0;
 IF CVALID='N' THEN FLAG=128;
 IF CPRINT='N' THEN FLAG=FLAG+64;
 IF CINCR='N' THEN FLAG=FLAG+32;
 PT=(10*I)+10;
 PUT @PT CHARID @PT+8 FLAG PIB1. CDPOINT $CHAR1. @;
END;
WORK=CDPOINTS*10+8;
PUT @1 '5A'X @2 WORK IB2. @4 'D38C87000005'X;
PUT '5A0008D3A987000006'X; /* END CODE PAGE */;
//  EXEC AGIBMCOM,REPO=W COMPARE UTILITY
OPT
//OLD DD DSN=SYS1.FONTLIB(T1D0BASE),DISP=SHR
//NEW DD DSN=&&TEMP,DISP=(OLD,PASS,DELETE)
// EXEC AGRPRINT,REPO=W,COND=(0,NE) PRINT UTILITY
PRTBV
//INPUT DD DSN=&&TEMP,DISP=(OLD,DELETE,DELETE)
