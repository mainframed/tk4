//HPZ$U2 JOB ,ZBIEGIEN,MSGCLASS=W,CLASS=3
/*JOBPARM ROOM=3426
//A EXEC PGM=AGCGENER
//SYSPRINT DD SYSOUT=W
//SYSUT2 DD DSN=&&TEMP,DISP=(,PASS,DELETE),UNIT=DISK,
// DCB=(LRECL=8205,RECFM=VBM,BLKSIZE=8209),
// SPACE=(CYL,(5,5))
//SYSUT1 DD DSN=SYS1.FONTLIB(C1D0GB10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GB12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GC15),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GI12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GP12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GR10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT15),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT18),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT20),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0GT24),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0RT10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0SB12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0SI10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0SI12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0SO12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0ST10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0ST12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1D0ST15),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0DUMP),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0FM10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0FM12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0FM15),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0GU10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0GU12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0GU15),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0KATA),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L0TU10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00AOA),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00AON),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00BOA),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00BON),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00GSC),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00GUC),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00OAB),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1L00T11),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0AE10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0AE20),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0BITR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0BRTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CE10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CE12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CH10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CI10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CO10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0CR10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0DOTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0EBTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0EITR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0ELTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0EOTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0ESTR),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0LB12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0LR12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0OB10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0OR10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0PB12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0PI12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0PR10),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0PR12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0SR12),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0S192),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0S193),DISP=SHR
// DD DSN=SYS1.FONTLIB(C1S0S198),DISP=SHR
//A EXEC SAS@
//OUT DD SYSOUT=W
//CHARS DD DSN=&&TEMP,DISP=(OLD,DELETE,DELETE)
OPTIONS GEN=0;
 /* PRINT OUT INFO ABOUT THE CHARACTER SETS */;
DATA _NULL_;
RETAIN CODES 0;
FILE OUT PRINT;
INFILE CHARS STOPOVER;
INPUT @4 ID $CHAR3. @ /* READ ID */;
IF ID ='D3A889'X OR ID= 'D3A989'X THEN RETURN;
IF ID='D3EEEE'X THEN DO /* NOOP RECORD */;
 INPUT @2 LEN PIB2. @;
 LEN=LEN-8;
 INPUT @36 SETNAME $CHAR8.
       @10 COMMT $VARYING132. LEN;
 PUT _PAGE_ ' CHARACTER SET NAME: ' SETNAME / / COMMT / ;
 RETURN;
END;
IF ID='D3A689'X THEN DO /* FONT DESCRIPTOR */;
 INPUT @ 10 TYPEFACE $CHAR32.
            WEIGHT $CHAR1.
            WIDTH $CHAR1.
         +2 POINTS PIB2.1
        +10 DIRECTN $CHAR2.
        +4  ROTATN  $CHAR2.
        +8 FLG $CHAR1.
        +4 UWIDTH IB2.
           UPOS   IB2.;
 IF DIRECTN='0000'X THEN XDIR='ACROSS';
 ELSE IF DIRECTN='2D00'X THEN XDIR='DOWN  ';
                         ELSE XDIR='UP    ';
 IF      ROTATN='0000'X THEN XROT='  0';
 ELSE IF ROTATN='2D00'X THEN XROT=' 90';
 ELSE IF ROTATN='5A00'X THEN XROT='180';
 ELSE                        XROT='270';
 IF FLG='0... ....'B THEN ITALIC=' NO';
                     ELSE ITALIC='YES';
 IF FLG='.0.. ....'B THEN UNDERS=' NO';
                     ELSE UNDERS='YES';
 IF FLG='.... 0...'B THEN OVERS=' NO';
                     ELSE OVERS='YES';
 IF      WEIGHT='01'X THEN XWEIGHT='ULTRA-LIGHT';
 ELSE IF WEIGHT='02'X THEN XWEIGHT='EXTRA-LIGHT';
 ELSE IF WEIGHT='03'X THEN XWEIGHT='LIGHT      ';
 ELSE IF WEIGHT='04'X THEN XWEIGHT='SEMI-LIGHT ';
 ELSE IF WEIGHT='05'X THEN XWEIGHT='MEDIUM     ';
 ELSE IF WEIGHT='06'X THEN XWEIGHT='SEMI-BOLD  ';
 ELSE IF WEIGHT='07'X THEN XWEIGHT='BOLD       ';
 ELSE IF WEIGHT='08'X THEN XWEIGHT='EXTRA-BOLD ';
 ELSE IF WEIGHT='09'X THEN XWEIGHT='ULTRA-BOLD ';
 IF      WIDTH= '01'X THEN XWIDTH='ULTRA-CONDENSED';
 ELSE IF WIDTH= '02'X THEN XWIDTH='EXTRA-CONDENSED';
 ELSE IF WIDTH= '03'X THEN XWIDTH='CONDENSED      ';
 ELSE IF WIDTH= '04'X THEN XWIDTH='SEMI-CONDENSED ';
 ELSE IF WIDTH= '05'X THEN XWIDTH='MEDIUM         ';
 ELSE IF WIDTH= '06'X THEN XWIDTH='SEMI-EXPANDED  ';
 ELSE IF WIDTH= '07'X THEN XWIDTH='EXPANDED       ';
 ELSE IF WIDTH= '08'X THEN XWIDTH='EXTRA-EXPANDED ';
 ELSE IF WIDTH= '09'X THEN XWIDTH='ULTRA-EXPANDED ';
 PUT 'WEIGHT:' XWEIGHT '  WIDTH:' XWIDTH '  POINTSIZE:' POINTS
     '  PRINT DIRECTION:' XDIR '  CHARACTER ROTATION:' XROT /
     '  ITALIC:' ITALIC '  UNDERSCORE:' UNDERS '  OVERSTRIKE:' OVERS;
END;
IF ID='D3AC89'X THEN DO /* FONT CHARACTER POSITION */;
 INPUT @12 LCASEH IB2.
           UCASEH IB2.
           MASCEND   IB2.
           MDESCEND  IB2.;
 PUT 'LOWERCASE HEIGTH:' LCASEH '  UPPER CASE HEIGTH:' UCASEH
     '  MAXIMUM ASCENDER:' MASCEND '  MAXIMUM DESCENDER:' MDESCEND;
END;
IF ID='D3A789'X THEN DO /* FONT CONTROL */;
 INPUT @12 CID $CHAR1.
           FLG $CHAR1.
        +6 MAXBOXW IB2.
           MAXBOXH IB2.
        +3 PATTRNC IB3.
           INLNOFF IB2.;
 IF CID ='00'X THEN PUT'SINGLE BYTE FONT';
 ELSE PUT 'NOT A SINGLE BYTE FONT, THIS PROGRAM CAN NOT HANDLE THIS';
 IF FLG='.... .0..'B THEN KERNING='NOT ALLOWED';
                     ELSE KERNING='ALLOWED    ';
 IF FLG='.... ..0.'B THEN BOXSIZE='VARIABLE';
                     ELSE BOXSIZE='UNIFORM ';
 PUT 'KERNING:' KERNING '  BOX SIZE:' BOXSIZE
   '  MAXIMUM BOX SIZE WIDTH:'
   MAXBOXW '  HEIGTH:' MAXBOXH / 'PATTERN DATA COUNT:' PATTRNC
   '  UNIFORM INLINE OFFSET:' INLNOFF;
END;
IF ID='D3AE89'X THEN DO /* FONT ORIENTATON */;
 INPUT @10 TEXTOR $CHAR4.
           UBASEOFF IB2.
           MAXCHARI IB2.
           VARINCR  IB2.
           MAXBASEE IB2.
           FLG      $CHAR1.
       +1  EMSPACE  IB2.
       +2  FIGSPACE IB2.
       +2  BASEINCR IB2.;
 IF FLG='.... ...0'B THEN XVAR='EACH CHARACTER HAS OWN INCREMENT     ';
                     ELSE XVAR='UNIFORM CHAR INCREMENT APPLIES TO ALL';
 IF      TEXTOR='00002D00'X THEN XTEXT='  0, 90';
 ELSE IF TEXTOR='2D005A00'X THEN XTEXT=' 90,180';
 ELSE                            XTEXT='270,  0';
 PUT 'TEXT ORIENTATION:' XTEXT '  UNIFORM BASELINE OFFSET:' UBASEOFF
  '  MAXIMUM CHARACTER INCREMENT:' MAXCHARI
  '  VARIABLE SPACE INCREMENT:'
  VARINCR / 'MAXIMUM BASELINE EXTENT:' MAXBASEE +1 XVAR '  EM SPACE:'
  EMSPACE / 'FIGURE SPACE:' FIGSPACE '  DEFAULT BASELINE INCREMENT:'
     BASEINCR @;
END;
IF ID='D38C89'X THEN DO /* FONT INDEX */;
 INPUT @2 LEN IB2. @;
 CHARS= ((LEN-8)/24)   /* TAKE OFF 8 BYTE HEADER, EACH ENTRY 24 BYTES
                        */;
 PUT @60 'CHARACTERS:'CHARS;
 PUT / 'CHAR-ID INCR  W    H   A   B   C'
  @ 34 'CHAR-ID INCR  W    H   A   B   C'
  @ 67 'CHAR-ID INCR  W    H   A   B   C'
 @ 100 'CHAR-ID INCR  W    H   A   B   C';
 PT3=0;
 ARRAY CADDS(X) ADDD1-ADDD256;
 RETAIN ADDD1-ADDD256;
 DO I=0 TO (CHARS-1);
  PT=10+(I*24);
  INPUT @PT        CHARID $CHAR8.
                   CINCR IB2.
                   CWIDTH IB2.
                   CHEIGTH IB2.
                   CADD   IB4.
                   ASPACE IB2.
                   BSPACE IB2.
                   CSPACE IB2. @;
  X=I+1; CADDS=CADD*8;
  PT2=PT3*33+1;
  PUT @PT2    CHARID    CINCR 3. +1  CWIDTH 3. +1 CHEIGTH 3.
    +1  ASPACE 3. +1 BSPACE 3. +1 CSPACE 3. @;
  IF PT3=3 THEN DO; PUT; PT3=0; END; ELSE PT3=PT3+1;
 END;
END;
