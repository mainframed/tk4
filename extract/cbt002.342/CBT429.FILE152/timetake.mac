TIMETAKE SUBENTRY
* CHANGED 6/14/89 BY H. ZBIEGIEN TO HANDLE 31 BIT PROGRAMS
         ST    R1,SAVER1
         OPEN  (TIMEPARM,(INPUT))  OPEN PARAMETER FILE
         GET   TIMEPARM      GET A PARM CARD
         LR    R10,R1        R10 NOW POINTS TO CARD
         MVC   PGM,0(R10)    GET PROGRAM NAME
         PACK  PTUS,8(5,R10) GET # OF TIMER UNITS
         CVB   R2,PTUS       CONVERT TO BINARY
         ST    R2,PTUB       SAVE TIMER UNITS
NOCARD   TAG
         CLOSE (TIMEPARM)    CLOSE PARAMETER FILE
         LOAD  EPLOC=PGM     LOAD PROGRAM
         ST    R0,EPLOC      SAVE ENTRY POINT ADDRESS
         OPEN  (TIMEDATA,(OUTPUT)) OPEN DATA FILE
         STIMER TASK,EXITA,TUINTVL=PTUB   SET TIMMER
         L     R1,SAVER1     RETURN R1
         L     R15,EPLOC     GET ADDR OF EP OF PROGRAM
         BASSM R14,R15       BRANCH AND SET ADDRESS MODE   HPZ
         L     R14,=A(NEXT)  RETURNED, NOW SET BACK TO     HPZ
         BASSM R14,R14       24 BIT MODE                   HPZ
NEXT     TTIMER CANCEL       CANCEL OUTSTANDING TIME
         CLOSE (TIMEDATA)    CLOSE DATA FILE
         SUBEXIT             RETURN FROM TIMINGS
         DROP  R13
EXITA    TAG
         USING *,R15
         STM   R0,R15,MYSAVEA  SAVE IN MY AREA
         LA    R13,NEWSAVEA    NEW SAVE AREA
         L     R12,EXITAC      SET UP R12 AS A BASE
         DROP  R15
         USING EXITA,R12
         BAL   R11,COMMON      BRANCH TO COMMON ROUTINE
         STIMER TASK,EXITB,TUINTVL=PTUB  SET TIMER
         LM    R0,R15,MYSAVEA  RESTORE REGISTER
         BR    14              RETURN FROM TIMMING A
EXITB    TAG
         DROP  R12
         USING *,R15
         STM   R0,R15,MYSAVEB  SAVE IN MYAREA
         LA    R13,NEWSAVEB    NEW SAVE AREA
         L     R12,EXITAC      SET UP R12 AS A BASE
         DROP  R15
         USING EXITA,R12
         BAL   R11,COMMON      BRANCH TO COMMON ROUTINE
         STIMER TASK,EXITC,TUINTVL=PTUB SET TIMER
         LM    R0,R15,MYSAVEB  RESTORE REGISTERS
         BR    14              RETURN FROM TIMMING B
         DROP  R12
EXITC    TAG
         USING *,R15
         STM   R0,R15,MYSAVEC  SAVE IN MY AREA
         LA    R13,NEWSAVEC    NEW SAVE AREA
         L     R12,EXITAC      SET UP R12 AS A BASE
         DROP  R15
         USING EXITA,R12
         BAL   R11,COMMON      BRANCH TO COMMON ROUTINE
         STIMER TASK,EXITA,TUINTVL=PTUB SET TIMER
         LM    R0,R15,MYSAVEC  RESTORE REGISTERS
         BR    14              RETURN FROM TIMMING
         DROP  R12
COMMON   TAG
         USING EXITA,R12
         L     R5,CVTADDR    R5= A(CVT)
         L     R5,0(R5)      R5= A(TCB WORDS)
         L     R5,4(R5)      R5= A(MYTCB)
         LR    R6,R5         SAVE FOR TEST
         L     R5,0(R5)      R5= A(LAST ACTIVE RB)
         LA    R6,0(R6)      STRIP BYTE
RBLOOP   TAG
         L     R4,12(R5)     GET CDE ADDRESS    MVS HPZ
         CLC   MYNAME,8(R4)  SEE IF MY TCB      MVS HPZ
         BE    FOUND
         L     R5,28(R5)     GET NEXT RB
         LA    R5,0(R5)      STRIP BYTE
         CR    R6,R5         BACK AT TCB ?
         BE    ABEND1        YES
         B     RBLOOP        LOOP
FOUND    TAG
         L     R4,20(R5)     PSW WORD
         N     R4,=X'7FFFFFFF'   STRIP HIGH BIT    HPZ
         L     R5,36(R6)     R5=A(LLS)
LLSLOOP  TAG
         L     R3,4(R5)      PT TO CDE         MVZ HPZ
         CLC   PGM,8(R3)     CHECK LPRB NAME   MVS HPZ
         BE    FOUND2
         CLC   MYNAME,8(R3)  SEE IF ERROR      MVS HPZ
         BE    ABEND2
         L     R5,0(R5)      GET NEXT  LOAD LIST ENTRY MVS HPZ
         LTR   R5,R5         SEE IF NOMORE             MVS HPZ
         BZ    ABEND2                                  MVS HPZ
         B     LLSLOOP
FOUND2   TAG
         L     R3,20(R3)     GET ADDRESS OF EXTENT LIST  MVS HPZ
         L     R3,12(R3)     GET  LOAD ADDRESS           MVS HPZ
         N     R3,=X'7FFFFFFF'   STRIP HIGH BIT HPZ
         SR    R4,R3         R3-R4 INTO R4
         ST    R4,WORD
         PUT   TIMEDATA,WORD  WRITE ADDRESS OUT
         BR    R11           RETURN FROM COMMON
ABEND1   ABEND 1000,DUMP
ABEND2   ABEND 1001,DUMP
         LTORG
         EQUREGS
CVTADDR  EQU  X'4C'
MYSAVEA  DC    16F'0'        SAVE AREA EXITA
NEWSAVEA DC    18F'0'        SAVE AREA EXITA
MYSAVEB  DC    16F'0'        SAVE AREA EXITB
NEWSAVEB DC    18F'0'        SAVE AREA EXITB
MYSAVEC  DC    16F'0'        SAVE AREA EXITC
NEWSAVEC DC    18F'0'        SAVE AREA EXITC
TIMEPARM DCB   DSORG=PS,MACRF=GL,DDNAME=TIMEPARM,EODAD=NOCARD,         X
               RECFM=F,LRECL=80
TIMEDATA  DCB   DSORG=PS,MACRF=PM,DDNAME=TIMEDATA,RECFM=FB,            X
               LRECL=4
PGM      DC    CL8'ABCDEFGH' DUMMY PROGRAM NAME
MYNAME   DC    CL8'TIMETAKE'
         DS    0D
PTUS     DC    PL8'38'       #OF TIMMER UNITS IN PACKED MODE
PTUB     DC    F'38'         #OF TIMMER UNITS IN BINARY 26.0416
EPLOC    DC    F'0'          ENTRY POINT LOCATION OF PROGRAM
SAVER1   DC    F'0'          JCL PARM POINTER SAVE
EXITAC   DC    A(EXITA)      ADCON FOR BASING
WORD     DC    F'0'          THE RELATIVE ADDRESS
         END   TIMETAKE
