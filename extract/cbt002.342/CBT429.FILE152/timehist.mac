00001  IDENTIFICATION DIVISION.
00002  PROGRAM-ID. TIMEHIST.
00003  ENVIRONMENT DIVISION.
00004  CONFIGURATION SECTION.
00005  SOURCE-COMPUTER. IBM-370.
00006  OBJECT-COMPUTER. IBM-370.
00007  SPECIAL-NAMES.
00008      C01 IS TOP-OF-PAGE.
00009  INPUT-OUTPUT SECTION.
00010  FILE-CONTROL.
00011      SELECT UCRI-CARD    ASSIGN TO UT-S-SYSIN.
00012      SELECT UPRO-PRINTER ASSIGN TO UT-S-PRINTER.
00013      SELECT UDAI-DATA    ASSIGN TO UT-S-TIMEDATA.
00014  DATA DIVISION.
00015  FILE SECTION.
00016  FD  UCRI-CARD
00017      BLOCK CONTAINS 0 RECORDS
00018      RECORD CONTAINS 80 CHARACTERS
00019      LABEL RECORDS ARE OMITTED.
00020  01  UCRI-REC.
00021  05  UCRI-START        PIC X(6).
00022  05  UCRI-ILENX.
00023  10  UCRI-ILEN         PIC 9(4).
00024  05     FILLER         PIC X(30).
00025  05  UCRI-COMMENT      PIC X(40).
00026  FD  UPRO-PRINTER
00027      BLOCK CONTAINS 0 RECORDS
00028      RECORD CONTAINS 133 CHARACTERS
00029      LABEL RECORDS ARE OMITTED.
00030  01  UPRO-REC.
00031  05     FILLER         PIC X.
00032  05  UPRO-LINE         PIC X(132).
00033  FD  UDAI-DATA
00034      BLOCK CONTAINS 0 RECORDS
00035      RECORD CONTAINS 4 CHARACTERS
00036      LABEL RECORDS ARE STANDARD.
00037  01  UDAI-REC.
00038  05  UDAI-ADDR         PIC S9(8) COMP SYNC.
00039  WORKING-STORAGE SECTION.
00040  01     FILLER.
00041  05     FILLER         PIC X(36) VALUE
00042        'TIMEHIST WORKING-STORAGE STARTS HERE'.
00043  05  WS-CARDS-EOF-FLAG PIC X VALUE SPACES.
00044  88  WS-GOT-A-CARD     VALUE SPACES.
00045  88  WS-CARDS-AT-EOF   VALUE 'E'.
00046 *
00047  05  WS-VALID-FLAG     PIC X VALUE SPACES.
00048  88  WS-INVALID        VALUE 'I'.
00049  88  WS-VALID          VALUE 'V'.
00050 *
00051  05  WS-START          PIC S9(8) COMP SYNC.
00052  05  WS-ILEN           PIC S9(4) COMP SYNC.
00053 *
00054  05  WS-UN-HEX         PIC X(6).
00055  05     FILLER         REDEFINES WS-UN-HEX.
00056  10  WS-UN-HEX-CH      OCCURS 6 TIMES
00057                        INDEXED BY WS-UH-IX
00058                        PIC X.
00059  05  WS-CHAR.
00060  10  WS-CHARU          PIC 9.
00061 *
00062  05  WS-TITLE.
00063  10     FILLER         PIC X(10) VALUE SPACES.
00064  10     FILLER         PIC X(8) VALUE 'TIMEHIST'.
00065  10     FILLER         PIC X(13) VALUE '    INTERVAL='.
00066  10  WS-TITLE-INT      PIC ZZZ9.
00067  10     FILLER         PIC X(11) VALUE SPACES.
00068  10  WS-COMMENT        PIC X(40).
00069  10     FILLER         PIC X(05) VALUE 'MAX ='.
00070  10  WS-TITLE-MAX      PIC ZZ,ZZZ,ZZ9.
00071  10     FILLER         PIC X(10) VALUE ', TOTAL = '.
00072  10  WS-TITLE-TOTAL    PIC ZZ,ZZZ,ZZ9.
00073 *
00074  05  WS-TOTAL          PIC S9(8) COMP SYNC.
00075 *
00076  05  WS-HIGH           PIC S9(8) COMP SYNC.
00077  05  WS-LOW-COUNT      PIC S9(8) COMP SYNC.
00078  05  WS-HIGH-COUNT     PIC S9(8) COMP SYNC.
00079 *
00080  05  WS-WORK-COUNT     PIC S9(8) COMP SYNC.
00081  05     FILLER         REDEFINES WS-WORK-COUNT.
00082  10  WS-WORK-1B        PIC X.
00083  10  WS-WORK-3B        PIC XXX.
00084 *
00085  05  WS-MAX            PIC S9(8) COMP SYNC.
00086 *
00087  05  WS-PRINT-FLAG     PIC X VALUE SPACES.
00088  88  WS-MORE-TO-PRINT  VALUE SPACES.
00089  88  WS-DONE-PRINTING  VALUE 'D'.
00090 *
00091  05  WS-WORK-AST       PIC S9(4) COMP SYNC.
00092 *
00093  05  WS-PRINT-LINE.
00094  10  WS-PL-1ST-HALF    PIC X(66).
00095  10  WS-PL-2ND-HALF    PIC X(66).
00096 *
00097  05  WS-HALF.
00098  10  WS-HALF-FROM      PIC X(6).
00099  10     FILLER         PIC X VALUE '-'.
00100  10  WS-HALF-TO        PIC X(6).
00101  10  WS-HALF-TIMES     PIC Z(8).
00102  10  WS-HALF-AST-F.
00103  15  WS-HA-CH          OCCURS 45 TIMES
00104                        INDEXED BY WS-HA-IX
00105                        PIC X.
00106 *
00107  05  WS-LG-FLAG        PIC X VALUE SPACES.
00108  88  WS-LESS           VALUE 'L'.
00109  88  WS-GTR            VALUE 'G'.
00110 *
00111  05  WS-REM            PIC S9(4) COMP SYNC.
00112 *
00113  05  WS-HEX-TBL        PIC X(16) VALUE '0123456789ABCDEF'.
00114  05     FILLER         REDEFINES WS-HEX-TBL.
00115  10  WS-HEX-CHAR       OCCURS 16 TIMES
00116                        PIC X.
00117 *
00118  05  WS-HEX-ADDR-F.
00119  10  WS-HEX-ADDR-CH    OCCURS 6 TIMES
00120                        INDEXED BY WS-HAC-IX
00121                        PIC X.
00122 *
00123  05  WS-CAPT.
00124  10     FILLER         PIC X(21) VALUE '  FROM-    TO   TIMES'.
00125  10     FILLER         PIC X(45) VALUE SPACES.
00126  10     FILLER         PIC X(21) VALUE '  FROM-    TO   TIMES'.
00127 *
00128  05  WS-PRINT-TABLE.
00129  10  WS-PRINT-ENTRY    OCCURS 100 TIMES
00130                        INDEXED BY WS-PT-IX.
00131  15  WS-FROMX.
00132  20  WS-FROM           PIC S9(8) COMP SYNC.
00133  15  WS-TIMES          PIC S9(8) COMP SYNC.
00134  15  WS-AST-CNT        PIC S9(4) COMP SYNC.
00135  01  WS-TIME-TABLE.
00136  05  WS-TIME-COUNT     OCCURS 32767 TIMES
00137                        INDEXED BY WS-TC-IX
00138                        PIC X(3).
00139  05     FILLER         PIC X(32760).
00140  05     FILLER         PIC X(9).
00141  PROCEDURE DIVISION.
00142  200-START.
00143      OPEN INPUT UCRI-CARD.
00144      OPEN OUTPUT UPRO-PRINTER.
00145      PERFORM 210-PROCESS THRU 210-P-EXIT
00146         UNTIL WS-CARDS-AT-EOF.
00147      CLOSE UPRO-PRINTER.
00148      CLOSE UCRI-CARD.
00149      STOP RUN.
00150 *
00151 *
00152  210-PROCESS.
00153      MOVE SPACES TO WS-VALID-FLAG.
00154      PERFORM 220-AUDIT-CARD THRU 220-AC-EXIT
00155         UNTIL WS-VALID OR WS-CARDS-AT-EOF.
00156      IF WS-GOT-A-CARD
00157         PERFORM 300-FILE-PROCESS THRU 300-FP-EXIT.
00158 *
00159 *
00160  210-P-EXIT.
00161      EXIT.
00162 *
00163 *
00164  220-AUDIT-CARD.
00165      READ UCRI-CARD AT END
00166         MOVE 'E' TO WS-CARDS-EOF-FLAG
00167         GO TO 220-AC-EXIT.
00168      MOVE UCRI-REC TO UPRO-LINE.
00169      WRITE UPRO-REC AFTER ADVANCING TOP-OF-PAGE.
00170      MOVE ZERO TO WS-START.
00171      IF UCRI-START IS NOT EQUAL TO SPACES
00172         MOVE UCRI-START TO WS-UN-HEX
00173         SET WS-UH-IX TO 1
00174         PERFORM 230-HEX-TO-BIN THRU 230-HTB-EXIT 6 TIMES.
00175      IF WS-INVALID
00176         MOVE 'INVALID START FIELD' TO UPRO-LINE
00177         WRITE UPRO-REC AFTER ADVANCING 2 LINES.
00178      EXAMINE UCRI-ILENX REPLACING LEADING SPACES BY ZERO.
00179      IF UCRI-ILEN IS NOT NUMERIC
00180         OR UCRI-ILEN IS EQUAL TO ZERO
00181         MOVE 'INVALID INTERVAL LENGTH' TO UPRO-LINE
00182         WRITE UPRO-REC AFTER ADVANCING 2 LINES
00183         MOVE 'I' TO WS-VALID-FLAG
00184      ELSE
00185         MOVE UCRI-ILEN TO WS-TITLE-INT
00186         MOVE UCRI-ILEN TO WS-ILEN.
00187      IF NOT WS-INVALID
00188         MOVE 'V' TO WS-VALID-FLAG
00189      ELSE
00190         MOVE SPACES TO WS-VALID-FLAG.
00191      MOVE UCRI-COMMENT TO WS-COMMENT.
00192 *
00193 *
00194  220-AC-EXIT.
00195      EXIT.
00196 *
00197 *
00198  230-HEX-TO-BIN.
00199      MOVE WS-UN-HEX-CH (WS-UH-IX) TO WS-CHAR.
00200      MULTIPLY 16 BY WS-START.
00201      IF WS-CHAR IS NUMERIC
00202         ADD WS-CHARU TO WS-START
00203      ELSE
00204         IF WS-CHAR IS EQUAL TO 'A'
00205            ADD 10 TO WS-START
00206      ELSE
00207         IF WS-CHAR IS EQUAL TO 'B'
00208            ADD 11 TO WS-START
00209      ELSE
00210         IF WS-CHAR IS EQUAL TO 'C'
00211            ADD 12 TO WS-START
00212      ELSE
00213         IF WS-CHAR IS EQUAL TO 'D'
00214            ADD 13 TO WS-START
00215      ELSE
00216         IF WS-CHAR IS EQUAL TO 'E'
00217            ADD 14 TO WS-START
00218      ELSE
00219         IF WS-CHAR IS EQUAL TO 'F'
00220            ADD 15 TO WS-START
00221      ELSE
00222         MOVE 'I' TO WS-VALID-FLAG.
00223      SET WS-UH-IX UP BY 1.
00224 *
00225 *
00226  230-HTB-EXIT.
00227      EXIT.
00228 *
00229 *
00230  300-FILE-PROCESS.
00231      OPEN INPUT UDAI-DATA.
00232      MOVE LOW-VALUES TO WS-TIME-TABLE.
00233      MOVE WS-ILEN TO WS-HIGH.
00234      MULTIPLY 43690 BY WS-HIGH.
00235      SUBTRACT 1 FROM WS-HIGH.
00236      ADD WS-START TO WS-HIGH.
00237      MOVE ZERO TO WS-LOW-COUNT, WS-HIGH-COUNT.
00238      MOVE ZERO TO WS-TOTAL.
00239      PERFORM 310-INPUT-DATA THRU 310-ID-EXIT.
00240      CLOSE UDAI-DATA.
00241      MOVE LOW-VALUES TO WS-WORK-1B.
00242      MOVE ZERO TO WS-MAX.
00243      SET WS-TC-IX TO 1.
00244      PERFORM 320-MAX-GET THRU 320-MG-EXIT.
00245      MOVE SPACES TO WS-PRINT-FLAG.
00246      MOVE WS-MAX TO WS-TITLE-MAX.
00247      MOVE WS-TOTAL TO WS-TITLE-TOTAL.
00248      SET WS-TC-IX TO 1.
00249      PERFORM 330-PRINT-ALL THRU 330-PA-EXIT
00250         UNTIL WS-DONE-PRINTING.
00251 *
00252 *
00253  300-FP-EXIT.
00254      EXIT.
00255 *
00256 *
00257  310-INPUT-DATA.
00258      READ UDAI-DATA AT END
00259         GO TO 310-ID-EXIT.
00260      ADD 1 TO WS-TOTAL.
00261      IF UDAI-ADDR IS LESS THAN WS-START
00262         ADD 1 TO WS-LOW-COUNT
00263      ELSE
00264         IF UDAI-ADDR IS IS GREATER THAN WS-HIGH
00265            ADD 1 TO WS-HIGH-COUNT
00266         ELSE
00267            SUBTRACT WS-START FROM UDAI-ADDR
00268            DIVIDE WS-ILEN INTO UDAI-ADDR
00269            ADD 1 TO UDAI-ADDR
00270            MOVE LOW-VALUES TO WS-WORK-1B
00271            MOVE WS-TIME-COUNT (UDAI-ADDR) TO WS-WORK-3B
00272            ADD 1 TO WS-WORK-COUNT
00273            MOVE WS-WORK-3B TO WS-TIME-COUNT (UDAI-ADDR).
00274      GO TO 310-INPUT-DATA.
00275 *
00276 *
00277  310-ID-EXIT.
00278      EXIT.
00279 *
00280 *
00281  320-MAX-GET.
00282      IF WS-TIME-COUNT (WS-TC-IX) IS GREATER THAN WS-WORK-3B
00283         MOVE WS-TIME-COUNT (WS-TC-IX) TO WS-WORK-3B.
00284      SET WS-TC-IX UP BY 1.
00285      IF WS-TC-IX IS LESS THAN 43691
00286         GO TO 320-MAX-GET.
00287      MOVE WS-WORK-COUNT TO WS-MAX.
00288 *
00289 *
00290  320-MG-EXIT.
00291      EXIT.
00292 *
00293 *
00294  330-PRINT-ALL.
00295      IF WS-TC-IX IS NOT LESS THAN 43691
00296         MOVE 'D' TO WS-PRINT-FLAG
00297         GO TO 330-PA-EXIT.
00298      SET WS-PT-IX TO 1.
00299      MOVE LOW-VALUES TO WS-PRINT-TABLE.
00300      PERFORM 340-FILL-TABLE THRU 340-FT-EXIT
00301      PERFORM 350-PRINT-TABLE THRU 350-PT-EXIT.
00302 *
00303 *
00304  330-PA-EXIT.
00305      EXIT.
00306 *
00307 *
00308  340-FILL-TABLE.
00309      IF WS-TC-IX IS EQUAL TO 1
00310         MOVE 'LESS' TO WS-FROMX (WS-PT-IX)
00311         MOVE WS-LOW-COUNT TO WS-TIMES (WS-PT-IX)
00312         MOVE ZERO TO WS-WORK-AST
00313         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)
00314         SET WS-PT-IX UP BY 1.
00315      IF WS-TC-IX IS EQUAL TO 43691
00316         MOVE 'GTR ' TO WS-FROMX (WS-PT-IX)
00317         MOVE WS-HIGH-COUNT TO WS-TIMES (WS-PT-IX)
00318         MOVE ZERO TO WS-WORK-AST
00319         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)
00320         SET WS-PT-IX UP BY 1
00321         GO TO 340-FT-EXIT.
00322      IF WS-TIME-COUNT (WS-TC-IX) IS NOT EQUAL TO LOW-VALUES
00323         SET WS-WORK-COUNT TO WS-TC-IX
00324         SUBTRACT 1 FROM WS-WORK-COUNT
00325         MULTIPLY WS-ILEN BY WS-WORK-COUNT
00326         ADD WS-START TO WS-WORK-COUNT
00327         MOVE WS-WORK-COUNT TO WS-FROM (WS-PT-IX)
00328         MOVE WS-TIME-COUNT (WS-TC-IX) TO WS-WORK-3B
00329         MOVE LOW-VALUES TO WS-WORK-1B
00330         MOVE WS-WORK-COUNT TO WS-TIMES (WS-PT-IX)
00331         COMPUTE WS-WORK-AST = (WS-WORK-COUNT * 45) / WS-MAX
00332         MOVE WS-WORK-AST TO WS-AST-CNT (WS-PT-IX)
00333         SET WS-PT-IX UP BY 1.
00334      SET WS-TC-IX UP BY 1.
00335      IF WS-PT-IX IS LESS THAN 101
00336         AND WS-TC-IX IS NOT GREATER THAN 43691
00337         GO TO 340-FILL-TABLE.
00338 *
00339 *
00340  340-FT-EXIT.
00341      EXIT.
00342 *
00343 *
00344 *
00345  350-PRINT-TABLE.
00346      MOVE WS-TITLE TO UPRO-LINE.
00347      WRITE UPRO-REC AFTER ADVANCING TOP-OF-PAGE.
00348      MOVE WS-CAPT TO UPRO-LINE.
00349      WRITE UPRO-REC AFTER ADVANCING 3 LINES.
00350      SET WS-PT-IX TO 1.
00351 *
00352  350-LOOP.
00353      PERFORM 360-BUILD-HALF THRU 360-BH-EXIT.
00354      MOVE WS-HALF TO WS-PL-1ST-HALF.
00355      SET WS-PT-IX UP BY 50.
00356      PERFORM 360-BUILD-HALF THRU 360-BH-EXIT.
00357      MOVE WS-HALF TO WS-PL-2ND-HALF.
00358      MOVE WS-PRINT-LINE TO UPRO-LINE.
00359      WRITE UPRO-REC AFTER ADVANCING 1 LINES.
00360      SET WS-PT-IX DOWN BY 49.
00361      IF WS-PT-IX IS NOT EQUAL TO 51
00362         GO TO 350-LOOP.
00363 *
00364 *
00365  350-PT-EXIT.
00366      EXIT.
00367 *
00368 *
00369  360-BUILD-HALF.
00370      IF WS-TIMES (WS-PT-IX) IS EQUAL TO ZERO
00371         MOVE SPACES TO WS-HALF
00372         GO TO 360-BH-EXIT.
00373      MOVE SPACES TO WS-LG-FLAG.
00374      IF WS-FROMX (WS-PT-IX) IS EQUAL TO 'LESS'
00375         MOVE 'L' TO WS-LG-FLAG
00376         MOVE WS-START TO WS-FROM (WS-PT-IX)
00377         ADD 1 TO WS-FROM (WS-PT-IX)
00378         SUBTRACT WS-ILEN FROM WS-FROM (WS-PT-IX).
00379      IF WS-FROMX (WS-PT-IX) IS EQUAL TO 'GTR '
00380         MOVE 'G' TO WS-LG-FLAG
00381         MOVE WS-ILEN TO WS-WORK-COUNT
00382         MULTIPLY 43690 BY WS-WORK-COUNT
00383         ADD WS-START TO WS-WORK-COUNT
00384         MOVE WS-WORK-COUNT TO WS-FROM (WS-PT-IX).
00385      MOVE '      -' TO WS-HALF.
00386      MOVE WS-FROM (WS-PT-IX) TO WS-WORK-COUNT.
00387      PERFORM 370-CVT-TO-HEX THRU 370-CTH-EXIT.
00388      MOVE WS-HEX-ADDR-F TO WS-HALF-FROM.
00389      MOVE WS-FROM (WS-PT-IX) TO WS-WORK-COUNT.
00390      ADD WS-ILEN TO WS-WORK-COUNT.
00391      SUBTRACT 1 FROM WS-WORK-COUNT.
00392      PERFORM 370-CVT-TO-HEX THRU 370-CTH-EXIT.
00393      MOVE WS-HEX-ADDR-F TO WS-HALF-TO.
00394      MOVE WS-TIMES (WS-PT-IX) TO WS-HALF-TIMES.
00395      IF WS-LESS
00396         MOVE 'LESS' TO WS-HALF-FROM.
00397      IF WS-GTR
00398         MOVE 'GTR' TO WS-HALF-TO.
00399      SET WS-HA-IX TO 1.
00400  360-LOOP.
00401      IF WS-HA-IX IS NOT GREATER THAN WS-AST-CNT (WS-PT-IX)
00402         MOVE '*' TO WS-HA-CH (WS-HA-IX)
00403         SET WS-HA-IX UP BY 1
00404         GO TO 360-LOOP.
00405 *
00406 *
00407  360-BH-EXIT.
00408      EXIT.
00409 *
00410 *
00411  370-CVT-TO-HEX.
00412      SET WS-HAC-IX TO 6.
00413  370-LOOP.
00414      DIVIDE 16 INTO WS-WORK-COUNT GIVING WS-WORK-COUNT
00415         REMAINDER WS-REM.
00416      ADD 1 TO WS-REM.
00417      MOVE WS-HEX-CHAR (WS-REM) TO WS-HEX-ADDR-CH (WS-HAC-IX).
00418      IF WS-HAC-IX IS NOT EQUAL TO 1
00419         SET WS-HAC-IX DOWN BY 1
00420         GO TO 370-LOOP.
00421 *
00422 *
00423  370-CTH-EXIT.
00424      EXIT.
00425 *
00426 *
