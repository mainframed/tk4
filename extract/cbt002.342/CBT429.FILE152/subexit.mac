         MACRO
&NAME    SUBEXIT &RC=0,&RENT=NO,&RWALNG=0
.*             MACRO-ID. 'SUBEXIT'.
.*             AUTHOR.   DON HIGGINS.
.*             DATE WRITTEN.  07/28/70.
.*             REMARKS.  THIS MACRO GENERATES THE CODE TO RESTORE
.*                       REGISTERS AND RETURN FROM A CSECT.  A RETURN
.*                       CODE OF ZERO IS RETURNED UNLESS A DIFFERENT
.*                       CODE IS SPECIFIED USING THE KEY WORD RC=.
.*             REV1 11/14/74 ADD REENTRANT OPTION. SEE SUBENTRY.
         AIF   ('&RENT'(1,1) EQ 'N').NORENT
.RENT    ANOP
&NAME    L     13,4(13)
         STM   0,1,20(13)
         L     1,8(13)
         LA    0,72+&RWALNG
         FREEMAIN R,LV=(0),A=(1)
         LM    14,12,12(13)
         LA    15,&RC
         BR    14
         AGO   .EXIT
.NORENT  ANOP
&NAME    L     13,4(13)
         L     14,12(13)
         LM    2,12,28(13)
         LA    15,&RC
         BR    14
.EXIT    ANOP
         MEND
