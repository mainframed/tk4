//HPZ$F   JOB ,ZBIEGIEN,MSGCLASS=W,CLASS=D
/*JOBPARM ROOM=3426
//* READ A SINGLE TRANSLATE MEMBER AND FORMAT OUT ITS CONTENTS
//A EXEC SAS@
//OUT DD SYSOUT=W
//TRANS DD DSN=SYS1.FONTLIB(T1D0BASE),DISP=SHR  INPUT
OPTIONS GEN=0;
 /* PRINT OUT INFO ABOUT THE TRANSLATE TABLES */;
DATA _NULL_;
RETAIN CODES 0;
FILE OUT PRINT;
INFILE TRANS STOPOVER;
INPUT @4 ID $CHAR3. @ /* READ RECORD ID */;
IF ID ='D3A887'X OR ID= 'D3A987'X THEN RETURN; /* START AND END */;
IF ID='D3EEEE'X THEN DO /* NOOP RECORD */;
 INPUT @2 LEN PIB2. @;
 LEN=LEN-8;
 INPUT @36 TBLID $CHAR8.
       @10 COMMT $VARYING101. LEN;  /* PICK UP COMMENT /*;
 PUT _PAGE_ ' TRANSLATE TABLE: ' TBLID / / COMMT / ;
 RETURN;
END;
IF ID='D3A687'X THEN DO /* CODE PAGE DESCRIPTOR */;
 INPUT @ 10 CPD $CHAR32.
       @46 CODES PIB2.;
 PUT 'DESCRIPTOR: ' CPD ' CODEPOINTS: ' CODES / ;
END;
IF ID='D3A787'X THEN DO /* CODE PAGE CONTROL */;
 INPUT @10 DEFGRAPH $CHAR8.
           FLG1 $CHAR1.
       @22 FLG2 $CHAR1.;
 IF FLG1 ='0... ....'B THEN XVALID='YES';
                       ELSE XVALID=' NO';
 IF FLG1 ='.0.. ....'B THEN XPRINT='YES';
                       ELSE XPRINT=' NO';
 IF FLG1 ='..0. ....'B THEN XINCR='YES';
                       ELSE XINCR=' NO';
 IF FLG2 ='.... 0...'B THEN XVB='VARIABLE SPACING NOT ENABLED';
                       ELSE XVB='VARIABLE SPACING ENABLED    ';
 PUT 'DEFAULT CHARACTER:' DEFGRAPH 'VALID='    XVALID 'INCREMENT='
        XINCR 'PRINT='    XPRINT  +1 XVB /;
 PUT @001 'CHARACTER CODE VALID INCR PRINT'  /* HEADING LINE */
     @034 'CHARACTER CODE VALID INCR PRINT'
     @067 'CHARACTER CODE VALID INCR PRINT'
     @100 'CHARACTER CODE VALID INCR PRINT';
END;
IF ID='D38C87'X THEN DO /* CODE PAGE INDEX */;
 X=0;
 DO I=0 TO (CODES-1);
  PT=10+(I*10);
  INPUT @PT        CHARID $CHAR8.  /* CHARACTER NAME */
                   CFLAG $CHAR1.
                   CODEPNT $CHAR1. @; /* HEX VALUE */
  IF CFLAG='0... ....'B THEN XVAL='YES';
                        ELSE XVAL=' NO';
  IF CFLAG='.0.. ....'B THEN XINCR='YES';
                        ELSE XINCR=' NO';
  IF CFLAG='..0. ....'B THEN XPRNT='YES';
                        ELSE XPRNT=' NO';
  PT2=X*33+1;
  PUT @PT2    CHARID +3 CODEPNT $HEX2. +2 XVAL +1 XINCR +2 XPRNT @;
  IF X=3 THEN DO; PUT; X=0; END; ELSE X=X+1;
 END;
END;
