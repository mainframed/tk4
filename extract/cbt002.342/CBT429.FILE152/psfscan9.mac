//HPZ$9   JOB ,ZBIEGIEN,MSGCLASS=W,CLASS=3
/*JOBPARM ROOM=3426
//* MAP OUT ALL INFO ON A CHARACTER SET INCLUDING THE RASTER
//* PATTERN
//A EXEC SAS@
//OUT DD SYSOUT=W
//CHARS DD DSN=AGPP.FONTLIB(C1BAR050),DISP=SHR
OPTIONS GEN=0;
 /* PRINT OUT INFO ABOUT THE CHARACTER SETS AND THE RASTER PATTERN */;
PROC FORMAT;
 VALUE $BIN (MAX=4)
  '0' ='....'
  '1' ='...*'
  '2' ='..*.'
  '3' ='..**'
  '4' ='.*..'
  '5' ='.*.*'
  '6' ='.**.'
  '7' ='.***'
  '8' ='*...'
  '9' ='*..*'
  'A' ='*.*.'
  'B' ='*.**'
  'C' ='**..'
  'D' ='**.*'
  'E' ='***.'
  'F' ='****';
DATA _NULL_;
RETAIN CODES 0;
FILE OUT PRINT;
INFILE CHARS STOPOVER;
INPUT @4 ID $CHAR3. @ /* READ ID */;
IF ID ='D3A889'X OR ID= 'D3A989'X THEN RETURN; /*START AND END */;
IF ID='D3EEEE'X THEN DO /* NOOP RECORD */;
 INPUT @2 LEN PIB2. @;
 LEN=LEN-8;
 INPUT @36 SETNAME $CHAR8.
       @10 COMMT $VARYING132. LEN; /* COMMENT */;
 PUT _PAGE_ ' CHARACTER SET NAME: ' SETNAME / / COMMT / ;
 RETURN;
END;
IF ID='D3A689'X THEN DO /* FONT DESCRIPTOR */;
 INPUT @ 10 TYPEFACE $CHAR32.
            WEIGHT $CHAR1.
            WIDTH $CHAR1.
         +2 POINTS PIB2.1
        +10 DIRECTN $CHAR2.
        +4  ROTATN  $CHAR2.
        +8 FLG $CHAR1.
        +4 UWIDTH IB2.
           UPOS   IB2.;
 IF DIRECTN='0000'X THEN XDIR='ACROSS';
 ELSE IF DIRECTN='2D00'X THEN XDIR='DOWN  ';
                         ELSE XDIR='UP    ';
 IF      ROTATN='0000'X THEN XROT='  0';
 ELSE IF ROTATN='2D00'X THEN XROT=' 90';
 ELSE IF ROTATN='5A00'X THEN XROT='180';
 ELSE                        XROT='270';
 IF FLG='0... ....'B THEN ITALIC=' NO';
                     ELSE ITALIC='YES';
 IF FLG='.0.. ....'B THEN UNDERS=' NO';
                     ELSE UNDERS='YES';
 IF FLG='.... 0...'B THEN OVERS=' NO';
                     ELSE OVERS='YES';
 IF      WEIGHT='01'X THEN XWEIGHT='ULTRA-LIGHT';
 ELSE IF WEIGHT='02'X THEN XWEIGHT='EXTRA-LIGHT';
 ELSE IF WEIGHT='03'X THEN XWEIGHT='LIGHT      ';
 ELSE IF WEIGHT='04'X THEN XWEIGHT='SEMI-LIGHT ';
 ELSE IF WEIGHT='05'X THEN XWEIGHT='MEDIUM     ';
 ELSE IF WEIGHT='06'X THEN XWEIGHT='SEMI-BOLD  ';
 ELSE IF WEIGHT='07'X THEN XWEIGHT='BOLD       ';
 ELSE IF WEIGHT='08'X THEN XWEIGHT='EXTRA-BOLD ';
 ELSE IF WEIGHT='09'X THEN XWEIGHT='ULTRA-BOLD ';
 IF      WIDTH= '01'X THEN XWIDTH='ULTRA-CONDENSED';
 ELSE IF WIDTH= '02'X THEN XWIDTH='EXTRA-CONDENSED';
 ELSE IF WIDTH= '03'X THEN XWIDTH='CONDENSED      ';
 ELSE IF WIDTH= '04'X THEN XWIDTH='SEMI-CONDENSED ';
 ELSE IF WIDTH= '05'X THEN XWIDTH='MEDIUM         ';
 ELSE IF WIDTH= '06'X THEN XWIDTH='SEMI-EXPANDED  ';
 ELSE IF WIDTH= '07'X THEN XWIDTH='EXPANDED       ';
 ELSE IF WIDTH= '08'X THEN XWIDTH='EXTRA-EXPANDED ';
 ELSE IF WIDTH= '09'X THEN XWIDTH='ULTRA-EXPANDED ';
 PUT 'WEIGHT:' XWEIGHT '  WIDTH:' XWIDTH '  POINTSIZE:' POINTS
     '  PRINT DIRECTION:' XDIR '  CHARACTER ROTATION:' XROT /
     '  ITALIC:' ITALIC '  UNDERSCORE:' UNDERS '  OVERSTRIKE:' OVERS;
END;
IF ID='D3AC89'X THEN DO /* FONT CHARACTER POSITION */;
 INPUT @12 LCASEH IB2.
           UCASEH IB2.
           MASCEND   IB2.
           MDESCEND  IB2.;
 PUT 'LOWERCASE HEIGTH:' LCASEH '  UPPER CASE HEIGTH:' UCASEH
     '  MAXIMUM ASCENDER:' MASCEND '  MAXIMUM DESCENDER:' MDESCEND;
END;
IF ID='D3A789'X THEN DO /* FONT CONTROL */;
 INPUT @12 CID $CHAR1.
           FLG $CHAR1.
        +6 MAXBOXW IB2.
           MAXBOXH IB2.
        +3 PATTRNC IB3.
           INLNOFF IB2.;
 RETAIN PATTRNC;
 IF CID ='00'X THEN PUT'SINGLE BYTE FONT';
 ELSE PUT 'NOT A SINGLE BYTE FONT, THIS PROGRAM CAN NOT HANDLE THIS';
 IF FLG='.... .0..'B THEN KERNING='NOT ALLOWED';
                     ELSE KERNING='ALLOWED    ';
 IF FLG='.... ..0.'B THEN BOXSIZE='VARIABLE';
                     ELSE BOXSIZE='UNIFORM ';
 PUT 'KERNING:' KERNING '  BOX SIZE:' BOXSIZE
   '  MAXIMUM BOX SIZE WIDTH:'
   MAXBOXW '  HEIGTH:' MAXBOXH / 'PATTERN DATA COUNT:' PATTRNC
   '  UNIFORM INLINE OFFSET:' INLNOFF;
END;
IF ID='D3AE89'X THEN DO /* FONT ORIENTATON */;
 INPUT @10 TEXTOR $CHAR4.
           UBASEOFF IB2.
           MAXCHARI IB2.
           VARINCR  IB2.
           MAXBASEE IB2.
           FLG      $CHAR1.
       +1  EMSPACE  IB2.
       +2  FIGSPACE IB2.
       +2  BASEINCR IB2.;
 IF FLG='.... ...0'B THEN XVAR='EACH CHARACTER HAS OWN INCREMENT     ';
                     ELSE XVAR='UNIFORM CHAR INCREMENT APPLIES TO ALL';
 IF      TEXTOR='00002D00'X THEN XTEXT='  0, 90';
 ELSE IF TEXTOR='2D005A00'X THEN XTEXT=' 90,180';
 ELSE                            XTEXT='270,  0';
 PUT 'TEXT ORIENTATION:' XTEXT '  UNIFORM BASELINE OFFSET:' UBASEOFF
  '  MAXIMUM CHARACTER INCREMENT:' MAXCHARI
  '  VARIABLE SPACE INCREMENT:'
  VARINCR / 'MAXIMUM BASELINE EXTENT:' MAXBASEE +1 XVAR '  EM SPACE:'
  EMSPACE / 'FIGURE SPACE:' FIGSPACE '  DEFAULT BASELINE INCREMENT:'
     BASEINCR @;
END;
IF ID='D38C89'X THEN DO /* FONT INDEX */;
 INPUT @2 LEN IB2. @;
 CHARS= ((LEN-8)/24)   /* TAKE OFF 8 BYTE HEADER, EACH ENTRY 24 BYTES
                        */;
 PUT @60 'CHARACTERS:'CHARS;
 PUT / 'CHAR-ID INCR  W    H   A   B   C'
  @ 34 'CHAR-ID INCR  W    H   A   B   C'
  @ 67 'CHAR-ID INCR  W    H   A   B   C'
 @ 100 'CHAR-ID INCR  W    H   A   B   C';
 PT3=0;
 ARRAY VADDS(X) AADD1-AADD256;  /* ADDRESS */;
 ARRAY    VA(X)   AA1-AA256; /* A SPACE */;
 ARRAY    VB(X)   AB1-AB256; /* B SPACE */;
 ARRAY    VC(X)   AC1-AC256; /* C SPACE */;
 ARRAY    VH(X)   AH1-AH256; /* HEIGHT  */;
 ARRAY    VW(X)   AW1-AW256; /* WIDTH   */;
 ARRAY VINCR(X) AINCR1-AINCR256; /* INCREMENT */;
 ARRAY VNAME(X) $ 8 ANAME1-ANAME256; /* CHARACTER NAME */;
 RETAIN AADD1-AADD256;
 RETAIN   AA1-AA256;
 RETAIN   AB1-AB256;
 RETAIN   AC1-AC256;
 RETAIN   AH1-AH256;
 RETAIN   AW1-AW256;
 RETAIN AINCR1-AINCR256;
 RETAIN ANAME1-ANAME256;
 RETAIN CHARS;
 DO I=0 TO (CHARS-1);
  PT=10+(I*24);
  INPUT @PT        CHARID $CHAR8.
                   CINCR IB2.
                   CWIDTH IB2.
                   CHEIGTH IB2.
                   CADD   IB4.
                   ASPACE IB2.
                   BSPACE IB2.
                   CSPACE IB2. @;
  X=I+1; VADDS=CADD*8; /* ADDRESS IS MULTIPLE OF 8 IN THE RECORD */;
  VA=ASPACE;VB=BSPACE;VC=CSPACE;VINCR=CINCR;VW=CWIDTH;VH=CHEIGTH;
  VNAME=CHARID;
  PT2=PT3*33+1;
  PUT @PT2    CHARID    CINCR 3. +1  CWIDTH 3. +1 CHEIGTH 3.
    +1  ASPACE 3. +1 BSPACE 3. +1 CSPACE 3. @;
  IF PT3=3 THEN DO; PUT; PT3=0; END; ELSE PT3=PT3+1;
 END;
 RETAIN CHARP;
 CHARP=0; /* CHARACTER COUNTER OR POINTER */;
END;
IF ID='D3EE89'X THEN DO /* FONT PATTERN RECORDS */;
 INPUT @2 LEN PIB2. @;
 LEN=LEN+1;TBYTES=0;
 PT=10;/* GET NEXT CHARACTER INFO, START LOOP */;
 DO UNTIL(PT>10000) /* THIS IS A NEVER ENDING LOOP */;
  CHARP=CHARP+1;IF CHARP>CHARS THEN RETURN;X=CHARP;
  WBYTES=(VW+1)/8;
  TBYTES=(WBYTES*(VH+1));
  RTBYTES=8*CEIL(TBYTES/8) /* ROUND UP TO 8 BYTE BOUNDARY */;
  TPT=2048*(CEIL((PT-10)/2048)); /* 2K PAGES */;
  IF (PT-10+RTBYTES)>TPT THEN PT=TPT+10;
  /* HAS TO FIT IN 2048 BYTE BLOCKS */;
  PUT 'ADD=' VADDS ', TOTAL BYTES=' TBYTES ', ROUNDED TOAL BYTES='
    RTBYTES ' ,POINTER=' PT ', 2048 BOUNDARY POINTER=' TPT
    ', CHARACTER =' CHARP;
     X=X+1;
     IF X>CHARS THEN AD=PATTRNC;ELSE AD=VADDS;
     X=X-1;
     DIFF=AD-VADDS; IF DIFF NE RTBYTES THEN PUT '******<====';
     PUT DIFF;IF CHARP NE CHARS THEN RTBYTES=DIFF;
 /* PREV 2 LINES PROGRAM DIAGNOSTIC DATA */;
  IF (PT-10+RTBYTES)>LEN THEN DO;
   CHARP=CHARP-1;
   RETURN;
  END;
    PUT _PAGE_ VNAME '  W=' VW  '  H=' VH '  A=' VA
        '  B=' VB  '  C=' VC   / /; SEQ=0;
  DO K=0 TO VH BY 1; /* START LOOP OVER HEIGHT */;
   XPATTRN='                '; /* INITIALIZE*/;
   DO L=0 TO (WBYTES-1) BY 1;  /* WIDTH OF RASTER IN BYTES */;
    INPUT @PT XC $CHAR1. @;
    SUBSTR(XPATTRN,L+1,1)=XC;
    PT=PT+1;
   END;
   /* FORMAT PRINT LINE */;
   DO;
    LENGTH XHEX $ 32;
    LENGTH XPRTLN $ 128;
    XHEX=PUT(XPATTRN,$HEX32.); /* TEMP FORMAT IN HEX */;
    XPRTLN=REPEAT(' ',128);    /* INITIALIZE RESULT */;
    DO J=1 TO ((VW+1)/4);      /* LOOP ACROSS THE PRINT LINE */;
     XC=SUBSTR(XHEX,J,1);    /* CONVERT HEX TO DOT-ASTERICK PATTERN*/;
     SUBSTR(XPRTLN,((J-1)*4+1),4)=PUT(XC,$BIN.);
    END;
    PUT XPRTLN;  /* PRINT ONE LINE OF PATTERN */;
   END;
   SEQ=SEQ+1;
  END;
  PT=PT+RTBYTES-TBYTES;
 END;
END;
