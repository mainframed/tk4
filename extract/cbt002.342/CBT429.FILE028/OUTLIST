00010000PROC 0 LIST SYMLIST CONLIST INPUT(OUTLIST) OUTPUT(OUTLWK) +
00020001       VOL(PL8608) DSN(A.CNTL) TAPE(TAPE) JNAM(TST2#SG$)  +
00030001       PFX1(TST2SSG) PFX2(PUT8608)
00040000 
00050003/* THIS CLIST BACKS TO TAPE MULTIPLE SYSOUTS HAVING THE SAME JOB NAME
00060003/*  "JNAM ", WHICH ARE IN A TSO-HELD OUTPUT CLASS.
00070002/*
00080002/* THIS CLIST TAKES AS INPUT A LIST OF JOB NUMBERS, AND GENERATES
00090002/* ON "OUTPUT" A JOBSTREAM TO DUMP THE JOBS VIA THE OUTPUT TSO COMMAND
00100002/* TO AN SL TAPE THAT HAS BEEN PREVIOUSLY INITIALIZED.
00110002/*
00120003/* JOB NUMBERS MUST BE IN COLUMNS 1 TO 4, AND LEFT-PADDED WITH ZEROS.
00130003/*
00140003/* AN ASTERISK IN COLUMN 1 CAUSES THE CARD TO BE TREATED AS COMMENT.
00150003/*
00160003/* THE INPUT CARD IS ASSUMED TO BE A MEMBER "OUTLIST" IN A DATASET
00170003/* PREFIX.A.CNTL .  THE OUTPUT IS WRITTEN TO A MEMBER "OUTLWK" IN
00180003/* THAT DATASET.
00190003/*
00200002/*   THE JOB NUMBERS CAN BE GENERATED USING THE TSO STATUS COMMAND.
00210000 
00220000IF &SYMLIST ^= THEN CONTROL SYM
00230000IF &CONLIST ^= THEN CONTROL CON
00240000IF &LIST    ^= THEN CONTROL LIST
00250000ALLOC F(IN) DA(&DSN(&INPUT)) SH  REU
00260000OPENFILE IN INPUT
00270000/*              12345678901234567890  */
00280001SET &J = &STR(J)
00290001SET &DSNS =
00300000SET &NUMDSNS = 0
00310000SET &EOFG = 0
00320000DO WHILE &EOFG = 0
00330000    ERROR DO
00340000        WRITE
00350000        WRITE END OF FILE ON &DSN(&INPUT).
00360000        CLOSFILE IN
00370000        FREE F(IN)
00380000        SET &EOFG = 1
00390000        RETURN
00400000    END
00410000    GETFILE IN
00420000    ERROR OFF
00430000    IF &EOFG = 0 THEN DO
00440000        IF &SUBSTR(1,&STR(&IN)) ^= &STR(*) THEN DO
00450000            SET &DSNS = &STR(&DSNS&SUBSTR(1:4,&STR(&IN))
00460000            SET &NUMDSNS = &NUMDSNS + 1
00470000        END
00480000        ELSE DO
00490000            SET &SYSDVAL = &SUBSTR(2:4,&STR(&IN))
00500000            READDVAL IN
00510000            WRITE JOB NUMBER &IN WILL NOT BE BACKED UP.
00520000        END
00530000    END
00540000END
00550000IF &EOFG = 0 THEN DO
00560000    WRITE
00570000    WRITE &DSN(&INPUT) IS EMPTY.  NO JCL GENERATED.
00580000    WRITE
00590000END
00600000ELSE DO
00610000    WRITE GENERATING JCL IN &DSN(&OUTPUT).
00620000    ALLOC F(OUT) DA(&DSN(&OUTPUT)) SH  REU
00630000    OPENFILE OUT OUTPUT
00640000    /*              12345678901234567890
00650000    SET &OUT = &STR(//TST2OP2L JOB ,'TECH.SUPP-SAM.GOLOB',CLASS=M,NOTIFY=TST2SSG,)
00660000    PUTFILE OUT
00670000    SET &OUT = &STR(// MSGLEVEL=(1,1),MSGCLASS=T TYPRUN=HOLD               )
00680000    PUTFILE OUT
00690000    SET &OUT = &STR(//*                                                    )
00700000    PUTFILE OUT
00710000    SET &OUT = &STR(//**************************************************//)
00720000    PUTFILE OUT
00730000    SET &OUT = &STR(//*                                                *// )
00740000    PUTFILE OUT
00750000    SET &OUT = &STR(//* GENERATED JCL FOR OUTPUT DUMP OF SMP JOBS.     *// )
00760000    PUTFILE OUT
00770000    SET &OUT = &STR(//*                                                *// )
00780000    PUTFILE OUT
00790000    SET &OUT = &STR(//**************************************************//)
00800000    PUTFILE OUT
00810000    SET &OUT = &STR(//*                   )
00820000    PUTFILE OUT
00830001    SET &OUT = &STR(//LIST    EXEC PGM=IKJEFT01,DYNAMNBR=50,REGION=4096K)
00840000    PUTFILE OUT
00850000    SET &OUT = &STR(//STEPLIB  DD  DSN=TST.TSO.CMDLIB,DISP=SHR     )
00860000    PUTFILE OUT
00870001    SET &OUT=  &STR(//SYSPRINT DD  SYSOUT=*)
00880001    PUTFILE OUT
00890001    SET &OUT=  &STR(//SYSTSPRT DD  SYSOUT=*)
00900001    PUTFILE OUT
00910001    SET &OUT=  &STR(//SYSTERM  DD  SYSOUT=*)
00920001    PUTFILE OUT
00930001    SET &IX = 1
00940001    SET &IZ = 1
00950001    SET &L = &LENGTH(&STR(&DSNS))
00960001    DO WHILE &IX <= &L
00970001        SET &IY = &IX + 3
00980001        SET &IA = &IZ - 1
00990001        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
01000001        READDVAL IN
01010001        SET &OUT = &STR(//OUTP&IZ  DD  DSN=&PFX1..&PFX2..&J&SYSDVAL,DISP=(,PASS), )
01020001        PUTFILE OUT
01030001        SET &OUT = &STR(//        LABEL=(&IZ,SL,EXPDT=98000),UNIT=&TAPE,)
01040001        PUTFILE OUT
01050001        SET &OUT = &STR(//        VOL=(,RETAIN,SER=&VOL),)
01060001        PUTFILE OUT
01070001        SET &OUT = &STR(//        DCB=(RECFM=FBA,LRECL=133,BLKSIZE=3059)      )
01080001        PUTFILE OUT
01090001        SET &IX = &IX + 4
01100001        SET &IZ = &IZ + 1
01110001    END
01120000    SET &OUT = &STR(//SYSTSIN  DD  DATA,DLM='/$')
01130000    PUTFILE OUT
01140000    SET &GROOT = &STR(/)&STR(*)
01150000    SET &OUT = &STR(&GROOT THIS JCL CREATED ON &SYSDATE AT &SYSTIME..)
01160000    PUTFILE OUT
01170000    SET &L = &LENGTH(&STR(&DSNS))
01180000    SET &IX = 1
01190000    DO WHILE &IX <= &L
01200000        SET &IY = &IX + 3
01210000        SET &IA = &IZ - 1
01220000        SET &SYSDVAL = &SUBSTR(&IX:&IY,&STR(&DSNS))
01230000        READDVAL IN
01240000        /*              12345678901234567890
01250001        SET &OUT = &STR( OUT &JNAM(&J&SYSDVAL) HOLD KEEP PRINT('&PFX1..&PFX2..&J&SYSDVAL')
01260000        PUTFILE OUT
01270000        SET &IX = &IX + 4
01280000    END
01290001    SET &OUT = &STR(/$                          )
01300001    PUTFILE OUT
01310000    SET &OUT = &STR(//RESET   EXEC PGM=IEFBR14,REGION=768K)
01320000    PUTFILE OUT
01330000    SET &OUT = &STR(//MAP     EXEC PGM=TAPEMAP,REGION=768K)
01340000    PUTFILE OUT
01350000    SET &OUT = &STR(//SYSPRINT DD  SYSOUT=*)
01360000    PUTFILE OUT
01370000    SET &OUT = &STR(//SYSPRNT2 DD  SYSOUT=*)
01380000    PUTFILE OUT
01390000    SET &OUT = &STR(//SYSUT1   DD  UNIT=&TAPE,DISP=OLD,   )
01400000    PUTFILE OUT
01410000    SET &OUT = &STR(//     VOL=(,RETAIN,,,SER=&VOL),     )
01420000    PUTFILE OUT
01430000    SET &OUT = &STR(//     LABEL=(1,BLP,EXPDT=98000)                )
01440000    PUTFILE OUT
01450000    CLOSFILE OUT
01460000    FREE F(OUT)
01470000END
