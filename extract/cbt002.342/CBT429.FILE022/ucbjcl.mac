 TITLE '       WRITE ONLINE-DASD-NON/STORAGE-UCB''S TO SYSOUT DATASET'
UCBJCL    CSECT
          STM    R14,R12,12(R13)       * SETUP
          LR     R12,R15
          USING  UCBJCL,R12,R11       * R12, AND R11 BASE REGS
          B      *+24
          DC     C'&SYSDATE_&SYSTIME_V1.0'
          L      R11,BASE2
          LA     R10,SAVE
          ST     R10,8(R13)
          ST     R13,SAVE+4
          LR     R13,R10
          B      START
BASE2     DC     A(UCBJCL+4096)
BALREG    EQU    14
CVTADR    EQU    2
UCBADR    EQU    3
UCBEXT1   EQU    4
START     DS     0H
$READJCB  DS     0H
          RDJFCB SYSOUT                * DDNAME ALLOCATED ?
          LTR    R15,R15               * TEST RESULTS
          BNZ    $ERROR1               * NOPE, DO TPUTS
          OPEN   (SYSOUT,OUTPUT)       * OPEN SYSOUT FILE
          TESTOPEN SYSOUT              * IS DDNAME THERE
          LTR    R15,R15               * TEST OPEN
          BNZ    $ERROR1               * NOPE, DO TPUTS
$UCBSCAN  DS     0H
          PUT    SYSOUT,JOBCARD        * PUT JOB CARD
          PUT    SYSOUT,ROOM           * PUT ROOM CARD
          PUT    SYSOUT,EXECARD        * PUT EXEC CARD
          PUT    SYSOUT,SYSPRINT       * PUT EXEC CARD
          XR     R1,R1                 * CLEAR - USED FOR 'CONVADR'
          XR     R2,R2                 * CLEAR - USED FOR CVT ADDR
          XR     R3,R3                 * CLEAR - USED UCB ADDR (DSECT)
          XR     R4,R4                 * CLEAR - WORK
          XR     R5,R5                 * USED
          XR     R6,R6                 *      FOR
          XR     R7,R7                 *               DETERMIN-
          LA     R7,VOLTBL             * VOLSER TABLE
          XR     R8,R8                 * NUMBER OF VOLUMES
          XC     WORKAREA,WORKAREA     * CLEAR R AREA
          L      R2,CVTPTR             * CVT POINTER
          USING  CVT,R2                * ADDRES CVT
          SPACE
UCBLOOK   L      R15,CVTUCBSC          * UCB SCAN ROUTINE: V-CONSTANT
          CALL   (15),(WORKAREA,DEVCLASS,ADDRUCB),VL
          LTR    R15,R15               * GOOD CALL ?
          BNZ    $ENDUCB               * NOPE, ALL DONE
          L      R3,ADDRUCB            * ADDRESS OF UCB
          LTR    R3,R3                 * WAS ONE RETURNED ???
          BZ     UCBLOOK               * TRY AGAIN
          USING  UCB,R3                * ADDRESS IT
          LA     R4,UCBPRFX            * LENGTH OF UCB PREFIX
          SR     R3,R4                 * ADDRESS PREFIX
          CLI    UCBID,UCBSTND         * IS THIS A STANDARD UCB ???
          BNE    UCBLOOK               * NOPE, LOOK AGAIN
          MVC    UNITNAM(3),UCBNAME    * EBCDIC UCB NAME
          XR     R5,R5                 * CLEAR
          MVC    STATUS(L'STATUS),BLANKS
          SPACE
*     ONLINE  ?
          CLI    UCBSTAT,UCBONLI
          BL     UCBLOOK                * BRANCH IF LOW
*     ONLINE, ALLOCATED, PERM, SYSRES ?
          CLI    UCBSTAT,UCBONLI+UCBALOC+UCBPRES+UCBSYSR
          BNE    *+10
          MVC    STATUS(10),=C'A-RES-PERM'  * SYSRES-ONLINE
*     ONLINE, ALLOCATED, PERM  ?
          CLI    UCBSTAT,UCBONLI+UCBALOC+UCBPRES
          BNE    *+10
          MVC    STATUS(8),=C'O-A-PERM'
*     ONLINE, ALLOCATED  ?
          CLI    UCBSTAT,UCBONLI+UCBALOC
          BNE    *+10
          MVC    STATUS(3),=C'O-A'       * ALLOCATED ONLINE
*     ONLINE, PERM  ?
          CLI    UCBSTAT,UCBONLI+UCBPRES
          BNE    *+10
          MVC    STATUS(6),=C'O-PERM'    * PERM-ONLINE NOT ALLOC
*     ONLINE, PERM, SYSRES?
          CLI    UCBSTAT,UCBONLI+UCBPRES+UCBSYSR
          BNE    *+10
          MVC    STATUS(10),=C'O-RES-PERM'  * SYSRES-ONLINE
*     ONLINE, CONSOLE ?
          CLI    UCBSTAT,UCBONLI+UCBSYSR
          BNE    *+10
          MVC    STATUS(3),=C'O-C'           * CONSOLE
*--------------------------------------*
* R2 = ADDR(CVT)                       *
* R3 = ADDR(UCB)                       *
* R4 = ADDR(UCBEXT1)                   *
*--------------------------------------*
          XR     R4,R4                 * UCB EXTENSION
          XR     R5,R5                 * WORK
          L      R4,UCBEXTPT           * EXTENSION
          USING  UCBCMEXT,R4           * ADDRESS IT
$DASD     EQU    *                     *
          CLI    #TYPE4,DA4HEX07       * 2305 FIXED HEAD
          BNE    *+10
          MVC    DEVT(4),=C'2305'
          CLI    #TYPE4,DA4HEX09       * 3330-1,2
          BNE    *+10
          MVC    DEVT(4),=C'3330'
          CLI    #TYPE4,DA4HEX0A       * 3340
          BNE    *+10
          MVC    DEVT(4),=C'3340'
          CLI    #TYPE4,DA4HEX0B       * 3350
          BNE    *+10
          MVC    DEVT(4),=C'3350'
          CLI    #TYPE4,DA4HEX0C       * 3375
          BNE    *+10
          MVC    DEVT(4),=C'3375'
          CLI    #TYPE4,DA4HEX0D       * 3330-11
          BNE    *+10
          MVC    DEVT(4),=C'3330'
          CLI    #TYPE4,DA4HEX0E       * 3380
          BNE    *+10
          MVC    DEVT(4),=C'3380'
          CLI    UCBSTAB,UCBBSTR                  * JUST STORAGE
          BNE    *+14
          B      UCBLOOK               * DON'T MAP STORAGE DEVICES
* ISSUE THE CVAFTST MACRO TO DETERMINE IF INDEXED VTOC'S ON ???
          SPACE
          L      R5,ADDRUCB
          CVAFTST  UCB=(5)
          C      R15,=F'8'             * IS RC=8, SYSTEM SUPP, INDEXED
          BNE    $NOINDEX              * SKIP ALL OTHERS
          MVC    STATUS+8(4),=C'INDX'
$NOINDEX  MVC    VOL1(6),UCBVOLI     * SAVE VOLSER
          MVC    VOL2(6),UCBVOLI     * SAVE VOLSER
          MVC    OVOL1(6),UCBVOLI    * SAVE VOLSER
          MVC    OVOL2(6),UCBVOLI    * SAVE VOLSER
          MVC    0(6,R7),UCBVOLI     * SAVE VOLSER
          A      R8,=F'1'            * FOR VOLSER TABLE
          A      R7,=F'6'            * NEXT IN TABLE
          PUT    SYSOUT,VOLINE
          PUT    SYSOUT,TAPEOUT1
          PUT    SYSOUT,TAPEOUT2
          B      UCBLOOK
$ERROR1   DS     0H
          B      $FINE
$ENDUCB   PUT    SYSOUT,SYSIN          *
          LA     R7,VOLTBL             *
$UCBLOOP  EQU    *
          MVC    VOLCARD1(6),0(R7)     *
          MVC    VOLCARD2(6),0(R7)     *
          PUT    SYSOUT,MAPCARD
          A      R7,=F'6'
          BCT    R8,$UCBLOOP
          CLOSE  (SYSOUT)
$FINE     SR     R15,R15               *
          L      R13,SAVE+4            * RESTORE OLD SAVE AREA ADDR.
          L      R14,12(R13)           * RETURN ADDRESS
          LM     R0,R12,20(R13)        * REST OF THE REGS
          BR     R14                   * RETURN
          EJECT
 TITLE 'DATA SECTION - CONSTANTS'
SAVE      DS     18F                   * SAVE AREA
          DS     0D
DWORD     DS     D                     * DOUBLE WORD
BLANKS    DC     CL80' '
 SPACE
JOBCARD   DC     CL80'//TECHSUPP JOB (000000),TECHSUPPORT,'
ROOM      DC     CL80'/*JOBPARM ROOM=TECH'
EXECARD   DC     CL80'//S1  EXEC PGM=ADRDSSU,REGION=6M'
SYSPRINT  DC     CL80'//SYSPRINT  DD  SYSOUT=A'
*
VOLINE    DC     CL2'//'
VOL1      DC     CL8' ',CL4'DD',CL5'UNIT='
DEVT      DC     CL4' ',CL9',VOL=SER='
VOL2      DC     CL6' ',CL10',DISP=OLD'
UNITNAM   DC     CL4' '
          DC     CL4' '
STATUS    DC     CL10' '
FILLER1   DC     CL(80-(*-VOLINE))' '
*
TAPEOUT1  DC     C'//T'
OVOL1     DC     C'       DD  UNIT=TAPE,DSN=TS.BK'
OVOL2     DC     CL(80-(*-TAPEOUT1))'      .GDG(+1),'
TAPEOUT2  DC     CL80'//        DCB=MODELDCB,DISP=(,CATLG)'
*
SYSIN     DC     CL80'//SYSIN    DD   *'
MAPCARD   DC     C'  DUMP  INDD('
VOLCARD1  DC     C'      )   OUTDD(T'
VOLCARD2  DC     CL(80-(*-MAPCARD))'      )'
VOLTBL    DC     100CL6' '
*
*
*--------------------  USED IN  UCB SCAN ROUTINE -------------------*
ADDRUCB   DS     A                     *
WORKAREA  DC     XL100'00'             *
DEVCLASS  DC     X'20'                 * DASD ONLY DEVICE CLASS
*-------------------------------------------------------------------*
*
OREC      DS     CL80
          PRINT NOGEN
SYSOUT    DCB  DDNAME=SYSOUT,LRECL=80,RECFM=FB,DSORG=PS,EXLST=EXL,     X
               MACRF=PM
EXL       DC    X'87',AL3(JFCB)
JFCB      DS    176C
          LTORG
          TITLE 'CVT'
          CVT    DSECT=YES
          TITLE 'IEFUCBOB'
          IEFUCBOB PREFIX=YES,LIST=NO
          TITLE 'UCBTYPES EQUATES'
          UCBTYPES
          REGS
          END
