PROC 0 TAPE CART VB RC(0)
ISREDIT MACRO (LRECL,TAPE,VB)
IF &LRECL=  THEN DO
   WRITENE ENTER LRECL:&STR( )
   READ &LRECL
   IF &LRECL=  THEN EXIT
 END
IF &TAPE=VB THEN SET &VB=VB
IF &VB=TAPE THEN SET &TAPE=TAPE
IF &VB=VB THEN SET &LRECL=&LRECL+4     /* RECORD DESCRIPTOR WORD */
IF &TAPE=TAPE OR &CART=CART THEN GOTO TAPEPROC
SET &BLK=47476/&LRECL
SET &BLK=&BLK/2
/* BLKSIZE=23476 HIGH-WATER-MARK */
SET &BLK=&BLK*&LRECL
IF &BLK>23476 THEN DO
   DO WHILE &BLK>23476
      SET &BLK=&BLK-&LRECL
     END
  END
/* VARIABLE LENGTH ADD 4 */
IF &VB=VB THEN SET &BLK=&BLK+4
IF &BLK>23476 THEN SET &BLK=&BLK-&LRECL

SET &ZEDSMSG = &STR(&LRECL / &BLK )

ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(&RC)

/* TAPE OR CART PROCESSING.  NOTE: MAX BLKSIZE=32756/32760 */
TAPEPROC: +
SET &BLK=32760/&LRECL
/* VARIABLE LENGTH SUB 4 */
IF &VB=VB THEN SET &BLK=&BLK-4
SET &BLK=&BLK*&LRECL
IF &BLK>32760 THEN SET &BLK=&BLK-&LRECL

SET &ZEDSMSG = &STR(&LRECL / &BLK - TAPE)

ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(&RC)
