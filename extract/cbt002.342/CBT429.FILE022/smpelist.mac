  TITLE 'PROCESS THE OUTPUT OF AND SMP/E LIST FUNCTIONS'
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                   *
*   FUNCTION:  THIS PROGRAM PROCESSES THE OUTPUT OF A               *
*              A SMP/E 'LIST FUNCTIONS' COMMAND, CREATING           *
*              A "ONE-LINER" FOR EACH FMID AS TO ITS STATUS.        *
*                                                                   *
*      INPUT:  DDNAME(SMPELIST) - OUTPUT OF SMP/E LISTING           *
*                                                                   *
*     OUTPUT:  DDNAME(SYSOUT) - ONE LINERS, LRECL(80), FOR EACH     *
*              FMID                                                 *
*       PARM:  OPTIONALLY SPECIFYING: NOHEADER TO REMOVE HEADERS    *
*              FROM THE REPORT                                      *
*                                                                   *
*       REGS: R1 ...... PARM, IF ANY                                *
*             R12 ..... BASE                                        *
*             R6 ...... LINES PER PAGE (#LINES)                     *
*             R7 ...... PAGE NUMBER (#PGNUM)                        *
*             R8 ...... BAL REG (#BAL)                              *
*                                                                   *
*      AUTHOR:  MARK V. HEDGES -                                    *
*               RIVERSIDE METHODIST HOSPITAL                        *
*               250 GRACELAND BLVD.                                 *
*               COL. OH 43214                                       *
*               (614) 431-1535                               P.T.L. *
*                                                                   *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
SMPELIST  CSECT
          STM    R14,R12,12(R13)       * SETUP
          LR     R12,R15
          USING  SMPELIST,R12
          B      *+24
          DC     C'&SYSDATE_&SYSTIME_V1.0'
          LA     R10,SAVE
          ST     R10,8(R13)
          ST     R13,SAVE+4
          LR     R13,R10
          L      R2,0(,R1)             * CHECK FOR PARM (IE NOHEADER)
          LH     R3,0(,R2)             *
          LTR    R3,R3                 * LENGTH
          BZ     $HOUSEK               * NOPE, SKIP
          BCTR   R3,0                  *
          CLC    2(*-*,R2),=C'NOHEADER' * IS IT NO HEADER
          EX     R3,*-6                *
          BNE    $HOUSEK               *
          MVI    FLG,X'FF'             * SET FLAG FO NOHEADER
#LINES    EQU    6                     * NUMBER OF LINES
#PGNUM    EQU    7                     * PAGENUMBER REG
#BAL      EQU    8                     * REG TO BRANCH ON
          SPACE
$HOUSEK   XR    #PGNUM,#PGNUM          * INIT PAGE NUMBER (R7)
          XR    #LINES,#LINES          * INIT LINE COUNTER
          MVC   PAGE(4),=C'PAGE'       * PUT PAGE IN ITS PLACE
          PRINT  NOGEN
$OPEN     OPEN  (SMPELIST,,SYSOUT,OUTPUT)
          PRINT  GEN
          CLI    FLG,X'FF'             * IS NO HEADER FLAG ON ?
          BE     $GET1                 * YEP,SKIP IT
          GET    SMPELIST,RECORD       * READ 1ST RECORD
          MVC    DATETIME(25),RECORD+1 * GET DATE AND TIME FORM REC
          GET    SMPELIST,RECORD       * READ RECORD
          GET    SMPELIST,RECORD       * READ RECORD
          MVC    ZONENAME(8),RECORD+1  * GET ZONE NAME
$BODY     BAL    #BAL,$HEADERS         * PUT THE HEADER OUT
$GET1     GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
*                                      * IS IT A FUNCTION RECORD ?
          CLC    RECORD+29(8),=C'FUNCTION'
          BNE    $GET1
          MVC    FUNCTION(8),RECORD+1  * SAVE FUNCTION NAME
          GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
*                                      * IS IT THE STATUS RECORD ?
          CLC    RECORD+11(6),=C'STATUS'
          BNE    $GET1                 * NOPE, DON'T WANT RECORD
          MVC    STATUS1(3),RECORD+29  * START OF STATUS INFO
          MVC    STATUS2(3),RECORD+34  * STATUS INFO
          MVC    STATUS3(3),RECORD+39  * STATUS INFO
          GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
*                                      * IS IT THE FMID RECORD ?
          CLC    RECORD+11(4),=C'FMID'
          BNE    $PUTRECE              * PROBLEM !!!
          GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
          MVI    JCLIN,C' '            * RESET JCLIN
*                                      * IS IT A JCLIN FMID ?
          CLC    RECORD+11(5),=C'JCLIN'
          BNE    $SKIP1
          MVI    JCLIN,C'Y'
          GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
*                                      * IS IT A 'DATE/TIME' RECORD
$SKIP1    CLC    RECORD+11(9),=C'DATE/TIME'
          BNE    $PUTRECE              * ERROR IF NOT
          GET    SMPELIST,RECORD       * GET RECORD
          BAL    #BAL,$CHECK           * CHECK FOR '1' IN COL 1
          MVC    DATE(6),RECORD+29     * SAVE DATE INFO
* GOOD RECORD
          MVC    OUTREC+0(8),FUNCTION  * PUT FUNCTION
          MVC    OUTREC+10(3),STATUS1  * 1ST STATUS
          MVC    OUTREC+15(3),STATUS2  * 2ND STATUS
          MVC    OUTREC+20(3),STATUS3  * 3RD STATUS
          MVC    OUTREC+28(6),DATE     * PUT THE DATE
          LA     #BAL,$PUTREC
          CLI    FLG,X'FF'             * IS NO HEADER FLAG ON ?
          BE     $PUTREC               * YEP, SKIP IT
          C      #LINES,=F'59'         * LINE CHECK
          BH     $HEADERS
$PUTREC   PUT    SYSOUT,OUTREC
          B      $GET1
          SPACE 4
$PUTRECE  MVI    RECORD+0,C'*'         * SIGNAL ERROR
          PUT    SYSOUT,RECORD         * PUT THE RECORD
          B      $GET1                 * READ ANOTHER
          SPACE
$CHECK    CLI    RECORD,C'1'           * IF CC1 = '1'
          BE     $REREAD               * READ SEVEN MORE
          BR     #BAL                  * RETURN
          SPACE
          PRINT  NOGEN
$REREAD   GET    SMPELIST,RECORD       * SKIP SEVEN RECORDS
          GET    SMPELIST,RECORD
          GET    SMPELIST,RECORD
          GET    SMPELIST,RECORD
          GET    SMPELIST,RECORD
          GET    SMPELIST,RECORD
          GET    SMPELIST,RECORD
          PRINT  GEN
          BR     #BAL
          SPACE
$HEADERS  A      #PGNUM,=F'1'           * ADD ONE TO PAGE NUMBER
          CVD    #PGNUM,DWORD           * CONVERT TO DECIMAL
          UNPK   PAGENUM(2),DWORD+6(2)  * PUT EDIT CHARS
          OI     PAGENUM+1,X'F0'        * EDIT IT
          PUT    SYSOUT,HEADER1         * PUT IT AND THE HEEADERS
          PUT    SYSOUT,HEADER2         *
          PUT    SYSOUT,HEADER3         *
          XR     #LINES,#LINES          * INIT LINE COUNTER
          A      #LINES,=F'3'           * LINES PRINTED
          BR     #BAL
          SPACE
EOF       CLOSE  (SMPELIST)
          CLOSE  (SYSOUT)
$FINISH   SR     R15,R15              *  ZERO RETURN REGISTER
          L      R13,SAVE+4           * RESTORE OLD SAVE AREA ADDR.
          L      R14,12(R13)          * RETURN ADDRESS
          LM     R0,R12,20(R13)       * REST OF THE REGS
          BR     R14                  * RETURN
SAVE      DS    18F
FLG       DC    X'00'
          DS    0D
DWORD     DS    D
RECORD    DS    CL121
HEADER1   DC    CL31'   THIS IS A LISTING OF ZONE = '
ZONENAME  DC    CL9' '
          DC    CL3'ON '
DATETIME  DC    CL(80-(*-HEADER1))' '
PAGE      EQU    *-7                   * ENOUGH FOR 'PAGE XX'
PAGENUM   EQU    *-2                   * ENOUGH FOR 'PAGE XX'
HEADER2   DC    CL34'__FMID__ ____ STATUS ____   _DATE_'
*                    HOZ2103   REC  APP  ACC     YY.DDD
          DC    CL46' _________________ FUNCTION __________________'
HEADER3   DC    CL80' '
OUTREC    DC    CL80' '
FUNCTION  DC    CL8' '
STATUS1   DC    CL3' '
STATUS2   DC    CL3' '
STATUS3   DC    CL3' '
JCLIN     DC    CL1' '
DATE      DC    CL6' '
          PRINT NOGEN
SMPELIST  DCB DDNAME=SMPELIST,RECFM=FBA,LRECL=121,DSORG=PS,            X
               MACRF=GM,EODAD=EOF
SYSOUT    DCB DDNAME=SYSOUT,RECFM=FB,LRECL=80,BLKSIZE=3120,DSORG=PS,   X
               MACRF=PM
R0       EQU      0
R1       EQU      1
R2       EQU      2
R3       EQU      3
R4       EQU      4
R5       EQU      5
R6       EQU      6
R7       EQU      7
R8       EQU      8
R9       EQU      9
R10      EQU     10
R11      EQU     11
R12      EQU     12
R13      EQU     13
R14      EQU     14
R15      EQU     15
          END
