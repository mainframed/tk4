*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*  MODULE NAME = UCBSVC78                                             *
*                                                                     *
*  DESCRIPTIVE NAME =                                                 *
*                                                                     *
*  OPERATING ENVIRONMENT = MVS/XA 2.1.7                               *
*                                                                     *
*  FUNCTION = WRITES 1 LINE OF SPACE INFORMATION FROM LSPACE, SVC 78, *
*             TO DATASET SYSOUT, GOOD MONITORING TOOL                 *
*  NOTES =                                                            *
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*           R1 .....                                                  *
*           R2 .....                                                  *
*           R3 .....                                                  *
*           R4 .....                                                  *
*           R5 .....                                                  *
*           R6 .....                                                  *
*           R7 .....                                                  *
*           R8 .....                                                  *
*           R9 .....                                                  *
*          R10 .....                                                  *
*          R11 .....                                                  *
*          R12 ..... BASE                                             *
*          R13 .....                                                  *
*          R14 .....                                                  *
*          R15 .....                                                  *
*                                                                     *
*             MACROS = IBM: GET, PUT, DCB, OPEN                       *
*                                                                     *
*                    OTHER: SVC 78                                    *
*                                                                     *
*                                                                     *
*        SUBROUTINES = PRC:                                           *
*                                                                     *
*             DSECTS = IBM:                                           *
*                                                                     *
*                    OTHER:                                           *
*                                                                     *
*             MODULE TYPE = ASMH V2.1                                 *
*                                                                     *
*                PROCESSOR = 4381-11                                  *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
  EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*             ENTRY POINTS = UCBSVC78 (ONLY ENTRY POINT)              *
*                                                                     *
*             INPUT = PARM AS TO VOLSER                               *
*                                                                     *
*             OUTPUT = DDNAME SYSOUT, FOR LSPACE INFO                 *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*             TABLES =                                                *
*                                                                     *
*             AUTHOR = MARK V. HEDGES                                 *
*                      ADRIA LABORATORIES                             *
*                      7001 POST ROAD                                 *
*                      DUBLIN OH. 43017                               *
*                      (614) 764-8175                            PTL  *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE - WE HOPE                            *
*                                                                     *
*                                                                     *
*                                                                     *
* -----------------------  CHANGE SECTION  ---------------------------*
*                                                                     *
* 03/23/88 ADDED                                                      *
*                                                                     *
*                                                                     *
*                                                                     *
* -----------------------  THINGS TO DO  -----------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         EJECT  ,
 TITLE '>>>     WRITE FREE SPACE INFORMATION     <<<'
UCBSVC78  CSECT
          STM   R14,R12,12(R13)         SAVE PREVIOUS REGS
          LR    R12,R15                 SAVE PGM BEGIN ADDR
          USING UCBSVC78,R12
          LA     R10,SAVE               ADDR NEW SAVE AREA
          ST     R10,8(R13)             NEW SAVE ADDR IN OLD SAVE AREA
          ST     R13,SAVE+4             OLD SAVE ADDR IN NEW SAVE AREA
          LR     R13,R10                R13 CONTAINS NEW SAVE ADDR
BALREG    EQU    14
CVTADR    EQU    2
UCBADR    EQU    3
UCBEXT1   EQU    4
START     DS     0H
          L      R2,0(,R1)             * TEST FOR PSWD DSN
          LH     R3,0(,R2)
          LTR    R3,R3
          BZ     $ERROR1
          BCTR   R3,0
          MVC    PARM(*-*),2(R2)
          EX     R3,*-6
$RDJFCB   DS     0H
          RDJFCB SYSOUT                * DDNAME ALLOCATED ?
          LTR    R15,R15               * TEST RESULTS
          BNZ    $ERROR1               * NOPE, DO TPUTS
          OPEN   (SYSOUT,OUTPUT)       * OPEN SYSOUT FILE
$UCBSCAN  DS     0H
          XR     R6,R6                 * N/A
          S      R6,=F'1'              * FLAG FOR UNIT=AFF PARM
          XR     R8,R8                 * NUMBER OF VOLUMES
          XC     WORKAREA,WORKAREA     * CLEAR R AREA
          L      R2,CVTPTR             * CVT POINTER
          USING  CVT,R2                * ADDRES CVT
          SPACE
UCBLOOK   L      R15,CVTUCBSC          * UCB SCAN ROUTINE: V-CONSTANT
          CALL   (15),(WORKAREA,DEVCLASS,ADDRUCB),VL
          LTR    R15,R15               * GOOD CALL ?
          BNZ    $ENDUCB               * NOPE, ALL DONE
          A      R6,=F'1'
          L      R3,ADDRUCB            * ADDRESS OF UCB
          LTR    R3,R3                 * WAS ONE RETURNED ???
          BZ     UCBLOOK               * TRY AGAIN
          USING  UCB,R3                * ADDRESS IT
          LA     R4,UCBPRFX            * LENGTH OF UCB PREFIX
          SR     R3,R4                 * ADDRESS PREFIX
          CLI    UCBID,UCBSTND         * IS THIS A STANDARD UCB ???
          BNE    UCBLOOK               * NOPE, LOOK AGAIN
          L      R4,UCBEXTPT           * EXTENSION
          USING  UCBCMEXT,R4           * ADDRESS IT
          CLC    UCBVOLI(6),PARM       * CHECK PARM
          BNE    UCBLOOK               *
* HOW MUCH SPACE ON DISK PACK ????
          LA     R0,UCBCMSEG           * LOAD UCB ADDR
          LA     R1,WORKA              * LOAD ADDR OF WORKAREA: CL30
          SVC    78                    * ISSUE LSPACE
          ST     R15,SVC78RC
          CVD    R15,DWORD
          UNPK   RC(2),DWORD(8)
          OI     RC+1,X'F0'
          LTR    R15,R15
          BNZ    $ERRMSG
          MVC    FREECYL(4),#CYL       * FREE CLYS
          MVC    FREETRK(4),#TRK       * FREE TRKS
          MVC    FREEEXT(4),#EXT       * AVAILABLE EXTENTS
          MVC    CONTCYL(4),#CCYL      * CONTIG AVAIL CYLS
          MVC    CONTTRK(4),#CTRK      * CONTIG AVAIL TRKS
          B      $GETDATE
$ERRMSG   MVC    LSPACE(BLANKOUT),BLANKS
          MVC    RC-2(3),=C'RC('
          MVI    RC+1,C')'
          MVC    LSPACE(L'WORKA),WORKA
 TITLE '>>> GET DATE AND TIME ROUTINE <<<'
$GETDATE  DS     0H
          $STCK
 TITLE '>>>     WRITE FREE SPACE INFORMATION     <<<'
          MVC    ODATE(8),XDATE        * GET MM/DD
          MVC    ODATE+9(8),XTIME      * PUT TIME
          MVC    ODATE+18(3),XWEEKDAY  * PUT WEEK DAY
          PUT    SYSOUT,OREC
          SPACE
          B      $ENDUCB
$ERROR1   DS     0H
          WTO    'NO PARM OR NO SYSOUT DATASET SPECIFIED ... RC = 8'
          LA     R15,8
          B      $BADEND
$ENDUCB   DS     0H
          CLOSE  (SYSOUT)
$FINE     SR     R15,R15               *
$BADEND   L      R13,SAVE+4            * RESTORE OLD SAVE AREA ADDR.
          L      R14,12(R13)           * RETURN ADDRESS
          LM     R0,R12,20(R13)        * REST OF THE REGS
          BR     R14                   * RETURN
          EJECT
 TITLE 'DATA SECTION - CONSTANTS'
SAVE      DS     18F                   * SAVE AREA
BLANKS    DC     CL80' '
*
          DS     0D
DWORD     DS     D                     * DOUBLE WORD
 SPACE
STATUS    DC     CL10' '
*
*--------------------  USED IN  UCB SCAN ROUTINE -------------------*
ADDRUCB   DS     A                     *
WORKAREA  DC     XL100'00'             *
DEVCLASS  DC     X'20'                 * DASD ONLY DEVICE CLASS
*-------------------------------------------------------------------*
*
*  LSPACE HEADER AND MESSAGES
WORKA     DS     CL30                  * WORK AREA FOR LSPACE SVC
#CYL      EQU    WORKA+06,4            * FREE TOTAL CYL'S
#TRK      EQU    WORKA+11,4            * FREE TOTAL TRACKS
#EXT      EQU    WORKA+16,4            * AVAILABLE EXTENTS
#CCYL     EQU    WORKA+21,4            * CONTIGUOUS CYL'S
#CTRK     EQU    WORKA+26,4            * CONTIGUOUS TRK'S
*
SVC78RC   DC     F'0'
OREC      DC     CL80' '
          ORG    OREC
ODATE     DC     CL22' '
LSPACE    DC     C'CYL='
FREECYL   DC     CL4' '
          DC     C','
CONTCYL   DC     CL4' '
          DC     C',TRK='
FREETRK   DC     CL4' '
          DC     C','
CONTTRK   DC     CL4' '
          DC     C',EXT='
FREEEXT   DC     CL4' '
BLANKOUT  EQU    *-LSPACE-1
          DC     CL3' '
PARM      DC     CL6' '
          DC     CL3' '
RC        DC     CL3' '
          ORG    ,
*
          PRINT NOGEN
SYSOUT    DCB  DDNAME=SYSOUT,LRECL=80,RECFM=FB,DSORG=PS,EXLST=EXL,     X
               MACRF=PM
EXL       DC    X'87',AL3(JFCB)
          TITLE '>>> IEFJFCBN <<<'
JFCB      EQU   *
          IEFJFCBN  LIST=NO
          LTORG
          TITLE '>>> CVT      <<<'
          CVT    DSECT=YES
          TITLE '>>> IEFUCBOB <<<'
          IEFUCBOB PREFIX=YES,LIST=NO
          TITLE '>>> UCBTYPES <<<'
          UCBTYPES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
          END
