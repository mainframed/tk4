
   %MACRO SASTIMES(VALUE);
   *-------------------------------------------------------------*
   *          MACRO: SASTIMES
   *      VARIABLES: VALUE - A SAS DATETIME VALUE OR NULL
   * CALL STATEMENT: %INCLUDE DDNAME(SASTIMES)
   *                 ... %SASTIMES ... OR ...
   *                 ... %SASTIMES('01OCT86:12:02:01'DT)
   *
   *
   * VARIABLES RTND: _VARIABLE_    _DESCRIPTION_      (FORMAT)
   *                 D_H_M_S ..... DAY,HOUR,MIN,SEC (DATETIME)
   *                 D_T ......... DAY,HOUR,MIN,SEC (DATETIME)
   *                 T_IME ....... TIME OF DAY        (TIME8.)
   *                 H_M_S ....... HOUR,MINUTE,SECOND (TIME8.)
   *                 H_OUR ....... HOUR                   (2.)
   *                 M_INUTE ..... MINUTE                 (2.)
   *                 S_ECOND ..... SECOND                 (2.)
   *                 D_ATE ....... CURRENT DATE       (DATE7.)
   *                 M_D_Y ....... MONTH,DAY,YEAR     (DATE7.)
   *                 W_DTE ....... WEEK DATE: MON (WEEKDATE3.)
   *                 W_DAY ....... WEEK DAY: 1-7 (SUN-SAT)(1.)
   *                 Y_EAR ....... YEAR                   (2.)
   *                 M_ONTH ...... MONTH                  (2.)
   *                 D_O_M ....... DAY OF MONTH           (2.)
   *                 WEEKNUM ..... WEEK NUMBER FORM 1/1/  (2.)
   *                 WEEK_MON..... WEEK NUMBER W/I MONTH  (1.)
   *                 Q_TR ........ QUARTER                (1.)
   *                 Y_EARQ ...... YEAR TO QUARTER     (YYQ4.)
   *
   * CHECK TO SEE IF DATE TIME VARIABLE WAS PRESPECIFIED
   *-----------------------------------------------------------*;
   %IF %STR(&VALUE)^=    %THEN     D_T=&VALUE.    %STR(;)  ;
                         %ELSE     D_T=DATETIME() %STR(;)  ;
 * TIME MANIPULATIONS                                       ;
   T_IME=TIMEPART(D_T);             /* TIME PART OF DATETIME       */
   H_OUR=HOUR(T_IME);               /* HOUR OF DAY                 */
   M_INUTE=MINUTE(T_IME);           /* MINUTE OF HOUR              */
   S_ECOND=SECOND(T_IME);           /* SECOND OF MINUTE            */
                                    /* HOUR, MINUTE, SECOND        */
   H_M_S=HMS(H_OUR,M_INUTE,S_ECOND);

 * DATE MANIPULATIONS                                       ;
   D_ATE=DATEPART(D_T);             /* DATE PART OF DATETIME       */
   J_DATE=JULDATE(D_ATE);           /* JULIAN DATE                 */
   Y_EAR=YEAR(D_ATE);               /* YEAR FROM DATE              */
   Q_TR=QTR(D_ATE);                 /* QUARTER: 1-4                */
   Y_EARQ=YYQ(Y_EAR,Q_TR);          /* YEAR AND QUARTER: 1-4       */
   M_ONTH=MONTH(D_ATE);             /* MONTH OF YEAR               */
   D_O_M=DAY(D_ATE);                /* DAY OF MONTH                */
   M_D_Y=MDY(M_ONTH,D_O_M,Y_EAR);   /* MONTH, DAY, YEAR            */
   W_DAY=WEEKDAY(D_ATE);            /* WEEK DAY :SUN(1) - SAT(7)   */
   W_DTE=D_ATE;                     /* WEEK DAY :SUN - SAT         */
                                    /* DAY, HOUR, MINUTE, SECOND   */
   D_H_M_S=DHMS(D_ATE,H_OUR,M_INUTE,S_ECOND);

 * FORMAT THE VARIABLES                                     ;
   FORMAT T_IME       H_M_S                                   TIME8.
          D_ATE       M_D_Y     M_D_CY   M_D_CZ               DATE7.
          W_DTE                                           WEEKDATE3.
          Y_EARQ                                               YYQ4.
          Y_EAR                                                   4.
          M_ONTH  H_OUR  M_INUTE  S_ECOND  D_O_M  WEEKNUM         2.
          Q_TR        W_DAY   WEEK_MON                            1.
          D_H_M_S     D_T                                DATETIME19.2
        ;

 * CALCULATE WEEK: 1-52, WHERE A WEEK=(SUN=>SAT)         ;
   M_D_CY=MDY(1,1,Y_EAR);                /* JAN 1 OF CURRENT YEAR  */
   WEEKNUM=INTCK('WEEK',M_D_CY,D_ATE);   /* NUMBER OF WEEKS SINCE
                                            JAN 1, OF THIS YEAR    */
 * CALCULATE WEEK OF MONTH                               ;
   M_D_CZ=MDY(M_ONTH,1,Y_EAR);         /* 1ST DAY OF THIS MONTH  */
   WEEK_MON=INTCK('WEEK',M_D_CZ,D_ATE);


   /*          - POSSIBLE KEEP STATEMENT -
   KEEP T_IME D_ATE J_DATE H_OUR M_INUTE S_ECOND H_M_S D_H_M_S
        Y_EAR Q_TR Y_EARQ M_ONTH D_O_M WEEK_DAY WEEK_DTE
        WEEKNUM D_T
        ;                                                   */
  /*
     COMPARING DATE AND TIME VALUES:
               IF D_T='12JAN86:12:13:21'DT THEN ....
               IF D_ATE='12JAN86'D         THEN ....
               IF T_IME='00:12:00'T        THEN ....
  */
  %MEND ;
