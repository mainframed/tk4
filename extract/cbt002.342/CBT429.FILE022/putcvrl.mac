 TITLE '        PTF COVER LETTER PRINTER FOR MVS PTF TAPES'
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
* FUNCTION:    THIS PROGRAM READS A PTF TAPE AND PRINTS ALL PTFS    *
*              LOOKING FOR '++ PTF' OR '++ APAR', PER PAGE. IT ALSO *
*              LISTS THE PTF NUMBER ON THE FAR LEFT SIDE FOR        *
*              EASY REFERENCE.  FINALLY A LIST OF ALL FMIDS ON THE  *
*              TAPE ARE PRINTED.                                    *
*                                                                   *
* REG USAGE:                                                        *
*     R12,R11 ........ BASE REGS                                    *
*     R3 ............. BAL REG                                      *
*     R5 ............. PAGE COUNTER REG                             *
*     R6 ............. FMIDLIST ADDRESS INCREMENTED                 *
*     R7,R8 .......... USED FOR BCT FOR PAGE COUNTER                *
*     R9 ............. FMIDLIST ADDRESS INITIAL                     *
*     AVAILABLE R2,R4                                               *
*                                                                   *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         EJECT
PUTCVRL  CSECT
         STM   R14,R12,12(R13)         SAVE PREVIOUS REGS
         LR    R12,R15                 SAVE PGM BEGIN ADDR
         USING PUTCVRL,R12,R11
         L     R11,SEC4K               ADDR 2ND 4K
         LA    R10,SAVE                ADDR NEW SAVE AREA
         ST    R10,8(R13)              NEW SAVE ADDR IN OLD SAVE AREA
         ST    R13,SAVE+4              OLD SAVE ADDR IN NEW SAVE AREA
         LR    R13,R10                 R13 CONTAINS NEW SAVE ADDR
         B     BEGIN                   JUMP OVER SAVE
SAVE     DS    18F                     SAVE AREA FOR THIS PGM
SEC4K    DC    A(PUTCVRL+4096)          ADDR OF 2ND 4K
         EJECT
BEGIN    DS    0H
         L     R2,0(,R1)           PARM ADDR
         LH    R3,0(,R2)           PARM LENGTH
         LTR   R3,R3               TEST
         BZ    NOPARM
         BCTR  R3,0
         MVC   LINES(*-*),2(R2)    MOVE PARM
         EX    R3,*-6              EXECUTED
         PACK  DWORD(8),LINES(2)   YES, PACK LINECOUNT
         CVB   R5,DWORD                 CONVERT IT
         ST    R5,DWORD                 STORE COUNTER
         B     GETT
NOPARM   L     R2,=F'59'           NO, DEFAULT LINECOUNT
         ST    R2,DWORD                STORE IN DOUBLE WORD
GETT     EQU   *
         GETTIME DATE=XDATE        GET DATE AND TIME
         MVC   HDRDATE(15),XDATE+1 PUT IT ON HEADER
         EJECT
         PRINT NOGEN
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                   *
* PTFFILE .... SEQ FILE OF SYSMODS LRECL(80) MACRF(GM)              *
* SYSOUT  .... SEQ PRINT FILE OF SYSMODS LRECL(133) MACRF(PM)       *
* SYSUT1 ..... SEQ TEMP FILE FOR WORK AREA LRECL(133) MACRF(PM,GM)  *
*                                                                   *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
OPEN     OPEN   (PTFFILE,,SYSOUT,OUTPUT,SYSUT1,OUTPUT)
         LA     R3,2
HEADER   MVI    COVER1,C'1'         ALL '****'
         PUT    SYSOUT,COVER1
         MVI    COVER1,C' '
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,#COVER
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,#LETTER
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,#PRINTER
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,#REL
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER2
         PUT    SYSOUT,COVER1
         BCT    R3,HEADER
         SPACE 3
         XR     R3,R3                  BAL REG FOR PTFS
         LA     R7,1                   INIT FMID COUNTER
         LR     R8,R7                  FMID VALUE SAVED
         LA     R6,FMIDLIST+1          ADDRESS OF FMIDLISTS
         PRINT GEN
         EJECT
         SPACE
START    GET    PTFFILE,RECORD         GET RECORD
         CLC    RECORD(6),=C'++ PTF'   IS IT A PTF ?
         BE     PTF
         CLC    RECORD(7),=C'++ APAR'  IS IT AN APAR ?
         BE     APAR
         CLC    RECORD(7),=C'++ HOLD'  IS IT RELATED HOLD DATA ?
         BE     HOLDATA
         B      START                  IF NONE ABOVE, RETURN
         EJECT
APAR     MVC    PTFNUM(8),RECORD+9     MOVE APAR NUMBER IN
         B      *+10
PTF      AP     PAGENUM(2),=PL2'01'    INCREMENT PAGE COUNTER BY ONE
         UNPK   PG(2),PAGENUM(2)
         OI     PG+1,X'F0'
         SPACE
         MVC    PTFNUM(8),RECORD+8     SAVE PTF NUMBER
         MVC    P1(1),PTFNUM+0         SET PTF NUMBER FOF PRINTING
         MVC    P2(1),PTFNUM+1
         MVC    P3(1),PTFNUM+2
         MVC    P4(1),PTFNUM+3
         MVC    P5(1),PTFNUM+4
         MVC    P6(1),PTFNUM+5
         MVC    P7(1),PTFNUM+6
         SPACE
         PUT    SYSOUT,HEADER1         PUT HEADER1
         MVC    CONT(5),=C'     '      INIT 'CONT.' FIELD
         PUT    SYSOUT,HEADER2         PUT SECOND HRD W/PTF NAME
         LA     R5,2                   RESET PAGE COUNTER: 2 LINES
         SPACE
PRINTM   MVC    RECORD+71(10),=10C' '  GET RID OF LINE NUMBERS
         CLC    RECORD+1(2),=C'/*'     BEGINNING OF COMMENTS
         BE     COMMENTS
         CLC    RECORD+2(2),=C'/*'     BEGINNING OF COMMENTS
         BE     COMMENTS
         CLC    RECORD+3(2),=C'/*'     BEGINNING OF COMMENTS
         BE     COMMENTS
         LR     R7,R8
         PUT    SYSOUT,ORECORD         PUT RECORD
         A      R5,=F'1'               ADD ONE TO  PAGE COUNTER
         GET    PTFFILE,RECORD         GET ANOTHER RECORD
         CLC    RECORD+3(4),=C'FMID'   IS IT THE 'FMID (XXXXXXX)' REC
         BE     FMIDS
*        CLC    RECORD+3(4),=C'    '   IT WAS A CONTINUATION .....
*                                         OF A PRE,REQ, OR SUP
*                                         SO WE MUST BRANCH TO THE
*                                         CORRECT ROUTINE USING
*                                         REG R3
*        BE     BALIT
*        CLC    RECORD+3(3),=C'PRE'    IS IT A PRE REQ ?
*        BE     PRE
*        CLC    RECORD+3(3),=C'REQ'    IS IT A PRE REQ ?
*        BE     REQ
*        CLC    RECORD+3(3),=C'SUP'    IS IT A SUP     ?
*        BE     SUP
         B      PRINTM
COMMENTS MVC    RECORD+71(10),=10C' '  GET RID OF LINE NUMBERS
         PUT    SYSOUT,ORECORD         PUT RECORD
         LR     R7,R8
         A      R5,=F'1'               ADD ONE TO  PAGE COUNTER
         C      R5,DWORD               CHECK FOR PAGE
         BNE    CGET
         BAL    R2,PAGE
CGET     GET    PTFFILE,RECORD         GET ANOTHER RECORD
         CLC    RECORD+1(3),=C'*/.'    IS IT THE END OF COMMENTS ?
         BE     START
         CLC    RECORD+2(3),=C'*/.'    IS IT THE END OF COMMENTS ?
         BE     START
         CLC    RECORD+3(3),=C'*/.'    IS IT THE END OF COMMENTS ?
         BE     START
         B      COMMENTS
         SPACE
HOLDATA  EQU    *
         PUT    SYSOUT,HOLDHDR
         A      R5,=F'2'
OHOLDATA MVC    RECORD+71(10),=10C' '  GET RID OF LINE NUMBERS
         PUT    SYSOUT,ORECORD
         A      R5,=F'1'               ADD ONE TO  PAGE COUNTER
         C      R5,DWORD               CHECK FOR PAGE
         BNE    OGET
         BAL    R2,PAGE
OGET     GET    PTFFILE,RECORD
         CLC    RECORD(2),=C'++'       LOOK FOR END OF HOLDDATA
         BNE    OHOLDATA
         B      START
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                   *
*        PRINT A NEW PAGE AND ADD ONE TO PAGE COUNTER               *
*                                                                   *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
PAGE     AP     PAGENUM(2),=PL2'01'
         UNPK   PG(2),PAGENUM(2)
         OI     PG+1,X'F0'
         PUT    SYSOUT,HEADER1
         MVC    CONT(5),=C'CONT.'      PUT 'CONT.' IN HEADER
         PUT    SYSOUT,HEADER2
         LA     R5,2                   RESET PAGE COUNTER
         BR     R2
         SPACE 2
FMIDS    LA     R9,FMIDLIST-7
*        MVC    $OFMID(8),RECORD+8
*        MVC    $OPTF(8),PTFNUM
FMIDLOOP LA     R9,7(R9)               ADDR OF FMID LIST
         CLC    0(7,R9),RECORD+8       IS THIS NEW ONE EQUAL TO AN
         BE     PRINTM                    OLD ONE ?
         BCT    R7,FMIDLOOP
         MVC    0(7,R9),RECORD+8       NEW FMID - SAVE IT
         A      R8,=F'1'               ADD ONE TO FMID COUNTER
         B      PRINTM
*PRE      EQU    *
*         LA     R2,RECORD+9            SET RECORD TO PARSE IT
*         CLI    0(R2),C','
*         MVC    $OFMID(8),RECORD+8
*         MVC    $OPTF(8),PTFNUM
*         LA     R3,PRE                 FOR POSSIBLE BAL
*         PUT    SYSOUT,ORECORD         PUT RECORD
*         B      PRINTM
*REQ      EQU    *
*         LA     R3,REQ                 FOR POSSIBLE BAL
*         PUT    SYSOUT,ORECORD         PUT RECORD
*         B      PRINTM
*SUP      EQU    *
*         LA     R3,SUP                 FOR POSSIBLE BAL
*         PUT    SYSOUT,ORECORD         PUT RECORD
*         B      PRINTM
*BALIT    BR     R3                     BRANCH ROUTINE FOR CONTINUATION
EOF      CLOSE  (PTFFILE)
         CLOSE  (SYSUT1)
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                   *
*   PRINT THE FMID LIST FILE FOR ALL VALID FMIDS ON THSI TAPE       *
*                                                                   *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         LA     R2,FMIDLIST-7
         PUT    SYSOUT,OFMIDH1
         PUT    SYSOUT,OFMIDH2
FMIDPRT  LA     R2,7(R2)
         CLI    0(R2),C' '
         BE     EOF2
         MVC    OFMID(7),0(R2)
         PUT    SYSOUT,OFMIDREC
         B      FMIDPRT
EOF2     CLOSE  (SYSOUT)
         SR     R15,R15                 ZERO RETURN REGISTER
         L      R13,SAVE+4             RESTORE OLD SAVE AREA ADDR.
         L      R14,12(R13)            RETURN ADDRESS
         LM     R0,R12,20(R13)         REST OF THE REGS
         BR     R14                    RETURN
         EJECT
         TITLE '        DATA --- SECTION'
LINES    DS     CL2
         DS    0D
DWORD    DS     D
PTFNUM   DC     CL8' '
         DS     0F
ORECORD  DC     CL1' '
         DC     CL26' '
RECORD   DC     CL80' '
         DC     CL26' '
         DS     0H
HEADER1  DC CL42'1  - - - - - - - - - - - - - - - - - -    '
         DC CL41'C O V E R    L E T T E R    P R I N T E R'
         DC CL41'    - - - - - - - - - - - - - - - - - -  '
         DC     C'  PAGE '
PG       DC     CL8'0'
HDRDATE  EQU    HEADER1+42+41+41-15
         SPACE
HOLDHDR  DC     CL15'0'
         DC     CL(133-(*-HOLDHDR))' + +  H O L D D A T A'
PAGENUM  DC     PL2'0'
         SPACE 2
HEADER2  DC     CL1'-'
P1       DC     CL2' -'
P2       DC     CL2' -'
P3       DC     CL2' -'
P4       DC     CL2' -'
P5       DC     CL2' -'
P6       DC     CL2' -'
P7       DC     CL2' '
         DC     CL112' '
CONT     DC     CL6'     '
         SPACE 2
FMIDLIST DC     40CL7' '
OFMIDH1  DC     CL133'1   A L L    A P P L I C A B L E    F M I D " S'
OFMIDH2  DC     CL133'-'
OFMIDREC DC     CL5'0'
OFMID    DC     CL128' '
         DS     0H
COVER1   DC     C'1',132C'*'
COVER2   DC     C' ',C'*',130C' ',C'*'
#COVER   DC     C' *'
         CENTER 'C O V E R',LEN=130
         DC     C'*'
#LETTER  DC     C' *'
         CENTER 'L E T T E R',LEN=130
         DC     C'*'
#PRINTER DC     C' *'
         CENTER 'P R I N T E R',LEN=130
         DC     C'*'
#REL     DC     C' *'
         CENTER 'REL 2.2.1',LEN=130
         DC     C'*'
         SPACE 2
         PRINT  NOGEN
PTFFILE  DCB  DDNAME=PTFFILE,DSORG=PS,LRECL=80,MACRF=(GM),             X
               RECFM=FB,EODAD=EOF
SYSOUT DCB DDNAME=SYSOUT,DSORG=PS,LRECL=133,BLKSIZE=1330,MACRF=(PM),   X
               RECFM=FBA
SYSUT1 DCB DDNAME=SYSUT1,DSORG=PS,LRECL=256,BLKSIZE=2560,MACRF=(PM,GM),X
               RECFM=FB
         SPACE 2
         PRINT GEN
* REGISTER EQUATES
         SPACE
         REGS
         END
