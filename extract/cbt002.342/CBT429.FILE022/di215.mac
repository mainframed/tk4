 TITLE 'DISPLAY INITIATORS  -  UNDER JES 2.1.5               &SYSDATE'
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                               *
*   THIS ROUTINE DISPLAYS INITIATOR STATUS TO THE TERMINAL      *
*        RUNNING TSO.                                           *
*                                                               *
*   JES2 LEVEL: 2.1.5                                           *
*                                                               *
*   CALLS: MODULE 'CLEAR' LINE # 56                             *
*                                                               *
*           AUTHOR = MARK V. HEDGES -                           *
*                    ADRIA LABORATORIES                         *
*                    7001 POST ROAD                             *
*                    DUBLIN OH 43017                            *
*                    (614) 764-8175                      P.T.L. *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*;
DI       CSECT
         STM    R14,R12,12(R13)       * SETUP
         LR     R12,R15               *
         USING  DI,R12                * ADDRESSABILITY
         B      *+24                  * BRANCH OVER CONSTANTS
         DC     C'&SYSDATE_&SYSTIME_V2.0'
         LA     R10,SAVE              *
         ST     R10,8(R13)            *
         ST     R13,SAVE+4            *
         LR     R13,R10               *
         L      R2,CVTPTR             * GET CVT POINTER
         USING  CVT,R2
***********************************************************************
*                                                                     *
*        FIND THE ACTIVE MAIN SUBSYSTEM SSVT                          *
*        AND CHECK TO SEE IF XA                                       *
*                                                                     *
***********************************************************************
         L     R3,CVTSMCA             * POINT TO SMF
         USING SMCABASE,R3            * ADDRESS SMF
         MVC   SYSID(4),SMCASID       * GET THIST SYSTEMS SYSID
         DROP  R3
         L     R2,CVTJESCT            * POINT TO JESCT
         DROP  R2
         USING JESCT,R2               *
         L     R2,JESSSCT             * POINT TO SSCT
         DROP  R2
         USING SSCT,R2                *
         L     R2,SSCTSSVT            * POINT TO SSVT
         DROP  R2
         USING SSVT,R2                *
***********************************************************************
*                                                                     *
*        FIND THE ACTIVE MAIN SUBSYSTEM'S PITS                        *
*                                                                     *
***********************************************************************
         L     R7,SVTPIT              * POINT TO PITTABLE
         LTR   R7,R7                  * TEST IF ANY PITS
         BZ    NOPITS                 * NO, IGNORE COMMAND
*******  LINK  EP=CLEAR
         TPUT  INITHDR,80             * PUT HEADER
         MODE  AMODE=31               * LET'S GO UP
         LA    R6,36    36            * NUMBER OF CLASSES ALWAYS 36
         USING PIT,R7                 *
***********************************************************************
*        BUILD THE MESSAGE(S) DESCRIBING THE PITS                     *
***********************************************************************
BLDMSG   MVC   $$OUT($$LOUT),BLANKS       CLEAR OUTPUT
         MVC   INIT#(2),PITPATID          MOVE IN PIT ID
         LR    R1,R6                      COPY THE LENGTH
         BCTR  R1,0                       SUBTRACT ONE
         MVC   CLASSES(*-*),PITCLASS      SET THE CLASSES
         EX    R1,*-6                     MOVE THE CLASSES
         TM    PITSTAT,PITHOLDA+PITHOLD1  TEST FOR DRAINED
         BNZ   HOLDPIT                    YES
         TM    PITSTAT,PITHALTA+PITHALT1  TEST FOR HALTED
         BNZ   HALTPIT                    YES
         MVC   STATUS,=CL8'ACTIVE'
         TM    PITSTAT,PITBUSY            TEST FOR BUSY
         BO    ACTPIT                     YES
         MVC   STATUS,=CL8'INACTIVE'
         B     NEXTPIT                    END OF MESSAGE LINE.
HOLDPIT  MVC   STATUS,=CL8'DRAINED'       SET STATUS
         TM    PITSTAT,PITBUSY            TEST FOR BUSY
         BZ    NEXTPIT                    NO
         MVC   STATUS+5(3),=C'ING'        SET STATUS
         B     ACTPIT                     YES
HALTPIT  MVC   STATUS,=CL8'HALTED'        SET STATUS
         TM    PITSTAT,PITBUSY            TEST FOR BUSY
         BZ    NEXTPIT                    NO
         MVC   STATUS+4(3),=C'ING'        SET STATUS
ACTPIT   L     R5,PITSJB                  POINT TO THE SJB
         USING SJB,R5
         MVC   JOBNAME(8),SJBJOBNM        MOVE IN JOBNAME
         MVI   JOBCLAS-1,C'('
         MVC   JOBCLAS(1),SJBJCLAS        JOB CLASS
         MVI   JOBCLAS+1,C')'
         DROP  R5
***********************************************************************
*        SEND THE MESSAGE DESCRIBING THE PIT                          *
***********************************************************************
NEXTPIT  TPUT  $$OUT,$$LOUT               SEND IT
         L     R7,PITNEXT                 POINT TO NEXT PIT
NEXTPIT1 DS    0H
         LTR   R7,R7                      TEST FOR NEXT PIT
         BZ    CLEAR15                    NO, LETS STOP
         B     BLDMSG
***************************
*        END IT ALL       *
***************************
NOPITS   EQU    *
         DROP R5
CLEAR15  EQU    *
         MODE   AMODE=24
         SR     R15,R15              *  ZERO RETURN REGISTER
         L      R13,SAVE+4           * RESTORE OLD SAVE AREA ADDR.
         L      R14,12(R13)          * RETURN ADDRESS
         LM     R0,R12,20(R13)       * REST OF THE REGS
         BR     R14                  * RETURN
INITHDR  DC    C'INIT    STATUS   JOBNAME       CLASSES  SYSID('
SYSID    DC    C'    )'
         DC    CL(80-(*-INITHDR))' '
*
ED5      DC    X'402020202120'
SAVE     DS      18F
SAVE1    DS      F                   * PARM IF ANY ...
SAVBAL   DS      F                   * SAVE AREA FOR 'BALREG'
WORK     DS    0H     COULD BE DSECT
$$OUT    DS    C
INIT#    DC    CL2' '
         DC    CL4' '
STATUS   DC    CL8' '
         DC    CL2' '
JOBNAME  DC    CL9' '
JOBCLAS  DC    CL5' '
CLASSES  DC    CL8' '
$$LOUT   EQU   *-$$OUT
CONVERT  DS    D
BLANKS   DC    CL100' '
##JOBNO  DC    H'0'
         LTORG
 TITLE 'CVT'
         CVT   PREFIX=NO,DSECT=YES
 TITLE 'IEESMCA'
        IEESMCA
 TITLE 'IEFJESCT'
         IEFJESCT TYPE=DSECT
 TITLE 'IEFJSCVT'
         IEFJSCVT ,
 TITLE '$PIT'
         $PIT
 TITLE '$SJB'
         $SJB
 TITLE '$JQE'
         $JQE
 TITLE '$TQE'
         $TQE
 TITLE '$XECB'
         $XECB ,
 TITLE '$SCAT'
         $SCAT ,
 TITLE '$SVT'
         $SVT  ,
 TITLE '$HASPEQU'
         $HASPEQU
         END
