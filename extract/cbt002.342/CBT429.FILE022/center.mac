         MACRO
&N       CENTER &M,&LEN=133,&FILL=' '    LEN=LENGTH OF LINE,
         LCLA  &AL1,&AL2,&AL3            FILL=FILLER CHARS
.*-------------------------------------------------------------------*
.*                       E X A M P L E                               *
.*      OUTFILE   DS   0H                               OUTPUT AREA  *
.*      CARR      DC   C'1'                             TOP OF PAGE  *
.*                CENTER 'T O P   O F   R E P O R T',LEN=132         *
.*-------------------------------------------------------------------*
&AL1     SETA  K'&M
         AIF   ('&M'(1,1) NE '''').A
&AL1     SETA  &AL1-2
.A       ANOP
         AIF   (&AL1 GT &LEN).D
&AL2     SETA  (&LEN-&AL1)/2
&AL3     SETA  &LEN-&AL1-&AL2
.F       ANOP
         AIF   ('&N' EQ '').E
&N       EQU   *
.E       AIF   (&AL3 EQ 0).C
         DC    &AL3.C&FILL
.C       AIF   ('&M'(1,1) EQ '''').B
         DC    CL&AL1'&M'
         AIF   (&AL2 EQ 0).END
         DC    &AL2.C&FILL
         MEXIT
.B       DC    CL&AL1&M
         AIF   (&AL2 EQ 0).END
         DC    &AL2.C&FILL
         MEXIT
.D       ANOP
&AL2     SETA  0
&AL3     SETA  0
         AGO   .F
.END     MEND
