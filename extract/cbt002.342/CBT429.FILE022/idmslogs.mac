 TITLE '>>> PROCESS THE DUMPED IDMS/LOG DATASET WITH STATS AS PARM <<<'
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*  MODULE NAME = IDMSLOGS                                             *
*                                                                     *
*  DESCRIPTIVE NAME =                                                 *
*                                                                     *
*  OPERATING ENVIRONMENT = MVS/XA 2.1.7, IDMS/R 10.1                  *
*                                                                     *
*  FUNCTION = PROCESSES THE OUTPUT OF THE IDMS/R LOG FILES,           *
*             RETRIEVING ONLY THE SHUTDOWN STATS AND REPORTING        *
*             ON THEM                                                 *
*  NOTES =                                                            *
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*           R1 .....                                                  *
*           R2 .....                                                  *
*           R3 .....                                                  *
*           R4 .....                                                  *
*           R5 ..... PAGE COUNT                                       *
*           R6 ..... LINE COUNT                                       *
*           R7 ..... BAL REG                                          *
*           R8 ..... RECORD COUNT                                     *
*           R9 .....                                                  *
*          R10 .....                                                  *
*          R11 ..... BASE+4095                                        *
*          R12 ..... BASE                                             *
*          R13 .....                                                  *
*          R14 .....                                                  *
*          R15 .....                                                  *
*                                                                     *
*             MACROS = IBM: GET, PUT, DCB, OPEN, IEFJFCBN, RDJFCB     *
*                                                                     *
*                    OTHER:                                           *
*                                                                     *
*                                                                     *
*        SUBROUTINES = PRC: JULSUB BIIL G. FROM CBT FILE              *
*                                                                     *
*             DSECTS = IBM:                                           *
*                                                                     *
*                    OTHER:                                           *
*                                                                     *
*             MODULE TYPE = ASMH V2.1                                 *
*                                                                     *
*                PROCESSOR = 4381-11                                  *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
  EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*             ENTRY POINTS = IDMSLOGS (ONLY ENTRY POINT)              *
*                                                                     *
*             INPUT = DDNAME 'SYSLST' OUTPUT FORM IDMS/R LOG DUMP     *
*                                                                     *
*             OUTPUT = DDNAME SYSPRINT, SHUTDOWN STATS ONLY           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*             TABLES =                                                *
*                                                                     *
*             AUTHOR = MARK V. HEDGES  AND  SHEILA KAISER             *
*                      ADRIA LABORATORIES                             *
*                      7001 POST ROAD                                 *
*                      DUBLIN OH. 43017                               *
*                      (614) 764-8175                            PTL  *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE - WE HOPE                            *
*                                                                     *
*                                                                     *
*                                                                     *
* -----------------------  CHANGE SECTION  ---------------------------*
*                                                                     *
* 03/23/88 ADDED                                                      *
*                                                                     *
*                                                                     *
*                                                                     *
* -----------------------  THINGS TO DO  -----------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         EJECT  ,
IDMSLOGS CSECT
         STM   R14,R12,12(R13)         * SAVE PREVIOUS REGS
         LR    R12,R15                 * SAVE PGM BEGIN ADDR
         USING IDMSLOGS,R12,R11        *
         L     R11,SEC4K               *
         LA    R10,SAVE                * ADDR NEW SAVE AREA
         ST    R10,8(R13)              * NEW SAVE ADDR IN OLD SAVE AREA
         ST    R13,SAVE+4              * OLD SAVE ADDR IN NEW SAVE AREA
         LR    R13,R10                 * R13 CONTAINS NEW SAVE ADDR
         XR    R8,R8                   *
PAGECNT  EQU   5                       * RECORD COUNTER
LINECNT  EQU   6                       * RECORD COUNTER
BALREG   EQU   7                       * BAL REG
COUNT    EQU   8                       * RECORD COUNTER
         XR    PAGECNT,PAGECNT
         XR    LINECNT,LINECNT
         XR    COUNT,COUNT
         XC    RC,RC                   * 0 RETURN CODE
         RDJFCB SYSLST
         LTR   R15,R15
         BNZ   ESYSLST
         OPEN  (SYSLST,,SYSPRINT,OUTPUT)
         GETTIME DATE=QDATE
         SPACE
$READ    GET   SYSLST,REC              * READ RECORD
         BAL   BALREG,$READCNT         * INCREASE RECORD COUNT
         CLC   REC+17(17),=C'SYSTEM STATISTICS'
         BNE   $READ
         PACK  JULPARM1(4),REC+1(5)    * PACK DATE
         BAL   BALREG,$JULDATE
         MVC   ORDATE(8),JULPARM3
         MVC   ORDAY(3),JULPARM3+8
         MVI   REC+9,C':'
         MVI   REC+12,C':'
         MVC   ORTIME(8),REC+7
         MVC   ODATE(15),QDATE+1
         BAL   BALREG,$HEADER
         LA    R3,15
$PUTLOOP GET   SYSLST,REC
         BAL   BALREG,$READCNT         * INCREASE RECORD COUNT
         CLI   REC,C'1'
         BNE   $CONT
         GET   SYSLST,REC
         BAL   BALREG,$READCNT         * INCREASE RECORD COUNT
         GET   SYSLST,REC
         BAL   BALREG,$READCNT         * INCREASE RECORD COUNT
         GET   SYSLST,REC
         BAL   BALREG,$READCNT         * INCREASE RECORD COUNT
$CONT    MVI   REC,C'0'
         PUT   SYSPRINT,REC
         BCT   R3,$PUTLOOP
         B     $READ
$HEADER  DS    0H                      *
         A     PAGECNT,=F'1'
         CVD   PAGECNT,DWORD
         MVC   OPAGECNT(4),=X'40202021'
         ED    OPAGECNT(4),DWORD+6
         PUT   SYSPRINT,HEADER         *
         XR    LINECNT,LINECNT         *
         A     LINECNT,=F'1'           *
         BR    BALREG                  *
         SPACE
ESYSLST  DS     0H
         WTO    'MISSING DDNAME SYSLST ...'
         LA     R1,8                   * SET RETURN CODE
         ST     R1,RC                  * RC=08
         B      FINISH                 * GO BACK
$READCNT A      COUNT,=F'1'            * ADD ONE TO RECORD COUNT
         BR     BALREG                 * GO BACK
         SPACE
$JULDATE DS    0H                      * THIS IS A JULIAN DATE
         LA    R1,3                    * THIS IS A JULIAN DATE
         ST    R1,JULPARM2             * ... SAVE IT
         LA    R1,8+128                * WE WANT 'DDJANYY' AND WEEKDAY
         ST    R1,JULPARM4             * ... SAVE THAT
         LA    R1,JULPTRS              * LOAD ADDR OF PARM LIST
         L     R15,=V(JULSUB)          * GET READY ...
         BALR  R14,R15                 * DO IT
         BR    BALREG
         EJECT
$EOF     CLOSE  (SYSLST)               *
         CVD    COUNT,DWORD            * CONVERT
         ED     RECREAD(8),DWORD+4     *
         MVC    ODSN(44),JFCBDSNM      * GET DSN
         MVC    OVOL(6),JFCBVOLS       * GET VOLSER
         PUT    SYSPRINT,RECOUNT       *
         CLOSE  (SYSPRINT)             *
         B      FINISH                 *
FINISH   SR     R15,R15                * ZERO RETURN REGISTER
         L      R15,RC                 * LOAD RETURN CODE
         L      R13,SAVE+4             * RESTORE OLD SAVE AREA ADDR.
         L      R14,12(R13)            * RETURN ADDRESS
         LM     R0,R12,20(R13)         * REST OF THE REGS
         BR     R14                    * RETURN
SAVE     DS     18F                    * SAVE AREA FOR THIS PGM
SAVE1    DS     1F
SEC4K    DC     A(IDMSLOGS+4095)       *
REC      DS     CL133                  * INPUT RECORD
TRTBL    DC     C'0123456789ABCDEF'    * TRANSLATE TABLE FOR RCODE
RC       DS     F                      * RETURN CODE
         DS     0D                     *
DWORD    DS     D                      * DOUBLE WORD
JULPTRS  DC     A(JULPARM1)
         DC     A(JULPARM2)
         DC     A(JULPARM3)
         DC     A(JULPARM4)
         DC     A(JULPARM5)
JULPARM1 DS     F                      * INPUT DATE: 'YYYYDDDF' PACKED
JULPARM2 DS     F                      * INPUT CODE: JULIAN PL4
JULPARM4 DS     F                      * OUTPUT CODE: 'YYMMMDD'
JULPARM3 DS     CL8                    * OUTPUT FIELD
         DS     CL12                   * WEEKDAY CL12
JULPARM5 DS     32D                    * 256 BYE WORK AREA
BLANKS   DC     CL133' '
RECOUNT  DC     CL133'-'
         ORG    RECOUNT+1
         DC     C'DSNAME='
ODSN     DC     CL44' ',C'VOLSER='
OVOL     DC     CL6' ',C'  RECORDS READ='
RECREAD  DC     X'4020202020202021'
         ORG    ,
HEADER   DC     CL133'1'               * OUTPUT OF IDMSLOGS HEADER
         ORG    HEADER+1
         DC     C'SHUTDOWN STATISTICS'
         DC     C'                REPORT DATE='
ORDATE   DC     CL8' '
ORDAY    DC     CL3' ',C' '
ORTIME   DC     CL7' '
         ORG    ,
         ORG    *-16-9-5
ODATE    DC     CL20' '
         DC     C' PAGE'
OPAGECNT DC     CL4' '
         ORG    ,
         SPACE
         PRINT  NOGEN
SYSLST   DCB   DDNAME=SYSLST,DSORG=PS,LRECL=133,RECFM=FBA,             X
               MACRF=GM,EXLST=EXL,EODAD=$EOF
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,BLKSIZE=23408,       X
               RECFM=FBA,MACRF=PM
         SPACE
         DS     0F
EXL      DC     X'87',AL3(JFCB)
JFCB     DS     0H
         IEFJFCBN LIST=NO
         PRINT  GEN
         LTORG
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
