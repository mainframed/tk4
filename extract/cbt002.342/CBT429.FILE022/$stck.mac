         MACRO
&LABEL   $STCK  &GEN=GEN
         LCLC   &L
         GBLB   &SETI
&L       SETC  '&SYSNDX'
.**********************************************************************
.****                                                             *****
.****    THIS MACRO WILL PROVIDE THE DAY, DATE, AND TIME FROM     *****
.****    THE TIME-OF-DAY CLOCK IN GREGORIAN (ENGLISH) FORMAT.     *****
.****                                                             *****
.****   XDAY    DS   X      A BINARY NUMBER (HEX) RELATIVE TO     *****
.****                       THE DAY OF THE WEEK AS FOLLOWS        *****
.****                       0=MONDAY, 1=TUESDAY, 2=WEDNESDAY,     *****
.****                       3=THURSDAY, 4=FRIDAY,                 *****
.****                       5=SATURDAY, 6=SUNDAY                  *****
.****                                                             *****
.****   XWEEKDAY DC  CL9    A CHAR REP OF DAY-OF-WEEK             *****
.****                       'MONDAY'-'SUNDAY'                     *****
.****                                                             *****
.****   XMONTH   DC  CL9    A CHAR REP OF MONTH-OF-YEAR           *****
.****                       'JANUARY'-'DECEMBER'                  *****
.****                                                             *****
.****                                                             *****
.****   XDATE   DS   CL8    AN EIGHT CHARACTER FIELD CONTAINING   *****
.****                       THE DATE IN MM/DD/YY FORMAT           *****
.****                                                             *****
.****   XJDATE  DS   CL6    AN SIX CHARACTER FIELD CONTAINING     *****
.****                       THE DATE IN YY.DDD                    *****
.****                                                             *****
.****   XTIME   DS   CL8    AN EIGHT CHARACTER FIELD CONTAINING   *****
.****                       THE TIME IN HH:MM:SS FORMAT           *****
.****                                                             *****
.****    NOTE THAT THE CALLER MUST PROVIDE A REGISTER SAVE AREA   *****
.****    THAT BEGINS ON A DOUBLEWORD BOUNDARY TO BE USED AS A     *****
.****    WORK AREA BY THIS ROUTINE (R-13 BASED).                  *****
.****                                                             *****
.**********************************************************************
         AIF   ('&GEN' EQ 'NOGEN').NOGEN
         AGO   .START
.NOGEN   ANOP
         PUSH PRINT
         PRINT OFF
.START   ANOP
         AIF   ('&LABEL' EQ '').NOLAB
&LABEL   EQU   *
.NOLAB   ANOP
         STM   0,15,SV&L
         STCK  8(13)              STORE THE TIME-OF-DAY CLOCK
         LM    0,1,8(13)          LOAD INTO WORK REGS
         L     15,16              GET CVT ADDRESS
         A     0,304(15)          ADD LOCAL TIME DEVIATION
         SRDL  0,12               ISOLATE NUMBER OF MICROSECONDS
         D     0,MIN60            DIVIDE BY 60M (R1=MINUTES AFT EPOCH)
         LR    15,0               COPY REMAINDER OF MICS TO GET SECS
         SR    14,14              CLEAR FOR DIVIDE
         D     14,SECR            DIVIDE TO GET REMAINING SECONDS (R5)
         LR    14,15              COPY TO WORK REG
S&L.B    SL    14,SEC60           DECREMENT BY 60 SECONDS
         BM    S&L.S              LESS THAN SIXTY, CONTINUE
         SL    15,SEC60           MORE THAN SIXTY, ADJUST FOR LEAP
         AL    1,=F'1'            BUMP MINUTES
         B     S&L.B              CHECK AGAIN
S&L.S    CVD   15,8(13)           CONVERT SECONDS TO PACKED FORMAT
         UNPK  24(4,13),14(2,13)  UNPACK SECONDS FOR PRINT
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   XTIME+6(2),26(13)   MOVE THE SECONDS
         CVD   1,8(13)            CONVERT MINUTES TO PACKED FORMAT
         DP    8(8,13),PACK60     DIVIDE INTO HOURS AND MINUTES
         UNPK  24(4,13),14(2,13)  UNPACK THE MINUTES
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   XTIME+3(2),26(13)   MOVE THE MINUTES
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),PACK24     DIVIDE INTO DAYS AND HOURS
         UNPK  24(4,13),14(2,13)  UNPACK THE HOURS
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   XTIME(2),26(13)     MOVE THE HOURS
         MVI   XTIME+2,C':'        ADD COLIN
         MVI   XTIME+5,C':'        ADD COLIN
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),PACK7      DIVIDE BY NUMBER OF DAYS IN A WEEK
         ZAP   8(8,13),15(1,13)   FILL DOUBLEW WITH THE REMAINDER
         CVB   0,8(13)            CONVERT RELATIVE DAY TO BINARY
         STC   0,XDAY              SET RELATIVE DAY OF WEEK
.* CODE ADDED BY MVH
         CLI   XDAY,X'00'         MONDAY ?
         BE    MON&L
         CLI   XDAY,X'01'         TUESDAY ?
         BE    TUE&L
         CLI   XDAY,X'02'         WEDNESDAY ?
         BE    WED&L
         CLI   XDAY,X'03'         THURSDAY ?
         BE    THR&L
         CLI   XDAY,X'04'         FRIDAY ?
         BE    FRI&L
         CLI   XDAY,X'05'         SATURDAY ?
         BE    SAT&L
         MVC   XWEEKDAY(9),=CL9'SUNDAY'
         B     ZAP&L
MON&L    MVC   XWEEKDAY(9),=CL9'MONDAY'
         B     ZAP&L
TUE&L    MVC   XWEEKDAY(9),=CL9'TUESDAY'
         B     ZAP&L
WED&L    MVC   XWEEKDAY(9),=CL9'WEDNESDAY'
         B     ZAP&L
THR&L    MVC   XWEEKDAY(9),=CL9'THURSDAY'
         B     ZAP&L
FRI&L    MVC   XWEEKDAY(9),=CL9'FRIDAY'
         B     ZAP&L
SAT&L    MVC   XWEEKDAY(9),=CL9'SATURDAY'
.**** END OF ADDED CODE
ZAP&L    ZAP   16(8,13),8(8,13)   SAVE THE NUMBER OF DAYS
         L     1,16               GET CVT ADDRESS
.* EDIT CVT-DATE
         MVC   XTEMP(4),57(1)     EDIT CVT DATE
         ED    XJDATE-1(7),XTEMP    EDIT CVT DATE
.* LOAD PACKED CVT DATE
         MVC   24(4,13),57(1)     MOVE CVT DATE TO WORK AREA
         ZAP   8(8,13),PACK0      ZERO WORK AREA
         MVO   14(2,13),24(1,13)  MOVE YEAR PORTION
         UNPK  XDATE+6(2),14(2,13) UNPACK INTO OUTPUT AREA
         LA    15,S&L.T           LOAD ADDR OF TABLE
         CVB   1,8(13)            CONVERT TO BIN
         SLL   1,30               SHIFT ALL BUT LAST TWO
         LTR   1,1                TEST RESIDUAL
         BNZ   *+8                NOT 0, BRANCH
         LA    15,S&L.TL          LEAP YEAR
         LR    14,15              SAVE TABLE IN USE
         ZAP   8(8,13),25(2,13)   MOVE DAY PORTION
         CVB   0,8(13)            CONVERT TO BIN
         SR    1,1                CLEAR REG
         IC    1,0(15)            LOAD CHAR FROM TABLE
         LA    15,1(15)           INDEX TABLE POINTER
         SR    0,1                SUBTRACT FROM WORK VALUE
         BP    *-10               STILL POSITIVE, DO AGAIN
         AR    0,1                ELSE, ADD IT BACK
         CVD   0,8(13)            CONVERT TO DEC
         UNPK  XDATE+3(2),14(2,13) UNPACK DAY INTO OUTPUT
         SR    15,14              FIND VALUE OF CURRENT INDEX
         CVD   15,8(13)           CONVERT TO DEC
         UNPK  XDATE(2),14(2,13)   UNPACK MONTH INTO OUTPUT
         OI    XDATE+1,C'0'        INSURE NUMERICS
         OI    XDATE+4,C'0'        INSURE NUMERICS
         OI    XDATE+7,C'0'        INSURE NUMERICS
         MVI   XDATE+2,C'/'        ADD SLASH
         MVI   XDATE+5,C'/'        ADD SLASH
.* ADDED CODE FOR XMONTH - MVH
         CLC   XDATE(2),=C'01'     JAN
         BE    JAN&L
         CLC   XDATE(2),=C'02'     FEB
         BE    FEB&L
         CLC   XDATE(2),=C'03'     MAR
         BE    MAR&L
         CLC   XDATE(2),=C'04'     APR
         BE    APR&L
         CLC   XDATE(2),=C'05'     MAY
         BE    MAY&L
         CLC   XDATE(2),=C'06'     JUN
         BE    JUN&L
         CLC   XDATE(2),=C'07'     JLY
         BE    JLY&L
         CLC   XDATE(2),=C'08'     AUG
         BE    AUG&L
         CLC   XDATE(2),=C'09'     SEP
         BE    SEP&L
         CLC   XDATE(2),=C'10'     OCT
         BE    OCT&L
         CLC   XDATE(2),=C'11'     NOV
         BE    NOV&L
         MVC   XMONTH(9),=CL9'DECEMBER'
         B     MEN&L
JAN&L    MVC   XMONTH(9),=CL9'JANUARY'
         B     MEN&L
FEB&L    MVC   XMONTH(9),=CL9'FEBRURAY'
         B     MEN&L
MAR&L    MVC   XMONTH(9),=CL9'MARCH'
         B     MEN&L
APR&L    MVC   XMONTH(9),=CL9'APRIL'
         B     MEN&L
MAY&L    MVC   XMONTH(9),=CL9'MAY'
         B     MEN&L
JUN&L    MVC   XMONTH(9),=CL9'JUNE'
         B     MEN&L
JLY&L    MVC   XMONTH(9),=CL9'JULY'
         B     MEN&L
AUG&L    MVC   XMONTH(9),=CL9'AUGUST'
         B     MEN&L
SEP&L    MVC   XMONTH(9),=CL9'SEPTEMBER'
         B     MEN&L
OCT&L    MVC   XMONTH(9),=CL9'OCTOBER'
         B     MEN&L
NOV&L    MVC   XMONTH(9),=CL9'NOVEMBER'
MEN&L    LM    0,15,SV&L
         B     S&L.E              BRANCH AROUND CONSTANTS
S&L.T    DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31) MONTH TABLE
S&L.TL   DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31) LEAP YEAR
         AIF   (&SETI).MEND
&SETI    SETB  1
MIN60    DC    F'60000000'
SECR     DC    F'951424'
SEC60    DC    F'60'
PACK60   DC    P'60'
PACK24   DC    P'24'
PACK7    DC    P'7'
PACK0    DC    P'0'
SV&L     DS    18F
XDAY     DC    XL1'00'
XDATE    DC    CL8'  /  /  '
XWEEKDAY DC    CL9' '
XMONTH   DC    CL9' '
XTIME    DC    CL8'  :  :  '
*                Y Y . D D D
         DC    X'40'
XJDATE   DC    X'21204B202020'
XTEMP    DC    CL4' '
S&L.E    DS    0H
.MEND    AIF   ('&GEN' EQ 'NOGEN').NOGEN2
         AGO   .SKIP
.NOGEN2  ANOP
         POP  PRINT
         PRINT ON
.SKIP    MEND
