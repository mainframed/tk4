 TITLE '>>>  ISSUE A NON-ROLL WTO  <<<'
*-------------------------------------------------------------------*
*   NAME: WTONROLL                                                  *
*                                                                   *
*   FUNCTION: TO ISSUE A MESSAGE TO THE CONSOLE VIA PARM PASSED     *
*             BY A PROGRAM. GOOD FOR CONDITION CODE CHECKING        *
*             FOR ABENDED OR NON-ZERO RETURN CODES                  *
*                                                                   *
*                                                                   *
*   MACROS: WTO, REGS                                               *
*                                                                   *
*   EXECUTION: //S1  EXEC PGM=WTONROLL,PARM='MESSAGE TO OPERATOR'   *
*                                                                   *
*             AUTHOR = MARK V. HEDGES                               *
*                      ADRIA LABORATORIES                           *
*                      7001 POST ROAD                               *
*                      DUBLIN OH. 43017                             *
*                      (614) 764-8175                          PTL  *
*                                                                   *
*                                                                   *
*-------------------------------------------------------------------*
         USING  *,R15
         L      R2,0(,R1)              * LOAD PARM ADDRESS
         LH     R3,0(,R2)              * LOAD LENGTH OF PARM
         LTR    R3,R3                  * IS THERE ANY PARM ??
         BZ     EXIT                   * NOPE, EXIT
         LA     R4,WTOCMD+8            * ADDRESS INTO MESSAGE
         BCTR   R3,0                   * FOR PARM LENGTH <EXEC>
         MVC    0(*-*,R4),2(R2)        * <<EXCUTED>> MOVE PARM
         EX     R3,*-6                 * DO IT
WTOCMD   WTO   '                                                       X
                             ',                                        X
               ROUTCDE=(1),DESC=(11)
EXIT     EQU    *
         DROP   R15                    *
         BR     R14
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
