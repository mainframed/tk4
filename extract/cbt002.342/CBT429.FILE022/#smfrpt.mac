//#SMFRPT JOB
//*-------------------------------------------------------------------*
//*   DOC: CREATE ALL REPORTS PRODUCED BY THE SMFDUMP ROUTINE IN THIS *
//*        LIBRARY:                                                   *
//*   _ STEP _   _________ FUNCTION ___________                       *
//*                                                                   *
//*     SMFCNT   COUNT AND DESCRIBE ALL SMF RECORDS                   *
//*                                                                   *
//*     SMF5     PROCESSES SMF TYPE 5 RECORDS                         *
//*                                                                   *
//*                                                                   *
//*                                                                   *
//*                                                                   *
//*-------------------------------------------------------------------*
//SMFCNT  EXEC SAS,OPTIONS='NONEWS'
//SMFDATA DD DSN=SMFDUMP,DISP=SHR
//SYSOUT   DD DUMMY
//SYSIN    DD *
  OPTIONS NOSOURCE2 NODATE ERRORS=0 LABEL NOMACROGEN ;

 DATA A (KEEP=TYPE DATE TIME NUM)
         ;
    INFILE SMFDATA;
    INPUT
     @ 2 TYPE PIB1.
     @ 3 ENDTIME    SMFSTAMP8.
     @;
    NUM=TYPE;
    DATE=DATEPART(ENDTIME) ;
    TIME=TIMEPART(ENDTIME) ;
    LABEL DATE='REPORT*DATE'  TIME='REPORT*TIME';
    FORMAT DATE DATE7. TIME TIME8.  ;
    DROP ENDTIME;

PROC SUMMARY NWAY DATA=A;CLASS DATE NUM;ID TYPE;OUTPUT OUT=B;

* TOTALS BY DATE;
PROC SUMMARY NWAY DATA=A; CLASS DATE; OUTPUT OUT=C;
DATA C (KEEP=DATE N); SET C; N=_FREQ_;

PROC FORMAT ;
     VALUE SMFR
      0='IPL'                     2='DUMP HEADER'
      3='DUMP TRAILER'            4='STEP TERMINATION'
      5='JOB TERMINATION'         6='JES2 WRITER'
      7='DATA LOST'               8='I/O CONFIG'
      9='VARY ONLINE'            10='ALLOC RECOVERY'
     11='VARY OFFLINE'           14='OPEN INPUT'
     15='OPEN OUTPUT'            16='SORT/MERGE'
     17='SCRATCH NONVSAM'        18='RENAME NONVSAM'
     19='DIRECT ACCESS'          20='JOB INITIALIZATION'
     21='VOLUME ERRORS'          22='CONFIGURATION'
     23='SMF STATUS'             25='JES3 DEVICE ALLOC'
     26='JOB PURGE'              30='JOB/TSU/STC TERM'
     31='TIOC INIT'              32='TSO ACCOUNTING'
     34='TSU TERM'               35='LOGOFF'
     38='NPM STATS.'             39='NLDM STATS.'
     40='DYNAMIC ALLOC'          43='JES2 START'
     45='JES2 WITHDRAWAL'        47='JES2 SIGNON/S LINE'
     48='JES2 SIGNOFF/P LINE'    49='JES2 INTEGRITY'
     50='ACF/VTAM STATS'         52='JES2 LOGON (SNA)'
     53='JES2 LOGOFF (SNA)'      54='JES2 INTEGRITY (SNA)'
     55='JES2 NETWORK SIGNON'    56='JES2 NETWORK INTEGRITY'
     57='JES2 NETWORK SYSOUT'    58='JES2 NETWORK SIGNOFF'
     60='VSAM VOL UPDATE'        61='ICF DEFINE'
     62='VSAM CLUSTER OPEN'      63='VSAM DEFINE'
     64='VSAM CLUSTER STATUS'    65='ICF DELETE'
     66='ICF ALTER'              67='VSAM DELETE'
     68='VSAM RENAME'            69='DEFINE VSAM SPACE'
     70='RMF: CPU'               71='RMF: PAGING'
     72='RMF: WORKLOAD'          73='RMF: CHANNEL PATH'
     74='RMF: DEVICE'            75='RMF: PAGE/SWAP'
     76='RMF: TRACE'             77='RMF: ENQUEUE'
     78='RMF: MINITOR I'         79='RMF: MONITOR II'
     80='RACF PROCESSING'        81='RACF INITIALIZATION'
     90='SYSTEM STATUS'
     128='ROSCOE'               129='CA/DISPATCH SMF REC6'
     133='HSM133'               134='HSM134'
     ;
PROC SORT  DATA=B;  BY DATE;
PROC SORT  DATA=C;  BY DATE;
DATA MERGE;  MERGE B C; BY DATE;
     PERCENT=(_FREQ_/N)*100;
PROC PRINT DATA=MERGE LABEL NOOBS ;
     BY DATE;
     SUM _FREQ_ PERCENT;
     VAR TYPE NUM _FREQ_ PERCENT;
     FORMAT TYPE 2. NUM SMFR. PERCENT 3.2;

/*
//SMF5    EXEC SAS,OPTIONS='NONEWS'
//SMFDATA DD DSN=SMFDUMP,DISP=SHR
//SYSOUT   DD DUMMY
//SYSIN    DD *
  OPTIONS NOSOURCE2 NODATE ERRORS=0 LABEL NOMACROGEN ;

 DATA SMF5 (KEEP=TYPE ENDTIME SYSID JOBNAME READTIME NUMSTPS
                 INITTIME INITDTE INITME WAITINIT
                 ELAPSED
                 RESDTIME JCLASS SRBTME TRANTIME PROGNAME TCBTME
                 CPUTIME WAIT XREAD SHOUR)
      ;
    INFILE SMFDATA;
    INPUT
     @ 2 TYPE PIB1.
     @ 3 ENDTIME    SMFSTAMP8.
     @;
    NUM=TYPE;
    DATE=DATEPART(ENDTIME) ;
    TIME=TIMEPART(ENDTIME) ;
    LABEL DATE='REPORT*DATE'  TIME='REPORT*TIME';
    FORMAT DATE DATE7. TIME TIME8.  ;
    DROP ENDTIME;

    IF TYPE=5 THEN DO;
     LABEL TYPE='REC*TYPE'                 ENDTIME='JOB*END*TIME'
           SYSID='SYSTEM*ID'               JOBNAME='JOB*NAME'
           READTIME='READER*TIME'          NUMSTPS='NUMBER*OF*STEPS'
           INITTIME='INIT*STRT*TIME*DATE'  INITDTE='INIT*STRT*DATE'
           INITME='INIT*STRT'              WAITINIT='INIT*WAIT*TIME'
           ELAPSED='JOB*ELAPSED*TIME'
           RESDTIME='PROGRAM*RESIDENCY*TIME'
           JCLASS='JOB*CLASS'              SRBTME='SRB*TIME'
           TRANTIME='TRANS*TIME'           PROGNAME='PROGRAMMER*NAME'
           TCBTME='TCB*TIME'               CPUTIME='CPU*TIME'
           WAIT='JOB*SWAPPED*SEC'          XREAD='READER*STRT*TIME'
           ;
   FORMAT ENDTIME READTIME INITTIME DATETIME19.2
          INITDTE DATE7.
          TRANTIME RESDTIME ELAPSED XREAD INITME WAITINIT CPUTIME
                  TIME8.
          SHOUR 2.
          ;
         INPUT @3  ENDTIME   SMFSTAMP8.
               @11 SYSID     $4.
               @15 JOBNAME   $8.
               @23 READTIME  SMFSTAMP8.
               @39 NUMSTPS   PIB1.
               @40 INITTIME  SMFSTAMP8. @;
     * DETERMINE TIME WAITING FOR INITIATOR ;
       XREAD=TIMEPART(READTIME);
       WAITINIT=INITTIME-READTIME;
     * DETERMINE STEP ELAPSED TIME;
       ELAPSED=(ENDTIME-INITTIME);

     * DATE AND TIME OF INITIATOR START TIMES;
       INITDTE=DATEPART(INITTIME);
       INITME=TIMEPART(INITTIME);
       SHOUR=HOUR(INITTIME);

       INPUT @65  RESDTME PIB4.     /* TRANSACTION RESD TIME  */
             @72  JCLASS  $1.       /* JOBCLASS               */
             @74  SRBTME  PIB3.     /* TIME DISPATCHED VIA SRB*/
             @81  TRANTME PIB4.     /* TRANSACTION ACT  TIME  */
             @94  PROGNAME $20.     /* PROGRAMMER NAME FIELD  */
             @114 TCBTME  PIB3. ;   /* TIME DISPATCHED VIA TCB*/

         * REMOVE ANY STARTED TASKS;
           IF JCLASS=' ' THEN DELETE;

         * TOTAL RESIDENCY TIME X 1024 MICROSECONDS;
           RESDTIME=RESDTME*1.024;
           IF RESDTME=0 THEN DELETE;   *** NO CYCLES USED !!!;

         * TOTAL TRANS ACT TIME X 1024 MICROSECONDS;
           TRANTIME=TRANTME*1.024;

         * TOTAL CPU TIME SRB+TBB IN 100TH OF A SECOND;
           CPUTIME=(SRBTME+TCBTME)/100;

         * WAIT TIME FOR JOB: SWAPPED FOR CPU OR I/O;
           WAIT=((RESDTIME/100)-CPUTIME)/1000;

           TRANTIME=TRANTIME/1000;
           RESDTIME=RESDTIME/1000;

           OUTPUT SMF5 ;
     END;

  PROC TABULATE DATA=SMF5;
       CLASS INITDTE JCLASS ;
       VAR CPUTIME ;
       TABLE (INITDTE ALL), JCLASS*CPUTIME*(N MEAN) / RTS=16
       ;
       TITLE 'INITIATOR CLASSES BY CPU SECOND' ;

