//DATE80     JOB (000000,TECH),'HEDGESM',
//            CLASS=Z,MSGCLASS=X,
//            NOTIFY=HEDGESM
//*-----------------------------------------------*
//*   LIB: TS.SOURCE.CNTL(DATE80)
//*   DOC: ASSEMBLE AND LINK USING ASMHCL
//*-----------------------------------------------*
//ASMHCL  EXEC ASMHCL
//C.SYSLIB  DD  DSN=SYS1.MACLIB,DISP=SHR,DCB=BLKSIZE=32000
//          DD  DSN=SYS1.AMODGEN,DISP=SHR
//          DD  DSN=HEDGESM.SOURCE,DISP=SHR
//SYSIN    DD  *
 TITLE '  DATE 80 CHAR RECORD ADDING DATE + PAGE #'
DATE80   CSECT
         STM    R14,R12,12(R13)       * SETUP
         LR     R12,R15               *
         USING  DATE80,R12              * BASES
         LA     R10,SAVE              *
         ST     R10,8(R13)            *
         ST     R13,SAVE+4            *
         LR     R13,R10               *
         ST    R1,PARM
         $STCK GEN=NOGEN
         MVC    DATE(8),XDATE           * SAVE TODAY MM/DD/YY - JUST DD
         L      R1,PARM
         L      R2,0(,R1)
         LH     R3,0(,R2)
         LTR    R3,R3
         BZ     $OPEN1
         CLC    2(9,R2),=C'OVERPRINT'
         BNE    $OPEN1
         MVI    PRTFLG,X'FF'
*
$OPEN1   OPEN   (SYSCHK,)               * FIGURE NUMBER OF PAGES
         XR     R2,R2
         XR     R3,R3
$LOOP1   GET    SYSCHK
         CLI    0(R1),C'1'
         BE     $TOP
         CLI    0(R1),C'+'
         BE     $LOOP1
         CLI    0(R1),C'-'
         BE     $$ADD3
         CLI    0(R1),C'0'
         BE     $$ADD2
         B      $$ADD1
$$ADD3   A      R2,=F'1'                * LINE COUNTER+1
         C      R2,=F'59'
         BH     $TOP
$$ADD2   A      R2,=F'1'                * LINE COUNTER+1
         C      R2,=F'59'
         BH     $TOP
$$ADD1   A      R2,=F'1'                * LINE COUNTER+1
         C      R2,=F'59'
         BL     $LOOP1
$TOP     A      R3,=F'1'                * PAGE COUNTER
         XR     R2,R2
         B      $LOOP1
$PAGEOF  DC     F'0'
$EOF     ST     R3,$PAGEOF
         CVD    R3,DWORD
         UNPK   $PGOF(2),DWORD(8)
         OI     $PGOF+1,X'F0'
         CLOSE  (SYSCHK)
*
$OPEN2   OPEN   (SYSUT1,,SYSUT2,OUTPUT) * OPENFILES
         SPACE
         XR     R3,R3                   * INIT PAGE COUNT
         XR     R2,R2                   * INIT LINE COUNT
         XR     R6,R6                   * ZERO
         RDJFCB SYSUT1
         LTR    R15,R15
         BNZ    $ERROR
LOOP     GET    SYSUT1,RECORD           * GET RECORD
         CLI    RECORD,C'1'             * IS THIS T-O-P
         BE     PUTPAGE                 * YEP, BRANCH
         C      R3,=F'0'
         BE     PUTPAGE
         MVC    OFILE+1(79),RECORD+1    * PUT COLS 2-80 ON OUT FILE
         MVC    OFILE(1),RECORD+0       * PUT CC. IN COL 1
         PUT    SYSUT2,OFILE            * NOPE, WRITE RECORD
         CLI    OFILE,C'+'              * OVER PRINT
         BE     LOOP                    * NO EXTRA LINE
         CLI    PRTFLG,X'FF'
         BNE    *+8
         BAL    R6,$OVERPRT             * OVERPRINT
         CLI    OFILE,C'-'              * TRIPLE PRINT
         BE     $ADD3                   *
         CLI    OFILE,C'0'              * DOUBLE SPACE
         BE     $ADD2                   *
         B      $ADD1                   *
$ADD3    A      R2,=F'1'                * LINE COUNTER+1
         C      R2,=F'59'
         BH     PUTPAGE-4
$ADD2    A      R2,=F'1'                * LINE COUNTER+1
         C      R2,=F'59'
         BH     PUTPAGE-4
$ADD1    A      R2,=F'1'                * ADD ONE TO LINE COUNT
         C      R2,=F'59'
         BH     *+8
         B      LOOP                    * RETURN ...
         MVI    FLG,X'FF'               * SET PAGE OVERFLOW FLAG
PUTPAGE  DS     0H
         MVC    OFILE(81),=81C' '
         A      R3,=F'1'                * ADD ONE TO PAGE COUNTER
         XR     R2,R2                   * CLEAR LINE COUNTER
         CVD    R3,DWORD                * CONVERT PAGE COUNTER
         ED     EDIT(4),DWORD+6         * EDIT PAGE COUNTER
         MVC    PAGE-1(4),EDIT+1          * PUT PAGE COUNTER
         MVC    EDIT(4),EDIT1           * RESET EDIT CHARS
         CLI    FLG,X'FF'
         BE     OVERFLW#
         LA     R4,RECORD+1-1           * LOAD PAST CC
         LA     R5,80                   * RECORD LENGTH
LOOKEND  LA     R4,1(R4)                * NEXT PLACE
         CLI    0(R4),C' '
         BNE    FOUNDEND
         BCT    R5,LOOKEND
         B      PUTCC
FOUNDEND BCTR   R5,0
         SPACE
* NO MATER WHERE THE TITLE LINE IS PUT IT IN COLUMN '2' OF OUTPUT LINE
         SPACE
         MVC    OFILE+1(*-*),0(R4)      * MOVE HEADER
         EX     R5,*-6
PUTCC    MVI    OFILE,C'1'              * PUT CC. IN COL 1
         MVC    OFILE+81-LHDR(LHDR),HEADER
         MVC    SAVEHDR(81),=81C' '     * CLEAR HEADER
         MVC    SAVEHDR(81),OFILE       * SAVE HEADER
PUTOFILE PUT    SYSUT2,OFILE            * WRITE RECORD
         CLI    PRTFLG,X'FF'
         BNE    *+8
         BAL    R6,$OVERPRT             * OVERPRINT
         MVC    OFILE(81),=81C' '
         B      LOOP
OVERFLW# MVC    OFILE(81),SAVEHDR
         MVC    OFILE+81-LHDR(LHDR),HEADER
         PUT    SYSUT2,OFILE            * WRITE RECORD
         CLI    PRTFLG,X'FF'
         BNE    *+8
         BAL    R6,$OVERPRT             * OVERPRINT
         MVI    FLG,X'00'
         MVC    OFILE(81),=81C' '
         B      LOOP
$OVERPRT DS     0H
         MVC    XFILE(80),OFILE
         MVI    XFILE,C'+'
         PUT    SYSUT2,XFILE
         MVC    XFILE(81),=81C' '
         BR     R6                      * RETURN
EOF      CLOSE  (SYSUT1)
         CLC    JFCBDSNM(4),=C'JES2'
         BE     CLOSE
         LA     R2,DSNINFO+5
         LA     R3,JFCBDSNM
         LR     R4,R3
$AGAIN   A      R4,=F'1'
         CLI    0(R4),C' '
         BNE    $AGAIN
         SR     R4,R3
         MVC    0(*-*,R2),JFCBDSNM
         EX     R4,*-6
         AR     R2,R4
         CLI    JFCBELNM,C' '
         BE     $GETVOL
         MVI    0(R2),C'('
         MVC    1(8,R2),JFCBELNM
         MVI    9(R2),C')'
         A      R2,=F'10'
$GETVOL  MVC    4(6,R2),JFCBVOLS
         PUT    SYSUT2,DSNINFO
CLOSE    CLOSE  (SYSUT2)
         SR     R15,R15              *  ZERO RETURN REGISTER
         B      *+8
$ERROR   L      R15,=F'16'
         L      R13,SAVE+4           * RESTORE OLD SAVE AREA ADDR.
         L      R14,12(R13)          * RETURN ADDRESS
         LM     R0,R12,20(R13)       * REST OF THE REGS
         BR     R14                  * RETURN
HEADER   DS     0H
DATE     DC     CL8'  /'
         DC     CL4' PG '
PAGE     DC     CL3' '
         DC     C'/ '
$PGOF    DC     CL3' '
LHDR     EQU    *-HEADER
EDIT     DC     X'40202021'
EDIT1    DC     X'40202021'
RECORD   DC     CL80' '
FLG      DC     X'00'
OFILE    DC     CL81' '
XFILE    DC     CL81' '
SAVEHDR  DC     CL81' '
         DS     0D
DWORD    DS     D
ANY      EQU    *
PARM     DS     F
DSNINFO  DC     CL81'-DSN='
PRTFLG   DC     X'00'
         SPACE
         PRINT NOGEN
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,LRECL=80,MACRF=(GM),RECFM=FB,    X
               EODAD=EOF,EXLST=EXL
SYSCHK   DCB   DDNAME=SYSUT1,DSORG=PS,LRECL=80,MACRF=(GL),RECFM=FB,    X
               EODAD=$EOF
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,LRECL=80,MACRF=(PM),RECFM=FBA,   X
               BLKSIZE=800
LASTADDR EQU   *
SAVE     DS     18F
EXL      DC    X'87',AL3(JFCB)
JFCB     EQU   *
         IEFJFCBN  LIST=YES
         LTORG
         REGS
         END
//L.SYSLMOD DD  DSN=HEDGESM.LINKLIB(DATE801),DISP=SHR
