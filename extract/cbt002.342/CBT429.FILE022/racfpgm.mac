         MACRO
&NAME    RACFPGM  &PGMNAME
.*===================================================================
.*      RACF PROGRAM CHECKING FRONT - END MODULE                    =
.*===================================================================
         LCLA   &A
         AIF    (K'&PGMNAME GT 8).ERROR1   PGMNAME > 8
.EQUATE  ANOP
R&A      EQU    &A
&A       SETA   &A+1
         AIF    (&A LT 16).EQUATE
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                         *
*  THIS PROGRAM WILL 'FRONT-END' A PROGRAM MODULE AND     *
*                                                         *
*  PERFORM A 'RACHECK' FOR ENTITY 'PGMNAME' FOR PROGRAM   *
*                                                         *
*  '&PGMNAME'.  ALL AUTHORIZED USERS OF THIS PROGRAM MUST *
*                                                         *
*  HAVE THEIR PROFILE DEFINED WITH THE 'PGMNAME' ENTITY   *
*                                                         *
*  AUTHORIZED TO USE '&PGMNAME'                           *
*                                                         *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
RACFPGM  CSECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING RACFPGM,R12
         GETMAIN R,LV=72          GET A SAVEAREA
         ST    R13,4(1)
         ST    R1,8(R13)
         LR    R13,R1
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*   RACHECK INVOKES RACF TO SE IF USER IS AUTHORIZED      *
*   TO EXECUTE THIS PROGRAM.  RC=0 SAYS IT'S OK.          *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
RACHECK  RACHECK ENTITY=PGMNAME,CLASS='PGM1',ATTR=READ
         LTR   R15,R15                   IS USER AUTHORIZED
         BZ    CALLPGM                 YES, GO CALL PROGRAM
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*   USER IS NOT AUTHORIZED TO USE PROGRAM SO WRITE A      *
*   MESSAGE TO SECURITY CONSOLE & USER, THEN ABEND.       *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         XR    R0,R0
         WTO   ('USER IS NOT AUTHORIZED',D),                           X
               ('TO USE THE PROGRAM &PGMNAME.',DE),                    X
               ROUTCDE=(9,11),DESC=6
         ABEND 913,,STEP
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*   USER IS AUTHORIZED TO INVOKE THIS PROGRAM.            *
*   CLEAN UP THEN BRANCH TO THE PROGRAM.                  *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
CALLPGM  L     R11,4(13)
         FREEMAIN R,LV=72,A=(13)        FREE UP SAVE AREA
         LR    R13,R11                  R13=OLD SAVE AREA
         L     R14,12(R13)              SET RETURN TO MVS
         L     R15,PGMADDR              GET PROGRAM ADDRESS
         LM    R0,R12,20(R13)           RESTORE REGS
         BR    R15                      GO TO PROGRAM
PGMNAME  DC    CL8'&PGMNAME'
PGMADDR  DC    V(&PGMNAME)
    MNOTE ' '
    MNOTE '*********************************************************'
    MNOTE '*                                                       *'
    MNOTE '*   WHEN LINKING THE NEW MODULE "&PGMNAME", MAKE SURE   *'
    MNOTE '*                                                       *'
    MNOTE '*   TO "ORDER" THE MODULES WITH "RACFPGM(P)" ON A       *'
    MNOTE '*                                                       *'
    MNOTE '*   PAGE BOUNDARY AND AS THE ENTRY POINT INTO THE       *'
    MNOTE '*                                                       *'
    MNOTE '*   MODULE.  ALSO INCLUDE ANY ALIASES NECESSARY         *'
    MNOTE '*                                                       *'
    MNOTE '*********************************************************'
    MNOTE ' '
         END   RACFPGM
         AGO   .MEND
.ERROR1 MNOTE 12,'PROGRAM NAME  "&PGMNAME"  GREATER THAN 8 CHARACTERS'
.MEND    MEND
