         TITLE 'IEESK03D -   #C  CALCULATOR/CONSOLE'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESK03D -       #C  CALCULATOR/CONSOLE                   ***
***                                                                 ***
***       VERSION -        1.1                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITHOUT  SE2 OR SU64.     ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANLE ESTAE            ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESK03D CSECT
         LR    R12,R15
         USING IEESK03D,R12
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         MVC   WTOLIST(4),=X'00300100'
         MVI   ANSWER,C' '         CLEAR
         MVC   ANSWER+1(43),ANSWER    WTO AREA
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         L     R3,XAL              LOAD SUB-PARM ADDR
         LTR   R3,R3               SUB-PARM PRESENT
         BNZ   CALC                YES, CONTINUE
         STCK  DOUBLE              STORE CLOCK
         UNPK  ANSWER(9),DOUBLE(5)
         NC    ANSWER(8),=8X'0F'
         TR    ANSWER(8),=C'0123456789ABCDEF'
         MVI   ANSWER+8,C' '
         SPACE 3
EXIT     EQU   *
         ESTAE 0
         LA    R1,WTOLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
EXIT1    L     R0,SIZE             LOAD FREEMAIN SIZE
         LR    R1,R13              LOAD FREEMAIN ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14                 EXIT
         EJECT
SMFIDISP L     R4,16               CVT
         L     R4,196(0,R4)        SMCA
         MVC   SMFLIST(4),=X'00340100'
         MVC   SMFANS(48),=CL48'PRIMARY SMF DS -     SYS1.MAN'
         MVC   SMFANS+29(1),35(R4) ACTIVE SMF DSN ID
         MVC   SMFANS+33(6),24(R4) ACTIVE SMF VOLSER
         TM    30(R4),X'10'        DS EMPTY
         BZ    *+10                NO
         MVC   SMFANS+42(5),=C'EMPTY'
         LA    R1,SMFLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
         MVC   SMFANS(48),=CL48'ALTERNATE SMF DS -   SYS1.MAN'
         MVC   SMFANS+29(1),51(R4) ALT SMF DSN ID
         MVC   SMFANS+33(6),40(R4) ALT SMF VOLSER
         TM    46(R4),X'10'        DS EMPTY
         BZ    *+10                NO
         MVC   SMFANS+42(5),=C'EMPTY'
         LA    R1,SMFLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
         B     EXIT1
         EJECT
CALC     CLI   0(R3),C'M'          MSG REQUEST
         BE    MSGREQ              YES
         CLI   0(R3),C'B'          BROADCAST REQUEST
         BE    BRODCST             YES
         CLI   0(R3),C'S'          SMF ID DISPLAY REQUEST
         BE    SMFIDISP            YES
         CLI   0(R3),C'X'          LXYZ TRANSLATE REQUEST
         BE    LXYZREQ             YES
         CLI   0(R3),C'Z'          MSTCONS SWITCH REQUEST
         BE    MSTCONS             YES
         SPACE 1
CALC0    LA    R10,FIELDA          LOAD 1ST FIELD ADDR
         LA    R11,CALC2           RETURN POINT
         SPACE 3
CALC1    CLI   0(R3),C','          DECIMAL VALUE
         BE    DECCONV
         B     HEXCONV             HEX VALUE
         SPACE 3
CALC2    MVC   FUNCTION(1),0(R3)   SAVE FUNCTION CODE
         MVC   RESULT(4),FIELDA    SAVE 1ST FIELD IF JUST CONVERT
         CLI   FUNCTION,C' '       CONVERT FUNCTION
         BE    CONVERT             YES, CONTINUE
         LA    R3,1(0,R3)          NEXT VALUE START
         LA    R10,FIELDB          2ND FIELD ADDR
         LA    R11,CALC3           RETURN POINT
         B     CALC1
         SPACE 3
CALC3    CLI   0(R3),C' '          BLANK DELIMETER
         BNE   PARMERR             NO, ERROR
         SR    R4,R4               INIT FUNCTION WORK REGS
         L     R5,FIELDA
         L     R6,FIELDB
         CLI   FUNCTION,C'+'       ADD
         BE    ADD
         CLI   FUNCTION,C'-'       SUBTRACT
         BE    SUB
         CLI   FUNCTION,C'.'       MULTIPLY
         BE    MLT
         CLI   FUNCTION,C'/'       DIVIDE
         BE    DIV
         B     PARMERR             ERROR, INVALID FUNCTION
         EJECT
DECCONV  LA    R5,9                MAX LENGTH
         LA    R3,1(0,R3)          1ST BYTE OF FIELD
         CLI   0(R3),C'0'          VALID
         BL    PARMERR             NO, ERROR
         CLI   0(R3),C'9'          VALID
         BH    PARMERR             NO, ERROR
         LR    R6,R3               SAVE FIELD ADDR
         SPACE 3
DECCONV1 CLI   0(R3),C'0'          VALID
         BL    DECCONV2            NO
         CLI   0(R3),C'9'          VALID
         BH    DECCONV2            NO
         LA    R3,1(0,R3)          NEXT BYTE
         BCT   R5,DECCONV1
         B     PARMERR             ERROR, FIELD TOO LONG
         SPACE 3
DECCONV2 LR    R1,R3               CALCULATE
         BCTR  R1,0                   LENGTH
         SR    R1,R6
         EX    R1,PACK             PACK,
         CVB   R1,DOUBLE              CONVERT AND
         ST    R1,0(0,R10)               SAVE RESULT
         BR    R11                 EXIT
         SPACE 3
PACK     PACK  DOUBLE(8),0(1,R6)
         EJECT
HEXCONV  LA    R5,9                MAX LENGTH
         LR    R6,R3               SAVE FIELD ADDR
         SPACE 3
HEXCONV1 CLI   0(R3),C'A'          VALID
         BL    HEXCONV4            NO
         CLI   0(R3),C'F'          VALID
         BNH   HEXCONV2            YES
         CLI   0(R3),C'0'          VALID
         BL    HEXCONV4            NO
         CLI   0(R3),C'9'          VALID
         BH    HEXCONV4            NO
         NI    0(R3),X'0F'         SET UP FOR TRANSLATE LATER
         B     HEXCONV3
         SPACE 3
HEXCONV2 NI    0(R3),X'0F'         SET
         SR    R1,R1                  UP
         IC    R1,0(0,R3)                FOR
         LA    R1,9(0,R1)                   TRANSLATE
         STC   R1,0(0,R3)                      LATER
         SPACE 3
HEXCONV3 LA    R3,1(0,R3)          NEXT BYTE
         BCT   R5,HEXCONV1
         B     PARMERR             ERROR, FIELD TOO LONG
         SPACE 3
HEXCONV4 LR    R1,R3               CALCULATE
         SR    R1,R6                  LENGTH
         EX    R1,PACK             CREATE HEX
         MVC   0(4,R10),DOUBLE+3      AND SAVE RESULT
         BR    R11                 EXIT
         EJECT
ADD      AR    R5,R6               ADD FUNCTION
         ST    R5,RESULT           SAVE RESULT
         B     CONVERT             GO CONVERT AND PRINT
         SPACE 3
SUB      SR    R5,R6               SUBTRACT FUNCTION
         ST    R5,RESULT           SAVE RESULT
         B     CONVERT             GO CONVERT AND PRINT
         SPACE 3
MLT      LR    R4,R5               LOAD MULTIPLICAND
         MR    R4,R6               MULTIPLY FUNCTION
         ST    R5,RESULT           SAVE RESULT
         B     CONVERT             GO CONVERT AND PRINT
         SPACE 3
DIV      LTR   R6,R6               ZERO DIVISOR
         BZ    BADDIV              YES, ERROR
         DR    R4,R6               DIVIDE FUNCTION
         ST    R5,RESULT           SAVE RESULT,  QUOTIENT ONLY
         SPACE 3
CONVERT  UNPK  ANSWER(9),RESULT(5)
         NC    ANSWER(8),=8X'0F'
         TR    ANSWER(8),=C'0123456789ABCDEF'
         MVI   ANSWER+8,C' '
         L     R1,RESULT           LOAD RESULT
         CVD   R1,DOUBLE
         MVC   ANSWER2(15),MASK    EDIT MASK
         ED    ANSWER2(15),DOUBLE+2
         B     EXIT                GO PRINT
         SPACE 3
LXYZERR  MVC   ANSWER(44),LXYZMSG
         B     EXIT                GO PRINT ERROR MSG
         SPACE 3
PARMERR  MVC   ANSWER(44),ERRMSG
         B     EXIT                GO PRINT ERROR MSG
         SPACE 3
BADDIV   MVC   ANSWER(44),DIVMSG
         B     EXIT                GO PRINT ERROR MSG
         EJECT
MSGREQ   MVC   MSGTEXT(70),4(R3)
         MVC   MSGLIST(4),=X'004A0100'
         LA    R4,1                DEFAULT NO. OF MSGS
         MVC   DOUBLE(2),=C'00'
         MVZ   DOUBLE(2),1(R3)
         CLC   DOUBLE(2),=C'00'
         BNE   MSGREQ1
         PACK  DOUBLE(8),1(2,R3)
         CVB   R4,DOUBLE
         LTR   R4,R4               FORCE AT LEAST 1
         BNZ   *+8
         LA    R4,1
         CH    R4,=H'30'           MAX
         BNH   MSGREQ1
         LA    R4,30
         SPACE 1
MSGREQ1  SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         LA    R1,MSGLIST          WTO PARM LIST ADDR
         SVC   35                  WTO
         BCT   4,MSGREQ1
         CLC   TESTWTO(2),=X'0A23' WTO REPLACED
         BE    EXIT1               NO
         LA    R1,MSGLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
         B     EXIT1
         EJECT
LXYZREQ  MVC   MSGTEXT(24),=CL24'LXYZ= L-XXX-YY-Z (NNNN)'
         MVC   MSGLIST(4),=X'001C0100'
         CLI   2(R3),C'-'          DECIMAL INPUT
         BE    LXYZREQ1            YES
         MVC   MSGTEXT+18(4),1(R3)
         LA    R10,FIELDA
         LA    R3,1(0,R3)
         BAL   R11,HEXCONV
         SR    R14,R14
         L     R15,FIELDA
         SLL   R15,16
         SLDL  R14,2               SHIFT IN 'L'
         CVD   R14,DOUBLE
         UNPK  MSGTEXT+6(1),DOUBLE+7(1)
         OI    MSGTEXT+6,X'F0'
         MVI   MSGTEXT+7,C'-'
         SR    R14,R14
         SLDL  R14,1               SHIFT IN 'Z'
         CVD   R14,DOUBLE
         UNPK  MSGTEXT+15(1),DOUBLE+7(1)
         OI    MSGTEXT+15,X'F0'
         SR    R14,R14
         SLDL  R14,5               SHIFT IN 'Y'
         CVD   R14,DOUBLE
         UNPK  MSGTEXT+12(2),DOUBLE+6(2)
         OI    MSGTEXT+13,X'F0'
         MVI   MSGTEXT+14,C'-'
         SR    R14,R14
         SLDL  R14,8               SHIFT IN 'X'
         CVD   R14,DOUBLE
         UNPK  MSGTEXT+8(3),DOUBLE+6(2)
         OI    MSGTEXT+10,X'F0'
         MVI   MSGTEXT+11,C'-'
         B     LXYZREQ2
         SPACE 1
LXYZREQ1 MVC   MSGTEXT+6(10),1(R3)
         LR    R7,R3
         CLI   6(R7),C'-'          DECIMAL INPUT
         BNE   LXYZERR             NO, ERROR
         CLI   9(R7),C'-'          DECIMAL INPUT
         BNE   LXYZERR             NO, ERROR
         LA    R10,FIELDA
         BAL   R11,DECCONV
         SR    R14,R14
         L     R15,FIELDA
         SLL   R15,30
         SLDL  R14,2               SHIFT IN 'L'
         LA    R3,9(0,R7)
         BAL   R11,DECCONV
         L     R15,FIELDA
         SLL   R15,31
         SLDL  R14,1               SHIFT IN 'Z'
         LA    R3,6(0,R7)
         BAL   R11,DECCONV
         L     R15,FIELDA
         SLL   R15,27
         SLDL  R14,5               SHIFT IN 'Y'
         LA    R3,2(0,R7)
         BAL   R11,DECCONV
         L     R15,FIELDA
         SLL   R15,24
         SLDL  R14,8               SHIFT IN 'X'
         ST    R14,FIELDA
         UNPK  DOUBLE(5),FIELDA+2(3)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   MSGTEXT+18(4),DOUBLE
         SPACE 1
LXYZREQ2 LA    R1,MSGLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
         B     EXIT1
         EJECT
MSTCONS  CLC   XAS(8),2(R3)        PASSWORD PROVIDED
         BNE   NOAUTH              NO
         L     R15,16              CVT
         L     R15,100(0,R15)      UCM BASE
         LA    R6,0(0,R15)         UCM
         S     R6,=F'4'               MCS
         L     R6,0(0,R6)                PREFIX ADDR
         L     R7,0(0,R6)          MSTCONS UCM ENTRY ADDR
         L     R9,72(0,R15)        UCM 1ST ENTRY ADDR
         LM    R4,R5,76(R15)       UCM ENTRY LENGTH AND LAST ENTRY ADDR
         SR    R14,R14
         IC    R14,XAU             CONSOLE ID TO SWITCH MSTCONS TO
         SPACE 1
MSTCONS1 CLM   R14,1,26(R9)        UCM ID MATCH
         BNE   MSTCONS2            NO, CHECK NEXT
         TM    25(R9),X'10'        IS CONSOLE ACTIVE
         BZ    MSTCONS3            NO, ERROR
         TM    42(R9),X'50'        IS CONSOLE DISPLAY TYPE
         BNZ   MSTCONS3            NO, ERROR
         B     MSTCONS4            YES, CONTINUE
         SPACE 1
MSTCONS2 BXLE  R9,R4,MSTCONS1      NEXT UCM ENTRY
         SPACE 1
MSTCONS3 LA    R1,UCMMSG           UCM ID ERROR MSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     EXIT1
         SPACE 1
MSTCONS4 ST    R9,0(0,R6)          SAVE NEW MSTCONS UCM ENTRY
         OI    42(R9),X'80'        SET MSTCONS BIT ON NEW MSTCONS
         NI    42(R7),X'7F'        SET MSTCONS BIT OFF OLD MSTCONS
         LA    R1,MSTCMSG          MSTCONS SWITCH MSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     EXIT1
         SPACE 1
NOAUTH   LA    R1,AUTHMSG          NO AUTHORITY
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     EXIT1
         EJECT
BRODCST  L     R15,16              CVT
         L     R15,100(0,R15)      UCM BASE
         L     R9,72(0,R15)        UCM 1ST ENTRY ADDR
         LM    R4,R5,76(R15)       UCM ENTRY LENGTH AND LAST ENTRY ADDR
         SR    R14,R14
         SPACE 1
BRODCST1 ICM   R14,1,26(R9)        LOAD UCMID
         TM    25(R9),X'10'        IS CONSOLE ACTIVE
         BZ    BRODCST2            NO, NEXT
         TM    42(R9),X'50'        IS CONSOLE DISPLAY TYPE
         BNZ   BRODCST2            NO, NEXT
         B     BRODCST3
         SPACE 1
BRODCST2 BXLE  R9,R4,BRODCST1      NEXT UCM ENTRY
         CLC   TESTWTO(2),=X'0A23' WTO REPLACED
         BE    EXIT1               NO
         LA    R1,MSGLIST          WTO PARM LIST ADDR
         SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         SVC   35                  WTO
         B     EXIT1
         SPACE 1
BRODCST3 STC   R14,XAU             SAVE UCMID
         MVC   MSGTEXT(70),4(R3)
         MVC   MSGLIST(4),=X'004A0100'
         LA    R8,1                DEFAULT NO. OF MSGS
         MVC   DOUBLE(2),=C'00'
         MVZ   DOUBLE(2),1(R3)
         CLC   DOUBLE(2),=C'00'
         BNE   BRODCST4
         PACK  DOUBLE(8),1(2,R3)
         CVB   R8,DOUBLE
         LTR   R8,R8               FORCE AT LEAST 1
         BNZ   *+8
         LA    R8,1
         CH    R8,=H'30'           MAX
         BNH   BRODCST4
         LA    R8,30
         SPACE 1
BRODCST4 SR    R0,R0               LOAD
         IC    R0,XAU                 UCM ID
         LA    R1,MSGLIST          WTO PARM LIST ADDR
         SVC   35                  WTO
         BCT   R8,BRODCST4
         B     BRODCST2
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESK03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     EXIT1
         EJECT
         LTORG
         SPACE 5
TESTSEQ  IC    R0,XAU              UCM ID
TESTWTO  SVC   35                  WTO
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
MASK     DC    X'4020206B2020206B2020206B202120'
         SPACE 3
ERRMSG   DC    CL44'CALCULATOR PARAMETER ERROR'
         SPACE 3
LXYZMSG  DC    CL44'LXYZ TRANSLATE PARM ERROR'
         SPACE 3
UCMMSG   DC    X'00300100'
         DC    CL44'CONSOLE NOT ACTIVE, MSTCONS NOT SWITCHED'
         SPACE 3
AUTHMSG  DC    X'00300100'
         DC    CL44'AUTHORITY INVALID, MSTCONS NOT SWITCHED'
         SPACE 3
MSTCMSG  DC    X'00300100'
         DC    CL44'MSTCONS SWITCHED TO THIS CONSOLE'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
DIVMSG   DC    CL44'ANY IDIOT KNOWS YOU CANNOT DIVIDE BY ZERO'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
DOUBLE   DS    D
FIELDA   DS    F
FIELDB   DS    F
RESULT   DS    F
SAVRET   DS    F
XSASAVE  DS    F
FUNCTION DS    C
         SPACE 3
         DS    0F
SMFLIST  DS    0CL52
         DS    F
SMFANS   DS    CL48
         DS    0F
WTOLIST  DS    0CL48
         DS    F
ANSWER   DS    CL8
         DS    CL4
ANSWER2  DS    CL32
         SPACE 3
         DS    0F
MSGLIST  DS    0CL74
         DS    F
MSGTEXT  DS    CL70
         SPACE 3
ESTAELST DS    0F,(ESTAEOVX)X
WORKEND  DS    0D
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAV      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
