         TITLE 'CMDSBTSO -   TSO FULL SCREEN MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       CMDSBTSO -       TSO FULL SCREEN MONITOR                  ***
***                                                                 ***
***       VERSION -        2.3                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITHOUT  SE2 OR SU64.     ***
***                                                                 ***
***       UPDATED -        12/15/79                                 ***
***                                                                 ***
***       UPDATED -        09/29/81    TO PROVIDE 3278/79 SUPPORT   ***
***                                    AND TO PROVIDE FOR 24 PFKS   ***
***                                                                 ***
***  THIS MODULE SEARCHES THE CODE IT IS LOADING (#Q, #N, ETC.) TO  ***
***     SEE IF SUBPOOL 241 IS BEING REQUESTED FOR THE GETMAIN.  THIS***
***     MODULE DOES THIS BY SEARCHING FOR X'F100' IN THE CODE.      ***
***     IF THE EXECUTION IS TAKING PLACE VIA THE SYSTEM CONSOLE,    ***
***     THEN SUBPOOL 241 IS ALLOWED.  IF THE EXECUTION IS TAKING    ***
***     PLACE WITHIN TSO, THEN THE X'F100' IS CHANGED TO X'0000'    ***
***     (FOR SUBPOOL 0) PRIOR TO THE ACTUAL EXECUTION OF THE CODE.  ***
***     IF THE PROPER CODING CONVENTIONS ARE NOT FOLLOWED IN THE    ***
***     SUB-MODULES (#Q, #N, ETC.), THIS CHECK MAY NOT BE SUCCESS-  ***
***     FUL, AND AN ABEND B0A WILL TAKE PLACE.
***********************************************************************
***********************************************************************
         EJECT
CMDSBTSO CSECT
         SAVE  (14,12)
         LR    R3,R15
         USING CMDSBTSO,R3
         ST    R13,SAVE+4
         LA    R13,SAVE
         USING SAVE,R13
         ST    R1,DIRCPPL          SAVE CPPL ADDR IF APPLICABLE
         XC    GETMADR(4),GETMADR
         SPACE 1
CMDINIT  STATTN  LINES=0,TENS=0,INPUT=ATTNCHAR
         STAX  ATENTION,REPLACE=YES,DEFER=NO
         L     R0,SIZEGETM         GETMAIN SIZE
         L     R1,GETMADR          GETMAIN ADDR
         LTR   R1,R1               GETMAIN ALREADY DONE
         BNZ   CMDINIT1            YES
         GETMAIN R,LV=(0)          GET COMMAND WORK AREA
         LA    R1,0(0,R1)
         ST    R1,GETMADR          SAVE GETMAIN ADDR
         SPACE 1
CMDINIT1 LR    R4,R1
         L     R5,SIZEGETM
         LA    R5,0(0,R5)
         SR    R6,R6
         SR    R7,R7
         MVCL  R4,R6               CLEAR GETMAIN AREA
         MVI   RETCODE,X'00'
         L     R4,=A(CMDOUTPT)
         LA    R5,1760
         L     R6,=A(OPENTEXT)
         LA    R7,1760
         MVCL  R4,R6               MOVE OPENING TEXT TO OUTPUT
         L     R4,=A(CMDOUTPT)
         MVI   REFRSW,X'00'
         MVC   REFRESH(8),=CL8'00000000'
         EJECT
CMDGET   CLC   REFRESH(8),=CL8'00000000'  AUTO REFRESH
         BE    CMDGET1             NO
         L     R4,=A(CMDOUTPT)     OUTPUT ADDR
         BAL   R7,SCREEN           WRITE SCREEN AND GET NEXT COMMAND
         CLI   RETCODE,C'Z'        ATTENTION
         BE    CMDINIT             YES
         STIMER  WAIT,DINTVL=REFRESH
         L     R1,REFRCNT
         BCT   R1,*+8              BRANCH UNTIL END OF AUTO REFRESH
         B     CMDINIT
         ST    R1,REFRCNT
         MVC   SCRREPLY+6(57),SCRLSREP
         LA    R14,SCRREPLY+6
         B     CMDGET2
         SPACE 1
CMDGET1  L     R4,=A(CMDOUTPT)     OUTPUT ADDR
         BAL   R7,SCREEN           WRITE SCREEN AND GET NEXT COMMAND
         CLI   RETCODE,C'Z'        ATTENTION
         BE    CMDINIT             YES
         SPACE 1
CMDGET2  L     R4,=A(CMDOUTPT)
         XC    CMDOUTBG(4),CMDOUTBG
         XC    CMDOUTCT(4),CMDOUTCT
         CLI   0(R14),C'('         OUTPUT INCREMENT
         BNE   CMDGET2A            NO
         CLI   4(R14),C')'         OUTPUT INCREMENT
         BNE   CMDGET2A            NO
         MVC   DOUBLE(3),=C'000'
         MVZ   DOUBLE(3),1(R14)
         CLC   DOUBLE(3),=C'000'   NUMERIC
         BNE   CMDGET2A            NO, ERROR
         PACK  DOUBLE(8),1(3,R14)
         LR    R1,R14              SAVE REG14
         SR    R14,R14
         CVB   R15,DOUBLE
         M     R14,=F'1000'
         ST    R15,CMDOUTBG
         LR    R14,R1              POINT TO COMMAND
         MVC   0(58,R14),5(R14)    SHIFT OVER COMMAND
         SPACE 1
CMDGET2A LA    R10,COMTAB          # COMMAND TABLE
         LA    R11,MODLIST         # COMMAND MODULE LIST
         MVI   TCMDTYP,C' '
         CLI   0(R14),C'@'         EXEC PROGRAM
         BE    DIREXEC             YES, GO HANDLE
         CLI   0(R14),C'.'         EXEC CP
         BE    DIREXEC             YES, GO HANDLE
         CLI   0(R14),C'#'         # COMMAND
         BNE   PLOOPA              NO, CHECK CONTROL COMMANDS
         LA    R14,1(0,R14)        ADJUST INPUT PARM
         CLI   0(R14),C'#'         ## COMMAND
         BNE   PLOOPC              NO
         LA    R14,1(0,R14)        ADJUST INPUT PARM
         BCTR  R1,0                ADJUST LENGTH
         LA    R10,COMTAB2         ## COMMAND TABLE
         LA    R11,MODLIST2        ## COMMAND MODULE LIST
         MVI   TCMDTYP,C'#'        INDICATE ##
         B     PLOOPC
         EJECT
PLOOPA   LA    R10,COMTAB3         GRAPHIC MONITOR COMMAND TABLE
         LA    R11,MODLIST3        GRAPHIC MONITOR MODULE LIST
         MVI   TCMDTYP,C'-'        INDICATE GRAPHIC MONITOR
         TM    0(R14),X'F0'        CHANGE REFRESH TIME COMMAND
         BNO   PLOOPC              NO, GRAPHIC MONITOR COMMAND
         TM    1(R14),X'F0'        CHANGE REFRESH TIME COMMAND
         BNO   PLOOPB              NO, ERROR
         MVC   REFRESH+4(2),0(R14)
         LA    R1,20               DEFAULT REFRESH COUNT
         CLI   2(R14),C'/'
         BNE   PLOOPA1
         TM    3(R14),X'F0'        CHANGE REFRESH TIME COMMAND
         BNO   PLOOPB              NO, ERROR
         TM    4(R14),X'F0'        CHANGE REFRESH TIME COMMAND
         BNO   PLOOPB              NO, ERROR
         PACK  DOUBLE(8),3(2,R14)
         CVB   R1,DOUBLE
         SPACE 1
PLOOPA1  MVC   0(80,R4),=CL80'CONTROL COMMAND ACCEPTED'
         MVI   80(R4),X'FF'
         ST    R1,REFRCNT
         B     CMDGET1
         SPACE 1
PLOOPB   MVC   0(80,R4),=CL80'ERROR IN CONTROL COMMAND'
         MVI   80(R4),X'FF'
         B     CMDGET
         SPACE 1
PLOOPC   MVC   COMMAND(1),0(R14)   SAVE COMMAND
         SR    R15,R15
         LA    R1,1(0,R14)         1ST BYTE OF SUB-PARM
         CLI   0(R1),C' '          1ST BYTE BLANK
         BNE   *+16                NO
         LA    R1,1(0,R1)          NEXT BYTE
         CLI   0(R1),C' '          2ND BYTE BLANK
         BE    PROCESS             YES, NO SUB-PARM
         LA    R15,PARM
         MVC   PARM(60),0(R1)
         EJECT
PROCESS  ST    R15,EXITLIST+20     SAVE TEXT ADDR
         MVC   WAITTIME(8),=8C'0'
         XC    CSCSYSID(4),CSCSYSID
         XC    HEXTIME(4),HEXTIME
         LA    R1,PARM
         LA    R8,PARM+74
         SPACE 1
PROCESS1 CLC   0(2,R8),=C'T='      TIME PARM
         BE    PROCESS2            YES
         CLC   0(2,R8),=C'S='      SYSID PARM
         BE    PROCESS4            YES
         BCTR  R8,0                PREVIOUS CHARACTER
         CR    R1,R8               BACK TO BEGINNING
         BNE   PROCESS1            NO, CHECK NEXT
         B     PROCESS5            YES, NO T= FOUND
         SPACE 1
PROCESS2 LA    R1,PACK+3           TARGET FIELD
         MVC   PACK(4),=C'0000'
         CLI   4(R8),C' '          SENDING BYTE BLANK
         BE    *+12                YES, DONT MOVE
         MVC   0(1,R1),4(R8)       MOVE IT
         BCTR  R1,0                NEXT TARGET BYTE
         CLI   3(R8),C' '          SENDING BYTE BLANK
         BE    *+12                YES, DONT MOVE
         MVC   0(1,R1),3(R8)       MOVE IT
         BCTR  R1,0                NEXT TARGET BYTE
         CLI   2(R8),C' '          SENDING BYTE BLANK
         BE    *+10                YES, DONT MOVE
         MVC   0(1,R1),2(R8)       MOVE IT
         MVC   DOUBLE(4),=C'0000'
         MVZ   DOUBLE(4),PACK
         CLC   DOUBLE(4),=C'0000'  IS IT NUMERIC
         BE    PROCESS3            YES
         MVC   PACK(4),=C'0000'
         SPACE 1
PROCESS3 PACK  DOUBLE(8),PACK(4)
         CVB   R15,DOUBLE          SECONDS TO BINARY
         SR    R14,R14
         ST    R15,HEXTIME         SAVE BINARY SECONDS
         D     R14,=F'60'          CALCULATE MINUTES AND SECONDS
         CVD   R15,DOUBLE          MINUTES
         UNPK  WAITTIME+2(2),DOUBLE+6(2)
         OI    WAITTIME+3,X'F0'
         CVD   R14,DOUBLE          SECONDS
         UNPK  WAITTIME+4(2),DOUBLE+6(2)
         OI    WAITTIME+5,X'F0'
         MVC   0(4,R8),=CL4' '
         BCTR  R8,0
         CLI   0(R8),C','
         BNE   *+8
         MVI   0(R8),C' '
         B     PROCESS5
         SPACE 1
PROCESS4 MVC   CSCSYSID(4),2(R8)
         BCTR  R8,0
         MVC   0(8,R8),=CL8' '
         MVC   CSCCMD(1),COMMAND
         MVC   CSCCMD+1(79),PARM
         EJECT
PROCESS5 LA    R1,WAITTIME
         ST    R1,EXITLIST+24      PASS WAITTIME TO COMMANDS
         MVC   TCOMMAND(1),COMMAND
         TR    COMMAND,0(R10)      TRANSLATE COMMAND TO INDEX
         SR    R9,R9
         IC    R9,COMMAND          INSERT INDEX
         SLL   R9,3                OFFSET INTO MODULE LIST
         L     R15,16              CVT ADDR
         L     R15,0(0,R15)        TCB WORDS
         L     R14,12(0,R15)       CURRENT ASCB
         MVC   ASID(2),36(R14)     SAVE ASID
         L     R14,4(0,R15)        CURRENT TCB
         L     R14,12(0,R14)       TIOT
         MVC   USERID(7),0(R14)    USERID (JOBNAME)
         UNPK  TASID(5),ASID(3)
         NC    TASID(4),=8X'0F'
         TR    TASID(4),=C'0123456789ABCDEF'
         MVI   TASID+4,C' '
         L     R15,16              CVT ADDR
         L     R15,196(0,R15)      SMCA
         MVC   SYSTEM(4),16(R15)   SYSTEM ID
         TIME  DEC
         ST    R1,DOUBLE
         UNPK  YEAR(3),DOUBLE+1(2)
         MVI   YEAR+2,C'.'
         UNPK  DAY(3),DOUBLE+2(2)
         OI    DAY+2,X'F0'
         ST    R0,DOUBLE
         UNPK  HOUR(3),DOUBLE(2)
         MVI   HOUR+2,C'.'
         UNPK  MINUTE(3),DOUBLE+1(2)
         MVI   MINUTE+2,C' '
         MVI   68(R4),C' '
         MVC   69(11,R4),68(R4)
         MVC   0(68,R4),HEADER
         LA    R4,80(0,R4)
         CLI   CSCSYSID,X'00'      CROSS-SYSTEM REQUEST
         BNE   CSCPROC             YES
         LTR   R9,R9               COMMAND IN LIST
         BNZ   LOAD                YES, GO PROCESS
         MVC   0(80,R4),=CL80'COMMAND NOT FOUND'
         LA    R4,80(0,R4)
         MVI   0(R4),X'FF'
         B     CMDGET
         EJECT
CSCPROC  LA    R1,10               RESET TIMER COUNT
         MVI   0(R4),C' '
         MVC   1(79,R4),0(R4)
         SPACE 1
CSCPROC0 ST    R1,RESETCNT         RESET TIMER COUNT
         BAL   R10,CSCREAD         READ CSC DATA
         L     R1,16               CVT
         L     R1,0(0,R1)          TCB WORDS
         L     R1,4(0,R1)          CURRENT TCB
         L     R1,12(0,R1)         TIOT
         CLC   0(8,R11),=8X'00'    CSC BUSY
         BE    CSCPROC4            NO
         CLI   4(R11),X'FF'        TSO ID CROSS-SYSTEM COMMAND
         BNE   CSCPROC1            NO
         CLC   5(3,R11),4(R1)      TSO ID MATCH
         BE    CSCPROC2            YES
         SPACE 1
CSCPROC1 STIMER  WAIT,DINTVL=CSCWAIT
         L     R1,RESETCNT         RESET COUNT
         BCT   R1,CSCPROC0         TRY AGAIN
         MVC   TIMESID(4),4(R11)
         MVC   0(48,R4),TIMEMSG    TIMEOUT MSG
         LA    R4,80(0,R4)
         MVI   0(R4),X'FF'
         BAL   R10,CSCCLEAR        FORCE CLEAR
         B     CMDGET
         SPACE 3
CSCPROC2 LH    R9,10(0,R11)        WTO COUNT
         LA    R11,148(0,R11)      1ST WTO ADDR
         SPACE 1
CSCPROC3 MVI   0(R4),C' '
         MVC   1(79,R4),0(R4)
         LA    R1,4(0,R11)         MSG ADDR
         LH    R14,0(0,R11)        MSG LENGTH
         SH    R14,=H'5'
         EX    R14,CSCMVE
         LH    R1,0(0,R11)         WTO LENGTH
         LA    R11,0(R1,R11)       NEXT MSG
         LA    R4,80(0,R4)
         BCT   R9,CSCPROC3
         BAL   R10,CSCCLEAR        RESET DATASET TO ALLOW REUSE
         MVI   0(R4),X'FF'
         B     CMDGET
         SPACE 1
CSCMVE   MVC   0(0,R4),0(R1)
         EJECT
CSCPROC4 MVI   0(R11),X'FF'        TSO SENDER
         MVC   1(3,R11),4(R1)      TSO ID
         XC    8(2,R11),8(R11)
         MVC   12(4,R11),=F'148'   COMMAND RECORD LENGTH
         MVC   16(78,R11),CSCCMD   MOVE COMMAND TO RECORD
         XC    96(52,R11),96(R11)
         MVC   4(4,R11),CSCSYSID
         BAL   R10,CSCWRITE        REWRITE RECORD
         MVC   RECVMSG+20(1),16(R11)
         MVC   0(44,R4),RECVMSG
         LA    R4,80(0,R4)
         MVI   0(R4),X'FF'
         L     R4,=A(CMDOUTPT)     OUTPUT ADDR
         STM   R2,R12,SCRSAVE
         BAL   R7,SCREEN
         LM    R2,R12,SCRSAVE
         CLI   RETCODE,C'Z'        ATTENTION
         BE    CMDINIT             YES
         B     CSCPROC1
         EJECT
CSCREAD  MVC   CSCDATA+62(2),=H'19069'  FULL BLOCKSIZE
         OPEN  (CSCDATA)
         L     R8,=A(RECORD)
         GET   CSCDATA,(R8)
         L     R11,=A(RECORD)
         CLC   0(12,R11),=XL12'00'  ZERO REQUEST
         BE    CSCREAD1            NO
         CLC   0(96,R11),PREVREC   SAME REQUEST
         BE    CSCREAD2            NO
         SPACE 1
CSCREAD1 MVC   PREVREC(96),0(R11)  SAVE NEW REQUEST
         MVC   RESETCNT(4),=F'10'  SET NEW COUNT
         SPACE 1
CSCREAD2 CLOSE (CSCDATA)
         BR    R10
         EJECT
CSCCLEAR L     R1,=A(RECORD)
         XC    0(16,R1),0(R1)
         SPACE 1
CSCWRITE L     R1,=A(RECORD)
         L     R1,12(0,R1)         RECORD LENGTH
         LTR   R1,R1               ZERO LENGTH
         BNZ   CSCWRIT1            NO
         LA    R1,16               MINIMUM LENGTH
         SPACE 1
CSCWRIT1 STH   R1,CSCDATA+62       SAVE LENGTH FOR WRITE
         LR    R2,R1
         OPEN  (CSCDATA,(OUTPUT))
         L     R8,=A(RECORD)
         PUT   CSCDATA,(R8)
         CLOSE (CSCDATA)
         BR    R10
         EJECT
LOAD     LA    R9,0(R11,R9)        MODULE NAME ADDR
         MVC   LOADNAME(8),0(R9)
         LOAD  EPLOC=LOADNAME      LOAD COMMAND MODULE
         ST    R0,ENTRY            SAVE ENTRY ADDR
         LR    R5,R0               MODULE ADDR
         LA    R6,0(0,R1)          MODULE DOUBLEWORDS
         SLL   R6,3                MODULE LENGTH
         ST    R6,LENGTH           SAVE MODULE LENGTH
         SH    R6,=H'6'            ADJUST SCAN LENGTH
         SPACE  1
LOAD1    CLC   0(6,R5),=X'430020280A23'  WTO INSTRUCTIONS
         BE    LOAD3               YES
         SPACE 1
LOAD2    LA    R5,2(0,R5)          NEXT INSTRUCTION
         BCTR  R6,0
         BCT   R6,LOAD1            PROCESS NEXT
         B     LOAD4
         SPACE 1
LOAD3    MVC   0(6,R5),=X'58F0200005EF'
         B     LOAD2
         SPACE 1
LOAD4    L     R5,ENTRY            MODULE ADDR
         L     R6,LENGTH           MODULE LENGTH
         SH    R6,=H'4'            ADJUST SCAN LENGTH
         SPACE  1
LOAD5    CLC   0(2,R5),=X'F100'    SUBPOOL 241
         BE    LOAD7               YES
         SPACE 1
LOAD6    LA    R5,4(0,R5)          NEXT INSTRUCTION
         BCTR  R6,0
         BCTR  R6,0
         BCTR  R6,0
         BCT   R6,LOAD5            PROCESS NEXT
         B     EXECUTE             END OF MODULE
         SPACE 1
LOAD7    MVC   0(2,R5),=H'0'       SET SUBPOOL ZERO
         B     EXECUTE             END OF MODULE
         EJECT
DIREXEC  ST    R14,DIRSAVE
         TPUT  SCRBEGIN,8,FULLSCR
         L     R14,DIRSAVE
         MVC   DIREPLOC(8),1(R14)
         L     R1,DIRCPPL
         CLI   0(R14),C'.'         COMMAND PROCESSOR
         BE    *+8                 YES
         LA    R1,DIRPARM
         STM   R14,R12,DIRSAVE
         LINK  EPLOC=DIREPLOC
         BALR  R15,0
         USING *,R15
         LM    R14,R12,DIRSAVE
         DROP  R15
         B     CMDINIT
         EJECT
SCREEN   LA    R5,22               MAX OUTPUT LINES
         LA    R6,SCRLIN02         OUTPUT BEGIN LINE
         LA    R8,SCRLIN02
         LA    R9,1760
         SR    R10,R10
         SR    R11,R11
         MVCL  R8,R10              CLEAR SCREEN AREA
         SPACE 1
SCREEN1  MVC   0(80,R6),0(R4)      SET OUTPUT LINE
         L     R1,=A(SCRPLUS)
         MVI   0(R1),C' '
         LA    R4,80(0,R4)         NEXT INPUT LINE
         LA    R6,80(0,R6)         NEXT OUTPUT LINE
         CLI   0(R4),X'FF'         END OF INPUT
         BE    SCREEN2             YES, GO WRITE
         BCT   R5,SCREEN1
         L     R1,=A(SCRPLUS)
         MVI   0(R1),C'+'
         SPACE 1
SCREEN2  LA    R14,SCRLIN02
         TR    0(80,R14),TABLE
         TR    80(80,R14),TABLE
         TR    160(80,R14),TABLE
         TR    240(80,R14),TABLE
         TR    320(80,R14),TABLE
         TR    400(80,R14),TABLE
         TR    480(80,R14),TABLE
         TR    560(80,R14),TABLE
         TR    640(80,R14),TABLE
         TR    720(80,R14),TABLE
         TR    800(80,R14),TABLE
         TR    880(80,R14),TABLE
         TR    960(80,R14),TABLE
         TR    1040(80,R14),TABLE
         TR    1120(80,R14),TABLE
         TR    1200(80,R14),TABLE
         TR    1280(80,R14),TABLE
         TR    1360(80,R14),TABLE
         TR    1440(80,R14),TABLE
         TR    1520(80,R14),TABLE
         TR    1600(80,R14),TABLE
         TR    1680(80,R14),TABLE
         EJECT
         LA    R1,SCRBEGIN
         TPUT  (1),SCRLEN,FULLSCR
         CLI   REFRSW,X'FF'        REFRESH IN PROGRESS
         BCR   8,R7                YES
         CLC   REFRESH(8),=CL8'00000000'  REFRESH REQUESTED
         BE    *+8                 NO
         MVI   REFRSW,X'FF'
         MVC   SCRREPLY(63),=CL63' '
         LA    R1,SCRREPLY         REPLY ADDR
         TGET  (1),63,ASIS         GET REPLY
         OC    SCRREPLY+6(57),=CL63' '  CAPS
         CH    R15,=H'12'          INPUT LARGER THAN BUFFER
         BNE   SCREEN3             NO
         TCLEARQ INPUT
         SPACE 1
SCREEN3  CLI   SCRREPLY,X'6C'      ATTENTION (PA1)              FNB001
         BCR   8,R7                YES
         CLI   SCRREPLY,X'7E'      ATTN KEY                     FNB001
         BCR   8,R7                YES                          FNB001
         CLI   SCRREPLY,X'6D'      REPEAT (CLEAR)
         BE    REPEAT              YES
         CLI   SCRREPLY,X'6E'      REPEAT (PA2)                 FNB001
         BE    REPEAT              YES
         CLI   SCRREPLY,X'7D'      ENTER KEY
         BNE   PFKCHK              NO, CHECK PFK ENTRY
         SPACE 1
SCREEN3A LA    R14,SCRREPLY+6
         MVC   SCRLSREP(57),SCRREPLY+6
         CLI   SCRREPLY+6,C'Z'     TERMINATE
         BE    EXIT                YES
         CLC   SCRREPLY+6(3),=C'END'  TERMINATE
         BE    EXIT                YES
         CLI   SCRREPLY+6,C'-'     SCROLL BACK
         BE    SCROLL              YES
         CLI   SCRREPLY+6,C'+'     SCROLL FORWARD
         BE    SCROLL              YES
         CLI   SCRREPLY+6,C'='     SCROLL FIND EQ
         BE    SCROLL2             YES
         CLI   SCRREPLY+6,C' '     NEXT SCREEN REQUEST
         BCR   7,R7                NO
         SPACE 1
SCREEN3B LA    R8,SCRLIN02
         LA    R9,1760
         SR    R10,R10
         SR    R11,R11
         MVCL  R8,R10              CLEAR SCREEN AREA
         CLI   0(R4),X'FF'         ANY MORE OUTPUT
         BNE   SCREEN              YES
         MVC   SCRLIN02(80),=CL80'****   END  OF  OUTPUT   ****'
         B     SCREEN2
         SPACE 1
SCREEN4  LA    R15,SCRREPLY(R1)
         LH    R2,=H'63'
         SR    R2,R1
         EX    R2,CMDSCCLR         CLEAR EXCESS PARM
         BR    R7
         SPACE 1
CMDSCCLR MVC   0(0,R15),=CL63' '
         EJECT
SCROLL   MVC   DOUBLE(3),=C'000'
         MVZ   DOUBLE(3),SCRREPLY+7
         CLC   DOUBLE(3),=C'000'   NUMERIC
         BNE   SCROLL1
         PACK  DOUBLE(8),SCRREPLY+7(3)
         CVB   R14,DOUBLE
         B     SCROLL2
         SPACE 1
SCROLL1  L     R14,=F'99999999'
         SPACE 1
SCROLL2  LA    R11,SCROLLEQ
         CLI   SCRREPLY+6,C'='     SCROLL FIND EQ
         BE    SCROLLF             YES
         CLI   SCRREPLY+6,C'-'     SCROLL UP
         BE    SCROLLU             YES
         CLI   SCRREPLY+6,C'+'     SCROLL DOWN
         BE    SCROLLD             YES
         BR    R7
         SPACE 1
SCROLLU  LA    R14,22(0,R14)       ADJUST TO TOP OF PAGE
         SPACE 1
SCROLLU1 C     R4,=A(CMDOUTPT)     TOP
         BE    SCREEN3B
         S     R4,=F'80'
         BCT   R14,SCROLLU1
         B     SCREEN3B
         SPACE 1
SCROLLD  CLI   0(R4),X'FF'         BOTTOM
         BE    SCROLLD1
         A     R4,=F'80'
         BCT   R14,SCROLLD
         SPACE 1
SCROLLD1 LA    R14,22
         SPACE 1
SCROLLD2 C     R4,=A(CMDOUTPT)     TOP
         BE    SCREEN3B
         S     R4,=F'80'
         BCT   R14,SCROLLD2
         B     SCREEN3B
         EJECT
SCROLLF  LA    R14,21
         SPACE 1
SCROLLF1 C     R4,=A(CMDOUTPT)     TOP
         BE    SCROLLF2
         S     R4,=F'80'
         BCT   R14,SCROLLF1
         SPACE 1
SCROLLF2 SR    R14,R14
         LA    R10,SCRREPLY+7
         LA    R9,55
         SPACE 1
SCROLLF3 BCT   R9,*+6
         BR    R7
         LA    R10,1(0,R10)        NEXT SEARCH BYTE
         LA    R14,1(0,R14)
         CLC   0(1,R10),SCRREPLY+7  DELIMITER FOUND
         BNE   SCROLLF3
         BCTR  R14,0
         BCTR  R14,0
         SPACE 1
SCROLLF4 CLI   0(R4),X'FF'         BOTTOM
         BNE   SCROLLF5
         S     R4,=F'80'
         B     SCREEN3B
         SPACE 1
SCROLLF5 LA    R8,0(0,R4)          BEGIN OF OUTPUT LINE
         LA    R9,80
         SPACE 1
SCROLLF6 EX    R14,SCROLLCL        COMPARE
         EX    0,0(0,R11)          BR ON CONDITION
         LA    R8,1(0,R8)
         BCT   R9,SCROLLF6         CHECK NEXT
         LA    R4,80(0,R4)
         B     SCROLLF4
         SPACE 1
SCROLLCL CLC   0(0,R8),SCRREPLY+8
SCROLLEQ BC    8,SCREEN3B
         EJECT
PFKCHK   L     R2,=A(PFKTAB)
         SPACE 1
PFKCHK1  CLC   SCRREPLY(1),0(R2)   PFK MATCH
         BE    PFKCHK2
         CLI   0(R2),X'00'         END OF PFK TABLE
         BE    ATENTKEY            YES, I DONT KNOW
         LA    R2,4(0,R2)          NEXT ENTRY
         B     PFKCHK1
         SPACE 1
PFKCHK2  LA    R14,SCRREPLY+6
         CLI   0(R2),X'F1'         PFK 1
         BE    PFKCHK3             YES, CHANGE OR DISPLAY PFK
         L     R2,0(0,R2)          PFK DEFINITION
         MVC   SCRREPLY+6(57),13(R2)                            FNB001
         MVC   SCRLSREP(57),13(R2)                              FNB001
         BR    R7
         SPACE 1
PFKCHK3  CLI   SCRREPLY+6,C' '     DISPLAY PFK
         BNE   PFKCHK5             NO, CHANGE REQUEST
         SPACE 1
PFKCHK4  LR    R8,R7
         L     R4,=A(CMDOUTPT)
         LA    R5,1760
         L     R6,=A(PFKTEXT)
         LA    R7,1760
         MVCL  R4,R6               PFK LIST
         LR    R7,R8
         L     R4,=A(CMDOUTPT)
         B     SCREEN              DISPLAY PFK ASSIGNMENTS
         SPACE 1
PFKCHK5  MVC   DOUBLE(2),=C'00'
         MVZ   DOUBLE(2),SCRREPLY+6
         CLC   DOUBLE(2),=C'00'    NUMERIC PFK
         BNE   PFKCHK4             NO, DISPLAY
         CLI   SCRREPLY+8,C'='     VALID FORMAT
         BNE   PFKCHK4             NO, DISPLAY
         PACK  DOUBLE(8),SCRREPLY+6(2)
         CVB   R4,DOUBLE
         CH    R4,=H'2'            MIN PFK NUMBER
         BL    PFKCHK4             NO, DISPLAY
         CH    R4,=H'24'           MAX PFK NUMBER              @FNB0001
         BH    PFKCHK4             NO, DISPLAY
         SLL   R4,2                MULT BY 4
         L     R15,=A(PFKTAB-4)
         LA    R4,0(R15,R4)        TABLE SLOT ADDR
         L     R4,0(0,R4)          PFK DEFINITION ADDR
         MVC   13(50,R4),SCRREPLY+9  SAVE NEW PFK ASSIGNMENT    FNB001
         B     PFKCHK4
         EJECT
ATENTKEY LA    R14,SCRREPLY+6
         MVI   RETCODE,C'Z'        INDICATE ATTENTION
         XC    SCRREPLY(63),SCRREPLY
         BR    R7
         SPACE 3
REPEAT   LA    R14,SCRREPLY+6
         MVC   SCRREPLY+6(57),SCRLSREP  LAST COMMAND
         MVI   SCRREPLY,X'00'
         B     SCREEN3A
         SPACE 3
ATENTION BALR  R15,0
         USING *,R15
         MVI   RETCODE,C'Z'        INDICATE ATTENTION
         BR    R14
         DROP  R15
RETCODE  DC    F'0'
         EJECT
EXECUTE  LA    R2,EXITLIST         PARM LIST
         MVC   EXITLIST+16(4),=A(CMDOUTPT+80)
         L     R15,ENTRY           COMMAND ENTRY
         BALR  R14,R15             BRANCH
         SPACE 3
         BALR  R15,0
         USING *,R15
         L     R3,=A(CMDSBTSO)     LOAD ORIGINAL BASE
         L     R13,=A(SAVE)
         DELETE  EPLOC=LOADNAME    DELETE MODULE
         B     CMDGET
         DROP  R15
         SPACE 3
EXIT     TPUT  SCRBEGIN,8,FULLSCR
         STATTN  LINES=0,TENS=0,INPUT=0
         L     R0,SIZEGETM
         L     R1,GETMADR
         FREEMAIN  R,LV=(0),A=(1)
         L     R13,SAVE+4
         RETURN  (14,12),RC=0
         EJECT
         USING TPUT,R15
TPUT     ST    R14,8(0,R2)         SAVE RETURN ADDR
         ST    R4,12(0,R2)         SAVE WORK REG
         L     R4,16(0,R2)         LOAD OUTPUT ADDR
         LTR   R0,R0               NORMAL ENTRY
         BNZ   TPUT0               NO, QUEUE COMMAND
         C     R4,=A(CMDOUTEN)     END OF OUTPUT
         BCR   8,R14               YES
         MVI   0(R4),C' '
         MVC   1(79,R4),0(R4)
         LH    R14,0(0,R1)         ORIG MSG LENGTH
         SH    R14,=H'5'           NEW MSG LENGTH
         EX    R14,TPUTMVE         MSG TEXT ADDR
         LA    R4,80(0,R4)         ADD IN MSG LENGTH
         MVI   0(R4),X'FF'
         ST    R4,16(0,R2)         SAVE OUTPUT ADDR
         L     R14,8(0,R2)         LOAD RETURN ADDR
         L     R4,12(0,R2)         LOAD WORK REG
         BR    R14                 RETURN
         SPACE 1
TPUTMVE  MVC   0(0,R4),4(R1)
         SPACE 1
TPUT0    LR    R14,R0
         SPACE 1
TPUT1    L     R0,CMDOUTCT         LOAD CURRENT RECORD COUNT
         A     R0,=F'1'            ADD 1
         ST    R0,CMDOUTCT         SAVE NEW RECORD COUNT
         C     R0,CMDOUTBG         EQUAL TO 1ST REC TO PRINT
         BL    TPUT2               NOT YET
         MVC   0(80,R4),0(R1)
         LA    R4,80(0,R4)         ADD IN MSG LENGTH
         C     R4,=A(CMDOUTEN)     END OF OUTPUT
         BE    TPUT3               YES
         SPACE 1
TPUT2    LA    R1,80(0,R1)
         BCT   R14,TPUT1
         MVI   0(R4),X'FF'
         SPACE 1
TPUT3    ST    R4,16(0,R2)         SAVE OUTPUT ADDR
         SR    R0,R0
         C     R4,=A(CMDOUTEN)     END OF OUTPUT
         BL    *+8                 NO
         LA    R0,1                INDICATE END
         L     R14,8(0,R2)         LOAD RETURN ADDR
         L     R4,12(0,R2)         LOAD WORK REG
         BR    R14                 RETURN
CMDOUTCT DC    F'0'                CURRENT COUNT
CMDOUTBG DC    F'0'                1ST RECORD TO PRINT
         DROP  R15
         EJECT
         LTORG
         SPACE 3
SAVE     DS    18F
DIRSAVE  DS    18F
SCRSAVE  DS    12F
         DS    0D
ATTNCHAR DC    CL8'#'
DOUBLE   DS    2F
DIREPLOC DC    CL8' '
REFRESH  DC    CL8'00000000'
REFRCNT  DC    F'20'
         DS    0D
LOADNAME DC    CL8' '
WAITTIME DS    2F
HEXTIME  DS    F
SIZEGETM DC    F'16384'
REFRSW   DS    F
PACK     DS    F
ENTRY    DS    F
LENGTH   DS    F
COMMAND  DC    F'0'
PARM     DC    CL80' '
CSCSYSID DC    F'0'
CSCCMD   DC    CL80' '
         SPACE 1
DIRCPPL  DC    F'0'
DIRPARM  DC    H'0'
         DC    H'0'
         SPACE 3
HEADER   DS    0CL68
         DC    C'---  <TSO>  CMDSBSYS (#'                       FNB001
TCMDTYP  DC    C' '
TCOMMAND DC    C' ',C')  '
SYSTEM   DC    CL4' ',C'  '
YEAR     DC    CL2' ',C' '
DAY      DC    CL3' ',C'  '
HOUR     DC    CL2' ',C' '
MINUTE   DC    CL2' ',C'  '
USERID   DC    CL7' ',C'  '
TASID    DC    CL4' ',C'  ---'
         SPACE 3
EXITLIST DC    A(TPUT)
ASID     DC    F'0'
         DC    9F'0'
GETMADR  DC    F'0'
         DC    A(SCRREPLY+8)
         EJECT
         DS    0D
TABLE    DC    64C' '
         DC    C'           .<(+×&&          $*);^-/         '
         DC    C',%_>?          :#@''=" '
     DC    C'ABCDEFGHI',CL7' ',C'JKLMNOPQR',CL8' ',C'STUVWXYZ',CL23' '
     DC    C'ABCDEFGHI',CL7' ',C'JKLMNOPQR',CL8' ',C'STUVWXYZ',CL6' '
         DC    C'0123456789',CL6' '
         SPACE 3
         DS    0D
COMTAB   DC    193X'00'
*                 A B C D E F G H I
         DC    X'04000A030005000C09',XL7'00'
*                 J K L M N O P Q R
         DC    X'0B000200010F0D0E06',XL8'00'
*                 S T U V W X Y Z
         DC    X'0700000800030010',XL23'00'
         SPACE 3
MODLIST  DC    CL8' '
         DC    CL8'CSCSA03D'     1
         DC    CL8'CSCSC03D'     2
         DC    CL8'CSCSD03D'     3
         DC    CL8'CSCSE03D'     4
         DC    CL8'CSCSF03D'     5
         DC    CL8'CSCSG03D'     6
         DC    CL8'CSCSH03D'     7
         DC    CL8'CSCSI03D'     8
         DC    CL8'CSCSJ03D'     9
         DC    CL8'CSCSK03D'     A
         DC    CL8'CSCSL03D'     B
         DC    CL8'CSCSM03D'     C
         DC    CL8'CSCSP03D'     D
         DC    CL8'CSCSQ03D'     E
         DC    CL8'CSCSR03D'     F
         DC    CL8'CSCSZ03D'    10
         DC    CL8'        '
         DC    CL8'        '
         DC    CL8'        '
         EJECT
         DS    0D
COMTAB2  DC    193X'00'
*                 A B C D E F G H I
         DC    X'010203000000000000',XL7'00'
*                 J K L M N O P Q R
         DC    X'040000000000050000',XL8'00'
*                 S T U V W X Y Z
         DC    X'0600000000000000',XL23'00'
         SPACE 3
MODLIST2 DC    CL8'        '   0
         DC    CL8'CSCZA03D'   1
         DC    CL8'CSCZB03D'   2
         DC    CL8'CSCZC03D'   3
         DC    CL8'CSCZJ03D'   4
         DC    CL8'CSCZP03D'   5
         DC    CL8'CSCZS03D'   6
         DC    CL8'        '
         EJECT
         DS    0D
COMTAB3  DC    193X'00'
*                 A B C D E F G H I
         DC    X'010000000203000405',XL7'00'
*                 J K L M N O P Q R
         DC    X'060000000007000800',XL8'00'
*                 S T U V W X Y Z
         DC    X'09000A0000000000',XL23'00'
         SPACE 3
MODLIST3 DC    CL8'        '
         DC    CL8'CSCGA03D'  1
         DC    CL8'CSCGE03D'  2
         DC    CL8'CSCGF03D'  3
         DC    CL8'CSCGH03D'  4
         DC    CL8'CSCGI03D'  5
         DC    CL8'CSCGJ03D'  6
         DC    CL8'CSCGO03D'  7
         DC    CL8'CSCGQ03D'  8
         DC    CL8'CSCGS03D'  9
         DC    CL8'CSCGU03D'  A
         DC    CL8'        '
         EJECT
         DS    0D
CSCWAIT  DC    CL8'00000200'
RESETCNT DC    F'10'
PREVREC  DC    CL96' '
         SPACE 5
RECVMSG  DS    0CL44
         DC    CL44'CMD SUBSYS -   CSC #X COMMAND ACCEPTED'
         SPACE 3
TIMEMSG  DS    0CL48
         DC    CL43'CMD SUBSYS -   CSC TIMEOUT, CMD PENDING ON '
TIMESID  DC    CL4' ',C' '
         EJECT
CSCDATA  DCB   DDNAME=CSCDATA,DSORG=PS,RECFM=U,BLKSIZE=19069,          X
               MACRF=(GM,PM)
         EJECT
SCRREPLY DC    CL63' ',CL17' '
SCRBEGIN DS    0D
SCRLIN01 DC    X'C31140403C404000'
         DC    C'CMD SUBSYSTEM -'
         DC    X'1DC8'
SCRLSREP DC    CL63' '
         DC    X'1DF0'
SCRLIN02 DC    CL80' '
SCRLIN03 DC    CL80' '
SCRLIN04 DC    CL80' '
SCRLIN05 DC    CL80' '
SCRLIN06 DC    CL80' '
SCRLIN07 DC    CL80' '
SCRLIN08 DC    CL80' '
SCRLIN09 DC    CL80' '
SCRLIN10 DC    CL80' '
SCRLIN11 DC    CL80' '
SCRLIN12 DC    CL80' '
SCRLIN13 DC    CL80' '
SCRLIN14 DC    CL80' '
SCRLIN15 DC    CL80' '
SCRLIN16 DC    CL80' '
SCRLIN17 DC    CL80' '
SCRLIN18 DC    CL80' '
SCRLIN19 DC    CL80' '
SCRLIN20 DC    CL80' '
SCRLIN21 DC    CL80' '
SCRLIN22 DC    CL80' '
SCRLIN23 DC    CL80' '
SCRLIN24 DC    C'REPLY -'
         DC    X'1DC8115DF71DF0'
SCRPLUS  DC    C' '
         DC    X'115CF813'
SCRLEN   EQU   *-SCRBEGIN
         EJECT
PFKTAB   DC    X'F1',AL3(PFK01)
         DC    X'F2',AL3(PFK02)
         DC    X'F3',AL3(PFK03)
         DC    X'F4',AL3(PFK04)
         DC    X'F5',AL3(PFK05)
         DC    X'F6',AL3(PFK06)
         DC    X'F7',AL3(PFK07)
         DC    X'F8',AL3(PFK08)
         DC    X'F9',AL3(PFK09)
         DC    X'7A',AL3(PFK10)
         DC    X'7B',AL3(PFK11)
         DC    X'7C',AL3(PFK12)
         DC    X'C1',AL3(PFK13)                                @FNB0001
         DC    X'C2',AL3(PFK14)                                @FNB0001
         DC    X'C3',AL3(PFK15)                                @FNB0001
         DC    X'C4',AL3(PFK16)                                @FNB0001
         DC    X'C5',AL3(PFK17)                                @FNB0001
         DC    X'C6',AL3(PFK18)                                @FNB0001
         DC    X'C7',AL3(PFK19)                                @FNB0001
         DC    X'C8',AL3(PFK20)                                @FNB0001
         DC    X'C9',AL3(PFK21)                                @FNB0001
         DC    X'4A',AL3(PFK22)                                @FNB0001
         DC    X'4B',AL3(PFK23)                                @FNB0001
         DC    X'4C',AL3(PFK24)                                @FNB0001
         DC    X'00'
         EJECT
OPENTEXT DC    CL80'#####   CMD  SUB-SYSTEM  MONITOR   #####'
         DC    CL80' '
         DC    CL80' '
 DC CL80'--->  ENTER  # CMDS,  ## CMDS,  OR  GRAPHIC MONITOR CMDS'
         DC    CL80'             BEGINNING WHERE CURSOR IS POSITIONED'
         DC    CL80' '
 DC CL80'--->  ENTER  NN/XX  TO CHANGE MONITOR REFRESH INTERVAL'
         DC    CL80' '
         DC    CL80'             WHERE  NN  =   NUMBER OF SECONDS'
         DC    CL80'                    XX  =   NUMBER OF INTERVALS'
         DC    CL80' '
 DC CL80'--->  ENTER  ''.XXXX''  TO CALL COMMAND PROCESSOR  XXXX'
         DC    CL80'             ''@XXXX''  TO CALL PROGRAM  XXXX'
         DC    CL80' '
         DC    CL80'--->  ENTER  ''Z''  OR  ''END''   TO TERMINATE'
         DC    CL80' '
         DC    CL80'--->  PRESS  PA1 TO STOP AUTOMATIC REFRESH'
         DC    CL80'--->  PRESS  PA2 TO REPEAT THE LAST COMMAND'
         DC    CL80'--->  PRESS  PFK 1  TO DISPLAY PFK ASSIGNMENTS'
         DC    CL80' '
 DC CL80'--->  ENTER  ''H''  TO OBTAIN HELP FOR GRAPHIC MONITOR CMDS'
         DC    CL80' '
         SPACE 5
PFKTEXT  DC    CL80'#####        PFK  ASSIGNMENTS        #####'
         DC    CL80' '
         DC    CL80' '
PFK01    DC    CL80'PFK1     =   DISPLAY PFK ASSIGNMENTS'      @FNB0001
PFK02    DC    CL80'PFK2     =   H'                            @FNB0001
PFK03    DC    CL80'PFK3     =   E'                            @FNB0001
PFK04    DC    CL80'PFK4     =   F'                            @FNB0001
PFK05    DC    CL80'PFK5     =   J'                            @FNB0001
PFK06    DC    CL80'PFK6     =   S'                            @FNB0001
PFK07    DC    CL80'PFK7     =   #J'                           @FNB0001
PFK08    DC    CL80'PFK8     =   #O'                           @FNB0001
PFK09    DC    CL80'PFK9     =   #OX'                          @FNB0001
PFK10    DC    CL80'PFK10    =   #Q'                           @FNB0001
PFK11    DC    CL80'PFK11    =   ##SD'                         @FNB0001
PFK12    DC    CL80'PFK12    =   05/50'                        @FNB0001
PFK13    DC    CL80'PFK13    =   #A'                           @FNB0001
PFK14    DC    CL80'PFK14    =   #R'                           @FNB0001
PFK15    DC    CL80'PFK15    =   ##P,T=5'                      @FNB0001
PFK16    DC    CL80'PFK16    =   ##A,T=5'                      @FNB0001
PFK17    DC    CL80'PFK17    =   #N*'                          @FNB0001
PFK18    DC    CL80'PFK18    =   #P'                           @FNB0001
PFK19    DC    CL80'PFK19    =   A'                            @FNB0001
PFK20    DC    CL80'PFK20    =   U'                            @FNB0001
PFK21    DC    CL80'PFK21    =   ##AT,T=5'                     @FNB0001
PFK22    DC    CL80'PFK22    =   #F'                           @FNB0001
PFK23    DC    CL80'PFK23    =   #I'                           @FNB0001
PFK24    DC    CL80'PFK24    =   01/50'                        @FNB0001
         DC    CL80' '
         DC    CL80' '
         DC    CL80' '
 DC CL80'--->  ENTER  NN=XXXX  AND PRESS PFK 1  TO CHANGE PFK FUNCTION'
         DC    CL80' '
 DC CL80'             WHERE  NN -   IS THE PFK NUMBER  02 - 24' FNB001
 DC CL80'                    XXXX - IS THE COMMAND TO BE ASSIGNED'
         SPACE 5
RECORD   DS    CL19069
         SPACE 5
CMDOUTPT DS    4000CL80
CMDOUTEN DC    CL80' '
         DC    30C'-',CL20'  OUTPUT TRUNCATED  ',30C'-'
         DC    X'FF'
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
