         TITLE 'IEEZP03D -   ##P  PAGING MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEEZP03D -       ##P  PAGING MONITOR                      ***
***                                                                 ***
***       VERSION -        1.1                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.  TO MVS/SP1.3               ***
***                                                                 ***
***       UPDATED -        09/01/79                                 ***
***                        01/26/83  AXC-CBT CHANGE ESTAE PARM LEN  ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEEZP03D CSECT
         LR    R12,R15
         USING IEEZP03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         MVC   WTOLIST(4),=X'002C0100'
         L     R1,XAT              WAITTIME ADDR
         L     R15,8(0,R1)         LOAD HEX TIME
         LTR   R15,R15             TIME NOT SPECIFIED
         BNZ   SETINIT             NO
         MVC   0(8,R1),=CL8'00003000'  30 SECONDS DEFAULT
         MVC   8(4,R1),=F'30'          30 SECONDS DEFAULT
         EJECT
SETINIT  L     R4,16               CVT
         L     R4,356(0,R4)        PVT
         L     R6,XAT
         L     R6,8(0,R6)          HEX NO. OF SECS
         LA    R8,PAGTABLE         PAGE STATISTICS TABLE
         MVC   0(52,R8),248(R4)
         L     R3,XAT              WAITTIME ADDR
         STIMER WAIT,DINTVL=(R3)
         EJECT
PAGOUT   MVI   PAGMSG,C' '
         MVC   PAGMSG+1(39),PAGMSG
         MVC   PSEC(10),=CL10'PAGES/SEC'
         L     R4,16               CVT ADDR
         L     R4,356(0,R4)        PVT ADDR
         SR    R7,R7
         L     R1,256(0,R4)        PVTVAMI
         A     R1,260(0,R4)        PVTVAMO
         A     R1,16(0,R8)         OLDVAMR
         S     R1,264(0,R4)        PVTVAMR
         S     R1,8(0,R8)          OLDVAMI
         S     R1,12(0,R8)         OLDVAMO
         LTR   R1,R1               OVERFLOW
         BNP   *+6                 NO
         AR    R7,R1               ACCUMULATE TOTAL PAGING
         MVC   PAGNAME(16),=CL16'VIO PAGING'
         BAL   R11,PAGFORM
         L     R1,268(0,R4)        PVTSPIN
         A     R1,272(0,R4)        PVTSPOUT
         S     R1,20(0,R8)         OLDSPIN
         S     R1,24(0,R8)         OLDSPOUT
         LTR   R1,R1               OVERFLOW
         BNP   *+6                 NO
         AR    R7,R1               ACCUMULATE TOTAL PAGING
         MVC   PAGNAME(16),=CL16'SWAP PAGING'
         BAL   R11,PAGFORM
         L     R1,284(0,R4)        PVTCAIN
         A     R1,288(0,R4)        PVTCAOUT
         A     R1,44(0,R8)         OLDCAREC
         S     R1,292(0,R4)        PVTCAREC
         S     R1,36(0,R8)         OLDCAIN
         S     R1,40(0,R8)         OLDCAOUT
         LTR   R1,R1               OVERFLOW
         BNP   *+6                 NO
         AR    R7,R1               ACCUMULATE TOTAL PAGING
         MVC   PAGNAME(16),=CL16'COMMON PAGING'
         BAL   R11,PAGFORM
         L     R1,248(0,R4)        PVTNPIN
         A     R1,252(0,R4)        PVTNPOUT
         A     R1,292(0,R4)        PVTCAREC
         A     R1,28(0,R8)         OLDNPREC
         A     R1,36(0,R8)         OLDCAIN
         A     R1,40(0,R8)         OLDCAOUT
         S     R1,284(0,R4)        PVTCAIN
         S     R1,288(0,R4)        PVTCAOUT
         S     R1,0(0,R8)          OLDNPIN
         S     R1,4(0,R8)          OLDNPOUT
         S     R1,276(0,R4)        PVTNPREC
         S     R1,44(0,R8)         OLDCAREC
         LTR   R1,R1               OVERFLOW
         BNP   *+6                 NO
         AR    R7,R1               ACCUMULATE TOTAL PAGING
         MVC   PAGNAME(16),=CL16'ADDRSPC PAGING'
         BAL   R11,PAGFORM
         LR    R1,R7               TOTAL PAGES/INTERVAL
         MVC   PAGNAME(16),=CL16'TOTAL PAGING'
         BAL   R11,PAGFORM
         B     RETURN
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
************************************************************** GLA-BFG
*  THIS WILL RECORD ANY ABENDS IN LOGREC WITH THE              GLA-BFG
*  OPERANDS, REGISTERS, AND PSW IN IT.                         GLA-BFG
************************************************************** GLA-BFG
         STM   R14,R12,12(R13)     SAVE REGISTER               GLA-BFG
         LR    R14,R13             SAVE ORIG R13               GLA-BFG
         L     R13,0(R1)           GET ESTAE PARM              GLA-BFG
         L     R2,XSASAVE          GET XSA ADDRESS             GLA-BFG
         LR    R13,R14             RESTORE R13                 GLA-BFG
         ICM   R2,15,XAL-XSA(R2)   GET PARM ADDRESS            GLA-BFG
         BZ    ESTNOP              IF NO PARMS THEN SKIP DATA  GLA-BFG
         OI    SDWADPVA-SDWA(1),SDWAVRAM+SDWAEBC SET TO VRA    GLA-BFG
*                                  AND EBCDIC                  GLA-BFG
         MVC   SDWAVRA-SDWA(L'ESTPRFX,1),ESTPRFX MOVE HEADER   GLA-BFG
         MVC   SDWAVRA+11-SDWA(8,1),0(R2) MOVE IN DATA         GLA-BFG
         MVI   SDWAURAL-SDWA(1),19 SET LENGTH OF DATA          GLA-BFG
         B     ESTNOP              SKIP CONSTANTS              GLA-BFG
         ESTMODS ,                                             GLA-BFG
ESTPRFX  DC    0CL11' ',AL1(VRAHID,7),C'OPERAND',AL1(VRARRK,8) GLA-BFG
ESTNOP   L     R2,=A(STAERTRY)     GET RETRY ADDRESS           GLA-BFG
         LA    R3,ESTMODS          POINT AT NAMES              GLA-BFG
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO,           GLA-BFG X
               RECORD=YES,RECPARM=(3),REGS=(14,12)             GLA-BFG
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEEZP03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         LA    R11,RETURN          LOAD RET ADDR
         B     MSGRTN
         EJECT
PAGFORM  LTR   R1,R1               PAGE COUNT OVERFLOW
         BM    PAGFORM1            YES
         L     R15,XAT
         SR    R14,R14
         LR    R15,R1
         M     R14,=F'100'         SET UP HUNDREDTHS
         DR    R14,R6              FIND PAGING RATE
         MVC   PAGSEC(7),=XL7'402020214B2020'
         CVD   R15,DOUBLE          PAGES
         ED    PAGSEC(7),DOUBLE+5
         B     PAGFORM2
         SPACE 1
PAGFORM1 MVC   PAGSEC(7),=CL7'*OVER*'
         SPACE 1
PAGFORM2 LA    R1,WTOLIST
         EJECT
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         BR    R11
         SPACE 3
RETURN   ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R14,SAVRET          RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         LTORG
         DS    0D
WAITTIME DC    CL8'00003000'
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
         SPACE 5
         DS    0F
WTOLIST  DS    F
PAGMSG   DS    0CL40
PAGNAME  DS    CL16
         DS    CL4
PAGSEC   DS    CL7
         DS    CL3
PSEC     DS    CL10
         SPACE 3
PAGTABLE DS    13F
         SPACE 3
ESTAELST DS    0F,(ESTAEOVX)X
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
