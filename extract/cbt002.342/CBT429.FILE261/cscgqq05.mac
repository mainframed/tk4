DISPLAY  QSTART 'QUEUE COMMAND - 3270 DISPLAY ROUTINES'
         LR    R10,R15        PREVIOUS SAVE AREA ADDR        (CMDSBMON)
         LM    R14,R15,12(R10) RELOAD R14 AND R15 AT ENTRY   (CMDSBMON)
         CR    R14,R15        LOOP ENTRY                     (CMDSBMON)
         BNE   *+12           NO                             (CMDSBMON)
         LA    R14,INTER      ALTERNATE RETURN ADDR          (CMDSBMON)
         ST    R14,12(0,R10)  CHANGE RETURN ADDR             (CMDSBMON)
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         LH    R4,QDMLNG      LOAD MSG LENGTH
         CH    R4,=H'1680'    IS THE MSG LENGTH MORE THAN 1680?
         BH    RETURN         YES. GO AWAY.
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR         YES. WRITE SCREEN.
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         AH    R4,QDNEXT      ADD CURRENT LOCATION ON SCREEN
         CH    R4,=H'1680'    IS THERE ROOM ON THE SCREEN?
         BH    WRTSCR         NO. WRITE SCREEN.
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER
         LR    R1,R4          SAVE LINE NUMBER
         LA    R4,QDLINE1(R4) LOAD ADDRESS OF NEXT LINE
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE
         LA    R5,80          LENGTH MUST BE MULTIPLE OF 80
DSP3     CR    R5,R7          IS 5 NOT LESS THAN 7?
         BNL   DSP4           YES. GO DO IT.
         LA    R5,80(R5)      INCR BY 80
         B     DSP3           TRY AGAIN
DSP4     AR    R1,R5          UPDATE LINE NUMBER
         STH   R1,QDNEXT      STORE LINE NUMBER
         MVCL  R4,R6          MOVE THE MESSAGE TO THE SCREEN
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY                               *
*                                                                     *
***********************************************************************
WRTSCR   TR    QDLINE1(256),TABLE ENSURE PRINTABILITY
         TR    QDLINE1+256(256),TABLE
         TR    QDLINE1+512(256),TABLE
         TR    QDLINE1+768(256),TABLE
         TR    QDLINE1+1024(256),TABLE
         TR    QDLINE1+1280(256),TABLE
         TR    QDLINE1+1536(144),TABLE
         LA    R1,QDHLINE     DATA BEGIN                     (CMDSBMON)
         LA    R0,22          DATA LINES PER PAGE            (CMDSBMON)
         QCLNK                                               (CMDSBMON)
         LTR   R0,R0          TEST RETURN CODE               (CMDSBMON)
         BNZ   INTER          FAKE INTERUPT, DATA OVERFLOW   (CMDSBMON)
*        LA    R1,QDSCREEN    LOAD ADDRESS OF SCREEN BUFFER  (CMDSBMON)
*        TPUT  (1),QDSLNG,FULLSCR WRITE SCREEN               (CMDSBMON)
         LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE
         LA    R5,1680        LOAD LENGTH OF SCREEN
         SR    R6,R6          NO SENDING FIELD NEEDED
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER
         SR    R7,R7          FILL SCREEN WITH NULLS
         MVCL  R4,R6          CLEAR THE SCREEN
         SR    R1,R1                                         (CMDSBMON)
         MVI   QDREPLY,C' '                                  (CMDSBMON)
         MVC   QDREPLY+1(62),QDREPLY                         (CMDSBMON)
*        LA    R1,QDREPLY     LOAD ADDRESS OF REPLY AREA     (CMDSBMON)
*        TGET  (1),63         WAIT FOR REPLY                 (CMDSBMON)
*        CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?   (CMDSBMON)
*        BNE   NOCLEAR        NO. CONTINUE.                  (CMDSBMON)
*        TCLEARQ INPUT        CLEAR THE QUEUE                (CMDSBMON)
NOCLEAR  STH   R1,QDRLNG      STORE LENGTH OF REPLY
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW
         B     DSP2           CONTINUE PROCESSING
INTER    L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
* TABLE OF PRINTABLE CHARACTERS
TABLE    DC    CL64' '
*     0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF
 DC C'          Ö.<(+×&&         !$*);^-/         ,%_>?          :#@'''
 DC C'="' CONTINUANCE OF PREVIOUS LINE BECAUSE OF DUPLICATION
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ                      '
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ      0123456789      '
         QCOMMON
         END
