         TITLE 'SE2GO03D -   O  I/O MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       SE2GO03D -       O  I/O MONITOR                           ***
***                                                                 ***
***       VERSION -        1.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITH  SE2 AND SU64.       ***
***                                                                 ***
***       UPDATED -        04/01/80                                 ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
CSCGO03D CSECT
         LR    R12,R15
         LA    R7,4095(0,R12)
         LA    R7,1(0,R7)
         USING CSCGO03D,R12,R7
         MODESET KEY=ZERO,MODE=SUP
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         SPACE 1
PARMCHK  L     R3,XAL
         LA    R3,0(0,R3)
         MVI   PARMDEV,C'D'        DASD IS DEFAULT
         LTR   R3,R3               ANY PARM
         BZ    PARMCHK1            NO
         MVC   PARMDEV(1),0(R3)    SET SCAN TYPE
         MVC   DOUBLE(2),=C'00'
         MVZ   DOUBLE(2),1(R3)     BUSY PCT
         CLC   DOUBLE(2),=C'00'    NUMERIC
         BNE   PARMCHK1            NO
         PACK  DOUBLE(8),1(2,R3)
         CVB   R1,DOUBLE
         ST    R1,THRLIMIT
         SPACE 1
PARMCHK1 LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         EJECT
CSCSTART L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         MVC   WTOLIST(84),SRMOVLY
         L     R5,88(0,R4)         WAMT
         L     R1,THRTSOPG         TSO PERF GROUP NUMBER
         SLL   R1,2                TIMES 4
         L     R6,80(R1,R5)        WAMP OFFSET
         AR    R5,R6               WAMP ADDRESS
         SR    R14,R14
         L     R15,0(0,R5)         TOTAL ENDED TRANS
         LTR   R15,R15             ZERO TRANS
         BZ    NOTSO               YES, BYPASS 0C9
         L     R15,12(0,R5)        TOTAL ELAPSED TIME FOR ENDED TRANS
         D     R14,0(0,R5)         DIVIDE BY ENDED TRANSACTIONS
         SR    R14,R14
         D     R14,=F'1000'
         SPACE 1
NOTSO    CVD   R15,DOUBLE          AVG SECONDS RESPONSE TIME
         SRDL  R14,32
         D     R14,=F'10'
         CVD   R15,DOUBLEX
         MVC   WTOLIST+8(7),=X'402021204B2020'
         ED    WTOLIST+8(4),DOUBLE+6
         UNPK  WTOLIST+13(2),DOUBLEX+6(2)
         OI    WTOLIST+14,X'F0'
         SR    R1,R1
         IC    R1,376(0,R4)        LONG TERM CPU AVG
         CVD   R1,DOUBLE
         MVC   WTOLIST+25(4),=X'40212020'
         ED    WTOLIST+25(4),DOUBLE+6
         LH    R1,374(0,R4)        AVERAGE CPU
         CVD   R1,DOUBLE
         MVC   WTOLIST+38(4),=X'40212020'
         ED    WTOLIST+38(4),DOUBLE+6
         LH    R1,858(0,R4)        AVERAGE PAGING
         CVD   R1,DOUBLE
         MVC   WTOLIST+51(4),=X'40212020'
         ED    WTOLIST+51(4),DOUBLE+6
         LH    R1,846(0,R4)        AVERAGE UIC
         CVD   R1,DOUBLE
         MVC   WTOLIST+65(4),=X'40212020'
         ED    WTOLIST+65(4),DOUBLE+6
         LH    R1,854(0,R4)        AVERAGE ASMQ
         CVD   R1,DOUBLE
         MVC   WTOLIST+78(4),=X'40212020'
         ED    WTOLIST+78(4),DOUBLE+6
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              MCS ID OF REQUESTING CONSOLE
         SVC   35
         MVI   WTOLIST+4,C' '
         MVC   WTOLIST+5(79),WTOLIST+4
         MVC   WTOLIST(4),=X'00540100'
         L     R8,XAA              ASCB/OUCB INFO STORAGE
         USING JOBTABLE,R8
         XC    C1BSYCNT(4),C1BSYCNT
         XC    C2BSYCNT(4),C2BSYCNT
         XC    C3BSYCNT(4),C3BSYCNT
         XC    C4BSYCNT(4),C4BSYCNT
         XC    C5BSYCNT(4),C5BSYCNT
         XC    C6BSYCNT(4),C6BSYCNT
         XC    C7BSYCNT(4),C7BSYCNT
         XC    C8BSYCNT(4),C8BSYCNT
         XC    C9BSYCNT(4),C9BSYCNT
         XC    CABSYCNT(4),CABSYCNT
         XC    CBBSYCNT(4),CBBSYCNT
         XC    CCBSYCNT(4),CCBSYCNT
         XC    CDBSYCNT(4),CDBSYCNT
         XC    CEBSYCNT(4),CEBSYCNT
         XC    CFBSYCNT(4),CFBSYCNT
         L     R3,THRSTIMR
         BAL   R10,CHKUCB          CLEAR UCB TABLE
         BAL   R10,CHNLOOP
         CLI   WJOB1,C' '          ANY OUTPUT
         BE    CHNNODSP            NO
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              MCS ID OF REQUESTING CONSOLE
         SVC   35
         SPACE 1
CHNNODSP MVI   WTOLIST+4,C' '
         MVC   WTOLIST+5(79),WTOLIST+4
         MVC   WTOLIST(4),=X'00540100'
         BAL   R10,CHKUCB6         CHECK BUSY UCBS
         B     RETURN
         EJECT
CHNLOOP  STIMER WAIT,DINTVL=THRINTVL
         LR    R4,R10
         BAL   R10,CHKUCB1         CHECK BUSY UCBS
         LR    R10,R4
         SPACE 1
         TCH   CHAN1               TEST CHANNEL 01 BUSY
         L     R1,C1BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C1BSYCNT         SAVE BUSY COUNT
         TCH   CHAN2               TEST CHANNEL 02 BUSY
         L     R1,C2BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C2BSYCNT         SAVE BUSY COUNT
         TCH   CHAN3               TEST CHANNEL 03 BUSY
         L     R1,C3BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C3BSYCNT         SAVE BUSY COUNT
         TCH   CHAN4               TEST CHANNEL 04 BUSY
         L     R1,C4BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C4BSYCNT         SAVE BUSY COUNT
         TCH   CHAN5               TEST CHANNEL 05 BUSY
         L     R1,C5BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C5BSYCNT         SAVE BUSY COUNT
         TCH   CHAN6               TEST CHANNEL 06 BUSY
         L     R1,C6BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C6BSYCNT         SAVE BUSY COUNT
         TCH   CHAN7               TEST CHANNEL 07 BUSY
         L     R1,C7BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C7BSYCNT         SAVE BUSY COUNT
         TCH   CHAN8               TEST CHANNEL 08 BUSY
         L     R1,C8BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C8BSYCNT         SAVE BUSY COUNT
         EJECT
         TCH   CHAN9               TEST CHANNEL 09 BUSY
         L     R1,C9BSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,C9BSYCNT         SAVE BUSY COUNT
         TCH   CHAN10              TEST CHANNEL 10 BUSY
         L     R1,CABSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CABSYCNT         SAVE BUSY COUNT
         TCH   CHAN11              TEST CHANNEL 11 BUSY
         L     R1,CBBSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CBBSYCNT         SAVE BUSY COUNT
         TCH   CHAN12              TEST CHANNEL 12 BUSY
         L     R1,CCBSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CCBSYCNT         SAVE BUSY COUNT
         TCH   CHAN13              TEST CHANNEL 13 BUSY
         L     R1,CDBSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CDBSYCNT         SAVE BUSY COUNT
         TCH   CHAN14              TEST CHANNEL 14 BUSY
         L     R1,CEBSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CEBSYCNT         SAVE BUSY COUNT
         TCH   CHAN15              TEST CHANNEL 15 BUSY
         L     R1,CFBSYCNT         LOAD CURRENT BUSY COUNT
         BC    1,CHNEND            END OF CHANNELS
         BC    13,*+8              NOT BUSY
         LA    R1,1(0,R1)          ADD 1 TO BUSY COUNT
         ST    R1,CFBSYCNT         SAVE BUSY COUNT
         EJECT
CHNEND   BCT   R3,CHNLOOP
         SR    R14,R14
         L     R15,C1BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C1BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C1BSYPCT
         SR    R14,R14
         L     R15,C2BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C2BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C2BSYPCT
         SR    R14,R14
         L     R15,C3BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C3BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C3BSYPCT
         SR    R14,R14
         L     R15,C4BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C4BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C4BSYPCT
         SR    R14,R14
         L     R15,C5BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C5BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C5BSYPCT
         EJECT
         SR    R14,R14
         L     R15,C6BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C6BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C6BSYPCT
         SR    R14,R14
         L     R15,C7BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C7BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C7BSYPCT
         SR    R14,R14
         L     R15,C8BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C8BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C8BSYPCT
         SR    R14,R14
         L     R15,C9BSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,C9BSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,C9BSYPCT
         SR    R14,R14
         L     R15,CABSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CABSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CABSYPCT
         EJECT
         SR    R14,R14
         L     R15,CBBSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CBBSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CBBSYPCT
         SR    R14,R14
         L     R15,CCBSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CCBSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CCBSYPCT
         SR    R14,R14
         L     R15,CDBSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CDBSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CDBSYPCT
         SR    R14,R14
         L     R15,CEBSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CEBSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CEBSYPCT
         SR    R14,R14
         L     R15,CFBSYCNT
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         L     R1,CFBSYPCT         CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+8                 NO
         LA    R1,30               FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         ST    R1,CFBSYPCT
         EJECT
CHKCHN1  LA    R3,C1BSYPCT         CHANNEL BUSY PCT
         LA    R4,C1BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'01'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C2BSYPCT         CHANNEL BUSY PCT
         LA    R4,C2BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'02'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C3BSYPCT         CHANNEL BUSY PCT
         LA    R4,C3BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'03'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C4BSYPCT         CHANNEL BUSY PCT
         LA    R4,C4BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'04'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C5BSYPCT         CHANNEL BUSY PCT
         LA    R4,C5BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'05'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C6BSYPCT         CHANNEL BUSY PCT
         LA    R4,C6BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'06'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C7BSYPCT         CHANNEL BUSY PCT
         LA    R4,C7BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'07'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,C8BSYPCT         CHANNEL BUSY PCT
         LA    R4,C8BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'08'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         EJECT
         LA    R3,C9BSYPCT         CHANNEL BUSY PCT
         LA    R4,C9BSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'09'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CABSYPCT         CHANNEL BUSY PCT
         LA    R4,CABSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'10'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CBBSYPCT         CHANNEL BUSY PCT
         LA    R4,CBBSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'11'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CCBSYPCT         CHANNEL BUSY PCT
         LA    R4,CCBSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'12'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CDBSYPCT         CHANNEL BUSY PCT
         LA    R4,CDBSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'13'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CEBSYPCT         CHANNEL BUSY PCT
         LA    R4,CEBSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'14'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         LA    R3,CFBSYPCT         CHANNEL BUSY PCT
         LA    R4,CFBSYTIM         CHANNEL BUSY TIME
         LA    R5,THRLIMIT         CHANNEL BUSY PERCENT LIMIT
         LA    R6,THRBSYTM         CHANNEL BUSY TIME THRESHOLD
         LA    R9,=C'15'           CHANNEL NUMBER
         BAL   R11,CHNDISP
         BR    R10
         EJECT
CHNDISP  L     R1,0(0,R3)
         C     R1,0(0,R5)          THRESHOLD REACHED
         BL    CHNDISP1            NO
         TIME  BIN
         L     R1,0(0,R4)          OLD TIME
         LTR   R1,R1               ANY OLD TIME
         BNZ   *+8                 YES
         ST    R0,0(0,R4)          OLD TIME = NEW TIME
         LR    R1,R0               BINARY TIME
         S     R1,0(0,R4)          INTERVAL TIME
         LTR   R1,R1               TIME EQUAL 0
         BCR   8,R11               YES
         BP    CHNDISP2
         SPACE 1
CHNDISP1 XC    0(4,R4),0(R4)
         BR    R11
         SPACE 1
CHNDISP2 LR    R14,R1
         SRDL  R14,32
         D     R14,=F'100'         SECONDS
         L     R1,0(0,R3)          PERCENT BUSY
         CVD   R1,DOUBLE
         CVD   R15,DOUBLEX
         C     R15,0(0,R6)         CHANNEL BUSY TIME THRESHOLD REACHED
         BCR   4,R11               NO
         CLI   WJOB1,C' '          1ST PASS
         BNE   CHNDISP3            NO
         MVC   WJOB1(16),=CL16'CHANNEL XX  XXX%'
         MVC   WJOB1+8(2),0(R9)    CHANNEL NUMBER
         MVC   WJOB1+11(4),=X'40202120'
         ED    WJOB1+11(4),DOUBLE+6
         BR    R11
         SPACE 1
CHNDISP3 CLI   WJOB2,C' '          2ND PASS
         BNE   CHNDISP4            NO
         MVC   WJOB2(16),=CL16'CHANNEL XX  XXX%'
         MVC   WJOB2+8(2),0(R9)    CHANNEL NUMBER
         MVC   WJOB2+11(4),=X'40202120'
         ED    WJOB2+11(4),DOUBLE+6
         BR    R11
         SPACE 1
CHNDISP4 CLI   WJOB3,C' '          3RD PASS
         BNE   CHNDISP5            NO
         MVC   WJOB3(16),=CL16'CHANNEL XX  XXX%'
         MVC   WJOB3+8(2),0(R9)    CHANNEL NUMBER
         MVC   WJOB3+11(4),=X'40202120'
         ED    WJOB3+11(4),DOUBLE+6
         BR    R11
         SPACE 1
CHNDISP5 MVC   WJOB4(16),=CL16'CHANNEL XX  XXX%'
         MVC   WJOB4+8(2),0(R9)    CHANNEL NUMBER
         MVC   WJOB4+11(4),=X'40202120'
         ED    WJOB4+11(4),DOUBLE+6
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         MVI   JOBMSG,C' '
         MVC   JOBMSG+1(79),JOBMSG
         BR    R11
         EJECT
CHKUCB   STM   R0,R15,SAVEREGS
         MVC   UCBTABND(2),=X'FFFF'
         LA    R6,UCBTAB-12
         SPACE 1
CHKUCB0  LA    R6,12(0,R6)
         CLC   0(2,R6),=X'FFFF'    END OF TABLE
         BE    CHKUCB9             YES
         CLI   2(R6),X'00'         REFERENCED LAST PASS
         BNE   *+10                YES
         XC    0(8,R6),0(R6)
         MVI   2(R6),X'00'
         XC    8(4,R6),8(R6)
         B     CHKUCB0
         SPACE 1
CHKUCB1  XC    UCBDMSAV(24),UCBDMSAV
         STM   R0,R15,SAVEREGS
         L     R9,16               .CVT
         L     R9,40(0,R9)         .UCB LOOKUP TABLE
         SPACE 1
CHKUCB1A BAL   R10,UCBDML#0        GET NEXT UCB ADDR
         LTR   R1,R1               END OF UCBS
         BZ    CHKUCB9             YES, EXIT
         LA    R6,UCBTAB-12
         TM    3(R1),X'88'         ONLINE/ALLOCATED
         BNO   CHKUCB1A            NO
         CLI   PARMDEV,C'A'        ALL DEV REQ
         BE    CHKUCB2             YES
         CLI   PARMDEV,C'D'        DASD REQ
         BNE   *+12                NO
         CLI   18(R1),X'20'        DASD
         BE    CHKUCB2             YES
         CLI   PARMDEV,C'T'        TAPE REQ
         BNE   *+12                NO
         CLI   18(R1),X'80'        TAPE
         BE    CHKUCB2             YES
         CLI   PARMDEV,C'V'        ALL DEV WITH VOLSER REQ
         BNE   *+20                NO
         CLI   18(R1),X'20'        DASD
         BE    CHKUCB2             YES
         CLI   18(R1),X'80'        TAPE
         BE    CHKUCB2             YES
         CLI   PARMDEV,C'U'        UNIT REC REQ
         BNE   *+12                NO
         CLI   18(R1),X'08'        UNIT REC
         BE    CHKUCB2             YES
         CLI   PARMDEV,C'C'        COMM REQ
         BNE   CHKUCB1A            NO
         CLI   18(R1),X'20'        DASD
         BE    CHKUCB1A            YES
         CLI   18(R1),X'80'        TAPE
         BE    CHKUCB1A            YES
         CLI   18(R1),X'08'        UNIT REC
         BE    CHKUCB1A            YES
         SPACE 1
CHKUCB2  LA    R6,12(0,R6)         NEXT UCB ENTRY
         CLC   0(2,R6),=X'FFFF'    END OF UCB TABLE
         BE    CHKUCB3             YES, CHECK FOR EMPTY SLOT
         CLM   R1,3,0(R6)          UCB ADDR MATCH
         BNE   CHKUCB2             NO
         B     CHKUCB5
         SPACE 1
CHKUCB3  LA    R6,UCBTAB-12
         SPACE 1
CHKUCB4  LA    R6,12(0,R6)         NEXT UCB ENTRY
         CLC   0(2,R6),=X'FFFF'    END OF UCB TABLE
         BE    CHKUCB1A            YES, NEXT UCB
         CLC   0(2,R6),=X'0000'    EMPTY SLOT
         BNE   CHKUCB4             NO, NEXT ENTRY
         SPACE 1
CHKUCB5  STCM  R1,3,0(R6)          SAVE UCB ADDR
         MVI   2(R6),X'FF'         SLOT IN USE
         L     R1,8(0,R6)
         LA    R1,1(0,R1)
         ST    R1,8(0,R6)
         B     CHKUCB1A
         EJECT
CHKUCB6  STM   R0,R15,SAVEREGS
         XC    PREVUADR(4),PREVUADR
         SPACE 1
CHKUCB6A LA    R6,UCBTAB-12
         XC    CURRULST(4),CURRULST
         MVC   CURRUADR(4),=X'0000FFFF'
         SPACE 1
CHKUCB7  LA    R6,12(0,R6)
         CLC   0(2,R6),=X'FFFF'    END OF UCB TABLE
         BE    CHKUCB7A            YES, GO CHECK
         SR    R1,R1
         ICM   R1,3,0(R6)          UCB ADDR
         C     R1,PREVUADR         COMPARE TO PREVIOUS UCB
         BNH   CHKUCB7             LOWER, NEXT
         C     R1,CURRUADR         COMPARE TO CURRENT UCB
         BNL   CHKUCB7             NOT LOWER, NEXT
         ST    R1,CURRUADR
         ST    R6,CURRULST
         B     CHKUCB7
         SPACE 1
CHKUCB7A L     R1,CURRUADR         LOAD UCB ADDR
         L     R6,CURRULST         LOAD UCB TABLE ENTRY ADDR
         LTR   R6,R6               END OF UCBS
         BZ    CHKUCB9             YES
         ST    R1,PREVUADR         SAVE PREV UCB ADDR
         LA    R3,UCBNRDY
         LA    R4,THRBSYTM
         TM    6(R1),X'40'         NOT READY
         BO    CHKUCB7B
         LA    R3,UCBBUSY
         LA    R4,THRBSYTM
         SR    R14,R14
         L     R15,8(0,R6)         UCB BUSY COUNT
         XC    8(4,R6),8(R6)
         M     R14,=F'100'
         D     R14,THRSTIMR        PERCENT BUSY
         SR    R1,R1
         IC    R1,3(0,R6)          CUMULATIVE PERCENT
         LTR   R1,R1               ZERO
         BNZ   *+6                 NO
         LR    R1,R15              FAKE START
         MH    R1,=H'3'            3/4 PREVIOUS
         AR    R1,R15              1/4 CURRENT
         SRL   R1,2
         STC   R1,3(0,R6)          NEW CUM PERCENT
         C     R1,THRLIMIT         THRESHOLD REACHED
         BL    CHKUCB7C            NO
         SPACE 1
CHKUCB7B TIME  BIN
         L     R1,4(0,R6)          OLD TIME
         LTR   R1,R1               ANY OLD TIME
         BNZ   *+8                 YES
         ST    R0,4(0,R6)          OLD TIME = NEW TIME
         LR    R1,R0               BINARY TIME
         S     R1,4(0,R6)          INTERVAL TIME
         LTR   R1,R1               TIME EQUAL 0
         BZ    CHKUCB6A            YES
         BP    CHKUCB8
         SPACE 1
CHKUCB7C XC    4(8,R6),4(R6)
         B     CHKUCB6A
         EJECT
CHKUCB8  LR    R14,R1
         SRDL  R14,32
         D     R14,=F'100'         SECONDS
         SR    R1,R1
         IC    R1,3(0,R6)
         CVD   R1,DOUBLE
         CVD   R15,DOUBLEX
         C     R15,0(0,R4)         TIME THRESHOLD REACHED
         BL    CHKUCB6A            NO
         CLI   WJOB1,C' '          1ST PASS
         BNE   CHKUCB8A            NO
         MVC   WJOB1(16),0(R3)
         SR    R1,R1
         ICM   R1,3,0(R6)          UCB ADDR
         MVC   WJOB1(3),13(R1)     DEVICE ADDRESS
         TM    18(R1),X'A0'        DASD OR TAPE
         BZ    *+10                NO, NO VOLSER
         MVC   WJOB1+4(6),28(R1)   DEVICE VOLSER
         LA    R1,UCBNRDY
         CR    R1,R3               NOT READY MESSAGE
         BE    CHKUCB6A            YES, NO PERCENT
         MVC   WJOB1+11(4),=X'40202120'
         ED    WJOB1+11(4),DOUBLE+6
         B     CHKUCB6A
         SPACE 1
CHKUCB8A CLI   WJOB2,C' '          2ND PASS
         BNE   CHKUCB8B            NO
         MVC   WJOB2(16),0(R3)
         SR    R1,R1
         ICM   R1,3,0(R6)          UCB ADDR
         MVC   WJOB2(3),13(R1)     DEVICE ADDRESS
         TM    18(R1),X'A0'        DASD OR TAPE
         BZ    *+10                NO, NO VOLSER
         MVC   WJOB2+4(6),28(R1)   DEVICE VOLSER
         LA    R1,UCBNRDY
         CR    R1,R3               NOT READY MESSAGE
         BE    CHKUCB6A            YES, NO PERCENT
         MVC   WJOB2+11(4),=X'40202120'
         ED    WJOB2+11(4),DOUBLE+6
         B     CHKUCB6A
         SPACE 1
CHKUCB8B CLI   WJOB3,C' '          3RD PASS
         BNE   CHKUCB8C            NO
         MVC   WJOB3(16),0(R3)
         SR    R1,R1
         ICM   R1,3,0(R6)          UCB ADDR
         MVC   WJOB3(3),13(R1)     DEVICE ADDRESS
         TM    18(R1),X'A0'        DASD OR TAPE
         BZ    *+10                NO, NO VOLSER
         MVC   WJOB3+4(6),28(R1)   DEVICE VOLSER
         LA    R1,UCBNRDY
         CR    R1,R3               NOT READY MESSAGE
         BE    CHKUCB6A            YES, NO PERCENT
         MVC   WJOB3+11(4),=X'40202120'
         ED    WJOB3+11(4),DOUBLE+6
         B     CHKUCB6A
         SPACE 1
CHKUCB8C MVC   WJOB4(16),0(R3)
         SR    R1,R1
         ICM   R1,3,0(R6)          UCB ADDR
         MVC   WJOB4(3),13(R1)     DEVICE ADDRESS
         TM    18(R1),X'A0'        DASD OR TAPE
         BZ    *+10                NO, NO VOLSER
         MVC   WJOB4+4(6),28(R1)   DEVICE VOLSER
         LA    R1,UCBNRDY
         CR    R1,R3               NOT READY MESSAGE
         BE    CHKUCB8D            YES, NO PERCENT
         MVC   WJOB4+11(4),=X'40202120'
         ED    WJOB4+11(4),DOUBLE+6
         SPACE 1
CHKUCB8D LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         MVI   JOBMSG,C' '
         MVC   JOBMSG+1(79),JOBMSG
         B     CHKUCB6A
         SPACE 1
CHKUCB9  LM    R0,R15,SAVEREGS
         BR    R10
         EJECT
UCBDML#0 ST    R8,UCBDMSAV         .SAVE WORK REG 1
         ST    R10,UCBDMSAV+8      .SAVE WORK REG 3
         ST    R11,UCBDMSAV+12     .SAVE WORK REG 4
         L     R8,UCBDMLOW         .SET SEARCH ARGUMENT
         SPACE 1
UCBDML#1 LA    R8,1(0,R8)          .NEXT UNIT ADDR POSSIBLE
         L     R10,=X'0000FFFF'    .SET MAX NEXT LEAST HIGH
         SPACE 1
UCBDML#2 CLC   0(2,R9),=X'FFFF'    .END OF TABLE
         BE    UCBDMOUT            .YES, GO PROCESS
         SR    R1,R1
         ICM   R1,3,0(R9)          .LOAD UCB ADDR
         LTR   R1,R1               .ZERO ADDR
         BNZ   UCBDML#4            .NO, CONTINUE
         SPACE 1
UCBDML#3 LA    R9,2(0,R9)          .NEXT UCB LOOKUP TABLE ENTRY
         B     UCBDML#2
         SPACE 1
UCBDML#4 TM    6(R1),X'8A'         BUSY AND/OR CHANNEL PROGRAM ACTIVE
         BZ    UCBDML#3            NO, NEXT UCB
         BAL   R11,UCBDMCNV        .CONVERT UNIT ADDR TO HEX
         CR    R1,R8               .UNIT EQUAL
         BL    UCBDML#3            .LOW NOT EQUAL, NEXT UCB
         LR    R10,R1              .SET NEXT LEAST HIGH UNIT ADDR
         MVC   UCBDMCUR+2(2),0(R9) .SET NEXT LEAST HIGH UCB ADDR
         B     UCBDMOUT            .GO PROCESS
         EJECT
UCBDMCNV SR    R14,R14
         SR    R15,R15
         IC    R15,13(0,R1)        .UNIT ADDR DIGIT
         TM    13(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         IC    R15,14(0,R1)        .UNIT ADDR DIGIT
         TM    14(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         IC    R15,15(0,R1)        .UNIT ADDR DIGIT
         TM    15(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         LR    R1,R14              .SET RESULTS REG
         BR    R11
         SPACE 3
UCBDMOUT C     R10,=X'0000FFFF'    .ALL DONE
         BE    UCBDMEX             .YES
         ST    R10,UCBDMLOW        .NEXT LOW UNIT ADDR
         L     R1,UCBDMCUR         .LOAD UCB ADDR FOR FOUND UNIT
         B     UCBDMCON            .CONTINUE
         SPACE 1
UCBDMEX  SR    R1,R1               .CLEAR UCB ADDR REGISTER
         L     R8,UCBDMSAV         .RELOAD WORK REG 1
         L     R10,UCBDMSAV+8      .RELOAD WORK REG 3
         L     R11,UCBDMSAV+12     .RELOAD WORK REG 4
         B     UCBDMRET            .END OF UCBS
         SPACE 1
UCBDMCON L     R8,UCBDMSAV         .RELOAD WORK REG 1
         L     R10,UCBDMSAV+8      .RELOAD WORK REG 3
         L     R11,UCBDMSAV+12     .RELOAD WORK REG 4
         SPACE 1
UCBDMRET EQU   *
         BR    R10
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(CSCGO03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         LA    R11,RETURN          LOAD RET ADDR
         EJECT
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         MVI   JOBMSG,C' '
         MVC   JOBMSG+1(79),JOBMSG
         BR    R11
         SPACE 3
RETURN   CLI   WJOB1,C' '          ANY OUTPUT
         BE    EXIT                NO
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         SPACE 1
EXIT     ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R2,SAVRET           RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         MODESET KEY=NZERO,MODE=PROB
         SR    R15,R15
         LR    R14,R2
         BR    R14
         EJECT
         LTORG
         EJECT
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
UCBNRDY  DC    CL16'XXX         NRDY'
UCBBUSY  DC    CL16'XXX         XXX%'
         SPACE 3
         DS    0F
STAEMSG  DC    X'00540100'
         DC    CL80'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
SRMOVLY  DS    0CL84
         DC    XL4'00540100'
         DC    CL5'TSO-',CL10' '
         DC    CL6'LGCP-',CL7' '
         DC    CL6'AVCP-',CL7' '
         DC    CL6'AVPG-',CL7' '
         DC    CL7'AVUIC-',CL7' '
         DC    CL6'ASMQ-',CL6' '
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*        THRESHOLD VALUES FOR ALL DISPLAY LIMITS                      *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 5
         DS    0D
THRINTVL DC    CL8'00000020'       1/5 SEC STIMER FOR CHANNEL BUSY
THRSTIMR DC    F'50'               CHANNEL BUSY STIMER REPETITIONS
***   THE ABOVE VALUES MULTIPLIED TOGETHER GIVE THE MINIMUM
***   REFRESH FOR THIS COMMAND
THRTSOPG DC    F'13'               TSO PERFORMANCE GROUP
THRBSYTM DC    F'05'               BUSY / NOT READY THRESHOLD
THRLIMIT DC    F'20'               BUSY / NOT READY LIMIT
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
SAVEREGS DS    18F
DOUBLE   DS    2F
DOUBLEX  DS    2F
PARMDEV  DS    F
XSASAVE  DS    F
PREVUADR DS    F
CURRUADR DS    F
CURRULST DS    F
         SPACE 3
UCBDMSAV DS    4F
UCBDMLOW DS    F
UCBDMCUR DS    F
         SPACE 5
         DS    0F
WTOLIST  DS    F
JOBMSG   DS    0CL80
WJOB1    DS    CL16
         DS    CL5
WJOB2    DS    CL16
         DS    CL5
WJOB3    DS    CL16
         DS    CL5
WJOB4    DS    CL16
         DS    CL1
         SPACE 5
ESTAELST DS    0F,(ESTAEOVX)X
WORKEND  DS    0F
         EJECT
JOBTABLE DSECT
C1BSYTIM DS    F
C1BSYCNT DS    F
C1BSYPCT DS    F
C2BSYTIM DS    F
C2BSYCNT DS    F
C2BSYPCT DS    F
C3BSYTIM DS    F
C3BSYCNT DS    F
C3BSYPCT DS    F
C4BSYTIM DS    F
C4BSYCNT DS    F
C4BSYPCT DS    F
C5BSYTIM DS    F
C5BSYCNT DS    F
C5BSYPCT DS    F
C6BSYTIM DS    F
C6BSYCNT DS    F
C6BSYPCT DS    F
C7BSYTIM DS    F
C7BSYCNT DS    F
C7BSYPCT DS    F
C8BSYTIM DS    F
C8BSYCNT DS    F
C8BSYPCT DS    F
C9BSYTIM DS    F
C9BSYCNT DS    F
C9BSYPCT DS    F
CABSYTIM DS    F
CABSYCNT DS    F
CABSYPCT DS    F
CBBSYTIM DS    F
CBBSYCNT DS    F
CBBSYPCT DS    F
CCBSYTIM DS    F
CCBSYCNT DS    F
CCBSYPCT DS    F
CDBSYTIM DS    F
CDBSYCNT DS    F
CDBSYPCT DS    F
CEBSYTIM DS    F
CEBSYCNT DS    F
CEBSYPCT DS    F
CFBSYTIM DS    F
CFBSYCNT DS    F
CFBSYPCT DS    F
UCBTAB   DS    100CL12
UCBTABND DS    F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
XAA      DS    F
         SPACE 5
CHAN1    EQU   256
CHAN2    EQU   512
CHAN3    EQU   768
CHAN4    EQU   1024
CHAN5    EQU   1280
CHAN6    EQU   1536
CHAN7    EQU   1792
CHAN8    EQU   2048
CHAN9    EQU   2304
CHAN10   EQU   2560
CHAN11   EQU   2816
CHAN12   EQU   3072
CHAN13   EQU   3328
CHAN14   EQU   3584
CHAN15   EQU   3840
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
