         TITLE 'CSCGF03D -   F  DISPLAY STORAGE USAGE'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       CSCGF03D -       F  DISPLAY STORAGE USAGE                 ***
***                                                                 ***
***       VERSION -        1.1                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITHOUT  SE2 OR SU64.     ***
***                                                                 ***
***       UPDATED -        07/19/81 AXC-CBT FIX ESTAE               ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
CSCGF03D CSECT
         LR    R12,R15
         USING CSCGF03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         L     R1,XAA
         ST    R1,JOBTABAD
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         L     R4,JOBTABAD         POINT TO TIME
         TIME  BIN
         L     R6,4(0,R4)          PREVIOUS TIME
         LR    R5,R0               CURRENT TIME
         SR    R5,R6               INTERVAL TIME
         ST    R0,4(0,R4)
         LTR   R6,R6               1ST TIME
         BZ    PFTLIST
         ST    R5,0(0,R4)          SAVE INTERVAL TIME
         EJECT
PFTLIST  MVC   OUTMSG(4),=X'00540100'
         L     R14,16              LOAD CVT ADDR
         L     R6,556(0,R14)       LOAD ASVT ADDR
         L     R7,516(0,R6)        MAX NO. OF ASCBS
         LA    R6,524(0,R6)        1ST ASCB SLOT
         L     R3,356(0,R14)       LOAD PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AH    R4,16(0,R3)         PFT ACTUAL ADDR
         SR    R5,R5               INDEX
         IC    R5,18(0,R3)            TO
         SLL   R5,8                      END OF
         IC    R5,19(0,R3)                  PFT
         SH    R5,16(0,R3)
         SRL   R5,4                DIVIDE BY 16
         STM   R4,R5,PFTREGS       SAVE PFT REGS
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SRL   R14,4               DIVIDE BY 16
         AH    R14,8(0,R3)         NON-NUCLEUS FRAMES
         ST    R14,TOTFRMS
         CVD   R14,DOUBLE          CONVERT TO DECIMAL
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(79),OUTMSG+4
         MVC   OUTMSG+4(12),=CL12'TOTAL'
         MVC   OUTMSG+12(6),=X'402020202120'
         ED    OUTMSG+12(6),DOUBLE+5
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SRL   R14,4               DIVIDE BY 16
         AH    R14,1830(0,R3)      TOTAL FIXED
         L     R1,TOTFRMS
         SR    R1,R14              TOTAL PAGABLE
         ST    R1,TOTFRMS          TOTAL PAGABLE
         CVD   R1,DOUBLE
         MVC   OUTMSG+24(12),=CL12'PAGABLE'
         MVC   OUTMSG+32(6),=X'402020202120'
         ED    OUTMSG+32(6),DOUBLE+5
         L     R14,16              CVT
         L     R14,604(0,R14)      RMCT
         SR    R1,R1
         IC    R1,320(0,R14)       LONG TERM CPU AVG
         CVD   R1,DOUBLE
         MVC   OUTMSG+44(14),=CL14'LGCPU='
         MVC   OUTMSG+50(4),=X'40202120'
         ED    OUTMSG+50(4),DOUBLE+6
         LH    R1,318(0,R14)       AVERAGE CPU
         CVD   R1,DOUBLE
         MVC   OUTMSG+58(14),=CL14'AVCPU='
         MVC   OUTMSG+64(4),=X'40202120'
         ED    OUTMSG+64(4),DOUBLE+6
         LH    R1,654(0,R14)       AVERAGE UIC
         CVD   R1,DOUBLE
         MVC   OUTMSG+72(12),=CL12'AVUIC='
         MVC   OUTMSG+78(4),=X'40202120'
         ED    OUTMSG+78(4),DOUBLE+6
         LH    R1,664(0,R14)       AVERAGE PAGING
         ST    R1,AVPAG
         LH    R1,662(0,R14)       AVERAGE ASMQ
         ST    R1,AVASQ
         SR    R2,R2
         BAL   R11,PGPRINT
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(79),OUTMSG+4
         L     R10,=X'0000FFFF'
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(79),OUTMSG+4
         L     R2,XSASAVE
         EJECT
GETASCB  LA    R6,4(0,R6)          NEXT SLOT
         BCT   R7,ASCBFND          GO PROCESS
         B     PAGEPJP             GO OUTPUT RESULTS
         SPACE 3
RETURN   EQU   *
         ESTAE 0
         L     R0,SIZE
         LR    R1,R13              WORK AREA ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
ASCBFND  TM    0(R6),X'80'         AVAILABLE SLOT
         BO    GETASCB             YES, GET NEXT SLOT
         L     R8,0(0,R6)          LOAD ASCB ADDR
         SR    R10,R10             LOAD
         IC    R10,36(0,R8)
         SLL   R10,8                  ASID
         IC    R10,37(0,R8)
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         L     R2,XSASAVE
         B     GETASCB
         EJECT
JPAGE    L     R14,16              LOAD CVT ADDR
         L     R14,556(0,R14)      LOAD ASVT ADDR
         LA    R14,524(0,R14)      1ST ASCB SLOT
         SLL   R10,2
         LA    R14,0(R10,R14)      THIS ASID ASCB SLOT
         LR    R10,R2              JOB TABLE SLOT THIS ASID
         L     R8,0(0,R14)         ASCB ADDR
         L     R2,JOBTABAD         POINT TO TIME
         L     R2,0(0,R2)          INTERVAL TIME
         L     R1,148(0,R8)        OUXB ADDR
         LA    R1,0(0,R1)
         LTR   R1,R1               VALID ADDR
         BCR   8,R11               NO
         ST    R1,SAVEOUXB
         CLC   0(24,R10),=24X'00'  1ST TIME
         BNE   JPAGE2              NO
         SPACE 1
JPAGE1   L     R1,SAVEOUXB
         MVC   0(24,R10),16(R1)    SAVE OUXB PAGING ACCUM FIELDS
         BR    R11
         SPACE 1
JPAGE2   LTR   R2,R2               1ST TIME
         BCR   8,R11
         L     R14,16(0,R1)        CURR PAGEIN
         S     R14,0(0,R10)        SUB PREV PAGEIN
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         LR    R15,R14             ACCUMULATE
         L     R14,20(0,R1)        CURR PAGEOUT
         S     R14,4(0,R10)        SUB PREV PAGEOUT
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         AR    R15,R14             ACCUMULATE
         L     R14,24(0,R1)        CURR PAGE RECLAIM
         S     R14,8(0,R10)        SUB PREV PAGE RECLAIM
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         SR    R15,R14             ACCUMULATE
         L     R14,28(0,R1)        CURR VIO PAGEIN
         S     R14,12(0,R10)       SUB PREV VIO PAGEIN
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         AR    R15,R14             ACCUMULATE
         L     R14,32(0,R1)        CURR VIO PAGEOUT
         S     R14,16(0,R10)       SUB PREV VIO PAGEOUT
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         AR    R15,R14             ACCUMULATE
         L     R14,36(0,R1)        CURR VIO RECLAIM
         S     R14,20(0,R10)       SUB PREV VIO RECLAIM
         LTR   R14,R14             OVERFLOW
         BM    JPAGE4              YES
         SR    R15,R14             ACCUMULATE
         SR    R14,R14
         M     R14,=F'100'
         DR    R14,R2              PAGES PER SEC
         CVD   R15,DOUBLE
         LR    R1,R15
         MVC   OUTMSG+25(10),=X'402021204B2020404040'
         ED    OUTMSG+25(4),DOUBLE+6
         SRDL  R14,32
         M     R14,=F'100'
         DR    R14,R2              HUNDREDTHS
         CVD   R15,DOUBLE
         UNPK  OUTMSG+30(2),DOUBLE+6(2)
         OI    OUTMSG+31,X'F0'
         SLL   R1,1
         C     R15,=F'50'          ROUND UP GRAPH
         BL    *+8                 NO
         LA    R1,1(0,R1)
         CH    R1,=H'46'
         BNH   *+8
         LH    R1,=H'46'          FORCE MAX
         EX    R1,PAGMVE
         LA    R1,OUTMSG+38(R1)
         MVI   0(R1),C'>'
         B     JPAGE1
         SPACE 1
JPAGE4   MVC   OUTMSG+25(18),=CL18'   0 '
         B     JPAGE1
         SPACE 1
PAGMVE   MVC   OUTMSG+38(0),PAGGRAPH
         EJECT
PAGEPRNT LM    R4,R5,PFTREGS       LOAD PFT REGS
         SR    R14,R14             CLEAR FRAME COUNT
         SR    R9,R9               CLEAR AVAILABLE FRAME COUNT
         SPACE 3
PAGEPR1  SR    R15,R15             LOAD
         IC    R15,0(0,R4)            PFT
         SLL   R15,8                     ASID
         IC    R15,1(0,R4)
         CR    R10,R15             ASID MATCH
         BNE   PAGEPR2             NO, DONT INCR FRAME COUNT
         LA    R14,1(0,R14)        ADD 1 TO FRAME COUNT
         LA    R2,1
         SPACE 3
PAGEPR2  LA    R4,16(0,R4)         NEXT PFT
         BCT   R5,PAGEPR1          PROCESS NEXT
         C     R10,=X'00000001'    MASTER
         BE    ASIDMSTR            YES, GO PRINT MASTER
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   PAGEPR3             NO, ASID
         L     R15,AVPAG           AVERAGE PAGING
         CVD   R15,DOUBLE
         MVC   OUTMSG+4(12),=CL12'*SYSTEM*'
         MVC   OUTMSG+27(12),=CL12'AVPAG='
         MVC   OUTMSG+33(5),=X'4020212040'
         ED    OUTMSG+33(4),DOUBLE+6
         CH    R15,=H'46'
         BNH   *+8
         LH    R15,=H'46'          FORCE MAX
         EX    R15,SYSMVE
         LA    R15,OUTMSG+38(R15)
         MVI   0(R15),C'>'
         B     PAGEPR4
         SPACE 1
SYSMVE   MVC   OUTMSG+38(0),SYSGRAPH
         EJECT
PAGEPR3  LTR   R14,R14             ANY FRAMES
         BCR   8,R11               NO, BYPASS PRINT
         CH    R10,=H'140'         MAX ASID ALLOWED
         BCR   2,R11               OVER MAX
         MH    R10,=H'28'          COUNT AREA SIZE
         A     R10,JOBTABAD        POINT TO ASID COUNT SLOTS
         ST    R14,24(0,R10)       SAVE FRAME COUNT
         BR    R11
         SPACE 3
PAGEPR4  CVD   R14,DOUBLE
         MVC   OUTMSG+12(6),=X'402020202120'
         ED    OUTMSG+12(6),DOUBLE+5
         CLC   OUTMSG+4(3),=C'AS  '
         BE    JOBFIND
         EJECT
PGPRINT  L     R1,TOTFRMS          LOAD TOTAL PAGABLE FRAMES
         LTR   R2,R2               DATA LINE
         BZ    PRINT               NO
         CVB   R15,DOUBLE
         SR    R14,R14
         M     R14,=F'100'
         DR    R14,R1
         LA    R15,1(0,R15)
         CVD   R15,DOUBLE          PERCENT FRAMES
         MVC   OUTMSG+19(5),=X'402021206C'
         ED    OUTMSG+19(4),DOUBLE+6
         SPACE 3
PRINT    LA    R1,OUTMSG           LOAD OUTPUT MSG ADDR
         SR    R0,R0
         L     R2,XSASAVE
         IC    R0,XAU
         SVC   35
         BR    R11
         SPACE 3
ASIDMSTR L     R15,AVASQ           AVERAGE ASMQ
         CVD   R15,DOUBLE
         MVC   OUTMSG+4(12),=CL12'*MASTER*'
         MVC   OUTMSG+27(12),=CL12'ASMQ='
         MVC   OUTMSG+33(5),=X'4020212040'
         ED    OUTMSG+33(4),DOUBLE+6
         CH    R15,=H'46'
         BNH   *+8
         LH    R15,=H'46'          FORCE MAX
         EX    R15,SYSMVE
         LA    R15,OUTMSG+38(R15)
         MVI   0(R15),C'>'
         B     PAGEPR4
         EJECT
PAGEPJP  L     R2,JOBTABAD         JOB TABLE
         LA    R1,140              MAX ASID
         SR    R10,R10
         XC    HIGHFRAM(4),HIGHFRAM
         XC    HIGHJCNT(4),HIGHJCNT
         XC    HIGHASID(4),HIGHASID
         SPACE 1
PAGEPJP1 LA    R2,28(0,R2)         NEXT JOB ENTRY
         LA    R10,1(0,R10)        ASID
         L     R14,24(0,R2)        FRAME COUNT
         LTR   R14,R14             GOOD FRAME COUNT
         BNP   PAGEPJP2            NO
         C     R14,HIGHFRAM        HIGHER
         BL    PAGEPJP2            ANY FRAMES
         ST    R14,HIGHFRAM        HIGH FRAME COUNT
         ST    R10,HIGHASID        HIGH ASID
         ST    R2,HIGHJCNT         HIGH COUNT ADDR
         SPACE 1
PAGEPJP2 BCT   R1,PAGEPJP1
         L     R2,HIGHJCNT
         LTR   R2,R2               END OF OUTPUT
         BZ    RETURN              YES
         L     R10,HIGHASID
         SPACE 1
PAGEPJP3 ST    R10,SAVEASID
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(79),OUTMSG+4
         MVC   OUTMSG+4(12),=CL12'AS  '
         XC    24(4,R2),24(R2)     CLEAR FRAME COUNT
         BAL   R11,JPAGE           CALCULATE JOB INTERVAL PAGING
         L     R10,SAVEASID
         L     R14,HIGHFRAM
         STH   R10,DOUBLE+6
         UNPK  DOUBLE(5),DOUBLE+6(3)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+7(4),DOUBLE  ASID TO MSG
         LA    R2,1
         BAL   R11,PAGEPR4
         B     PAGEPJP
         EJECT
JOBFIND  EQU   *
         L     R9,56(0,R8)         CSCB ADDR
         CLI   28(R9),X'03'        INIT
         BE    JOBFIND1            YES, GO SEARCH
         SPACE
JOBFIND0 MVC   OUTMSG+4(8),8(R9)   JOBNAME
         MVI   OUTMSG+12,C' '
         B     PGPRINT
         SPACE
JOBFIND1 LA    R1,0(0,R9)          SAVE ADDR
         SR    R14,R14             LOAD
         IC    R14,30(0,R9)
         SLL   R14,8                  ASID
         IC    R14,31(0,R9)
         SPACE
JOBFIND2 L     R9,0(0,R9)          NEXT CSCB
         LA    R9,0(0,R9)
         LTR   R9,R9               END OF CHAIN
         BNZ   JOBFIND3            NO, CONTINUE
         LR    R9,R1               USE ORIG CSCB
         B     JOBFIND0
         SPACE
JOBFIND3 CR    R9,R1               END OF CHAIN
         BE    JOBFIND0            YES, USE ORIG CSCB
         CH    R14,30(0,R9)        ASID EQUAL
         BE    JOBFIND0            YES, USE THIS CSCB
         B     JOBFIND2
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(CSCGF03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     RETURN
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
PAGGRAPH DC    C'0=1=2=3=4=5=6=7=8==10========15========20========='
         SPACE 3
SYSGRAPH DC    C'0=========1=========2=========3=========4========='
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
AVPAG    DS    F
AVASQ    DS    F
HIGHASID DS    F
HIGHJCNT DS    F
HIGHFRAM DS    F
SAVEASID DS    F
SAVEOUXB DS    F
JOBTABAD DS    F
TOTFRMS  DS    F
PFTREGS  DS    2F
         DS    0F
OUTMSG   DS    84C
         SPACE 1
ESTAELST DS    0F,(ESTAEOVX)X
WORKEND  DS    0F
         EJECT
JOBTAB   DSECT
JOBOUXB  DS    140XL28
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
XAA      DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
