         TITLE 'SE2GS03D -   S  SYSTEM INDICATOR DISPLAY'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       SE2GS03D -       S  SYSTEM INDICATOR DISPLAY              ***
***                                                                 ***
***       VERSION -        1.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITH  SE2 AND SU64.       ***
***                                                                 ***
***       UPDATED -        04/01/80                                 ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
CSCGS03D CSECT
         LR    R12,R15
         USING CSCGS03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         L     R3,XAL
         LA    R3,0(0,R3)
         XC    PARM(3),PARM
         LTR   R3,R3
         BZ    *+10                NO PARM
         MVC   PARM(3),0(R3)
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         MVC   WTOLIST(4),=X'00540100'
         MVC   WTOLIST+4(80),WTOHDR
         MVC   WTOLIST+22(9),=C'AVRG CURR'
         CLI   PARM,C'A'           AVERAGE GRAPHIC DISPLAY
         BNE   *+10                NO
         MVC   WTOLIST+22(9),=C'CURR AVRG'
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         L     R5,XAA
         USING AVERAGE,R5
         CLC   0(4,R5),=F'0'       1ST PASS
         BNE   SYSIND              NO
         LA    R1,LGCPU
         ST    R1,LGCPU
         LA    R1,AVCPU
         ST    R1,AVCPU
         LA    R1,AVPAG
         ST    R1,AVPAG
         LA    R1,AVUIC
         ST    R1,AVUIC
         LA    R1,AVASQ
         ST    R1,AVASQ
         EJECT
SYSIND   L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         SR    R1,R1
         IC    R1,376(0,R4)        LONG TERM CPU AVG
         LA    R6,LGCPU
         LA    R11,LGCPUEND
         BAL   R7,SYSACCUM
         LA    R6,LGCPUOVL
         LA    R8,2
         LA    R10,GRAPH2
         BAL   R7,SYSWTO
         LH    R1,374(0,R4)        AVERAGE CPU
         LA    R6,AVCPU
         LA    R11,AVCPUEND
         BAL   R7,SYSACCUM
         LA    R6,AVCPUOVL
         LA    R8,2
         LA    R10,GRAPH2
         BAL   R7,SYSWTO
         LH    R1,858(0,R4)        AVERAGE PAGING
         LA    R6,AVPAG
         LA    R11,AVPAGEND
         BAL   R7,SYSACCUM
         LA    R6,AVPAGOVL
         LA    R8,1
         LA    R10,GRAPH1
         BAL   R7,SYSWTO
         LH    R1,846(0,R4)        AVERAGE UIC
         LA    R6,AVUIC
         LA    R11,AVUICEND
         BAL   R7,SYSACCUM
         LA    R6,AVUICOVL
         LA    R8,1
         LA    R10,GRAPH1
         BAL   R7,SYSWTO
         LH    R1,854(0,R4)        AVERAGE ASMQ
         LA    R6,AVASQ
         LA    R11,AVASQEND
         BAL   R7,SYSACCUM
         LA    R6,AVASQOVL
         LA    R8,1
         LA    R10,GRAPH1
         BAL   R7,SYSWTO
         EJECT
DOMAIN   L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         MVC   DOMAINO1(80),DOMAINB1
         MVC   DOMAINO2(80),DOMAINB2
         LH    R9,188(0,R4)        NO. OF DOMAINS
         BCTR  R9,0
         L     R4,180(0,R4)        DMDT
         SPACE 1
DOMAIN1  LA    R4,56(0,R4)         NEXT DOMAIN
         CLC   6(4,R4),=F'0'       EMPTY DOMAIN
         BNE   DOMAIN2             NO
         CLC   12(4,R4),=F'0'      EMPTY DOMAIN
         BNE   DOMAIN2             NO
         BCT   R9,DOMAIN1          NEXT DOMAIN
         B     RETURN
         SPACE 1
DOMAIN2  SR    R1,R1
         IC    R1,0(0,R4)          DOMAIN NUMBER
         CVD   R1,DOUBLE
         LA    R6,DOMAINO1
         UNPK  DOMAINO1+4(2),DOUBLE+6(2)
         OI    DOMAINO1+5,X'F0'
         LH    R1,10(0,R4)         CURRENT MPL
         LR    R11,R1
         LA    R8,1
         LA    R10,GRAPH1
         BAL   R7,SYSWTO
         SR    R1,R1
         IC    R1,0(0,R4)          DOMAIN NUMBER
         CVD   R1,DOUBLE
         LA    R6,DOMAINO2
         UNPK  DOMAINO2+4(2),DOUBLE+6(2)
         OI    DOMAINO2+5,X'F0'
         LH    R1,6(0,R4)          CURRENT MPL
         LA    R1,8(0,R1)
         SRL   R1,4
         LR    R11,R1
         LA    R8,1
         LA    R10,GRAPH1
         BAL   R7,SYSWTO
         BCT   R9,DOMAIN1          NEXT DOMAIN
         B     RETURN
         EJECT
SYSACCUM SR    R14,R14
         SR    R15,R15
         L     R10,0(0,R6)         PREVIOUS SLOT
         LA    R10,4(0,R10)        NEXT SLOT
         CR    R10,R11             SLOT OK
         BL    SYSACC1             YES
         LA    R10,4(0,R6)         1ST SLOT
         SPACE 1
SYSACC1  ST    R1,0(0,R10)         SAVE VALUE
         ST    R10,0(0,R6)         SAVE NEW SLOT ADDR
         SR    R10,R10
         SPACE 1
SYSACC2  LA    R6,4(0,R6)          NEXT TABLE SLOT
         CLC   0(4,R6),=F'0'       END OF SLOTS
         BE    SYSACC3             YES
         CR    R6,R11              END OF SLOTS
         BE    SYSACC3             YES
         LA    R10,1(0,R10)        NO. OF SLOTS FOR AVERAGE
         A     R15,0(0,R6)         ADD TO TOTAL
         B     SYSACC2
         SPACE 1
SYSACC3  C     R10,=F'0'           0C9 POSSIBLE
         BE    SYSACC4             YES
         DR    R14,R10             AVG VALUE
         LR    R11,R15
         BR    R7
         SPACE 1
SYSACC4  SR    R11,R11
         BR    R7
         EJECT
SYSWTO   MVC   WTOLIST+4(80),0(R6)
         CLI   PARM,C'A'           AVG GRAPHIC DISPLAY
         BNE   *+10                NO
         LR    R14,R1
         LR    R1,R11
         LR    R11,R14
         CVD   R11,DOUBLE
         MVC   AAA(4),=X'40202120'
         ED    AAA(4),DOUBLE+6
         CVD   R1,DOUBLE
         MVC   BBB(4),=X'40202120'
         ED    BBB(4),DOUBLE+6
         SR    R14,R14
         SR    R15,R15
         C     R8,=F'0'            0C9 POSSIBLE
         BE    SYSWTO1             YES
         LR    R15,R1
         DR    R14,R8              NUMBER OF GRAPH POSITIONS
         SPACE 1
SYSWTO1  CH    R15,=H'50'          MAX POSITIONS
         BNH   *+8                 NO
         LH    R15,=H'50'          FORCE MAX
         EX    R15,GRAPHMVE
         LA    R1,GRAPH(R15)
         MVI   0(R1),C'>'
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         BR    R7
         SPACE 1
GRAPHMVE MVC   GRAPH(0),0(R10)
         EJECT
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         SPACE 3
RETURN   ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R14,SAVRET          RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(CSCGS03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         B     MSGRTN
         EJECT
         LTORG
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
WTOHDR   DC   CL29' '
         DC   5CL10'×----×----',C'× '
GRAPH1   DC   CL51'0=========1=========2=========3=========4=========='
GRAPH2   DC   CL51'0====1====2====3====4====5====6====7====8====9====='
         SPACE 3
LGCPUOVL DC    CL80'LONG TERM CPU     '
AVCPUOVL DC    CL80'AVERAGE CPU       '
AVPAGOVL DC    CL80'AVERAGE PAGING    '
AVUICOVL DC    CL80'AVERAGE UIC       '
AVASQOVL DC    CL80'ASMQ LENGTH       '
         SPACE 1
DOMAINB1 DC    CL80'DMN XX CURR MPL   '
DOMAINB2 DC    CL80'DMN XX RDY USER   '
         SPACE 3
         DS    0F
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
PARM     DS    F
DOMAINO1 DS    CL80
DOMAINO2 DS    CL80
ESTAELST DS    0F,(ESTAEOVX)X
         SPACE 3
         DS    0F
WTOLIST  DS    F
         DS    CL17
AAA      DS    CL4
         DS    CL1
BBB      DS    CL4
         DS    CL3
GRAPH    DS    CL51
         SPACE 3
WORKEND  DS    0F
         SPACE 5
AVERAGE  DSECT
LGCPU    DS    21F
LGCPUEND DS    0F
AVCPU    DS    21F
AVCPUEND DS    0F
AVPAG    DS    21F
AVPAGEND DS    0F
AVUIC    DS    21F
AVUICEND DS    0F
AVASQ    DS    21F
AVASQEND DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
XAA      DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
