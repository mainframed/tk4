         TITLE 'NSEZS03D -   ##S  SYSTEM INDICATOR MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       NSEZS03D -       ##S  SYSTEM INDICATOR MONITOR            ***
***                                                                 ***
***       VERSION -        1.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS  3.8     NON-SE                      ***
***                                                                 ***
***       UPDATED -        09/01/79                                 ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEEZS03D CSECT
         LR    R12,R15
         USING IEEZS03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         L     R3,XAL
         LA    R3,0(0,R3)
         XC    PARM(3),PARM
         LTR   R3,R3
         BZ    *+10                NO PARM
         MVC   PARM(3),0(R3)       SAVE UNIT NAME SCAN
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         LA    R5,20
         LA    R6,DMNTAB
         USING DMNTAB,R6
         ST    R6,DMNTABAD
         L     R1,=X'7FFFFFFF'     FORCE INITIAL MIN FILL
         SPACE 1
DMNCLR   XC    0(116,R6),0(R6)     CLEAR AN ENTRY
         ST    R1,DMINTGT
         ST    R1,DMINMPL
         ST    R1,DMINRDY
         ST    R1,DMINGOO
         ST    R1,DMINOUT
         ST    R1,DMININC
         LA    R6,116(0,R6)        NEXT ENTRY
         BCT   R5,DMNCLR           CLEAR WHOLE DMNTAB
         XC    SYSTAB(100),SYSTAB
         ST    R1,MINMLGCP
         ST    R1,MINMAVCP
         ST    R1,MINMAVPG
         ST    R1,MINMUICA
         ST    R1,MINMASMQ
         L     R1,XAT              WAITTIME ADDR
         L     R15,8(0,R1)         LOAD HEX TIME
         LTR   R15,R15             TIME NOT SPECIFIED
         BNZ   BYPASS              NO
         MVC   0(8,R1),=CL8'00003000'  30 SECONDS DEFAULT
         MVC   8(4,R1),=F'30'          30 SECONDS DEFAULT
         SPACE 1
BYPASS   L     R3,8(0,R1)          LOAD MONITOR TIME
         LR    R5,R3
         EJECT
SYSCHK   STIMER WAIT,DINTVL=INTERVL
         SPACE 1
         L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         SR    R1,R1
         IC    R1,308(0,R4)        LONG TERM CPU AVG
         ST    R1,CURRLGCP
         C     R1,MINMLGCP
         BNL   *+8
         ST    R1,MINMLGCP
         C     R1,MAXMLGCP
         BNH   *+8
         ST    R1,MAXMLGCP
         A     R1,ACUMLGCP
         ST    R1,ACUMLGCP
         LH    R1,302(0,R4)        AVERAGE CPU
         ST    R1,CURRAVCP
         C     R1,MINMAVCP
         BNL   *+8
         ST    R1,MINMAVCP
         C     R1,MAXMAVCP
         BNH   *+8
         ST    R1,MAXMAVCP
         A     R1,ACUMAVCP
         ST    R1,ACUMAVCP
         LH    R1,550(0,R4)        AVERAGE PAGING
         ST    R1,CURRAVPG
         C     R1,MINMAVPG
         BNL   *+8
         ST    R1,MINMAVPG
         C     R1,MAXMAVPG
         BNH   *+8
         ST    R1,MAXMAVPG
         A     R1,ACUMAVPG
         ST    R1,ACUMAVPG
         LH    R1,542(0,R4)        AVERAGE UIC
         ST    R1,CURRUICA
         C     R1,MINMUICA
         BNL   *+8
         ST    R1,MINMUICA
         C     R1,MAXMUICA
         BNH   *+8
         ST    R1,MAXMUICA
         A     R1,ACUMUICA
         ST    R1,ACUMUICA
         LH    R1,548(0,R4)        AVERAGE ASMQ
         ST    R1,CURRASMQ
         C     R1,MINMASMQ
         BNL   *+8
         ST    R1,MINMASMQ
         C     R1,MAXMASMQ
         BNH   *+8
         ST    R1,MAXMASMQ
         A     R1,ACUMASMQ
         ST    R1,ACUMASMQ
         CLI   PARM,C'D'           DOMAIN MONITOR
         BE    DOMAIN              YES
         EJECT
SYSCHK0  BCT   R3,SYSCHK
         SR    R14,R14
         L     R15,ACUMAVCP
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,AVRGAVCP
         SR    R14,R14
         L     R15,ACUMLGCP
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,AVRGLGCP
         SR    R14,R14
         L     R15,ACUMAVPG
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,AVRGAVPG
         SR    R14,R14
         L     R15,ACUMUICA
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,AVRGUICA
         SR    R14,R14
         L     R15,ACUMASMQ
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,AVRGASMQ
         B     SYSCHK1
         EJECT
SYSCHK1  LA    R10,SYSTAB
         LA    R9,SYSOTAB
         MVC   WTOLIST(4),=X'00360100'
         LA    R3,5                NO. OF OUTPUT LINES
         SPACE 1
SYSCHK2  MVC   WTOLIST+4(50),0(R9)
         L     R1,8(0,R10)         MIN
         CVD   R1,DOUBLE
         MVC   MIN(4),=X'40202120'
         ED    MIN(4),DOUBLE+6
         L     R1,12(0,R10)        MAX
         CVD   R1,DOUBLE
         MVC   MAX(4),=X'40202120'
         ED    MAX(4),DOUBLE+6
         L     R1,16(0,R10)        AVG
         CVD   R1,DOUBLE
         MVC   AVG(4),=X'40202120'
         ED    AVG(4),DOUBLE+6
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         LA    R9,50(0,R9)
         LA    R10,20(0,R10)
         BCT   R3,SYSCHK2
         CLI   PARM,C'D'           DOMAIN MONITOR
         BE    DOMAIN4             YES
         B     RETURN
         EJECT
DOMAIN   L     R4,16               CVT
         L     R4,604(0,R4)        RMCT
         LH    R9,188(0,R4)        NO. OF DOMAINS
         BCTR  R9,0
         L     R4,180(0,R4)        DMDT
         LA    R7,20
         L     R6,DMNTABAD
         SPACE 1
DOMAIN1  LA    R4,20(0,R4)         NEXT DOMAIN
         CLC   6(4,R4),=F'0'       EMPTY DOMAIN
         BNE   DOMAIN3             NO
         CLC   12(4,R4),=F'0'      EMPTY DOMAIN
         BNE   DOMAIN3             NO
         BCT   R9,DOMAIN1          NEXT DOMAIN
         B     SYSCHK0
         SPACE 1
DOMAIN2  LA    R6,116(0,R6)
         BCT   R7,DOMAIN3
         B     SYSCHK0
         SPACE 1
DOMAIN3  SR    R1,R1
         IC    R1,0(0,R4)          DOMAIN NO.
         C     R1,DMNNBR           DOMAIN MATCH
         BE    DOMAIN3A            YES
         CLI   DMNNBR+3,X'00'      EMPTY SLOT
         BNE   DOMAIN2             NO
         ST    R1,DMNNBR
         IC    R1,1(0,R4)          MIN TARGET
         ST    R1,DMNMIN
         IC    R1,2(0,R4)          MAX TARGET
         ST    R1,DMNMAX
         IC    R1,3(0,R4)          WT
         ST    R1,DMNWT
         EJECT
DOMAIN3A LH    R1,4(0,R4)          TARGET
         C     R1,DMINTGT
         BNL   *+8
         ST    R1,DMINTGT
         C     R1,DMAXTGT
         BNH   *+8
         ST    R1,DMAXTGT
         A     R1,DACCTGT
         ST    R1,DACCTGT
         LH    R1,6(0,R4)          READY USERS
         LA    R1,8(0,R1)          ROUND UP
         SRL   R1,4                DIVIDE BY 16
         C     R1,DMINRDY
         BNL   *+8
         ST    R1,DMINRDY
         C     R1,DMAXRDY
         BNH   *+8
         ST    R1,DMAXRDY
         A     R1,DACCRDY
         ST    R1,DACCRDY
         LH    R1,8(0,R4)          GOING OUT USERS
         C     R1,DMINGOO
         BNL   *+8
         ST    R1,DMINGOO
         C     R1,DMAXGOO
         BNH   *+8
         ST    R1,DMAXGOO
         A     R1,DACCGOO
         ST    R1,DACCGOO
         LH    R1,10(0,R4)         CURRENT MPL
         C     R1,DMINMPL
         BNL   *+8
         ST    R1,DMINMPL
         C     R1,DMAXMPL
         BNH   *+8
         ST    R1,DMAXMPL
         A     R1,DACCMPL
         ST    R1,DACCMPL
         LH    R1,12(0,R4)         OUT USERS
         C     R1,DMINOUT
         BNL   *+8
         ST    R1,DMINOUT
         C     R1,DMAXOUT
         BNH   *+8
         ST    R1,DMAXOUT
         A     R1,DACCOUT
         ST    R1,DACCOUT
         LH    R1,14(0,R4)         IN USERS
         C     R1,DMININC
         BNL   *+8
         ST    R1,DMININC
         C     R1,DMAXINC
         BNH   *+8
         ST    R1,DMAXINC
         A     R1,DACCINC
         ST    R1,DACCINC
         BCTR  R9,0
         LA    R6,116(0,R6)        NEXT DOMAIN AREA
         BCT   R7,DOMAIN1
         B     SYSCHK0
         EJECT
DOMAIN4  L     R6,DMNTABAD         DOMAIN TABLE
         SPACE 1
DOMAIN4A CLI   DMNNBR+3,X'00'      END OF TABLE
         BE    DOMAIN5             YES
         SR    R14,R14
         L     R15,DACCTGT
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGTGT
         SR    R14,R14
         L     R15,DACCMPL
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGMPL
         SR    R14,R14
         L     R15,DACCRDY
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGRDY
         SR    R14,R14
         L     R15,DACCGOO
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGGOO
         SR    R14,R14
         L     R15,DACCOUT
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGOUT
         SR    R14,R14
         L     R15,DACCINC
         DR    R14,R5              AVERAGE CALCULATION
         ST    R15,DAVGINC
         LA    R6,116(0,R6)
         B     DOMAIN4A
         SPACE 1
DOMAIN5  L     R6,DMNTABAD         DOMAIN TABLE
         LA    R5,20
         LA    R1,DMNHDR1
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         LA    R1,DMNHDR2
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         MVC   DMNLIST(4),=X'00360100'
         MVI   DMNLIST+4,C' '
         MVC   DMNLIST+5(49),DMNLIST+4
         SPACE 1
DOMAIN6  MVC   LTYP(3),=C'AVG'
         L     R1,DMNNBR
         CVD   R1,DOUBLE
         UNPK  LDMN(2),DOUBLE+6(2)
         OI    LDMN+1,X'F0'
         SPACE 1
DOMAIN7  L     R1,DMNWT
         LA    R8,DAVGTGT          AVG VALUES
         CLI   LTYP+2,C'G'         AVG LINE
         BE    DOMAIN8             YES
         L     R1,DMNMIN
         LA    R8,DMINTGT          MIN VALUES
         MVC   LDMN(2),=C'  '
         CLI   LTYP+2,C'N'         MIN LINE
         BE    DOMAIN8             YES
         L     R1,DMNMAX
         LA    R8,DMAXTGT          MAX VALUES
         EJECT
DOMAIN8  MVC   LLIM-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LLIM-1(4),DOUBLE+6
         L     R1,0(0,R8)
         MVC   LTGT-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LTGT-1(4),DOUBLE+6
         L     R1,4(0,R8)
         MVC   LMPL-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LMPL-1(4),DOUBLE+6
         L     R1,8(0,R8)
         MVC   LRDY-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LRDY-1(4),DOUBLE+6
         L     R1,12(0,R8)
         MVC   LGOO-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LGOO-1(4),DOUBLE+6
         L     R1,16(0,R8)
         MVC   LOUT-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LOUT-1(4),DOUBLE+6
         L     R1,20(0,R8)
         MVC   LINC-1(4),=X'40202120'
         CVD   R1,DOUBLE
         ED    LINC-1(4),DOUBLE+6
         CLI   PARM+1,C'A'         AVG LINE DISPLAY ONLY
         BNE   DOMAIN8A            NO
         CLI   LTYP,C'A'           IS THIS AVG LINE
         BNE   DOMAIN8B            NO, BYPASS PRINT
         SPACE 1
DOMAIN8A LA    R1,DMNLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         SPACE 1
DOMAIN8B CLI   LTYP+2,C'G'         AVG LINE
         BNE   *+14                NO
         MVC   LTYP(3),=C'MIN'
         B     DOMAIN7
         CLI   LTYP+2,C'N'         MIN LINE
         BNE   DOMAIN9             NO
         MVC   LTYP(3),=C'MAX'
         B     DOMAIN7
         SPACE 1
DOMAIN9  LA    R6,116(0,R6)
         CLI   DMNNBR+3,X'00'      END OF OUTPUT
         BE    RETURN              YES
         BCT   R5,DOMAIN6
         B     RETURN
         EJECT
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         BR    R11
         SPACE 3
RETURN   ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R14,SAVRET          RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEEZS03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         LA    R11,RETURN          LOAD RET ADDR
         B     MSGRTN
         EJECT
         LTORG
         DS    0D
INTERVL  DC    CL8'00000100'
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
DMNHDR1  DC    X'00300100'
         DC    CL44' '
         SPACE 3
DMNHDR2  DC    X'00360100'
         DC    CL50'DMN  TYP  LIM  TGT  MPL  RDY  GOO  OUT  INC      '
         SPACE 3
SYSOTAB  DS    0F
AVCPOVLY DC    CL50'AVERAGE CPU       MIN- NNN    MAX- NNN    AVG- NNN'
LGCPOVLY DC    CL50'LONG TERM CPU     MIN- NNN    MAX- NNN    AVG- NNN'
AVPGOVLY DC    CL50'AVERAGE PAGING    MIN- NNN    MAX- NNN    AVG- NNN'
UICAOVLY DC    CL50'AVERAGE UIC       MIN- NNN    MAX- NNN    AVG- NNN'
ASMQOVLY DC    CL50'ASMQ LENGTH       MIN- NNN    MAX- NNN    AVG- NNN'
         SPACE 3
         DS    0F
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
DMNTABAD DS    F
PARM     DS    F
ESTAELST DS    0F,(ESTAEOVX)X
         EJECT
         DS    0F
SYSTAB   DS    0CL100
CURRAVCP DS    F
ACUMAVCP DS    F
MINMAVCP DS    F
MAXMAVCP DS    F
AVRGAVCP DS    F
CURRLGCP DS    F
ACUMLGCP DS    F
MINMLGCP DS    F
MAXMLGCP DS    F
AVRGLGCP DS    F
CURRAVPG DS    F
ACUMAVPG DS    F
MINMAVPG DS    F
MAXMAVPG DS    F
AVRGAVPG DS    F
CURRUICA DS    F
ACUMUICA DS    F
MINMUICA DS    F
MAXMUICA DS    F
AVRGUICA DS    F
CURRASMQ DS    F
ACUMASMQ DS    F
MINMASMQ DS    F
MAXMASMQ DS    F
AVRGASMQ DS    F
         EJECT
         DS    0F
WTOLIST  DS    F
         DS    CL22
MIN      DS    CL4
         DS    CL8
MAX      DS    CL4
         DS    CL8
AVG      DS    CL4
         SPACE 3
         DS    0F
DMNLIST  DS    F
LDMN     DS    CL3
         DS    CL2
LTYP     DS    CL3
         DS    CL2
LLIM     DS    CL3
         DS    CL2
LTGT     DS    CL3
         DS    CL2
LMPL     DS    CL3
         DS    CL2
LRDY     DS    CL3
         DS    CL2
LGOO     DS    CL3
         DS    CL2
LOUT     DS    CL3
         DS    CL2
LINC     DS    CL3
         DS    CL2
         DS    CL5
         EJECT
DMNTAB   DS    0CL116
DMNNBR   DS    F
DMNMIN   DS    F
DMNMAX   DS    F
DMNWT    DS    F
DMINTGT  DS    F
DMINMPL  DS    F
DMINRDY  DS    F
DMINGOO  DS    F
DMINOUT  DS    F
DMININC  DS    F
DMAXTGT  DS    F
DMAXMPL  DS    F
DMAXRDY  DS    F
DMAXGOO  DS    F
DMAXOUT  DS    F
DMAXINC  DS    F
DAVGTGT  DS    F
DAVGMPL  DS    F
DAVGRDY  DS    F
DAVGGOO  DS    F
DAVGOUT  DS    F
DAVGINC  DS    F
DACCTGT  DS    F
DACCMPL  DS    F
DACCRDY  DS    F
DACCGOO  DS    F
DACCOUT  DS    F
DACCINC  DS    F
         SPACE 3
         DS    19CL116
         SPACE 3
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
