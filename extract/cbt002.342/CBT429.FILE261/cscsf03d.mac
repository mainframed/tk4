         TITLE 'XA1SF03D -   #F  DISPLAY STORAGE USAGE'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       SE2SF03D -       #F  DISPLAY STORAGE USAGE                ***
***                                                                 ***
***       VERSION -        2.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/XA  2.1    TO                        ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANDLE ESTAE           ***
***                        01/26/83  AXC-CBT CHANGE ESTAE PARM LEN  ***
***                        05/25/86  GLA-BFG COMPLETE REWRITE FOR   ***
***                                  XA STRUCTURE                   ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESF03D CSECT
         LR    R12,R15
         USING IEESF03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         XC    OUTMSLIM(4),OUTMSLIM
         MVI   VOSWIT,X'00'        CLEAR OFFLINE SWITCH
         MVI   VBSWIT,X'00'        CLEAR BAD FRAME SWITCH
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(65),OUTMSG+4
         XC    OUTMSG+70(4),OUTMSG+70 CLEAR FLAGS
         LR    R10,R13             LOAD SAVE AREA ADDR
         L     R15,=A(X'80000000'+ESTAEBYP)
         BSM   0,R15          CHANGE MODES   TO 31 BIT
ESTAEOVL ESTAE STAEXIT,CT,MF=L               * ESTAE PARM FIX GLA-BFG*
ESTAEOVX EQU   *-ESTAEOVL                    * ESTAE PARM FIX GLA-BFG*
ESTAEBYP MVC   ESTAELST(ESTAEOVX),ESTAEOVL   * ESTAE PARM FIX GLA-BFG*
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         L     R3,XAL              LOAD LIST POINTER
         LA    R3,0(0,R3)          CLEAR HIGH ORDER
         LTR   R3,R3               PARM EXIST
         BZ    PFTLIST             NO, GO PROCESS PFT LIST
         CLI   0(R3),C'C'          CSA DISPLAY REQUEST
         BE    VCDISP              YES, GO PROCESS
         CLI   0(R3),C'B'          BAD FRAME REQUEST
         BE    VBDISP              YES, GO PROCESS
         CLI   0(R3),C'R'          V=R DISPLAY REQUEST
         BE    VRDISP              YES, GO PROCESS
         CLI   0(R3),C'M'          MEGABYTE DISPLAY REQUEST
         BE    VVDISP              YES, GO PROCESS
         CLI   0(R3),C'O'          OFFLINE INTERCEPT REQUEST
         BE    VODISP              YES, GO PROCESS
         LA    R11,RETURN          LOAD RET ADDR
         LA    R1,ERRMSG           PARM ERROR
         B     PRINT               GO ISSUE WTO
         EJECT
PFTLIST  MVC   OUTMSG(4),=X'00460100'
         L     R14,16              LOAD CVT ADDR
         L     R6,556(0,R14)       LOAD ASVT ADDR
         L     R7,516(0,R6)        MAX NO. OF ASCBS
         LA    R6,524(0,R6)        1ST ASCB SLOT
         L     R3,356(0,R14)       LOAD PVT ADDR
         L     R14,8(R3)      GET ADDR OR AREA IN RIT
         L     R4,0(0,R14)    GET ADDR OF FIRST REAL PFTE
         L     R5,4(0,R14)    GET ADDR OF LAST REAL PFTE
         SR    R5,R4          GET DIFFERENCE
         SRL   R5,5           GET # ENTRIES
         LA    R5,1(0,R5)     ADD IN LAST ONE
         STM   R4,R5,PFTREGS  SAVE REGS OF PFT
*
**       GET TOTAL FRAMES
*
         LR    R14,R5         PUT IN PROPER AREA
         MVC   OUTMSG+4(12),=CL12'TOTAL'
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       GET NUCLEUS FRAMES
**          = GDAESQA - (GDASQA + GDASQASZ)
*
         L     R14,16         LOAD CVT ADDR
         L     R4,560(0,R14)  GET GDA ADDRESS
         L     R14,152(0,R4)  GET GDAESQA
         L     R15,144(0,R4)  GET GDASQA
         A     R15,148(0,R4)  GDASQA + GDASQASZ
         SR    R14,R15        SIZE OF NUCLEUS
         SRL   R14,12         GET NUMBER OF FRAMES
         MVC   OUTMSG+4(12),=CL12'NUCLEUS'
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       CYCLE PFT FOR INTERESTING SYSTEM COUNTS
*
         L     R10,=X'0000FFFF'
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         LR    R4,R2          SAVE VALUES
         LR    R5,R14         AND AGAIN
*
**       TOTAL FIXED  (R3 VALUE)
*
         MVC   OUTMSG+4(12),=CL12'TOT FIXED'
         LR    R14,R3         PUT IN PROPER REG
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       TOTAL SQA FIXED (R8 VALUE)
*
         MVC   OUTMSG+4(12),=CL12'SQA FIXED'
         LR    R14,R8         PUT IN PROPER REG
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       TOTAL SYSTEM (R14 VALUE NOW IN R5)
*
         MVC   OUTMSG+4(12),=CL12'SYS PAGES'
         LR    R14,R5         PUT IN PROPER REG
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       TOTAL SQA    (R2 VALUE NOW IN R4)
*
         MVC   OUTMSG+4(12),=CL12'TOTAL SQA'
         LR    R14,R4         PUT IN PROPER REG
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
*
**       TOTAL AVAIL  (R9 VALUE)
*
         MVC   OUTMSG+4(12),=CL12'AVAILABLE'
         LR    R14,R9         PUT IN PROPER REG
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
         EJECT
*
**       NOW START EACH ASCB
*
         L     R2,XSASAVE
         MVC   OUTMSG+4(10),=CL12' '
         MVC   OUTMSG+14(56),=CL56'    TOTAL        FIXED         LSQA X
                   FXD LSQA    '
         BAL   R11,PRINT      PRINT HEADER
GETASCB  LA    R6,4(0,R6)          NEXT SLOT
         BCT   R7,ASCBFND          GO PROCESS
         SPACE 3
RETURN   EQU   *
         ESTAE 0
         LA    R15,*+6        GET ADDRESS
         BSM   0,R15          CHANGE MODES
         L     R0,SIZE
         LR    R1,R13              WORK AREA ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
ASCBFND  TM    0(R6),X'80'         AVAILABLE SLOT
         BO    GETASCB             YES, GET NEXT SLOT
         L     R8,0(0,R6)          LOAD ASCB ADDR
         SR    R10,R10             LOAD
         ICM   R10,3,36(R8)          ASID
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         L     R2,XSASAVE
         B     GETASCB
         EJECT
PAGEPRNT LM    R4,R5,PFTREGS       LOAD PFT REGS
         SR    R14,R14             CLEAR FRAME COUNT
         SR    R2,R2               CLEAR LSQA/SQA COUNT
         SR    R9,R9               CLEAR AVAILABLE FRAME COUNT
         SR    R3,R3               CLEAR FIXED FRAME COUNT
         SR    R8,R8               CLEAR FIXED & SQA FRAME COUNT
         SPACE 3
PAGEPR1  SR    R15,R15             LOAD
         ICM   R15,3,18(R4)           PFT ASID
         TM    10(R4),X'80'        AVAILABLE PAGE
         BO    AVAILCNT            GO ADD TO AVAILABLE COUNT
         CR    R10,R15             ASID MATCH
         BNE   PAGEPR2A            NO, DONT INCR FRAME COUNT
         LA    R14,1(0,R14)        ADD 1 TO FRAME COUNT
         CLI   8(R4),X'82'    IS IT ALSO FIXED
         BNE   PAGEPR1A       SKIP IF NOT FIXED
         LA    R3,1(0,R3)     ADD 1 TO FIXED FRAME COUNT
PAGEPR1A TM    10(R4),X'10'        LSQA FRAME
         BZ    PAGEPR2             NO
         LA    R2,1(0,R2)          ADD 1 T0 LSQA FRAME COUNT
         CLI   8(R4),X'82'    IS IT ALSO FIXED
         BNE   PAGEPR2        IF NOT DONE
         LA    R8,1(0,R8)     ADD 1 TO FIXED & LSQA COUNT
         B     PAGEPR2        GO TO NEXT ENTRY
         SPACE 3
PAGEPR2A C     R10,=X'0000FFFF' IS THIS THE SYSTEM
         BNE   PAGEPR2        IF NOT THEN SKIP OTHER FIXED
         CLI   8(R4),X'82'    IS THIS FIXED
         BNE   PAGEPR2        SKIP IF NOT FIXED
         LA    R3,1(0,R3)     ADD 1 TO TOTAL FIXED FRAME COUNT
         SPACE 3
PAGEPR2  LA    R4,32(0,R4)         NEXT PFT
         BCT   R5,PAGEPR1          PROCESS NEXT
         MVC   OUTMSG+4(12),=CL12'ASID'
         C     R10,=X'00000001'    MASTER
         BE    ASIDMSTR            YES, GO PRINT MASTER
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   PAGEPR3             NO, ASID
         BR    R11            OUT IF SYSTEM
         SPACE 3
*
**       START FORMATING FOR ASID  -  NOTE:
**             R14 - TOTAL PAGES OWNED
**             R3  - TOTAL FIXED FRAMES OWNED
**             R2  - TOTAL LSQA FRAMES OWNED
**             R8  - TOTAL LSQA FIXED FRAMES
*
PAGEPR3  LTR   R14,R14             ANY FRAMES
         BZR   R11                 NO, BYPASS PRINT
         STH   R10,DOUBLE+6        SAVE ASID
         UNPK  DOUBLE(5),DOUBLE+6(3)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+9(4),DOUBLE  ASID TO MSG
         SPACE 3
PAGEPR4  CVD   R14,DOUBLE
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         CLC   OUTMSG+4(4),=C'ASID'
         BE    JOBFIND
         EJECT
PGPRINT  DS    0H
         LA    R1,OUTMSG+13   POINT AT PLACE TO PUT IT
         BAL   R15,FORMNUM    FORMAT TOTAL PAGES
         LTR   R2,R2               TOP LINES INDICATOR ON
         BM    PRINT               IF YES BRANCH
         LR    R14,R3         GET NUMBER TO FORMAT
         LA    R1,OUTMSG+26   AND PLACE TO PUT IT
         BAL   R15,FORMNUM    FORMAT TOTAL FIXED FRAMES
         LR    R14,R2         GET NUMBER TO FORMAT
         LA    R1,OUTMSG+39   AND PLACE TO PUT IT
         BAL   R15,FORMNUM    FORMAT TOTAL LSQA FRAMES
         LR    R14,R8         GET NUMBER TO FORMAT
         LA    R1,OUTMSG+52   AND PLACE TO PUT IT
         BAL   R15,FORMNUM    FORMAT TOTAL FIXED LSQA FRAMES
         SPACE 3
PRINT    LA    R1,OUTMSG
         SR    R0,R0
         L     R2,XSASAVE
         IC    R0,XAU
         SVC   35
         BR    R11
         SPACE 3
FORMNUM  MVC   0(13,R1),=XL13'402020202120402020202120D2'
         CVD   R14,DOUBLE     MAKE FRAME COUNT DECIMAL
         ED    0(6,R1),DOUBLE+5   AND PUT IN MESSAGE
         SLL   R14,2          GET K FROM FRAMES
         CVD   R14,DOUBLE2    MAKE K COUNT DECIMAL
         ED    6(6,R1),DOUBLE2+5  AND PUT IN MESSAGE
         MVI   6(R1),C'/'     SET PROPER CHAR
         BR    R15
         SPACE 3
AVAILCNT LA    R9,1(0,R9)          ADD 1 TO COUNT
         B     PAGEPR2
         SPACE 3
ASIDMSTR MVC   OUTMSG+4(12),=CL12'MASTER'
         B     PAGEPR4
         SPACE 2
JOBFIND  EQU   *
         L     R15,0(0,R6)         LOAD ASCB ADDR
         L     R15,56(0,R15)       CSCB ADDR
         CLI   28(R15),X'03'       INIT
         BE    JOBFIND1            YES, GO SEARCH
         SPACE
JOBFIND0 MVC   OUTMSG+4(8),8(R15)  JOBNAME
         MVI   OUTMSG+12,C' '
         B     PGPRINT
         SPACE
JOBFIND1 LA    R1,0(0,R15)         SAVE ADDR
         SR    R14,R14             LOAD
         ICM   R14,3,30(R15)         ASID
         SPACE
JOBFIND2 L     R15,0(0,R15)        NEXT CSCB
         LA    R15,0(0,R15)
         LTR   R15,R15             END OF CHAIN
         BNZ   JOBFIND3            NO, CONTINUE
         LR    R15,R1              USE ORIG CSCB
         B     JOBFIND0
         SPACE
JOBFIND3 CR    R15,R1              END OF CHAIN
         BE    JOBFIND0            YES, USE ORIG CSCB
         CH    R14,30(0,R15)       ASID EQUAL
         BE    JOBFIND0            YES, USE THIS CSCB
         B     JOBFIND2
         EJECT
VONONE   LA    R1,VOMSG            NO OFFLINE INTERCEPT MSG
         LA    R11,RETURN
         B     PRINT
         SPACE 3
VBNONE   LA    R1,VBMSG            NO BAD FRAMES MSG
         LA    R11,RETURN
         B     PRINT
         SPACE 3
VRDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVC   OUTMSG(20),VRHDR1   SET 1ST HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVC   OUTMSG(20),VRHDR2   SET 2ND HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R14,560(0,R14) GDA ADDRESS
         L     R4,8(0,R3)     GET ADDR OF AREA IN RIT
         L     R4,0(R4)       GET PFT ORIGIN
         L     R15,192(0,R14) V=R START ADDRESS FROM GDA
         SRL   R15,12-5       GET PAGE NUMBER * 32
         AR    R4,R15         GET PFTE ADDRESS
         L     R5,196(0,R14)  V=R SIZE FROM GDA
         SRL   R5,12          GET NUMBER OF PAGES
         MVI   VRSWIT,X'00'        CLEAR V=R END SWITCH
         MVC   OUTMSG(20),VRMSG    SET UP V=R MSG BASE
         B     VRDISP3             GO PROCESS
         SPACE 3
VRDISP1  CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BE    VRDISP1A            NO
         TM    11(R4),X'08'        OFFLINE INTERCEPT FLAG BIT ON
         BNO   VRDISP2O            NO, NEXT PFTE
         SPACE 3
VRDISP1A CLI   VBSWIT,X'00'        BAD FRAME REQUEST
         BE    VRDISP1O            NO
         TM    9(R4),X'01'         BAD FRAME FLAG BIT ON
         BNO   VRDISP2O            NO, NEXT PFTE
         SPACE 3
VRDISP1O CLC   18(2,R4),VRASID     ASID MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         CLC   8(4,R4),VRFLAGS     FLAGS MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         SPACE 3
VRDISP2  L     R14,VRNUM           ADD 1
         LA    R14,1(0,R14)           TO EQUAL
         ST    R14,VRNUM                 ENTRY COUNT
         SPACE 3
VRDISP2O LA    R4,32(0,R4)         NEXT PFTE
         BCT   R5,VRDISP1
         MVI   VRSWIT,X'FF'        SET END OF V=R SWITCH
         B     VRDISP4             GO FORMAT AND PRINT LAST
         SPACE 3
VRDISP3  XC    VRNUM(4),VRNUM      CLEAR COUNT
         MVC   VRASID(2),18(R4)    SET ASID
         MVC   VRFLAGS(4),8(R4)    SET FLAGS
         B     VRDISP2
         EJECT
VRDISP4  L     R14,VRNUM           NO. OF FRAMES
         CVD   R14,DOUBLE          DECIMAL
         UNPK  OUTMSG+4(3),DOUBLE+6(2)
         OI    OUTMSG+6,X'F0'
         UNPK  DOUBLE(5),VRASID(3) TRANSLATE ASID
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+10(4),DOUBLE PUT ASID IN MSG
         SPACE 3
VRDISP5A MVI   OUTMSG+17,C'O'      OFFLINE
         TM    VRFLAGS+2,X'20'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'C'      OFFLINE INTERCEPT
         TM    VRFLAGS+3,X'08'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'R'      V=R ALLOCATED
         TM    VRFLAGS+2,X'04'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'I'      V=R INTERCEPT
         TM    VRFLAGS+3,X'10'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'A'      AVAILABLE
         TM    VRFLAGS+2,X'80'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'-'      NO STATUS
         SPACE 3
VRDISP5B MVI   OUTMSG+18,C'B'      BAD PAGE
         TM    VRFLAGS+1,X'01'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'F'      LONG TERM FIXED
         TM    VRFLAGS+3,X'40'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'S'      LSQA/SQA PAGE
         TM    VRFLAGS+2,X'10'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'V'      VIO PAGE
         TM    VRFLAGS+3,X'20'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'-'      NO STATUS
         EJECT
VRDISP5C MVI   OUTMSG+19,C'A'      AVAILABLE
         TM    VRFLAGS,X'FF'
         BZ    VRDISP5D
         MVI   OUTMSG+19,C'N'      NOT QUEUED
         TM    VRFLAGS,X'F0'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'S'      SQA
         CLI   VRFLAGS,X'21'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'R'      SQA RESERVED
         CLI   VRFLAGS,X'22'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'-'      NO STATUS
         SPACE 3
VRDISP5D LA    R1,OUTMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         TM    VRSWIT,X'FF'        TEST HDR/END SWITCH
         BO    RETURN              END
         BCR   4,R11               HDR
         B     VRDISP3
         EJECT
VBDISP   MVI   VBSWIT,X'FF'        SET BAD FRAME ONLY SW
         B     VVDISP
         SPACE 3
VODISP   MVI   VOSWIT,X'FF'        SET OFFLINE INTERCEPT ONLY SW
         SPACE 3
VVDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVI   DOUBLE,X'F0'        TEST
         MVZ   DOUBLE(1),1(R3)        NUMERIC
         CLI   DOUBLE,X'F0'              MEGABYTE NUMBER
         BNE   VVDISP1             NOT NUMERIC, ERROR
         MVC   OUTMSG(20),VRHDR1   1ST HDR
         BAL   R11,VRDISP5D
         MVC   OUTMSG(20),VRHDR2   2ND HDR
         BAL   R11,VRDISP5D
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         MVC   OUTMSG(20),VRMSG    BASE MSG
         B     VVDISP2
         EJECT
VVDISP1  LA    R11,RETURN          PARM
         LA    R1,ERRMSG              ERROR
         B     PRINT
         SPACE 3
VVDISP2  IC    R5,1(0,R3)          CALCULATE
         SLL   R5,28                  PFTE INDEX
         SRL   R5,15                     TO THIS MEG
         LTR   R5,R5               ZERO MEG
         BZ    VVDISP1             YES, ERROR
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R14,8(0,R3)         GET ADDR OF AREA IN RIT
         L     R4,0(R14)      GET PFT ORIGIN
         L     R14,4(R14)     GET LAST PTFE ADDRESS
         AR    R4,R5               PFT MEG START ADDR
         CR    R4,R14              Q. PAST END OF PFT
         BH    VVDISP1             YES, ERROR
         LA    R5,256              NO. OF FRAMES IN × MEG
         CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BNE   VODISP1             YES, CONTINUE PROCESS
         CLI   VBSWIT,X'00'        BAD FRAME REQUEST
         BNE   VBDISP1             YES, CONTINUE PROCESS
         B     VRDISP3             GO PROCESS
         SPACE 3
VODISP1  TM    11(R4),X'08'        OFFLINE INTERCEPT FLAG BIT ON
         BO    VRDISP3             YES, GO CONTINUE PROCESS
         LA    R4,32(0,R4)         NEXT PFTE
         BCT   R5,VODISP1          GO CHECK NEXT
         B     VONONE              NO INTERCEPT OFFLINE, GO MSG
         SPACE 3
VBDISP1  TM    9(R4),X'01'         BAD FRAME BIT ON
         BO    VRDISP3             YES, GO CONTINUE PROCESS
         LA    R4,32(0,R4)         NEXT PFTE
         BCT   R5,VBDISP1          GO CHECK NEXT
         B     VBNONE              NO INTERCEPT OFFLINE, GO MSG
         EJECT
*
**       DISPLAY THE CURRENT CSA STATUS AND ALLOCATED/FREED PARTS
*
VCDISP   L     R1,16               CVT
         L     R3,356(0,R1)        LOAD PVT ADDR
         L     R6,560(0,R1)        GDA
         L     R4,104(0,R6)        LAST FBQE FOR CSA
         L     R5,100(0,R6)        1ST FBQE FOR CSA
         SR    R7,R7
         CR    R4,R5               LAST IN CHAIN
         BE    VCDISPB             YES
         SPACE 1
VCDISPA  A     R7,8(0,R5)          ADD FREE AREA
         L     R5,0(0,R5)          NEXT FBQE
         CR    R4,R5               END OF CHAIN
         BNE   VCDISPA             NO
VCDISPB  A     R7,8(0,R4)          ADD FREE AREA OF LAST
         SPACE 1
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'CSA SIZE ='
         L     R1,112(0,R6)        CSA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'     CSA ADDR ='
         UNPK  CSADISP+42(9),108(5,R6)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 1
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'ECSA SIZE ='
         L     R1,128(0,R6)        ECSA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'    ECSA ADDR ='
         UNPK  CSADISP+42(9),124(5,R6)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'SQA FREE ='
         L     R1,132(0,R6)        SQA+CSA AVAIL
         SR    R1,R7               SUBTRACT CSA
         BP    *+6                 IF NOT NEGATIVE, BYPASS
         SR    R1,R1
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'    SQA AVAIL ='
         L     R1,148(0,R6)        SQA AVAIL
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+41(8),=X'402020212020D240'
         ED    CSADISP+41(6),DOUBLE+5
         MVC   CSADISP+49(3),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         STM   R4,R5,VCSASAV
         SPACE 1
VCDISPD  MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'V=R SIZE ='
         L     R1,196(0,R6)        V=R SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'     V=R ADDR ='
         UNPK  CSADISP+42(9),192(5,R6)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'PRV SIZE ='
         L     R1,164(0,R6)        PRIVATE AREA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'     PRV ADDR ='
         UNPK  CSADISP+42(9),160(5,R6)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 1
*
**       SAVE INFOR FOR CSA ALLOCATED/FREED BLOCKS
*
         LA    R8,100(0,R6)   POINT AT CSA DESCRIPTOR
*                             (1ST FBQE,LASTFBQE,ADDR,SIZE)
         BAL   R3,VCDISP0     DISPLAY CSA BLOCKS
         LA    R8,116(0,R6)   POINT AT ECSA DESCRIPTOR
*                             (1ST FBQE,LASTFBQE,ADDR,SIZE)
         BAL   R3,VCDISP0     DISPLAY CSA BLOCKS
         B     RETURN
         EJECT
*
**       PRINT ALLOCATED/FREED SEGMENT ADDRESSES
**       ON ENTRY R8 ->  +0 ADDR OF FIRST FBQE
**                       +4 ADDR OF LAST FBQE
**                       +8 LOWEST ADDRESS OF AREA
**                       +12 SIZE OF AREA
*
VCDISP0  L     R5,0(0,R8)         GET FIRST AND LAST FBQE
         LA    R4,0(0,R8)         GET VALUE OF LAST POINTER
         L     R1,8(R8)            GET LOWEST ADDR
         C     R1,12(0,R5)         1ST CHUNK FREE
         BE    VCDISP1             YES
         L     R14,12(0,R5)        1ST FREE AREA ADDR
         B     VCDISP3             GO PROCESS ALLOCATED CHUNK
         SPACE 1
VCDISP1  CR    R4,R5               END OF FBQES
         BER   R3                  YES
         MVC   CSADISP(4),=X'00340100'
         L     R1,8(0,R5)          FREE AREA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+4(30),=CL30'FREE        SIZE='
         MVC   CSADISP+34(9),=CL9'ADDR ='
         MVC   CSADISP+22(8),=X'402020202120D240'
         ED    CSADISP+22(6),DOUBLE+5
         UNPK  CSADISP+42(9),12(5,R5)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         L     R1,OUTMSLIM
         LA    R1,1(0,R1)
         ST    R1,OUTMSLIM
         C     R1,=F'100'           200 +  LINES OF OUTPUT
         BH    RETURN               YES, THATS ALL.  AVOID SQA FILL
         EJECT
VCDISP2  L     R1,12(0,R5)         FREE AREA ADDR
         A     R1,8(0,R5)          ADD FREE AREA LENGTH
         L     R5,0(0,R5)          NEXT FBQE
         CR    R4,R5               END OF FBQES
         BE    VCDISP4             YES
         L     R14,12(0,R5)        NEXT FREE AREA ADDR
         SPACE 3
VCDISP3  SR    R14,R1
         SRL   R14,10              USED AREA LENGTH
         CVD   R14,DOUBLE
         ST    R1,DOUBLE           USED AREA ADDR
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(30),=CL30'ALLOCATED   SIZE='
         MVC   CSADISP+34(9),=CL9'ADDR ='
         MVC   CSADISP+22(8),=X'402020202120D240'
         ED    CSADISP+22(6),DOUBLE+5
         UNPK  CSADISP+42(9),DOUBLE(5)
         NC    CSADISP+42(8),=8X'0F'
         TR    CSADISP+42(8),=C'0123456789ABCDEF'
         MVC   CSADISP+50(2),=CL2' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     VCDISP1
         SPACE  3
VCDISP4  L     R14,8(0,R8)         START ADDRESS
         A     R14,12(0,R8)        ADD LENGTH OF BLOCK
         B     VCDISP3
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
************************************************************** GLA-BFG
*  THIS WILL RECORD ANY ABENDS IN LOGREC WITH THE              GLA-BFG
*  OPERANDS, REGISTERS, AND PSW IN IT.                         GLA-BFG
************************************************************** GLA-BFG
         STM   R14,R12,12(R13)     SAVE REGISTER               GLA-BFG
         LR    R14,R13             SAVE ORIG R13               GLA-BFG
         L     R13,0(R1)           GET ESTAE PARM              GLA-BFG
         L     R2,XSASAVE          GET XSA ADDRESS             GLA-BFG
         LR    R13,R14             RESTORE R13                 GLA-BFG
         ICM   R2,15,XAL-XSA(R2)   GET PARM ADDRESS            GLA-BFG
         BZ    ESTNOP              IF NO PARMS THEN SKIP DATA  GLA-BFG
         OI    SDWADPVA-SDWA(1),SDWAVRAM+SDWAEBC SET TO VRA    GLA-BFG
*                                  AND EBCDIC                  GLA-BFG
         MVC   SDWAVRA-SDWA(L'ESTPRFX,1),ESTPRFX MOVE HEADER   GLA-BFG
         MVC   SDWAVRA+11-SDWA(8,1),0(R2) MOVE IN DATA         GLA-BFG
         MVI   SDWAURAL-SDWA(1),19 SET LENGTH OF DATA          GLA-BFG
         B     ESTNOP              SKIP CONSTANTS              GLA-BFG
         ESTMODS ,                                             GLA-BFG
ESTPRFX  DC    0CL11' ',AL1(VRAHID,7),C'OPERAND',AL1(VRARRK,8) GLA-BFG
ESTNOP   L     R2,=A(STAERTRY)     GET RETRY ADDRESS           GLA-BFG
         LA    R3,ESTMODS          POINT AT NAMES              GLA-BFG
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO,           GLA-BFG X
               RECORD=YES,RECPARM=(3),REGS=(14,12)             GLA-BFG
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESF03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     RETURN
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
ERRMSG   DC    X'001C0100'
         DC    CL24'PARAMETER ERROR'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
         DS    0F
VRHDR1   DC    X'00140100'
         DC    CL16'PAGES.ASID.FLAGS'
         SPACE 3
         DS    0F
VRHDR2   DC    X'00140100'
         DC    16C'-'
         SPACE 3
         DS    0F
VRMSG    DC    X'00140100'
         DC    CL16'   ...    ...   '
         SPACE 3
         DS    0F
VBMSG    DC    X'00220100'
         DC    CL30'NO BAD FRAMES'
         SPACE 3
         DS    0F
VOMSG    DC    X'00220100'
         DC    CL30'NO FRAMES INTERCEPTED OFFLINE'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
DOUBLE2  DS    2F
VCSASAV  DS    2F
XSASAVE  DS    F
PFTREGS  DS    2F
VRNUM    DS    F
VRASID   DS    H
VRFLAGS  DS    CL4
VRSWIT   DS    C
VOSWIT   DS    C
VBSWIT   DS    C
         DS    0F
OUTMSG   DS    74C
         SPACE 1
         DS    0F
CSADISP  DS    CL52
OUTMSLIM DS    F
ESTAELST DS    0F,(ESTAEOVX)X
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAV      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
