00010010PROC 0  DEBUG
00020020CONTROL NOLIST MSG PROMPT NOFLUSH
00030022IF &DEBUG NE &STR() THEN +
00040022  CONTROL LIST CONLIST SYMLIST
00041030ISPEXEC CONTROL DISPLAY LINE SM
00050022SET &INDSN EQ &STR()
00060022SET &INUNIT EQ &STR()
00070022SET &INVOL EQ &STR()
00080022SET &INRECFM EQ &STR()
00090022SET &INLR EQ &STR()
00100000SET &INBLK EQ &STR()
00110010SET &INDENS EQ &STR()
00120020SET &INLABEL EQ &STR()
00130026SET &INEXP EQ &STR()
00131026SET &INPOS EQ &STR()
00140022SET &OUTDSN EQ &STR()
00150022SET &OUTUNIT EQ &STR()
00160022SET &OUTVOL EQ &STR()
00170022SET &OUTRECFM EQ &STR()
00180022SET &OUTLR EQ &STR()
00190022SET &OUTBLK EQ &STR()
00200000SET &OUTDENS EQ &STR()
00210010SET &OUTLABEL EQ &STR()
00220026SET &OUTEX EQ &STR()
00221026SET &OUTPOS EQ &STR()
00230022SET &SPCU EQ &STR()
00240022SET &PRIQ EQ &STR()
00250022SET &SECQ EQ &STR()
00260022SET &DIRB EQ &STR()
00270022SET &DISP EQ &STR()
00271032SET &FORM EQ &STR()
00280022SET &ALCBL EQ &STR()
00290022SET &IPRT EQ &STR(DUMMY)
00300022SET &PROGNAME EQ &STR(IEBGENER)
00310022ISPEXEC CONTROL ERRORS RETURN
00320000TRYAGIN: +
00322028ISPEXEC CONTROL DISPLAY LINE SM
00330010CONTROL NOMSG
00340034FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2,SYSUT3,SYSUT4)
00360022CONTROL MSG
00370022SET &INATTR EQ &STR()
00380022SET &OUTATTR EQ &STR()
00390022SET &INTAPE EQ &STR()
00400022SET &OUTTAPE EQ &STR()
00410022SET &INUV EQ &STR()
00420000SET &OUTUV EQ &STR()
00450022SET &OUTNEW EQ &STR()
00460022SET &ODISP EQ &STR()
00470022SET &INRFM EQ &STR()
00480022SET &OUTRFM EQ &STR()
00490022ISPEXEC DISPLAY PANEL(GENER1) CURSOR(INDSN)
00500022IF &LASTCC EQ 8 THEN +
00510022  GOTO ALLDONE
00511031ISPEXEC CONTROL DISPLAY LINE SM
00520000ALLOC F(SYSIN) DUMMY
00530023IF &STR(&IPRT) EQ &STR() OR &STR(&IPRT) EQ &STR(DUMMY) THEN +
00540022  ALLOC F(SYSPRINT) DUMMY
00550022ELSE +
00560023  IF &STR(&IPRT) EQ &STR(*) THEN +
00570022    ALLOC F(SYSPRINT) DA(*)
00580022  ELSE +
00581025    DO
00582025      IF &STR(&IPRT) EQ &STR(X) THEN +
00583025        SET &HOLD EQ &STR(HOLD KEEP)
00584025      ELSE +
00585025        SET &HOLD EQ &STR(NOHOLD KEEP)
00590025      ALLOC F(SYSPRINT) SYSOUT(&IPRT) &HOLD
00591025    END
00600020IF &INRECFM NE &STR() THEN +
00610022  DO
00620022    IF &EVAL(&LENGTH(&INRECFM)) EQ 3 THEN +
00630022      DO
00640022        SET &INRFM EQ &STR(&SUBSTR(1:1,&INRECFM)&STR( )&SUBSTR(2:2,&INR+
00650022ECFM)&STR( )&SUBSTR(3:3,&INRECFM))
00660022      END
00670022    ELSE +
00680000      IF &EVAL(&LENGTH(&INRECFM)) EQ 2 THEN +
00690010        DO
00700020          SET &INRFM EQ &STR(&SUBSTR(1:1,&INRECFM)&STR( )&SUBSTR(2:2,&I+
00710022NRECFM))
00720022        END
00730022  END
00740022IF &INRFM NE &STR() THEN +
00750022  SET &INATTR EQ &STR(&STR(&INATTR) RECFM(&INRFM) )
00760022IF &INLR NE &STR() THEN +
00770022  SET &INATTR EQ &STR(&STR(&INATTR) LRECL(&INLR) )
00780000IF &INBLK NE &STR() THEN +
00790010  SET &INATTR EQ &STR(&STR(&INATTR) BLKSIZE(&INBLK) )
00800020IF &INDENS NE &STR() THEN +
00810022  SET &INATTR EQ &STR(&STR(&INATTR) DEN(&INDENS) )
00811026IF &INEXP NE &STR() THEN +
00812026  SET &INATTR EQ &STR(&STR(&INATTR) EXPDT(&INEXP) )
00820022IF &STR(&INLABEL) NE &STR() THEN +
00830022  SET &INTAPE EQ &STR(&INTAPE LABEL(&INLABEL) )
00840022IF &INPOS NE &STR() THEN +
00850022  SET &INTAPE EQ &STR(&INTAPE POSITION(&INPOS) )
00860022IF &INUNIT NE &STR() THEN +
00870022  SET &INUV EQ &STR(&INUV UNIT(&INUNIT) )
00880000IF &INVOL NE &STR() THEN +
00890010  SET &INUV EQ &STR(&INUV VOLUME(&INVOL) )
00900034ALLOC F(SYSUT3) NEW DELETE UNIT(SYSDA) SPACE(4) CYL
00910034ALLOC F(SYSUT4) NEW DELETE UNIT(SYSDA) SPACE(4) CYL
00950022IF &SUBSTR(1:1,&STR(&INDSN)) EQ &SUBSTR(3:3,&STR(12(456))  THEN +
00960022  DO
00970027    ALLOC F(SYSUT1) DA&INDSN SHR &INUV &INTAPE &STR(&INATTR)
00980000    GOTO ALCSYS2
00990010  END
01000027ALLOC F(SYSUT1) DA('&INDSN') SHR &INUV &INTAPE &STR(&INATTR)
01010022ALCSYS2: +
01020022IF &OUTRECFM NE &STR() THEN +
01030022  DO
01040022    IF &EVAL(&LENGTH(&OUTRECFM)) EQ 3 THEN +
01050022      DO
01060022        SET &OUTRFM EQ &STR(&SUBSTR(1:1,&OUTRECFM)&STR( )&SUBSTR(2:2,&O+
01070022UTRECFM)&STR( )&SUBSTR(3:3,&OUTRECFM))
01080000      END
01090010    ELSE +
01100020      IF &EVAL(&LENGTH(&OUTRECFM)) EQ 2 THEN +
01110022        DO
01120022          SET &OUTRFM EQ &STR(&SUBSTR(1:1,&OUTRECFM)&STR( )&SUBSTR(2:2,+
01130022&OUTRECFM))
01140022        END
01150022  END
01160022IF &OUTRFM NE &STR() THEN +
01170022  SET &OUTATTR EQ &STR(&STR(&OUTATTR) RECFM(&OUTRFM) )
01180000IF &OUTLR NE &STR() THEN +
01190010  SET &OUTATTR EQ &STR(&STR(&OUTATTR) LRECL(&OUTLR) )
01200020IF &OUTBLK NE &STR() THEN +
01210022  SET &OUTATTR EQ &STR(&STR(&OUTATTR) BLKSIZE(&OUTBLK) )
01220022IF &OUTDENS NE &STR() THEN +
01230022  SET &OUTATTR EQ &STR(&STR(&OUTATTR) DEN(&OUTDENS) )
01231026IF &OUTEX NE &STR() THEN +
01232026  SET &OUTATTR EQ &STR(&STR(&OUTATTR) EXPDT(&OUTEX) )
01240022IF &STR(&OUTLABEL) NE &STR() THEN +
01250022  SET &OUTTAPE EQ &STR(&OUTTAPE LABEL(&OUTLABEL) )
01260022IF &OUTPOS NE &STR() THEN +
01270022  SET &OUTTAPE EQ &STR(&OUTTAPE POSITION(&OUTPOS) )
01280000IF &OUTUNIT NE &STR() THEN +
01290010  SET &OUTUV EQ &STR(&OUTUV UNIT(&OUTUNIT) )
01300020IF &OUTVOL NE &STR() THEN +
01310022  SET &OUTUV EQ &STR(&OUTUV VOLUME(&OUTVOL) )
01370022IF &SPCU NE &STR() THEN +
01380024  IF &SPCU EQ &STR(BLOCK) THEN +
01390022    SET &OUTNEW EQ &STR( &OUTNEW &SPCU(&ALCBL) )
01400022  ELSE +
01410022    SET &OUTNEW EQ &STR( &OUTNEW &SPCU )
01420010IF &PRIQ NE &STR() THEN +
01430020  DO
01440022    IF &SECQ NE &STR() THEN +
01450022      SET &OUTNEW EQ &STR( &OUTNEW SPACE(&PRIQ &SECQ) )
01460022    ELSE +
01470022      SET &OUTNEW EQ &STR( &OUTNEW SPACE(&PRIQ) )
01480022  END
01490022IF &DIRB NE &STR() THEN +
01500022  SET &OUTNEW EQ &STR( &OUTNEW DIR(&DIRB) )
01510000IF &OUTNEW NE &STR() THEN +
01520010  SET &ODISP EQ &STR( &ODISP NEW )
01530020ELSE +
01540022  SET &ODISP EQ &STR( &ODISP SHR )
01550022IF &DISP NE &STR() THEN +
01560022  SET &ODISP EQ &STR( &ODISP &DISP )
01570022IF &EVAL(&LENGTH(&STR(&OUTDSN))) GE 8 THEN +
01580022  DO
01590022    IF &SUBSTR(1:7,&OUTDSN) EQ &STR(SYSOUT=) THEN +
01600022      DO
01601032        IF &FORM NE &STR() THEN +
01602032          DO
01603032            XRXALLOC DDNAME(SYSUT2) FORM(&FORM)
01603133            GOTO EXECPROG
01604032          END
01605032        ELSE +
01610032          IF &DEST NE &STR() THEN +
01620032           DO
01630032              ALLOC F(SYSUT2) SYSOUT(&SUBSTR(8:8,&OUTDSN)) NOHOLD KEEP +
01640032                &STR(&OUTATTR) DEST(&DEST)
01650032              GOTO EXECPROG
01660032            END
01670032          ELSE +
01680032            DO
01690032              ALLOC F(SYSUT2) SYSOUT(&SUBSTR(8:8,&OUTDSN)) NOHOLD KEEP +
01700032                &STR(&OUTATTR)
01710032              GOTO EXECPROG
01720032            END
01730020      END
01740022  END
01750027ALLOC F(SYSUT2) DA('&OUTDSN') &OUTUV &OUTTAPE &STR(&OUTATTR) &OUTNEW +
01760022 &ODISP
01770022EXECPROG:$ &PROGNAME
01780022WRITE &PROGNAME COMPLETED RETURN CODE = &LASTCC
01790022CONTROL NOMSG
01800034FREE F(SYSUT1,SYSUT2,SYSIN,SYSPRINT,SYSUT3,SYSUT4)
01820010CONTROL MSG
01830020GOTO TRYAGIN
01840022ALLDONE: +
01850022ALLOC F(SYSIN) DA(*)
01860022ALLOC F(SYSPRINT) DA(*)
01870027ALLOC F(SYSUT1) NEW DELETE UNIT(SYSDA) SPACE(4) CYL
01880027ALLOC F(SYSUT2) NEW DELETE UNIT(SYSDA) SPACE(4) CYL
01890022END
