00010000PROC 0 DEBUG
00020000/*                                                               */
00030000/*  PHONEMP - PRINT EMPLOYEES PHONE LIST                         */
00040000/*                                                               */
00050000CONTROL NOLIST NOMSG NOFLUSH PROMPT ASIS
00060000IF &DEBUG NE &STR() THEN +
00070000  CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST ASIS
00080000ISPEXEC CONTROL ERRORS RETURN
00090000ISPEXEC DISPLAY PANEL(PHONEMP)
00100000IF &LASTCC = 8 THEN +
00110000  DO
00120000    ISPEXEC  SETMSG MSG(PHN009T)
00130000    GOTO DONE
00140000  END
00150000ERROR +
00160000  DO
00170000    ISPEXEC  SETMSG MSG(PHN009Y)
00180000    EXIT
00190000  END
00200000ISPEXEC  VGET (PHONORD PHONCOPY) ASIS
00210000IF (&PHONORD = N) THEN +
00220000  DO
00230000    SET &OPTN = 1
00240000  END
00250000IF (&PHONORD = E) THEN +
00260000  DO
00270000    SET &OPTN = 2
00280000  END
00290000/*                                                               */
00300000/*  CREATE SEQUENTIAL C271 FILE FROM PHONE TABLE                 */
00310000/*                                                               */
00320000ISPEXEC CONTROL DISPLAY REFRESH
00330000ALLOC F(SYSUDUMP) SYSOUT(X)
00340000ALLOC F(SYSOUT) SYSOUT(X)
00350000ALLOC F(O271C) DA(&SYSUID..T271C) NEW SPACE(65 10) BLKSIZE(6160) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(80) RECFM(F B)
00360000ERROR OFF
00370000ISPEXEC SELECT PGM(PHONDUMP) NEWAPPL(PHN)
00380000SET &RC = &LASTCC
00390000FREE F(SYSUDUMP SYSOUT O271C)
00400000ERROR +
00410000  DO
00420000    ISPEXEC  SETMSG MSG(PHN009Y)
00430000    EXIT
00440000  END
00450000/*                                                               */
00460000/*  SORT C271 BASED ON SORT OPTION                               */
00470000/*                                                               */
00480000ALLOC F(SYSUDUMP) DUMMY
00490000ALLOC F(SYSOUT) DUMMY
00500000ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
00510000OPENFILE SYSIN OUTPUT
00520000IF (&PHONORD = N) THEN +
00530000  DO
00540000    SET &SYSIN EQ &STR( SORT FIELDS=(20,25,A),FORMAT=CH)
00550000  END
00560000IF (&PHONORD = E) THEN +
00570000  DO
00580000    SET &SYSIN EQ &STR( SORT FIELDS=(8,4,A),FORMAT=CH)
00590000  END
00600000PUTFILE SYSIN
00610000CLOSFILE SYSIN
00620000ALLOC F(SORTIN) DA(&SYSUID..T271C) SHR REUSE DELETE
00630000ALLOC F(SORTOUT) DA(&SYSUID..T271CS) NEW SPACE(65 10) BLOCK(80) BLKSIZE(6160) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(80) RECFM(F B)
00640000$ SORT
00650000FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
00660000/*                                                               */
00670000/*  SORT HEADER (ACTUALLY CONVERTS LRECL=135 TO LRECL=132)       */
00680000/*                                                               */
00690000ALLOC F(SYSUDUMP) DUMMY
00700000ALLOC F(SYSOUT) DUMMY
00710000ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
00720000OPENFILE SYSIN OUTPUT
00730000SET &SYSIN EQ &STR( SORT FIELDS=COPY)
00740000PUTFILE SYSIN
00750000SET &SYSIN EQ &STR( OUTREC FIELDS=(1,132))
00760000PUTFILE SYSIN
00770000CLOSFILE SYSIN
00780000ALLOC F(SORTIN) DA(VIDEO.LONGER.DATA(PHONE)) SHR REUSE
00790000ALLOC F(SORTOUT) DA(&SYSUID..HEADER) NEW SPACE(65 10) BLOCK(132) BLKSIZE(6204) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(132) RECFM(F B)
00800000$ SORT
00810000FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
00820000/*                                                               */
00830000/*  CREATE PHONE DIRECTORY                                       */
00840000/*                                                               */
00850000ALLOC F(SYSUDUMP) DUMMY
00860000ALLOC F(SYSOUT) DUMMY
00870000ALLOC F(SYS001) DA(&SYSUID..T271CS) SHR REUSE DELETE
00880000ALLOC F(SYS002) DA(&SYSUID..PRT) NEW SPACE(50 5) BLOCK(133) BLKSIZE(6118) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(133) RECFM(F B M)
00890000ALLOC F(SYS003) DA(&SYSUID..HEADER) OLD REUSE DELETE
00900000SET &PARM EQ &STR(   &OPTN)
00910000STEPLIB DATASET('MIS.LINKLIB') SHR
00920000$ MIS03600 '&PARM'
00930000STEPLIB
00940000FREE F(SYSUDUMP SYSOUT SYS001 SYS002 SYS003)
00950000/*                                                               */
00960000/*  IF DIRECTORY IS TO BE PRINTED IN EMPLOYEE NAME ORDER         */
00970000/*  SORT BEEPERS (ACTUALLY CONVERTS LRECL=135 TO LRECL=133)      */
00980000/*                                                               */
00990000IF (&PHONORD = N) THEN +
01000000  DO
01010000    ALLOC F(SYSUDUMP) DUMMY
01020000    ALLOC F(SYSOUT) DUMMY
01030000    ALLOC F(SYSIN) NEW SPACE(1 1) BLOCK(80) BLKSIZE(80) REUSE UNIT(SYSDA) RELEASE DELETE LRECL(80) RECFM(F)
01040000    OPENFILE SYSIN OUTPUT
01050000    SET &SYSIN EQ &STR( SORT FIELDS=COPY)
01060000    PUTFILE SYSIN
01070000    SET &SYSIN EQ &STR( OUTREC FIELDS=(1X,1,132))
01080000    PUTFILE SYSIN
01090000    CLOSFILE SYSIN
01100000    ALLOC F(SORTIN) DA(VIDEO.LONGER.DATA(PHONEZ)) SHR REUSE
01110000    ALLOC F(SORTOUT) DA(&SYSUID..BEEPER) NEW SPACE(50 5) BLOCK(133) BLKSIZE(6118) REUSE UNIT(SYSDA) RELEASE CATALOG LRECL(133) RECFM(F B M)
01120000    $ SORT
01130000    FREE F(SYSUDUMP SYSOUT SYSIN SORTIN SORTOUT)
01140000  END
01480000/*                                                               */
01490000/*  PRINT DIRECTORY ON XEROX 9700                                */
01500000/*                                                               */
01530000  ALLOC F(SYSPRINT) DUMMY REUSE
01531000  ALLOC F(SYSUDUMP) DUMMY REUSE
01540000  ALLOC F(SYSUT1) DA(&SYSUID..PRT) OLD REUSE DELETE
01550000  ALLOC F(SYSIN) DUMMY REUSE
01560000  IF (&PHONORD = N) THEN +
01570000    DO
01580000      ALLOC F(SYSUT1A) DA(&SYSUID..BEEPER) OLD REUSE DELETE
01590000      CONCAT (SYSUT1,SYSUT1A)
01600000    END
01611000  XRXALLOC DDNAME(SYSUT2) FORM(L001) REUSE
01612000  DO WHILE (&PHONCOPY NE 0)
01620000    $ IEBGENER
01620100    SET &PHONCOPY = (&PHONCOPY - 1)
01621000  END
01630000  FREE F(SYSPRINT SYSUDUMP SYSUT1 SYSUT2 SYSIN)
01630100  IF (&PHONORD = N) THEN +
01630200    DO
01630300      FREE F(SYSUT1A)
01630400    END
01650000ISPEXEC  SETMSG MSG(PHN009W)
01660000DONE: +
01670000  SET &PHONCOPY = &STR( )
01680000  ISPEXEC  VPUT (PHONCOPY) ASIS
01690000  EXIT
01700000END
