//RACFTSO5 JOB (NNNNN,8),'BILL HORTON',CLASS=A,NOTIFY=XXXXXX
//        EXEC PGM=FILEAID
//SYSPRINT DD  SYSOUT=$
//SYSIN    DD  DSN=PROD.CNTRLIB2($$DD01),DISP=SHR
//DD01     DD  DSN=SYS1.UADS,DISP=SHR
//DD01O    DD  DSN=XXXXXX.UADS.LIST,SPACE=(CYL,(1,1)),DISP=(,CATLG),
//             UNIT=SYSDA,DCB=(LRECL=172,RECFM=FB,BLKSIZE=17200)
//        EXEC ASMHCL,PARM.LKED='LIST,MAP,AC=1'
//SYSIN    DD  *
RACFTSO5 CSECT
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9            POINTER TO DYNAMIC AREA
R10      EQU   10           BASE REGISTER
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,RACFTSO5._&SYSDATE._&SYSTIME
         LR    R10,R15
         USING RACFTSO5,R10
         L     R0,LDYNAREA              LOAD LENGTH OF DYNAMIC AREA.
         GETMAIN R,LV=(R0)              GET DYNAMIC AREA.
         LR    R9,R1                    SAVE ADDRESS.
         LR    R2,R9                    POINT TO DYNAMIC AREA.
         LR    R3,R0                    GET LENGTH.
         LA    R4,0                     ZERO.
         LA    R5,0                     PADDING BYTE AND LENGTH.
         MVCL  R2,R4                    CLEAR DYNAMIC AREA.
         USING DYNAREA,R9               ADDRESSIBILITY.
         LA    R2,SAVEAREA
         ST    R13,4(R2)
         ST    R2,8(R13)
         LR    R13,R2
         LA    R3,=A(WEND-WORKAREA)     GET LENGTH OF WORKAREA.
         ST    R3,WLENGTH
         ICHEACTN FIELD=TCOMMAND,FLDATA=(80,LIDATAX),MF=(E,LUPD1)
         MVI   LUPD1+1,X'01'            SET NUMBER OF FIELDS BYTE.
         OPEN  (SYSUT1,(INPUT))
*
*
*
READLOOP GET   SYSUT1
         LR    R8,R1
         LA    R3,4(R8)                 POINT TO USERID.
         MVI   12(R8),C' '              PUT IN ONE MORE BLANK.
         TRT   4(9,R8),BLANKTAB         GET L(USERID) + 1
         SR    R1,R3                    LENGTH OF USERID + 1.
         BCTR  R1,0                     DECREMENT TO GET TRUE LENGTH.
         STC   R1,LUSERID               SAVE LENGTH OF USERID.
         BCTR  R1,0                     DECREMENT FOR EX.
         EX    R1,MOVE1                 COPY USERID.
MOVE1    MVC   USERID(0),0(R3)          COPY USERID.
         MVC   WTP1+16(7),=CL7' '
         EX    R1,MOVE2                 COPY USERID.
MOVE2    MVC   WTP1+16(0),0(R3)         COPY USERID.
WTP1     WTP   'USERID: XXXXXXX'
         LA    R2,LUSERID               POINT TO LENGTH OF USERID.
         LA    R3,WORKAREA              POINT TO WORK AREA.
         ICHEINTY LOCATE,TYPE='USR',ENTRY=(R2),ACTIONS=(ACT1),         X
               WKAREA=(R3),OPTIONS=(FLDEF,ACTION),MF=(E,LOCATE)
         LR    R5,R15                   SAVE RETURN CODE.
         LR    R6,R0                    ADDITIONAL CODES.
         LTR   R5,R5                    GOOD RETURN FROM RACF ?
         BZ    GETOK                    YES, CONTINUE.
         CH    R5,=H'12'
         BNE   BADNEWS
         WTP   'ABOVE NOT A RACF USERID'
         B     GETOK
BADNEWS  ABEND 1,DUMP                   NO, THROW UP.
         PUSH  PRINT
         PRINT NOGEN
GETOK    GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         GET   SYSUT1
         LR    R8,R1
         POP   PRINT
         MVC   RCMD(6),166(R8)
         GET   SYSUT1
         LR    R8,R1
         MVC   RCMD+6(74),0(R8)
         CLC   RCMD,BLANKS              NOTHING HERE?
         BE    READLOOP                 YES, GO GET NEXT USER'S DATA.
         CH    R5,=H'0'                 DID USERID EXIST?
         BNE   READLOOP                 NO, CONTINUE.
         MVC   WTP2+26(80),RCMD
WTP2     WTP   'DATA CHANGED TO: "----5---10----5---20----5---30----5--X
               -40----5---50----5---60----5---70----5---80"'
         MVC   LIDATA(3),=X'005150'     SPECIFY LENGTH.
         LA    R2,LUSERID               POINT TO LENGTH OF USERID.
         ICHEINTY ALTER,TYPE='USR',ENTRY=(R2),ACTIONS=(LUPD1),         X
               OPTIONS=(FLDEF,ACTION),MF=(E,ALTER)
         LTR   R15,R15                  GOOD RETURN FROM RACF?
         BZ    READLOOP                 YES, CONTINUE.
         ABEND 2,DUMP                   NO, THROW UP.
*
* PROCESSING COMPLETED.
*
EOF      CLOSE (SYSUT1)
         L     R13,SAVEAREA+4
         L     R0,LDYNAREA              FREE
         FREEMAIN R,LV=(R0),A=(R9)      DYNAMIC AREA.
         LA     R15,0                   SET RETURN CODE.
         RETURN (14,12),,RC=(15)        RETURN TO CALLER.
LDYNAREA DC    A(EDYNAREA-DYNAREA)
SYSUT1   DCB   DSORG=PS,LRECL=172,RECFM=FB,DDNAME=SYSUT1,EODAD=EOF,    X
               MACRF=(GL)
ACT1     ICHEACTN FIELD=TCOMMAND
TCOMMAND EQU   *-8
BLANKS   DC    CL80' '
BLANKTAB DC    XL256'00'
         ORG   BLANKTAB+X'40'
         DC    X'40'
         ORG
         LTORG ,
         EJECT
DYNAREA  DSECT
SAVEAREA DS    18F
LUPD1    ICHEACTN FIELD=TCOMMAND,FLDATA=(80,LUPD1),MF=L
LOCATE   ICHEINTY LOCATE,ACTIONS=(LUPD1),MF=L
ALTER    ICHEINTY ALTER,ACTIONS=(LUPD1),MF=L
HALF1    DS    D
LUSERID  DS    C
USERID   DS    CL7
WORKAREA DS    0F                      /* WORK AREA FOR ICHEACTN     */
WLENGTH  DS    A                       /* LENGTH OF WORK AREA        */
WRBA     DS    CL6                     /* RBA RETURN AREA            */
WFLAGS   DS    C                       /* FLAGS                      */
WRES1    DS    C                       /* RESERVED                   */
WDDSNC   DS    CL4                     /* DUPL. DATASET NAME COUNT   */
WRES2    DS    CL8                     /* RESERVED                   */
WDATALEN DS    CL4                     /* LENGTH OF RETURNED DATA    */
LIDATA   DS    CL2                     /* LENGTH OF INSTDATA FIELD   */
LIDATAX  DS    C                       /* LENGTH OF DATA IN FIELD    */
RCMD     DS    CL80                    /* ACCOUNTING CODE            */
         DS    CL255                   /* RESERVED                   */
         DS    F                       /*                            */
WEND     EQU   *                       /* END OF WORK AREA           */
EDYNAREA EQU   *
         END
/*
//LKED.SYSLMOD DD DSN=APF.LIBRARY(RACFTSO5),DISP=SHR
//GO      EXEC PGM=RACFTSO5,COND=(0,NE)
//STEPLIB  DD  DSN=APF.LIBRARY,DISP=SHR
//SYSUDUMP DD  SYSOUT=$
//SYSUT1   DD  DSN=XXXXXX.UADS.LIST,DISP=SHR
