/*-------------------------------------------------------------------*/
/*       Macro: SPELL                                                */
/*       Purpose: Run SCRIPT against current data to check spelling  */
/*-------------------------------------------------------------------*/

         ISREDIT MACRO (PARM)
         CONTROL NOFLUSH NOMSG ASIS
         IF &PARM = DEBUG THEN CONTROL LIST CONLIST SYMLIST MSG
         ISREDIT (LAST)    = LINENUM .ZLAST
         ISREDIT (WIDTH)   = DATA_WIDTH
         SET ZEDSMSG =
         SET ZEDLMSG = &STR(** Spell checking in progress **)
         ISPEXEC CONTROL DISPLAY LOCK
         ISPEXEC DISPLAY MSG(ISRZ000)

         /* Allocate DCF input dataset */

         ALLOC FI(ESPELL) NEW CAT SPACE(5 5) TRACKS UNIT(SYSDA) +
         LRECL(80) RECFM(F B) BLKSIZE(23440) DSN(SPELL.MACRO.TEXT) +
         REUSE

         /* Open file and write some SCRIPT commands to be sure */
         /* data is formatted properly for parsing (later).     */

         OPENFILE ESPELL OUTPUT
         SET EXTRA_LINES = 0
         SET ESPELL = &STR(.PW &WIDTH)
         PUTFILE ESPELL
         SET EXTRA_LINES = &EXTRA_LINES + 1
         SET ESPELL = &STR(.LL &WIDTH)
         PUTFILE ESPELL
         SET EXTRA_LINES = &EXTRA_LINES + 1
         SET ESPELL = &STR(.FO OFF)
         PUTFILE ESPELL
         SET EXTRA_LINES = &EXTRA_LINES + 1

         /* Write each line of the data-being-edited */

         SET I = 1
         DO WHILE (&I <= &LAST)
           ISREDIT (ESPELL) = LINE &I
           PUTFILE ESPELL
           SET I = &I + 1
           END
         CLOSFILE ESPELL

         /* Invoke SCRIPT/VS */

         ISPEXEC SELECT MODE(FSCR) CMD(SCRIPT SPELL.MACRO SPELLCHK +
         FILE(SPELL.MACRO.LIST) CO M(DELAY) Q B(0))
         SET SCRIPT_RC = &LASTCC
         FREE FI(ESPELL) DELETE
         SET EOF = NO
         SET MISSPELLED = 0
      /* ISPEXEC BROWSE DATASET(SPELL.MACRO.LIST) */
         ALLOC FI(EDCFOUT) DSN(SPELL.MACRO.LIST) OLD REUSE
         OPENFILE EDCFOUT INPUT
         GETFILE EDCFOUT

         /*------------------------------------------------------*/
         /* Read SCRIPT error messages and create MSG records to */
         /* show user which card images have misspelled words.   */
         /*------------------------------------------------------*/

         DO WHILE (&EOF = NO)

           SET L = &LENGTH(&EDCFOUT)
           IF &L > 18 THEN DO
             IF &SUBSTR(2:16,&STR(&EDCFOUT)) = &STR(WORDS NOT FOUND) +
               THEN DO
               SET MISSPELLED = &MISSPELLED + 1
               SET DCFMSG = &SUBSTR(2:&L,&EDCFOUT)
               SET OK = NO
               GETFILE EDCFOUT
               IF &LENGTH(&EDCFOUT) > 7 +
               THEN IF &SUBSTR(2:8,&EDCFOUT) = 'MACRO' +
               THEN SET OK = YES
               DO WHILE (&OK = NO)
                 SET DCFMSG = +
                 &STR(&DCFMSG&SUBSTR(2:&LENGTH(&EDCFOUT),&EDCFOUT))
                 GETFILE EDCFOUT
                 IF &LENGTH(&EDCFOUT) > 7 +
                 THEN IF &SUBSTR(2:8,&EDCFOUT) = 'MACRO' +
                 THEN SET OK = YES
                 END
               SET IC = &SYSINDEX(:,&EDCFOUT) - 1
               SET BAD_LINE = &SUBSTR(15:&IC,&EDCFOUT)
               SET BAD_LINE = &BAD_LINE - &EXTRA_LINES
               ISREDIT LINE_AFTER &BAD_LINE = +
               NOTELINE "&NRSTR(&DCFMSG)"
               END
             END

           ERROR DO
             SET EOF = YES
             RETURN
             END
           GETFILE EDCFOUT
           ERROR OFF
           END

         CLOSFILE EDCFOUT
         FREE FI(EDCFOUT) DELETE

         IF &MISSPELLED > 0 THEN DO
           ISREDIT LINE_AFTER 0 = MSGLINE +
           "Misspelled words were found (see below)."
           SET TECSMSG = &MISSPELLED lines had errors
           SET TECLMSG =
           ISPEXEC SETMSG MSG(TECZ001)
           END

         ELSE DO
           SET TECSMSG = No misspelled words!
           SET TECLMSG =
           ISPEXEC SETMSG MSG(TECZ000)
           END
