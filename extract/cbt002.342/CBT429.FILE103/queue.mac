QUEUE    TITLE 'QUEUE COMMAND - MAINLINE MODULE'                  ONL01
QUEUE    QSTART MAIN=YES           START QUEUE CSECT ASSEMBLY     ONL01
***********************************************************************
* RNB CHANGES:                                                        *
*      (1) RNB01 - FIX FINAL TPUT MESSAGE TO WORK WITH SPF TCAM       *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*   CALL - INITIALIZATION                                             *
*                                                                     *
***********************************************************************
         L     R15,=V(INIT)   ADDR OF INIT
         BALR  R14,R15        GO TO IT
         LA    R10,LOOP       INTERRUPTED RETURN ADDRESS
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
******************************************************************UF003
*                                                                 UF003
*   INITIALIZE 3270 SCREEN VARIABLES                              UF003
*                                                                 UF003
******************************************************************UF003
         GTSIZE ,             READ 3270 SCREEN SIZE               UF003
         STM   R0,R1,QDOSZR0  SAVE FOR LATER RESTORE              UF003
         LTR   R0,R0          DISPLAY DEVICE?                     UF003
         BZ    NOTDISP        NO, ABORT                           UF003
         SPACE 1                                                  UF003
         CLI   QSPF,C'Y'      RUNNING UNDER ISPF?                   TEC
         BE    NOVTAM         YES, SKIP VTAM CODE.                  TEC
         STFSMODE ON,INITIAL=YES  TELL VTAM ABOUT FULLSCREEN MODE UF003
         SPACE 1                                                  UF003
NOVTAM   DS    0H                                                   TEC
         LM    R0,R1,QDOSZR0  RESTORE DESTROYED REGS              UF003
         CH    R1,=H'80'      POSSIBLE MODEL 2 OR 4 TERMINAL?     UF003
         BNE   TRYM5          NO, TRY FOR MODEL 5                 UF003
         CH    R0,=H'43'      MODEL 4 TERMINAL?                   UF003
         BE    MODEL4         YES, SET IT                         UF003
         CH    R0,=H'32'      MODEL 3 TERMINAL?                   UF003
         BNE   SETM2          NO, SET MODEL 2 DEFAULT             UF003
         SPACE 1                                                  UF003
MODEL3   MVC   QDLNELEN,=H'80'     LINE LENGTH                    UF003
         MVC   QDLNES,=PL2'29'     LINES PER SCREEN AREA          UF003
         MVC   QDPFKTAB+6*8+6(2),=C'29'                           UF043
         MVC   QDPFKTAB+7*8+6(2),=C'29'                           UF043
         MVC   QDSCRLEN,=AL2(29*80)  SCR LENGTH                   UF003
         MVC   QDSCRPLN,=AL2(29*80+QDLINE1-QDSCREEN) TPUT LEN     UF003
         MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE          UF003
         MVC   QDSCRO2,=X'C150'    (2,1)                          UF003
         MVC   QDSCRO3,=X'E6F0'    (32,1)                         UF003
         MVC   QDSCRO4,=X'E7F7'    (32,72)                        UF003
         MVC   QDSCRO5,=X'C260'    (3,1)                          UF003
         MVI   QPANEL+6,C'3'       USE MODEL 3 ISPF PANEL.          TEC
         B     SPFDEF              JOIN COMMON CODE                 TEC
         SPACE 1                                                  UF003
MODEL4   MVC   QDLNELEN,=H'80'     LINE LENGTH                    UF003
         MVC   QDLNES,=PL2'40'     LINES PER SCREEN AREA          UF003
         MVC   QDPFKTAB+6*8+6(2),=C'40'                           UF043
         MVC   QDPFKTAB+7*8+6(2),=C'40'                           UF043
         MVC   QDSCRLEN,=AL2(40*80)  SCR LENGTH                   UF003
         MVC   QDSCRPLN,=AL2(40*80+QDLINE1-QDSCREEN) TPUT LEN     UF003
         MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE          UF003
         MVC   QDSCRO2,=X'C150'    (2,1)                          UF003
         MVC   QDSCRO3,=X'F460'    (43,1)                         UF003
         MVC   QDSCRO4,=X'F5E7'    (43,72)                        UF003
         MVC   QDSCRO5,=X'C260'    (3,1)                          UF003
         MVI   QPANEL+6,C'4'       USE MODEL 4 ISPF PANEL.          TEC
         B     SPFDEF              JOIN COMMON CODE                 TEC
         SPACE 1                                                  UF003
TRYM5    CH    R1,=H'132'     POSSIBLE MODEL 5 TERMINAL?          UF003
         BNE   SETM2          NO, FORCE MODEL 2 DEFAULT           UF003
         CH    R0,=H'27'      ACTUAL MODEL 5?                     UF003
         BNE   SETM2          NO, FORCE MODEL 2 DEFAULT           UF003
         SPACE 1                                                  UF003
MODEL5   MVC   QDLNELEN,=H'132'    LINE LENGTH                    UF003
         MVC   QDLNES,=PL2'24'     LINES PER SCREEN AREA          UF003
         MVC   QDPFKTAB+6*8+6(2),=C'24'                           UF043
         MVC   QDPFKTAB+7*8+6(2),=C'24'                           UF043
         MVC   QDSCRLEN,=AL2(24*132)  SCR LENGTH                  UF003
         MVC   QDSCRPLN,=AL2(24*132+QDLINE1-QDSCREEN) TPUT LEN    UF003
         MVI   QDSCRO1,X'7E'       ERASE/WRITE ALTERNATE          UF003
         MVC   QDSCRO2,=X'C2C4'    (2,1)                          UF003
         MVC   QDSCRO3,=X'F5E8'    (27,1)                         UF003
         MVC   QDSCRO4,=X'F66F'    (27,72)                        UF003
         MVC   QDSCRO5,=X'C4C8'    (3,1)                          UF003
         MVI   QPANEL+6,C'5'       USE MODEL 5 ISPF PANEL.          TEC
         B     SPFDEF              JOIN COMMON CODE                 TEC
         SPACE 1                                                  UF003
SETM2    CLI   QSPF,C'Y'      RUNNING UNDER ISPF?                   TEC
         BE    SPFDEF         YES, BYPASS VTAM CODE.                TEC
         STSIZE SIZE=80,LINE=24  FORCE MODEL 2 DEFAULT              TEC
         SPACE 1                                                  UF003
         B     LOOP           GO TO PROCESS LOOP                  UF003
         SPACE 1                                                  UF003
NOTDISP  TPUT  NOTDSPL,L'NOTDSPL SEND MESSAGE TO USER             UF003
         B     EXIT2          AND RETURN TO CALLER                UF003
         SPACE 1                                                  UF003
NOTDSPL  DC    C'QUEUE COMMAND REQUIRES DISPLAY TERMINAL'         UF003
         EJECT ,                                                  UF003
********************************************************************TEC
*                                                                   TEC
*        INITIALIZE ISPF ENVIRONMENT                                TEC
*                                                                   TEC
********************************************************************TEC
SPFDEF   CLI   QSPF,C'Y'      RUNNING UNDER ISPF?                   TEC
         BNE   LOOP           NO, CONTINUE.                         TEC
         L     R15,@ISPLINK   LOAD ADDRESS OF ISPF ROUTINE.         TEC
         CALL  (15),(#VDEFINE,#QDTLINE,QDTLINE,#CHAR,L63),VL,       TECX
               MF=(E,VDEFINE)                                       TEC
         L     R15,@ISPLINK   LOAD ADDRESS OF ISPF ROUTINE.         TEC
         CALL  (15),(#VDEFINE,#QDHLINE,QDHLINE,#CHAR,L80),VL,       TECX
               MF=(E,VDEFINE)                                       TEC
         L     R15,@ISPLINK   LOAD ADDRESS OF ISPF ROUTINE.         TEC
         CALL  (15),(#VDEFINE,#QDLINE1,QDLINE1,#CHAR,LBIG),VL,      TECX
               MF=(E,VDEFINE)                                       TEC
         L     R15,@ISPLINK   LOAD ADDRESS OF ISPF ROUTINE.         TEC
         CALL  (15),(#VDEFINE,#QDREPLY,QDREPLY,#CHAR,L63),VL,       TECX
               MF=(E,VDEFINE)                                       TEC
***********************************************************************
*                                                                     *
*   CALL - COMMAND LINE PARSE                                         *
*                                                                     *
***********************************************************************
LOOP     DS    0H                                                 UF006
         L     R15,=V(PARSE)  ADDR OF PARSE
         BALR  R14,R15        GO TO IT
         CLC   =C'E ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'EXIT ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'END ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'STOP ',QSUBNAME STOP?
         BE    EXIT           YES.
***********************************************************************
*                                                                     *
*   CALL - SUB-COMMAND MODULE SELECTED BY PARSE                       *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,DUMMY  NO OUTPUT MESSAGE
         L     R15,QSUBCMD    ADDR OF SUBCMD FROM QCOMMON
         BALR  R14,R15        GO TO IT
         MVC   QDMLNG,=H'0'   ZERO OUT MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        WRITE LAST SCREEN, GET NEXT INPUT
         B     LOOP           DO IT AGAIN
***********************************************************************
*                                                                     *
*   CLEAN UP AND GO HOME                                              *
*                                                                     *
***********************************************************************
EXIT     CLI   QSPF,C'Y'      RUNNING UNDER ISPF?                   TEC
         BNE   EXIT1A         NO, CONTINUE.                         TEC
         L     R15,@ISPLINK   LOAD ADDRESS OF ISPF ROUTINE.         TEC
         CALL  (15),(#VDELETE,#ALL),MF=(E,VDELETE)                  TEC
         DELETE EP=ISPLINK                                          TEC
         B     EXIT2                                                TEC
EXIT1A   STSIZE SIZELOC=QDOSZR1,LINELOC=QDOSZR0  RESTORE SCRSIZE  UF003
         TPUT  CLEAR,L'CLEAR,FULLSCR,MF=(E,QTPUT) CLEAR SCREEN    UF003
         STLINENO LINE=1,MODE=OFF                 OFF FULLSCR     UF003
         USING QCKPT,R8 BASE REG FOR CKPT WORK AREA
EXIT2    DS    0H                                                 UF003
         L     R8,QVCKPT      LOAD BASE REG
         CLOSE MF=(E,HOCKPT)
         SPACE 1                                                     CL
         LA    R7,QCSPOOLS         ADDR OF FIRST DCB POINTER         CL
         LA    R6,9                MAXIMUM NUMBER OF EXTENTS         CL
         SPACE 1                                                     CL
CLOSELOP OC    0(4,R7),0(R7)       SKIP IF THIS EXTENT               CL
         BZ    CLOSEBMP             IS NOT ACTIVE                    CL
         OI    0(R7),X'80'         SET END OF LIST INDICATE          CL
         CLOSE (,FREE),MF=(E,(R7)) CLOSE ONE EXTENT                  CL
CLOSEBMP LA    R7,4(,R7)           BUMP TO NEXT EXTENT               CL
         BCT   R6,CLOSELOP         CONTINUE IF MORE EXTENTS          CL
         SPACE 1                                                     CL
         TM    QPFLAG,HARDCPY         IS HARDCOPY INVOKED?         FCI*
         BNO   FREEUP                   NO..SPLIT THIS STUFF       FCI*
         L     R15,=V(PRINT)               INVOKE PRINT            FCI*
         MVC   QDREPLY,QBLANK                  TO                  FCI*
         MVC   QDREPLY(09),=C'PRINT OFF'          FREE UP          FCI*
         MVC   QDRLNG,=X'0009'                        HARDCOPY     FCI*
         BALR  R14,R15                                     OUTPUT  FCI*
         EJECT
***********************************************************************
*                                                                  FCI*
*   FREE HASPCKPT AND HASPACEN DDNAMES BEFORE LEAVING TO BE NEAT.. FCI*
*                                                                  FCI*
***********************************************************************
FREEUP   MVI   DAIRFLAG,X'18'    INDICATE FREE DDNAME(XXXXXXXX)    FCI*
         MVC   DA18DDN,HASPCKPT+40  GET DDNAME USED...             FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           GO FREE IT..                      FCI*
*
         LA    R2,QCSPOOLS       GET ADDR OF LIST OF HASPACE DCBS  FCI*
         LA    R4,35             MAX OF 35 PASSES THROUGH HERE..   FCI*
FREEUP1  L     R3,0(R2)          GET ADDRESS OF DCB TO WORK ON     FCI*
         LTR   R3,R3             ANYONE THERE?                     FCI*
         BZ    EXITQCK                                             FCI*
         MVC   DA18DDN,40(R3)    MOVE IN DDNAME FROM DCB           FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           AND GO INVOKE DAIR TO FREE IT..   FCI*
         LA    R2,4(R2)          BUMP                              FCI*
         BCT   R4,FREEUP1                                          FCI*
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   ISSUE FINAL AUTHORIZATION CHECK BEFORE RETURNING TO THE SYSTEMONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
EXITQCK  DS    0H                  QUEUE COMMAND PROCESSING DONE  ONL02
         QAUTHCK QAEND             ISSUE FINAL AUTHORIZATION CHECKONL02
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   FREE THE AREAS ACQUIRED IN INIT (Q3)                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                  ONL02
         OI    QGETL3,X'80'        PREPARE FOR FREEMAIN           ONL02
         FREEMAIN MF=(E,QFREE)
         TM    QFLAG1,QFLG1DBC    NEED TO TERMINATE ESTAE?        UF024
         BZ    NOESTAE            NO, SKIP THIS                   UF024
         ESTAE 0                  DELETE CURRENT ESTAE            UF024
         NI    QFLAG1,255-QFLG1DBC  CLEAR FLAG                    UF024
NOESTAE  DS    0H                                                 UF024
         QSTOP RC=0               RETURN TO TMP WITH RC=0         ONL11
***********************************************************************
*                                                                     *
*   CONSTANTS AND OTHER ODDITIES                                      *
*                                                                     *
***********************************************************************
         LTORG
CLEAR1   EQU   *                   START OF CLEAR DATA            UF003
*        DC    X'27F5C1'           ESC; ERASE/WRITE; RESET MDT    UF003
         DC    X'C1'               FIX FOR SPF/TCAM               RNB01
         DC    X'115D7E'           SBA  24,80                     UF003
         DC    X'114040'           SBA  1,1                       UF003
         DC    X'3C404000'         RTA  1,1 WITH NULLS            UF003
         DC    X'1DC8'             SF, INTENSIFIED                UF003
         DC    X'13'               INSERT CURSOR                  UF003
CLEAR    EQU   CLEAR1,*-CLEAR1                                    UF003
DUMMY    DC    CL80'    NO DATA IS AVAILABLE FOR YOUR REQUEST'
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
