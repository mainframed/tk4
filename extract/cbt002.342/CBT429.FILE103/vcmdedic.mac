         PROC 0 DEBUG
/*--------------------------------------------------------------------*/
/* THIS CLIST COPIES A COMMAND TABLE INTO THE USER'S PROFILE DATASET, */
/* INVOKES THE COMMAND TABLE EDIT UTILITY AGAINST THE MEMBER          */
/* (VCMDCMDS), AND, IF THE TABLE WAS CHANGED, COPIES THE TABLE BACK   */
/* TO THE ORIGINAL DATASET, REPLACING THE OLD TABLE.                  */
/*--------------------------------------------------------------------*/
         IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST
DISP:    ISPEXEC DISPLAY PANEL(VCMDEDIT)
         IF &LASTCC > 0 THEN EXIT
         ISPEXEC LMINIT DATAID(LMFROM) DATASET(&DSNFROM)
         IF &LASTCC > 0 THEN DO
           SET TECSMSG = "FROM" DATASET ERROR
           SET TECLMSG = UNABLE TO OPEN &DSNFROM..
           ISPEXEC SETMSG MSG(TECZ001)
           EXIT
           END
         ISPEXEC LMINIT DATAID(LMTO) DDNAME(ISPTABL)
         IF &LASTCC > 0 THEN DO
           SET TECSMSG = "TO" DATASET ERROR
           SET TECLMSG = UNABLE TO OPEN ISPTABL DDNAME.
           ISPEXEC SETMSG MSG(TECZ001)
           EXIT
           END
         ISPEXEC LMCOPY FROMID(&LMFROM) FROMMEM(&MEMF.CMDS) +
           TODATAID(&LMTO) TOMEM(VCMDCMDS) REPLACE
         IF &LASTCC > 0 THEN DO
           SET TECSMSG = MEMBER NOT FOUND
           SET TECLMSG = MEMBER &MEMF.CMDS NOT FOUND IN +
                         &DSNFROM..
           ISPEXEC SETMSG MSG(TECZ001)
           GOTO DISP
           END
         SET TECSMSG =
         SET TECLMSG = MAKE DESIRED CHANGES TO COMMAND TABLE, HIT +
                       END KEY TO SAVE IN TABLE &MEMF..
         ISPEXEC SETMSG MSG(TECZ000)
         ISPEXEC SELECT PGM(ISPUCM) PARM(VCMD)
         SET GOCC = &LASTCC
         IF &GOCC = 8 THEN DO
           ISPEXEC LMMOVE FROMID(&LMTO) FROMMEM(VCMDCMDS) +
             TODATAID(&LMFROM) TOMEM(&MEMF.CMDS) REPLACE
           IF &LASTCC = 0 THEN DO
             SET TECSMSG = CMD TABLE EDITED
             SET TECLMSG = ISPF COMMAND TABLE &MEMF SUCCESSFULLY EDITED.
             ISPEXEC SETMSG MSG(TECZ000)
             END
           ELSE DO
             SET TECSMSG = CMD TABLE COPY FAILED
             SET TECLMSG = ISPF COMMAND TABLE &MEMF WAS NOT COPIED +
                           BACK TO &DSNFROM
             ISPEXEC SETMSG MSG(TECZ001)
             END
           END
         ELSE DO
           IF &GOCC = 101 THEN DO
             SET TECSMSG = NOTHING CHANGED
             SET TECLMSG = ISPF COMMAND TABLE &MEMF WAS NOT +
                           CHANGED.
             ISPEXEC SETMSG MSG(TECZ000)
             END
           ELSE DO
             SET TECSMSG = CMD TABLE EDIT FAILED
             SET TECLMSG = ISPF COMMAND TABLE &MEMF WAS NOT +
                           SUCCESSFULLY EDITED.  RETURN CODE WAS &GOCC..
             ISPEXEC SETMSG MSG(TECZ001)
             END
           END
