         TITLE 'IGGPRE00 -DF/DS ALLOCATE PRE PROCCESSING EXIT TESTER'
*
*        ATTRIBUTES:  RENT REUS
*        REGISTER USAGE:
*        R0
*        R1    ON ENTRY, ADDRESS OF EXIT PARAMETER LIST
*        R2    DYNAMIC AREA ADDRESSABILITY
*        R3    EXIT PARAMETER LIST ADDRESSABILITY
*        R4    JFCB DSECT BASE
*        R5    UCB DSECT ADDRESSABILITY
*        R6    WORK
*        R7    WORK
*        R8    WORK
*        R9    WORK
*        R10   WORK
*        R11   WORK
*        R12   PROGRAM BASE REG
*        R13   SAVE AREA POINTER
*        R14   RETURN ADDRESS
*        R15   RETURN CODES: 0 = ALLOW ALLOCATE
*                            4 = CONDITIONAL REJECTION (THIS VOLUME)
*                            8 = UNCONDITIONAL REJECTION
*
*        MACROS:
*              SAVE
*              GETMAIN
*              FREEMAIN
*              RACHECK
*              RETURN
*              EQUATE        DELUXE REGISTER EQUATES
*              IECIEXPL      PRE/POST PARAMETER LIST MAP
*              IEFJFCBN      JFCB MAPPING
*              IEFUCBOB      UCB MAPPING
*
IGGPRE00 CSECT
         SAVE  (14,12),,IGGPRE00_DFDS_ALLOC_PRE_TEST_&SYSDATE_&SYSTIME
         LR    R12,R15                  PROGRAM ADDRESSABILITY
         USING IGGPRE00,R12             TELL ASSEMBLER
         LR    R3,R1                    SAVE PARM LIST ADDRESS IN R2
         USING IEXPL,R3                 TELL ASSEMBLER
         CLI   IEXFUNC,IEXALL           IS THIS ALLOCATE?
         BNE   RETURN                   QUICK RETURN
         CLI   IEXFLAG,IEXVIO           IS THIS VIO REQUEST?
         BE    RETURN                   QUICK RETURN
         L     R4,IEXPTR1               ADDRESS OF JFCB
         USING JFCBDSCT,R4              TELL ASSEMBLER
         LH    R11,MAXBLKSI             MAXIMUM BLKSIZE TO R11
         L     R5,IEXUCB                GET ADDRESS OF THE UCB
         USING UCBCMSEG,R5              TELL ASSEMBLER
         DROP  R3
CHKRACF  EQU   *                        FIND OUT WHAT UNITNAME IS
         CLI   JFCDSRG2,JFCORGAM        IS THIS VSAM?
         BNE   NOTVSAM                  NO ELSE
         RACHECK CLASS='APPL',ENTITY=VSAMDEFI
         C     R15,=F'8'                NOT ALLOWED?
         BE    NOTALOW                  FAIL REQUEST
         B     CKVOLSER                 CHECK FOR VALID VOLUME
NOTVSAM  EQU   *
         CH    R11,JFCBLKSI             FOR COMPARE WITH JFCBLKSI
         BH    CKVOLSER                 LESS OR EQUAL MAX IS OK
         CH    R11,JFCLRECL             SEE IF LRECL IS HIGH
         BNH   CKVOLSER                 GO SEE WHETHER RACF WILL ALLOW
         RACHECK CLASS='APPL',ENTITY=LRGBLKSI
         C     R15,=F'8'                NOT ALLOWED?
         BE    NOTALOW                  FAIL REQUEST
         DROP  R4
CKVOLSER EQU   *
         L     R0,DYNSIZE               DYNAMIC AREA SIZE TO R0
         GETMAIN R,LV=(0)
         LR    R2,R1                    DYNAMIC AREA ADDRESSABILITY
         USING DATD,R2                  TELL ASSEMBLER
         MVC   RACFE(RACFLEN),RACFLIST  PRIME RACHECK PARMLIST
         RACHECK CLASS=DASDVOL,ENTITY=UCBVOLI,MF=(E,RACFE)
         LR    R4,R15                   SAVE RETURN CODE
         L     R0,DYNSIZE               DYNAMIC AREA SIZE TO R0
         LR    R1,R2                    ADDRESS OF AREA TO R1
         FREEMAIN R,LV=(0),A=(1)        FREE DYNAMIC AREA
         DROP  R2
         LR    R15,R4                   RETURN RETURN CODE
         C     R15,=F'8'                NOT ALLOWED?
         BE    NOTALOW                  FAIL REQUEST
         DROP  R5
RETURN   SLR   R15,R15                  SET RC=0
RETURNX  RETURN (14,12),,RC=(15)        RETURN TO CONTROL PROGRAM
NOTALOW  EQU   *
         LA    R15,8                    SET RETURN CODE 8
         LA    R15,0(R15)               CLEAR HI ORDER BIT
         B     RETURNX                  FAIL ALLOCATION
MAXBLKSI DC    H'23477'                 MAXIMUM BLKSIZE
LRGBLKSI DC    CL8'LRGBLKSI'            LRGBLKSI ENTITY
VSAMDEFI DC    CL8'VSAMDEFI'            VSAMDEFI ENTITY
RACFLIST RACHECK MF=L
RACFLEN  EQU   *-RACFLIST
DASDVOL  DC    AL1(7),CL7'DASDVOL'      DASDVOL CLASS
         CNOP  0,4
DYNSIZE  DC    AL1(0)                   SUBPOOL
         DC    AL3(SIZEDATD)            DYNAMIC AREA SIZE
         LTORG
         EQUATE
DATD     DSECT
RACFE    RACHECK MF=L
SIZEDATD EQU   *-DATD                   LENGTH OF DSECT
IEXPL    IECIEXPL DSECT=YES
JFCBDSCT DSECT
         IEFJFCBN
         IEFUCBOB
         END   IGGPRE00
