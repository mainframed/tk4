         TITLE 'DELUXE JES2 COMMAND SCREEN USER EXIT - PREAMBLE'
**********************************************************************
*                                                                    *
*           DELUXE JES2 CMD SCREEN USER EXIT 255                     *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE WILL SET AUTHORIZATION     *
*       FOR S XXXRN,N=PROCNAME COMMANDS THAT ARE                     *
*       COMING FROM RN AND DIRECTED AT ADMIN TO ALLOW. THIS          *
*       WILL BE DONE BY ANDING THE CMBFLAG FIELD WITH X'F0' WHICH    *
*       WILL ALLOW ALL COMMANDS.  IN ALL OTHER CASES THE EXIT WILL   *
*       SIMPLY RETURN AND ALLOW JES2 TO DO THE AUTHORITY CHECKING.   *
*                                                                    *
*    ENTRY POINT = DLXUX255                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0-3  N/A                                                    *
*       R4    ADDRESS OF THE CMB                                     *
*       R5-10 N/A                                                    *
*       R11   ADDRESS OF THE JES2 HCT                                *
*       R12   N/A                                                    *
*       R13   N/A                                                    *
*       R14   RETURN ADDRESS                                         *
*       R15   ENTRY ADDRESS                                          *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             =0  CONTINUE WITH NORMAL $WTO PROCESSING               *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $SAVE, $RETURN, $MODEND, $HASPGBL                   *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
CMDEX255 $MODULE $BUFFER,                                              +
               $CAT,                    REQ'D BY HCT                   +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $CMB,                    CONSOLE MESSAGE BUFFER MAPPING +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D BY $CAT                  +
               $JQE,                    REQ'D BY HCT                   +
               $MIT,                    REQ'D BY $MODEND               +
               $PCE,                    REQ'D BY HCT                   +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE JES2 COMMAND SCREEN USER EXIT '
         SPACE 1
DLXUX255 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         USING CMB,R4                   ADDRESSABILITY TO CMB
         SLR   R15,R15
         TM    CMBFLAG,CMBFLAGC         IS IT A COMMAND?
         BNO   RETURN                   NO QUIT
         CLC   CMBFMNOD,=H'1'           IS IT ORIGINATING AT ADMIN?
         BE    RETURN                   ALLOW IT
         CLC   CMBFMNOD,=H'10'          IS IT ORIGINATING AT ELOIGNE?
         BE    RETURN                   ALLOW IT
         CLC   CMBTONOD,=H'1'           IS IT DESTINED FOR ADMIN?
         BE    NOTADM1                  CONTINUE CHECKING
         CLC   CMBTONOD,=H'10'          IS IT DESTINED FOR ELOIGNE?
         BE    NOTADM1                  CONTINUE CHECKING
         BNE   RETURN                   LET JES2 FIGURE IT OUT
NOTADM1  EQU   *
         CLC   CMBMSG(2),=CL2'S '       IS IT A START COMMAND?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
         CLI   CMBMSG+5,C'R'            IS IT A REGIONAL STC?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
         TM    CMBMSG+6,X'F0'           IS IT NUMERIC?
         BNO   RETURN                   GREATER THAN 8; LET JES DO IT
         CLC   CMBMSG+7(3),=CL3',N='    IS IT THE CORRECT FORMAT?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
CHKCMD11 EQU   *
         CLI   CMBFMNOD+1,X'0B'         IS IT NODE 11 (REG1)?
         BNE   CHKCMD12                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'1'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD12 EQU   *
         CLI   CMBFMNOD+1,X'0C'         IS IT NODE 12 (REG2)?
         BNE   CHKCMD13                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'2'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD13 EQU   *
         CLI   CMBFMNOD+1,X'0D'         IS IT NODE 13 (REG3)?
         BNE   CHKCMD14                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'3'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD14 EQU   *
         CLI   CMBFMNOD+1,X'0E'         IS IT NODE 14 (REG4)?
         BNE   CHKCMD15                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'4'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD15 EQU   *
         CLI   CMBFMNOD+1,X'0F'         IS IT NODE 15 (REG5)?
         BNE   CHKCMD16                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'5'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD16 EQU   *
         CLI   CMBFMNOD+1,X'10'         IS IT NODE 16 (REG6)?
         BNE   CHKCMD17                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'6'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD17 EQU   *
         CLI   CMBFMNOD+1,X'11'         IS IT NODE 17 (REG7)?
         BNE   CHKCMD18                 CHECK NEXT NODE
         CLI   CMBMSG+6,C'7'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES OK TO ISSUE COMMAND
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CHKCMD18 EQU   *
         CLI   CMBFMNOD+1,X'12'         IS IT NODE 18 (REG8)?
         BNE   RETURN                   LET JES2 HANDLE IT
         CLI   CMBMSG+6,C'8'            IS THIS FOR THEMSELVES?
         BNE   RETURN                   JES2 WILL NIX IT
OKIT     EQU   *
         NI    CMBFLAG,X'F0'            SAY IT IS OK TO ISSUE COMMAND
RETURN   EQU   *
         $RETURN RC=(R15)               RETURN TO CALLER
         SPACE 1
         LTORG
         $MODEND
         END  ,
