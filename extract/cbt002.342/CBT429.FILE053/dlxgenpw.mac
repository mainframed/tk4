         TITLE 'DLXGENPW - GENERATE PASSWORD'
***********************************************************************
*                                                                     *
*             MODULE NAME = DLXGENPW                                  *
*                                                                     *
*             DESCRIPTIVE NAME = GENERATE PASSWORD                    *
*                                                                     *
*             FUNCTION =                                              *
*                GENERATES FROM 1 TO 9 RANDOM 8-CHARACTER             *
*                PASSWORDS.                                           *
*                                                                     *
*             PARAMETER =                                             *
*                NUMBER OF PASSWORDS TO BE GENERATED                  *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                10/30/85  WRITTEN.      BC.                          *
*                                                                     *
***********************************************************************
*
DLXGENPW CSECT
         SAVE  (14,12),,DLXGENPW_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING DLXGENPW,R12            ESTABLISH ADDRESSABILITY
         LR    R2,R1                   SAVE PTR TO PARM ADDRESS
         LA    R11,1                   DEFAULT TO 1 PASSWORD
         L     R3,0(R2)                GET ADDRESS OF PARM
         SLR   R4,R4
         LH    R4,0(R3)                GET LENGTH OF PARM
         LTR   R4,R4                   IF NO PARMS
         BZ    CONT                    THEN CONTINUE
         PACK  DBL,2(1,R3)             PACK PARM
         CVB   R11,DBL                 CONVERT TO BINARY
CONT     EQU   *
         TIME  BIN                     GET THE TIME (IN 1/100 SECONDS)
         LR    R7,R0                   STORE IT
         SLR   R6,R6                   MAKE IT A DOUBLE WORD
         D     R6,=F'262139'           DIVIDE BY 'M' REMAINDER='SEED'
         LR    R7,R6                   SAVE REMAINDER
         SLR   R6,R6
NEXT     EQU   *
         LA    R10,8                   SET TO REPEAT 8 TIMES
         LA    R9,PASSWORD             POINT TO START OF PASSWORD
RANDOM   EQU   *
         M     R6,=F'5243'             MULTIPLY BY 'J'
         A     R7,=F'55397'            ADD 'K'
         D     R6,=F'262139'           DIVIDE BY 'M'
         LR    R5,R6                   SAVE 'I' (SEED)
         SLR   R4,R4                   MAKE IT A DOUBLE WORD
         M     R4,=F'10000'            MULTIPLY SO RESULT NOT < 1
         A     R5,=F'5000'             ADD '.5X10000'
         D     R4,=F'262139'           DIVIDE BY 'M'
         SLR   R4,R4                   GET RID OF REMAINDER
         D     R4,=F'36'               DIVIDE BY 36 REMAINDER=INDEX
         LA    R8,TABLE                POINT TO TABLE
         AR    R8,R4                   INDEX INTO TABLE
         MVC   0(1,R9),0(R8)           INSERT CHARACTER INTO PASSWORD
         LA    R9,1(R9)                POINT TO NEXT POSITION
         LR    R7,R6                   GET SEED FOR NEXT
         SLR   R6,R6
         BCT   R10,RANDOM              REPEAT 8 TIMES
         TPUT  PASSWORD,8              OUTPUT THE PASSWORD
         BCT   R11,NEXT
EXIT     EQU   *
         RETURN (14,12),RC=(15)
         LTORG
*
*  REGISTER EQUATES
*
         EQUATE
*
DBL      DS    D
PASSWORD DS    CL8' '
TABLE    DC    C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'
*
         END   DLXGENPW
