         TITLE 'TMCTAB3 - EXTRACT ACTIVE DATA SETS FROM TMC'
***********************************************************************
*                                                                     *
*             MODULE NAME = TMCTAB3                                   *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                04/09/86  WRITTEN.                              JWC. *
*                                                                     *
*                10/27/86  FIX INCORRECT HANDLING OF DSNB'S.     JWC. *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'TMCTAB3 - INITIALIZATION SECTION'
TMCTAB3 CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,TMCTAB3_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING TMCTAB3,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         LA    R4,DATA                 SET UP
         LA    R5,DATALEN                  REGISTERS
         LA    R6,INITDATA                     FOR
         LA    R7,DATALEN                          LONG MOVE
         MVCL  R4,R6                   INITIALIZE PART GETMAINED AREA
         MVC   TMCMSG,SPACES           INITIALIZE ERROR MESSAGE
***********************************************************************
*  INITIALIZATION SECTION - OPEN 'UCC.ONE.TMC', '&&TMOUT'
***********************************************************************
OPEN     EQU   *
         OPEN  (INPUT,INPUT),MF=(E,OPENLST_)     OPEN TMCIN
         LTR   R15,R15                 RETURN CODE ZERO?
         BZ    OPEN10                  YES, OPEN TMCOUT
         B     OPEN20(R15)             NO, PROCESS ERROR
OPEN10   EQU   *
         OPEN  (OUTPUT1,OUTPUT),MF=(E,OPENLST_)  OPEN TMCOUT
         B     OPEN20(R15)             CHECK RETURN CODE
OPEN20   EQU   *
         B     PROCESS                 RC = 00 (CONTINUE)
         B     ERR04                   RC = 04 (PRINT ERROR MESSAGE)
         B     ERR08                   RC = 08 (PRINT ERROR MESSAGE)
         B     ERR12                   RC = 12 (PRINT ERROR MESSAGE)
         B     ERR16                   RC = 16 (PRINT ERROR MESSAGE)
         B     ERR20                   RC = 20 (PRINT ERROR MESSAGE)
         TITLE 'TMCTAB3 - PROCESSING SECTION'
***********************************************************************
*  PROCESSING SECTION - GET RECORD FROM TMC, EXTRACT INFO & WRITE
***********************************************************************
PROCESS  EQU   *
         GET   INPUT                   GET RECORD FROM TMC
         LR    R2,R1                   SAVE R1
         TM    0(R2),DSNB              DATA SET NAME BLOCK?
         BO    PROC10                  YES, PROCESS DSNB
         TM    131(R2),SCRATCH         TAPE LISTED ON SCRATCH LIST?
         BO    PROCESS                 YES, GET ANOTHER RECORD
         TM    131(R2),DELETED         VOLUME DELETED FROM LIBRARY?
         BO    PROCESS                 YES, GET ANOTHER RECORD
         MVC   TMCDSN,0(R2)            EXTRACT DATA SET NAME FROM TMC
         MVC   TMCVOL,71(R2)           EXTRACT VOLUME SERIAL FROM TMC
         B     PROC30                  WRITE RECORD TO TMCOUT
PROC10   EQU   *
         MVC   TMCDSN,9(R2)            EXTRACT DATA SET NAME FROM DSNB
         MVC   TMCVOL,1(R2)            EXTRACT VOLUME SERIAL FROM DSNB
         CLC   FULL00,TMCDSN           DATA SET NAME PRESENT?
         BE    PROC20                  NO, GET ANOTHER RECORD
         PUT   OUTPUT1,TMCREC          WRITE RECORD TO TMCOUT
PROC20   EQU   *
         MVC   TMCDSN,109(R2)          EXTRACT DATA SET NAME FROM DSNB
         MVC   TMCVOL,101(R2)          EXTRACT VOLUME SERIAL FROM DSNB
         CLC   FULL00,TMCDSN           DATA SET NAME PRESENT?
         BE    PROCESS                 NO, GET ANOTHER RECORD
PROC30   EQU   *
         PUT   OUTPUT1,TMCREC          WRITE RECORD TO TMCOUT
         B     PROCESS                 INITIALIZE TMCDSN & TMCVOL
         TITLE 'TMCTAB3 - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
ERR04    EQU   *
         MVC   RETNCOD,FOUR            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
ERR08    EQU   *
         MVC   RETNCOD,EIGHT           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
ERR12    EQU   *
         MVC   RETNCOD,TWELVE          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
ERR16    EQU   *
         MVC   RETNCOD,SIXTEEN         SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
ERR20    EQU   *
         MVC   RETNCOD,TWENTY          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
MSGDISP1 EQU   *
         OPEN  (OUTPUT2,OUTPUT),MF=(E,OPENLST_)  OPEN ERROUT
         MVC   TMCMSG,TMCERR           SET ERROR MESSAGE
         MVC   TMCMSG+47(2),RETNCOD    SET DIALOG SERVICE RETURN CODE
         PUT   OUTPUT2,TMCMSG          PRINT ERROR MESSAGE
         CLOSE OUTPUT2,MF=(E,CLOSLST_)    CLOSE ERROUT
         TITLE 'TMCTAB3 - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         CLOSE INPUT,MF=(E,CLOSLST_)   CLOSE INPUT
         CLOSE OUTPUT1,MF=(E,CLOSLST_)    CLOSE TMCOUT
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'TMCTAB3 - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
SCRATCH  EQU   X'04'                   TMFLAG1 - TAPE LISTED SCR LIST
DELETED  EQU   X'02'                   TMFLAG1 - VOL DELETED FROM LIB
DSNB     EQU   X'FF'                   1ST BYTE DSN-DATA SET NAME BLOCK
         TITLE 'TMCTAB3 - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
FULL00   DC    F'00'                   FULLWORD CONSTANT; VALUE '00'
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
SPACES   DC    CL133' '                SPACES
TMCERR   DC    CL133' UNABLE TO OPEN ''UCC.ONE.TMC''. RETURN CODE = 00.+
               NOTIFY SYSTEMS PROGRAMMING.'
*                                      ERROR MESSAGE TEXT
         TITLE 'TMCTAB3 - LITERAL POOL'
         LTORG
***********************************************************************
*        INITIALIZATION DATA FOR PART OF GETMAINED AREA
***********************************************************************
INITDATA DS    0F
DCBIN    DCB   DDNAME=TMCIN,DSORG=PS,MACRF=(GL),EODAD=FINISH
DCBOUT1  DCB   DDNAME=TMCOUT,DSORG=PS,MACRF=(PM)
DCBOUT2  DCB   DDNAME=ERROUT,DSORG=PS,MACRF=(PM)
OPENLST  OPEN  (,),MF=L            STATIC LIST FORM OF OPEN MACRO
CLOSLST  CLOSE (,),MF=L            STATIC LIST FORM OF CLOSE MACRO
DATALEN  EQU   *-INITDATA
         TITLE 'TMCTAB3 - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
TMCMSG   DS    CL133                   ERROR MESSAGE
TMCREC   DS    0CL80                   TMC OUTPUT RECORDS
TMCDSN   DS    CL44                    DATA SET NAME FROM TMC
TMCVOL   DS    CL6                     VOLSER FROM TMC
         DS    CL30
RETNCOD  DS    CL2                     ISPF DIALOG SERVICE RETURN CODE
DATA     DS    0F
INPUT    DCB   DDNAME=TMCIN,DSORG=PS,MACRF=(GL),EODAD=FINISH
OUTPUT1  DCB   DDNAME=TMCOUT,DSORG=PS,MACRF=(PM)
OUTPUT2  DCB   DDNAME=ERROUT,DSORG=PS,MACRF=(PM)
OPENLST_ OPEN  (,),MF=L            DYNAMIC LIST FORM OF OPEN MACRO
CLOSLST_ CLOSE (,),MF=L            DYNAMIC LIST FORM OF CLOSE MACRO
WORKLEN  EQU   *-WORKAREA
         END   TMCTAB3
