         TITLE 'DLXTSO - TSO SYSTEM ENFORCEMENT'
***********************************************************************
*
*        MODULE NAME: DLXTSO
*
*        DESCRIPTION: TSO SYSTEM ENFORCEMENT
*
*        FUNCTION: RESTRICT UNAUTHORIZED USERS FROM ACCESSING TSO
*                  ON A CPU WHICH IS DESIGNATED FOR BATCH PROCESSING
*
*        OPERATION:
*              SAVE REGISTERS AND ESTABLISH ADDRESSABILITY
*              GETMAIN DYNAMIC STORAGE
*              OBTAIN SYSID
*              PERFORM RACHECK FOR APPL OF SYSID
*              SET RETURN CODE
*              FREEMAIN DYNAMIC STORAGE
*              RESTORE REGISTERS
*              RETURN
*
*        MACROS: SAVE, RETURN, EQUATE, GETMAIN, FREEMAIN, RACHECK
*
*        MAPPING MACROS: CVT, IEESMCA
*
*        ASSEMBLER OPTIONS: LIST,RENT
*
*        LINKAGE EDITOR OPTIONS: LET,LIST,XREF,RENT,REUS,REFR,AC=1
*              (RESIDES IN SYS1.PPLINK)
*
*        CHANGE ACTIVITY: 12/10/86  WRITTEN                      JWC
*
***********************************************************************
***********************************************************************
*        INITIALIZATION
***********************************************************************
DLXTSO   CSECT
         SAVE  (14,12),,DLXTSO_DELUXE_CHECK_&SYSDATE_&SYSTIME
*                                      SAVE REGISTERS
         LR    R12,R15                 R12 - BASE REGISTER
         USING DLXTSO,R12              ESTABLISH PGM ADDRESSABILITY
         GETMAIN R,LV=WORKLEN
*                                      GETMAIN DYNAMIC STORAGE
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
*                                      IN CURRENT SAVEAREA
         ST    R1,8(R13)               STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  R13 - ADDRESS OF GETMAINED AREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*        OBTAIN SYSTEM ID
***********************************************************************
SYSID    DS    0H
         L     R2,CVTPTR               R2 - ADDRESS OF CVT
         USING CVT,R2                  ESTABLISH ADDRESSABLITY TO CVT
         L     R2,CVTSMCA              R2 - ADDRESS OF SMCA
         USING SMCABASE,R2             ESTABLISH ADDRESSABLITY TO SMCA
         LA    R3,SMCASID              R3 - ADDRESS OF 4 CHAR SYSID
***********************************************************************
*        PERFORM RACHECK FOR CLASS OF 'APPL'
***********************************************************************
RACHECK  DS    0H
         MVC   RACLIST(INITLEN),INITDATA    STATIC INFO TO DYNAMIC FORM
         RACHECK ENTITY=((R3)),CLASS=APPL,LOG=ASIS,                    +
               MF=(E,RACLIST)          RACHECK FOR CLASS = 'APPL'
***********************************************************************
*        FREEMAIN DYNAMIC STORAGE, FREE REGISTERS AND RETURN
***********************************************************************
FREEMAIN DS    0H
         LR    R2,R15                  R2 - SAVE RACHECK RETURN CODE
         LR    R3,R13                  R3 - ADDRESS OF DYNAMIC WORKAREA
         L     R13,4(R13)              RESTORE REGISTER 13
         FREEMAIN R,LV=WORKLEN,A=(R3)
*                                      FREEMAIN DYNAMIC STORAGE
         LR    R15,R2                  R15 - RESTOR RACHECK RETURN CODE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
***********************************************************************
*        CONSTANTS
***********************************************************************
APPL     DS    0H
         DC    AL1(4)                  LENGTH OF CLASSNAME
         DC    C'APPL'                 CLASSNAME FOR RACHECK MACRO
***********************************************************************
*        INITIATIALIZATION DATA FOR PORTION OF GETMAIN'D AREA
***********************************************************************
INITDATA RACHECK MF=L                  LIST FORM OF RACHECK MACRO
INITLEN  EQU   *-INITDATA
***********************************************************************
*        DSECT FOR COMMUNICATIONS VECTOR TABLE
***********************************************************************
         CVT DSECT=YES
         EJECT
DLXTSO   CSECT
***********************************************************************
*        DSECT FOR SMF CONTROL AREA
***********************************************************************
         IEESMCA
         EJECT
DLXTSO   CSECT
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         EQUATE
         LTORG
***********************************************************************
*        DYNAMIC STORAGE AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     DLXTSO SAVE AREA
RACLIST  DS    CL(INITLEN)             LIST FORM OF RACHECK MACRO
WORKLEN  EQU   *-WORKAREA
         END   DLXTSO
