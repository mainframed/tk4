         TITLE 'SMF -  I E F U 2 9   EXIT'
* DESCRIPTION: STANDARD SMF EXIT WHICH GETS CONTROL WHEN THE
*              THE SYSTEM DETECTS 'IEE362A' MESSAGE WHEN A
*              SWITCH OF THE RECORDING DATASETS HAS OCCURRED.
*              WE ISSUE A COMMAND TO START AN IFASMFDP JOB
*              BASED ON WHICH SYSID (ADM1 OR ADM2) ISSUED THE
*              MESSAGE AND WHICH SMF DATASET (MAN1,MAN2 OR MAN3)
*              NEEDS TO BE DUMPED.
*
*
*         REGISTER USAGE
*             R0  - STANDARD
*             R1  - STANDARD
*             R2  - WORK TO GAIN ACCESSIBILITY TO VARIOUS CSECTS
*             R3  - WORK
*             R4  - NOT USED
*             R5  - NOT USED
*             R6  - NOT USED
*             R7  - NOT USED
*             R8  - NOT USED
*             R9  - NOT USED
*             R10 - NOT USED
*             R11 - NOT USED
*             R12 - BASE REGISTER
*             R13 - BASE REGISTER FOR WKAREA DSECT
*             R14 - STANDARD
*             R15 - STANDARD
*
*     PROGRAM LINK-EDIT VALUES
*      AC=1,RENT,REFR,REUS -- SYS1.LPALIB
*
*    NOTES   : REGISTER 1 POINTS TO FULLWORD ADDRESS OF DATASET
*              NAME
*
*              SVC 34 IS USED TO ISSUE THE INTERNAL START COMMAND
*
*-----------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
IEFU29   CSECT
         SAVE  (14,12),,IEFU29_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15                  SAVE ENTRY ADDRESS
         USING IEFU29,R12               ADDRESSABILITY WITH NEW BASE
         L     R3,0(R1)                 GET ADDRESSABILITY FOR
         USING PLIST,R3                 SMF DATASET NAME
         L     R0,GETAREA               SETUP FOR GETMAIN
         GETMAIN R,LV=(0)
         ST    R13,4(R1)                STOR ADDRESS OF OLD SAVE IN NEW
         ST    R1,8(R13)                STOR ADDRESS OF NEW SAVE IN OLD
         LR    R13,R1                   R13 NOW POINTS TO NEW SAVEAREA
         USING WKAREA,R13               GAIN ADDRESSABILITY TO DSECT
         SPACE
         L     R2,CVTPTR               R2 - ADDRESS OF CVT
         USING CVT,R2                  ESTABLISH ADDRESSABILITY TO CVT
         L     R2,CVTSMCA              R2 - ADDRESS OF SMCA
         USING SMCABASE,R2             ESTABLISH ADDRESSABILITY TO SMCA
         LA    R2,SMCASID              R2 - ADDRESS OF 4 CHAR SYSID
         DROP  R2                      DROP SMCA ADDRESSABILITY
         MVC   CMDAREA(4),=XL4'00180000' SET FLAG1,LENGTH AND FLAG2
         MVC   COMMAND(L'STARTCMD),STARTCMD MOVE IN COMMAND MODEL
         MVC   MAN(1),SMFDSN+8          MOVE IN WHICH MAN DATASET
         MVC   SYSID(1),3(R2)          MOVE IN 4TH CHAR OF SYSID
         XR    R0,R0                    WANT TO DO OPER CMD
         LA    R1,CMDAREA               POINT TO THE CMD
         SVC   34                       ISSUE THE CMD
         SPACE
RETURN   EQU   *
*******************************************************************
*                                       WITH CANCEL MESSAGE
JOBOK    EQU    *
         LR     R1,R13                  R1 NOW HAS ADDR OF GOTTEN AREA
         L      R13,4(R13)              R13 PTS TO PREVIOUS SAVEAREA
         L      R0,GETAREA              SETUP FOR FREEMAIN
         FREEMAIN R,LV=(0),A=(1)
         LA    15,0
         RETURN (14,12),RC=(15)         RESTORE REGISTERS AND RETURN
         EJECT
*---------------------------------------------------------------------*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
         SPACE
GETAREA  DC    0F'0',AL1(000),AL3(WKAREAL) SP=0 AND LENGTH FOR GETMAIN
         SPACE
STARTCMD DC    CL20'START SMFDUMPX,MAN= '
*--------------------------------------------------------------------*
*        DEFINE DSECT                                                *
*--------------------------------------------------------------------*
         SPACE
         LTORG
PLIST    DSECT
SMFDSN   DS    CL10                     SMF 10 BYTE DATASET NAME
WKAREA   DSECT
SAVEAREA DS    18F
         CNOP  0,4
CMDAREA  DS    XL4
COMMAND  DS    XL19
MAN      DS    X
         ORG   COMMAND+13
SYSID    DS    X
         ORG
WKAREAL  EQU   *-WKAREA
         CVT   DSECT=YES
         IEESMCA
         END
