****************************** SMFSEL *********************************
*                                                                     *
*  THIS PROGRAM COPIES ONE SMF HISTORY TAPE TO ANOTHER BUT ALLOWS     *
*  SELECTION  BY DATE (JULIAN) AND TIME (MILITARY 24 HR CLOCK) .      *
*  IT ALSO WILL ALLOW REBLOCKING OF THE DATA.                         *
*  DDNAMES:    SYSUT1 - INPUT SMF FILE                                *
*              SYSUT2 - OUTPUT SMF FILE                               *
*               SYSIN - CONTROL STATEMENTS                            *
*            SYSPRINT - MESSAGE DATA SET                              *
*                                                                     *
* THERE MUST BE 2 CONTROL CARDS WITH THE FOLLOWING FORMAT:            *
*  CARD1 IS THE DATE SELECTION CARD AND CARD2 IS THE TIME SELECTION   *
*        CARD.  ALL DATES AND TIMES ARE INCLUSIVE.                    *
*  COLS   ----+----1----+----2----+----3                              *
*  CARD1  YYDDD,YYDDD                                                 *
*   THE FIRST DATE IS THE BEGINNING DATE AND THE SECOND IS THE ENDING *
*   DATE. THE COMMA IS REQUIRED.                                      *
*  CARD2  HHMMSS,HHMMSS                                               *
*   THE FIRST TIME IS THE BEGINNING TIME AND THE SECOND IS THE ENDING *
*   TIME. THE COMMA IS REQUIRED.                                      *
*                                                                     *
*   FOR EXAMPLE IF YOU WISHED TO SELECT ALL SMF RECORDS BETWEEN       *
*   9:35 AM AND 1:40 PM ON JANUARY 4TH THRU 7TH 1981 YOU WOULD CODE:  *
*                                                                     *
*  COLS   ----+----1----+----2----+----3                              *
*  CARD1  81004,81007                                                 *
*  CARD2  093500,134000                                               *
*                                                                     *
*  YOU MAY SELECT ALL TIMES OR ALL DATES BY CODEING "ALL" IN          *
*  COLUMNS 1 - 3 OF EITHER THE DATE CARD OR THE TIME CARD.            *
*  CODEING "ALL" ON BOTH CARDS GIVES A FULL COPY.                     *
*                                                                     *
***********************************************************************
         TITLE 'SMFSEL - SMF RECORD DATE AND TIME SELECTIVE COPY '
SMFSEL   CSECT
         STM   R14,R12,12(R13)
         LR    R11,R15
         USING SMFSEL,R11
         ST    R13,SAVEAREA+4
         LR    R6,R13
         LA    R13,SAVEAREA
         ST    R13,8(R6)
* END OF PROLOUGE
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         MVI   CTLCHAR,C'1'
         OPEN  (SMFINT,,SMFOUT,(OUTPUT),CNTRLIN,,CNTRLOUT,(OUTPUT))
         USING  SMFRCD0,R5
         LA    R9,SMFINT
         USING IHADCB,R9
         SR    R6,R6
         SR    R7,R7
         PUT   SMFOUT
         ST    R1,SAVE1
         GET   CNTRLIN,INREC
         MVC   OUTREC+1(80),INREC
         PUT   CNTRLOUT,OUTREC
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         CLC   INREC(3),=CL3'ALL'
         BNE   NOTALLD
         MVI   ALLDATES,C'1'
         B     NEXTCARD
NOTALLD  PACK  DATEHI(4),INREC+6(5)
         PACK  DATELOW(4),INREC(5)
         OI    DATEHI+3,X'0F'
         OI    DATELOW+3,X'0F'
         CLI   INREC+5,C','
         BNE   BADCARD
NEXTCARD GET   CNTRLIN,INREC
         MVC   OUTREC+1(80),INREC
         PUT   CNTRLOUT,OUTREC
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         CLC   INREC(3),=CL3'ALL'
         BNE   NOTALLT
         MVI   ALLTIMES,C'1'
         B     COPYSMF
NOTALLT  EQU   *
         CLI   INREC+6,C','
         BNE   BADCARD
         MVC   TIMEHOLD(6),INREC
         BAL   R10,TIMEB
         MVC   TIMELOW,TIME
         MVC   TIMEHOLD(6),INREC+7
         BAL   R10,TIMEB
         MVC   TIMEHI,TIME
COPYSMF  GET   SMFINT
         LR    R5,R1   SAVE BUFFER ADDRESS
         A     R6,=F'1'
         CLI   ALLDATES,C'1'
         BE    CHECKT
         CLC   SMF0DTE,DATELOW
         BL    COPYSMF
         CLC   SMF0DTE,DATEHI
         BH    COPYSMF
CHECKT   CLI   ALLTIMES,C'1'
         BE    WRITEIT
         CLC   SMF0TME,TIMELOW
         BL    COPYSMF
         CLC   SMF0TME,TIMEHI
         BH    COPYSMF
WRITEIT  EQU   *
         L     R4,SAVE1                 GET ADDRESS OF OUTPUT BUFFER
         LR    R2,R5                    GET ADDRESS OF INPUT BUFFER
         SR    R3,R3                    CLEAR R3
         SR    R5,R5                            AND R5 (COUNT FIELD)
         LH    R3,DCBLRECL              LOAD COUNT IN R3
         LR    R5,R3                                     AND R5
         LA    R5,0(R5)                 ZERO HI ORDER BIT
         MVCL  R4,R2                    MOVE 0(R4,R5),0(R2,R3)
*                                       MVC  D1(L1,B1),D2(L2,B2)
         L     R1,SAVE1                 RESTORE R1
         PUT   SMFOUT
         ST    R1,SAVE1
         A     R7,=F'1'
         B     COPYSMF
CLOSEIT  EQU   *
         CLOSE (SMFINT,,SMFOUT,,CNTRLIN,,CNTRLOUT)
* BEGINNING OF EPILOUGE
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         RETURN RC=0
* END OF EPILOUGE
BADCARD  EQU   *
         MVC   OUTREC+1(L'BADMSG),BADMSG
         PUT   CNTRLOUT,OUTREC
         B     CLOSEIT
EOF      EQU   *
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         CVD   R6,WORK
         UNPK  WORK2,WORK+4(4)
         OI    WORK2+7,X'F0'
         MVC   RECIN,WORK2+1
         MVC   OUTREC+1(32),RECINMSG
         PUT   CNTRLOUT,OUTREC
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         CVD   R7,WORK
         UNPK  WORK2,WORK+4(4)
         OI    WORK2+7,X'F0'
         MVC   RECOUT,WORK2+1
         MVC   OUTREC+1(35),RECOTMSG
         PUT   CNTRLOUT,OUTREC
         B     CLOSEIT
CARDEOF  MVC   OUTREC+1(L'CARDMSG),CARDMSG
         PUT   CNTRLOUT,OUTREC
         B     CLOSEIT
TIMEB    EQU   *                      CONVERT TIME HHMMSS TO BINARY
*                                     BINARY VALUE IN 100THS OF SECONDS
         PACK  WORK,TIMEHOLD(2)       PACK HOURS
         MP    WORK,=P'3600'          CONVERT HOURS TO SECONDS
         ZAP   WORK2,WORK             ZERO AND ADD TO WORK2
         PACK  WORK,TIMEHOLD+2(2)     PACK MINUTES
         MP    WORK,=P'60'            CONVERT MINUTES TO SECONDS
         AP    WORK2,WORK             ADD TO WORK2
         PACK  WORK,TIMEHOLD+4(2)     PACK SECONDS
         AP    WORK2,WORK             ADD TO WORK2
         MP    WORK2,=P'100'          CONVERT SECONDS TO 100THS OF SECS
         CVB   R8,WORK2               CONVERT TO BINARY
         ST    R8,TIME                STORE BINARY VALUE IN FULLWORD
         BR    R10                    RETURN
SAVEAREA DC    18F'0'
SAVE1    DS    F
WORK     DS    D
WORK2    DS    D
DATE     DS    F
TIME     DS    F
TIMEHOLD DS    CL6
INREC    DS    CL80
CARDMSG  DC    C'PREMATURE EOF ON SYSIN'
BADMSG   DC    C'ABOVE CONTROL CARD IN ERROR'
RECINMSG DS    0CL32
         DC    C'NUMBER OF RECORDS READ = '
RECIN    DC    CL7' '
RECOTMSG DS    0CL35
         DC    C'NUMBER OF RECORDS WRITTEN = '
RECOUT   DC    CL7' '
OUTREC   DS    0CL133
CTLCHAR  DS    CL1
PRINTREC DS    CL132
ALLDATES DC    CL1'0'
ALLTIMES DC    CL1'0'
TIMELOW  DS    F
TIMEHI   DS    F
DATELOW  DS    F
DATEHI   DS    F
         LTORG
         PRINT NOGEN
SMFINT   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=EOF,BFTEK=A
SMFOUT   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PL,BFTEK=A
CNTRLIN  DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,LRECL=80,BLKSIZE=80,     X
               RECFM=FB,EODAD=CARDEOF
CNTRLOUT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA,  X
               BLKSIZE=133
         DCBD  DSORG=LR
RECDEF   DSECT
RECS     IFASMFR (0)
         EQUATE
         END   SMFSEL
