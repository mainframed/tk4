         TITLE 'DELUXE JES2 COMMAND SCREEN USER EXIT - PREAMBLE'
**********************************************************************
*                                                                    *
*           DELUXE JES2 CMD SCREEN USER EXIT 255                     *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE WILL SET AUTHORIZATION     *
*       FOR S XXXRN,N=PROCNAME COMMANDS THAT ARE                     *
*       COMING FROM RN AND DIRECTED AT ADMIN TO ALLOW. THIS          *
*       WILL BE DONE BY ANDING THE CMBFLAG FIELD WITH X'F0' WHICH    *
*       WILL ALLOW ALL COMMANDS.  IN ALL OTHER CASES THE EXIT WILL   *
*       SIMPLY RETURN AND ALLOW JES2 TO DO THE AUTHORITY CHECKING.   *
*                                                                    *
*    ENTRY POINT = DLXUX255                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0-3  N/A                                                    *
*       R4    ADDRESS OF THE CMB                                     *
*       R5-10 N/A                                                    *
*       R11   ADDRESS OF THE JES2 HCT                                *
*       R12   N/A                                                    *
*       R13   N/A                                                    *
*       R14   RETURN ADDRESS                                         *
*       R15   ENTRY ADDRESS                                          *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             =0  CONTINUE WITH NORMAL $WTO PROCESSING               *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $SAVE, $RETURN, $MODEND, $HASPGBL                   *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
DLXCMDUX $MODULE $BUFFER,                                              +
               $CAT,                    REQ'D BY HCT                   +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $CMB,                    CONSOLE MESSAGE BUFFER MAPPING +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D BY $CAT                  +
               $JQE,                    REQ'D BY HCT                   +
               $MIT,                    REQ'D BY $MODEND               +
               $PCE                     REQ'D BY HCT                   +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE JES2 COMMAND SCREEN USER EXIT '
         SPACE 1
DLXUX255 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         USING CMB,R4                   ADDRESSABILITY TO CMB
         SLR   R15,R15
         TM    CMBFLAG,CMBFLAGC         IS IT A COMMAND?
         BNO   RETURN                   NO QUIT
         CLC   CMBFMNOD,=H'1'           IS IT ORIGINATING AT ADMIN?
         BE    RETURN                   ALLOW IT
         CLC   CMBFMNOD,=H'10'          IS IT ORIGINATING AT ELOIGNE?
         BE    RETURN                   ALLOW IT
         CLC   CMBTONOD,=H'1'           IS IT DESTINED FOR ADMIN?
         BE    NOTADM1                  CONTINUE CHECKING
         CLC   CMBTONOD,=H'10'          IS IT DESTINED FOR ELOIGNE?
         BE    NOTADM1                  CONTINUE CHECKING
         BNE   RETURN                   LET JES2 FIGURE IT OUT
NOTADM1  EQU   *
         CLC   CMBMSG(2),=CL2'S '       IS IT A START COMMAND?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
         CLI   CMBMSG+5,C'R'            IS IT A REGIONAL STC?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
         TM    CMBMSG+6,X'F0'           IS IT NUMERIC?
         BNO   RETURN                   GREATER THAN 8; LET JES DO IT
         CLC   CMBMSG+7(3),=CL3',N='    IS IT THE CORRECT FORMAT?
         BNE   RETURN                   LET JES2 FIGURE IT OUT
         CLI   CMBFMNOD+1,X'02'         IS IT NODE 2?
         BE    CK8                      NODE 2 IS REGION 8
         CLI   CMBFMNOD+1,X'03'         IS IT NODE 3?
         BE    CK2                      NODE 3 IS REGION 2
         CLI   CMBFMNOD+1,X'04'         IS IT NODE 4?
         BE    CK4                      NODE 4 IS REGION 4
         CLI   CMBFMNOD+1,X'05'         IS IT NODE 5?
         BE    CK1                      NODE 5 IS REGION 1
         CLI   CMBFMNOD+1,X'06'         IS IT NODE 6?
         BE    CK5                      NODE 6 IS REGION 5
         CLI   CMBFMNOD+1,X'07'         IS IT NODE 7?
         BE    CK7                      NODE 7 IS REGION 7
         CLI   CMBFMNOD+1,X'08'         IS IT NODE 8?
         BE    CK6                      NODE 8 IS REGION 6
         CLI   CMBFMNOD+1,X'09'         IS IT NODE 9?
         BE    CK3                      NODE 9 IS REGION 3
         B     RETURN                   GET OUT
CK1      EQU   *                        REGION 1 CHECK
         CLI   CMBMSG+6,C'1'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK2      EQU   *                        REGION 2 CHECK
         CLI   CMBMSG+6,C'2'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK3      EQU   *                        REGION 3 CHECK
         CLI   CMBMSG+6,C'3'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK4      EQU   *                        REGION 4 CHECK
         CLI   CMBMSG+6,C'4'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK5      EQU   *                        REGION 5 CHECK
         CLI   CMBMSG+6,C'5'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK6      EQU   *                        REGION 6 CHECK
         CLI   CMBMSG+6,C'6'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK7      EQU   *                        REGION 7 CHECK
         CLI   CMBMSG+6,C'7'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
CK8      EQU   *                        REGION 8 CHECK
         CLI   CMBMSG+6,C'8'            IS THIS FOR THEMSELVES?
         BE    OKIT                     YES ALLOW IT
         B     RETURN                   OTHERWISE JES2 WILL NIX IT
OKIT     EQU   *
         NI    CMBFLAG,X'F0'            SAY IT IS OK TO ISSUE COMMAND
RETURN   EQU   *
         $RETURN RC=(R15)               RETURN TO CALLER
         SPACE 1
         LTORG
         $MODEND
         END  ,
