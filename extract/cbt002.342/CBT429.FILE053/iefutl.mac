         TITLE 'IEFUTL - DELUXE SMF USET TIME LIMIT EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  DELUXE SMF USER TIME LIMIT EXIT     *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW TSO SESSIONS WHICH ARE EXECUTING UNDER A       *
*                   MULTIPLE SESSION MANAGER PRODUCT TO NOT TIME      *
*                   OUT FOR WAIT TIME EXCESSION.                      *
*                                                                     *
*                OPERATION =                                          *
*                   ANALYZE SMF COMMON EXIT PARAMETER AREA.           *
*                   IF WAIT TIME LIMIT EXCEEDED SEE IF IT IS A TSO    *
*                      SESSION, IF NOT CANCEL THE SESSION.  IF IT IS  *
*                      A TSO SESSION CHECK TO SEE IF THE TERMINAL     *
*                      NAME FROM THE TSB BEGINS WITH THE CHARACTERS   *
*                      "DLX". IF IT DOES NOT THEN CANCEL THE SESSION, *
*                      OTHERWISE GRANT A 10 MINUTE EXTENSION,         *
*                      INDEFINITELY.                                  *
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 2  = WORK REGISTERS                *
*                   REGISTERS 3 TO 9  = UNUSED                        *
*                   REGISTER  10      = WORK                          *
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA           *
*                   REGISTER  12      = ADDRESSABILITY TO IEFUTL      *
*                                       CSECT                         *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPL DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                USER COMMUNICATION FIELD - EXTENSION COUNT           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             MACROS = SAVE, RETURN                                   *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                                                                     *
*      05/28/87  CHANGED EXTENSION TIME TO 50 MINUTES.  ALSO ADDED    *
*                CODE TO CHECK FOR AND ALLOW ONLY ONE EXTENSION.  MJL *
*                                                                     *
*             MESSAGES = NONE                                         *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL_&SYSTIME_&SYSDATE
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
*
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,0(R1)               LOAD POINTER TO PARAMETER AREA
         USING SMFCEPL,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R0,FULL8                SEE IF WAIT TIME  EXCEEDED
         BNE   CANCEL                  NO.....GO CANCEL
*                                      TIME EXCEEDED
*
***********************************************************************
*                                                                     *
*        WAIT TIME EXCEEDED                                           *
*           - IF NOT TSO ALLOW SESSION TO BE CANCELED                 *
*                                                                     *
***********************************************************************
*
WAITTIME DS    0H
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   CANCEL                  NO.....GO CANCEL - ONLY EXTEND
*                                      WAIT TIME FOR TSO SESSIONS
*
         CLI   SMFUSERW,X'00'          CHECK TO SEE IF AN EXTENSION HAS
         BNE   CANCEL                  ALREADY BEEN GRANTED, IF SO
*                                      CANCEL ELSE CONTINUE CHECKING..
         L     R10,16                  GET ADDRESS OF CVT
         L     R10,0(R10)              GET ADDR OF TCB ADDRESS WORD
         L     R10,12(R10)             GET ADDRESS OF CURRENT ASCB
         USING ASCB,R10                ESTABLISH ADDRESSABILITY TO ASCB
         L     R10,ASCBTSB             LOAD ADDRESS OF TSB
         USING TSB,R10                 ESTABLISH ADDRESSABILITY TO TSB
         CLC   DLXTERM,TSBTRMID        COMPARE ON TERMINAL NAME
         BNE   CANCEL                  IF NOT "DLX" THEN CANCEL
         DROP  R10
***********************************************************************
*                                                                     *
*        ALLOW JOB A 10 MINUTE EXTENSION AND NOTIFY OPERATOR          *
*                                                                     *
***********************************************************************
*
         L     R15,FULL8               INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,WAIT3000             LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         MVI   SMFUSERW,X'01'          INDICATE ONE EXTENSION GRANTED
         B     RETURN                  GO RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         SLR   R15,R15                 INDICATE RETURN CODE OF ZERO -
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         L     R14,12(,R13)            RESTORE RETURN ADDRESS
         LM    R2,R12,28(R13)          RESTORE REGS 2-12
         BR    R14                     RETURN TO CALLER
         SPACE 5
**************************** CONSTANTS ********************************
FULL8    DC    F'8'
WAIT3000 DC    F'3000'
DLXTERM  DC    CL3'DLX'
         EJECT
************************ DSECTS (MAPPING MACROS) **********************
SMFCEPL  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 5
********************************* EQUATES *****************************
         SPACE 5
***************************** REGISTER EQUATES ************************
         EQUATE
         IHAASCB  LIST=YES
         IKJTSB   LIST=YES
         CVT   DSECT=YES,LIST=YES
         END   IEFUTL
