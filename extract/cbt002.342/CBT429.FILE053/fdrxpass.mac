         TITLE 'FDRXPASS - FDR DATA SET SECURITY EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = FDRXPASS                                  *
*                                                                     *
*             DESCRIPTIVE NAME = FDR DATA SET SECURITY EXIT           *
*                                                                     *
*             FUNCTION -                                              *
*                EXIT KEEPS USERS FROM:                               *
*                  - RESTORING DATA SETS THEY DO NOT HAVE 'ALTER'     *
*                    ACCESS TO, UNLESS THEY HAVE READ ACCESS TO APPL  *
*                    'JCLMAINT'. IF SO, THEY WILL BE ALLOWED TO       *
*                    USE 'FDRABRUT' TO RESTORE DATA SETS THEY DO      *
*                    NOT HAVE 'ALTER' ACCESS TO. DATA SETS RESTORED   *
*                    MAY NOT BE RENAMED, EXCEPT TO CHANGE THE HIGH    *
*                    LEVEL QUALIFIER.                                 *
*                  - PERFORMING ABSOLUTE TRACK OPERATIONS WITHOUT     *
*                    PROPER AUTHORITY.                                *
*                  - EXECUTING 'FDR' TO DO A FULL VOLUME DUMP OR      *
*                    RESTORE WITHOUT PROPER AUTHORITY.                *
*                  - EXECUTING THE 'FDRABRM' MAINTENANCE UTILITY      *
*                    WITHOUT PROPER AUTHORITY.                        *
*                  - RESTORING DATA SETS WHICH THEY DO NOT HAVE       *
*                    'ALTER' ACCESS TO.                               *
*                  - RESTORING DATA SETS TO ANOTHER DATA SET NAME     *
*                    UNLESS:                                          *
*                      - THERE IS A GENERIC PROFILE FOR THE NEW       *
*                        DATA SET NAME.                               *
*                      - THEY HAVE 'ALTER' AUTHORITY TO THE NEW       *
*                        DATA SET NAME.                               *
*                      - THEY HAVE AT LEAST 'READ' ACCESS TO THE      *
*                        OLD DATA SET.                                *
*                  - ARCHIVING OR SCRATCHING DATA SETS UNLESS THEY    *
*                    HAVE 'ALTER' ACCESS TO THESE DATA SET NAMES.     *
*                  - DUMPING DATA SETS USING 'FDRDSF' UNLESS THEY     *
*                    HAVE AT LEAST 'READ' ACCESS TO THESE DATA        *
*                    SET NAMES.                                       *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                OPERATION =                                          *
*                   SAVE REGISTERS AND ESTABLISH ADDRESSABILITY       *
*                   INITIALY, ACCEPT THE OPERATION                    *
*                   GETMAIN DYNAMIC STORAGE, IF NEEDED                *
*                   FOR REMOTE QUEUE OPERATIONS USING FDRABRUT:       *
*                     - IF USER HAS 'READ' ACCESS TO THE AP-          *
*                       PLICATION 'JCLMAINT', ALLOW HIM TO            *
*                       RESTORE ANY DATASET WITHOUT RENAMING IT       *
*                       (EXCEPT TO CHANGE HIGH LEVEL QUALIFIER).      *
*                   FOR ABSOLUTE TRACK OPERATIONS USING DSF:          *
*                     - IF USER DOES NOT HAVE AT LEAST 'READ'         *
*                       ACCESS TO THE APPLICATION, 'FDRABSTK',        *
*                       TERMINATE THE VOLUME FOR PROCESSING           *
*                   FOR FULL VOLUME DUMP OR RESTORE USING FDR:        *
*                     - IF USER DOES NOT HAVE AT LEAST 'READ'         *
*                       ACCESS TO THE APPLICATION, 'FDR',             *
*                       TERMINATE THE VOLUME FOR PROCESSING           *
*                   FOR MAINTENANCE OPERATIONS:                       *
*                     - IF USER DOES NOT HAVE AT LEAST 'READ'         *
*                       ACCESS TO THE APPLICATION, 'FDRABRM',         *
*                       TERMINATE THE VOLUME FOR PROCESSING           *
*                   FOR RESTORE OPERATIONS WITH DSN RENAME:           *
*                     - IF NO GENERIC PROFILE EXISTS FOR NEW          *
*                       DSNAME, REJECT DSN FOR RESTORE                *
*                     - IF USER DOES NOT HAVE 'ALTER' ACCESS          *
*                       TO THE NEW DSNAME, REJECT THE DSNAME          *
*                       FOR RESTORE                                   *
*                     - IF USER DOES NOT HAVE AT LEAST 'READ'         *
*                       ACCESS TO THE OLD DSNAME, REJECT THE          *
*                       DSNAME FOR RESTORE                            *
*                   FOR RESTORE OPERATIONS WITHOUT DSN RENAME:        *
*                     - IF USER DOES NOT HAVE 'ALTER' AUTHORITY       *
*                       TO THE OLD DSNAME, REJECT THE DSNAME FOR      *
*                       RESTORE                                       *
*                   FOR SCRATCH AND ARCHIVE OPERATIONS:               *
*                     - IF USER DOES NOT HAVE 'ALTER' AUTHORITY       *
*                       TO THE DSNAME, REJECT THE DSNAME FOR THESE    *
*                       OPERATIONS                                    *
*                   FOR DUMP OPERATIONS USING FDRDSF:                 *
*                     - IF USER DOES NOT HAVE 'READ' ACCESS TO THE    *
*                       DSNAME, REJECT THE DSNAME FOR DSF DUMP        *
*                   FREEMAIN DYNAMIC STORAGE, IF NEEDED               *
*                   RESTORE REGISTERS                                 *
*                   RETURN                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTER  1       = PARAMETER LIST POINTER       *
*                    REGISTERS 2       = PARAMETER LIST WORK POINTER  *
*                    REGISTER  3       = PTR - DSNAME/APPLNAME (RACF) *
*                    REGISTER  4       = ACCESS AUTHORITY (RACHECK)   *
*                    REGISTER  5 - 10  = WORK REGISTERS               *
*                    REGISTER  11      = BAL REGISTER                 *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA POINTER            *
*                    REGISTER  14      = RETURN                       *
*                    REGISTER  15      = ENTRY POINT ADDRESS AND      *
*                                        RETURN CODE REGISTER         *
*                                                                     *
*             MACROS =                                                *
*                SAVE, RETURN, EQUATE, GETMAIN, FREEMAIN, RACHECK     *
*                                                                     *
*             MAPPING MACROS USED =                                   *
*                IECSDSL1                                             *
*                                                                     *
*             ASSEMBLER OPTIONS = LIST,RENT                           *
*                                                                     *
*             LINKAGE EDITOR OPTIONS = LET,LIST,XREF,RENT,REUS,REFR   *
*                                      AC=1                           *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                09/24/85  WRITTEN.      JWC.                         *
*                                                                     *
*                12/18/85  CHANGED TO INITIALIZE LIST FORM OF         *
*                          RACHECK MACRO PRIOR TO EACH INVOCATION.    *
*                                        JWC.                         *
*                                                                     *
***********************************************************************
         SPACE
***********************************************************************
*        INITIALIZATION
***********************************************************************
FDRXPASS CSECT                         FDR DATA SET SECURITY EXIT
         SAVE  (14,12),,FDRXPASS_DELUXE_CHECK_&SYSDATE_&SYSTIME
*                                      SAVE REGISTERS
         LR    R12,R15                 R12 - BASE REGISTER
         USING FDRXPASS,R12            ESTABLISH PGM ADDRESSABILITY
         LR    R2,R1                   PRESERVE PTR TO PARMS
         USING FDRPARM,R2              ESTABLISH ADDRESSABILITY TO
*                                      FDR PARAMETER LIST
         MVC   COMPCOD,ACCEPT          INIT, ACCEPT DSN FOR OPERATION
         EJECT
***********************************************************************
*        GETMAIN DYNAMIC STORAGE AND/OR ESTABLISH ADDRESSABILITY
***********************************************************************
GETMAIN  DS    0H
         CLC   REENT,FULL00            WORK ADDRESS IN PARAMETER LIST?
         BNE   SKIPGET                 YES, DON'T BOTHER WITH GETMAIN
         GETMAIN R,LV=WORKLEN
*                                      GETMAIN DYNAMIC STORAGE
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
*                                      IN CURRENT SAVEAREA
         ST    R1,8(R13)               STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         ST    R1,REENT                STORE ADDR OF DYNAMIC WORKAREA
*                                      IN RE-ENTRANT ADDR POINTER
         USING WORKAREA,R1             ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         DROP  R1                      FREE REG 1
SKIPGET  DS    0H
         L     R13,REENT               LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         CLC   OPMODE,EXITMODE         FINISHED WITH THIS OPERATION?
         BE    FREEMAIN                YES, PROCEED WITH FREEMAIN
         USING WORKAREA,R13            NO, ESTABLISH ADDRESSABILITY
*                                      TO DYNAMIC WORKAREA
         CLC   OPTYPE,QUEUTYPE         THIS A REMOTE QUEUE OPERATION?
         BNE   CONTINUE                NO, CONTINUE REGULAR CHECKING
         BAL   R11,QUEUEOP             YES, SET UP FOR JCLMAINT RACHECK
         B     FREEMAIN                FREE STORAGE AND RETURN
CONTINUE DS    0H
         CLC   OPTIONS,ABSOLUTE        THIS AN ABSOLUTE TRK OPERATION?
         BE    TRACKOP                 YES, SET UP FOR FDRABSTK RACHECK
         CLC   OPTYPE,FDRTYPE          THIS A FULL VOLUME OPERATION?
         BE    FDROP                   YES, SET UP FOR FDR RACHECK
         CLC   OPTYPE,MAINTYPE         THIS A MAINTENANCE OPERATION?
         BE    MAINTOP                 YES, SET UP FOR MAINT RACHECK
         CLC   OPMODE,RESTMODE         IS THIS A RESTORE OPERATION?
         BE    RESTOP                  YES, SET UP FOR RESTORE RACHECK
         CLC   OPTIONS,ARCHIVE         IS THIS AN ARCHIVE OPERATION?
         BE    ARCSCROP                YES, SET UP FOR ARCHIVE RACHECK
         CLC   OPTIONS,SCRATCH         IS THIS A SCRATCH OPERATION?
         BE    ARCSCROP                YES, SET UP FOR SCRATCH RACHECK
         CLC   OPTYPE(3),DSFDUMP       IS THIS A DSF DUMP OPERATION?
         BE    DSFDMPOP                YES, SET UP FOR DSF DUMP RECHECK
         B     FREEMAIN                FREE STORAGE (JUST IN CASE)
         EJECT
***********************************************************************
*        SET UP FOR REMOTE QUEUE OPERATION (CLASS OF 'APPL')
***********************************************************************
QUEUEOP  DS    0H
         MVC   RACLIST(INITLEN),INITDATA    STATIC INFO TO DYNAMIC FORM
         MVC   REJCTCOD,REJECT         VARIBLE REJECT CODE = 'R'
         LA    R3,JCLMAINT             R3 - ADDRESS OF 'JCLMAINT' APPL
         RACHECK ENTITY=((R3)),CLASS=APPL,LOG=NOSTAT,                  +
               MF=(E,RACLIST)          RACHECK FOR CLASS = 'APPL'
         LTR   R15,R15                 USER HAVE PROPER AUTHORITY?
         BNZ   CONTINUE                NO, RETURN AND CONTINUE CHECKING
         CLC   OPTIONS,DFEFRNAM        VSAM DFEF CLUSTER RESTORE
*                                      WITH NEWNAME
         BE    RENAME0                 YES, SET UP TO CONTINUE CHECKING
         CLC   OPTIONS,DSNRNAME        DATASET RESTORE
*                                      WITH NEWNAME
         BE    RENAME0                 YES, SET UP TO CONTINUE CHECKING
         CLC   NDSNAME,DSNZEROS        RENAME ?
         BER   R11                     NO, RETURN / DSN ACCEPTED
         CLC   NDSNAME,DSNSPACE        RENAME ?
         BER   R11                     NO, RETURN / DSN ACCEPTED
         CLC   DS1DSNAM,NDSNAME        RENAME ?
         BER   R11                     NO, RETURN / DSN ACCEPTED
RENAME0  EQU   *
         LA    R3,NDSNAME              R3 - ADDRESS OF NEW DSNAME
         LA    R4,DS1DSNAM             R4 - ADDRESS OF OLD DSNAME
         LA    R5,9                    R5 - MAX LENGTH QUALIFIER + .
HIGHLEV  DS    0H
         CLC   0(1,R3),0(R4)           HIGHLEV QUALIFIERS EQUAL?
         BNE   REJECTIT                NO, ILLEGAL RENAME OF DSN
         CLC   0(1,R3),PERIOD          YES, END OF HIGHLEV QUALIFIERS?
         BER   R11                     YES, RETURN / DSN ACCEPTED
         LA    R3,1(R3)                NO, INCREMENT R3
         LA    R4,1(R4)                AND INCREMENT R4
         BCT   R5,HIGHLEV              CHECK REST OF HIGHLEV QUALIFIERS
         B     REJECTIT                REJECT, ILLEAGAL DSN
         SPACE
         EJECT
***********************************************************************
*        SET UP FOR ABSOLUTE TRACK OPERATION (CLASS OF 'APPL')
***********************************************************************
TRACKOP  DS    0H
         MVC   REJCTCOD,TERMINAT       VARIBLE REJECT CODE = 'T'
         LA    R3,FDRABSTK             R3 - ADDRESS OF 'FDRABSTK' APPL
         BAL   R11,RACHECKA            PERFORM RACHECK-CLASS 'APPL'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
***********************************************************************
*        SET UP FOR FDR FULL DUMP/RESTORE OPERATION (CLASS OF 'APPL')
***********************************************************************
FDROP    DS    0H
         MVC   REJCTCOD,TERMINAT       VARIBLE REJECT CODE = 'T'
         LA    R3,FDR                  R3 - ADDRESS OF 'FDR' APPL
         BAL   R11,RACHECKA            PERFORM RACHECK-CLASS 'APPL'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
***********************************************************************
*        SET UP FOR MAINTENANCE OPERATION (CLASS OF 'APPL')
***********************************************************************
MAINTOP  DS    0H
         MVC   REJCTCOD,TERMINAT       VARIBLE REJECT CODE = 'T'
         LA    R3,FDRABRM              R3 - ADDRESS OF 'FDRABRM' APPL
         BAL   R11,RACHECKA            PERFORM RACHECK-CLASS 'APPL'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
***********************************************************************
*        SET UP FOR RESTORE OPERATION  (CLASS OF 'DATASET')
***********************************************************************
RESTOP   DS    0H
         MVC   REJCTCOD,REJECT         VARIBLE REJECT CODE = 'R'
         LA    R4,128                  SET AUTHORITY REG TO 'ALTER'
*                                      FOR RESTORE OPERATION (RACHECK)
         CLC   OPTIONS,DFEFRNAM        VSAM DFEF CLUSTER RESTORE
*                                      WITH NEWNAME
         BE    RENAME1                 YES, SET UP FOR EXTRA CHECK
         CLC   OPTIONS,DSNRNAME        DATASET RESTORE
*                                      WITH NEWNAME
         BE    RENAME1                 YES, SET UP FOR EXTRA CHECK
         CLC   NDSNAME,DSNZEROS        RENAME ?
         BE    SKPRENAM                NO, RETURN / DSN ACCEPTED
         CLC   NDSNAME,DSNSPACE        RENAME ?
         BE    SKPRENAM                NO, RETURN / DSN ACCEPTED
         CLC   DS1DSNAM,NDSNAME        RENAME ?
         BE    SKPRENAM                NO, RETURN / DSN ACCEPTED
RENAME1  EQU   *
         LA    R3,NDSNAME              R3 - ADDRESS OF NEW DSNAME
         BAL   R11,RACHECKD            PERFORM RACHECK-CLASS 'DATASET'
         LA    R4,2                    SET AUTHORITY REG TO 'READ'
*                                      FOR RENAME OPERATION (RACHECK)
SKPRENAM DS    0H
         LA    R3,DS1DSNAM             R3 - ADDRESS OF OLD DSNAME
         BAL   R11,RACHECKD            PERFORM RACHECK-CLASS 'DATASET'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
         EJECT
***********************************************************************
*        SET UP FOR ARCHIVE/SCRATCH OPERATION (CLASS OF 'DATASET')
***********************************************************************
ARCSCROP DS    0H
         CLC   DS1DSNAM(4),=CL4'SYS8'  IS THIS A TEMPORARY DSN?
         BE    FREEMAIN                ALLOW THE OPERATION
         MVC   REJCTCOD,REJECT         VARIBLE REJECT CODE = 'R'
         LA    R3,DS1DSNAM             R3 - ADDRESS OF OLD DSNAME
         LA    R4,128                  SET AUTHORITY REG TO 'ALTER'
*                                      FOR ARCHIVE/SCRATCH OPERATION
         BAL   R11,RACHECKD            PERFORM RACHECK-CLASS 'DATASET'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
***********************************************************************
*        SET UP FOR FDRDSF DUMP OPERATION (CLASS OF 'DATASET')
***********************************************************************
DSFDMPOP DS    0H
         MVC   REJCTCOD,REJECT         VARIBLE REJECT CODE = 'R'
         LA    R3,DS1DSNAM             R3 - ADDRESS OF OLD DSNAME
         LA    R4,2                    SET AUTHORITY REG TO 'READ'
*                                      FOR FDRDSF DUMP OPERATION
         BAL   R11,RACHECKD            PERFORM RACHECK-CLASS 'DATASET'
         B     FREEMAIN                FINISH AND RETURN
         SPACE
***********************************************************************
*        PERFORM RACHECK FOR CLASS OF 'APPL'
***********************************************************************
RACHECKA DS    0H
         MVC   RACLIST(INITLEN),INITDATA    STATIC INFO TO DYNAMIC FORM
         RACHECK ENTITY=((R3)),CLASS=APPL,LOG=ASIS,                    +
               MF=(E,RACLIST)          RACHECK FOR CLASS = 'APPL'
         LTR   R15,R15                 USER HAVE PROPER AUTHORITY?
         BNZ   REJECTIT                NO, TERMINATE THIS VOLUME
         BR    R11                     YES, RETURN
         SPACE
         EJECT
***********************************************************************
*        PERFORM RACHECK FOR CLASS OF 'DATASET'
***********************************************************************
RACHECKD DS    0H
         MVC   RACLIST(INITLEN),INITDATA    STATIC INFO TO DYNAMIC FORM
         RACHECK ENTITY=((R3)),VOLSER=VOLZERO,                         +
               RACFIND=NO,CLASS=DATASET,ATTR=(R4),LOG=ASIS,            +
               MF=(E,RACLIST)
         LTR   R15,R15                 RACHECK SUCCESSFUL?
         BNZ   REJECTIT                NO, REJECT DSNAME FOR OPERATION
         BR    R11                     YES, RETURN
         SPACE
***********************************************************************
*        REJECT DATA SET FOR THIS OPERATION
***********************************************************************
REJECTIT DS    0H
         MVC   COMPCOD,REJCTCOD        REJECTION FOR THIS OPERATION
         BR    R11                     RETURN
         SPACE
         EJECT
***********************************************************************
*        FREEMAIN DYNAMIC STORAGE
***********************************************************************
FREEMAIN DS    0H
         LR    R6,R13                  R6 - ADDRESS OF DYNAMIC WORKAREA
         L     R13,4(R13)              RESTORE REGISTER 13
         CLC   OPMODE,EXITMODE         FINISHED WITH THIS OPERATION?
         BNE   FINISH                  NO, DON'T BOTHER WITH FREEMAIN
         FREEMAIN R,LV=WORKLEN,A=(R6)
*                                      FREEMAIN DYNAMIC STORAGE
         SPACE
***********************************************************************
*        WRAP THINGS UP AND RETURN
***********************************************************************
FINISH   DS    0H
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         SR    R15,R15                 SET REG 15 TO ZERO
         BR    R14                     RETURN
         SPACE
         EJECT
***********************************************************************
*        CONSTANTS
***********************************************************************
FULL00   DC    F'00'                   FULLWORD VALUE ZERO
DSNZEROS DC    XL44'00'                DSNAME WITH VALUE OF HEX ZEROS
FDRTYPE  DC    CL1'F'                  FDR TYPE OF OPERATION
MAINTYPE DC    CL1'M'                  FDRABRM TYPE OF OPERATION
QUEUTYPE DC    CL1'U'                  FDRABRUT TYPE OF OPERATION
EXITMODE DC    CL1'Z'                  INDICATES END OF VOL PROCESSING
RESTMODE DC    CL1'R'                  INDICATES RESTORE OPERATION
ARCHIVE  DC    CL1'A'                  INDICATES DS ARCHIVE PROCESSING
SCRATCH  DC    CL1'S'                  INDICATES DS SCRATCH PROCESSING
DFEFRNAM DC    CL1'E'                  VSAM DFEF RESTORE AND RENAME
DSNRNAME DC    CL1'M'                  DSN RESTORE AND RENAME
ABSOLUTE DC    CL1'Z'                  INDICATE ABSOLUTE TRK PROCESSING
DSFDUMP  DC    CL3'DDD'                INDICATE FDRDSF DUMP PROCESSING
ACCEPT   DC    CL1'A'                  ACCEPT THE OPERATION
TERMINAT DC    CL1'T'                  TERMINATE THIS VOLUME
REJECT   DC    CL1'R'                  REJECT THIS DATA SET
PERIOD   DC    CL1'.'                  DELIMITER BETWEEN DSN QUALIFIERS
DATASET  DS    0H
         DC    AL1(7)                  LENGTH OF CLASSNAME
         DC    C'DATASET'              CLASSNAME FOR RACHECK MACRO
APPL     DS    0H
         DC    AL1(4)                  LENGTH OF CLASSNAME
         DC    C'APPL'                 CLASSNAME FOR RACHECK MACRO
FDR      DC    CL8'FDR     '           ENTITY FOR RACHECK CLASS='APPL'
FDRABRM  DC    CL8'FDRABRM '           ENTITY FOR RACHECK CLASS='APPL'
FDRABSTK DC    CL8'FDRABSTK'           ENTITY FOR RACHECK CLASS='APPL'
JCLMAINT DC    CL8'JCLMAINT'           ENTITY FOR RACHECK CLASS='APPL'
DSNSPACE DC    CL44' '                 DSNAME WITH VALUE OF SPACES
VOLZERO  DC    CL6'000000'             VOLUME SERIAL FOR RACHECK MACRO
         SPACE
***********************************************************************
*        INITIATIALIZATION DATA FOR PORTION OF GETMAIN'D AREA
***********************************************************************
INITDATA RACHECK MF=L                  LIST FORM OF RACHECK MACRO
INITLEN  EQU   *-INITDATA
         SPACE
         EJECT
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         EQUATE
         LTORG
         SPACE
***********************************************************************
*        DYNAMIC STORAGE AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     FDRXPASS SAVE AREA
REJCTCOD DS    CL1                     VARIABLE REJECT CODE, 'R' OR 'T'
RACLIST  DS    CL(INITLEN)             LIST FORM OF RACHECK MACRO
WORKLEN  EQU   *-WORKAREA
         SPACE
         EJECT
***********************************************************************
*        MAPPING OF FDR PARAMETER LIST
***********************************************************************
FDRPARM  DSECT                         FDR PARAMETER LIST MAP
REENT    DS    CL4                     RE-ENTRANT ADDRESS POINTER
OPTYPE   DS    CL1                     TYPE OF OPERATION
*              F - FDR                 D - FDRDSF
*              A - FDRABR              M - FDRABRM
*              U - FDRABRUT
OPMODE   DS    CL1                     MODE OF OPERATIONS
*              D - DUMP                S - SIM
*              C - COPY                R - RESTORE
*              P - PRINT               Z - EXIT
*              X - RESET (FDRABRUT)    Y - REMOVE (FDRABRUT)
OPTIONS  DS    CL1                     OPTIONS
*              F - FDR FULL VOLUME DUMP
*              R - FULL VOLUME RESTORE - PARM R (CPYVOLID=YES)
*              N - FULL VOLUME RESTORE - PARM N (CPYVOLID=NO)
*              C - VSAM DF/EF CLUSTER NAME RESTORE
*              D - DATA SET OPERATION
*              E - VSAM DF/EF CLUSTER RESTORE WITH NEWNAME
*              A - ARCHIVE DATA SET OPERATION (TYPE=ARC)
*              S - SCRATCH DATA SET (TYPE=SCR)
*              M - DATA SET RESTORE WITH NEWNAME
*              Z - ABSOLUTE TRACK OPERATION
COMPCOD  DS    CL1                     COMPLETION CODE (SET BY EXIT)
*              A - ACCEPT THE OPERATION
*              T - TERMINATE THIS VOLUME (A USER ABEND IS ISSUED)
*              R - REJECT THIS DATA SET (DATA SET EXIT ONLY)
         DS    CL4                     RESERVED
DDNAME   DS    CL8                     DISK DD NAME
VOLSER   DS    CL6                     VOLUME SERIAL OF VOLUME ACCESSED
         DS    CL6                     RESERVED
DSCB1    DS    0F
         IECSDSL1 (1)                  DSCB MAPPING MACRO
ODSLEN   DS    CL2                     DATA SET NAME LENGTH (FDRABRUT)
NDSLEN   DS    CL2                     NEW DSNAME LENGTH (FDRABRUT)
NDSNAME  DS    CL44                    NEWNAME OF DATA SET (OPTION 'M')
         END   FDRXPASS
