         TITLE 'USRDEL2 - DELUXE USER REGISTRATION (DELETE A USER)'
***********************************************************************
*                                                                     *
*        MODULE NAME = USRDEL2                                        *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*              05/27/86  WRITTEN.                               JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'USRDEL2 - INITIALIZATION SECTION'
USRDEL2  CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,USRDEL2_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING USRDEL2,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         LA    R4,DATA                 SET UP
         LA    R5,DATALEN                  REGISTERS
         LA    R6,INITDATA                     FOR
         LA    R7,DATALEN                          LONG MOVE
         MVCL  R4,R6                   INITIALIZE PART GETMAINED AREA
***********************************************************************
*  INITIALIZATION SECTION - OPEN '&&LISTC' & '&&DELET'; WRITE 1ST REC
***********************************************************************
OPEN     EQU   *
         OPEN  (LISTCIN,INPUT),MF=(E,OPENLST_)    OPEN 'LISTC' INPUT
         OPEN  (DELETOUT,OUTPUT),MF=(E,OPENLST_)  OPEN 'DELETE' OUTPUT
         MVC   DELETREC,SPACES         INITIALIZE DELETREC
         MVC   DELETREC,PROC           MOVE 'PROC 0'
         PUT   DELETOUT,DELETREC       WRITE DELETE RECORD
         MVC   DELETREC,CONTROL        MOVE 'CONTROL LIST'
         PUT   DELETOUT,DELETREC       WRITE DELETE RECORD
         MVC   DELETREC,PROFILE        MOVE 'PROFILE NOPREFIX'
         PUT   DELETOUT,DELETREC       WRITE DELETE RECORD
         MVC   DELETREC,SPACES         INITIALIZE DELETREC
         MVC   DELETREC(7),DELETLIT    MOVE 'DELETE ' TO DELETREC
         TITLE 'USRDEL2 - PROCESSING SECTION'
***********************************************************************
*  PROCESSING SECTION - CREATE 'DELETE' STMTS USING 'LISTC' INPUT
***********************************************************************
DELETE   EQU   *
         GET   LISTCIN                 GET LISTC RECORD
         CLC   LISTC#,0(R1)            ARE WE READY TO START?
         BNE   DELETE                  NO, GET NEXT RECORD
         GET   LISTCIN                 GET LISTC RECORD
DELET10  EQU   *
         CLC   INCAT#,5(R1)            VALID RECORD?
         BE    DELET20                 NO, SKIP THIS RECORD
         CLC   DASHES,12(R1)           VALID RECORD?
         BNE   DELET20                 NO, SKIP THIS RECORD
         MVC   DELETNAM,16(R1)         MOVE DSN TO DELETE RECORD
         PUT   DELETOUT,DELETREC       WRITE DELETE RECORD
DELET20  EQU   *
         GET   LISTCIN                 GET LISTC RECORD
         MVC   DELETNAM,SPACES         INITIALIZE DELETNAM
         B     DELET10                 PROCESS IT
         TITLE 'USRDEL2 - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         CLOSE LISTCIN,MF=(E,CLOSLST_)    CLOSE INPUT
         CLOSE DELETOUT,MF=(E,CLOSLST_)    CLOSE INPUT
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'USRDEL2 - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
         TITLE 'USRDEL2 - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
SPACES   DC    CL80' '                 SPACES
PROC     DC    CL80'PROC 0'            TO INITIALIZE DELETREC
CONTROL  DC    CL80'CONTROL LIST'      TO INITIALIZE DELETREC
PROFILE  DC    CL80'PROFILE NOPREFIX'  TO INITIALIZE DELETREC
DELETLIT DC    CL7'DELETE '            TO INITIALIZE DELETREC
LISTC#   DC    CL6'LISTC '             BEGINNING OF LISTC OUTPUT
INCAT#   DC    CL7'IN-CAT '            INDICATES UNUSED INFORMATION
DASHES   DC    CL4'--- '               INDICATES VALID INFORMATION
         TITLE 'USRDEL2 - LITERAL POOL'
         LTORG
***********************************************************************
*        INITIALIZATION DATA FOR PART OF GETMAINED AREA
***********************************************************************
INITDATA DS    0F
DCBIN1   DCB   DDNAME=LISTCIN,DSORG=PS,MACRF=(GL),EODAD=FINISH
DCBOUT   DCB   DDNAME=DELETOUT,DSORG=PS,MACRF=(PM)
OPENLST  OPEN  (,),MF=L            STATIC LIST FORM OF OPEN MACRO
CLOSLST  CLOSE (,),MF=L            STATIC LIST FORM OF CLOSE MACRO
DATALEN  EQU   *-INITDATA
         TITLE 'USRDEL2 - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
DELETREC DS    0CL80
         DS    CL07                    'DELETE '
DELETNAM DS    CL44                    DSN TO BE DELETED
         DS    CL29
DATA     DS    0F
LISTCIN  DCB   DDNAME=LISTCIN,DSORG=PS,MACRF=(GL),EODAD=FINISH
DELETOUT DCB   DDNAME=DELETOUT,DSORG=PS,MACRF=(PM)
OPENLST_ OPEN  (,),MF=L            DYNAMIC LIST FORM OF OPEN MACRO
CLOSLST_ CLOSE (,),MF=L            DYNAMIC LIST FORM OF CLOSE MACRO
WORKLEN  EQU   *-WORKAREA
         END   USRDEL2
