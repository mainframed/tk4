         TITLE 'USRDEL3 - DELUXE USER REGISTRATION (DELETE A USER)'
***********************************************************************
*                                                                     *
*        MODULE NAME = USRDEL3                                        *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*              05/27/86  WRITTEN.                               JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'USRDEL3 - ISPCALL MACRO'
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         +
               &EP=ISPLINK,                                            +
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    +
               &EPADDR=,       LABEL FOR ADDRESS OF EP OF ISPLINK      +
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    +
               &WKREG=,        WORK REGISTER (DEFAULT 1)               +
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG,&WKEP
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1,&ISPEP
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHES+
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
         EJECT
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 +
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LIS+
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
         EJECT
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
.*       LA    1,&WKAR.+4                                       ISPCALL
.*       L     15,=V(&EP)                                       ISPCALL
.*       BALR  14,15                                            ISPCALL
         AIF   (T'&EPADDR EQ 'O').EP2
&ISPEP   SETC  '&EPADDR'
         LOAD  EP=ISPLINK
         ST    R0,&ISPEP
.EP2     ANOP
&WKEP    SETC  '&ISPEP'
         L     15,&WKEP
         LA    1,&WKAR.+4
         CALL  (15)
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
         EJECT
         TITLE 'USRDEL3 - INITIALIZATION SECTION'
USRDEL3  CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,USRDEL3_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING USRDEL3,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R11,4095(R12)           ESTABLISH ADDITIONAL BASE REG
         USING USRDEL3+4095,R11        ESTABLISH ADDRESSABILITY TO
*                                      REST OF PROGRAM
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         LA    R4,DATA                 SET UP
         LA    R5,DATALEN                  REGISTERS
         LA    R6,INITDATA                     FOR
         LA    R7,DATALEN                          LONG MOVE
         MVCL  R4,R6                   INITIALIZE PART GETMAINED AREA
***********************************************************************
*  INITIALIZATION SECTION - SET DIALOG CONTROL, DEFINE VARIABLES
***********************************************************************
CONTROL  EQU   *
*                                      SET DIALOG CONTROL
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        +
               WKAREA=PARMS,                                           +
               EPADDR=ISPADDR
VDEF1    EQU   *
         MVC   ISPFSERV,DEFID          SET ISPF DIALOG SERVICE
*                                      DEFINE VARIABLES
         ISPCALL ('VDEFINE',NAMELST1,ICHUTRB1,'FIXED',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF2                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF2    EQU   *
         ISPCALL ('VDEFINE',NAMELST2,ICHUTCOD,'FIXED',1)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF3                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF3    EQU   *
         ISPCALL ('VDEFINE',NAMELST3,ICHUTDAT,'CHAR',52)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF4                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF4    EQU   *
         ISPCALL ('VDEFINE',NAMELST4,USERMSGS,'CHAR',24)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF5                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF5    EQU   *
         ISPCALL ('VDEFINE',NAMELST5,USERMSGL,'CHAR',78)
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF10   EQU   *
         B     TBCREATE                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VDEF)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VDEF)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - CREATE TEMPORARY TABLE FOR ICHUT100 PROCESS
***********************************************************************
TBCREATE EQU   *
         MVC   ISPFSERV,CREID          SET ISPF DIALOG SERVICE
*                                      CREATE A TABLE
         ISPCALL ('TBCREATE','USRDEL',NAMELST6,NAMELST7,'NOWRITE','REPL+
               ACE')
         B     TBCRE10(R15)            CHECK RETURN CODE
TBCRE10  EQU   *
         B     OPEN                    RC = 00 (CONTINUE)
         B     OPEN                    RC = 04 (CONTINUE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - OPEN '&&ICHUT' & '&&RACF'
***********************************************************************
OPEN     EQU   *
         OPEN  (ICHUTIN,INPUT),MF=(E,OPENLST_)    OPEN 'ICHUT100' INPUT
         OPEN  (RACFOUT,OUTPUT),MF=(E,OPENLST_)   OPEN 'RACF' OUTPUT
         MVC   RACFCMD,PROFILE         MOVE 'PROFILE NOPREFIX'
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
***********************************************************************
*  INITIALIZATION SECTION - INITIALIZE TABLE VALUES
***********************************************************************
INITVAL  EQU   *
         MVC   ICHUTRB1,FULL00         RELATIVE BLOCK ADDRESS FOR REC
         MVC   ICHUTRB2,FULL00         RBA OF NEXT RECORD IN CHAIN
         MVC   ICHUTCOD,FULL00         RECORD TYPE CODE
         MVC   ICHUTDAT,SPACES         RACF INFORMATION DATA
         MVC   RACFCMD,SPACES          GENERIC RACF COMMAND
         SR    R2,R2                   R2 - USED TO SET ICHUTRB1 VALUES
         TITLE 'USRDEL3 - PROCESSING SECTION'
***********************************************************************
*  PROCESSING SECTION - PROCESS ICHUT100 WORK DATA SET
***********************************************************************
ICHUT    EQU   *
         GET   ICHUTIN                 GET INPUT RECORD
         ST    R2,ICHUTRB1             STORE CURRENT RBA
         MVC   ICHUTRB2+1(3),0(R1)     STORE CHAINING RBA
         MVC   ICHUTCOD,3(R1)          STORE RECORD TYPE CODE
         MVC   ICHUTDAT,4(R1)          STORE RACF INFORMATION DATA
         LA    R2,1(R2)                INCREMENT R2
***********************************************************************
*  PROCESSING SECTION - ADD A USER TO THE TEMPORARY TABLE
***********************************************************************
TBADD    EQU   *
         MVC   ISPFSERV,ADDID          SET ISPF DIALOG SERVICE
*                                      ADD A ROW TO A TABLE
         ISPCALL ('TBADD','USRDEL')
         B     TBADD10(R15)            CHECK RETURN CODE
TBADD10  EQU   *
         B     ICHUT                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBADD)
         B     USRERR08                RC = 08 (NOT VALID FOR TBADD)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - INITIAL VALUES FOR TABLE SEARCH
***********************************************************************
ICHUTEND EQU   *
         MVC   ICHUTRB1,FULL00         INITIALIZE KEY TO POINT TO TOP
         MVI   USERSCAN,ON             INDICATE INITIAL SCAN FOR USERID
         SR    R7,R7                   R7 - KEEP TRACK PROCESSED USERS
***********************************************************************
*  PROCESSING SECTION - RETRIEVE INFO FROM TABLE FOR SELECTED RBA
***********************************************************************
TBGET    EQU   *
         MVC   ISPFSERV,TBGID          SET ISPF DIALOG SERVICE
*                                      EXECUTE CLIST
         ISPCALL ('TBGET','USRDEL')
         B     TBGET10(R15)            CHECK RETURN CODE
TBGET10  EQU   *
         B     BLDCMD                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - BUILD RACF COMMANDS USING INFO FROM TABLE
***********************************************************************
BLDCMD   EQU   *
         SR    R3,R3                   ZERO R3
         ICM   R3,B'0001',ICHUTCOD     EXTRACT TYPE CODE FROM TABLE
         SLL   R3,2(0)                 MULTIPLY BY FOUR FOR BRANCHING
         B     BLDCMD0(R3)             BRANCH ACCORDING TO TYPE CODE
BLDCMD0  EQU   *
         B     BLDCMD00                CODE TYPE X'00'
         B     BLDCMD01                CODE TYPE X'01'
         B     BLDCMD02                CODE TYPE X'02'
         B     BLDCMD03                CODE TYPE X'03'
         B     BLDCMD04                CODE TYPE X'04'
         B     BLDCMD05                CODE TYPE X'05'
         B     BLDCMD06                CODE TYPE X'06'
         B     BLDCMD07                CODE TYPE X'07'
         B     BLDCMD08                CODE TYPE X'08'
         B     BLDCMD09                CODE TYPE X'09'
         B     BLDCMD0A                CODE TYPE X'0A'
         B     BLDCMD0B                CODE TYPE X'0B'
         B     BLDCMD0C                CODE TYPE X'0C'
         B     BLDCMD0D                CODE TYPE X'0D'
         B     BLDCMD0E                CODE TYPE X'0E'
         B     BLDCMD0F                CODE TYPE X'0F'
         B     BLDCMD10                CODE TYPE X'10'
         B     BLDCMD11                CODE TYPE X'11'
         B     BLDCMD12                CODE TYPE X'12'
         B     BLDCMD13                CODE TYPE X'13'
         B     BLDCMD14                CODE TYPE X'14'
BLDCMD00 EQU   *
         B     MSGDISP2                ILLEAGAL RECORD TYPE CODE
BLDCMD01 EQU   *
         C     R7,ICHUTRB1             USER BEEN PROCESSED?
         BE    BLDC011                 NO, SET USERID & ICHUTRB1
         L     R4,ICHUTRB1             R4 - RBA OF CURRENT RECORD
         LA    R4,1(R4)                INCREMENT R4
         ST    R4,ICHUTRB1             R4 - RBA OF NEXT RECORD
         B     TBGET
BLDC011  EQU   *
         MVI   USERSCAN,OFF            INDICATE NOT SCANNING FOR USER
         MVC   USERID,ICHUTDAT         OBTAIN USER-ID
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD02 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD03 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD04 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD05 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,ALGCMD          SET RACF COMMAND TO 'ALTGROUP'
         MVC   RACFCMD+10(8),ICHUTDAT  MOVE GROUP NAME TO RACF COMMAND
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD06 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,RECMD           SET RACF COMMAND TO 'REMOVE'
         MVC   RACFCMD+8(8),USERID     MOVE USERID TO RACF COMMAND
         MVC   RACFCMD+24(8),ICHUTDAT  MOVE GROUP NAME TO RACF COMMAND
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD07 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,DUCMD           SET RACF COMMAND TO 'DELUSER'
         MVC   RACFCMD+9(8),USERID     USERID DELETED FROM RACF
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         MVC   RACFCMD,DELCMD1         SET RACF COMMAND TO 'DELETE'
         MVC   RACFCMD+7(8),USERID     USERID DELETED FROM RACF
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         MVC   RACFCMD,DELCMD2         SET RACF COMMAND TO 'DELETE'
         MVC   RACFCMD+7(8),USERID     USERID DELETED FROM RACF
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         MVC   ICHUTRB1,FULL00         INITIALIZE KEY TO POINT TO TOP
         MVI   USERSCAN,ON             INDICATE INITIAL SCAN FOR USERID
         LA    R7,1(R7)                R7 - KEEP TRACK PROCESSED USERS
         B     TBGET
BLDCMD08 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD09 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD0A EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD0B EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,DDCMD           SET RACF COMMAND TO 'DELDSD'
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,8(R4)                POSITION FOR DSN ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R6,52                   R6 - MAXIMUM LENGTH OF ICHUTDAT
BLDC0B1  EQU   *
         CLC   0(2,R5),SPACES          END OF PROFILE NAME?
         BE    BLDC0B2                 YES, CONTINUE NON-GENERIC
         CLC   0(4,R5),GENERIC#        END OF PROFILE NAME?
         BE    BLDC0B3                 YES, CONTINUE GENERIC
         MVC   0(1,R4),0(R5)           MOVE PROFILE NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC0B1              CONTINUE TO MOVE PROFILE
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC0B2  EQU   *
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         B     BLDC0B4                 WRITE RACF COMMAND TO DATA SET
BLDC0B3  EQU   *
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(7,R4),GENERIC         INDICATE GENERIC PROFILE
BLDC0B4  EQU   *
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET
BLDCMD0C EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,ALDCMD          SET RACF COMMAND TO 'ALTDSD'
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,8(R4)                POSITION FOR DSN ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R6,52                   R6 - MAXIMUM LENGTH OF ICHUTDAT
BLDC0C1  EQU   *
         CLC   0(2,R5),SPACES          END OF PROFILE NAME?
         BE    BLDC0C2                 YES, CONTINUE NON-GENERIC
         CLC   0(4,R5),GENERIC#        END OF PROFILE NAME?
         BE    BLDC0C3                 YES, CONTINUE GENERIC
         MVC   0(1,R4),0(R5)           MOVE PROFILE NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC0C1              CONTINUE TO MOVE PROFILE
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC0C2  EQU   *
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         B     BLDC0C4                 WRITE RACF COMMAND TO DATA SET
BLDC0C3  EQU   *
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(7,R4),GENERIC         INDICATE GENERIC PROFILE
BLDC0C4  EQU   *
         LA    R4,8(R4)                INCREMENT R4
         MVC   0(11,R4),OWNER#         INDICATE NEW OWNER (SYS1)
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET
BLDCMD0D EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,PECMD           SET RACF COMMAND TO 'PERMIT'
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,7(R4)                POSITION FOR DSN ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R6,52                   R6 - MAXIMUM LENGTH OF ICHUTDAT
BLDC0D1  EQU   *
         CLC   0(2,R5),SPACES          END OF PROFILE NAME?
         BE    BLDC0D3                 YES, CONTINUE NON-GENERIC
         CLC   0(4,R5),GENERIC#        END OF PROFILE NAME?
         BE    BLDC0D2                 YES, CONTINUE GENERIC
         MVC   0(1,R4),0(R5)           MOVE PROFILE NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC0D1              CONTINUE TO MOVE PROFILE
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC0D2  EQU   *
         LA    R4,1(R4)                INCREMENT R4
         MVC   0(7,R4),GENERIC         INDICATE GENERIC PROFILE
         LA    R4,7(R4)                INCREMENT R4
BLDC0D3  EQU   *
         LA    R4,1(R4)                INCREMENT R4
         MVC   0(3,R4),ID#             MOVE 'ID' KEYWORD
         LA    R4,3(R4)                INCREMENT R4
         MVC   0(8,R4),USERID          USERID DELETED FROM ACCESS LIST
         LA    R4,8(R4)                INCREMENT R4
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(6,R4),DELETE          INDICATE DELETE FROM ACCESS LIST
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET                   WRITE RACF COMMAND TO DATA SET
BLDCMD0E EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD0F EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,RALTCMD         SET RACF COMMAND TO 'RALTER'
         MVC   RACFCMD+7(8),ICHUTDAT   MOVE CLASS NAME TO RACF COMMAND
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,17(R4)               POSITION FOR DSN ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R5,9(R5)                POSITION FOR DSN ENTRY
         LA    R6,43                   R6 - MAXIMUM LENGTH OF ICHUTDAT
BLDC0F1  EQU   *
         CLC   0(1,R5),SPACES          END OF PROFILE NAME?
         BE    BLDC0F2                 YES, CONTINUE NON-GENERIC
         MVC   0(1,R4),0(R5)           MOVE PROFILE NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC0F1              CONTINUE TO MOVE PROFILE
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC0F2  EQU   *
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(11,R4),OWNER#         INDICATE NEW OWNER (SYS1)
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET
BLDCMD10 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,PECMD           SET RACF COMMAND TO 'PERMIT'
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,7(R4)                POSITION FOR DSN ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R5,9(R5)                POSITION FOR DSN ENTRY
         LA    R6,43                   R6 - MAXIMUM LENGTH OF ICHUTDAT
BLDC101  EQU   *
         CLC   0(3,R5),SPACES          END OF PROFILE NAME?
         BE    BLDC102                 YES, CONTINUE NON-GENERIC
         CLC   0(4,R5),GENERIC#        END OF PROFILE NAME?
         BE    BLDC103                 YES, CONTINUE GENERIC
         MVC   0(1,R4),0(R5)           MOVE PROFILE NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC101              CONTINUE TO MOVE PROFILE
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC102  EQU   *
         LA    R4,1(R4)                INCREMENT R4
         MVC   0(6,R4),CLASS#          INDICATE CLASS OF PROFILE
         LA    R4,6(R4)                INCREMENT R4
         LA    R5,ICHUTDAT             R5 - ADDRESS OF CLASS NAME
         MVC   0(8,R4),0(R5)           MOVE CLASS NAME TO RACF COMMAND
         LA    R4,8(R4)                INCREMENT R4
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         B     BLDC104                 ADD 'ID(USERID)' & 'DELETE'
BLDC103  EQU   *
         MVC   0(6,R4),CLASS#          INDICATE CLASS OF PROFILE
         LA    R4,6(R4)                INCREMENT R4
         LA    R5,ICHUTDAT             R5 - ADDRESS OF CLASS NAME
         MVC   0(8,R4),0(R5)           MOVE CLASS NAME TO RACF COMMAND
         LA    R4,8(R4)                INCREMENT R4
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(7,R4),GENERIC         INDICATE GENERIC PROFILE
         LA    R4,8(R4)                INCREMENT R4
BLDC104  EQU   *
         MVC   0(3,R4),ID#             MOVE 'ID' KEYWORD
         LA    R4,3(R4)                INCREMENT R4
         MVC   0(8,R4),USERID          USERID DELETED FROM ACCESS LIST
         LA    R4,8(R4)                INCREMENT R4
         MVC   0(1,R4),CLOPAREN        MOVE CLOSING PARENTHESIS
         LA    R4,2(R4)                INCREMENT R4
         MVC   0(6,R4),DELETE          INDICATE DELETE FROM ACCESS LIST
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET
BLDCMD11 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   RACFCMD,COCMD           SET RACF COMMAND TO 'CONNECT'
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,9(R4)                POSITION FOR USERID ENTRY
         LA    R5,ICHUTDAT             R5 - INDEX THROUGH RACF PROF NAM
         LA    R6,20                   R6 - MORE THAN ENOUGH SPACE FOR
*                                           USERID, GROUP NAME AND '/'
BLDC111  EQU   *
         CLC   0(1,R5),SLASH           END OF USERID?
         BE    BLDC112                 YES, CONTINUE
         MVC   0(1,R4),0(R5)           MOVE USERID TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC111              CONTINUE TO MOVE USERID
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC112  EQU   *
         LA    R4,RACFCMD              R4 - INDEX THROUGH RACF COMMAND
         LA    R4,25(R4)               POSITION FOR GROUP NAME ENTRY
         LA    R5,1(R5)                INCREMENT R5
         BCTR  R6,0                    DECREMENT R6
BLDC113  EQU   *
         CLC   0(1,R5),SPACES          END OF GROUP NAME?
         BE    BLDC114                 YES, CONTINUE
         MVC   0(1,R4),0(R5)           MOVE GROUP NAME TO RACF CMD
         LA    R4,1(R4)                INCREMENT R4
         LA    R5,1(R5)                INCREMENT R5
         BCT   R6,BLDC113              CONTINUE TO MOVE GROUP NAME
         B     MSGDISP2                THIS SHOULD NEVER BE EXECUTED
BLDC114  EQU   *
         PUT   RACFOUT,RACFCMD         WRITE RACF COMMAND TO DATA SET
         B     TBGET
BLDCMD12 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD13 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
BLDCMD14 EQU   *
         CLI   USERSCAN,ON             INITIAL SCAN FOR USER?
         BE    TBEND                   YES, NO USERS LEFT (DONE)
         MVC   ICHUTRB1,ICHUTRB2       OBTAIN RBA OF NEXT RECORD
         B     TBGET
***********************************************************************
*  PROCESSING SECTION - CLOSE TEMPORARY TABLE (USRDEL)
***********************************************************************
TBEND    EQU   *
         MVC   ISPFSERV,ENDID          SET ISPF DIALOG SERVICE
*                                      CLOSE TEMPORARY TABLE
         ISPCALL ('TBEND','USRDEL')
         B     TBEND10(R15)            CHECK RETURN CODE
TBEND10  EQU   *
         B     FINISH                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBCLOSE)
         B     USRERR08                RC = 08 (NOT VALID FOR TBCLOSE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'USRDEL3 - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
USRERR04 EQU   *
         MVC   ISPFRC,FOUR             SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR08 EQU   *
         MVC   ISPFRC,EIGHT            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR12 EQU   *
         MVC   ISPFRC,TWELVE           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR16 EQU   *
         MVC   ISPFRC,SIXTEEN          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR20 EQU   *
         MVC   ISPFRC,TWENTY           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
MSGDISP1 EQU   *
         OPEN  (ERROUT,OUTPUT),MF=(E,OPENLST_)    OPEN ERROR OUTPUT
         MVC   USERMSG,USERERR         SET SHORT MESSAGE
         MVC   USERMSG+22(8),ISPFSERV     SET ISPF DIALOG SERVICE
         MVC   USERMSG+48(2),ISPFRC    SET DIALOG SERVICE RETURN CODE
         PUT   ERROUT,USERMSG          WRITE ERROR MESSAGE
         CLOSE ERROUT,MF=(E,CLOSLST_)  CLOSE ERROR OUTPUT
         B     FINISH
MSGDISP2 EQU   *
         OPEN  (ERROUT,OUTPUT),MF=(E,OPENLST_)    OPEN ERROR OUTPUT
         MVC   USERMSG,BLDERR          SET SHORT MESSAGE
         PUT   ERROUT,USERMSG          WRITE ERROR MESSAGE
         CLOSE ERROUT,MF=(E,CLOSLST_)  CLOSE ERROR OUTPUT
         TITLE 'USRDEL3 - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         CLOSE ICHUTIN,MF=(E,CLOSLST_)    CLOSE ICHUT100 INPUT
         CLOSE RACFOUT,MF=(E,CLOSLST_)    CLOSE RACF COMMAND OUTPUT
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'USRDEL3 - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
OFF      EQU   X'00'                   NO USERS HAVE BEEN ADDED
ON       EQU   X'10'                   USER(S) HAVE BEEN ADDED
CODE01   EQU   X'01'                   ICHUT100 RECORD CODE
CODE05   EQU   X'05'                   ICHUT100 RECORD CODE
CODE06   EQU   X'06'                   ICHUT100 RECORD CODE
CODE07   EQU   X'07'                   ICHUT100 RECORD CODE
CODE0B   EQU   X'0B'                   ICHUT100 RECORD CODE
CODE0C   EQU   X'0C'                   ICHUT100 RECORD CODE
CODE0D   EQU   X'0D'                   ICHUT100 RECORD CODE
CODE0F   EQU   X'0F'                   ICHUT100 RECORD CODE
CODE10   EQU   X'10'                   ICHUT100 RECORD CODE
CODE11   EQU   X'11'                   ICHUT100 RECORD CODE
         TITLE 'USRDEL3 - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
FULL00   DC    F'00'                   FULLWORD CONSTANT VALUE '00'
SPACES   DC    CL80' '                 SPACES
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
CLOPAREN DC    CL1')'                  CLOSING PARENTHESIS
SLASH    DC    CL1'/'                  SLASH
CLASS#   DC    CL6'CLASS('             CLASS OF PROFILE
DELETE   DC    CL6'DELETE'             INDICATES DELETE FROM ACCES LIST
GENERIC# DC    CL4' (G)'               INDICATES A GENERIC PROFILE
GENERIC  DC    CL7'GENERIC'            INDICATES A GENERIC PROFILE
ID#      DC    CL3'ID('                ID TO BE DELETED ACCESS LIST
OWNER#   DC    CL11'OWNER(SYS1)'       NEW OWNER OF PROFILE
PROFILE  DC    CL80'PROFILE NOPREFIX'  INITIAL TSO STATEMENT
ALGCMD   DC    CL80'ALTGROUP (12345678) OWNER(SYS1)                    +
                                            '
RECMD    DC    CL80'REMOVE (12345678) GROUP(12345678) OWNER(SYS1)      +
                                            '
DDCMD    DC    CL80'DELDSD (                                           +
                                            '
ALDCMD   DC    CL80'ALTDSD (                                           +
                                            '
PECMD    DC    CL80'PERMIT                                             +
                                            '
RALTCMD  DC    CL80'RALTER 12345678 (                                  +
                                            '
COCMD    DC    CL80'CONNECT (        ) GROUP(        ) OWNER(SYS1)     +
                                            '
DUCMD    DC    CL80'DELUSER (12345678)                                 +
                                            '
DELCMD1  DC    CL80'DELETE 12345678 ALIAS CAT(CATALOG.MVSICFM.VMVSCAT) +
                                            '
DELCMD2  DC    CL80'DELETE 12345678 ALIAS CAT(CATALOG.MVSICFM.VMVSCTB) +
                                            '
ADDID    DC    CL8'TBADD'              ISPF DIALOG SERVICE (TBADD)
ENDID    DC    CL8'TBEND'              ISPF DIALOG SERVICE (TBEND)
CREID    DC    CL8'TBCREATE'           ISPF DIALOG SERVICE (TBCREATE)
DEFID    DC    CL8'VDEFINE'            ISPF DIALOG SERVICE (VDEFINE)
TBGID    DC    CL8'TBGET'              ISPF DIALOG SERVICE (TBGET)
USERERR  DC    CL80' ISPF DIALOG SERVICE, 12345678, ENDED WITH RC = 00.+
                                            '    ERROR MESSAGE TEXT
BLDERR   DC    CL80' ERROR OCCURRED BUILDING RACF COMMAND STATEMENTS.  +
                                            '    ERROR MESSAGE TEXT
NAMELST1 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'02'
         DC    F'00'
         DC    CL8'ICHUTRB1'
         DC    CL8'ICHUTRB2'
NAMELST2 DS    0F                      NAMES OF VARIABLES (LENGTH 1)
         DC    F'01'
         DC    F'00'
         DC    CL8'ICHUTCOD'
NAMELST3 DS    0F                      NAMES OF VARIABLES (LENGTH 52)
         DC    F'01'
         DC    F'00'
         DC    CL8'ICHUTDAT'
NAMELST4 DS    0F                      NAMES OF VARIABLES (LENGTH 24)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGS'
NAMELST5 DS    0F                      NAMES OF VARIABLES (LENGTH 78)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGL'
NAMELST6 DS    0F                      NAME OF KEY TABLE VARIABLES
         DC    F'01'
         DC    F'00'
         DC    CL8'ICHUTRB1'
NAMELST7 DS    0F                      NAME OF TABLE VARIABLES
         DC    F'03'
         DC    F'00'
         DC    CL8'ICHUTRB2'
         DC    CL8'ICHUTCOD'
         DC    CL8'ICHUTDAT'
         TITLE 'USRDEL3 - LITERAL POOL'
         LTORG
***********************************************************************
*        INITIALIZATION DATA FOR PART OF GETMAINED AREA
***********************************************************************
INITDATA DS    0F
DCBIN    DCB   DDNAME=ICHUTIN,DSORG=PS,MACRF=(GL),EODAD=ICHUTEND
DCBOUT1  DCB   DDNAME=RACFOUT,DSORG=PS,MACRF=(PM)
DCBOUT2  DCB   DDNAME=ERROUT,DSORG=PS,MACRF=(PM)
OPENLST  OPEN  (,),MF=L            STATIC LIST FORM OF OPEN MACRO
CLOSLST  CLOSE (,),MF=L            STATIC LIST FORM OF CLOSE MACRO
DATALEN  EQU   *-INITDATA
         TITLE 'USRDEL3 - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
ISPADDR  DS    A                       ADDRESS OF EP TO ISPLINK
PARMS    DS    7A                      PARM LIST FOR CALL
USERSCAN DS    AL1                     INDICATES SCAN FOR NEW USER
USERID   DS    CL8                     ID OF USER BEING DELETED
RACFCMD  DS    CL80                    GENERIC RACF COMMAND
USERMSG  DS    CL80                    ERROR MESSAGE TEXT
ICHUTRB1 DS    F                       RELATIVE BLOCK ADDRESS FOR REC
ICHUTRB2 DS    F                       RBA OF NEXT RECORD IN CHAIN
ICHUTCOD DS    AL1                     RECORD TYPE CODE
ICHUTDAT DS    CL52                    RACF INFORMATION DATA
USERMSGS DS    CL24                    SHORT VERSION OF ISPF ERROR MSG
USERMSGL DS    CL78                    LONG VERSION OF ISPF ERROR MSG
ISPFSERV DS    CL8                     ISPF SERVICE ROUTINE ID
ISPFRC   DS    CL2                     ISPF SERVICE ROUTINE RETURN CODE
DATA     DS    0F
ICHUTIN  DCB   DDNAME=ICHUTIN,DSORG=PS,MACRF=(GL),EODAD=ICHUTEND
RACFOUT  DCB   DDNAME=RACFOUT,DSORG=PS,MACRF=(PM)
ERROUT   DCB   DDNAME=ERROUT,DSORG=PS,MACRF=(PM)
OPENLST_ OPEN  (,),MF=L            DYNAMIC LIST FORM OF OPEN MACRO
CLOSLST_ CLOSE (,),MF=L            DYNAMIC LIST FORM OF CLOSE MACRO
WORKLEN  EQU   *-WORKAREA
         END   USRDEL3
