$CAL     TITLE '$ - TSO CALL FUNCTION FOR LINKLIB MODULES'
*
*        LAST UPDATED 09 MAY 73 - BMD (*BMD*)
*                     FIX PARM OF '' AND SHARE
*                     SUBPOOL 78 FOR IKJPUTGT
*
*        LAST UPDATED 05 JUN 80 - HMD (*HMD MM/YY*)
*                     ADD ALIASES AUTH AND XEQ
*                     USE PUTLINE FOR ALL MESSAGES
*                     ADD LPDE SEARCH PRIOR TO BLDL         *HMD 06/80*
*                     TURN ON AUTHORIZATION DYNAMICALLY     *HMD 06/80*
*                     IF COMMAND INVOKED IS '$' OR 'AUTH'   *HMD 06/80*
*                                                           *HMD 06/80*
*
*        LAST UPDATED 09 JAN 85 - JWC (*JWC MM/YY*)
*                     RE-WRITTEN TO UTILIZE THE TSO
*                     SERVICE FACILITY (TSF) PROVIDED WITH
*                     TSO/E. THE TSF ROUTINE (IKJEFTSR),
*                     ACCESSED VIA THE LINK MACRO, ACTUALLY
*                     INVOKES THE REQUESTED PROGRAM AND ALLOWS
*                     PARAMETERS TO BE PASSED TO THE PROGRAM.
*                     THE LPA SEARCH AND BLDL SEARCH PORTIONS OF THE
*                     FORMER PROGRAM ARE NO LONGER NEEDED. ALSO,
*                     THE DOATTACH AND CORRESPONDING STAI ROUTINES
*                     HAVE BEEN REMOVED. ALL NON-IBM MACROS HAVE
*                     BEEN REMOVED, ALONG WITH THE ECT MAPPING MACRO.
*
*        LAST UPDATED 11 FEB 85 - JWC (*JWC MM/YY*)
*                     ADD 'NOMSG' PARAMETER TO ALLOW FOR
*                     SUPPRESSION OF ALL INFORMATIONAL MESSAGES.
*
*        LAST UPDATED 19 MAR 85 - JWC (*JWC MM/YY*)
*                     SET LENGTH OF PARM LIST PASSED TO CALLED PROGRAM
*                     TO DEFAULT AT ZERO. FIX PGM ABEND CODE DISPLAY
*
***********************************************************************
         PRINT NOGEN
         EJECT
$        CSECT
         SAVE  (14,12),,$-&SYSDATE-&SYSTIME   SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING $,R12                 ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LR    R6,R1                   PRESERVE PTR TO CPPL CNTL BLK
         GETMAIN R,LV=LDYNAMIC         GETMAIN DYNAMIC WORKAREA
         LR    R11,R1                  PRESERVE PTR TO WORKAREA
         ST    R13,4(R11)              STORE ADDRESS PREVIOUS SAVEAREA
         ST    R11,8(,R13)             STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R11                 LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         MVC   TSFNUM,FULL01           INITIALIZE TSFMSG
         LA    R1,TSFMSG                   OUTPUT LINE
         ST    R1,TSFADDR                      DESCRIPTOR
         MVC   RETNNUM,FULL01          INITIALIZE RETNMSG
         LA    R1,RETNMSG                  OUTPUT LINE
         ST    R1,RETNADDR                     DESCRIPTOR
         MVC   IOPB,FULL00             INIT CNTL & TPUT OPTION FLAGS
         MVC   PARMLEN(2),HALF00       INITIALIZE PARAMETER LENGTH (0)
***********************************************************************
*        BUILD PARSE PARAMETER AND I/O PARAMETER LISTS USING COMMAND  *
*        PROCESSOR PARAMETER LIST. INITIALIZE I/O CONTROL BLOCKS      *
***********************************************************************
*                                                                     *
         USING CPPL,R6                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,PPLSECT              LOAD ADDRESS OF PARSE PARM LIST
         USING PPL,R4                  ESTABLISH ADDRESSABILITY TO PPL
         LA    R5,IOPLSECT             LOAD ADDR OF PUTLINE PARM LIST
         USING IOPL,R5                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD PTR TO USER PROFILE TABLE
*                                      FROM CPPL
         L     R2,CPPLECT              LOAD PTR TO ENVIRONMENTAL
*                                      CONTROL TABLE FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REG AS ECB FOR PPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,PPLSECT           STORE ADDRESS OF UPT,ECT, AND
*                                      ECB IN PPL
         STM   R1,R3,IOPLSECT          STORE ADDRESS OT UPT, ECT, AND
*                                      ECB IN PPL
         LA    R3,IOPB                 LOAD ADDRESS OF PUTLINE
*                                      PARAMETER BLOCK
         ST    R3,IOPLIOPB             STORE IOPB ADDRESS IN IOPL
         MVC   PPLPCL,PCLADDR          MOVE PTR TO PARAMETER CONTROL
*                                      LIST INTO PPL
         LA    R2,PARSBACK             LOAD ADDRESS OF FULLWORD TO
*                                      CONTAIN ADDRESS OF PDE RETURNED
*                                      BY PARSE
         ST    R2,PPLANS               STORE PTR TO RETURN ADDRESS
         MVC   PPLCBUF,CPPLCBUF        MOVE PTR TO CMD BUFFER INTO PPL
         LA    R2,PPLUWA               LOAD ADDRESS OF USER WORKAREA
         ST    R2,PPLUWA               STORE ADDRESS IN PPL
         DROP  R4,R5,R6                DROP ADDRESSABILITY TO CPPL,
*                                      IOPL, AND PPL
         EJECT
***********************************************************************
*        CALLTSSR TO PARSE (IKJPARS). ON RETURN, PARSBACK POINTS TO   *
*        THE PDL. IF NEEDED, ISSUE PARSE SEVICE ROUTINE ERROR         *
***********************************************************************
         CALLTSSR EP=IKJPARS,MF=(E,PPLSECT)
         LTR   R15,R15                 IS RETURN CODE ZERO?
         BNZ   BADPARSE                NO, ISSUE ERROR MSG AND STOP
***********************************************************************
*        SET UP ADDRESSABILITY TO PDL. REG 4 - ADDR OF PGMNAME PDE    *
***********************************************************************
GOODPARS EQU   *
         L     R11,PARSBACK            R11 - PTR TO PARS ANSWER
         USING PDL,R11                 ESTABLISH ADDRESSABILITY TO PDL
         MVC   TSFLAGS,TSFLGS          TSO SERVICE ROUTINE PROCESS FLGS
         L     R4,MODNAME              R4 - PTR TO PROGRAM NAME
         TM    MODNAME+6,X'80'         IS PROGRAM NAME PRESENT?
         BNO   BADPARSE                NO, ISSUE ERROR MSG AND STOP
         LH    R5,MODNAME+4            R5 - LENGTH OF PROGRAM NAME
         ST    R5,PGMLEN               PGMLEN - PARM PASSED TO IKJEFTSR
         BCTR  R5,0                    DECREMENT FOR EXECUTE
         EX    R5,MVCMOD               MODNAME TO PGMNAME(FOR IKJEFTSR)
         L     R6,PDLPARM              R6 - PTR TO PROGRAM PARM LIST
         TM    PDLPARM+6,X'80'         IS PARAMETER LIST PRESENT?
         BNO   BUILDLST                NO, BUILD IKJEFTSR PARM LIST
         LH    R5,PDLPARM+4            R5 - LENGTH OF PARAMETER LIST
         CH    R5,HALF100              IS LENGTH OK?
         BNH   LENOK                   YES, PROCESS PARAMETER LIST
         LA    R10,8                   R10 - BRANCH ADDR FOR PARM ERR
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R5,100                  SET PARM LIST LENGTH AT MAX
LENOK    EQU   *
         CH    R5,HALF01               IS LENGTH OK?
         BL    BUILDLST                NO, BUILD IKJEFTSR PARM LIST
         STH   R5,PARMLEN              PARMLEN - PASSED TO IKJEFTSR
         BCTR  R5,0                    DECREMENT FOR EXECUTE
         EX    R5,MVCPARM              PDLPARM TO PARMLIST (IKJEFTSR)
         LA    R5,1(R5)                RESET R5 TO ORIGINAL VALUE
         EJECT
***********************************************************************
*        BUILD PARAMETER LIST TO BE PASSED TO IKJEFTSR VIA LINK       *
***********************************************************************
BUILDLST EQU   *
         LA    R2,TSFLAGS              R2 - ADDR OF TSF FLAGS
         ST    R2,TSFLGPTR                  TO BE STORED IN LINKLIST
         LA    R2,PGMNAME              R2 - ADDR OF PROGRAM NAME
         ST    R2,PGMNMPTR                  TO BE STORED IN LINKLIST
         LA    R2,PGMLEN               R2 - ADDR OF PROGRAM LENGTH
         ST    R2,PGMLNPTR                  TO BE STORED IN LINKLIST
         LA    R2,RETURNCD             R2 - ADDR OF PGM RETURN CODE
         ST    R2,RETCDPTR                  TO BE STORED IN LINKLIST
         LA    R2,REASONCD             R2 - ADDR OF PGM/TSF REASON CODE
         ST    R2,REASNPTR                  TO BE STORED IN LINKLIST
         LA    R2,ABENDCD              R2 - ADDR OF PROGRAM ABEND CODE
         ST    R2,ABECDPTR                  TO BE STORED IN LINKLIST
         LA    R2,PARMLEN              R2 - ADDR OF PROGRAM PARM LIST
         ST    R2,PARMADDR                  TO BE STORED IN PARMADDR
         OI    PARMADDR,X'80'          MARK PARMADDR AS LAST PARAMETER
*                                          PASSED TO IKJEFTSR
         LA    R2,PARMADDR             R2 - ADDR OF PGM PARAMETER LIST
         ST    R2,PARAMPTR                  TO BE STORED IN LINKLIST
         OI    PARAMPTR,X'80'          MARK PARAMPTR AS LAST PARAMETER
*                                          PASSED TO LINK
         B     LINKTSR                 LINK TSO SERVICE ROUTINE
ABECDEND EQU   *
         OI    ABECDPTR,X'80'          MARK ABECDPTR AS LAST PARAMETER
*                                          PASSED TO LINK
LINKTSR  EQU   *
         LINK  EP=IKJEFTSR,                                            X
               MF=(E,LINKLIST)         NO, INVOKE IKJEFTSR WITHOUT
***********************************************************************
*        CHECK RETURN CODE PASSED BACK FROM IKJEFTSR                  *
***********************************************************************
ERRCHECK EQU   *
         C     R15,FULL00              RETURN CODE = 00?
         BNE   CHK04                   NO, CHECK FOR RC = 04
         B     FINISH                  RETURN
CHK04    EQU   *
         C     R15,FULL04              RETURN CODE = 04?
         BNE   CHK08                   NO, CHECK FOR RC = 08
         LA    R10,12                  R10 - BRANCH ADDR - NON-ZERO MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R7,RETURNCD             R7 - RETURN CODE FROM PROGRAM
         LA    R10,4                   R10 - BRANCH ADDR - RETURN CODE
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,RETURNCD            R15 - PROGRAM RETURN CODE
         B     FINISH                  RETURN
         EJECT
CHK08    EQU   *
         C     R15,FULL08              RETURN CODE = 08?
         BNE   CHK12                   NO, CHECK FOR RC = 12
         LA    R10,16                  R10 - BRANCH ADDR - ATTENTN MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL08              R15 - $ RETURN CODE
         B     FINISH                  RETURN
CHK12    EQU   *
         C     R15,FULL12              RETURN CODE = 12?
         BNE   CHK16                   NO, CHECK FOR RC = 16
         LA    R10,20                  R10 - BRANCH ADDR - PGMABEND MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LH    R7,ABENDCD+1            R7 - ABEND CODE FROM IKJEFTSR
         SRL   R7,4                    COMPENSATE FOR BAD ALLIGNMENT
         L     R8,REASONCD             R8 - REASON CODE FROM IKJEFTSR
         LA    R10,24                  R10 - BRANCH ADDR - ABEND/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LH    R15,ABENDCD+1           R15 - $ RETURN CODE
         SRL   R15,4                   COMPENSATE FOR BAD ALLIGNMENT
         B     FINISH                  RETURN
CHK16    EQU   *
         C     R15,FULL16              RETURN CODE = 16?
         BNE   CHK20                   NO, CHECK FOR RC = 20
         LA    R10,28                  R10 - BRANCH ADDR - TSF ERR MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LR    R7,R15                  R7 - RETURN CODE FROM IKJEFTSR
         LA    R10,4                   R10 - BRANCH ADDR - RETURN CODE
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL16              R15 - $ RETURN CODE
         B     FINISH                  RETURN
CHK20    EQU   *
         C     R15,FULL20              RETURN CODE = 20?
         BNE   CHK24                   NO, CHECK FOR RC = 24
         ABEND 405,DUMP
         LR    R7,R15                  R7 - RETURN CODE FROM IKJEFTSR
         L     R8,REASONCD             R8 - BRANCH ADDR - REASON CODE
         B     REASONS(R8)             BRANCH TO CORRECT REASON ROUTINE
REASONS  DS    F                       NO ZERO REASON CODE
         B     REASON01                TSO SERVICE FACILITY ERROR
         B     REASON01                             "
         B     REASON01                             "
         B     REASON01                             "
         B     REASON01                             "
         B     REASON02                PGM INVOKED IN NON-TSO ENVIRONMT
         B     REASON01                TSO SERVICE FACILITY ERROR
         B     REASON01                             "
         B     REASON01                             "
         B     REASON03                PROGRAM NOT FOUND ERROR
         B     REASON04                SYNTAX ERROR IN PROGRAM NAME
         B     REASON01                TSO SERVICE FACILITY ERROR
         B     REASON05                PGM NOT SUPPORTED IN BACKGROUND
         B     REASON06                PGM NOT IN AUTHORIZED LIBRARY
REASON01 EQU   *
         LA    R10,28                  R10 - BRANCH ADDR - TSF ERR MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
         EJECT
REASON02 EQU   *
         LA    R10,36                  R10 - BRANCH ADDR - NON-TSO MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
REASON03 EQU   *
         LA    R10,40                  R10 - BRANCH ADDR - NOT FOUND
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
REASON04 EQU   *
         LA    R10,44                  R10 - BRANCH ADDR - SYNTAX ERROR
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
REASON05 EQU   *
         LA    R10,48                  R10 - BRANCH ADDR - NON-SUPPORT
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
REASON06 EQU   *
         LA    R10,52                  R10 - BRANCH ADDR - AUTHORIZED
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,32                  R10 - BRANCH ADDR - RETURN/REASN
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL20              R15 - $ RETURN CODE
         B     FINISH                  RETURN
CHK24    EQU   *
         C     R15,FULL24              RETURN CODE = 24?
         BNE   BADRETN                 NO, BAD RETURN CODE
         LA    R10,28                  R10 - BRANCH ADDR - TSF ERR MSG
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LR    R7,R15                  R7 - RETURN CODE FROM IKJEFTSR
         LA    R10,4                   R10 - BRANCH ADDR - RETURN CODE
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         L     R15,FULL24              R15 - $ RETURN CODE
         B     FINISH                  RETURN
BADPARSE EQU   *
         LR    R7,R15                  R7 - RETURN CODE FROM REG 15
         LA    R10,0                   R10 - BRANCH ADDR FOR PARSE ERR
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,4                   R10 - BRANCH ADDR FOR RETNCD
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LR    R15,R7                  R15 - $ RETURN CODE
         B     FINISH                  RETURN
         EJECT
BADRETN  EQU   *
         LR    R7,R15                  R7 - RETURN CODE FROM IKJEFTSR
         LA    R10,56                  R10 - BRANCH ADDR - BAD RETNCD
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LA    R10,4                   R10 - BRANCH ADDR - RETURN CODE
         BAL   R9,MSGHANDL             BRANCH TO ERROR HANDLING ROUTINE
         LR    R15,R7                  R15 - $ RETURN CODE
         B     FINISH                  RETURN
***********************************************************************
*        MESSAGE HANDLING ROUTINE                                     *
***********************************************************************
MSGHANDL EQU   *
         B     LOADMSG(R10)            BRANCH TO CORRECT ERROR ROUTINE
LOADMSG  B     LOADPARS                PARSE ERROR
         B     LOADRET1                RETURN CODE
         B     LOADPARM                PARM LIST ERROR
         B     LOADNON0                PGM COMPLETED - NON-ZERO RETNCD
         B     LOADATTN                PGM TERMINATED BY AN ATTENTION
         B     LOADABEN                PROGRAM ABEND MESSAGE
         B     LOADRET2                ABEND AND REASON CODE
         B     LOADTSF                 TSO SERVICE FACILITY ERROR
         B     LOADRET3                RETURN AND REASON CODE
         B     LOADTSO                 FACILITY IN NON-TSO ENVIRONMENT
         B     LOADNOT                 PROGRAM NOT FOUND ERROR
         B     LOADSYN                 PROGRAM SYNTAX ERROR
         B     LOADSUPP                PROGRAM NOT SUPPORTED BACKGND
         B     LOADAUTH                PROGRAM NOT IN AUTHORIZED LIB
         B     LOADRETN                BAD RETURN CODE RECEIVED
LOADPARS EQU   *
         MVC   TSFMSG(76),TSFMSG0      MOVE PARSE ERROR TO TSF MESSAGE
         B     PUTMSG
LOADPARM EQU   *
         MVC   TSFMSG(76),TSFMSG1      MOVE PARM ERROR TO TSF MESSAGE
         B     PUTMSG
LOADNON0 EQU   *
         MVC   TSFMSG(76),TSFMSG2      MOVE NON-ZERO TO TSF MESSAGE
         B     PUTMSG
LOADATTN EQU   *
         MVC   TSFMSG(76),TSFMSG3      MOVE ATTENTION TO TSF MESSAGE
         B     PUTMSG
LOADABEN EQU   *
         MVC   TSFMSG(76),TSFMSG4      MOVE PGM ABEND TO TSF MESSAGE
         B     PUTMSG
LOADTSF  EQU   *
         MVC   TSFMSG(76),TSFMSG5      MOVE TSF ERROR TO TSF MESSAGE
         B     PUTMSG
LOADTSO  EQU   *
         MVC   TSFMSG(76),TSFMSG6      MOVE TSO ERROR TO TSF MESSAGE
         B     PUTMSG
LOADNOT  EQU   *
         MVC   TSFMSG(76),TSFMSG7      MOVE NOT FOUND TO TSF MESSAGE
         B     PUTMSG
         EJECT
LOADSYN  EQU   *
         MVC   TSFMSG(76),TSFMSG8      MOVE SYNTAX ERR TO TSF MESSAGE
         B     PUTMSG
LOADSUPP EQU   *
         MVC   TSFMSG(76),TSFMSG9      MOVE NON-SUPPORT TO TSF MESSAGE
         B     PUTMSG
LOADAUTH EQU   *
         MVC   TSFMSG(76),TSFMSGA      MOVE NON-AUTHRZD TO TSF MESSAGE
         B     PUTMSG
LOADRETN EQU   *
         MVC   TSFMSG(76),TSFMSGB      MOVE BAD RETNCD TO TSF MESSAGE
         B     PUTMSG
LOADRET1 EQU   *
         ST    R7,DEC                  STORE RETURN CODE
         MVC   DEC(3),DEC+1            SHIFT DATA LEFT ONE BYTE
         UNPK  CONDATA,DEC+1           UNPACK
         TR    CONDATA(8),TRANS        MAKE IT PRINTABLE
         MVC   RETNNUM1(4),CONDATA+3   MOVE TO OUTPUT AREA
         MVC   RETNMSG1,=C'RETURN CODE = '
         MVC   RETNLEN,HALF42          MOVE RETNLEN TO RETNMSG
         B     PUTRETN
LOADRET2 EQU   *
         ST    R7,DEC                  STORE RETURN CODE
         MVC   DEC(3),DEC+1            SHIFT DATA LEFT ONE BYTE
         UNPK  CONDATA,DEC+1           UNPACK
         TR    CONDATA(8),TRANS        MAKE IT PRINTABLE
         MVC   RETNNUM1(4),CONDATA+3   MOVE TO OUTPUT AREA
         MVC   RETNMSG1,=C' ABEND CODE = '
         ST    R8,DEC                  STORE RETURN CODE
         MVC   DEC(3),DEC+1            SHIFT DATA LEFT ONE BYTE
         UNPK  CONDATA,DEC+1           UNPACK
         TR    CONDATA(8),TRANS        MAKE IT PRINTABLE
         MVC   RETNNUM2(4),CONDATA+3   MOVE TO OUTPUT AREA
         MVC   RETNMSG2,=C'  REASON CODE = '
         MVC   RETNLEN,HALF42          MOVE RETNLEN TO RETNMSG
         B     PUTRETN
LOADRET3 EQU   *
         ST    R7,DEC                  STORE RETURN CODE
         MVC   DEC(3),DEC+1            SHIFT DATA LEFT ONE BYTE
         UNPK  CONDATA,DEC+1           UNPACK
         TR    CONDATA(8),TRANS        MAKE IT PRINTABLE
         MVC   RETNNUM1(4),CONDATA+3   MOVE TO OUTPUT AREA
         MVC   RETNMSG1,=C'RETURN CODE = '
         ST    R8,DEC                  STORE RETURN CODE
         MVC   DEC(3),DEC+1            SHIFT DATA LEFT ONE BYTE
         UNPK  CONDATA,DEC+1           UNPACK
         TR    CONDATA(8),TRANS        MAKE IT PRINTABLE
         MVC   RETNNUM2(4),CONDATA+3   MOVE TO OUTPUT AREA
         MVC   RETNMSG2,=C'  REASON CODE = '
         MVC   RETNLEN,HALF42          MOVE RETNLEN TO RETNMSG
         B     PUTRETN
         EJECT
PUTMSG   EQU   *
         CLI   NOMESSG+1,X'00'         SHOULD MESSAGE BE ISSUED?
         BNZ   CLEARIT1                NO, RETURN ('NOMSG' KEYWD)
         PUTLINE PARM=IOPB,                                            X
               OUTPUT=(TSFOLD,TERM,SINGLE,INFOR),                      X
               MF=(E,IOPLSECT)         ISSUE APPROPRIATE TSF MESSAGE
         LTR   R15,R15                 CHECK PUTLINE RETURN CODE
         BZ    CLEARIT1                IF ZERO, CLEAR MSG LINE
         ABEND 405,DUMP                ABEND FOR DUMP
CLEARIT1 EQU   *
         MVC   TSFMSG+4(72),SPACES     CLEAR MESSAGE AREA
         BR    R9                      RETURN
PUTRETN  EQU   *
         CLI   NOMESSG+1,X'00'         SHOULD MESSAGE BE ISSUED?
         BNZ   CLEARIT2                NO, RETURN ('NOMSG' KEYWD)
         PUTLINE PARM=IOPB,                                            X
               OUTPUT=(RETNOLD,TERM,SINGLE,INFOR),                     X
               MF=(E,IOPLSECT)         ISSUE APPROPRIATE ERR MESSAGE
CLEARIT2 EQU   *
         MVC   RETNMSG+4(38),SPACES    CLEAR RETURN CODE MESSAGE
         BR    R9                      RETURN
***********************************************************************
*        CLEANUP AREAS GETMAINED BY PARSE AND PGM, SET RETURN CODE,   *
*        RESTORE REGISTERS AND EXIT                                   *
***********************************************************************
FINISH   EQU   *
         LR    R2,R15                  SAVE RETURN CODE IN R2
         IKJRLSA PARSBACK              RELEASE VIRTUAL STORAGE
*                                          ALLOCATED BY PARSE
         LA    R0,LDYNAMIC             R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         LR    R15,R2                  RETURN RETURN CODE TO R15
         RETURN (14,12),,RC=(15)       RESTORE REGISTERS AND RETURN
***********************************************************************
*        EXECUTED INSTRUCTIONS                                        *
***********************************************************************
MVCMOD   MVC   PGMNAME(0),0(R4)        <<EXECUTED>>
MVCPARM  MVC   PARMLIST(0),0(R6)       <<EXECUTED>>
***********************************************************************
*        GENERAL DECLARE STATEMENTS                                   *
***********************************************************************
HALF00   DC    H'00'                   CONSTANT
HALF01   DC    H'01'                   CONSTANT
HALF42   DC    H'42'                   CONSTANT
HALF100  DC    H'100'                  CONSTANT
FULL00   DC    F'00'                   CONSTANT
FULL01   DC    F'01'                   CONSTANT
FULL02   DC    F'02'                   CONSTANT
FULL04   DC    F'04'                   CONSTANT
FULL08   DC    F'08'                   CONSTANT
FULL12   DC    F'12'                   CONSTANT
FULL16   DC    F'16'                   CONSTANT
FULL20   DC    F'20'                   CONSTANT
FULL24   DC    F'24'                   CONSTANT
TSFLGS   DS    0F
         DC    XL2'0000'               FLGS 1 & 2 PASSED TO IKJEFTSR
         DC    XL1'00'                 FLG 3 - NO DUMP TO BE TAKEN
         DC    XL1'02'                 FLG 4 - PROGRAM SPECIFICATION
SPACES   DC    80C' '                  CONSTANT
PCLADDR  DC    A(PCL)                  PCL ADDRESS TO BE PASSED TO PPL
         DS    0F
TRANS    EQU   *-C'0'
         DC    C'0123456789ABCDEF'
         DS    0F
         EJECT
***********************************************************************
*        ERROR AND INFORMATIONAL MESSAGES                             *
***********************************************************************
TSFMSG0  DC    AL2(TSFMSG0L)
         DC    AL2(0)
         DC    CL72'AN ERROR WAS ENCOUNTERED IN THE PARSE SERVICE ROUTIX
               NE.'
TSFMSG0L EQU   *-TSFMSG0
TSFMSG1  DC    AL2(TSFMSG1L)
         DC    AL2(0)
         DC    CL72'PARM FIELD TOO LONG. TRUNCATED AT 100 CHARACTERS'
TSFMSG1L EQU   *-TSFMSG1
TSFMSG2  DC    AL2(TSFMSG2L)
         DC    AL2(0)
         DC    CL72'PROGRAM COMPLETED WITH NON-ZERO RETURN CODE. RETURNX
                CODE FOLLOWS'
TSFMSG2L EQU   *-TSFMSG2
TSFMSG3  DC    AL2(TSFMSG3L)
         DC    AL2(0)
         DC    CL72'INVOKED PROGRAM WAS TERMINATED BECAUSE OF AN ATTENTX
               ION'
TSFMSG3L EQU   *-TSFMSG3
TSFMSG4  DC    AL2(TSFMSG4L)
         DC    AL2(0)
         DC    CL72'PROGRAM ABENDED. ABEND AND REASON CODES FOLLOW'
TSFMSG4L EQU   *-TSFMSG4
TSFMSG5  DC    AL2(TSFMSG5L)
         DC    AL2(0)
         DC    CL72'TSO SERVICE FACILITY ERROR - CONTACT SYSTEMS PERSONX
               NEL'
TSFMSG5L EQU   *-TSFMSG5
TSFMSG6  DC    AL2(TSFMSG6L)
         DC    AL2(0)
         DC    CL72'FACILITY MAY ONLY BE INVOKED IN TSO ENVIRONMENT(FORX
               EGROUND / BACKGROUND)'
TSFMSG6L EQU   *-TSFMSG6
TSFMSG7  DC    AL2(TSFMSG7L)
         DC    AL2(0)
         DC    CL72'REQUESTED PROGRAM NOT FOUND. RE-ENTER COMMAND'
TSFMSG7L EQU   *-TSFMSG7
         EJECT
TSFMSG8  DC    AL2(TSFMSG8L)
         DC    AL2(0)
         DC    CL72'IKJSCAN DETECTED A SYNTAX ERROR IN THE PROGRAM NAMEX
               . RE-ENTER COMMAND'
TSFMSG8L EQU   *-TSFMSG8
TSFMSG9  DC    AL2(TSFMSG9L)
         DC    AL2(0)
         DC    CL72'REQUESTED PROGRAM NOT SUPPORTED IN A BACKGROUND ENVX
               IRONMENT'
TSFMSG9L EQU   *-TSFMSG9
TSFMSGA  DC    AL2(TSFMSGAL)
         DC    AL2(0)
         DC    CL72'PROGRAM IS AUTHORIZED, BUT COPY CANNOT BE FOUND IN X
               AN AUTHORIZED LIBRARY'
TSFMSGAL EQU   *-TSFMSGA
TSFMSGB  DC    AL2(TSFMSGBL)
         DC    AL2(0)
         DC    CL72'BAD RETURN CODE RECEIVED FROM EITHER IKJEFTSR OR LIX
               NK. RETN CODE FOLLOWS'
TSFMSGBL EQU   *-TSFMSGB
***********************************************************************
*        GENERAL REGISTER EQUATES                                     *
***********************************************************************
         LTORG
PATCH    DC    50S(*)                  GENERATE SCON'S FOR PATCH AREA
R0       EQU   0                       WORK REGISTER
R1       EQU   1                       PARAMETER PTR & WORK REGISTER
R2       EQU   2                       WORK REGISTER
R3       EQU   3                       WORK REGISTER
R4       EQU   4                       PTR TO PGM NAME & WORK REGISTER
R5       EQU   5                       PGM LENGTH & WORK REGISTER
R6       EQU   6                       PTR TO PGM PARM LIST & WORK REG
R7       EQU   7                       ABEND & RETURN CODE CONVERSION
R8       EQU   8                       REASON CODE CONVERSION
R9       EQU   9                       BAL REGISTER TO MSG HANDLER
R10      EQU   10                      BRANCH REGISTER - MSG HANDLER
R11      EQU   11                      PTR TO PARSE ANS & WORK REGISTER
R12      EQU   12                      BASE REG
R13      EQU   13                      SAVE AREA ADDRESS
R14      EQU   14                      OS LINKAGE REGISTER
R15      EQU   15                      OS LINKAGE REG AND RETURN CODE
         EJECT
***********************************************************************
*        DSECTS FOR PARSE CONTROL BLOCKS                              *
***********************************************************************
PCL      IKJPARM DSECT=PDL
MODNAME  IKJIDENT 'PROGRAM',MAXLNTH=8,FIRST=ALPHANUM,                  X
               OTHER=ALPHANUM,PROMPT='PROGRAM NAME',                   X
               HELP='NAME OF PROGRAM TO BE CALLED'
PDLPARM  IKJPOSIT QSTRING,HELP='PARAMETER FIELD FOR PROGRAM'
NOMESSG  IKJKEYWD
         IKJNAME 'NOMSG'
         IKJENDP
***********************************************************************
*        TSO SERVICE FACILITY MAPPING MACROS                          *
***********************************************************************
         CVT   DSECT=YES               CVT MAPPING MACRO
         IKJCPPL                       COMMAND PROCESSOR PARAMETER LIST
CPPLLEN  EQU   *-CPPL
         IKJPPL                        PARSE PARAMETER LIST
PPLLEN   EQU   *-PPL
         IKJIOPL                       I / O PARAMETER LIST
IOPLLEN  EQU   *-IOPL
         EJECT
***********************************************************************
*        DSECT FOR GETMAINED WORKAREA                                 *
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
DEC      DS    F                       RETURN CODE CONVERSION (PACKED)
ECB      DS    F                       EVENT CONTROL BLOCK
PPLSECT  DS    0F                      PARSE PARAMETER LIST
         ORG   PPLSECT+PPLLEN
IOPLSECT DS    0F                      I / O PARAMETER LIST
         ORG   IOPLSECT+IOPLLEN
PARSBACK DS    F                       ADDRESS OF PDE RETURNED BY PARSE
TSFLAGS  DS    F                       TSO SERVICE FACILITY FLAGS
PGMNAME  DS    CL8                     PROGRAM TO BE EXECUTED
PGMLEN   DS    F                       LENGTH OF PGMNAME
RETURNCD DS    F                       PROGRAM RETURN CODE
REASONCD DS    F                       TSO SERVICE FACILITY REASON CODE
ABENDCD  DS    F                       ABEND CODE
PARMLEN  DS    H                       LEN OF PROGRAM PARAMETER LIST
PARMLIST DS    CL100                   PROGRAM PARAMETER LIST
PARMADDR DS    A                       ADDRESS OF PGM PARAMETER LIST
LINKLIST DS    0F
TSFLGPTR DS    F                       PTR TO TSO SERVICE FACILITY FLGS
PGMNMPTR DS    F                       PTR TO PROGRAM TO BE EXECUTED
PGMLNPTR DS    F                       PTR TO LENGTH OF PGMNAME
RETCDPTR DS    F                       PTR TO PROGRAM RETURN CODE
REASNPTR DS    F                       PTR TO PGM/IKJEFTSR REASON CODE
ABECDPTR DS    F                       PTR TO PROGRAM ABEND CODE
PARAMPTR DS    F                       PTR TO ADDR OF PGM PARAMETER LST
CONDATA  DS    XL9                     RETURN CODE CONVERSION
         DS    0F
TSFOLD   DS    0FL2                    TSFMSG OUTPUT LINE DESCRIPTOR
TSFNUM   DS    F                       NUMBER OF TSFMSG OUTPUT LINES
TSFADDR  DS    F                       ADDRESS OF TSFMSG
TSFMSG   DS    0CL76                   INFORMATIONAL OR ERROR MESSAGE
TSFLEN   DS    2H                      LENGTH OF MESSAGE (76)
TSFLINE  DS    CL72                    MESSAGE TEXT
RETNOLD  DS    0FL2                    RETNMSG OUTPUT LINE DESCRIPTOR
RETNNUM  DS    F                       NUMBER OF RETNMSG OUTPUT LINES
RETNADDR DS    F                       ADDRESS OF RETNMSG
RETNMSG  DS    0CL42                   RETURN, ABEND & REASON CODE MSG
RETNLEN  DS    2H                      LENGTH OF MESSAGE (42)
RETNMSG1 DS    CL14                    'RETURN CODE =' / 'ABEND CODE ='
RETNNUM1 DS    CL4                     RETURN OR ABEND CODE NUMBER
RETNMSG2 DS    CL16                    'REASON CODE ='
RETNNUM2 DS    CL4                     REASON CODE MESSAGE
IOPB     PUTLINE MF=L                  LIST FORM OF PUTLINE MACRO
LDYNAMIC EQU   *-WORKAREA
         END   $
