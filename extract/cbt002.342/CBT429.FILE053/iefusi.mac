         TITLE 'IEFUSI - SMF STEP INITIATION EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUSI                                    *
*                                                                     *
*             DESCRIPTIVE NAME = SMF STEP INITIATION EXIT             *
*                                                                     *
*             FUNCTION =                                              *
*                EXIT ALLOWS INSTALLATION TO INCREASE JOB STEP        *
*                REGION SIZE TO A 4096K MINIMUM IF NEEDED. ALSO,      *
*                A TABLE SCAN WILL BE PERFORMED TO KEEP '3090 -       *
*                ONLY' LICENCED PROGRAMS FROM RUNNING ON THE 3081.    *
*                A RACHECK WILL BE PERFORMED TO KEEP UN-AUTHORIZED    *
*                USERS FROM EXECUTING AUTHORIZED PROGRAMS.            *
*                                                                     *
*                OPERATION =                                          *
*                   SAVE REGISTERS AND ESTABLISH ADDRESSABILITY       *
*                   GETMAIN WORKAREA                                  *
*                   SCAN 'PGMAUTH' TABLE FOR MATCH                    *
*                       IF FOUND, RACHECK FOR APPL OF PGMAUTH         *
*                       IF RC = 0, CONTINUE. OTHERWISE CANCEL         *
*                   SCAN 'PGM3090' TABLE FOR MATCH                    *
*                       IF FOUND, CHECK FOR SYSID OF ADM2.            *
*                       SYSID = ADM2, CONTINUE. OTHERWISE CANCEL      *
*                   IF REGION SIZE IS LESS THAN 4096K, SET REGION     *
*                       SIZE TO 4096K                                 *
*                   FREEMAIN WORKAREA                                 *
*                   RESTORE REGISTERS                                 *
*                   EXIT                                              *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTER  0       = NOT USED                     *
*                    REGISTER  1       = PARAMETER LIST POINTER       *
*                    REGISTERS 2 - 5   = NOT USED                     *
*                    REGISTERS 6       = WORK REG / ADDR OF 'PGMNAME' *
*                    REGISTERS 7       = ADDRESS OF 'SYSID'           *
*                    REGISTER  8       = PARM LIST ADDRESSABILITY     *
*                    REGISTERS 9 - 11  = NOT USED                     *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA POINTER            *
*                    REGISTER  14      = RETURN                       *
*                    REGISTER  15      = ENTRY POINT ADDRESS AND      *
*                                        RETURN CODE REGISTER         *
*                                                                     *
*             MACROS =                                                *
*                SAVE, RETURN, EQUATE, RACCHECK,                      *
*                GETMAIN, FREEMAIN                                    *
*                                                                     *
*             MAPPING MACROS USED =                                   *
*                CVT, IEESMCA                                         *
*                                                                     *
*             ASSEMBLER OPTIONS = LIST,RENT                           *
*                                                                     *
*             LINKAGE EDITOR OPTIONS = LET,LIST,XREF,RENT,REUS,REFR   *
*                                                                     *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                08/01/85  WRITTEN.                             JWC.  *
*                                                                     *
*                01/19/87  ADD RACLIST/FRACHECK FOR PROGRAMS WHICH    *
*                          CAN ONLY RUN ON THE 3090.            JWC.  *
*                                                                     *
*                07/09/87  REMOVE RACLIST/FRACHECK. ADD PGMAUTH AND   *
*                          PGM3090 TABLES AND RACHECK.          JWC.  *
*                                                                     *
*                09/04/87  ADDED CHECK FOR REGION=0 AND IF TRUE       *
*                          ALLOW ALLOCATION OF LIMIT VALUES.    MJL.  *
*                                                                     *
***********************************************************************
***********************************************************************
*        INITIALIZATION
***********************************************************************
IEFUSI   CSECT                         SMF STEP INITIATION EXIT
         SAVE  (14,12),,IEFUSI_DELUXE_CHECK_&SYSDATE_&SYSTIME
*                                      SAVE REGISTERS
         LR    R12,R15                 R12 - BASE REGISTER
         USING IEFUSI,R12              ESTABLISH ADDRESSABILITY
         LR    R8,R1                   PRESERVE PTR TO PARMS
         USING SIPLBEGN,R8             ESTABLISH ADDRESSABILITY TO
*                                      INPUT PARAMETER LIST DSECT
         GETMAIN R,LV=WORKLEN
*                                      GETMAIN DYNAMIC STORAGE
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
*                                      IN CURRENT SAVEAREA
         ST    R1,8(R13)               STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  R13 - ADDRESS OF GETMAINED AREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*        PERFORM RACHECK FOR AUTHORIZED PROGRAMS
***********************************************************************
RACF     DS    0H
         L     R2,SIPLPGM              ADDRESS OF PROGRAM NAME
         LA    R3,PGMAUTH              ADDRESS TABLE OF AUTHORIZED PGMS
         SLR   R4,R4                   CLEAR REG 4
RACF10   DS    0H
         CLC   SPACES,0(R3)            END OF TABLE?
         BE    SYSID                   YES, CHECK FOR CORRECT CPU
         IC    R4,8(R3)                GET LENGTH OF PGM
         EX    R4,CLCPGM               AUTHORIZED PROGRAM?
         BE    RACF20                  YES, CHECK AUTHORITY
         LA    R3,9(R3)                POINT TO NEXT AUTHORIZED PGM
         B     RACF10                  CONTINUE CHECKING
RACF20   DS    0H
         MVC   RACDYN,RACSTAT          MOVE DYNAMIC TO STATIC
         RACHECK ENTITY=PGMAPPL,CLASS=APPL,                            +
               MF=(E,RACDYN)
         LTR   R15,R15                 USER AUTHORIZED?
         BNZ   CANJOB10                NO, CANCEL JOB / ISSUE MESSAGE
         B     SYSID                   YES, CHECK FOR CORRECT CPU
***********************************************************************
*        OBTAIN SYSTEM ID AND CHECK FOR 3090 PROGRAMS
***********************************************************************
SYSID    DS    0H
         L     R6,CVTPTR               R6 - ADDRESS OF CVT
         USING CVT,R6                  ESTABLISH ADDRESSABLITY TO CVT
         L     R7,CVTSMCA              R7 - ADDRESS OF SMCA
         USING SMCABASE,R7             ESTABLISH ADDRESSABLITY TO SMCA
         L     R2,SIPLPGM              ADDRESS OF PROGRAM NAME
         LA    R3,PGM3090              ADDRESS TABLE OF 3090 PGMS
         SLR   R4,R4                   CLEAR REG 4
SYSID10  DS    0H
         CLC   SPACES,0(R3)            END OF TABLE?
         BE    CHKREGN                 YES, CHECK REGION SIZE
         IC    R4,8(R3)                GET LENGTH OF PGM
         EX    R4,CLCPGM               3090 PROGRAM?
         BE    SYSID20                 YES, CHECK FOR CORRECT SYSID
         LA    R3,9(R3)                POINT TO NEXT 3090 PGM
         B     SYSID10                 CONTINUE CHECKING
SYSID20  DS    0H
         CLC   ADM2,SMCASID            IS THIS THE CORRECT CPU?
         BNE   CANJOB00                NO, ISSUE MESSAGE AND RETURN
         B     CHKREGN                 CHECK REGION SIZE
CLCPGM   CLC   0(0,R3),0(R2)           PROGRAM FOUND?
***********************************************************************
*        REGION SIZE CHECKING
***********************************************************************
CHKREGN  DS    0H
         L     R4,SIPLVSM              R4 - ADDRESS OF VSM PARM LIST
         USING VSPLBEGN,R4             ESTABLISH ADDRESSABILITY TO
*                                      VSM PARAMETER LIST DSECT
         CLC   VSPLREGR,ZEROMEG        REGION SIZE REQUESTED = 0?
         BNE   REGNCONT                NO, PERFORM RACF PROCESSING
         OI    VSPLFLAG,X'80'          TURN ON BIT 0 TO INDICATE THAT
*                                      IEFUSI HAS SET REGION VALUES
         B     FINISH                  WE'RE DONE
*
REGNCONT EQU   *
         CLC   VSPLREGR,FOURMEG        REGION SIZE REQUESTED < 4096K?
         BNL   FINISH                  NO, PERFORM RACF PROCESSING
         OI    VSPLFLAG,X'80'          TURN ON BIT 0 TO INDICATE THAT
*                                      IEFUSI HAS SET REGION VALUES
         MVC   VSPLREGR,FOURMEG        SET REG SIZE REQUESTED TO 4096K
         MVC   VSPLGETB,FOURMEG        SET GETMAIN LIMIT BELOW 16 MEG
         MVC   VSPLREGB,FOURMEG        SET REGION LIMIT BELOW 16 MEG
         B     FINISH                  WE'RE DONE
***********************************************************************
*        PROCESSING SHOULD BE CANCELED
***********************************************************************
CANJOB00 DS    0H
         WTO   'JOB CANCELED BY SMF EXIT - IEFUSI. THIS PROGRAM MUST BE+
                RUN ON THE 3090 CPU',ROUTCDE=(11)
         LA    R15,4                   SET REG15 (RETURN CODE) TO X'04'
         B     FINISH                  WE'RE DONE
CANJOB10 DS    0H
         WTO   'JOB CANCELED BY SMF EXIT - IEFUSI. USER NOT AUTHORIZED +
               TO EXECUTE THIS PROGRAM',ROUTCDE=(11)
         LA    R15,4                   SET REG15 (RETURN CODE) TO X'04'
         B     FINISH                  WE'RE DONE
***********************************************************************
*        WRAP THINGS UP AND RETURN
***********************************************************************
FINISH   DS    0H
         LR    R4,R15                  R4 - SAVE RETURN CODE
         LR    R7,R13                  R7 - ADDRESS OF DYNAMIC WORKAREA
         L     R13,4(R13)              RESTORE REGISTER 13
         FREEMAIN R,LV=WORKLEN,A=(R7)
*                                      FREEMAIN DYNAMIC STORAGE
         LR    R15,R4                  R15 - RESTORE RETURN CODE
         RETURN (14,12),,RC=(15)       RETURN
***********************************************************************
*        CONSTANTS
***********************************************************************
HALF01   DC    H'01'                   HALFWORD CONSTANT, VALUE X'01'
FULL08   DC    F'08'                   FULLWORD CONSTANT, VALUE X'08'
SPACES   DC    CL8' '                  END-OF-TABLE
ADM2     DC    CL4'ADM2'               SYSID
FOURMEG  DC    A(4096*1024)            FOUR MEG REGION SIZE
ZEROMEG  DC    A(0)                    REGION=ZERO REQUESTED
APPL     DS    0F
         DC    AL1(4)                  LENGTH OF CLASS NAME
         DC    CL4'APPL'               CLASS NAME
PGMAPPL  DC    CL8'PGMAUTH'            APPL NAME
***********************************************************************
*        PROGRAMS PROTECTED FROM GENERAL USE
***********************************************************************
PGMAUTH  DS    0F
         DC    CL8'FATS',AL1(3)
         DC    CL8'FATAR',AL1(4)
         DC    CL8'GIMSMP',AL1(5)
         DC    CL8'ICPIOCP',AL1(6)
         DC    CL8'IOPIOCP',AL1(6)
         DC    CL8'ICKDSF',AL1(5)
         DC    CL8'AMASPZAP',AL1(7)
         DC    CL8'IMASPZAP',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
         DC    CL8'        ',AL1(7)
***********************************************************************
*        PROGRAMS FOR THE 3090 ONLY
***********************************************************************
PGM3090  DS    0F
         DC    CL8'EMG',AL1(2)
         DC    CL8'DFS',AL1(2)
         DC    CL8'DSQ',AL1(2)
         DC    CL8'DSN',AL1(2)
         DC    CL8'ACCPREJB',AL1(7)
         DC    CL8'ADDSCPID',AL1(7)
         DC    CL8'ATTRHELP',AL1(7)
         DC    CL8'ATTRTABL',AL1(7)
         DC    CL8'ATTRTBLE',AL1(7)
         DC    CL8'BATCHPLT',AL1(7)
         DC    CL8'BLDNDX',AL1(5)
         DC    CL8'BLDTIER',AL1(6)
         DC    CL8'BUFFPLOT',AL1(7)
         DC    CL8'CADCD',AL1(4)
         DC    CL8'CADCHR',AL1(5)
         DC    CL8'CADCHRB',AL1(6)
         DC    CL8'CADCHRBL',AL1(7)
         DC    CL8'CADCHRHI',AL1(7)
         DC    CL8'CADCHRLO',AL1(7)
         DC    CL8'CADCLRTB',AL1(7)
         DC    CL8'CADDIMST',AL1(7)
         DC    CL8'CADET',AL1(4)
         DC    CL8'CADFMTBC',AL1(7)
         DC    CL8'CADICTON',AL1(7)
         DC    CL8'CADMACGM',AL1(7)
         DC    CL8'CADPARMC',AL1(7)
         DC    CL8'CADPLGEN',AL1(7)
         DC    CL8'CADSEL',AL1(5)
         DC    CL8'CADSELB',AL1(6)
         DC    CL8'CADSTAT1',AL1(7)
         DC    CL8'CADSTAT2',AL1(7)
         DC    CL8'CADSYMTB',AL1(7)
         DC    CL8'CADTCF00',AL1(7)
         DC    CL8'CMSCADIN',AL1(7)
         DC    CL8'CREATEMP',AL1(7)
         DC    CL8'DATADINT',AL1(7)
         DC    CL8'DBMSGP01',AL1(7)
         DC    CL8'DBMSGP02',AL1(7)
         DC    CL8'DBMWALL1',AL1(7)
         DC    CL8'DBMWALL2',AL1(7)
         DC    CL8'DMMONCMS',AL1(7)
         DC    CL8'DMMONTSO',AL1(7)
         DC    CL8'DRAWINIT',AL1(7)
         DC    CL8'DRAWRBLD',AL1(7)
         DC    CL8'FLGREL',AL1(5)
         DC    CL8'IHOSTAE',AL1(6)
         DC    CL8'IMAGER',AL1(5)
         DC    CL8'IMAGEZAP',AL1(7)
         DC    CL8'INTSEC',AL1(5)
         DC    CL8'NEWPLOTA',AL1(7)
         DC    CL8'NUPLTREC',AL1(7)
         DC    CL8'NURESTOR',AL1(7)
         DC    CL8'PLOTSORT',AL1(7)
         DC    CL8'PLTINDX',AL1(6)
         DC    CL8'PRNTPLOT',AL1(7)
         DC    CL8'PRSPEC',AL1(5)
         DC    CL8'SCLIB',AL1(4)
         DC    CL8'TAPEALOC',AL1(7)
         DC    CL8'TRNFRMB',AL1(6)
         DC    CL8'UTILITY',AL1(6)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
         DC    CL8'       ',AL1(7)
***********************************************************************
*        INITIATIALIZATION DATA FOR PORTION OF GETMAIN'D AREA
***********************************************************************
RACSTAT  RACHECK MF=L                  STATIC VERSION RACHECK LIST FORM
RACLEN   EQU   *-RACSTAT               LENGTH OF STATIC VERSION
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         EQUATE
***********************************************************************
*        IEFUSI INPUT PARAMETER LIST
***********************************************************************
SIPLBEGN DSECT
SIPLCEPL DS    F                       ADDR - COMMON SMF EXIT PARM ADDR
SIPLSTEP DS    F                       ADDR - 8 BYTE JOB STEP NAME
SIPLPGM  DS    F                       ADDR - 8 BYTE PROGRAM NAME
SIPLACCT DS    F                       ADDR - JOB STEP ACCTING INFO
SIPLVSM  DS    F                       ADDR - VSM PARAMETER LIST
SIPLSRM  DS    F                       ADDR - PERFORMANCE GROUP NUMBER
*                                             AND V=R FLAG
***********************************************************************
*        VSM PARAMETER LIST
***********************************************************************
VSPLBEGN DSECT
VSPLFLAG DS    F                       VSM FLAG WORD CONTAINS:
*
*              BIT 0  ---------------  ON - IEFUSI SET REGION VALUES
*                                      OFF - REGION VALUES NOT SET
*              BIT 1  ---------------  ON - VSM NOT TO CHECK FOR AVAIL-
*                                           ABILITY OF REQUESTED FREE
*                                           SPACE BELOW 16 MEG LINE
*                                      OFF - VSM IS TO CHECK FOR AVAIL-
*                                           ABILITY OF REQUESTED FREE
*                                           SPACE BELOW 16 MEG LINE
*              BIT 2  ---------------  ON - VSM NOT TO CHECK FOR AVAIL-
*                                           ABILITY OF REQUESTED FREE
*                                           SPACE ABOVE 16 MEG LINE
*                                      OFF - VSM IS TO CHECK FOR AVAIL-
*                                           ABILITY OF REQUESTED FREE
*                                           SPACE ABOVE 16 MEG LINE
*              BITS 3-31  -----------  RESERVED
*
VSPLREGR DS    F                       REGION SIZE REQUESTED
*
*                                      ON ENTRY TO IEFUSI THE FOLLOWING
*                                      4 WORDS ARE SET TO X'FFFFFFFF'
*
VSPLGETB DS    F                       GETMAIN LIMIT VALUE BELOW 16 MEG
VSPLREGB DS    F                       REGION SIZE BELOW 16 MEG
VSPLGETA DS    F                       GETMAIN LIMIT VALUE ABOVE 16 MEG
VSPLREGA DS    F                       REGION SIZE ABOVE 16 MEG
***********************************************************************
*        COMMON SMF PARAMETER LIST
***********************************************************************
CEPLBEGN DSECT                         COMMON EXIT PARAMETER AREA
CEPLJOBN DS    CL8                     JOB NAME
CEPLTIME DS    BL4                     TIME IN HUNDREDTHS OF A SECOND
*                                      READER RECOGNIZED THE JOB CARD
*                                      FOR THIS JOB
CEPLDATE DS    PL4                     DATE READER RECOGNIZED THE JOB
*                                      CARD FOR THIS JOB IN THE FORM:
*                                      00YYDDDF - WHERE F IS THE SIGN
CEPLSID  DS    CL4                     SYSTEM IDENTIFICATION
CEPLUID  DS    CL8                     USER ID FIELD
*                                      FOR DELUXES USE THIS FIELD IS
*                                      BROKEN DOWN AS FOLLOWS
*                                      BL2        ROUTE CODE
*                                      CL6        USERID
CEPLSTEP DS    BL1                     STEP NUMBER BEING PROCESSED
CEPLSMFO DS    BL1                     SMF OPTIONS USED INDICATOR
CEPLREST DS    BL1                     RESTART INDICATOR
CEPLJCLS DS    CL1                     JOB CLASS
CEPLUCOM DS    CL4                     USER COMMUNICATION FIELD
***********************************************************************
*        DSECT FOR COMMUNICATIONS VECTOR TABLE
***********************************************************************
         CVT DSECT=YES
IEFUSI   CSECT
***********************************************************************
*        DSECT FOR SMF CONTROL AREA
***********************************************************************
         IEESMCA
IEFUSI   CSECT
***********************************************************************
*        DYNAMIC STORAGE AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     IEFUSI SAVE AREA
RACDYN   DS    CL(RACLEN)              DYNAMIC VER RACHECK LIST FORM
WORKLEN  EQU   *-WORKAREA
         END   IEFUSI
