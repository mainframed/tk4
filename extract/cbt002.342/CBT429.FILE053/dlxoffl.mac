DLXOFFL CSECT
         SAVE  (14,12),,DLXOFFL_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING DLXOFFL,R12              TELL ASSEMBLER
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
         LR    R3,R1                    SAVE ADDRESS OF PARMLIST
* END OF PROLOGUE                       SAVE AREA
GETPARM1 EQU   *                        GET PARM FIELD
         L     R1,0(R3)                 ADDRESS OF PARM TO R2
         LH    R3,0(R1)                 LENGTH OF PARM TO R3
         LTR   R3,R3                    CHECK FOR NO PARM
         BZ    OPENCARD                 NO PARMS
         CH    R3,=H'4'                 CHECK FOR VALID ADDRESS LENGTH
         BNE   ABEND12                  BAD LENGTH
         MVC   DEVADDR(4),2(R1)         MOVE PARM
LOOP1    EQU   *
         TR    DEVADDR(3),TTAB
         ICM   R1,B'0001',DEVADDR+1
         SLL   R1,4
         STCM  R1,B'0001',DEVADDR+1
         OC    DEVADDR+2(1),DEVADDR+1
         MVC   DEVADDR+1(1),DEVADDR
         MVI   DEVADDR,X'00'
         SLR   R6,R6
         ICM   R6,B'0111',DEVADDR
         MODESET KEY=ZERO,MODE=SUP
         LA    R13,IOSSAVE
         IOSLOOK DEV=(6),UCB=(7)
         LA    R13,SAVEAREA
         LTR   R15,R15
         BNZ   ABEND4
         USING UCBCMSEG,R7
         CLI   DEVADDR+3,C'0'
         BE    BITOFF
         NI    UCBSTAT,X'FF'-UCBONLI
         B     MODEST
BITOFF   EQU   *
         OI    UCBSTAT,UCBONLI
MODEST   MODESET KEY=NZERO,MODE=PROB
         B     GETCARD
RETURN0  SLR   R15,R15
RETURNX  L     R13,SAVEAREA+4           LOAD OS SAVE AREA ADDR TO R13
         RETURN (14,12),RC=(15)         RETURN TO OS RC=R15
OPENCARD EQU   *
         OPEN  (SYSIN,(INPUT))
         LTR   R15,R15
         BNZ   ABEND8
         MVI   CARDSW,C'1'
GETCARD  EQU   *
         CLI   CARDSW,C'1'
         BNE   RETURN0
         GET   SYSIN,CARDAREA
         MVC   DEVADDR(4),CARDAREA
         B     LOOP1
CLOSCARD EQU   *
         CLOSE (SYSIN)
         B     RETURN0
WTO      WTO   'DLXOFFL -- DEVICE IS ONLINE',ROUTCDE=(11)
         LA    R15,8
         B     RETURNX
ABEND4   ABEND 4,DUMP
ABEND8   ABEND 8,DUMP
ABEND12  ABEND 12,DUMP
* END OF EPILOGUE
PGMNAME  DC    CL8'DLXOFFL'
DEVADDR  DC    CL4'0'
SAVEAREA DC    18F'0'                   OUR SAVE AREA
IOSSAVE  DC    16F'0'                   IOSLOOK SAVE AREA
TTAB     DS    0CL256
         DC    XL16'000102030405060708090A0B0C0D0E0F'
         DC    XL16'101112131415161718191A1B1C1D1E1F'
         DC    XL16'202122232425262728292A2B2C2D2E2F'
         DC    XL16'303132333435363738393A3B3C3D3E3F'
         DC    XL16'404142434445464748494A4B4C4D4E4F'
         DC    XL16'505152535455565758595A5B5C5D5E5F'
         DC    XL16'606162636465666768696A6B6C6D6E6F'
         DC    XL16'707172737475767778797A7B7C7D7E7F'
         DC    XL16'800A0B0C0D0E0FC7C8C98A8B8C8D8E8F'
         DC    XL16'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    XL16'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    XL16'C00A0B0C0D0E0FC7C8C9CACBCCCDCECF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    XL16'00010203040506070809FAFBFCFDFEFF'
CARDSW   DC    C'0'
CARDAREA DC    CL80' '
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,LRECL=80,MACRF=(GM),              X
               RECFM=FB,EODAD=CLOSCARD
         LTORG
         EQUATE
         IHAACEE
         IHAASCB
         IHAASXB
         IEFUCBOB
         IECDIOCM                       REQUIRED FOR IOSLOOK
         CVT   DSECT=YES                REQUIRED FOR IOSLOOK
         END   DLXOFFL
