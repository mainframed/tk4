         TITLE 'INMXZ01' - TRANSMIT INITIALIZATION EXIT
***********************************************************************
*                                                                     *
*             MODULE NAME = INMXZ01                                   *
*                                                                     *
*             DESCRIPTIVE NAME = TRANSMIT INITIALIZATION EXIT         *
*                                                                     *
*             FUNCTION =                                              *
*                EXIT PRVENTS MEMBERS OF JCLGRP FROM USING            *
*                TRANSMIT.                                            *
*                                                                     *
*                OPERATION =                                          *
*                   SAVE REGISTERS AND ESTABLISH ADDRESSABILITY       *
*                   GET ASCB ADDRESSABILITY                           *
*                   GET ASXB ADDRESSABILITY                           *
*                   DROP ASCB ADDRESSABILITY                          *
*                   GET ACEE ADDRESSABILITY                           *
*                   DROP ASXB ADDRESSABILITY                          *
*                   IF USER HAS CONNECT GROUP 'JCLGRP',               *
*                     EXIT WITH RC=4                                  *
*                   ELSE,                                             *
*                     EXIT WITH RC=0                                  *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTER  1       = PARAMETER LIST POINTER       *
*                    REGISTER  2       = ASCB AND LATER TCB           *
*                                        ADDRESSABILITY AND           *
*                                        WORK REGISTER                *
*                    REGISTER  3       = ASXB AND LATER TIOT          *
*                                        ADDRESSABILITY AND           *
*                                        WORK REGISTER                *
*                    REGISTERS 4 - 10  = WORK REGISTERS               *
*                    REGISTER  11      = ACEE ADDRESSABILITY          *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA POINTER            *
*                    REGISTER  14      = RETURN ADDRESS               *
*                    REGISTER  15      = ENTRY POINT ADDRESS AND      *
*                                        RETURN CODE REGISTER         *
*                                                                     *
*             MACROS =                                                *
*                SAVE, RETURN, EQUATE                                 *
*                                                                     *
*             MAPPING MACROS USED =                                   *
*                IHAACEE       ACEE MAP                               *
*                IHAASCB       ASCB MAP                               *
*                IHAASXB       ASXB MAP                               *
*                                                                     *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                02/21/85  WRITTEN.      BC.                          *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*
INMXZ01 CSECT
         SAVE  (14,12),,INMXZ01_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING INMXZ01,R12             ESTABLISH ADDRESSABILITY
         LR    R7,R1                   PRESERVE PTR TO PARMS
*
*  CHECK TO SEE IF USER HAS CONNECT GROUP 'JCLGRP'.  IF SO,
*    FAIL THE TRANSMIT.
*
CHKGRP   EQU   *
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
         L     R3,ASCBASXB             GET ADDRESS OF ASXB
         USING ASXB,R3                 TELL ASSEMBLER
         L     R11,ASXBSENV            GET ADDRESS OF ACEE
         DROP  R3
         USING ACEE,R11                ESTABLISH ADDRESSABILITY TO ACEE
         CLC   ACEEGRPN,=CL8'JCLGRP'   IS USER IN JCLGRP?
         BNE   EXIT                    IF NOT, OK
         L     R5,32(R7)               GET ADDRESS OF CHAIN
         LA    R6,16(R5)               GET ADDRESS OF RECEIVE USERID
         LH    R3,14(R5)               GET LENGTH OF USERID
LOCUSER  EQU   *
         GETMAIN R,LV=LAREA            GET DYNAMIC WORK AREA
         LR    R10,R1                  PRESERVE POINTER TO AREA
         USING WORKAREA,R10            ESTABLISH ADDRESSABILITY
         MVC   EXLOC(LDATA),DATA       MOVE DATA INTO WORK AREA
         MVC   USERID+1(8),0(R6)       LOAD RECEIVE USERID
         STCM  R3,B'0001',USERID       LOAD LENGTH OF USERID
         LA    R3,LOCAEND-LOCAREA
         ST    R3,LOCAREA
         ICHEINTY LOCATE,TYPE='USR',ENTRY=USERID,                      X
               WKAREA=LOCAREA,MF=(E,EXLOC)
         LR    R3,R15                  SAVE RETURN CODE
         FREEMAIN R,LV=LAREA,A=(10)    FREE THE GETMAINED AREA
         LTR   R3,R3                   USER FOUND?
         BZ    LU30                    IF SO, BRANCH
         LA    R15,4                   SET RC TO FAIL TRANSMIT
         B     EXITX                   AND EXIT
LU30     EQU   *
         CLI   0(R6),C'T'              DOES USERID START WITH 'T'?
         BE    LU40                    IF SO, OK
         LA    R15,4                   SET RC TO FAIL TRANSMIT
         B     EXITX                   AND EXIT
LU40     EQU   *
         LA    R5,5                    CHECK FOR 5 NUMERICS
LU50     EQU   *
         LA    R6,1(R6)                GO TO NEXT CHARACTER
         TM    0(R6),X'F0'             IS IT NUMERIC?
         BNO   LU60                    IF NOT, BRANCH
         BCT   R5,LU50                 KEEP CHECKING
         B     EXIT                    ALL NUMERIC
LU60     EQU   *
         LA    R15,4                   SET RC TO FAIL TRANSMIT
         B     EXITX
EXIT     EQU   *
         SLR   R15,R15                 SET RETURN CODE TO ZERO
EXITX    EQU   *
         RETURN (14,12),RC=(15)        END THE EXIT
*
         LTORG
*
*  REGISTER EQUATES
*
         EQUATE
*
*  INITIAL DATA FOR WORK AREA
*
DATA     ICHEINTY LOCATE,TYPE='USR',MF=L
LDATA    EQU  *-DATA
*
*  WORK AREA DSECT
*
WORKAREA DSECT
*
EXLOC    ICHEINTY LOCATE,TYPE='USR',MF=L
USERID   DS   AL1
         DS   CL8
LOCAREA  DS   F
         DS   CL24
         DS   F
LOCAEND  EQU  *
LAREA    EQU  *-WORKAREA
*
*
*  DSECTS
*
         IHAACEE
         IHAASCB
         IHAASXB
*
         END   INMXZ01
