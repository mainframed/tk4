         TITLE 'USRDEL1 - DELUXE USER REGISTRATION (DELETE A USER)'
***********************************************************************
*                                                                     *
*        MODULE NAME = USRDEL1                                        *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*              05/27/86  WRITTEN.                               JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'USRDEL1 - ISPCALL MACRO'
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         +
               &EP=ISPLINK,                                            +
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    +
               &EPADDR=,       LABEL FOR ADDRESS OF EP OF ISPLINK      +
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    +
               &WKREG=,        WORK REGISTER (DEFAULT 1)               +
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG,&WKEP
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1,&ISPEP
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHES+
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
         EJECT
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 +
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LIS+
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
         EJECT
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
.*       LA    1,&WKAR.+4                                       ISPCALL
.*       L     15,=V(&EP)                                       ISPCALL
.*       BALR  14,15                                            ISPCALL
         AIF   (T'&EPADDR EQ 'O').EP2
&ISPEP   SETC  '&EPADDR'
         LOAD  EP=ISPLINK
         ST    R0,&ISPEP
.EP2     ANOP
&WKEP    SETC  '&ISPEP'
         L     15,&WKEP
         LA    1,&WKAR.+4
         CALL  (15)
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
         EJECT
         TITLE 'USRDEL1 - INITIALIZATION SECTION'
USRDEL1  CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,USRDEL1_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING USRDEL1,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R11,4095(R12)           LOAD REG 11 WITH ENTRY + 4095
         USING USRDEL1+4095,R11        ESTABLISH ADDRESSABILITY TO
*                                      REST OF PROGRAM
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         MVI   DELFLAG,OFF             INITIALIZE DELFLAG
***********************************************************************
*  INITIALIZATION SECTION - SET DIALOG CONTROL, DEFINE VARIABLES
***********************************************************************
CONTROL  EQU   *
*                                      SET DIALOG CONTROL
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        +
               WKAREA=PARMS,                                           +
               EPADDR=ISPADDR
VDEF1    EQU   *
         MVC   ISPFSERV,DEFID          SET ISPF DIALOG SERVICE
*                                      DEFINE VARIABLES
         ISPCALL ('VDEFINE',NAMELST1,USERID,'CHAR',8)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF2                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF2    EQU   *
         ISPCALL ('VDEFINE',NAMELST2,NCCFUSER,'CHAR',1)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF3                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF3    EQU   *
         ISPCALL ('VDEFINE',NAMELST3,ZTEMPF,'CHAR',44)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF4                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF4    EQU   *
         ISPCALL ('VDEFINE',NAMELST4,USERMSGS,'CHAR',24)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF5                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF5    EQU   *
         ISPCALL ('VDEFINE',NAMELST5,USERMSGL,'CHAR',78)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF6                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF6    EQU   *
         ISPCALL ('VDEFINE',NAMELST6,SAVTOPB,'FIXED',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF7                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF7    EQU   *
         ISPCALL ('VDEFINE',NAMELST7,ZTDTOP,'CHAR',6)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF8                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF8    EQU   *
         ISPCALL ('VDEFINE',NAMELST8,ZCMD,'CHAR',40)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF9                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF9    EQU   *
         ISPCALL ('VDEFINE',NAMELST9,ZTDSELS,'CHAR',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFA                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFA    EQU   *
         ISPCALL ('VDEFINE',NAMELSTA,ZLLGJOB1,'CHAR',80)
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF10   EQU   *
         B     TBOPEN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VDEF)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VDEF)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - OPEN 'USERTAB'
***********************************************************************
TBOPEN   EQU   *
         MVC   ISPFSERV,OPNID          SET ISPF DIALOG SERVICE
*                                      OPEN A TABLE
         ISPCALL ('TBOPEN','USERTAB','WRITE','USRTLIB')
         B     TBOPN10(R15)            CHECK RETURN CODE
TBOPN10  EQU   *
         B     TBCREATE                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBOPEN)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - CREATE FILE TAILORING TABLE
***********************************************************************
TBCREATE EQU   *
         MVC   ISPFSERV,CREID          SET ISPF DIALOG SERVICE
*                                      CREATE A TABLE
         ISPCALL ('TBCREATE','USRDEL',' ',NAMELSTX,'WRITE','REPLACE')
         B     TBCRE10(R15)            CHECK RETURN CODE
TBCRE10  EQU   *
         B     INITVAL                 RC = 00 (CONTINUE)
         B     INITVAL                 RC = 04 (CONTINUE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - INITIALIZE TABLE VALUES
***********************************************************************
INITVAL  EQU   *
         MVC   USERID,SPACES           INITIALIZATION
         MVC   NCCFUSER,NO             INITIALIZATION
         MVC   TSOUSER,NO              INITIALIZATION
         MVC   NCCFPROF,NCCFINIT       INITIALIZATION
         MVC   USRDEL_,SKELNAM$        INITIALIZATION
         MVC   SELCT,SPACES            INITIALIZATION
         MVC   RACF$,NO                INITIALIZATION
         MVC   RACF1,NO                INITIALIZATION
         MVC   RACF2,NO                INITIALIZATION
         MVC   RACF3,NO                INITIALIZATION
         MVC   RACF4,NO                INITIALIZATION
         MVC   RACF5,NO                INITIALIZATION
         MVC   RACF6,NO                INITIALIZATION
         MVC   RACF7,NO                INITIALIZATION
         MVC   RACF8,NO                INITIALIZATION
         MVC   TSO$,NO                 INITIALIZATION
         MVC   TSO1,NO                 INITIALIZATION
         MVC   TSO2,NO                 INITIALIZATION
         MVC   TSO3,NO                 INITIALIZATION
         MVC   TSO4,NO                 INITIALIZATION
         MVC   TSO5,NO                 INITIALIZATION
         MVC   TSO6,NO                 INITIALIZATION
         MVC   TSO7,NO                 INITIALIZATION
         MVC   TSO8,NO                 INITIALIZATION
         MVC   SUB,NO                  INITIALIZATION
         MVC   SUBCMD2,SUBCMD1         MOVE STATIC INFO TO DYNAMIC
         LA    R6,SKELNAM$             R6 - INDEX THRU SKELETON NAMES
         TITLE 'USRDEL1 - PROCESSING SECTION'
***********************************************************************
*  PROCESSING SECTION - DISPLAY TABLE INFORMATION
***********************************************************************
TBDISPL1 EQU   *
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','USERTAB','USRDEL')
         B     TBDIS110(R15)           CHECK RETURN CODE
TBDIS110 EQU   *
         B     CHKZCMD                 RC = 00 (CONTINUE)
         B     CHKZCMD                 RC = 04 (CONTINUE)
         B     CHKSELS1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CHECK FOR 'LOCATE' PRIMARY COMMAND
***********************************************************************
CHKZCMD  EQU   *
         PACK  SAVTOPD,ZTDTOP          TRANSLATE ZTDTOP TO DECIMAL
         CVB   R2,SAVTOPD              CONVERT TO BINARY
         ST    R2,SAVTOPB              SAVE FOR FUTURE USE
         MVC   SAVLOC,SPACES           INITIALIZE SAVLOC
         LA    R2,ZCMD                 R2 - INDEX THROUGH ZCMD
         LA    R3,40                   R3 - MAXIMUM LENGTH OF ZCMD
         LA    R5,SAVLOC               R5 - INDEX THROUGH SAVLOC
CHKZCMD1 EQU   *
         LA    R4,2                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(2,R2),LOCATE1         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,3                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(3,R2),LOCATE2         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,4                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(4,R2),LOCATE3         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,5                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(5,R2),LOCATE4         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,6                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(6,R2),LOCATE5         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,7                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(7,R2),LOCATE6         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         CLC   0(1,R2),SPACES          ANY OTHER CHARACTER ENTERED?
         BNE   CHKLCMD                 YES, UNSUPPORTED COMMAND
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,CHKZCMD1             CONTINUE
         B     CHKLCMD                 CHECK FOR LINE COMMAND
CHKZCMD2 EQU   *
         AR    R2,R4                   POINT R2 PAST LOCATE COMMAND
CHKZCMD3 EQU   *
         CLC   0(1,R2),SPACES          START OF LOCATE DATA?
         BNE   CHKZCMD4                YES, EXTRACT DATA AND SAVE
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,CHKZCMD3             CONTINUE
         B     CHKLCMD                 NOTHING TO LOCATE
CHKZCMD4 EQU   *
         CLC   0(1,R2),SPACES          END OF LOCATE DATA?
         BE    CHKZCMD5                YES, ADD ASTERICK ON END
         MVC   0(1,R5),0(R2)           EXTRACT LOCATE DATA
         LA    R2,1(R2)                INCREMENT R2
         LA    R5,1(R5)                INCREMENT R5
         BCT   R3,CHKZCMD4             CONTINUE
         B     CHKLCMD                 CHECK LINE COMMANDS
CHKZCMD5 EQU   *
         MVC   0(1,R5),ASTERICK        ADD ASTERICK ON END
         B     CHKLCMD                 CHECK LINE COMMANDS
***********************************************************************
*  PROCESSING SECTION - CHECK LINE COMMAND
***********************************************************************
CHKLCMD  EQU   *
         CLC   ZTDSELS,CHAR00          NO LINES SELECTED?
         BE    CHKLOC                  CORRECT, PROCESS 'LOCATE' CMD
         CLC   ZTDSELS,CHAR01          ONE LINE SELECTED?
         BNE   CHKLCMD1                MORE, PROCESS
         MVI   DELFLAG,ON              TURN ON DELFLAG
         BAL   R10,TBGET               RETRIEVE INFO FOR USER DELETED
         B     CHKLOC                  PROCESS 'LOCATE' COMMAND
CHKLCMD1 EQU   *
         MVI   DELFLAG,ON              TURN ON DELFLAG
         BAL   R10,TBGET               RETRIEVE INFO FOR USER DELETED
***********************************************************************
*  PROCESSING SECTION - DISPLAY TABLE TO PROCESS LCMD
***********************************************************************
TBDISPL2 EQU   *
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','USERTAB')
         B     TBDIS210(R15)           CHECK RETURN CODE
TBDIS210 EQU   *
         B     CHKLCMD                 RC = 00 (CONTINUE)
         B     CHKLCMD                 RC = 04 (CONTINUE)
         B     CHKSELS2                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - PROCESS LOCATE DATA
***********************************************************************
CHKLOC   EQU   *
         CLC   SAVLOC,SPACES           ANYTHING TO LOCATE?
         BNE   TBTOP1                  YES, ATTEMPT TO LOCATE
         B     TBDISPL1                RE-DISPLAY TABLE
***********************************************************************
*  PROCESSING SECTION - SET CRP TO TOP OF TABLE
***********************************************************************
TBTOP1   EQU   *
         MVC   ISPFSERV,TOPID          SET ISPF DIALOG SERVICE
*                                      SET ROW POINTER TO TOP
         ISPCALL ('TBTOP','USERTAB')
         B     TBTOP110(R15)           CHECK RETURN CODE
TBTOP110 EQU   *
         B     TBVCLEAR                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     USRERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CLEAR 'USERTAB' VARIABLES
***********************************************************************
TBVCLEAR EQU   *
         MVC   ISPFSERV,CLRID          SET ISPF DIALOG SERVICE
*                                      CLEAR TABLE VARIABLES
         ISPCALL ('TBVCLEAR','USERTAB')
         B     TBCLR10(R15)            CHECK RETURN CODE
TBCLR10  EQU   *
         B     TBSARG                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBVCLEAR)
         B     USRERR08                RC = 08 (NOT VALID FOR TBVCLEAR)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBVCLEAR)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - DEFINE SCAN ARGUMENTS FOR 'USERTAB'
***********************************************************************
TBSARG   EQU   *
         MVC   ISPFSERV,ARGID          SET ISPF DIALOG SERVICE
         MVC   USERID,SAVLOC           SET USERID WITH SELECTION VALUE
*                                      DEFINE SCAN ARGUMENTS
         ISPCALL ('TBSARG','USERTAB')
         B     TBARG10(R15)            CHECK RETURN CODE
TBARG10  EQU   *
         B     TBSCAN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSARG)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBSARG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SEARCH 'USERTAB' TABLE
***********************************************************************
TBSCAN   EQU   *
         MVC   ISPFSERV,SCNID          SET ISPF DIALOG SERVICE
*                                      SEARCH A TABLE
         ISPCALL ('TBSCAN','USERTAB')
         B     TBSCN10(R15)            CHECK RETURN CODE
TBSCN10  EQU   *
         B     TBDISPL1                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSCAN)
         B     SCNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - END OR RETURN ENTERED. CHECK FOR SELECTED ROWS
***********************************************************************
CHKSELS1 EQU   *
         PACK  SAVTOPD,ZTDTOP          TRANSLATE ZTDTOP TO DECIMAL
         CVB   R2,SAVTOPD              CONVERT TO BINARY
         ST    R2,SAVTOPB              SAVE FOR FUTURE USE
         CLC   ZTDSELS,CHAR00          ANY ROWS SELECTED?
         BE    TBCLOSE1                NO, WE'RE FINISHED
         B     CHKLCMD                 YES, CHECK LINE COMMAND(S)
CHKSELS2 EQU   *
         CLC   ZTDSELS,CHAR01          SELECTED ROWS PROCESSED?
         BE    TBCLOSE1                YES, WE'RE FINISHED
         B     CHKLCMD                 NO, CHECK LINE COMMAND(S)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE INFO FROM TABLE FOR SELECTED USER
***********************************************************************
TBGET   EQU    *
         MVC   ISPFSERV,TBGID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE ROW FROM TABLE
         ISPCALL ('TBGET','USERTAB')
         B     TBGET10(R15)            CHECK RETURN CODE
TBGET10  EQU   *
         B     TBADD                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - ADD A USER TO THE FILE TAILORING TABLE
***********************************************************************
TBADD    EQU   *
         MVC   ISPFSERV,ADDID          SET ISPF DIALOG SERVICE
*                                      ADD A ROW TO A TABLE
         ISPCALL ('TBADD','USRDEL')
         B     TBADD10(R15)            CHECK RETURN CODE
TBADD10  EQU   *
         B     TBDELETE                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBADD)
         B     USRERR08                RC = 08 (NOT VALID FOR TBADD)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - DELETE USER FROM USER TABLE (USERTAB)
***********************************************************************
TBDELETE EQU   *
         MVC   ISPFSERV,DELID          SET ISPF DIALOG SERVICE
*                                      DELETE A ROW FROM A TABLE
         ISPCALL ('TBDELETE','USERTAB')
         B     TBDEL10(R15)            CHECK RETURN CODE
TBDEL10  EQU   *
         B     TBDEL20                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBDELETE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDELETE)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBDEL20  EQU   *
         BR    R10                     RETURN
***********************************************************************
*  PROCESSING SECTION - CLOSE USER TABLE (USERTAB)
***********************************************************************
TBCLOSE1 EQU   *
         MVC   ISPFSERV,CLOID          SET ISPF DIALOG SERVICE
*                                      CLOSE AND SAVE TABLE
         ISPCALL ('TBCLOSE','USERTAB','REPLCOPY',' ',' ','USRTLIB')
         B     TBCLO110(R15)           CHECK RETURN CODE
TBCLO110 EQU   *
         B     TBCLOSE2                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBCLOSE)
         B     USRERR08                RC = 08 (NOT VALID FOR TBCLOSE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CLOSE FILE TAILORING TABLE (USRDEL)
***********************************************************************
TBCLOSE2 EQU   *
         MVC   ISPFSERV,CLOID          SET ISPF DIALOG SERVICE
*                                      CLOSE AND SAVE TABLE
         ISPCALL ('TBCLOSE','USRDEL','REPLCOPY')
         B     TBCLO210(R15)           CHECK RETURN CODE
TBCLO210 EQU   *
         B     VGET1                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBCLOSE)
         B     USRERR08                RC = 08 (NOT VALID FOR TBCLOSE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE 'ZLLGJOB1' & 'ZLLGJOB2'
***********************************************************************
VGET1    EQU   *
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE DIALOG VARIABLE
         ISPCALL ('VGET',NAMELSTA)
         B     VGET110(R15)            CHECK RETURN CODE
VGET110  EQU   *
         B     FTOPEN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - OPEN TEMPORARY FILE TAILORING FILE
***********************************************************************
FTOPEN   EQU   *
         MVC   ISPFSERV,FTOID          SET ISPF DIALOG SERVICE
         TM    DELFLAG,ON              ANY DATA PROCESSED?
         BNO   TBERASE                 NO, ERASE FILE TAILORING TABLE
*                                      OPEN TEMP FILE TAILORING FILE
         ISPCALL ('FTOPEN','TEMP')
         B     FTOPN10(R15)            CHECK RETURN CODE
FTOPN10  EQU   *
         B     FTINCL                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTOPEN)
         B     FTINCL                  RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - INCLUDE SKELETON FOR FILE TAILORING
***********************************************************************
FTINCL   EQU   *
         MVC   ISPFSERV,FTIID          SET ISPF DIALOG SERVICE
*                                      INCLUDE SKELETON FOR TAILORING
         ISPCALL ('FTINCL',USRDEL_)
         B     FTINC10(R15)            CHECK RETURN CODE
FTINC10  EQU   *
         B     FTCLOSE                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTINCL)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CLOSE TEMPORARY FILE
***********************************************************************
FTCLOSE  EQU   *
         MVC   ISPFSERV,FTCID          SET ISPF DIALOG SERVICE
*                                      CLOSE TEMP FILE TAILORING FILE
         ISPCALL ('FTCLOSE')
         B     FTCLO10(R15)            CHECK RETURN CODE
FTCLO10  EQU   *
         B     VGET2                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTCLOSE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE 'ZTEMPF'
***********************************************************************
VGET2    EQU   *
         CLC   SUB,NO                  REGIONAL FILE TAILORING?
         BE    SELCT120                NO, PROCESS NEXT REGION
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE DIALOG VARIABLE
         ISPCALL ('VGET',NAMELST3)
         B     VGET210(R15)            CHECK RETURN CODE
VGET210  EQU   *
         B     SELECT1                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SELECT CMD ZTEMPF TO DEFINE USER(S)
***********************************************************************
SELECT1  EQU   *
         MVC   ISPFSERV,SELID          SET ISPF DIALOG SERVICE
         MVC   SUBCMD2+11(44),ZTEMPF   NO, MOVE ZTEMPF TO SUBCMD
         ISPCALL ('SELECT',FULL56,SUBCMD2)
         B     SELCT110(R15)           CHECK RETURN CODE
SELCT110 EQU   *
         B     SELCT120                RC = 00 (CONTINUE)
         B     SELCT120                RC = 04 (CONTINUE)
         B     USRERR08                RC = 08 (NOT VALID FOR SELECT)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
SELCT120 EQU   *
         CLC   0(8,R6),SKELNAM8        ARE WE DONE?
         BE    TBERASE                 YES, ERASE TABLE
         LA    R6,8(R6)                POINT TO NEXT SKELETON NAME
         MVC   USRDEL_,0(R6)           MOVE IT INTO USRDEL_
         MVC   SUB,NO                  RE-SET SUB
         B     FTOPEN                  CONTINUE WITH FILE TAILORING
***********************************************************************
*  PROCESSING SECTION - ERASE FILE TAILORING TABLE (USRDEL)
***********************************************************************
TBERASE  EQU   *
         MVC   ISPFSERV,ERAID          SET ISPF DIALOG SERVICE
*                                      CLOSE TABLE WITHOUT SAVING
         ISPCALL ('TBERASE','USRDEL')
         B     TBERA10(R15)            CHECK RETURN CODE
TBERA10  EQU   *
         B     FINISH                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBERASE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'USRDEL1 - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
USRERR04 EQU   *
         MVC   ISPFRC,FOUR             SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR08 EQU   *
         MVC   ISPFRC,EIGHT            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR12 EQU   *
         MVC   ISPFRC,TWELVE           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR16 EQU   *
         MVC   ISPFRC,SIXTEEN          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR20 EQU   *
         MVC   ISPFRC,TWENTY           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
MSGDISP1 EQU   *
         MVC   USERMSGS,USERERRS       SET SHORT MESSAGE
         MVC   USERMSGL,USERERRL       SET LONG MESSAGE
         MVC   USERMSGL+21(8),ISPFSERV    SET ISPF DIALOG SERVICE
         MVC   USERMSGL+47(2),ISPFRC   SET DIALOG SERVICE RETURN CODE
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','USRDEL',USERMSG)
         B     FINISH
***********************************************************************
*  ERROR PROCESSING SECTION - USER NOT FOUND ERROR ('LOCATE' COMMAND)
***********************************************************************
SCNERR08 EQU   *
         MVC   USERMSGS,SPACES         INITIALIZE USERMSGS
         MVC   USERMSGL,SPACES         INITIALIZE USERMSGL
         MVC   USERMSGL(1),QUOTE       MOVE LEADING QUOTE TO MESSAGE
         MVC   USERMSGL+1(40),SAVLOC   MOVE IN LOCATION DATA
         LA    R2,USERMSGL             R2 - INDEX THROUGH SAVLOC
         LA    R3,41                   R3 - MAXIMUM LENGTH
SCNERR18 EQU   *
         CLC   0(1,R2),ASTERICK        END OF LOCATION DATA
         BE    SCNERR28                YES, ADD TRAILING QUOTE, ETC.
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,SCNERR18             CONTINUE
SCNERR28 EQU   *
         MVC   USERMSGS,USRERR1S       ISPF SHORT MESSAGE
         MVC   0(21,R2),USRERR1L       ADD TRAILING QUOTE, ETC.
         BAL   R10,TBTOP2              RE-POSITION CRP
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
         ISPCALL ('TBDISPL','USERTAB','USRDEL',USERMSG)
         B     SCNERR38(R15)           CHECK RETURN CODE
SCNERR38 EQU   *
         B     SCNERR48                RC = 00 (CONTINUE)
         B     SCNERR48                RC = 04 (CONTINUE)
         B     CHKSELS1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
SCNERR48 EQU   *
         LA    R10,TBDISPL1            R10 - RETURN ADDRESS
***********************************************************************
*  ERROR PROCESSING SECTION - SET CRP TO TOP OF TABLE (AFTER 'LOCATE')
***********************************************************************
TBTOP2   EQU   *
         MVC   ISPFSERV,TOPID          SET ISPF DIALOG SERVICE
*                                      SET ROW POINTER TO TOP
         ISPCALL ('TBTOP','USERTAB')
         B     TBTOP210(R15)           CHECK RETURN CODE
TBTOP210 EQU   *
         B     TBSKIP                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     USRERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  ERROR PROCESSING SECTION - RESET ROWID (AFTER 'LOCATE')
***********************************************************************
TBSKIP   EQU   *
         MVC   ISPFSERV,SKPID          SET ISPF DIALOG SERVICE
*                                      RESET ROWID
         ISPCALL ('TBSKIP','USERTAB',SAVTOPB)
         B     TBSKP10(R15)            CHECK RETURN CODE
TBSKP10  EQU   *
         B     TBSKP20                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSKIP)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBSKP20  EQU   *
         BR    R10                     RC = 00 (CONTINUE)
         TITLE 'USRDEL1 - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'USRDEL1 - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
OFF      EQU   X'00'                   NO USERS HAVE BEEN ADDED
ON       EQU   X'10'                   USER(S) HAVE BEEN ADDED
         TITLE 'USRDEL1 - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
FULL12   DC    F'12'                   FULLWORD CONSTANT VALUE '12'
FULL56   DC    F'56'                   FULLWORD CONSTANT VALUE '56'
SPACES   DC    CL80' '                 SPACES
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
LOCATE1  DC    CL2'L '                 LOCATE COMMAND IDENTIFIER
LOCATE2  DC    CL3'LO '                LOCATE COMMAND IDENTIFIER
LOCATE3  DC    CL4'LOC '               LOCATE COMMAND IDENTIFIER
LOCATE4  DC    CL5'LOCA '              LOCATE COMMAND IDENTIFIER
LOCATE5  DC    CL6'LOCAT '             LOCATE COMMAND IDENTIFIER
LOCATE6  DC    CL7'LOCATE '            LOCATE COMMAND IDENTIFIER
CHAR00   DC    CL4'0000'               CHARACTER CONSTANT VALUE '0000'
CHAR01   DC    CL4'0001'               CHARACTER CONSTANT VALUE '0001'
NCCFINIT DC    CL8'DPF30'              NCCF PROFILE NAME PREFIX
NO       DC    CL1'N'                  INDICATE NOT TSO OR NCCF USER
YES      DC    CL1'Y'                  INDICATE TSO OR NCCF USER
ASTERICK DC    CL1'*'                  ASTERICK
QUOTE    DC    CL1''''                 QUOTE
USERMSG  DC    CL8'USER000'            GENERIC USER MESSAGE
USRDEL2  DC    C'PGM(USRDEL2)'         FUNTION CALLED FROM USRDEL1
SUBCMD1  DC    CL56'CMD(SUBMIT                                         +
                    )'
SKELNAM$ DC    CL8'USRDEL$'            ADMIN SKELETON
SKELNAM1 DC    CL8'USRDEL1'            REGION 1 SKELETON
SKELNAM2 DC    CL8'USRDEL2'            REGION 2 SKELETON
SKELNAM3 DC    CL8'USRDEL3'            REGION 3 SKELETON
SKELNAM4 DC    CL8'USRDEL4'            REGION 4 SKELETON
SKELNAM5 DC    CL8'USRDEL5'            REGION 5 SKELETON
SKELNAM6 DC    CL8'USRDEL6'            REGION 6 SKELETON
SKELNAM7 DC    CL8'USRDEL7'            REGION 7 SKELETON
SKELNAM8 DC    CL8'USRDEL8'            REGION 8 SKELETON
ADDID    DC    CL8'TBADD'              ISPF DIALOG SERVICE (TBADD)
CLOID    DC    CL8'TBCLOSE'            ISPF DIALOG SERVICE (TBCLOSE)
CREID    DC    CL8'TBCREATE'           ISPF DIALOG SERVICE (TBCREATE)
DEFID    DC    CL8'VDEFINE'            ISPF DIALOG SERVICE (VDEFINE)
DISID    DC    CL8'DISPLAY'            ISPF DIALOG SERVICE (DISPLAY)
ERAID    DC    CL8'TBERASE'            ISPF DIALOG SERVICE (TBERASE)
FTCID    DC    CL8'FTCLOSE'            ISPF DIALOG SERVICE (FTCLOSE)
FTIID    DC    CL8'FTINCL'             ISPF DIALOG SERVICE (FTINCL)
FTOID    DC    CL8'FTOPEN'             ISPF DIALOG SERVICE (FTOPEN)
GETID    DC    CL8'VGET'               ISPF DIALOG SERVICE (VGET)
OPNID    DC    CL8'TBOPEN'             ISPF DIALOG SERVICE (TBOPEN)
SELID    DC    CL8'SELECT'             ISPF DIALOG SERVICE (SELECT)
TBGID    DC    CL8'TBGET'              ISPF DIALOG SERVICE (TBGET)
DELID    DC    CL8'TBDELETE'           ISPF DIALOG SERVICE (TBDELETE)
TDSID    DC    CL8'TBDISPL'            ISPF DIALOG SERVICE (TBDISPL)
TOPID    DC    CL8'TBTOP'              ISPF DIALOG SERVICE (TBTOP)
CLRID    DC    CL8'TBVCLEAR'           ISPF DIALOG SERVICE (TBVCLEAR)
ARGID    DC    CL8'TBSARG'             ISPF DIALOG SERVICE (TBSARG)
SCNID    DC    CL8'TBSCAN'             ISPF DIALOG SERVICE (TBSCAN)
SKPID    DC    CL8'TBSKIP'             ISPF DIALOG SERVICE (TBSKIP)
USERERRS DC    CL24'USRDEL1 program error.  '
USERERRL DC    CL78'ISPF Dialog Service, 12345678, ended with RC = 00. +
               Notify Systems Programming.'
USRERR1S DC    CL24'Not found in table.'
USRERR1L DC    CL21''' not found in table.'
NAMELST1 DS    0F                      NAMES OF VARIABLES (LENGTH 8)
         DC    F'02'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'NCCFPROF'
NAMELST2 DS    0F                      NAMES OF VARIABLES (LENGTH 1)
         DC    F'21'
         DC    F'00'
         DC    CL8'NCCFUSER'
         DC    CL8'TSOUSER'
         DC    CL8'TSO$'
         DC    CL8'TSO1'
         DC    CL8'TSO2'
         DC    CL8'TSO3'
         DC    CL8'TSO4'
         DC    CL8'TSO5'
         DC    CL8'TSO6'
         DC    CL8'TSO7'
         DC    CL8'TSO8'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         DC    CL8'SUB'
NAMELST3 DS    0F                      NAMES OF VARIABLES (LENGTH 44)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZTEMPF'
NAMELST4 DS    0F                      NAMES OF VARIABLES (LENGTH 24)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGS'
NAMELST5 DS    0F                      NAMES OF VARIABLES (LENGTH 78)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGL'
NAMELST6 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'01'
         DC    F'00'
         DC    CL8'SAVTOPB'
NAMELST7 DS    0F                      NAMES OF VARIABLES (LENGTH 6)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZTDTOP'
NAMELST8 DS    0F                      NAMES OF VARIABLES (LENGTH 40)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZCMD'
NAMELST9 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZTDSELS'
NAMELSTA DS    0F                      NAMES OF VARIABLES (LENGTH 80)
         DC    F'02'
         DC    F'00'
         DC    CL8'ZLLGJOB1'
         DC    CL8'ZLLGJOB2'
NAMELSTX DS    0F                      NAMES OF VARS (FILE TAILORING)
         DC    F'19'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'TSO$'
         DC    CL8'TSO1'
         DC    CL8'TSO2'
         DC    CL8'TSO3'
         DC    CL8'TSO4'
         DC    CL8'TSO5'
         DC    CL8'TSO6'
         DC    CL8'TSO7'
         DC    CL8'TSO8'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         TITLE 'USRDEL1 - LITERAL POOL'
         LTORG
         TITLE 'USRDEL1 - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
ISPADDR  DS    A                       ADDRESS OF EP TO ISPLINK
PARMS    DS    7A                      PARM LIST FOR CALL
SAVTOPB  DS    F                       SAVE ZTDTOP (BINARY)
SAVTOPD  DS    D                       SAVE ZTDTOP (FIXED DECIMAL)
DELFLAG  DS    C                       FLAG SET WHEN USER DELETED
SUBCMD2  DS    CL56'CMD(SUBMIT                                         +
                    )'
USERID   DS    CL8                     RACF USER ID
NCCFPROF DS    CL8                     NCCF PROFILE NAME
USRDEL_  DS    CL8                     GENERIC SKELETON NAME
SELCT    DS    CL1                     DETERMINE NCCF PROFILE
NCCFUSER DS    CL1                     INDICATES ADD NCCF USER
TSOUSER  DS    CL1                     INDICATES ADD TSO USER
TSO$     DS    CL1                     CREATE TSO ACCOUNT REC AT ADMIN
TSO1     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 1
TSO2     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 2
TSO3     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 3
TSO4     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 4
TSO5     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 5
TSO6     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 6
TSO7     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 7
TSO8     DS    CL1                     CREATE TSO ACCOUNT REC IN REG 8
RACF$    DS    CL1                     CREATE RACF PROFILE IN REGION 1
RACF1    DS    CL1                     CREATE RACF PROFILE IN REGION 1
RACF2    DS    CL1                     CREATE RACF PROFILE IN REGION 2
RACF3    DS    CL1                     CREATE RACF PROFILE IN REGION 3
RACF4    DS    CL1                     CREATE RACF PROFILE IN REGION 4
RACF5    DS    CL1                     CREATE RACF PROFILE IN REGION 5
RACF6    DS    CL1                     CREATE RACF PROFILE IN REGION 6
RACF7    DS    CL1                     CREATE RACF PROFILE IN REGION 7
RACF8    DS    CL1                     CREATE RACF PROFILE IN REGION 8
SUB      DS    CL1                     INDICATES REGIONAL JOB TO SUBMIT
ZTDSELS  DS    CL4                     ZTDSELS
ZTDTOP   DS    CL6                     ZTDTOP
ZTEMPF   DS    CL44                    FILE TAILORING TEMPORARY FILE
ZCMD     DS    CL40                    ZCMD
SAVLOC   DS    CL40                    SAVE LOCATE COMMAND
USERMSGS DS    CL24                    SHORT VERSION OF ISPF ERROR MSG
USERMSGL DS    CL78                    LONG VERSION OF ISPF ERROR MSG
ZLLGJOB1 DS    CL80                    JOB CARD 1 FROM OPTION 0.2
ZLLGJOB2 DS    CL80                    JOB CARD 2 FROM OPTION 0.2
ISPFSERV DS    CL8                     ISPF SERVICE ROUTINE ID
ISPFRC   DS    CL2                     ISPF SERVICE ROUTINE RETURN CODE
WORKLEN  EQU   *-WORKAREA
         END   USRDEL1
