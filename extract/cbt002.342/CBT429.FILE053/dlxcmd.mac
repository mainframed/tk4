*          DATA SET CBT1081    AT LEVEL 001 AS OF 05/28/80
DLXCMD   TITLE 'DLXCMD  --  ISSUE SYSTEM OPERATOR COMMANDS V2.0'
***********************************************************************
*                                                                     *
*                           C O M M A N D                             *
*                                                                     *
*                   ISSUE SYSTEM OPERATOR COMMANDS                    *
*                                                                     *
*                                                                     *
*     THIS PROGRAM ACCEPTS SYSTEM OR JES OPERATOR COMMANDS AS         *
*     INPUT AND EXECUTES THEM USING SVC 34. IT MUST BE APF            *
*     AUTHORIZED.                                                     *
*                                                                     *
*     COMMAND CAN OPERATE IN THE FOLLOWING MODE:                      *
*                                                                     *
*     1) BATCH PROGRAM                                                *
*        IN THIS MODE COMMAND ACCEPTS INPUT AS A SINGLE COMMAND       *
*        FROM THE PARM FIELD.  THE RESPONSE TO THE OPERATOR COMMAND   *
*        WILL APPEAR ON ALL OPERATOR CONSOLES.  FOR EXAMPLE:          *
*                                                                     *
*   //STEP1    EXEC PGM=DLXCMD,PARM='NSEC,VARY 017,ONLINE'            *
*                                                                     *
***********************************************************************
         EJECT
DLXCMD   CSECT
         SPACE 3
R0       EQU   0                  REGISTER 0
R1       EQU   1                  REGISTER 1
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  WORK REGISTER
R4       EQU   4                  WORK REGISTER
R10      EQU   10                 PARAMETER LIST ADDRESS REGISTER
R11      EQU   11                 SUBROUTINE LINKAGE REGISTER
R12      EQU   12                 BASE REGISTER
R13      EQU   13                 SAVE AREA ADDRESS REGISTER
R14      EQU   14                 REGISTER 14
R15      EQU   15                 REGISTER 15
         EJECT
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         LR    R12,R15            ESTABLISH
         USING DLXCMD,R12         ADDRESSABILITY
         LA    R3,SAVEAREA        GET OUR SAVEAREA ADDRESS
         ST    R13,SAVEAREA+4     ESTABLISH BACK
         ST    R3,8(R13)          AND FORWARD SAVEAREA POINTERS
         LR    R13,R3             ESTABLISH OUR SAVEAREA ADDRESS
         LR    R10,R1             SAVE PARAMETER REGISTER
         SPACE 3
INITDONE DS    0H                 CHECK OUT INPUT PARM
***********************************************************************
*                                                                     *
*     ONLY ONE PARM WAS PASSED, SO WE ARE BEING CALLED IN MODE 1.     *
*     JUST ISSUE THE COMMAND FROM THE PARM STRING.                    *
*                                                                     *
***********************************************************************
ONEPARM  L     R10,0(R10)         GET PARAMETER ADDRESS
         LH    R3,0(R10)          GET PARAMETER TEXT LENGTH
         LTR   R3,R3              IS PARAMETER TEXT PRESENT?
         BZ    EOJ                NO - JUST QUIT
         TM    2(R10),X'F0'       IS IT NUMERIC?
         BNO   EOJ                NO JUST QUIT
         TM    3(R10),X'F0'       IS IT NUMERIC?
         BNO   EOJ                NO JUST QUIT
         TM    4(R10),X'F0'       IS IT NUMERIC?
         BNO   EOJ                NO JUST QUIT
         TM    5(R10),X'F0'       IS IT NUMERIC?
         BNO   EOJ                NO JUST QUIT
         TM    6(R10),C','        IS IT DELIMITER:
         BNO   EOJ                NO JUST QUIT
         MVC   PARMTIME(4),2(R10) MOVE TIME VALUE
         SH    R3,=H'6'           DECREMENT R3 FOR USE IN EXECUTE
         MVC   CARD(*-*),7(R10)   MOVE PARM TEXT INTO SVC 34 LIST
         EX    R3,*-6             EXECUTE MVC
         LA    R3,5(R3)           TEXT PLUS PLIST LENGTH
         STH   R3,CMDLIST         SET SVC 34 LIST LENGTH
         PACK  WORK,PARMTIME      PACK IT
         OI    WORK+7,X'0F'       STRIP SIGN
         CVB   R3,WORK            GET IT IN BINARY
         MH    R3,=H'100'         GET IT IN HUNDREDTHS OF A SECOND
         ST    R3,NUMSEC          STORE NUMBER OF SECONDS TO WAIT
         STIMER WAIT,BINTVL=NUMSEC  WAIT SPECIFIED LENGTH OF TIME
         B     SVC34              GO ISSUE COMMAND
         SPACE 3
EOJ      L     R13,SAVEAREA+4     GET ADDRESS OF CALLER'S SAVEAREA
         RETURN (14,12),RC=0      RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*     THIS ROUTINE ACTUALLY ISSUES THE SVC 34.                        *
*                                                                     *
***********************************************************************
SVC34    DS    0H                 GET SET TO ISSUE THE COMMAND
         MODESET KEY=ZERO         INDICATE KEY=ZERO
         SLR   R0,R0              LOAD R0 FOR SVC34 WITH ZEROS
         LA    R1,CMDLIST         GET ADDRESS OF SVC 34 LIST
         SVC   34                 ISSUE SVC 34 TO PROCESS COMMAND
         MODESET KEY=NZERO        GO BACK TO USER KEY
         SPACE
SVC34END DS    0H                 END OF COMMAND ROUTINE
         B     EOJ                GO TO EOJ
         EJECT
SAVEAREA DC    18F'0'             OUR SAVEAREA
         SPACE
         CNOP  4,8                ALIGN TO MIDDLE OF DOUBLE WORD
CMDLIST  DC    AL2(*-*,0)         SVC 34 LIST
CARD     DC    CL101' '           COMMAND TEXT
PARMTIME DC    CL4' '             TIME FROM PARM
WORK     DS    D                  WORK FIELD FOR PACK
NUMSEC   DC    A(0)               BINTVL VALUE FOR STIMER
         SPACE 3
         LTORG                    BEGIN LITERAL POOL
         END   DLXCMD
