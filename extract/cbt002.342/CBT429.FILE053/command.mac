*          DATA SET CBT1081    AT LEVEL 001 AS OF 05/28/80
COMMAND  TITLE 'C O M M A N D  --  Issue System Operator Commands V2.0'
***********************************************************************
*                                                                     *
*                           C O M M A N D                             *
*                                                                     *
*                   Issue System Operator Commands                    *
*                                                                     *
*                                                                     *
*     This program accepts system or JES operator commands as         *
*     input and executes them using SVC 34. It must be APF            *
*     authorized.                                                     *
*                                                                     *
*     COMMAND can operate in one of three modes:                      *
*                                                                     *
*     1) Batch or CALLed program                                      *
*        In this mode COMMAND accepts input as either 80-byte         *
*        card images from SYSIN or a single command from the PARM     *
*        field. The response to the operator command will             *
*        appear on all operator consoles. For example:                *
*                                                                     *
*           //STEP1    EXEC PGM=COMMAND,PARM='VARY 017,ONLINE'        *
*                                                                     *
*           - or -                                                    *
*                                                                     *
*           //STEP1    EXEC PGM=COMMAND                               *
*           //SYSIN    DD *                                           *
*           VARY 017,ONLINE                                           *
*           /*                                                        *
*                                                                     *
*     2) TSO Command Processor                                        *
*        In this mode COMMAND expects to find the operator command    *
*        directly following on the TSO input line. The response to    *
*        OS commands will then be displayed on the TSO terminal.      *
*        Response to JES commands will appear on all consoles.        *
*        For example:                                                 *
*                                                                     *
*           COMMAND D T         <--- (Response will appear on CRT)    *
*                                                                     *
*     3) Callable subroutine                                          *
*        In this mode COMMAND accepts input according to standard     *
*        OS linkage conventions. In addition to the command itself,   *
*        a second parameter may be passed which contains the          *
*        console number where you wish the response to appear.        *
*        (The SVC 34 will also assume the authority of that           *
*        console.) For example:                                       *
*                                                                     *
*           LA       R1,CMDADDR                                       *
*           LA       R15,=V(COMMAND)                                  *
*           BALR     R14,R15                                          *
*                                                                     *
*           CMDADDR  DC    A(OPERCMD)                                 *
*           CNSADDR  DC    XL1'80',AL3(CONSOLE)                       *
*           OPERCMD  DC    H(3),CL3'D T'                              *
*           CONSOLE  DC    F'5'                                       *
*                                                                     *
*        This would make it appear as if 'D T' were issued from       *
*        console 5. (This method is used by program 'SPY')            *
*                                                                     *
*     Just prior to issuing a command via SVC 34, COMMAND will        *
*     issue a WTO of the form                                         *
*                                                                     *
*        SCECMD01 - userid  - VARY 017,ONLINE                         *
*                                                                     *
*     This will appear on the operator's console and in SYSLOG.       *
*                                                                     *
*     COMMAND may be used to issue replies to WTORs. Since the        *
*     reply may be sensitive (for example, a password) COMMAND        *
*     will supress it whenever a REPLY is issued.                     *
*                                                                     *
*     Author:                                                         *
*       The original version of COMMAND was written by Mark Sorkin.   *
*       The current version has been heavily modified at SCE.         *
*       Questions and comments may be sent to                         *
*                                                                     *
*          Steve Langley                                              *
*          Southern California Edison                                 *
*          P.O. Box 800                                               *
*          Rosemead, Calif. 91770                                     *
*          1-213-572-3435                                             *
*                                                                     *
***********************************************************************
         EJECT
COMMAND  CSECT
         SPACE 3
R0       EQU   0                  register 0
R1       EQU   1                  register 1
R2       EQU   2                  work register
R3       EQU   3                  work register
R4       EQU   4                  work register
R5       EQU   5                  WORK register
R10      EQU   10                 parameter list address register
R11      EQU   11                 subroutine linkage register
R12      EQU   12                 base register
R13      EQU   13                 save area address register
R14      EQU   14                 register 14
R15      EQU   15                 register 15
         EJECT
         SAVE  (14,12)            save caller's registers
         LR    R12,R15            establish
         USING COMMAND,R12        addressability
         LA    R3,SAVEAREA        get our savearea address
         ST    R13,SAVEAREA+4     establish back
         ST    R3,8(R13)          and forward savearea pointers
         LR    R13,R3             establish our savearea address
         LR    R10,R1             save parameter register
         SPACE 2
*   BEGINNING OF RACF CHECKING
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
*
*  IF USER HAS SPECIAL ATTRIBUTE, EXIT WITHOUT FURTHER CHECKING
*
         L     R2,ASCBASXB             GET ADDRESS OF ASXB
         DROP  R2
         USING ASXB,R2                 TELL ASSEMBLER
         L     R2,ASXBSENV             GET ADDRESS OF ACEE
         DROP  R2                      DROP ASXB ADDRESSABILITY
         USING ACEE,R2                 ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEESPEC       USER HAVE SPECIAL ATTRIBUTE?
         BO    ENDRACF                 IF NOT, CHECK FURTHER
         DROP  R2                      DROP ASXB ADDRESSABILITY
         RACHECK ENTITY=PROGNAME,CLASS='APPL'
         LTR   R15,R15
         BZ    ENDRACF
         ABEND X'913',,STEP             ABEND USER
ENDRACF  EQU   *
*   END OF RACF CHECKING
         SPACE 2
         LR    R1,R10             restore parameter register
         L     R2,CVTPTR          R2 = addr of CVT
         USING CVT,R2
         L     R3,CVTTCBP         R3 = addr of addr of our TCB
         L     R4,8(R3)           R4 = addr of our TCB
         USING ASCB,R4
         MVC   ASID,ASCBASID      save our ASID
         DROP  R4
         L     R3,0(R3)           R3 = addr of our TCB
         USING TCB,R3
         L     R3,TCBTIO          R3 = addr of TIOT
         DROP  R3
         USING TIOT1,R3
         MVC   JOBNAME(8),TIOCNJOB save our jobname
         SPACE 3
INITDONE DS    0H                 check out input parm
         TM    0(R10),X'80'       is first the only parm?
         BO    ONEPARM            yes; only parm
         TM    4(R10),X'80'       is second the last parm?
         BO    TWOPARMS           yes; two parms (called with CNxx)
***********************************************************************
*                                                                     *
*     A X'80' was not found in the high order byte of the first       *
*     two parms, so we can assume command is being called as a CP.    *
*     If the command being issued is not a JES command then           *
*     place our ASID into the R0 mask and continue. If it is a        *
*     JES command, simply issue the command (Mode 1).                 *
*                                                                     *
***********************************************************************
         XR    R2,R2              clear R2
         LH    R2,ASID            R2 = ASID of caller
         O     R2,=XL4'00008000'  tell SVC34 its an ASID
         ST    R2,R0MASK          and override the R0 mask
         L     R3,0(R10)          R3 = addr of input line
         LA    R4,4(R3)           R4 = addr of first char of input
         AH    R4,2(R3)           R4 = addr of first char past command
         LH    R2,0(R3)           R2 = total length of string
         SH    R2,2(R3)           R2 = length of oper cmd portion
         BCTR  R2,0               R2 = R2 - 1 for EX MVC
         EX    R2,MOVE1           move cmd into card
         CLI   CARD,C'$'          is this a JES2 cmd?
         BNE   DOSVC34            no;  go do it
         XC    R0MASK(4),R0MASK   yes; don't try to get back reply
         B     DOSVC34
***********************************************************************
*                                                                     *
*     X'80' was found in the high order byte of the second parm,      *
*     so we have been called as a subroutine (Mode 3). The second     *
*     parm contains the console ID where the command is to 'come      *
*     from', so use that for the R0 mask.                             *
*                                                                     *
***********************************************************************
TWOPARMS L     R3,4(R10)          R3 = addr of R0 mask for SVC34
         MVC   R0MASK(4),0(R3)    save R0 mask for SVC34
***********************************************************************
*                                                                     *
*     Only one parm was passed, so we are being called in mode 1.     *
*     Just issue the command from the parm string.                    *
*                                                                     *
***********************************************************************
ONEPARM  L     R10,0(R10)         get parameter address
         LH    R3,0(R10)          get parameter text length
         LTR   R3,R3              is parameter text present?
         BZ    USESYSIN           no - go get commands from SYSIN
         BCTR  R3,R0              decrement R3 for use in execute
         EX    R3,MOVE            execute MVC
         LA    R11,EOJ            get address for return from SVC34
         B     SUBSYS                  ISSUE CMD FROM SUBSYS CONSOLE?
DOSVC34  LA    R11,EOJ            get address for return from SVC34
         B     SVC34              go issue command
         SPACE 3
***********************************************************************
*                                                                     *
*     No parm field found, so look at SYSIN for input                 *
*                                                                     *
***********************************************************************
USESYSIN OPEN  (SYSIN,INPUT)      open SYSIN
GETSYSIN GET   SYSIN,CARD         get a SYSIN record into SVC34 list
         MVC   CARD+72(8),CARD+80 blank out sequence field in 73-80
         CLI   CARD,C'*'          is this a comment?
         BE    GETSYSIN           yes;go get another card
         LA    R11,GETSYSIN       get address for return from SVC34
         B     SUBSYS                  ISSUE CMD FROM SUBSYS CONSOLE?
         SPACE 3
EODSYSIN CLOSE (SYSIN)            close SYSIN
         SPACE 3
EOJ      L     R13,SAVEAREA+4     get address of caller's savearea
         RETURN (14,12),RC=0      return to caller
         EJECT
***********************************************************************
*        CHECK TO SEE IF COMMAND SHOULD BE ISSUED FROM SUBSYS CONSOLE *
***********************************************************************
SUBSYS   EQU   *
         LA    R2,CARD                 R2 - INDEX THROUGH CARD
         LA    R3,68                   R3 - MAXIMUM LENGTH OF CARD
SUBSYS10 EQU   *
         CLC   0(4,R2),=CL4'/SUB'      ISSUE CMD FROM SUBSYS CONSOLE?
         BE    SUBSYS20                YES, GO FIND CONSOLE ID
         LA    R2,1(R2)                NO, INCREMENT R2
         BCT   R3,SUBSYS10                 AND CONTINUE CHECKING
         B     SUBSYS90                RETURN
SUBSYS20 EQU   *
         MVC   0(4,R2),=CL4' '         REMOVE '/SUB' FROM THE COMMAND
         L     R2,CVTPTR               R2 - ADDRESS OF CVT
         USING CVT,R2                  ESTABLISH ADDRESSABILITY TO CVT
         L     R2,CVTCUCB              R2 - ADDRESS OF UCM BASE
         USING UCM,R2                  ESTABLISH ADDRESSABILITY TO UCM
         L     R3,UCMVEA               R3 - ADDR OF UCM DEVICE ENTRIES
         USING UCMLIST,R3              ESTABLISH ADDRESSABILITY
SUBSYS30 EQU   *
         TM    UCMATR,UCMUF            ACTIVE CONSOLE?
         BNO   SUBSYS40                NO, EXTRACT CONSOLE ID
         A     R3,UCMVEZ               POINT TO NEXT UCM DEVICE ENTRY
         C     R3,UCMVEL               HAVE WE CHECKED THEM ALL?
         BNH   SUBSYS30                NO, CHECK NEXT DEVICE ENTRY
         ABEND 999,DUMP                WHAT NO CONSOLES AVAILABLE?
SUBSYS40 EQU   *
         SR    R2,R2                   CLEAR R2
         ICM   R2,B'0001',UCMID        EXTRACT SUBSYS CONSOLE ID
         ST    R2,R0MASK               SET R0 MASK FOR SVC34
         MVC   SAVSTS,UCMSTS           SAVE UCMSTS
         MVC   SAVAUTHA,UCMAUTHA       SAVE UCMAUTHA
         MVC   SAVDISP1,UCMDISP1       SAVE UCMDISP1
         MVC   SAVDISP2,UCMDISP2       SAVE UCMDISP2
         MODESET KEY=ZERO,MODE=SUP     INDICATE PSW KEY(0), SUPERVISOR
SUBSYS50 EQU   *
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=COND,REGS=USE,                 +
               RELATED=(UCM,COMMAND(SUBSYS65))
*                                      LOCAL LOCK NEEDED TO OBTAIN CMS
         C     R15,FULL08              LOCK OBTAINED?
         BE    SUBSYS50                NO, ALREADY HELD, TRY AGAIN
         BH    EOJ                     NO, LEVEL ERROR, QUIT
SUBSYS55 EQU   *
         SETLOCK OBTAIN,TYPE=CMS,MODE=COND,REGS=USE,                   +
               RELATED=(UCM,COMMAND(SUBSYS60))
*                                      CMS LOCK NEEDED TO UPDATE UCM
         C     R15,FULL08              LOCK OBTAINED?
         BE    SUBSYS55                NO, ALREADY HELD, TRY AGAIN
         BH    EOJ                     NO, LEVEL ERROR, QUIT
         OI    UCMAUTHA,UCMAUTH1+UCMAUTH2+UCMAUTH3    CMD GROUPS 1-3
         OI    UCMDISP1,UCMDISPE       MARK FULL I/O CAPABILITY
         OI    UCMDISP2,UCMDISPK+UCMDISPL+UCMDISPM   SUBSYS+PSEUDO MAST
SUBSYS60 EQU   *
         SETLOCK RELEASE,TYPE=CMS,REGS=USE,                            +
               RELATED=(UCM,COMMAND(SUBSYS55))
*                                      RELEASE THE CMS LOCK
SUBSYS65 EQU   *
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          +
               RELATED=(UCM,COMMAND(SUBSYS50))
*                                      RELEASE THE LOCAL LOCK
         MODESET KEY=NZERO,MODE=PROB   GO BACK TO USER KEY, PROBLEM
         LR    R2,R11                  SAVE RETURN ADDRESS
         LR    R5,R3                   SAVE UCM ADDRESS
         BAL   R11,SVC34               GO ISSUE COMMAND
         STIMER WAIT,DINTVL=ONESEC     WAIT FOR 02 SECONDS
         LR    R3,R5                   RESTORE UCM ADDRESS
         MODESET KEY=ZERO,MODE=SUP     INDICATE PSW KEY(0), SUPERVISOR
SUBSYS70 EQU   *
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=COND,REGS=USE,                 +
               RELATED=(UCM,COMMAND(SUBSYS85))
*                                      LOCAL LOCK NEEDED TO OBTAIN CMS
         C     R15,FULL08              LOCK OBTAINED?
         BE    SUBSYS70                NO, ALREADY HELD, TRY AGAIN
         BH    EOJ                     NO, LEVEL ERROR, QUIT
SUBSYS75 EQU   *
         SETLOCK OBTAIN,TYPE=CMS,MODE=COND,REGS=USE,                   +
               RELATED=(UCM,COMMAND(SUBSYS80))
*                                      CMS LOCK NEEDED TO UPDATE UCM
         C     R15,FULL08              LOCK OBTAINED?
         BE    SUBSYS75                NO, ALREADY HELD, TRY AGAIN
         BH    EOJ                     NO, LEVEL ERROR, QUIT
         MVC   UCMSTS,SAVSTS           RESTORE UCMATR
         MVC   UCMAUTHA,SAVAUTHA       RESTORE UCMAUTHA
         MVC   UCMDISP1,SAVDISP1       RESTORE UCMDISP1
         MVC   UCMDISP2,SAVDISP2       RESTORE UCMDISP2
SUBSYS80 EQU   *
         SETLOCK RELEASE,TYPE=CMS,REGS=USE,                            +
               RELATED=(UCM,COMMAND(SUBSYS75))
*                                      RELEASE THE CMS LOCK
SUBSYS85 EQU   *
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          +
               RELATED=(UCM,COMMAND(SUBSYS70))
*                                      RELEASE THE LOCAL LOCK
         MODESET KEY=NZERO,MODE=PROB   GO BACK TO USER KEY, PROBLEM
         BR    R2                      RETURN
SUBSYS90 EQU   *
         B     SVC34                   GO ISSUE COMMAND
         EJECT
***********************************************************************
*                                                                     *
*     This routine actually issues the SVC 34. It either returns      *
*     to the SYSIN-read loop or EOJ depending on the value of R11     *
*                                                                     *
***********************************************************************
SVC34    MVC   WTOTEXT(100),CARD  move command text into WTO list
         CLI   WTOTEXT,C'r'       is this a lower case reply?
         BE    REPLY              no; go on
         CLI   WTOTEXT,C'R'       is this an upper case reply?
         BE    REPLY              no; go on
         CLI   WTOTEXT,X'F0'      start with a number?
         BL    NOTREPLY           no; can't be a reply
REPLY    MVC   WTOTEXT(100),=CL100'Reply Suppressed by ''COMMAND'''
NOTREPLY LA    R3,WTOTEXT+L'WTOTEXT-1 point to end of text
         LA    R4,WTOTEXT         point to beginning of text
         SPACE
FINDTEXT DS    0H                 find last text character
         CR    R3,R4              all blank?
         BL    SVC34END           yes - ignore it
         CLI   0(R3),C' '         blank character?
         BNE   GOTTEXT            no - found end of text
         BCT   R3,FINDTEXT        keep on looking
         SPACE
GOTTEXT  DS    0H                 build parameter list lengths
         MVC   WTOUSER(8),JOBNAME save job name in message
         SR    R3,R4              compute length of
         LA    R3,5(R3)           text and list prefix
         STH   R3,CMDLIST         set SVC 34 list length
         LA    R3,L'WTOID(R3)     add WTO id length
         LA    R3,L'WTOUSER(R3)   add WTO id length
         STH   R3,WTOLEN          set WTO list length
         MODESET KEY=ZERO         indicate key=zero
*        WTO   MF=(E,WTOLIST)     echo command to operator via WTO
         L     R0,R0MASK          load R0 for SVC34 (probably zero)
         LA    R1,CMDLIST         get address of SVC 34 list
         SVC   34                 issue SVC 34 to process command
         MODESET KEY=NZERO        go back to user key
         SPACE
SVC34END DS    0H                 end of command routine
         BR    R11                return to mainline
         EJECT
SAVEAREA DC    18F'0'             our savearea
R0MASK   DC    A(0)               R0 mask for SVC34
JOBNAME  DC    CL8' '             our jobname
ASID     DC    H'00'              our ASID
         SPACE
PROGNAME DC    CL8'COMMAND'
WTOLIST  DS    0F                 WTO list
WTOLEN   DC    AL2(*-*,0)         text length & MCS flags
WTOID    DC    C'SCECMD01 - '     text identifier
WTOUSER  DC    C'XXXXXXXX - '     caller's jobname
WTOTEXT  DC    CL100' '           text passed to command processor
         SPACE
         CNOP  4,8                align to middle of double word
CMDLIST  DC    AL2(*-*,0)         SVC 34 list
CARD     DC    CL101' '           command text
FULL08   DC    F'08'                   FULLWORD CONSTANT, VALUE X'08'
ONESEC   DC    CL8'00000100'           02 SEC TIME INTERVAL
         SPACE
         DS    0F
SAVSTS   DC    XL1'00'            SAVE
SAVAUTHA DC    XL1'00'                 INFO
SAVDISP1 DC    XL1'00'                      FROM
SAVDISP2 DC    XL1'00'                           UCM
         SPACE 3
MOVE     MVC   CARD(*-*),2(R10)   move parm text into SVC 34 list
MOVE1    MVC   CARD(*-*),0(R4)    move parm text into SVC 34 list
         SPACE 3
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,LRECL=80,MACRF=(GM),RECFM=FB,     *
               EODAD=EODSYSIN
         SPACE 3
         LTORG                    begin literal pool
         EJECT
***********************************************************************
*                                                                     *
*                System Control Block Mapping DSECTs                  *
*                                                                     *
*      All these macros can be found in the 'SYS1.AMODGEN' maclib     *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         CVT   DSECT=YES,LIST=YES Communications Vector Table
         SPACE 2
         DSECT
         IEFTIOT1                 Task I/O Table
         SPACE 2
         IEECUCM FORMAT=NEW            UNIT CONTROL MODULE
         SPACE 2
         IHAPSA                        PREFIXED SAVE AREA
         SPACE 2
         IHAASCB                  Address Space Control Block
         SPACE 2
         IHAASXB
         SPACE 2
         IHAACEE
         SPACE 2
         IKJTCB                   Task Control Block
         SPACE 2
         END
