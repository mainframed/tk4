         TITLE 'USRINQ - DELUXE USER REGISTRATION (UPDATE A USER)'
***********************************************************************
*                                                                     *
*        MODULE NAME = USRINQ                                         *
*                                                                     *
*        CHANGE ACTIVITY:                                             *
*              06/25/86  WRITTEN.                               JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'USRINQ - ISPCALL MACRO'
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         +
               &EP=ISPLINK,                                            +
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    +
               &EPADDR=,       LABEL FOR ADDRESS OF EP OF ISPLINK      +
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    +
               &WKREG=,        WORK REGISTER (DEFAULT 1)               +
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG,&WKEP
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1,&ISPEP
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHES+
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
         EJECT
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 +
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LIS+
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
         EJECT
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
.*       LA    1,&WKAR.+4                                       ISPCALL
.*       L     15,=V(&EP)                                       ISPCALL
.*       BALR  14,15                                            ISPCALL
         AIF   (T'&EPADDR EQ 'O').EP2
&ISPEP   SETC  '&EPADDR'
         LOAD  EP=ISPLINK
         ST    R0,&ISPEP
.EP2     ANOP
&WKEP    SETC  '&ISPEP'
         L     15,&WKEP
         LA    1,&WKAR.+4
         CALL  (15)
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
         EJECT
         TITLE 'USRINQ - INITIALIZATION SECTION'
USRINQ   CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,USRINQ_&SYSDATE_&SYSTIME     SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING USRINQ,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*  INITIALIZATION SECTION - SET DIALOG CONTROL, DEFINE VARIABLES
***********************************************************************
CONTROL  EQU   *
*                                      SET DIALOG CONTROL
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        +
               WKAREA=PARMS,                                           +
               EPADDR=ISPADDR
VDEF1    EQU   *
         MVC   ISPFSERV,DEFID          SET ISPF DIALOG SERVICE
*                                      DEFINE VARIABLES
         ISPCALL ('VDEFINE',NAMELST1,USERID,'CHAR',8)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF2                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF2    EQU   *
         ISPCALL ('VDEFINE',NAMELST2,FNAME,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF3                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF3    EQU   *
         ISPCALL ('VDEFINE',NAMELST3,RACFATT0,'CHAR',12)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF4                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF4    EQU   *
         ISPCALL ('VDEFINE',NAMELST4,RACF$,'CHAR',1)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF5                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF5    EQU   *
         ISPCALL ('VDEFINE',NAMELST5,ROOM,'CHAR',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF6                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF6    EQU   *
         ISPCALL ('VDEFINE',NAMELST6,USERMSGS,'CHAR',24)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF7                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF7    EQU   *
         ISPCALL ('VDEFINE',NAMELST7,USERMSGL,'CHAR',78)
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF10   EQU   *
         B     VGET                    RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VDEF)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VDEF)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION II - RETRIEVE VARIABLES
***********************************************************************
VGET     EQU   *
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      SELECT A FUNCTION
         ISPCALL ('VGET',NAMELSTX)
         B     VGET10(R15)             CHECK RETURN CODE
VGET10   EQU   *
         B     DISPLAY1                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - DISPLAY INQUIRY SELECTION PANEL
***********************************************************************
DISPLAY1 EQU   *
         MVC   ISPFSERV,DISID          SET ISPF DIALOG SERVICE
*                                      DISPLAY PANEL
         ISPCALL ('DISPLAY','USRINQ1')
         B     DISPL110(R15)           CHECK RETURN CODE
DISPL110 EQU   *
         B     DISPL120                RC = 00 (CONTINUE)
         B     DISPL120                RC = 04 (CONTINUE)
         B     VPUT                    RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
DISPL120 EQU   *
         CLC   SELCT,RACF              RACF SELECTED?
         BE    DISPLAY2                YES, DISPLAY RACF ENTRY PANEL
         CLC   SELCT,TSO               TSO SELECTED?
         BE    DISPLAY3                YES, DISPLAY TSO ENTRY PANEL
         B     DISPLAY4                DISPLAY NCCF ENTRY PANEL
***********************************************************************
*  INITIALIZATION SECTION - DISPLAY RACF ENTRY PANEL
***********************************************************************
DISPLAY2 EQU   *
         MVC   ISPFSERV,DISID          SET ISPF DIALOG SERVICE
*                                      DISPLAY PANEL
         ISPCALL ('DISPLAY','USRINQ2')
         B     DISPL210(R15)           CHECK RETURN CODE
DISPL210 EQU   *
         B     DISPLAY2                RC = 00 (CONTINUE)
         B     DISPLAY2                RC = 04 (CONTINUE)
         B     DISPLAY1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - DISPLAY TSO ENTRY PANEL
***********************************************************************
DISPLAY3 EQU   *
         MVC   ISPFSERV,DISID          SET ISPF DIALOG SERVICE
*                                      DISPLAY PANEL
         ISPCALL ('DISPLAY','USRINQ3')
         B     DISPL310(R15)           CHECK RETURN CODE
DISPL310 EQU   *
         B     DISPLAY3                RC = 00 (CONTINUE)
         B     DISPLAY3                RC = 04 (CONTINUE)
         B     DISPLAY1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - DISPLAY NCCF ENTRY PANEL
***********************************************************************
DISPLAY4 EQU   *
         MVC   ISPFSERV,DISID          SET ISPF DIALOG SERVICE
*                                      DISPLAY PANEL
         ISPCALL ('DISPLAY','USRINQ4')
         B     DISPL410(R15)           CHECK RETURN CODE
DISPL410 EQU   *
         B     DISPLAY4                RC = 00 (CONTINUE)
         B     DISPLAY4                RC = 04 (CONTINUE)
         B     DISPLAY1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - UPDATE VARIABLES IN POOL
***********************************************************************
VPUT     EQU   *
         MVC   ISPFSERV,PUTID          SET ISPF DIALOG SERVICE
*                                      UPDATE VARIABLES IN POOL
         ISPCALL ('VPUT',NAMELSTX)
         B     VPUT10(R15)             CHECK RETURN CODE
VPUT10   EQU   *
         B     FINISH                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VPUT)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VPUT)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'USRINQ - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
USRERR04 EQU   *
         MVC   ISPFRC,FOUR             SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR08 EQU   *
         MVC   ISPFRC,EIGHT            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR12 EQU   *
         MVC   ISPFRC,TWELVE           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR16 EQU   *
         MVC   ISPFRC,SIXTEEN          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR20 EQU   *
         MVC   ISPFRC,TWENTY           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
MSGDISP1 EQU   *
         MVC   USERMSGS,USERERRS       SET SHORT MESSAGE
         MVC   USERMSGL,USERERRL       SET LONG MESSAGE
         MVC   USERMSGL+21(8),ISPFSERV    SET ISPF DIALOG SERVICE
         MVC   USERMSGL+47(2),ISPFRC   SET DIALOG SERVICE RETURN CODE
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','USRINQ1',USERMSG)
         B     FINISH
         TITLE 'USRINQ - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'USRINQ - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
         TITLE 'USRINQ - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
SPACES   DC    CL80' '                 SPACES
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
RACF     DC    CL1'1'                  INDICATES RACF ENTRY PANEL
TSO      DC    CL1'2'                  INDICATES TSO ENTRY PANEL
NCCF     DC    CL1'3'                  INDICATES NCCF ENTRY PANEL
USERMSG  DC    CL8'USER000'            GENERIC USER MESSAGE
DEFID    DC    CL8'VDEFINE'            ISPF DIALOG SERVICE (VDEFINE)
GETID    DC    CL8'VGET'               ISPF DIALOG SERVICE (VGET)
PUTID    DC    CL8'VPUT'               ISPF DIALOG SERVICE (VPUT)
DISID    DC    CL8'DISPLAY'            ISPF DIALOG SERVICE (DISPLAY)
USERERRS DC    CL24'USRINQ PROGRAM ERROR. '
USERERRL DC    CL78'ISPF Dialog Service, 12345678, Ended with RC = 00. +
               Notify Systems Programming.'
NAMELST1 DS    0F                      NAMES OF VARIABLES (LENGTH 8)
         DC    F'10'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'OWNER'
         DC    CL8'DFLTGRP'
         DC    CL8'TSOMAXR'
         DC    CL8'TSODEST'
         DC    CL8'TSOATT0'
         DC    CL8'TSOATT1'
         DC    CL8'TSOATT2'
         DC    CL8'TSOATT3'
         DC    CL8'NCCFPROF'
NAMELST2 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'02'
         DC    F'00'
         DC    CL8'FNAME'
         DC    CL8'LNAME'
NAMELST3 DS    0F                      NAMES OF VARIABLES (LENGTH 12)
         DC    F'04'
         DC    F'00'
         DC    CL8'RACFATT0'
         DC    CL8'RACFATT1'
         DC    CL8'RACFATT2'
         DC    CL8'RACFATT3'
NAMELST4 DS    0F                      NAMES OF VARIABLES (LENGTH 1)
         DC    F'21'
         DC    F'00'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         DC    CL8'TSO$'
         DC    CL8'TSO1'
         DC    CL8'TSO2'
         DC    CL8'TSO3'
         DC    CL8'TSO4'
         DC    CL8'TSO5'
         DC    CL8'TSO6'
         DC    CL8'TSO7'
         DC    CL8'TSO8'
         DC    CL8'TSOUSER'
         DC    CL8'NCCFUSER'
         DC    CL8'SELCT'
NAMELST5 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'02'
         DC    F'00'
         DC    CL8'ROOM'
         DC    CL8'EXTENS'
NAMELST6 DS    0F                      NAMES OF VARIABLES (LENGTH 24)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGS'
NAMELST7 DS    0F                      NAMES OF VARIABLES (LENGTH 78)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGL'
NAMELSTX DS    0F                      NAMES OF VPUT VARIABLES
         DC    F'38'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'FNAME'
         DC    CL8'LNAME'
         DC    CL8'OWNER'
         DC    CL8'DFLTGRP'
         DC    CL8'RACFATT0'
         DC    CL8'RACFATT1'
         DC    CL8'RACFATT2'
         DC    CL8'RACFATT3'
         DC    CL8'ROOM'
         DC    CL8'EXTENS'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         DC    CL8'TSOMAXR'
         DC    CL8'TSODEST'
         DC    CL8'TSOATT0'
         DC    CL8'TSOATT1'
         DC    CL8'TSOATT2'
         DC    CL8'TSOATT3'
         DC    CL8'TSO$'
         DC    CL8'TSO1'
         DC    CL8'TSO2'
         DC    CL8'TSO3'
         DC    CL8'TSO4'
         DC    CL8'TSO5'
         DC    CL8'TSO6'
         DC    CL8'TSO7'
         DC    CL8'TSO8'
         DC    CL8'TSOUSER'
         DC    CL8'NCCFPROF'
         DC    CL8'NCCFUSER'
         TITLE 'USRINQ - LITERAL POOL'
         LTORG
         TITLE 'USRINQ - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
ISPADDR  DS    A                       ADDRESS OF EP TO ISPLINK
PARMS    DS    7A                      PARM LIST FOR CALL
USERID   DS    CL8                     RACF USER ID
OWNER    DS    CL8                     RACF OWNER
DFLTGRP  DS    CL8                     RACF DFLTGRP
TSOMAXR  DS    CL8                     TSO PROC MAXIMUM REGION
TSODEST  DS    CL8                     TSO DEFAULT DESTINATION
TSOATT0  DS    CL8                     ACCT / NOACCT TSO ATTRIBUTE
TSOATT1  DS    CL8                     JCL / NOJCL TSO ATTRIBUTE
TSOATT2  DS    CL8                     MOUNT / NOMOUNT TSO ATTRIBUTE
TSOATT3  DS    CL8                     OPER / NOOPER TSO ATTRIBUTE
NCCFPROF DS    CL8                     NCCF PROFILE NAME
FNAME    DS    CL20                    USER'S FIRST NAME
LNAME    DS    CL20                    USER'S LAST NAME
RACFATT0 DS    CL12                    SPECIAL / NOSPECIAL RACF ATTR
RACFATT1 DS    CL12                    OPERATIONS / NOOPERATIONS RACF
RACFATT2 DS    CL12                    AUDITOR / NOAUDITOR RACF ATTR
RACFATT3 DS    CL12                    REVOKED INDICATION
USERMSGS DS    CL24                    SHORT VERSION OF ISPF ERROR MSG
USERMSGL DS    CL78                    LONG VERSION OF ISPF ERROR MSG
RACF$    DS    CL1                     ADMIN RACF USER
RACF1    DS    CL1                     REG 1 RACF USER
RACF2    DS    CL1                     REG 2 RACF USER
RACF3    DS    CL1                     REG 3 RACF USER
RACF4    DS    CL1                     REG 4 RACF USER
RACF5    DS    CL1                     REG 5 RACF USER
RACF6    DS    CL1                     REG 6 RACF USER
RACF7    DS    CL1                     REG 7 RACF USER
RACF8    DS    CL1                     REG 8 RACF USER
TSO$     DS    CL1                     ADMIN TSO USER
TSO1     DS    CL1                     REG 1 TSO USER
TSO2     DS    CL1                     REG 2 TSO USER
TSO3     DS    CL1                     REG 3 TSO USER
TSO4     DS    CL1                     REG 4 TSO USER
TSO5     DS    CL1                     REG 5 TSO USER
TSO6     DS    CL1                     REG 6 TSO USER
TSO7     DS    CL1                     REG 7 TSO USER
TSO8     DS    CL1                     REG 8 TSO USER
TSOUSER  DS    CL1                     THIS IS A TSO USER
NCCFUSER DS    CL1                     THIS IS A NCCF USER
SELCT    DS    CL1                     SELECTION - RACF, TSO, NCCF
ROOM     DS    CL4                     ROOM NUMBER
EXTENS   DS    CL4                     PHONE EXTENSION
ISPFSERV DS    CL8                     ISPF SERVICE ROUTINE ID
ISPFRC   DS    CL2                     ISPF SERVICE ROUTINE RETURN CODE
WORKLEN  EQU   *-WORKAREA
         END   USRINQ
