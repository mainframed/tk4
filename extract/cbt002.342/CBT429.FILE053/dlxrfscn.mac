         TITLE 'DLXRFSCN - SCAN RACF USER PROFILES'
***********************************************************************
*                                                                     *
*             MODULE NAME = DLXRFSCN                                  *
*                                                                     *
*             DESCRIPTIVE NAME = SCAN RACF USER PROFILES TO CHECK     *
*                FOR INACTIVE USERS.                                  *
*                                                                     *
*             FUNCTION =                                              *
*                SCANS THE RACF USER PROFILES AND CREATES 3 LISTS:    *
*                1) USERS WHO HAVE NEVER LOGGED ON, BUT WHOSE         *
*                   PROFILES ARE AT LEAST 7 DAYS OLD.                 *
*                2) USERS WHO ARE WITHIN 7 DAYS OF BEING REVOKED      *
*                   FOR INACTIVITY.                                   *
*                3) FINANCIAL INSTITUTIONS THAT HAVE BEEN INACTIVE    *
*                   FOR 'PARM=NN' DAYS.                               *
*                ALSO, A CLIST IS PRODUCED TO REVOKE THE USER         *
*                PROFILES PRINTED ON LIST #1.                         *
*                                                                     *
*                OPERATION =                                          *
*                   READ AND PROCESS PARM                             *
*                   OPEN OUTPUT DATASETS                              *
*                   READ USER PROFILE FROM RACF DATASET               *
*                   IF USER IS REVOKED, OUTPUT ON REPORT4             *
*                   IF USER HAS NEVER LOGGED ON, AND HAS BEEN         *
*                     DEFINED FOR AT LEAST 7 DAYS, OUTPUT ON          *
*                     REPORT1 AND OUTPUT LINE IN CLIST TO             *
*                     REVOKE THE USER                                 *
*                   IF USER IS WITHIN 7 DAYS OF BEING REVOKED,        *
*                     OUTPUT ON REPORT2                               *
*                   IF USER IS A FINANCIAL INSTITUTION WHICH HAS      *
*                     BEEN INACTIVE FOR THE NUMBER OF DAYS INDICATED  *
*                     BY THE PARM, OUTPUT ON REPORT3                  *
*                   GO TO READ NEXT USER PROFILE                      *
*                   IF ALL USER PROFILES PROCESSED, CLOSE FILES       *
*                   EXIT                                              *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO DLXRFSCN   *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER           *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*             MACROS =                                                *
*                SAVE, OPEN, PUT, CLOSE, RETURN                       *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                09/30/87  WRITTEN.      BHC.                         *
*                                                                     *
* SAMPLE JCL =                                                        *
*                                                                     *
*     //JOBNAME  JOB (GEE1,0104),'PROG NAME',CLASS=S,MSGCLASS=A,      *
*     //         MSGLEVEL=(2,0),USER=SYSPGM,PASSWORD=????????         *
*     //STEP1    EXEC  PGM=DLXRFSCN,PARM='14'                         *
*     //REPORT1  DD    SYSOUT=*                                       *
*     //REPORT2  DD    SYSOUT=*                                       *
*     //REPORT3  DD    SYSOUT=*                                       *
*     //REPORT4  DD    SYSOUT=*                                       *
*     //CLIST    DD    DSN=T99999.CMDS.CLIST(DLXRFSCN),DISP=SHR       *
*                                                                     *
***********************************************************************
         PRINT NOGEN
*
DLXRFSCN CSECT
         SAVE  (14,12),,DLXRFSCN_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING DLXRFSCN,R12            ESTABLISH ADDRESSABILITY
         LR    R2,R1                   PRESERVE PTR TO PARMS
         ST    R13,SAVEAREA+4          SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                  SAVE IT IN R6
         LA    R13,SAVEAREA            POINT R13 AT OUT SAVE AREA
         ST    R13,8(R6)               STORE OUR SAVEAREA ADDR IN OS
*
*  PROCESS PARAMETERS.
*
GETPARM  EQU   *
         L     R5,0(R2)                GET PARM ADDRESS
         LH    R3,0(R5)                SAVE PARM LENGTH
         LR    R7,R3                   AND SAVE FOR LATER
         LTR   R3,R3                   ANY PARM DATA?
         BZ    GP80                    IF NOT, BRANCH TO DEFAULT
         LA    R5,2(R5)                SKIP PARM LENGTH FIELD
GP05     EQU   *
         LA    R4,PARMBUF              SET POINTER INTO PARM BUFFER
         MVI   PARMBUF,X'40'           CLEAR PARM BUFFER
         MVC   PARMBUF+1(L'PARMBUF-1),PARMBUF
GP10     EQU   *
         MVC   0(1,R4),0(R5)           STORE PARM CHARACTER
         LA    R4,1(R4)                INCREMENT PARM BUFFER POINTER
         LA    R5,1(R5)                GO TO NEXT PARM CHARACTER
         BCT   R3,GP10                 LOOP BACK TIL DONE
         BCTR  R7,0
         EX    R7,PACKNUM              CONVERT TO PACKED DECIMAL
         MVC   P#DAYS,PWORK+4          STORE
         UNPK  TITLE3B+22(3),P#DAYS+2(2)  CONVERT TO EBCDIC
GP80     EQU   *
         MVC   RACFWORK(4),RACFLENG    INIT LENGTH OF WORK AREA
GETDATE  EQU   *
         TIME  DEC                     GET CURRENT DATE
         ST    R1,CDAY                 STORE IT
         ST    R1,WORK                 WORK = 00YYDDDF
         XC    WORK(2),WORK            WORK = 0000DDDF
         SRL   R1,12                   R1 = 00000YYD
         ST    R1,PWORK+4              PWORK+4 = 00000YYD
         OI    PWORK+7,X'0F'           PWORK+4 = 00000YYF
         UNPK  YY,PWORK+6(2)           STORE THE YEAR
         CVB   R1,PWORK                CONVERT THE YEAR TO BINARY
         N     R1,=F'3'                LEAP YEAR?
         BNZ   GD10                    IF NOT, BRANCH
         AP    DAYSYEAR,=P'1'          INCREASE DAYS IN A YEAR
         AP    MONTH-1,=P'1'           ADD A DAY TO FEBRUARY
GD10     EQU   *
         LA    R1,MONTH                POINT TO JANUARY
GD20     EQU   *
         SP    WORK,0(1,R1)            SUBTRACT DAYS IN THE MONTH
         SP    WORK,=P'30'
         BNP   GD30                    WHEN NEGATIVE, BRANCH OUT
         BCTR  R1,0                    GO BACKWARDS THRU TABLE
         AP    COUNT,=P'1'             COUNT THE MONTHS
         B     GD20                    AND LOOP
GD30     EQU   *
         AP    WORK,0(1,R1)            ADD LAST MONTH BACK IN
         AP    WORK,=P'30'
         OI    COUNT+L'COUNT-1,X'0F'   COUNT = 0MMF
         UNPK  MM,COUNT                STORE THE MONTH
         OI    WORK+3,X'0F'
         UNPK  DD,WORK                 STORE THE DAY
GETSYSID EQU   *
         L     R2,CVTPTR               GET ADDR OF CVT
         USING CVT,R2                  ESTABLISH ADDRESSABILITY
         L     R8,CVTSMCA              GET ADDR OF SMCA
         USING SMCABASE,R8             ESTABLISH ADDRESSABILITY
         MVC   SYSID,SMCASID           OBTAIN SYSTEM ID
         DROP  R2
         DROP  R8
         CLC   SYSID(3),=CL3'ADM'      ONE OF ADMIN'S COMPUTERS?
         BNE   OPENFILE                IF NOT, CONTINUE
         MVC   TITLEC,ADMIN            ELSE, LOAD ADMINISTRATION
OPENFILE EQU   *
         OPEN  (REPORT1,OUTPUT)        OPEN OUTPUT DATASETS
         OPEN  (REPORT2,OUTPUT)        OPEN OUTPUT DATASETS
         OPEN  (REPORT3,OUTPUT)        OPEN OUTPUT DATASETS
         OPEN  (REPORT4,OUTPUT)        OPEN OUTPUT DATASETS
         OPEN  (CLIST,OUTPUT)          OPEN CLIST DATASET
         MVI   CMDLINE,X'40'
         MVC   CMDLINE+1(L'CMDLINE-1),CMDLINE
         MVC   CMDLINE(L'CLINE1),CLINE1
         LA    R5,CMDLINE+L'CLINE1
         LA    R3,CLINE
         SR    R5,R3
         BAL   R10,PRINTC
         MVC   CMDLINE(L'CLINE2),CLINE2
         LA    R5,CMDLINE+L'CLINE2
         LA    R3,CLINE
         SR    R5,R3
         BAL   R10,PRINTC
GETUSR   EQU   *
         MVI   USERID,X'40'            INITIALIZE RACF NAME AREA
         MVC   USERID+1(43),USERID
         MVI   USERIDL,X'01'           INITIALIZE RACF NAME LENGTH
GETUSR1  EQU   *
         ICHEINTY NEXTC,TYPE='USR',ENTRY=USERIDL,                      X
               ACTIONS=(U1,U2,U3,U4,U5),                               X
               WKAREA=RACFWORK
         LTR   R15,R15                 IF OK,
         BZ    PROFU                      CONTINUE
         C     R15,=F'12'              END OF RACF GROUP PROFILES?
         BE    ENDUSR                  IF SO, GO CLOSE AND END
         B     CLOSEX                  IF NOT, TAKE ERROR EXIT
PROFU    EQU   *
         LA    R2,RACFWORK             GET ADDRESS OF RACF WORK AREA
         USING USRFLD0,R2              ESTABLISH ADDRESSABILITY
         CLI   USFLAG4,X'00'           CHECK REVOKE FLAG
         BE    PF20                    IF USER REVOKED, GO TO LIST4
LIST4    EQU   *
         LA    R3,LINE#4               POINT TO PARMS
         LA    R5,REPORT4              POINT TO DCB4
         BAL   R10,PRTLINE             OUTPUT THE LINE
         B     GETUSR1
PF20     EQU   *
         CLC   USLJD,=XL3'FFFFFF'      HAS USER EVER LOGGED ON?
         BNE   PF30                    IF SO, CONTINUE
         MVC   PDATE+1(3),USAUTHD      GET CREATION DATE
         AP    PDATE,=P'7'             ADD SEVEN DAYS
         BAL   R10,CHKDATE             CHECK FOR VALID DATE
         CP    PDATE,CDAY              COMPARE TO CURRENT DATE
         BH    GETUSR1                 IF < OR =, OUTPUT ON LIST1
LIST1    EQU   *
         LA    R3,LINE#1               POINT TO PARMS
         LA    R5,REPORT1              POINT TO DCB1
         BAL   R10,PRTLINE             OUTPUT THE LINE
         MVC   CMDLINE(L'CLINE3),CLINE3
         LA    R5,CMDLINE+L'CLINE3
         LA    R3,CLINE
         SR    R5,R3
         BAL   R10,PRINTC              ALSO ADD USER TO CLIST
         B     GETUSR1                 THAT'S ALL FOR THIS GROUP
PF30     EQU   *
         MVC   PDATE+1(3),USLJD        GET LAST LOGON
         AP    PDATE,=P'30'            ADD 30 DAYS
         BAL   R10,CHKDATE
         MVC   PDATE1,PDATE
         MVC   PDATE,CDAY              GET CURRENT DAY
         AP    PDATE,=P'7'             ADD 7 DAYS
         BAL   R10,CHKDATE
         CP    PDATE1,PDATE            COMPARE
         BH    PF40                    IF < OR =, OUTPUT ON LIST2
LIST2    EQU   *
         LA    R3,LINE#2               POINT TO PARMS
         LA    R5,REPORT2              POINT TO DCB2
         BAL   R10,PRTLINE             OUTPUT THE LINE
PF40     EQU   *
         CLC   USDFLTG(5),=CL5'FIAOE'  FINANCIAL INSTITUTION?
         BNE   GETUSR1                 IF NOT, ALL DONE
         MVC   PDATE+1,USLJD           GET LAST LOGON
         AP    PDATE,P#DAYS            ADD PARM
         BAL   R10,CHKDATE
         CP    PDATE,CDAY              COMPARE TO CURRENT DATE
         BH    GETUSR1                 IF < OR =, OUTPUT ON LIST3
LIST3    EQU   *
         LA    R3,LINE#3               POINT TO PARMS
         LA    R5,REPORT3              POINT TO DCB3
         BAL   R10,PRTLINE             OUTPUT THE LINE
         B     GETUSR1
ENDUSR   EQU   *
         MVC   CMDLINE(L'CLINE4),CLINE4
         LA    R5,CMDLINE+L'CLINE4
         LA    R3,CLINE
         SR    R5,R3
         BAL   R10,PRINTC              END THE CLIST
         B     CLOSE
*
*  CLOSE FILES AND TERMINATE PROGRAM.
*
CLOSEX   EQU   *                       ERROR EXIT
         LR    R7,R15                  SAVE CONTENTS OF R15
         CLOSE (REPORT1)               CLOSE THE FILE
         CLOSE (REPORT2)
         CLOSE (REPORT3)
         CLOSE (REPORT4)
         CLOSE (CLIST)
         LR    R15,R7                  RESTORE CONTENTS OF R15
         B     RETURN
CLOSE    EQU   *
         CLOSE (REPORT1)               CLOSE THE FILE
         CLOSE (REPORT2)
         CLOSE (REPORT3)
         CLOSE (REPORT4)
         CLOSE (CLIST)
         SR    R15,R15                 SET RETURN CODE
RETURN   L     R13,4(R13)              RESTORE REGISTERS
         RETURN (14,12),RC=(15)        AND END
***********************************************************************
*
*  WRITE THE CLIST
*
PRINTC   EQU   *
         AP    LINE#,=P'100'           INCREMENT LINE NUMBER
         UNPK  LINENO,LINE#            UNPACK
         MVZ   LINENO+7(1),LINENO+6      & MOVE ZONE BYTE
         STH   R5,CLINE                STORE LINE LENGTH
         MVC   CLINE+2(2),=X'0000'
         PUT   CLIST,CLINE             WRITE INTO CLIST FILE
         MVI   CLINE,X'40'
         MVC   CLINE+1(L'CLINE-1),CLINE
         BR    R10
*
*  CHECK FOR A VALID DATE
*
CHKDATE  EQU   *
         MVC   DAYSYEAR,=PL2'365'      INIT DAYS / YEAR
         L     R1,PDATE                R1 = 00YYDDDF
         MVC   PDAY,PDATE+2            SAVE JUST THE DAY
         SRL   R1,12                   R1 = 00000YYD
         ST    R1,PWORK+4
         OI    PWORK+7,X'0F'           PWORK+4 = 00000YYF
         CVB   R1,PWORK                CONVERT TO BINARY
         BCTR  R1,0                    DECREMENT FOR LAST YEAR
         N     R1,=F'3'                LEAP YEAR?
         BNZ   CD10                    IF NOT, BRANCH
         AP    DAYSYEAR,=P'1'          INCREASE DAYS IN LAST YEAR
CD10     EQU   *
         CP    PDAY,DAYSYEAR           INTO NEXT YEAR?
         BNH   CD20                    IF NOT, CONTINUE
         SP    PDATE,DAYSYEAR          IF SO, SUBTRACT DAYS / YEAR
         AP    PDATE,=P'1000'          AND ADD A YEAR
CD20     EQU   *
         BR    R10
*
*  FORMAT THE REPORT LINE
*
FMTLINE  EQU   *
         ICM   R7,B'0001',USERIDL      GET LENGTH OF USERID
         BCTR  R7,0                    DECREMENT
         EX    R7,MOVNAME              STORE USERID
         MVC   CLINE3+5(8),UID         ALSO STORE IN CLIST LINE
         CLC   USPGMNM(8),=CL8'########'
         BNE   FL10
         MVC   NAME(7),=CL7'UNKNOWN'
         B     FL20
FL10     EQU   *
         MVC   NAME,USPGMNM            STORE USER NAME
FL20     EQU   *
         MVC   GROUP,USDFLTG           STORE DEFAULT GROUP
         UNPK  YYDDD,USAUTHD           UNPACK DATE
         MVC   AYEAR,YYDDD             STORE THE YEAR
         MVI   AYEAR+2,C'.'
         MVC   ADAY,YYDDD+2            STORE THE DAY
         CLC   USLJD,=XL3'FFFFFF'      DATE UNKNOWN?
         BNE   FL30
         MVC   LYEAR(7),=CL7'UNKNOWN'
         B     FL40
FL30     EQU   *
         UNPK  YYDDD,USLJD             UNPACK DATE
         MVC   LYEAR,YYDDD             STORE THE YEAR
         MVI   LYEAR+2,C'.'
         MVC   LDAY,YYDDD+2            STORE THE DAY
FL40     EQU   *
         BR    R9
*
*  PRINT A REPORT LINE
*
PRTLINE  EQU   *
         MVI   LINE,X'40'              CLEAR OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         L     R4,0(R3)                GET LINE NUMBER
         LTR   R4,R4                   START NEW PAGE?
         BNZ   PL10                    IF NOT, BRANCH
         MVC   LINE+49(36),4(R3)       GET 'A' TITLE
         MVI   LINE,C'1'
         PUT   (R5),LINE               OUTPUT
         MVC   LINE+49(36),40(R3)      GET 'B' TITLE
         MVI   LINE,C' '
         PUT   (R5),LINE               OUTPUT
         MVC   LINE+49(36),TITLEC      GET 'C' TITLE
         MVI   LINE,C' '
         PUT   (R5),LINE               OUTPUT
         MVC   LINE+49(36),TITLED      GET 'D' TITLE
         MVI   LINE,C' '
         PUT   (R5),LINE               OUTPUT
         MVI   LINE,X'40'              CLEAR OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVI   LINE,C'-'
         MVC   LINE+21(75),HEADER      LOAD HEADER
         PUT   (R5),LINE               OUTPUT
         MVI   LINE,X'40'              CLEAR OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVI   LINE,C'0'
PL10     EQU   *
         BAL   R9,FMTLINE              FORMAT THE OUTPUT LINE
         PUT   (R5),LINE               OUTPUT
         MVI   LINE,X'40'              CLEAR OUTPUT LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R4,1(R4)                COUNT THE LINE
         C     R4,=F'51'               END-OF-PAGE?
         BNE   PL20                    IF NOT, BRANCH
         SLR   R4,R4
PL20     EQU   *
         ST    R4,0(R3)                STORE THE NEXT LINE#
         BR    R10
*
***********************************************************************
*                     CONSTANT DATA                                   *
***********************************************************************
*
*  REGISTER EQUATES
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*    THE FOLLOWING FIELDS WILL BE RETRIEVED FROM THE RACF DATA SET
*
U1       ICHEACTN FIELD=AUTHDATE
U2       ICHEACTN FIELD=FLAG4
U3       ICHEACTN FIELD=PGMRNAME
U4       ICHEACTN FIELD=DFLTGRP
U5       ICHEACTN FIELD=LJDATE
*
***********************************************************************
SAVEAREA DS    18F
*
PWORK    DS    1D
*
RACFLENG DC    F'76'
WORK     DS    1F
*
CDAY     DC    PL4'0'
PDATE    DC    PL4'0'
PDATE1   DC    PL4'0'
P#DAYS   DC    PL4'7'
LINE#    DC    PL4'0'
DAYSYEAR DC    PL2'365'
PDAY     DC    PL2'0'
COUNT    DC    PL2'001'
*
YYDDD    DS    CL5
PARMBUF  DS    CL8
USERIDL  DS    XL1
USERID   DS    CL8
RACFWORK DS    XL80
ADMIN    DC    CL36'           ADMINISTRATION           '
*
LINE#1   DC    F'0'
TITLE1A  DC    CL36'   USERS WHO HAVE NEVER LOGGED ON   '
TITLE1B  DC    CL36'   (PROFILES AT LEAST 7 DAYS OLD)   '
LINE#2   DC    F'0'
TITLE2A  DC    CL36'    USERS WHO ARE WITHIN 7 DAYS     '
TITLE2B  DC    CL36'         OF BEING REVOKED           '
LINE#3   DC    F'0'
TITLE3A  DC    CL36' FINANACIAL INSTITUTIONS THAT HAVE  '
TITLE3B  DC    CL36'    BEEN INACTIVE FOR XXX DAYS      '
LINE#4   DC    F'0'
TITLE4A  DC    CL36'  USERS WHO ARE CURRENTLY REVOKED   '
TITLE4B  DC    CL36' '
*
TITLEC   DS    0CL36
         DC    CL16' '
SYSID    DC    CL4' '
         DC    CL16' '
*
TITLED   DS    0CL36
         DC    CL14' '
MM       DC    CL2' '
         DC    CL1'/'
DD       DC    CL2' '
         DC    CL1'/'
YY       DC    CL2' '
         DC    CL14' '
*
HEADER   DC    CL44'USERID          GROUP           DATE DEFINED'
         DC    CL31'        LAST ACCESS        NAME'
*
LINE     DS    0CL133
         DS    CL1
         DS    CL20
UID      DS    CL8
         DS    CL8
GROUP    DS    CL8
         DS    CL8
AYEAR    DS    CL2
         DS    CL1
ADAY     DS    CL3
         DS    CL14
LYEAR    DS    CL2
         DS    CL1
LDAY     DS    CL3
         DS    CL13
NAME     DS    CL20
         DS    CL21
*
CLINE1   DC    CL6'PROC 0'
CLINE2   DC    CL31'CONTROL LIST NOFLUSH PROMPT MSG'
CLINE3   DC    CL20' ALU XXXXXXXX REVOKE'
CLINE4   DC    CL3'END'
*
         DS    0F
CLINE    DS    0CL255
         DS    CL4
LINENO   DS    CL8
CMDLINE  DS    CL243
*
         DS    0CL12                 BACKWARD TABLE FOR MONTH
         DC    P'1'                  DECEMBER
         DC    P'0'                  NOVEMBER
         DC    P'1'                  OCTOBER
         DC    P'0'                  SEPTEMBER
         DC    P'1'                  AUGUST
         DC    P'1'                  JULY
         DC    P'0'                  JUNE
         DC    P'1'                  MAY
         DC    P'0'                  APRIL
         DC    P'1'                  MARCH
         DC    P'-2'                 FEBRUARY
MONTH    DC    P'1'                  JANUARY
*
MOVNAME  MVC   UID(0),USERID
PACKNUM  PACK  PWORK,PARMBUF(0)
*
REPORT1  DCB   DDNAME=REPORT1,DSORG=PS,RECFM=FBA,LRECL=133,MACRF=(PM)
REPORT2  DCB   DDNAME=REPORT2,DSORG=PS,RECFM=FBA,LRECL=133,MACRF=(PM)
REPORT3  DCB   DDNAME=REPORT3,DSORG=PS,RECFM=FBA,LRECL=133,MACRF=(PM)
REPORT4  DCB   DDNAME=REPORT4,DSORG=PS,RECFM=FBA,LRECL=133,MACRF=(PM)
CLIST    DCB   DDNAME=CLIST,DSORG=PS,RECFM=VB,LRECL=255,MACRF=(PM)
*
USRFLD0  DSECT
         DS    CL10
USFLAG   DS    CL1
         DS    CL17
         DS    CL2
USAUTHD  DS    CL3
         DS    CL2
USFLAG4  DS    CL1
         DS    CL2
USPGMNM  DS    CL20
         DS    CL2
USDFLTG  DS    CL8
         DS    CL2
USLJD    DS    CL3
*
         ICHPRCVT
         CVT   DSECT=YES
         IEESMCA
*
         END   DLXRFSCN
