GDATE    CSECT
         USING GDATE,R15
         SAVE  (14,12)
         DROP  R15
         LR    R12,R15
         USING GDATE,R12
         LA    R6,SAVEAREA
         ST    R13,4(R6)
         ST    R6,8(R13)
         LR    R13,R6
         L     R6,0(R1)
         LA    R6,2(R6)                LOAD OUTPUT AREA ADDRESS
         CLC   0(3,R6),=X'000000'
         BE    GETTIME
         CLC   0(3,R6),=X'404040'
         BE    GETTIME
         B     GOTTIME
GETTIME  TIME
         STCM  R1,B'0111',DATE
         B     LETSGO
GOTTIME  MVC   DATE(3),1(R6)
LETSGO   LA    R1,MMDDYY
         USING OUTPUT,R1
         CP    DATE,=PL3'0'
         BE    NODATE
         UNPK  JULIAN,DATE             UNPACK DATE
         OI    JULDAY+2,X'F0'          SET ZONE
         MVI   BLANK,C' '              SET BLANK
         MVC   TARGET,=PL3'0'          00 00 0C
         MVO   YEAR2,YEAR              00 0Y YC
         DP    TARGET,=P'4'            0Q QC RC
         CP    REM,=P'0'               TEST REMAINDER
         BE    LEAP
         CP    DAY,=P'59'              TEST FOR BEFORE FEB 29
         BNH   LEAP
         AP    DAY,=P'1'               ADJUST TO LEAP YEAR
LEAP     LA    R15,TABLE               LOAD TABLE ADDRESS
         USING CALENDAR,R15            SET BASE ADDRESS
         ZAP   MONTHP,=P'1'            SET MONTH TO 1
LOOP1    CP    DAY,DAYS                SEE IF MORE DAYS THAN THIS MNTH
         BNH   DATERTRN
         SP    DAY,DAYS                SUBTRACT DAYS IN THIS MONTH
         AP    MONTHP,=P'1'            ADD 1 TO MONTH
         LA    R15,LEN(R15)            INCREMENT TO NEXT MONTH
         B     LOOP1
DATERTRN UNPK  MO,MONTHP               UNPACK MONTH
         OI    MO+1,X'F0'              SET ZONE
         UNPK  DA,DAY                  UNPACK DAY
         OI    DA+1,X'F0'              SET ZONE
         MVI   SLASH1,C'/'             SET SLASH
         MVI   SLASH2,C'/'             SET SLASH
         B     RETURN                  RETURN
NODATE   MVC   MMDDYY,=CL12' '         BLANK DATE FIELD
RETURN   MVC   0(12,R6),MMDDYY
         L     R13,4(R13)              RETURN
         RETURN (14,12),RC=0
***********************************************************************
*                                                                     *
*        CALENDAR TABLE - NUMBER OF DAYS IN EACH MONTH (LEAP YEAR)    *
*                                                                     *
***********************************************************************
         SPACE
SAVEAREA DC    18F'0'
TABLE    DC    PL2'31'                  JANUARY
         DC    PL2'29'                  FEBRUARY
         DC    PL2'31'                  MARCH
         DC    PL2'30'                  APRIL
         DC    PL2'31'                  MAY
         DC    PL2'30'                  JUNE
         DC    PL2'31'                  JULY
         DC    PL2'31'                  AUGUST
         DC    PL2'30'                  SEPTEMBER
         DC    PL2'31'                  OCTOBER
         DC    PL2'30'                  NOVEMBER
         DC    PL2'31'                  DECEMBER
         SPACE 2
***********************************************************************
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
***********************************************************************
         SPACE
DATEWORK DS    0F                       DATE
         DS    C                        FILLER
DATE     DS    0CL3                     JULIAN DATE - PACKED DECIMAL
YEAR     DS    C                        YEAR - NO SIGN
DAY      DS    CL2                      DD DS
MONTHP   DS    PL2                      0M MS
TARGET   DS    0CL3                     00 0Y YS  -  0Q QS RS
         DS    C                        0Q         PART OF QUOTIENT
YEAR2    DS    0PL2                     0Y YS      YEAR AND SIGN
         DS    C                        QS         QUOTIENT AND SIGN
REM      DS    C                        RS         REMAINDER AND SIGN
         DS    0F                       FULLWORD ALIGNMENT
MMDDYY   DC    CL12' '                  WORK AREA FOR FINAL DATE
         EQUATE
         LTORG
OUTPUT   DSECT
MO       DS    CL2
SLASH1   DS    C
DA       DS    CL2
SLASH2   DS    C
JULIAN   DS    0CL5
YR       DS    CL2
JULDAY   DS    CL3
BLANK    DS    C
CALENDAR DSECT
DAYS     DS    PL2
LEN      EQU   *-DAYS
         END   GDATE
