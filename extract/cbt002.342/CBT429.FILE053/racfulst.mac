         TITLE 'RACFULST - GEN CLIST FOR DELETING RACF USERS'
***********************************************************************
*                                                                     *
*             MODULE NAME = RACFULST                                  *
*                                                                     *
*             DESCRIPTIVE NAME = GENERATE CLIST TO DELETE ALL         *
*                BUT A FEW SPECIAL RACF USERS AND THEIR MODEL         *
*                DATA SETS.                                           *
*                                                                     *
*             FUNCTION =                                              *
*                CREATES CLIST WHICH WILL ALLOW A SYSTEM              *
*                PROGRAMMER TO DELETE ALL BUT A FEW SELECTED          *
*                USER IDS FROM THE RACF DATA SET.  IT WILL            *
*                ALSO DELETE THE RACF DESCRIPTIONS FOR THEIR          *
*                ASSOCIATED MODEL DATA SETS.                          *
*                                                                     *
*                OPERATION =                                          *
*                   OPEN THE SPECIFIED OUTPUT FILE (FOR CLIST)        *
*                   LOCATE A USER IN THE RACF DATA SET AND RETRIEVE   *
*                     ITS MODEL DATA SET NAME IF PRESENT              *
*                   COMPARE USER ID TO SPECIAL USER IDS               *
*                   IF MATCH, LOOP BACK TO LOCATE NEXT USER           *
*                   IF NO MATCH, WRITE LINE TO DELETE USER ID INTO    *
*                     THE CLIST DATA SET                              *
*                   IF MODEL DATA SET EXISTS, WRITE LINE TO DELETE    *
*                     IT INTO THE CLIST DATA SET                      *
*                   LOOP TO LOCATE NEXT USER                          *
*                   CLOSE OUTPUT FILE                                 *
*                   EXIT                                              *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 1  = WORK REGISTERS               *
*                    REGISTERS 2       = LINKAGE & WORK REGISTER      *
*                    REGISTERS 4 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO RACFULST   *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*             MACROS =                                                *
*                SAVE, GETMAIN, OPEN, CLOSE, ICHEINTY,                *
*                ICHEACTN, PUT                                        *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                03/16/84  WRITTEN.      BC.                          *
*                05/14/84  MODIFIED OUTPUT TO CLIST SO THAT MODEL     *
*                          DATA SETS ARE DELETED BEFORE DELETING      *
*                          THE USER.     BC.                          *
*                                                                     *
* SAMPLE JCL =                                                        *
*                                                                     *
*   //JOBNAME JOB (GEE1,0104),'PROG NAME',CLASS=S,MSGCLASS=A,         *
*   //        MSGLEVEL=(2,0),USER=SYSPGM,PASSWORD=????????            *
*   //STEP1 EXEC  PGM=RACFULST                                        *
*   //CLIST DD DSN=T99999.TEST.CLIST,VOL=SER=DLXTS3,UNIT=3380,        *
*   //         DISP=(NEW,CATLG),SPACE=(TRK,(10,1)),                   *
*   //         DCB=(RECFM=VB,LRECL=255,BLKSIZE=6233)                  *
*                                                                     *
***********************************************************************
*
         PRINT NOGEN
RACFULST CSECT
         SAVE  (14,12)                 SAVE REGISTERS
         LR    R12,R15
         USING RACFULST,R12            ESTABLISH ADDRESSABILITY
         LR    R2,R1                   PRESERVE PTR TO PARMS
         GETMAIN R,LV=LDATA            GET DYNAMIC WORK AREA
         LR    R10,R1                  PRESERVE PTR TO WORK AREA
         ST    R13,4(R10)              STORE ADDR OF PREVIOUS SA
         ST    R10,8(,R13)             STORE ADDR OF CURRENT SA
         LR    R13,R10                 LOAD ADDR OF CURRENT SA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
*
*  INITIALIZE THE WORK AREA.  OPEN THE CLIST OUTPUT FILE.  WRITE
*    THE FIRST 2 LINE OF THE CLIST INTO THE OUTPUT FILE.
*
INIT     EQU   *
         LA    R6,DATA                 SET UP REGISTERS FOR
         LA    R7,MOVEL                  LONG MOVE
         LA    R8,CDATA
         LA    R9,MOVEL
         MVCL  R6,R8                   LOAD INITIAL DATA INTO WORKAREA
         OPEN  (PRINT,OUTPUT),MF=(E,EX1) OPEN OUTPUT FILE
         LTR   R15,R15
         BNZ   CLOSEX
         MVI   LINE,X'40'              INITIALIZE LINE
         MVC   LINE+1(L'LINE-1),LINE     TO ALL SPACES
         MVC   DLXCMD(L'FSTLINE),FSTLINE
         BAL   R3,PRINTIT              OUTPUT FIRST LINE OF CLIST
         MVC   DLXCMD(L'SNDLINE),SNDLINE
         BAL   R3,PRINTIT              OUTPUT SECOND LINE OF CLIST
         LA    R4,296                  GET LENGTH OF RACF WORK AREA
         ST    R4,RACFWORK             STORE IN WORK AREA
*
*  LOOP THRU THE RACF DATA SET LOCATING ALL USERS AND THEIR MODEL
*     DATA SETS.
*
GTRACF   EQU   *
         ICHEINTY NEXT,TYPE='USR',ENTRY=USERBUF,WKAREA=RACFWORK,       X
               ACTIONS=(ACT1),OPTIONS=(ACTION),MF=(E,EX3)
         LTR   R15,R15                 USER FOUND?
         BE    GR10                    IF SO, CONTINUE
         C     R15,=F'12'              RETURN CODE 12?
         BE    CLOSE                   IF SO, END OF USERS
         B     CLOSEX                  ANYTHING ELSE IS AN ERROR
*
*  CHECK FOR USERS WHO SHOULD NOT BE DELETED.
*
GR10     EQU   *
         CLC   USERID(6),=CL6'T02930'     CHECK FOR EXCEPTION ID'S
         BE    GTRACF                    THAT SHOULD BE IGNORED
         CLC   USERID(6),=CL6'SYSJOB'
         BE    GTRACF
         CLC   USERID(6),=CL6'T27611'
         BE    GTRACF
         CLC   USERID(6),=CL6'T56714'
         BE    GTRACF
         CLC   USERID(6),=CL6'T53093'
         BE    GTRACF
         CLC   USERID(6),=CL6'T53031'
         BE    GTRACF
         CLC   USERID(6),=CL6'T53043'
         BE    GTRACF
         CLC   USERID(6),=CL6'ABRJOB'
         BE    GTRACF
         CLC   USERID(6),=CL7'CICSJOB'
         BE    GTRACF
         CLC   USERID(6),=CL7'IBMUSER'
         BE    GTRACF
         CLC   USERID(6),=CL7'SERIES1'
         BE    GTRACF
         CLC   USERID(6),=CL3'STC'
         BE    GTRACF
         SLR   R8,R8
         ICM   R8,B'0001',RACFWORK+30  MODEL DATA SET SPECIFIED?
         LTR   R8,R8
         BE    GR20                    IF NOT, JUST DELETE USER
         MVC   DLXCMD,=CL8'DLXAUTHC'   CONSTRUCT THE COMMAND
         MVC   RACFCMD,=CL2'DD'          TO DELETE THE MODEL DATA SET
         SLR   R7,R7
         ICM   R7,B'0001',USERBUF      GET LENGTH OF USERID
         BCTR  R7,0                    DECREMENT LENGTH
         EX    R7,LODUSER              LOAD USER
         LA    R5,RPARMS               POINT TO BEGINNING OF USERID
         BAL   R2,NXTSPC               FIND NEXT AVAILABLE SPACE
         MVI   0(R5),C'.'              INSERT A PERIOD
         BCTR  R8,0                    DECREMENT LENGTH
         EX    R8,LODMODL              LOAD MODEL NAME
         BAL   R3,PRINTIT              AND PUT INTO CLIST
GR20     EQU   *
         MVC   DLXCMD,=CL8'DLXAUTHC'   CONSTRUCT COMMAND
         MVC   RACFCMD,=CL2'DU'          TO DELETE THE USER
         SLR   R7,R7
         ICM   R7,B'0001',USERBUF      GET LENGTH OF USERID
         BCTR  R7,0                    DECREMENT LENGTH
         EX    R7,LODUSER              LOAD USERID
         BAL   R3,PRINTIT              PUT THE LINE INTO THE CLIST
         B     GTRACF                  LOOP BACK FOR NEXT USER
*
*  CLOSE FILES AND TERMINATE PROGRAM.
*
CLOSEX   EQU   *                       ERROR EXIT
         LR    R7,R15                  SAVE CONTENTS OF R15
         CLOSE PRINT,MF=(E,EX2)        CLOSE THE FILE
         LR    R15,R7                  RESTORE CONTENTS OF R15
         B     RETURN
CLOSE    EQU   *                       STANDARD EXIT
         MVC   DLXCMD(L'LSTLINE),LSTLINE
         BAL   R3,PRINTIT              OUTPUT LAST LINE OF CLIST
         CLOSE PRINT,MF=(E,EX2)        CLOSE THE FILE
RETURN   L     R13,4(R13)              RESTORE REGISTERS
         RETURN (14,12),RC=(15)        AND END
*
***********************************************************************
*                                                                     *
*                      SUBROUTINES                                    *
*                                                                     *
***********************************************************************
*
*  SUBROUTINE TO WRITE THE OUTPUT LINE INTO THE CLIST DATA SET
*
PRINTIT  EQU   *
         AP    LINE#,=P'100'           INCREMENT LINE NUMBER
         UNPK  LINENO,LINE#            UNPACK LINE NUMBER
         MVZ   LINENO+7(1),LINENO+6      & MOVE THE ZONE BYTE
         LA    R5,DLXCMD
         BAL   R2,NXTSPC               FIND END OF LINE
         LA    R2,LINE
         SR    R5,R2                   CALCULATE LINE LENGTH
         STH   R5,LINE                   AND STORE
         MVC   LINE+2(2),=X'0000'
         PUT   PRINT,LINE              PRINT THE LINE
         MVI   LINE,X'40'              RESET THE LINE BUFFER
         MVC   LINE+1(L'LINE-1),LINE     TO SPACES
         BR    R3                      RETURN
*
*  SUBROUTINE TO POINT TO THE NEXT DATA POSITION IN THE OUTPUT LINE.
*
NXTSPC   EQU   *
         LA    R5,1(R5)                INCREMENT POINTER
         CLC   0(4,R5),=C'    '        CHECK FOR A SPACE
         BNE   NXTSPC                  LOOP TIL SPACE FOUND
         BR    R2                      RETURN
*
***********************************************************************
*                                                                     *
*                     CONSTANT DATA                                   *
*                                                                     *
***********************************************************************
*
*  REGISTER EQUATES
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         LTORG
*
*    THE FOLLOWING DATA IS USED TO INIT PART OF THE GETMAIN'ED AREA
*
CDATA    DS    0F
ILINE#   DC    P'00000000'
UBUF     DC    X'08'
USID     DC    CL8' '
DCB1     DCB   DDNAME=CLIST,DSORG=PS,RECFM=VB,LRECL=255,               X
               MACRF=(PM)
LIST1    OPEN   (,),MF=L
LIST2    CLOSE  (,),MF=L
LIST3    ICHEINTY NEXT,TYPE='USR',ACTIONS=(ACT1),OPTIONS=(ACTION),MF=L
INST1    MVC    RPARMS(0),USERID
INST2    MVC    1(0,R5),RACFWORK+31
MOVEL    EQU   *-CDATA
*
*    THE FOLLOWING FIELDS WILL BE RETRIEVED FROM THE RACF DATA SET
*
ACT1     ICHEACTN FIELD=MODELNAM
*
*    MISCELLANEOUS CONSTANTS
*
FSTLINE  DC    C'PROC 0'
SNDLINE  DC    C'CONTROL LIST NOFLUSH PROMPT MSG'
LSTLINE  DC    C'END'
*
***********************************************************************
*                                                                     *
*                    DSECTS FOR GETMAIN'ED AREA                       *
*                                                                     *
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F
*
LINE     DS    0CL255
         DS    CL4
LINENO   DS    CL8
DLXCMD   DS    CL8
         DS    CL1
RACFCMD  DS    CL2
         DS    CL1
RPARMS   DS    CL231
*
         DS    0F
RACFWORK DS    XL300
*
DATA     DS    0F
LINE#    DS    P'00000000'
USERBUF  DS    XL1
USERID   DS    CL8
PRINT    DCB   DDNAME=CLIST,DSORG=PS,RECFM=VB,LRECL=255,               X
               MACRF=(PM)
EX1      OPEN   (,),MF=L
EX2      CLOSE  (,),MF=L
EX3      ICHEINTY NEXT,TYPE='USR',ACTIONS=(ACT1),OPTIONS=(ACTION),MF=L
LODUSER  MVC    RPARMS(0),USERID
LODMODL  MVC    1(0,R5),RACFWORK+31
*
LDATA    EQU   *-WORKAREA
*
         END   RACFULST
