         TITLE 'D U D A S D R   ---   D A S D   V S N   D I S P L A Y'
DUDASDR  CSECT
***********************************************************************
*              PERFORM PROCESSING INITIALIZATION                      *
***********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,DUDASDR-&SYSDATE-&SYSTIME   SAVE REGISTERS
         LR    R11,R15                 LOAD REG 11 WITH ENTRY ADDRESS
         USING DUDASDR,R11             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R12,4095(R11)           LOAD REG 12 WITH ENTRY ADDR 4095
         USING DUDASDR+4095,R12        ESTABLISH ADDRESSABILTY TO
*                                      REST OF PROGRAM
         LR    R5,R1                   PRESERVE PTR TO CPPL CNTL BLK
         GETMAIN R,LV=LDYNAMIC         GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  PRESERVE PTR TO WORKAREA
         ST    R13,4(R10)              STORE ADDRESS PREVIOUS SAVEAREA
         ST    R10,8(,R13)             STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R10                 LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
         USING CPPL,R5                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,IOPLSECT             LOAD ADDR OF PUTLINE PARM LIST
         USING IOPL,R4                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD PTR TO USER PROFILE TABLE
*                                      FROM CPPL
         L     R2,CPPLECT              LOAD PTR TO ENVIRONMENTAL
*                                      CONTROL TABLE FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REG AS ECB FOR IOPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,IOPLSECT          STORE ADDRESS OT UPT, ECT, AND
*                                      ECB IN IOPL
         LA    R3,PUTLINE              LOAD ADDRESS OF PUTLINE
*                                      PARAMETER BLOCK
         ST    R3,IOPLIOPB             STORE IOPB ADDRESS IN IOPL
         DROP  R4,R5                   DROP ADDRESSABILITY - CPPL/IOPL
         USING CVT,R4                  R4 - ADDRESSABILITY / CVT
         USING UCBCMSEG,R5             R5 - ADDRESSABILITY / UBC
         XC    PREFIX(4),PREFIX        CLEAR PUTLINE SINGLE-LINE HEADER
         LA    R15,L'BUFFER+4          R15 - LENGTH OF OUTPUT-LINE+HDR
         STH   R15,PREFIX              STORE R15 (LENGTH) INTO HEADER
         L     R4,16                   LOAD R4 WITH ADDRESS OF CVT
         L     R5,CVTUCBA              LOAD R5 WITH ADDRESS OF 1ST UCB
***********************************************************************
*  DULINE      OUTPUT LINE PROCESSING LOOP                            *
***********************************************************************
DULINE   EQU   *
         MVC   BUFFER(L'BUFFER),SPACES    CLEAR OUTPUT-LINE
         LA    R6,BUFFER               R6 - INDEX TO OUTPUT ELEMENT
         LA    R7,L'BUFFER/L'ELEMENT   R7 - NUM OF ELEMENTS PER LINE
         USING ELEMENT,R6              R6 - ADDRESSABILITY / ELEMENT
***********************************************************************
*  DUUNIT      OUTPUT ELEMENT PROCESSING LOOP                         *
***********************************************************************
DUUNIT   EQU   *
         LTR   R5,R5                   IS THIS THE LAST UCB IN CHAIN?
         BZ    PUT                     YES, WRITE OUTPUT-LINE TO TERM
         TM    UCBTBYT3,UCB3DACC       IS THIS A DIRECT ACCESS DEVICE?
         BNO   NEXTUCB                 NO, GET NEXT UCB IN CHAIN
         CLC   LASTUCB,UCBNAME         UNIT ALREADY BEEN DISPLAYED?
         BE    NEXTUCB                 YES, GET NEXT UCB IN CHAIN
         MVC   LASTUCB,UCBNAME         UPDATE FOR DUPLICATE UNIT CHECK
         MVC   ELEUNIT,UCBNAME         MOVE UCBNAME TO OUTPUT ELEMENT
         MVC   ELEVSN,UCBVOLI          MOVE VOLSER TO OUTPUT ELEMENT
         TM    UCBSTAT,UCBONLI         IS THIS DEVICE ONLINE?
         BO    ONLINE                  YES, PROCESS ONLINE VOLUME
         MVC   ELEVSN,=CL6'(OFF)'      NO, MOVE 'OFFLINE' INDICATOR
*                                      TO OUTPUT ELEMENT
         B     ULOOP                   PROCESS NEXT UCB IN CHAIN
NEXTUCB  EQU   *
         L     R5,UCBNXUCB             R5 - ADDR OF NEXT UCB IN CHAIN
         B     DUUNIT                  PROCESS NEXT UCB IN CHAIN
ONLINE   EQU   *
         MVI   ELEDASH,C'-'            MOVE '-' TO OUTPUT ELEMENT
         MVI   ELESTAT,C'O'            MOVE 'ONLINE UNALLOCATED'
*                                      INDICATOR TO OUTPUT ELEMENT
         TM    UCBSTAT,UCBALOC         IS THIS DEVICE ALLOCATED?
         BNO   PENDOFF                 NO, CHECK FOR PENDING OFFLINE
         MVI   ELESTAT,C'A'            YES, MOVE 'ONLINE ALLOCATED'
*                                      INDICATOR TO OUTPUT ELEMENT
PENDOFF  EQU   *
         TM    UCBSTAT,UCBCHGS         IS THIS VOLUME PENDING OFFLINE?
         BNO   RESERV                  NO, CHECK TO SEE IF VOL RESERVED
         MVI   ELESTAT,C'P'            YES, MOVE 'PENDING OFFLINE'
*                                      INDICATOR TO OUTPUT ELEMENT
RESERV   EQU   *
         CLI   UCBSQC,X'00'            IS THIS VOLUME RESERVED FOR TSO?
         BE    MOUNT                   NO, CHECK MOUNT STATUS
         MVI   ELERSRV,C'R'            YES, MOVE 'TSO RESERVED'
*                                      INDICATOR TO OUTPUT ELEMENT
MOUNT    EQU   *
         TM    UCBSTAT,UCBRESV         IS VOLUME RESERVED (MOUNT)?
         BZ    RESIDENT                NO, CHECK TO SEE IF RES VOL
         MVC   ELEMSTAT(3),=C'RSV'     YES, MOVE 'MOUNT STAT RESERVED'
*                                      INDICATOR TO OUTPUT ELEMENT
         B     ULOOP                   PROCESS NEXT UCB IN CHAIN
RESIDENT EQU   *
         TM    UCBSTAT,UCBPRES         IS THIS VOLUME PERM. RESIDENT?
         BZ    ULOOP                   NO, PROCESS NEXT UCB IN CHAIN
         MVC   ELEMSTAT(3),=C'RSD'     YES, MOVE 'PERMANENTLY RESIDENT'
*                                      INDICATOR TO OUTPUT ELEMENT
ULOOP    EQU   *
         LA    R6,ELENEXT              POINT TO NEXT OUTPUT ELEMENT
         L     R5,UCBNXUCB             R5 - ADDR OF NEXT UCB IN CHAIN
         BCT   R7,DUUNIT               PROCESS NEXT OUTPUT ELEMENT
***********************************************************************
*  PUT         WRITE OUTPUT-LINE ROUTINE                              *
***********************************************************************
PUT      EQU   *
         CLC   BUFFER,SPACES           IS THIS THE LAST OUTPUT-LINE?
         BE    DONE                    YES, WE'RE DONE
         PUTLINE PARM=PUTLINE,MF=(E,IOPLSECT),ECB=ECB,                 +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         B     DULINE
***********************************************************************
*  DONE        FREE GETMAINED AREA, RESTORE REGISTERS,                *
*              SET RETURN CODE AND EXIT                               *
***********************************************************************
DONE     EQU   *
         LA    R0,LDYNAMIC             R0 - LENGTH OF DYNAMIC WORK AREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR OF CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE DYNAMIC WORK AREA
         SRL   R15,R15                 CLEAR R15 (RETURN CODE)
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         LTORG
***********************************************************************
*              GENERAL DECLARE AND EQUATE STATEMENTS                  *
***********************************************************************
R0       EQU   00                      LENGTH OF DYNAMIC SAVE AREA
R1       EQU   01                      PARAMETER PTR AND WORK REGISTER
R2       EQU   02                      PTR TO ENVIRONMENTAL CONTROL TBL
R3       EQU   03                      WORK REGISTER
R4       EQU   04                      PTR TO IOPL AND CVT
R5       EQU   05                      PTR TO CPPL AND UCB
R6       EQU   06                      INDEX TO OUTPUT ELEMENT
R7       EQU   07                      NUMBER OF ELEMENTS / OUTPUT LINE
R8       EQU   08                      NOT USED
R9       EQU   09                      NOT USED
R10      EQU   10                      WORK REGISTER
R11      EQU   11                      NOT USED
R12      EQU   12                      ADDRESSABILITY TO CSECT
R13      EQU   13                      PTR TO SAVE AREA
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      ENTRY PT ADDRESS & RETURN CODE
SPACES   DC    80C' '
ELEWRK   DSECT
ELEMENT  DS    0CL20
ELEUNIT  DS    CL3
         DS    CL1
ELEVSN   DS    CL6
ELEDASH  DS    CL1
ELESTAT  DS    CL1
ELERSRV  DS    CL1
ELEMSTAT DS    CL3
         DS    CL2
ELENEXT  DS    0CL1
***********************************************************************
*              CONTROL BLOCK DSECTS                                   *
***********************************************************************
         TITLE 'IOPL DSECT'
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         TITLE 'CPPL DSECT'
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         TITLE 'UCB DSECT'
         IEFUCBOB
         TITLE 'CVT DSECT'
         CVT   DSECT=YES
***********************************************************************
*              DSECT FOR GETMAINED WORK AREA                          *
***********************************************************************
WORKAREA DSECT
SSA      DS    18F
IOPLSECT DS    0F
         ORG   IOPLSECT+IOPLLEN
ECB      DS    01F
LASTUCB  DS    CL3
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*4)
PUTLINE  PUTLINE MF=L
LDYNAMIC EQU   *-WORKAREA
         END   DUDASDR
