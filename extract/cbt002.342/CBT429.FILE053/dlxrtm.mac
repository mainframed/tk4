DLXRTM CSECT
         SAVE  (14,12),,DLXRTM_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING DLXRTM,R12               TELL ASSEMBLER
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
* END OF PROLOGUE                       SAVE AREA
         ST    R1,PARMSAVE
*   BEGINNING OF RACF CHECKING
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
*
*  IF USER HAS SPECIAL ATTRIBUTE, EXIT WITHOUT FURTHER CHECKING
*
         L     R2,ASCBASXB             GET ADDRESS OF ASXB
         DROP  R2
         USING ASXB,R2                 TELL ASSEMBLER
         L     R2,ASXBSENV             GET ADDRESS OF ACEE
         DROP  R2                      DROP ASXB ADDRESSABILITY
         USING ACEE,R2                 ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEESPEC       USER HAVE SPECIAL ATTRIBUTE?
         BO    ENDRACF                 IF NOT, CHECK FURTHER
         DROP  R2                      DROP ASXB ADDRESSABILITY
         RACHECK ENTITY=PROGNAME,CLASS='APPL'
         LTR   R15,R15
         BZ    ENDRACF
         ABEND X'913',,STEP             ABEND USER
ENDRACF  EQU   *
*   END OF RACF CHECKING
         L     R1,PARMSAVE
GETPARM1 EQU   *                        GET PARM FIELD
         L     R1,0(R1)                 ADDRESS OF PARM TO R2
         LH    R3,0(R1)                 LENGTH OF PARM TO R3
         LTR   R3,R3                    CHECK FOR NO PARM
         BZ    RETURN4                  NO PARMS
         CH    R3,=H'3'                 CHECK FOR VALID ADDRESS LENGTH
         BNE   RETURN8                  BAD LENGTH
         MVC   ASIDPARM(3),2(R1)        MOVE PARM
         PACK  WORK8,ASIDPARM
         CVB   R4,WORK8
         MODESET KEY=ZERO,MODE=SUP
         LR    R5,R13
         LA    R13,CALLAREA
         CALLRTM TYPE=MEMTERM,COMPCOD=2457,ASID=(4),DUMP=NO,STEP=YES
         MODESET KEY=NZERO,MODE=PROB
         LR    R13,R5
         SLR   R15,R15
         B     RETURNX
RETURN4  LA    R15,4
         B     RETURNX
RETURN8  LA    R15,8
         B     RETURNX
RETURNX  L     R13,SAVEAREA+4           LOAD OS SAVE AREA ADDR TO R13
         RETURN (14,12),RC=(15)         RETURN TO OS RC=R15
SAVEAREA DC    18F'0'                   OUR SAVE AREA
CALLAREA DC    18F'0'                   CALLRTM SAVE AREA
WORK8    DC    D'0'                     WORKAREA
PARMSAVE DC    F'0'
ASIDPARM DC    CL3'000'                 ASID
PROGNAME DC    CL8'DLXRTM  '            PROGRAM NAME
         LTORG
         EQUATE
         IHAACEE
         IHAASCB
         IHAASXB
         CVT   DSECT=YES                REQUIRED FOR CALLRTM
         END   DLXRTM
