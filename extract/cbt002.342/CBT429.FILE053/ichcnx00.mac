         TITLE 'ICHCNX00' - COMMAND PREPROCESSING EXIT'
***********************************************************************
*                                                                     *
*              MODULE NAME = ICHCNX00                                 *
*                                                                     *
*              DESCRIPTIVE NAME = COMMAND PREPROCESSING EXIT          *
*                                                                     *
*              FUNCTION =                                             *
*                  EXIT ALLOWS INSTALLATION TO FAIL ALL BATCH         *
*                  REQUESTS TO PROCESS 'ADDSD' AND 'PERMIT'           *
*                  COMMANDS FOR ALL USERS WITHOUT THE 'SPECIAL'       *
*                  ATTRIBUTE                                          *
*                                                                     *
*              OPERATION =                                            *
*                  SAVE REGISTERS AND ESTABLISH ADDRESSABILITY        *
*                  IF COMMAND TYPE IS OTHER THAN 'ADDSD' OR           *
*                      'PERMIT', EXIT                                 *
*                  IF USER IS A MEMBER OF GROUP JCLGRP SET RC=4 AND *
*                       EXIT                                          *
*                  IF JOB TYPE IS NOT BATCH, EXIT                     *
*                  IF USER HAS 'SPECIAL' ATTRIBUTE SET RC=0,          *
*                      ELSE SET RC=4                                  *
*                  EXIT                                               *
*                                                                     *
*              REGISTER CONVENTIONS = STANDARD CONVENTIONS.           *
*                  REGISTER  0      = NOT USED                        *
*                  REGISTER  1      = PARAMETER LIST POINTER          *
*                  REGISTER  2      = PRESERVED POINTER TO PARM LIST  *
*                  REGISTERS 3 - 5  = NOT USED                        *
*                  REGISTERS 6 - 7  = WORK REGISTERS                  *
*                  REGISTERS 8 - 11 = NOT USED                        *
*                  REGISTER  12     = BASE REGISTER                   *
*                  REGISTER  13     = SAVE AREA POINTER               *
*                  REGISTER  14     = RETURN ADDRESS                  *
*                  REGISTER  15     = ENTRY POINT ADDRESS AND         *
*                                     RETURN CODE REGISTER            *
*                                                                     *
*              MACROS =                                               *
*                  SAVE, RETURN, EQUATE                               *
*                                                                     *
*              MAPPING MACROS USED =                                  *
*                  IHAACEE - ACEE MAP                                 *
*                  IHAASCB - ASCB MAP                                 *
*                  IHAASXB - ASXB MAP                                 *
*                                                                     *
*                                                                     *
*              CHANGE ACTIVITY =                                      *
*                  12/10/84  WRITTEN.  JWC.                           *
*                                                                     *
***********************************************************************
*
         EJECT
         PRINT NOGEN
ICHCNX00 CSECT
ICHCNX00 AMODE ANY
ICHCNX00 RMODE ANY
         SAVE  (14,12),,ICHCNX00_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING ICHCNX00,R12            ESTABLISH ADDRESSABILITY
         LR    R2,R1                   PRESERVE PTR TO PARMS
         SLR   R15,R15                 CLEAR R15 FOR FUTURE USE
         L     R8,16                   GET ADDRESS OF CVT
         L     R8,0(R8)                GET ADDR OF TCB ADDRESS WORD
         L     R8,12(R8)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R8                 ADDRESSABILITY TO ASCB DSECT
         L     R9,ASCBASXB             GET THE ADDRESS OF ASXB
         USING ASXB,R9                 ADDRESSABILITY TO ASXB DSECT
         L     R9,ASXBSENV             GET ADDRESS OF ACEE
         USING ACEE,R9                 ADDRESSABILITY TO ACEE DSECT
*
*  CHECK TO SEE IF COMMAND TYPE IS 'ADDSD' OR 'PERMIT'. IF NOT, EXIT.
*
CHKCOMND EQU   *
         L     R6,4(R2)                GET CALLER ADDR (FUNCTION CODE)
         CLI   0(R6),X'03'             IS THIS AN 'ADDSD' COMMAND?
         BE    CHKGRP                  YES, CHECK JOB TO SEE IF BATCH
         CLI   0(R6),X'07'             IS THIS AN 'PERMIT' COMMAND?
         BE    CHKGRP                  YES, CHECK JOB TO SEE IF BATCH
         B     EXIT                    NO, RETURN (RC=0)
*
*  CHECK TO SEE IF THIS IS A MEMBER OF JCLGRP IF SO FAIL REQUEST
*
CHKGRP   EQU   *
         CLC   ACEEGRPN,=CL8'JCLGRP'   IS IT A MEMBER OF JCLGRP
         BNE   CHKJOB                  NO CONTINUE
         LA    R15,4                   SET TO FAIL REQUEST
         B     EXIT
*
*  CHECK TO SEE IF THIS IS A BATCH JOB. IF NOT, EXIT.
*
CHKJOB   EQU   *
         L     R7,ASCBTSB              GET ADDRESS OF TSB
         LTR   R7,R7                   IS TSB ADDRESS ZERO?
         BNZ   EXIT                    NO, A TSO USER. EXIT (RC=0)
*
*  CHECK TO SEE IF THIS USER HAS 'SPECIAL' ATTRIBUTE. IF NOT, SET RC=4.
*
         TM    ACEEFLG1,ACEESPEC       USER HAVE 'SPECIAL' ATTRIBUTE?
         BO    EXIT                    YES, EXIT (RC=0)
         LA    R15,4                   NO, SET RC=4 (FAIL REQUEST)
*
*  EXIT THE EXIT
*
EXIT     EQU   *
         RETURN (14,12),,RC=(15)       RESTORE REGISTERS AND RETURN
         EJECT
*
*  REGISTER EQUATES
*
         EQUATE
         LTORG
*
*  DSECTS
*
         IHAACEE
         IHAASCB
         IHAASXB
         END   ICHCNX00
