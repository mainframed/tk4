         TITLE 'DSN3@ATH - DB2 AUTHORIZATION EXIT'
***********************************************************************
*                                                                     *
*        MODULE NAME = DSN3@ATH                                       *
*                                                                     *
*        DESCRIPTIVE NAME =                                           *
*               DB2 AUTHORIZATION EXIT USED TO CHANGE DB2             *
*               AUTHORIZATION IDS                                     *
*                                                                     *
*        FUNCTION =                                                   *
*              PROGRAM CHECKS SUPPLIED USERID FOR A CONNECT TO A      *
*              DB2 GROUP. IF THE CONNECTION EXISTS, THE GROUP NAME    *
*              IS SUBTITUTED FOR THE USERID AND WILL BE USED AS THE   *
*              DB2 AUTHORIZATION ID.                                  *
*                                                                     *
*        OPERATION =                                                  *
*              SAVE REGISTERS AND ESTABLISH ADDRESSABILITY            *
*              ESTABLISH ADDRESSABILITY TO CGRP                       *
*              CHECK FOR CONNECT TO DB2* GROUP                        *
*              RESTORE REGISTERS                                      *
*              RETURN                                                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        NOTES =                                                      *
*                                                                     *
*              REGISTER CONVENTIONS = NONSTANDARD USAGE, HOWEVER,     *
*                               REGISTERS ARE SAVED AND RESTORED      *
*                               ON ENTRY AND EXIT.                    *
*                  REGISTER  1      = PARAMETER LIST POINTER          *
*                  REGISTER  2      = ADDRESS OF AUTHORIZATION ID     *
*                  REGISTERS 3 - 11 = WORK REGISTERS                  *
*                  REGISTER  12     = BASE REGISTER                   *
*                  REGISTER  13     =                                 *
*                  REGISTER  14     = RETURN                          *
*                  REGISTER  15     = ENTRY POINT ADDRESS AND         *
*                                     RETURN CODE REGISTER            *
*                                                                     *
*           REASON FOR NONSTANDARD USAGE : A GETMAIN COULD NOT BE     *
*                           DONE, BUT THIS EXIT STILL NEEDED TO       *
*                           BE REENTRANT.                             *
*                                                                     *
*                                                                     *
*                                                                     *
*        MACROS =                                                     *
*              SAVE, RETURN, EQUATE                                   *
*                                                                     *
*        MAPPING MACROS USED =                                        *
*              CVT, IHAASCB, IHAASXB, IHAACEE, ICHPCGRP               *
*                                                                     *
*        ASSEMBLER OPTIONS = LIST,RENT                                *
*                                                                     *
*        LINKAGE EDITOR OPTIONS = LET,LIST,XREF,RENT,AC=1,            *
*               RMODE=ANY,AMODE=31                                    *
*              (MUST BE LINKED INTO AN AUTHORIZED LIBRARY)            *
*                                                                     *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*              06/03/86  WRITTEN.                               MCR.  *
*              03/17/87  CHANGED TO NOT USE 'DB2' AS            MCR.  *
*                          AN AUTHORIZATION-ID.
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        INITIALIZATION
***********************************************************************
*
DSN3@ATH CSECT
         SAVE  (2,13),,DSN3@ATH_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING DSN3@ATH,R12            ESTABLISH ADDRESSABILITY
         L     R2,4(R1)                SAVE ADDRESS OF DB2 AUTH ID
         EJECT
***********************************************************************
*        SET UP ADDRESSABILITY TO CGRP
***********************************************************************
CGRPADDR DS    0H
         L     R3,CVTPTR               R3 - ADDRESS OF CVT
         L     R3,0(R3)                R3 - PTR TO FOUR WORD ADDR  LIST
         L     R3,12(R3)               R3 - ADDRESS OF CURRENT ASCB
         USING ASCB,R3                 ESTABLISH ADDRESSABILITY TO ASCB
         L     R3,ASCBASXB             R3 - ADDRESS OF ASXB
         USING ASXB,R3                 ESTABLISH ADDRESSABILITY TO ASXB
         L     R3,ASXBSENV             R3 - ADDRESS OF ACEE
         USING ACEE,R3                 ESTABLISH ADDRESSABILITY TO ACEE
         L     R3,ACEECGRP             R3 - ADDRESS OF CGRP
         USING CGRP,R3                 ESTABLISH ADDRESSABILITY TO CRGP
***********************************************************************
*        CHECK FOR GROUP NAMES THAT START WITH DB2
***********************************************************************
         SLR   R5,R5                   CLEAR REGISTER 5
         LH    R5,CGRPNUM              R5 = NUMBER OF GROUPS USER IS
*                                                     CONNECTED TO
         LTR   R5,R5                   IF FOR SOME REASON THERE ARE NO
         BZ    FINISH                       GROUPS, THEN END
         LA    R4,32(R3)               CGRPENT STARTS 32 CHARACTERS
*                                                     AFTER CGRP
NEXTGRP  EQU   *
         USING CGRPENTD,R4          ESTABLISH ADDRESSABILITY TO CRGPENT
*
         CLC   DB2GROUP,CGRPNAME       SEE IF GROUPNAME IS 'DB2'
         BE    SETGROUP                 IF IT IS, SET UP FOR NEXT GROUP
         CLC   DB2NAME,CGRPNAME        SEE IF GROUPNAME STARTS WITH DB2
         BE    CHGGROUP                  IF IT DOES, GO TO CHANGE GROUP
SETGROUP EQU   *
         LA    R4,24(R4)               ELSE  SET TO NEXT ENTRY AND
         BCT   R5,NEXTGRP                   GO TO NEXT GROUP IF PRESENT
*
         B     FINISH                  NO DB2 GROUP FOUND SO END.
CHGGROUP EQU   *
         MVC   0(8,R2),CGRPNAME      MOVE GROUPNAME TO DB2
*                                             AUTHORIZATION ID.
***********************************************************************
*        CLEANUP AREAS GETMAINED
*        SET RETURN CODE, RESTORE REGISTERS AND RETURN
***********************************************************************
FINISH   DS    0H
         RETURN (2,13),RC=(15)        RESTORE REGISTERS AND RETURN
         EJECT
***********************************************************************
         EJECT
***********************************************************************
*        CONSTANTS
***********************************************************************
DB2NAME  DC    CL3'DB2'                DB2
DB2GROUP DC    CL4'DB2 '               DB2GROUP
***********************************************************************
*        ERROR AND INFORMATIONAL MESSAGES
***********************************************************************
***********************************************************************
***********************************************************************
*        DSECT FOR COMMUNICATIONS VECTOR TABLE
***********************************************************************
         CVT DSECT=YES,LIST=YES
         EJECT
DSN3@ATH CSECT
***********************************************************************
*        DSECT FOR ADDRESS SPACE CONTROL BLOCK
***********************************************************************
         IHAASCB DSECT=YES
         EJECT
DSN3@ATH CSECT
***********************************************************************
*        DSECT FOR ADDRESS SPACE EXTENSION BLOCK
***********************************************************************
         IHAASXB DSECT=YES
         EJECT
DSN3@ATH CSECT
***********************************************************************
*        DSECT FOR ACCESSOR ENVIRONMENT ELEMENT
***********************************************************************
         IHAACEE
         EJECT
DSN3@ATH CSECT
***********************************************************************
*        DSECT FOR CONNECT GROUP NAME TABLE DEFINITION
***********************************************************************
         ICHPCGRP
         EJECT
DSN3@ATH CSECT
***********************************************************************
         EJECT
DSN3@ATH CSECT
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         EQUATE
         EJECT
***********************************************************************
*        DYNAMIC STORAGE AREA
***********************************************************************
WORKAREA DSECT
         DS    0F
SAVEAREA DS    18F                     DSN3@ATH SAVE AREA
WORKLEN  EQU   *-WORKAREA
***********************************************************************
         END   DSN3@ATH
