         TITLE 'CREATE SEQUENTIAL FILE OF RACF USER PROFILES'
***********************************************************************
*                                                                     *
*             MODULE NAME = USERLIST                                  *
*                                                                     *
*             DESCRIPTIVE NAME = CREATE A SEQUENTIAL FILE             *
*                OF RACF USERS AND THEIR DEFAULT GROUPS.              *
*                                                                     *
*             FUNCTION =                                              *
*                CREATES SEQUENTIAL RACF FILE FOR USE IN              *
*                CREATING THE IEAICS00 IN SYS1.PARMLIB.               *
*                                                                     *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                07/10/86  WRITTEN.     BHC.                          *
*                                                                     *
*                                                                     *
* RUN INSTRUCTIONS:                                                   *
*                                                                     *
* SYSPRINT DD - MUST BE PRESENT FOR ERROR & INFORMATION MSGS          *
*                                                                     *
* SYSUT2 DD - SEQUENTIAL OUTPUT DATA SET (RECFM=FB, LRECL=80)         *
*                                                                     *
***********************************************************************
USERLIST CSECT
         SAVE  (14,12),,USERLIST_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING USERLIST,R12,R11         TELL ASSEMBLER
         LA    R11,4095(R12)
         LA    R11,1(R11)
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
         PRINT NOGEN                    SAVE AREA
**********   FIND DDNAMES THAT ARE PRESENT
         SLR   R3,R3                    CLEAR R3
         L     R2,16                    ADDRESS OF CVT
         L     R2,0(R2)                 ADDRESS OF TCB ADDRESS WORDS
         L     R2,4(R2)                 ADDRESS OF CURRENT TCB
         L     R2,12(R2)                ADDRESS OF THIS TCBS TIOT
         LA    R2,24(R2)                ADDRESS OF START OF DD ENTRY
         IC    R3,0(R2)                 LENGTH OF THIS ENTRY
FINDDD1  EQU   *
         LTR   R3,R3
         BZ    DONEDD
         CLC   4(8,R2),=CL8'SYSPRINT'   IS IT SYSPRINT?
         BE    GOTPRINT
         CLC   4(8,R2),=CL8'SYSUT2'     IS IT SYSUT2?
         BE    GOTSYS2
         B     NEXTDD                   DONT KNOW WHY BUT TRY AGAIN
GOTPRINT EQU   *
         MVI   PRINTSW,C'1'            SET SYSPRINT DD PRESENT
         B     NEXTDD
GOTSYS2  EQU   *
         MVI   SYS2SW,C'1'             SET SYSUT2 DD PRESENT
         B     NEXTDD
NEXTDD   EQU   *
         AR    R2,R3                    BUMP TO NEXT DD ENTRY
         IC    R3,0(R2)                 GET LENGTH OF ENTRY
         B     FINDDD1                  LOOP
DONEDD   EQU   *
**************       VALIDATE  DD'S
         CLI   SYS2SW,C'1'              IF SYSUT2 DD NOT PRESENT
         BE    *+8                       PRINT ERROR MESSAGE
         B     ERROR01                    AND QUIT
         CLI   PRINTSW,C'1'             IF SYSPRINT NOT PRESENT
         BE    *+8                       PRINT ERROR MESSAGE
         B     ERROR02A                   AND QUIT
***************   INITIALIZE
         MVI   PRINTER,X'40'            INITIALIZE PRINT LINE
         MVC   PRINTER+1(131),PRINTER
         MVC   NUMPROFS,ZEROS          INITIALIZE PROFILE COUNTER
         MVC   RACFWORK(4),RACFLENG      STORE LENGTH OF WORK AREA
         MVI   OUTREC,X'40'            CLEAR OUTPUT AREA
         MVC   OUTREC+1(L'OUTREC-1),OUTREC
***************   OPEN SYSPRINT
         OPEN  (SYSPRINT,OUTPUT,SYSUT2,OUTPUT)  OPEN OUTPUT DATASETS
***************   OPEN AND READ SYSIN
*************************** GET NEXT USER RACF PROFILE
GETUSR   EQU   *
         MVC   TYPE,=CL3'USR'          INIT PROFILE TYPE
         MVC   NUMPROFS,ZEROS          INIT COUNTER
         MVI   DSNAME,X'40'            INITIALIZE RACF NAME AREA
         MVC   DSNAME+1(43),DSNAME
         MVC   DSNAME(2),=CL2'T0'      INITIALIZE RACF NAME AREA
         MVI   DSNAMEL,X'02'           INITIALIZE RACF NAME LENGTH
GETUSR1  EQU   *
         ICHEINTY NEXTC,TYPE='USR',ENTRY=DSNAMEL,                      X
               ACTIONS=(U1,U2),                                        X
               WKAREA=RACFWORK
*
*
         LTR   R15,R15                 IF OK,
         BZ    PROFU                      CONTINUE
         C     R15,=F'12'              END OF RACF GROUP PROFILES?
         BE    ENDUSR                  IF SO, GO CLOSE AND END
         B     CLOSEX                  IF NOT, TAKE ERROR EXIT
PROFU    EQU   *
         LA    R2,RACFWORK             GET ADDRESS OF RACF WORK AREA
         USING USRFLD0,R2              ESTABLISH ADDRESSABILITY
         LA    R3,OUTREC               GET ADDRESS OF OUTPUT RECORD
         USING TYPE2,R3                ESTABLISH ADDRESSABILITY
         MVI   T2NAME,X'40'            CLEAR USER
         MVC   T2NAME+1(L'T2NAME-1),T2NAME
         SR    R7,R7
         ICM   R7,B'0001',DSNAMEL      GET LENGTH OF USER
         BCTR  R7,0                    DECREMENT
         LA    R8,T2NAME
         EX    R7,MOVNAME
         MVC   T2PGMNM,USPGMNM         LOAD NAME OF USER
         MVC   T2DFLTG,USDFLTG         LOAD DEFAULT GROUP
GU70     EQU   *
         BAL   R5,WRITEIT              WRITE RECORD INTO SYSUT2 FILE
         L     R8,NUMPROFS
         LA    R8,1(R8)                COUNT THE PROFILE
         ST    R8,NUMPROFS
         CLC   DSNAME(6),=CL6'T99999'  LAST USER PROCESSED?
         BNE   GETUSR1                 IF NOT, CONTINUE
ENDUSR   EQU   *
         BAL   R5,INFOMSG              WRITE INFORMATION MESSAGE
         B     CLOSE                   ELSE, END PROGRAM
*
*  CLOSE FILES AND TERMINATE PROGRAM.
*
CLOSEX   EQU   *                       ERROR EXIT
         LR    R7,R15                  SAVE CONTENTS OF R15
         CLOSE (SYSUT2)                CLOSE THE FILE
         CLOSE (SYSPRINT)
         LR    R15,R7                  RESTORE CONTENTS OF R15
         B     RETURN
ENDIN    EQU   *
CLOSE    EQU   *
         CLOSE (SYSUT2)                CLOSE THE FILE
         CLOSE (SYSPRINT)
         SR    R15,R15                 SET RETURN CODE
RETURN   L     R13,4(R13)              RESTORE REGISTERS
         RETURN (14,12),RC=(15)        AND END
***********************************************************************
*
*  WRITE THE OUTPUT LINE
*
WRITEIT  EQU   *
         PUT   SYSUT2,OUTREC           PRINT THE LINE
         MVI   OUTREC,X'40'            CLEAR OUTPUT AREA
         MVC   OUTREC+1(L'OUTREC-1),OUTREC
         BR    R5                      RETURN
*
PRINTIT  EQU   *
         PUT   SYSPRINT,PRINTER        PRINT THE LINE
         MVI   PRINTER,X'40'           CLEAR PRINT LINE
         MVC   PRINTER+1(131),PRINTER
         BR    R8                      RETURN
*
ERROR01  EQU   *
         WTO   'USERLIST - SYSUT2 DD MISSING OR MISPELLED',            X
               ROUTCDE=(11)
         L     R15,=F'16'            SET RETURN CODE TO 16
         B     RETURN                END
*
ERROR02A EQU   *
         WTO   'USERLIST - SYSPRINT DD MISSING OR MISPELLED',          X
               ROUTCDE=(11)
         L     R15,=F'16'            SET RETURN CODE TO 16
         B     RETURN                END
*
INFOMSG  EQU   *
         L     R8,NUMPROFS          GET NUMBER PROCESSED
         CVD   R8,PWORK             CONVERT TO PACKED DECIMAL
         UNPK  WORK,PWORK+3(5)      UNPACK
         MVZ   WORK+7(1),WORK+6     MOVE ZONE BYTE
         MVC   INFONUM,WORK         LOAD INTO MESSAGE
         MVC   INFOTYP,TYPE         LOAD TYPE INTO MESSAGE
         MVC   PRINTER+2(L'INFOLINE),INFOLINE   LOAD INTO PRINT BUFFER
         BAL   R8,PRINTIT           PRINT THE LINE
         BR    R5                   CONTINUE
*
***********************************************************************
*                     CONSTANT DATA                                   *
***********************************************************************
*
*  REGISTER EQUATES
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*    THE FOLLOWING FIELDS WILL BE RETRIEVED FROM THE RACF DATA SET
*
*
U1       ICHEACTN FIELD=PGMRNAME
U2       ICHEACTN FIELD=DFLTGRP
*
***********************************************************************
SAVEAREA DS    18F
*
RETCODE  DS    1F'0'
*
         DS    1F'0'
OUTREC   DS    CL80
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FB,LRECL=132,            X
               MACRF=(PM)
*
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,RECFM=FB,LRECL=80,               X
               MACRF=(PM)
*
PRINTER  DS    CL132
SYS2SW   DC    C'0'           SYSUT2 DD -  1 PRESENT, 0 MISSING
PRINTSW  DC    C'0'           SYSPRINT -  1 PRESENT, 0 MISSING
DSNAMEL  DS    XL1
DSNAME   DS    CL44
RACFWORK DS    XL3076
TYPE     DS    CL3
WORK     DS    CL8
NUMPROFS DS    1F
PWORK    DS    1D
*
RACFLENG DC    F'3072'
*
ZEROS    DC    XL8'00'
*
MOVNAME  MVC   0(0,R8),DSNAME
*
INFOLINE DS    0CL45
         DC    C'NUMBER OF "'
INFOTYP  DS    CL3
         DC    C'" PROFILES PROCESSED = '
INFONUM  DS    CL8
*
*
USRFLD0  DSECT
         DS    CL30
USPGMNM  DS    CL20
         DS    CL2
USDFLTG  DS    CL8
*
*
TYPE2    DSECT          USER PROFILE RECORD
T2NAME   DS    CL8      USERID
T2PGMNM  DS    CL20     THE NAME OF THE USER
T2DFLTG  DS    CL8      THE DEFAULT GROUP ASSOCIATED WITH THE USER
*
         ICHPRCVT
         CVT   DSECT=YES
*
         END   USERLIST
