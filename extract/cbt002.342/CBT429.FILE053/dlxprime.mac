         TITLE 'DLXPRIME -- PRIME A KSDS VSAM DATASET'
*
*     THIS PROGRAM IS FOR USE ON KSDS FILES ONLY.
*     DELVSAM IS CALLED TO DELETE THE RECORD THAT WAS WRITTEN
*        TO PRIME THE FILE.
*
*         REGISTER USAGE
*             R0  - STANDARD
*             R1  - "
*             R2  - WORK
*             R3  - WORK THEN STORE ADDRESS OF GETMAIN STORAGE
*             R4  - STORE RECORD LENGTH
*             R5  - STORE RPL ADDRESS
*             R6  - WORK AND THEN STORE ACB ADDRESS
*             R7  - WORK
*             R8  -
*             R9  -
*             R10 - WORK
*             R11 -
*             R12 - BASE REGISTER
*             R13 - STANDARD
*             R14 -    "
*             R15 -    "
DLXPRIME CSECT
         SAVE  (14,12),,DLXPRIME_PRIME_ANY_KSDS_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING DLXPRIME,R12             TELL ASSEMBLER
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
         L     R10,0(R1)                LOAD ADDRESS OF PARAMETERS
         ST    R1,PARMADD               SAVE PARM ADDRESS
*   BEGINNING OF RACF CHECKING
         NOP   ENDRACF                 MAKE HARD BRANCH ON VS1
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
*
*  IF USER HAS SPECIAL ATTRIBUTE, EXIT WITHOUT FURTHER CHECKING
*
         L     R2,ASCBASXB             GET ADDRESS OF ASXB
         DROP  R2
         USING ASXB,R2                 TELL ASSEMBLER
         L     R2,ASXBSENV             GET ADDRESS OF ACEE
         DROP  R2                      DROP ASXB ADDRESSABILITY
         USING ACEE,R2                 ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEESPEC       USER HAVE SPECIAL ATTRIBUTE?
         BO    ENDRACF                 IF NOT, CHECK FURTHER
         DROP  R2                      DROP ASXB ADDRESSABILITY
         RACHECK ENTITY=PROGNAME,CLASS='APPL'
         LTR   R15,R15
         BNZ   ERROR11
ENDRACF  EQU   *
*   END OF RACF CHECKING
         LH    R2,0(R10)                LENGTH OF PARM TO R2
         LTR   R2,R2                    CHECK FOR NO PARM
         BZ    ERROR00                  IF NONE ISSUE MESSAGE AND QUIT
         LA    R10,2(R10)               BUMP PAST LENGTH
         BCTR  R2,R0                    DECREMENT FOR MOVE
* END OF PROLOGUE                       SAVE AREA
         EX    R2,MOVEDD                MOVE IN DDNAME
         L     R2,16                    ADDRESS OF CVT
         L     R2,0(R2)                 ADDRESS OF TCB ADDRESS WORDS
         L     R2,4(R2)                 ADDRESS OF CURRENT TCB
         L     R2,12(R2)                ADDRESS OF THIS TCBS TIOT
         LA    R2,24(R2)                ADDRESS OF START OF DD ENTRY
         SLR   R3,R3                    ZERO R3
         IC    R3,0(R2)                 LENGTH OF THIS ENTRY
FINDDD1  EQU   *
         LTR   R3,R3
         BZ    ERROR01
         CLC   4(8,R2),DDNAME           IS IT THE PASSED DDNAME?
         BE    DONEDD
         B     NEXTDD                   DONT KNOW WHY BUT TRY AGAIN
NEXTDD   EQU   *
         AR    R2,R3                    BUMP TO NEXT DD ENTRY
         IC    R3,0(R2)                 GET LENGTH OF ENTRY
         B     FINDDD1                  LOOP
DONEDD   EQU   *
*        AT THIS POINT WE KNOW THAT THE DDNAME THAT WAS PASSED TO US
*        HAS A DD CARD SO WE CAN TRY TO CREATE CONTROL BLOCKS FOR IT
*        AND OPEN IT
GETACB   GENCB BLK=ACB,                                                X
               AM=VSAM,                                                X
               CATALOG=NO,                                             X
               COPIES=1,                                               X
               DDNAME=(*,DDNAME),                                      X
               MACRF=(KEY,NFX,NDF,SEQ,NCI,OUT,NIS,NRM,NRS,NUB),        X
               STRNO=2
         LTR   R15,R15
         BNZ   ERROR02
         ST    R1,ACBADDR
         LR    R6,R1
OPENIT   OPEN  ((R6))                   OPEN THE ACB
         B     ERRTYPE(R15)
ERRTYPE  B     CLOSEIT                  IF OPEN GOOD --> CLOSE AND END
         B     ERROR05
         B     LOGICAL                  LOGIC ERROR --> CHECK ERRORCODE
         B     ERROR05
LOGICAL  TESTCB ACB=(R6),                                              X
               ERROR=X'A0',             CHECK IF FILE NEEDS PRIMING    X
               ERET=ERROR08
         BNE   ERROR05
MODACB   MODCB ACB=(R6),                MODIFY ACB FOR LOAD MODE       X
               AM=VSAM,                                                X
               STRNO=1
         LTR   R15,R15
         BNZ   ERROR03
OPEN2    OPEN  ((R6))                   OPEN THE ACB TO ADD RECORD
         LTR   R15,R15
         BNZ   ERROR05
*        OPEN WAS SUCCESSFUL SO TEST TO BE SURE IT IS A KSDS
TESTKSDS TESTCB ACB=(R6),                                              X
               ATRB=KSDS,                                              X
               ERET=ERROR08
         BNE   ERROR10
*        THIS IS A KSDS SO LETS FIND THE KEY LENGTH AND OFFSET
         SHOWCB ACB=(R6),                                              X
               AREA=SHOWFLD,                                           X
               LENGTH=16,                                              X
               FIELDS=(ERROR,KEYLEN,LRECL,RKP)
         LTR   R15,R15
         BNZ   ERROR06
*        SHOWCB WAS SUCCESSFUL SO LETS GET STORAGE AREA
         L     R4,RECLEN                 LOAD RECORD LENGTH TO R4
GETSTOR  GETMAIN R,LV=(R4)
         LR    R3,R1                     LOAD STORAGE ADDR TO R3
*        TIME TO GENERATE RPL FOR UPDATE
GETRPL   GENCB BLK=RPL,                                                X
               ACB=(R6),                                               X
               AM=VSAM,                                                X
               AREA=(R3),                                              X
               AREALEN=(R4),                                           X
               RECLEN=(R4),                                            X
               COPIES=1,                                               X
               OPTCD=(KEY,SEQ,ARD,FWD,SYN,NUP,MVE)
         LTR   R15,R15
         BNZ   ERROR04
         ST    R1,RPLADDR
         LR    R5,R1 LOAD ADDRESS OF RPL TO R5
*   RPL GEN IS GOOD, NOW ZERO OUT KEY AND WRITE RECORD
         LR    R7,R3
         AH    R7,KEYDISP            GET STARTING ADDRES OF KEY
         MVI   0(R7),C'0'            MOVE 0 TO FIRST CHARACTER
         L     R2,KEYYLEN            CALC KEY LENGTH
         SH    R2,=H'2'                    MINUS 2 FOR PROPAGATION
         EX    R2,ZEROKEY
PUTREC   PUT   RPL=(R5)              WRITE RECORD WITH KEY=ZEROS
         LTR   R15,R15
         BNZ   ERROR09
*   RELEASE STORAGE
FREESTOR FREEMAIN R,LV=(R4),A=(R3)
*
CLOSE2   CLOSE ((R6))                 CLOSE BEFORE CALLING DELVSAM
         LTR   R15,R15
         BNZ   ERROR07
*
* CALL DELVSAM TO DELETE RECORD IN KSDS
         L     R1,PARMADD
         LINK  EP=DELVSAM
         B     RETURNX
CLOSEIT  CLOSE ((R6))                    CLOSE THE FILE
         LTR   R15,R15
         BNZ   ERROR07
         WTO   'DLXPRIME MESSAGE - KSDS FILE WAS ALREADY PRIMED',      X
               ROUTCDE=(11)
* BEGINNING OF EPILOGUE
RETURN0  EQU   *
         LA    R15,4                    SET RETURN CODE TO 4
RETURNX  L     R13,SAVEAREA+4           LOAD OS SAVE AREA ADDR TO R13
         RETURN (14,12),RC=(15)         RETURN TO OS
* END OF EPILOGUE
SAVEAREA DC    18F'0'                   OUR SAVE AREA
MOVEDD   MVC   DDNAME(*-*),0(R10)       MOVE IN DDNAME
ZEROKEY  MVC   1(0,R7),0(R7)            PROPAGATE ZERO THROUGHOUT KEY
PROGNAME DC    CL8'DLXPRIME'            PROGRAM NAME FOR RACF
ACBADDR  DS    F                        ADDRESS OF ACB
RPLADDR  DS    F                        ADDRESS OF RPL
RECADDR  DS    F                        ADDRESS OF RECORD WITHIN BUFFER
PARMADD  DS    F                        ADDRESS OF PARAMETER LIST
SHOWFLD  DS    0F                       HOLD AREA FOR SHOWCB
ERRCODE  DS    F                        ERROR
KEYYLEN  DS    F                        KEYLEN
RECLEN   DS    F                        LRECL
KEYDISP  DS    F                        RKP (KEY DISPLACEMENT)
DDNAME   DC    CL8' '                   HOLD AREA FOR DDNAME
         PRINT NOGEN
ERROR00  EQU   *
         WTO   'DLXPRIME00 DLXPRIME DDNAME PARM MISSING',              X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR01  EQU   *
         WTO   'DLXPRIME01 DLXPRIME DD CARD MISSING OR MISPELLED',     X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR02  EQU   *
         WTO   'DLXPRIME02 GENCB FOR AN ACB FAILED',                   X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR03  EQU   *
         WTO   'DLXPRIME03 MODCB FOR AN ACB FAILED',                   X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR04  EQU   *
         WTO   'DLXPRIME04 GENCB FOR AN RPL FAILED',                   X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR05  EQU   *
         WTO   'DLXPRIME05 OPEN FOR AN ACB FAILED',                    X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR06  EQU   *
         WTO   'DLXPRIME06 SHOWCB FOR AN ACB FAILED',                  X
               ROUTCDE=(11)
         CLOSE ((R6))
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR07  EQU   *
         WTO   'DLXPRIME07 CLOSE OF AN ACB FAILED',                    X
               ROUTCDE=(11)
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR08  EQU   *
         WTO   'DLXPRIME08 TESTCB OF AN ACB FAILED',                   X
               ROUTCDE=(11)
         CLOSE ((R6))
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR09  EQU   *
         WTO   'DLXPRIME09 PUT OF RECORD FAILED',                      X
               ROUTCDE=(11)
         CLOSE ((R6))
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR10  EQU   *
         WTO   'DLXPRIME10 DDNAME DOES NOT SPECIFY A KSDS FILE',       X
               ROUTCDE=(11)
         CLOSE ((R6))
         LA    R15,12                   SET RETURN CODE 12
         B     RETURNX
ERROR11  EQU   *
         WTO   'DLXPRIME11 DLXPRIME NOT AUTHORIZED FOR YOUR USERID',   X
               ROUTCDE=(2,9,11)
         ABEND X'913',,STEP     ABEND WITH USER 913 AND ABEND USER
         PRINT GEN
         LTORG
         PRINT NOGEN
         EQUATE                         REGISTER EQUATES
*
*  DSECTS
*
         IHAACEE
         IHAASCB
         IHAASXB
*
         PRINT GEN
         END   DLXPRIME
