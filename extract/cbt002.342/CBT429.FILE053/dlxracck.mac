DLXRACCK CSECT
         SAVE  (14,12),,DLXRACCK_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING DLXRACCK,R12             TELL ASSEMBLER
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
* END OF PROLOGUE                       SAVE AREA
GETPARM1 EQU   *                        GET PARM FIELD
         L     R1,0(R1)                 ADDRESS OF PARM TO R2
         LH    R3,0(R1)                 LENGTH OF PARM TO R3
         LTR   R3,R3                    CHECK FOR NO PARM
         BZ    RETURN16                 NO PARMS
         CH    R3,=H'8'                 CHECK FOR VALID ADDRESS LENGTH
         BH    RETURN20                 BAD LENGTH
         BCTR  R3,R0                    DECREMENT LENGTH FOR EXECUTE
         MVC   PROGNAME(*-*),2(R1)      MOVE PARM
         EX    R3,*-6                   VIA EXECUTE
*   BEGINNING OF RACF CHECKING
         SLR   R15,R15                 START OUT WITH 0
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
*
*  IF USER HAS SPECIAL ATTRIBUTE, EXIT WITHOUT FURTHER CHECKING
*
         L     R2,ASCBASXB             GET ADDRESS OF ASXB
         DROP  R2
         USING ASXB,R2                 TELL ASSEMBLER
         L     R2,ASXBSENV             GET ADDRESS OF ACEE
         DROP  R2                      DROP ASXB ADDRESSABILITY
         USING ACEE,R2                 ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEESPEC       USER HAVE SPECIAL ATTRIBUTE?
         BO    ENDRACF                 IF NOT, CHECK FURTHER
         DROP  R2                      DROP ASXB ADDRESSABILITY
         RACHECK ENTITY=PROGNAME,CLASS='APPL',LOG=NOSTAT
ENDRACF  EQU   *
*   END OF RACF CHECKING
         B     RETURNX
RETURN16 LA    R15,16
         B     RETURNX
RETURN20 LA    R15,20
         B     RETURNX
RETURNX  L     R13,SAVEAREA+4           LOAD OS SAVE AREA ADDR TO R13
         RETURN (14,12),T,RC=(15)       RETURN TO OS RC=R15
SAVEAREA DC    18F'0'                   OUR SAVE AREA
PROGNAME DC    CL8'        '
         LTORG
         EQUATE
         IHAACEE
         IHAASCB
         IHAASXB
         CVT   DSECT=YES                REQUIRED FOR CALLRTM
         END   DLXRACCK
