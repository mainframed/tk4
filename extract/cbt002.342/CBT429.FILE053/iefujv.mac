IEFUJV   CSECT                          SMF JOB VALIDATION EXIT
         SAVE  (14,12)                  SAVE THE REGISTERS
         LR    R12,R15                  ESTABLISH ADDRESSABILITY
         USING IEFUJV,R12               TELL THE ASSEMBLER
         LR    R3,R1                    SAVE POINTER TO ADDR LIST IN R3
         GETMAIN R,LV=84,SP=241         GET SAVEAREA POINTED TO BY R1
         ST    R13,4(R1)                STOR ADDRESS OF OLD SAVE IN NEW
         ST    R1,8(R13)                STOR ADDRESS OF NEW SAVE IN OLD
         LR    R13,R1                   R13 NOW POINTS TO NEW SAVEAREA
         LM    R9,R11,0(R3)             LOAD REGISTERS AS FOLLOWS
*                                       R9  ADDRESS OF CEPL
*                                       R10 ADDRESS OF JCL IMAGE
*                                       R11 ADDRESS OF JCL TYPE
         STM   R9,R11,72(R13)           STORE PARAMETER POINTERS
         USING GOTTEN,R13               TELL ASSEMBLER HOW TO
         USING CEPLBEGN,R9              ADDRESS THESE AREAS
         L     R6,ADDRJCLT              R6 POINTS TO JCL TYPE DESC AREA
         CLI   0(R6),X'01'              IS IT A JOB CARD?
         BE    JOBROUT                  GO CHECK ACCOUNTING FIELD
         CLI   0(R6),X'02'              IS IT AN EXEC CARD?
         BE    EXECROUT                 GO CHECK PROGRAM VALIDITY
         CLI   0(R6),X'04'              IS IT AN DD CARD?
         BE    DDROUT                   GO CHECK FOR JOB/STEP CAT
         BNE   JOBOK                    NO GO TO JOBOK
***********************************************************************
*         BEGINNING OF JOB CARD CHECKING
***********************************************************************
JOBROUT  EQU   *
         L     R1,ADDRJCL               REG1 POINTS TO COL 1 OF CARD
         LA    R7,10(R1)                REG7 POINTS TO COL 11 OF CARD
         LA    R1,2(R1)                 POINTS PAST THE '//'
*
CHEKTSO  EQU   *                        CHECK TO SEE IF TSO OR STC
         CLC   70(3,R1),=C'JOB'         IS THIS A JOBS JOB CARD?
         BE    CHEKSPC                  YES, CONTINUE TO PARSE
         BNE   JOBOK                    NO, MUST BE TSO OR STC PASS IT
CHEKSPC  EQU   *
         CLI   0(R1),C' '               COMPARE TO SPACE
         BE    CHEKJOB                  CHECK FOR JOB
         CR    R1,R7                    COL 11 YET?
         BE    JOBOK                    WILL END WITH JCL ERROR
         LA    R1,1(R1)                 BUMP REG 1
         B     CHEKSPC
*
CHEKJOB  L     R7,ADDRJCL               GET ADDR OF JOBCARD
         LA    R7,67(R7)                R7 POINTS TO COL 68
CHEKLOOP CLC   0(5,R1),=C' JOB '        CHECK FOR ' JOB ' IN CARD
         BE    PASTJOB                  BRANCH TO ACCOUNT CHECK
         LA    R1,1(R1)                 MOVE POINTER TO NEXT COL
         CR    R1,R7                    END OF CARD?
         BNL   JOBOK                    MUST BE CONTINUATION CARD
         B     CHEKLOOP
*
PASTJOB  LA    R1,5(R1)                 POINT PAST 'JOB '
CHEKACCT EQU   *                        CHECK FOR ACCT # ROUTINE
         CLI   0(R1),C' '               IS IT A SPACE?
         BNE   ACTCHEK                  MUST BE ACCT #
         LA    R1,1(R1)                 MOVE POINTER TO NEXT COL
         CR    R1,R7                    HOW MUCH SPACE IS LEFT?
         BH    CANCEL                   ACCT MUST BE 4 CHARS
         B     CHEKACCT                 TRY AGAIN
*
ACTCHEK  EQU   *                        VERIFY ACCT #
         CLI   0(R1),C'('               IS IT A '('?
         BE    NEXTCHAR                 OR
         CLI   0(R1),X'7D'              IS IT AN APOST?
         BE    NEXTCHAR                 KEEP GOING OTHERWISE
         B     CHEK1ST                  GO TO START OF ACCT #
NEXTCHAR LA    R1,1(R1)                 BUMP PAST '(' OR APOST
         CR    R1,R7                    HOW MUCH SPACE IS LEFT?
         BH    CANCEL                   ACCT MUST BE 4 CHARS
*******************************************************************
*                                       WE NOW HAVE AT LEAST 4 CHARS
*                                       REMAINING ON THE JOB CARD FOR
*                                       THE ACCOUNT NO AND WE ARE FOR
*                                       SURE PROCCESSING POSITIONAL
*                                       PARAMETER NO 1 (ACCOUNT NO)
*        RULES FOR ACCOUNT FIELD ON DELUXE JOBS:
*        ACCOUNT FIELD IS FOUR CHARACTERS OF THE FORM "ABCN".
*        A     MUST BE AN "M" "P" "G" OR "E"
*        B     MUST BE ALPHABETIC
*        C     MUST BE ALPHABETIC
*        N     MUST BE NUMERIC
*******************************************************************
CHEK1ST  EQU   *                        VALID 1ST CHAR IS E,G,M OR P
         CLI   0(R1),C'E'               IS IT E ?
         BE    CHEK2ND                  YES. CHEK SECOND CHAR
         CLI   0(R1),C'G'               IS IT G ?
         BE    CHEK2ND                  YES. CHEK SECOND CHAR
         CLI   0(R1),C'M'               IS IT M ?
         BE    CHEK2ND                  YES. CHEK SECOND CHAR
         CLI   0(R1),C'P'               IS IT P ?
         BE    CHEK2ND                  YES. CHEK SECOND CHAR
         B     CANCEL                   FIRST CHAR NO GOOD
CHEK2ND  LA    R1,1(R1)                 BUMP TO SECOND CHAR.
         BAL   6,ALPHROUT               GO TO ALPHROUT
CHEK3RD  LA    R1,1(R1)                 BUMP TO THIRD CHAR.
         BAL   R6,ALPHROUT              GO TO ALPHROUT
CHEK4TH  LA    R1,1(R1)                 BUMP TO FOURTH
         CLI   0(R1),X'F0'              COMPARE TO ZERO
         BL    CANCEL                   LESS THAN ZERO
         CLI   0(R1),X'F9'              COMPARE TO NINE
         BH    CANCEL                   MORE THAN NINE
         B     JOBOK
*
ALPHROUT CLI   0(R1),X'C1'              COMPARE TO A
         BL    CANCEL                   LESS THAN A ?
         CLI   0(R1),X'C9'              GREATER THAN I ?
         BH    ALPH2                    TRY AGAIN
         BR    R6                       OK.
ALPH2    CLI   0(R1),X'D1'              COMPARE TO J
         BL    CANCEL                   LESS THAN J ?
         CLI   0(R1),X'D9'              GREATER THAN R ?
         BH    ALPH3                    TRY AGAIN
         BR    R6                       OK.
ALPH3    CLI   0(R1),X'E2'              COMPARE TO S
         BL    CANCEL                   LESS THAN S ?
         CLI   0(R1),X'E9'              GREATER THAN Z ?
         BH    CANCEL                   NOT ALPHABETIC
         BR    R6                       OK.
***********************************************************************
*         END OF JOB CARD CHECKING
***********************************************************************
***********************************************************************
*         BEGINNING OF EXEC CARD CHECKING
***********************************************************************
EXECROUT EQU   *
         CLI   CEPLUID+2,X'40'          USERID SPECIFIED?
         BE    CANCEL                   NO, CANCEL JOB
CLASCHK1 EQU   *                        SYSTEM PROGRAMMERS ONLY CLASS S
         CLI   CEPLJCLS,C'S'            CLASS S JOB?
         BE    CLASCHKS                 CONTINUE
         CLI   CEPLJCLS,C'R'            CLASS R JOB?
         BE    CLASCHKS                 CONTINUE
         CLI   CEPLJCLS,C'I'            CLASS I JOB?
         BE    CLASCHKI                 CONTINUE
         CLI   CEPLJCLS,C'J'            CLASS J JOB?
         BE    CLASCHKI                 CONTINUE
         B     CLASCHKX
CLASCHKS EQU   *
         LA    R1,SYSPTABL
         B     CLASCHK2
CLASCHKI EQU   *
         LA    R1,SPECTABL
         B     CLASCHK2
CLASCHK2 EQU   *                        LOOP THROUGH TABLE
         CLI   0(R1),C' '               END OF TABLE
         BE    CANCEL
         CLC   0(6,R1),CEPLUID+2        IN THE TABLE
         BE    CLASCHKX                 CONTINUE
         LA    R1,6(R1)                 NEXT TABLE ENTRY
         B     CLASCHK2                 LOOP
CLASCHKX EQU   *
         L     R1,ADDRJCL               R1 PTS TO COL 1 OF CARD
         LA    R7,66(R1)                R7 PTS TO COL 67 OF CARD
         LA    R1,8(R1)                 R1 PTS TO COL 9 OF CARD
EXECLOOP LR    R4,R7                    R4 HAS ADDRESS OF COL 67
         SR    R4,R1                    R4 HAS NO OF COLS TO WORK WITH
         EX    R4,TRTINST               EXEC TRT, R4 HAS LENGTH
         BZ    JOBOK                    'P' NOT FOUND IN CARD
         CLC   0(4,R1),=C'PGM='         R1 PTS TO LAST BYTE TRANSLATED
         BE    CHECKPGM                 GO CHECK PROGRAM NAME
         CR    R7,R1                    HOW MANY COLS HAVE BEEN TRTD?
         BE    JOBOK                    ALL OF THEM GET OUT
         LA    R1,1(R1)                 R1 POINTS TO NEXT COL RIGHT
         B     EXECLOOP                 TRY AGAIN
CHECKPGM EQU   *                        PROGRAM VALIDATION ROUTINE
         CLC   4(3,R1),FATS             TRYING TO EXEC FATS OR FATAR?
         BE    CHKAUTH0                 YES
         CLC   7(3,R1),SMP              TRYING TO EXEC SMP?
         BE    CHKAUTH1                 YES
         CLC   4(8,R1),IOCP             TRYING TO EXEC IOCP?
         BE    CHKAUTH1                 YES
         CLC   4(6,R1),DSF              TRYING TO EXEC ICKDSF?
         BE    CHKAUTH1                 YES
         CLC   5(7,R1),SPZAP            TRYING TO EXEC SUPERZAP?
         BE    CHKAUTH1                 YES
         B     JOBOK                    NOT EXECUTING PROTECTED PGM
CHKAUTH0 EQU   *
         CLC   CEPLUID+2(6),=CL6'T56736' IS IT DASDMGMT
         BE    JOBOK
         B     CHKAUTH1
CHKAUTH1 EQU   *
         LA    R1,SPECTABL
AUTH1    EQU   *                        LOOP THROUGH TABLE
         CLI   0(R1),C' '               END OF TABLE?
         BE    CANCEL                   IF SO, CANCEL
         CLC   0(6,R1),CEPLUID+2        MATCH TABLE?
         BE    JOBOK                    IF SO, GO AHEAD
         LA    R1,6(R1)                 NEXT ENTRY
         B     AUTH1                    LOOP
***********************************************************************
*         END OF EXEC CARD CHECKING
***********************************************************************
***********************************************************************
*         BEGINNING OF DD CARD CHECKING
***********************************************************************
DDROUT   EQU   *
         L     R1,ADDRJCL               REG1 POINTS TO COL 1 OF CARD
         LA    R1,2(R1)                 POINTS PAST THE '//'
         CLC   0(8,R1),=C'STEPCAT '     IS IT A STEPCAT
         BE    CHKAUTH1                 YES
         CLC   0(7,R1),=C'JOBCAT '      IS IT A JOBCAT
         BE    CHKAUTH1                 YES
         B     JOBOK
***********************************************************************
*         END OF DD CARD CHECKING
***********************************************************************
CANCEL   EQU   *
         L     R1,ADDRJCL               R1 NOW HAS ADDR OF JCL IMAGE
         MVC   8(31,R1),CANCLMSG        OVERLAY PART OF JCL IMAGE
*                                       WITH CANCEL MESSAGE
         LR    R1,R13                   R1 NOW HAS ADDR OF GOTTEN AREA
         L     R13,4(R13)               R13 PTS TO PREVIOUS SAVEAREA
         FREEMAIN R,LV=84,A=(R1),SP=241 FREE UP GOTTEN AREA
         RETURN (14,12),RC=4            RESTORE REGS AND CANCEL JOB
JOBOK    EQU    *
         LR     R1,R13                  R1 NOW HAS ADDR OF GOTTEN AREA
         L      R13,4(R13)              R13 PTS TO PREVIOUS SAVEAREA
         FREEMAIN R,LV=84,A=(R1),SP=241 FREE UP GOTTEN AREA
         RETURN (14,12),RC=0            RESTORE REGS AND LET JOB RUN
TRTINST  TRT    0(0,R1),TRANTBL         TRT INST IS USED VIA EX INST
********************** CONSTANTS *************************************
TRANTBL  DS    0CL256                   TRANSLATE TABLE
         DC    215X'00'
LETTERP  DC    X'D7'                    THIS BYTE REPRESENTS A 'P'
         DC    40X'00'                  FILL OUT TABLE
SMP      DC    CL3'SMP'                 SYSTEM MODIFICATION PROGRAM
FATS     DC    CL3'FAT'                 FATS OR FATAR
IOCP     DC    CL8'ICPIOCP '            IOCP
DSF      DC    CL6'ICKDSF'              ICKDSF
SPZAP    DC    CL7'MASPZAP'             SUPER ZAP PROGRAM
CANCLMSG DC    CL31' CANCELED BY INSTALLATION EXIT '  CANCEL MESSAGE
SYSPTABL DC    CL6'T04108'   STOECKL    SYSTEM PROGRAMMERS USERIDS
         DC    CL6'T23661'   LITTLE     FOR CLASS S CHECK
         DC    CL6'T40839'   VERPLAETSE
SPECTABL DC    CL6'T27611'   LOOS        FROM HERE FOR SMP, ETC.
         DC    CL6'T53007'   REICHERT
         DC    CL6'T53031'   KUNTZ
         DC    CL6'T53039'   TRETTER
         DC    CL6'T53043'   HAGSTROM
         DC    CL6'T53090'   WICKSTROM
         DC    CL6'T56714'   COUTURE
         DC    CL6'T60439'   ANDERSON
         DC    CL6'T68839'   SPATAFORE
         DC    CL6'T68878'   HOLLINGSWORTH
         DC    CL6'T79157'   CARLSON
         DC    CL6'ABRJOB'
         DC    CL6'CADJOB'
         DC    CL6'SYSJOB'
         DC    CL6'TPCJOB'
         DC    CL6'      '   END OF TABLE
         EQUATE
GOTTEN   DSECT
SAVEAREA DS    18F                      18 FULLWORD SAVEAREA
ADDRCEPL DS    F                        ADDRESS OF THE COMMON EXIT
*                                       PARAMETER AREA
ADDRJCL  DS    F                        ADDRESS OF THE JCL IMAGE
ADDRJCLT DS    F                        ADDRESS OF ONE BYTE AREA
*                                       INDICATING THE TYPE OF JCL
*                                       STATEMENT BEING PROCESSED
*                                       IN BINARY VALUES AS FOLLOWS:
*                                       0  - NULL STATEMENT
*                                       1  - JOB  STATEMENT
*                                       2  - EXEC STATEMENT
*                                       4  - DD   STATEMENT
*                                       8  - PROC STATEMENT
*                                       16 - ALL JCL CONVERTED
*                                       32 - ALL JCL INTERPRETED AND
*                                            AND PASSED TO EXIT IEFUJV
CEPLBEGN DSECT                          COMMON EXIT PARAMETER AREA
CEPLJOBN DS    CL8                      JOB NAME
CEPLTIME DS    BL4                      TIME IN HUNDREDTHS OF A SECOND
*                                       READER RECOGNIZED THE JOB CARD
*                                       FOR THIS JOB
CEPLDATE DS    PL4                      DATE READER RECOGNIZED THE JOB
*                                       CARD FOR THIS JOB IN THE FORM:
*                                       00YYDDDF - WHERE F IS THE SIGN
CEPLSID  DS    CL4                      SYSTEM IDENTIFICATION
CEPLUID  DS    CL8                      USER ID FIELD
*                                       FOR DELUXES USE THIS FIELD IS
*                                       BROKEN DOWN AS FOLLOWS
*                                       BL2        ROUTE CODE
*                                       CL6        USERID
CEPLSTEP DS    BL1                      STEP NUMBER BEING PROCESSED
CEPLSMFO DS    BL1                      SMF OPTIONS USED INDICATOR
CEPLREST DS    BL1                      RESTART INDICATOR
CEPLJCLS DS    CL1                      JOB CLASS
CEPLUCOM DS    CL4                      USER COMMUNICATION FIELD
         END   IEFUJV
