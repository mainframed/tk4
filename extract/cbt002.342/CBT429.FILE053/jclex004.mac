         TITLE 'DELUXE JCL AND JES2 CONTROL STATEMENT SCAN - PREAMBLE'
**********************************************************************
*                                                                    *
*           DELUXE JCL AND JES2 CONTROL STMT SCAN EXIT 4             *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE HAS THE PURPOSE            *
*       CANCELING ALL JOBS WHICH USE EITHER 'TIME=' OR               *
*       'TIME.PROCSTEPNAME=' ON THE EXEC STATEMENT UNLESS            *
*       PERMITTED BY RACF. ALSO, CANCELS JOBS WHICH USE              *
*       'JOBPARM' STATEMENT, UNLESS PERMITTED BY RACF.               *
*                                                                    *
*    ENTRY POINT = DLXXIT04                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0     CODE INDICATING JES2 OR JCL CONTROL STATEMENT         *
*              0 - INDICATES A JES2 CONTROL STATEMENT                *
*              4 - INDICATES A JCL STATEMENT                         *
*       R1     POINTER TO A 3-WORD PARAMETER LIST:                   *
*              WORD1 (+0) - ADDR OF CNTL STMT IMAGE BUFFER           *
*              WORD2 (+4) - ADDR OF EXIT FLAG BYTE, RDWFLAGX, IN PCE *
*              WORD3 (+8) - ADDR OF JCTXWRK FIELD IN JCT             *
*       R2-R9  N/A                                                   *
*       R10    ADDRESS OF THE JCT                                    *
*       R11    ADDRESS OF THE HCT                                    *
*       R12    N/A                                                   *
*       R13    ADDRESS OF THE HASPCOMM PCE                           *
*       R14    RETURN ADDRESS                                        *
*       R15    ENTRY ADDRESS                                         *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0-R1  N/A                                                   *
*       R2-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             =0  CONTINUE WITH ADDITIONAL EXITS/HASPRDR PROCESSING  *
*             =4  CONTINUE WITH NORMAL HASPRDR PROCESSING            *
*             =8  CONVERT STMT TO COMMENT(//*), BYPASS HASPRCCS      *
*             =12 CANCEL JOB. PRODUCE OUTPUT.                        *
*             =16 CANCEL JOB. NO OUTPUT IS PRODUCED.                 *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $SAVE, $RETURN, $MODEND, $HASPGBL                   *
*                                                                    *
*    CHANGE ACTIVITY:                                                *
*        01/19/87  WRITTEN.                                    JWC.  *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
JCLEX004 $MODULE $BUFFER,                                              +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D FOR MAPPING              +
               $JOE,                    REQ'D BY $COMWORK              +
               $MIT,                    REQ'D BY $MODULE               +
               $PCE,                    HASP PROCESSOR CONTROL ELEMENT +
               $RDRWORK,                HASP INPUT SERVICE PCE WORK    +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE JCL AND JES2 CONTROL STATEMENT SCAN EXIT'
         SPACE 1
***********************************************************************
*        JES2 - INITIALIZATION AND ADDRESSABILITY
***********************************************************************
DLXXIT04 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         SLR   R15,R15                  CLEAR REG 15
         USING JCT,R10                  CLEAR REG 15
         CLC   JOB,JCTJOBID             THIS A BATCH JOB?
         BNE   RETURN                   NO, RETURN
         L     R2,0(R1)                 POINT TO CNTL STMT IMAGE BUFFER
         LA    R3,70                    MAX LENGTH OF CNTL STMT BUFFER
         CLC   ERROR,0(R2)              SHOULD JOB BE CANCELED?
         BE    CANJOB10                 YES, SET FLAG AND RETURN
         C     R0,FULL00                JES2 CONTROL STATEMENT?
         BE    JPRM00                   YES, CHECK FOR 'JOBPARM' STMT
         CLC   COMMENT,0(R2)            COMMENT STATEMENT?
         BE    RETURN                   YES, RETURN
         CLC   JOBCAT,0(R2)             JOBCAT,STATEMENT?
         BE    JOBCAT00                 YES, CHECK AUTHORIZATION
         CLC   STEPCAT,0(R2)            STEPCAT,STATEMENT?
         BE    STPCAT00                 YES, CHECK AUTHORIZATION
         CLC   CONTINUE,0(R2)           CONTINUATION STATEMENT?
         BNE   EXEC00                   NO, SCAN FOR EXEC
         TM    JCTUSER1,CONTFLAG        THIS CONTINUATION OF EXEC STMT?
         BNO   RETURN                   NO, RETURN
***********************************************************************
*        JES2 - SEARCH FOR 'TIME' PARAMETER
***********************************************************************
TIME00   EQU   *
         CLC   TIME1,0(R2)              IS THIS ',TIME='?
         BE    TIME10                   YES, CHECK FOR AUTHORITY
         CLC   TIME2,0(R2)              IS THIS ',TIME.'?
         BE    TIME10                   YES, CHECK FOR AUTHORITY
         CLC   TIME3,0(R2)              IS THIS ' TIME='?
         BE    TIME10                   YES, CHECK FOR AUTHORITY
         CLC   TIME4,0(R2)              IS THIS ' TIME.'?
         BE    TIME10                   YES, CHECK FOR AUTHORITY
         CLC   TRAILCOM,0(R2)           IS THIS ', ' (TRAILING COMMA)?
         BE    RETURN                   RETURN WITHOUT RESETTING FLAG
         LA    R2,1(R2)                 NO, INCREMENT REG2
         BCT   R3,TIME00                CONTINUE TO SCAN FOR 'TIME'
         NI    JCTUSER1,NADA            RESET CONTFLAG
         B     RETURN                   END OF EXEC STMT. NO 'TIME'.
TIME10   EQU   *
         TM    JCTUSER0,TIMEAUTH        DEFAULT TIME OVERRIDE ALLOWED?
         BO    RETURN                   YES, RETURN
         MVC   JCTXWRK,BADTIME1         ISSUE MESSAGE
         B     CANJOB00                 CANCEL JOB AND RETURN
***********************************************************************
*        JES2 - DETERMINE IF 'EXEC' STATEMENT
***********************************************************************
EXEC00   EQU   *
         CLC   EXEC,0(R2)               IS THIS ' EXEC '?
         BE    EXEC10                   YES, SET CONTFLAG
         LA    R2,1(R2)                 NO, INCREMENT REG2
         BCT   R3,EXEC00                CONTINUE TO SCAN FOR 'EXEC'
         B     RETURN                   RETURN, NOT AN 'EXEC' STMT
EXEC10   EQU   *
         OI    JCTUSER1,CONTFLAG        SET CONTFLAG
         B     TIME00                   LOOK FOR 'TIME' PARAMETER
***********************************************************************
*        JES2 - SCAN '/*JOBPARM' STMT FOR 'TIME' & 'LINES' PARMS
***********************************************************************
JPRM00   EQU   *
         CLC   JOBPARM,0(R2)            'JOBPARM' STATEMENT?
         BNE   RETURN                   NO, RETURN
         LR    R3,R2                    ADDRESS 'JOBPARM' STATEMENT
         LA    R4,70                    MAXIMUM LENGTH OF JOBPARM
JPRM10   EQU   *
         CLC   TIME1,0(R3)              IS THIS ',TIME='?
         BE    JPRM20                   YES, CHECK FOR AUTHORITY
         CLC   TIME3,0(R3)              IS THIS ' TIME='?
         BE    JPRM20                   YES, CHECK FOR AUTHORITY
         CLC   TIME5,0(R3)              IS THIS ',T='?
         BE    JPRM20                   YES, CHECK FOR AUTHORITY
         CLC   TIME6,0(R3)              IS THIS ' T='?
         BE    JPRM20                   YES, CHECK FOR AUTHORITY
         CLC   LINE1,0(R3)              IS THIS ',LINES='?
         BE    JPRM30                   YES, CHECK FOR AUTHORITY
         CLC   LINE2,0(R3)              IS THIS ' LINES='?
         BE    JPRM30                   YES, CHECK FOR AUTHORITY
         CLC   LINE3,0(R3)              IS THIS ',L='?
         BE    JPRM30                   YES, CHECK FOR AUTHORITY
         CLC   LINE4,0(R3)              IS THIS ' L='?
         BE    JPRM30                   YES, CHECK FOR AUTHORITY
         LA    R3,1(R3)                 NO, INCREMENT REG 3
         BCT   R4,JPRM10                CONTINUE TO SCAN '/*JOBPARM'
         B     RETURN                   END OF '/*JOBPARM'; WE'RE DONE
JPRM20   EQU   *
         TM    JCTUSER0,TIMEAUTH        DEFAULT TIME OVERRIDE ALLOWED?
         BO    RETURN                   YES, RETURN
         MVC   JCTXWRK,BADTIME2         ISSUE MESSAGE
         B     CANJOB00                 CANCEL JOB AND RETURN
JPRM30   EQU   *
         TM    JCTUSER0,LINEAUTH        DEFAULT TIME OVERRIDE ALLOWED?
         BO    RETURN                   YES, RETURN
         MVC   JCTXWRK,BADLINE          ISSUE MESSAGE
         B     CANJOB00                 CANCEL JOB AND RETURN
***********************************************************************
*        JES2 - CHECK FOR '//JOBCAT' AUTHORIZATION
***********************************************************************
JOBCAT00 EQU   *
         TM    JCTUSER0,JCATAUTH        '//JOBCAT' USE ALLOWED?
         BO    RETURN                   YES, RETURN
         MVC   JCTXWRK,BADJCAT          ISSUE MESSAGE
         B     CANJOB00                 YES, RETURN
***********************************************************************
*        JES2 - CHECK FOR '//STEPCAT' AUTHORIZATION
***********************************************************************
STPCAT00 EQU   *
         TM    JCTUSER0,SCATAUTH        '//STEPCAT' USE ALLOWED?
         BO    RETURN                   YES, RETURN
         MVC   JCTXWRK,BADSCAT          ISSUE MESSAGE
***********************************************************************
*        JES2 - CANCEL JOB
***********************************************************************
CANJOB00 EQU   *
         L     R2,4(R1)                 ADDRESS OF EXIT FLAG BYTE
         OI    0(R2),RDWXXSNC           WE'VE SUPPLIED NEXT STMT IMAGE
CANJOB10 EQU   *
         OI    JCTUSER4,BYTEFF          JOB WILL BE CANCELED IN EXIT020
***********************************************************************
*        JES2 - RESTORE REGISTERS AND RETURN
***********************************************************************
RETURN   EQU   *
         $RETURN RC=(R15)               RETURN TO CALLER
***********************************************************************
*        JES2 - FLAGS
***********************************************************************
NADA     EQU   X'00'                    INITIALIZATION VALUE
TIMEAUTH EQU   X'01'                    ALLOWS 'TIME' PARM USE
LINEAUTH EQU   X'04'                    ALLOWS UNLIMITTED LINES SYSOUT
JCATAUTH EQU   X'08'                    ALLOWS USE OF '//JOBCAT' STMT
SCATAUTH EQU   X'10'                    ALLOWS USE OF '//STEPCAT' STMT
BYTEFF   EQU   X'FF'                    BYTE CONSTANT, VALUE X'FF'
CONTFLAG EQU   X'FF'                    CONTINUATION OF EXEC STMT
***********************************************************************
*        JES2 - CONSTANTS
***********************************************************************
FULL00   DC    F'00'                    FULLWORD CONSTANT, VALUE X'00'
COMMENT  DC    C'//* '                  CONSTANT STATEMENT INDICATOR
CONTINUE DC    C'// '                   CONTINUATION STMT INDICATOR
JOBCAT   DC    C'//JOBCAT'              '//JOBCAT' STMT INDICATOR
STEPCAT  DC    C'//STEPCAT'             '//STPCAT' STMT INDICATOR
JOBPARM  DC    C'/*JOBPARM '            'JOBPARM' STMT INDICATOR
BADTIME1 DC    CL80'//*** ERROR ***  YOU ARE NOT AUTHORIZED TO USE THE +
               ''TIME'' PARAMETER ON EXEC STMT'
BADTIME2 DC    CL80'//*** ERROR ***  YOU ARE NOT AUTHORIZED TO USE THE +
               ''TIME'' PARM ON ''/*JOBPARM''   '
BADLINE  DC    CL80'//*** ERROR ***  YOU ARE NOT AUTHORIZED TO USE THE +
               ''LINES'' PARM ON ''/*JOBPARM''  '
BADJCAT  DC    CL80'//*** ERROR ***  YOU ARE NOT AUTHORIZED TO USE THE +
               ''//JOBCAT'' STATEMENT           '
BADSCAT  DC    CL80'//*** ERROR ***  YOU ARE NOT AUTHORIZED TO USE THE +
               ''//STEPCAT'' STATEMENT          '
ERROR    DC    C'//*** ERROR ***'
TIME1    DC    C',TIME='                TIME PARAMETER
TIME2    DC    C',TIME.'                TIME PARAMETER
TIME3    DC    C' TIME='                TIME PARAMETER
TIME4    DC    C' TIME.'                TIME PARAMETER
TIME5    DC    C',T='                   TIME PARAMETER
TIME6    DC    C' T='                   TIME PARAMETER
LINE1    DC    C',LINES='               LINES PARAMETER
LINE2    DC    C' LINES='               LINES PARAMETER
LINE3    DC    C',L='                   LINES PARAMETER
LINE4    DC    C' L='                   LINES PARAMETER
TRAILCOM DC    C', '                    TRAILING COMMA
EXEC     DC    C' EXEC '                EXEC PARAMETER
JOB      DC    C'JOB'                   INDICATES BATCH JOB
         LTORG
         $MODEND
         END  ,
