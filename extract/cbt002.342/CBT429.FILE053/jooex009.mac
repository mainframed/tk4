         TITLE 'DELUXE JES2 JOB OUTPUT OVERFLOW EXIT - PREAMBLE'
**********************************************************************
*                                                                    *
*           DELUXE JES2 JOB OUTPUT OVERFLOW EXIT 9                   *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE HAS THE PURPOSE            *
*       OF EXTENDING THE ALLOWED NUMBER OF OUTPUT RECORDS            *
*       FOR SPECIFIED JOB CLASSES WHEN THE OUTPUT ESTIMATE           *
*       IS EXCEEDED.                                                 *
*                                                                    *
*    ENTRY POINT = DLXXIT09                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0     N/A                                                   *
*       R1     ADDRESS OF THE 7-WORD PARAMETER LIST:                 *
*              +0  JCTLINES OR JCTPUNCH VALUE                        *
*              +4  JCTPAGES VALUE                                    *
*              +8  JCTBYTES VALUE                                    *
*              +12 USER'S INCREMENT FOR RECORDS                      *
*              +16 USER'S INCREMENT FOR PAGES                        *
*              +20 USER'S INCREMENT FOR BYTES                        *
*              +24 OUTPUT OVERFLOW FLAG:                             *
*                  BIT 0   = 0 (CARDS HAVE NOT EXCEEDED THE LIMIT)   *
*                          = 1 (CARDS HAVE EXCEEDED THE ESTIMATE)    *
*                  BIT 1   = 0 (LINES HAVE NOT EXCEEDED THE LIMIT)   *
*                          = 1 (LINES HAVE EXCEEDED THE LIMIT)       *
*                  BIT 2   = 0 (PAGES HAVE NOT EXCEEDED THE LIMIT)   *
*                          = 1 (PAGES HAVE EXCEEDED THE ESTIMATE)    *
*                  BIT 3   = 0 (BYTES HAVE NOT EXCEEDED THE LIMIT)   *
*                          = 1 (BYTES HAVE EXCEEDED THE LIMIT)       *
*                  BITS 4-31 N/A                                     *
*       R2-R6  N/A                                                   *
*       R7     ADDRESS OF THE JCT                                    *
*       R8-R10 N/A                                                   *
*       R11    ADDRESS OF THE SVT                                    *
*       R12    N/A                                                   *
*       R13    OS-STYLE 18 WORD SAVE AREA                            *
*       R14    RETURN ADDRESS                                        *
*       R15    ENTRY ADDRESS                                         *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0    IF THE RETURN CODE (PASSED IN R15) IS NOT 8, THE       *
*             CONTENTS OF THIS REGISTER ARE IGNORED.  HOWEVER,       *
*             IF THE RETURN CODE IS 8, THIS REGISTER CONTAINS        *
*             RETURN PROCESSING FLAGS AS FOLLOWS:                    *
*             BIT 0     = 0 TELLS JES2 TO TAKE ACTION BASED ON THE   *
*                           VALUE CURRENTLY SPECIFIED FOR THE        *
*                           "EXCESSIVE OUTPUT OPTION" AT YOUR        *
*                           INSTALLATION.                            *
*                       = 1 TELLS JES2 TO TAKE ACTION BASED ON THE   *
*                           VALUE SPECIFIED FOR THE "EXCESSIVE       *
*                           OUTPUT OPTION" IN BITS 24-31 FOR THIS    *
*                           REGISTER.                                *
*             BIT 1     = 0 TELLS JES2 TO USE THE OUTPUT OVERFLOW    *
*                           INCREMENT SPECIFIED ON THE INT=          *
*                           PARAMETER OF THE ESTLNCT, ESTPUN,        *
*                           ESTPAGE, OR ESTBYTE INIT STATEMENTS.     *
*                       = 1 TELLS JES2 TO USE THE OUTPUT OVERFLOW    *
*                           INCREMENT SUPPLIED BY THE EXIT ROUTINE   *
*                           IN THE PARAMETER LIST.                   *
*             BIT 2     = 0 TELLS JES2 TO SEND THE DEFAULT ERROR     *
*                           MESSAGE ($HASP375) TO THE OPERATOR.      *
*                       = 1 TELLS JES2 TO SUPPRESS THE DEFAULT       *
*                           ERROR MESSAGE.                           *
*             BITS 3-23     N/A                                      *
*             BITS 24-31    TELLS JES2 THE EXECUTION OPTION FOR      *
*                           THIS EXIT CALL.  IF THIS BYTE IS ZERO    *
*                           THEN CONTINUE PROCESSING.  IF THIS BYTE  *
*                           IS 1 THEN ABEND (722) WITHOUT A DUMP.    *
*                           IF THIS BYTE IS 2 THEN ABEND (722) WITH  *
*                           A DUMP.                                  *
*       R1-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             = 0  TELLS JES2 THAT, IF THERE ARE ANY ADDITIONAL EXIT *
*                  ROUTINES, IT IS TO EXECUTE THE NEXT ONE.  IF      *
*                  THERE ARE NO ADDITIONAL ROUTINES, JES2 IS TO      *
*                  PERFORM STANDARD JOB OUTPUT OVERFLOW PROCESSING.  *
*             = 4  TELLS JES2 TO IGNORE ANY ADDITION EXIT ROUTINES   *
*                  ASSOCIATED WITH THIS EXIT AND TO PERFORM          *
*                  STANDARD JOB OUTPUT OVERFLOW PROCESSING.          *
*             = 8  TELLS JES2 TO TAKE ACTION BASED ON THE RETURN     *
*                  PROCESSING FLAGS IN R0.                           *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $MODEND, $HASPGBL                                   *
*                                                                    *
*    MVS MACROS USED:                                                *
*        GETMAIN, FREEMAIN, SAVE, RESTORE                            *
*                                                                    *
*                                                                    *
*    CHANGE ACTIVITY:                                                *
*        02/06/87  WRITTEN.                           BC.            *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
JOOEX009 $MODULE ENVIRON=USER,                                         +
               $BUFFER,                                                +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $MIT,                    REQ'D BY $MODULE               +
               $JCT,                    HASP JOB CONTROL TABLE MAPPING +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE JES2 JOB OUTPUT OVERFLOW EXIT '
         SPACE 1
DLXXIT09 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         SAVE  (14,12)                  SAVE REGISTERS
         LR    R12,R15                  LOAD LOCAL BASE REG
         LR    R3,R1                    SAVE ADDRESS OF PARAMETER LIST
         GETMAIN R,LV=72                GET SAVEAREA
         ST    R13,4(R1)                STORE ADDR OF PREV SAVE AREA
         ST    R1,8(R13)                STORE ADDR OF THIS SAVE AREA
         LR    R13,R1                   LOAD R13 WITH ADDR OF THIS AREA
         USING JCT,R7                   ADDRESSABILITY TO JCT
         SLR   R15,R15                  SET RC = 0
         SLR   R0,R0                    SET R0 = 0
         TM    24(R3),X'40'             # OF EST. LINES EXCEEDED?
         BNO   CANCEL                   IF NOT, ALLOW IT TO BE CANCELED
         L     R5,NOLIM                 DEFAULT TO NO LIMIT
         CLC   BATJOB,JCTJOBID          IS THIS A BATCH JOB?
         BNE   CONT                     IF NOT, CONTINUE
         L     R5,JCTUSER3              GET # LINES ALLOWED FOR CLASS
CONT     EQU   *
         L     R6,0(R3)                 GET # OF LINES USED SO FAR
         CR    R6,R5                    COMPARE THE TWO
         BNL   CANCEL                   IF EXCEEDED, CANCEL THE JOB
         SR    R5,R6                    CALC. # TO ADD
         ST    R5,12(R3)                STORE IN PARAMETER LIST
         ICM   R0,B'1111',SETBIT1       SET BIT 1 OF R0
RETURN   EQU   *
         LR    R1,R13                   GET ADDR OF SAVE AREA
         L     R13,4(R13)               RESTORE ADDR OF PREV SAVE AREA
         ST    R0,20(R13)               STORE R0
         FREEMAIN R,LV=72,A=(R1)        FREE PROGRAM SAVE AREA
         RETURN (14,12),RC=8            RESTORE REGISTERS & RETURN
CANCEL   EQU   *
         LR    R1,R13                   GET ADDR OF SAVE AREA
         L     R13,4(R13)               RESTORE ADDR OF PREV SAVE AREA
         FREEMAIN R,LV=72,A=(R1)        FREE PROGRAM SAVE AREA
         RETURN (14,12),RC=0            RESTORE REGISTERS & RETURN
********************** CONSTANTS *************************************
NOLIM    DC    F'9999000'               NO LIMIT FOR TSO AND STC
SETBIT1  DC    X'E0000000'
BATJOB   DC    C'JOB'
         LTORG
JOOEX009 CSECT
         $MODEND
         END  ,
