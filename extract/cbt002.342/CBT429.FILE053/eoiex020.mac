         TITLE 'DELUXE END OF INPUT EXIT - PREAMBLE'
**********************************************************************
*                                                                    *
*           DELUXE END OF INPUT EXIT 20                              *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE HAS THE PURPOSE            *
*       OF CHECKING THE JCTUSER4 FIELD FOR THE SETTING OF THE FIRST  *
*       BYTE OF THE FIELD.  IF THE BYTE IS = ZERO THEN THE EXIT      *
*       WILL ALLOW THE JOB TO CONINUE PROCESSING NORMALLY            *
*       WHILE A NON-ZERO VALUE WILL CUASE THE TOB TO BE CANCELED.    *
*                                                                    *
*    ENTRY POINT = DLXXIT20                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0     ZERO                                                  *
*       R1-R9  N/A                                                   *
*       R10    ADDRESS OF THE JCT                                    *
*       R11    ADDRESS OF THE HCT                                    *
*       R12    N/A                                                   *
*       R13    ADDRESS OF THE HASPRDR PCE                            *
*       R14    RETURN ADDRESS                                        *
*       R15    ENTRY ADDRESS                                         *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0-R1  N/A                                                   *
*       R2-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             =0  CONTINUE WITH ADDITIONAL EXITS/HASPRDR PROCESSING  *
*             =4  CONTINUE WITH NORMAL HASPRDR PROCESSING            *
*             =8  CANCEL JOB. PRODUCE OUTPUT.                        *
*             =12 PURGE JOB. NO OUTPUT IS PRODUCED.                  *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $SAVE, $RETURN, $MODEND, $HASPGBL                   *
*                                                                    *
*    CHANGE ACTIVITY:                                                *
*        01/20/87  WRITTEN.                                    JWC.  *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
EOIEX020 $MODULE $BUFFER,                                              +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D FOR MAPPING              +
               $MIT,                    REQ'D BY $MODULE               +
               $PCE,                    HASP PROCESSOR CONTROL ELEMENT +
               $RDRWORK,                HASP INPUT SERVICE PCE WORK    +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE END OF INPUT EXIT'
         SPACE 1
***********************************************************************
*        JES2 - INITIALIZATION AND ADDRESSABILITY
***********************************************************************
DLXXIT20 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         SLR   R15,R15                  CLEAR REG 15
         USING JCT,R10                  ADDRESSABILITY TO JCT
         CLC   JOB,JCTJOBID             THIS A BATCH JOB?
         BNE   RETURN                   NO, RETURN
         OI    JCTUSER0+2,X'04'         EXIT 04 PROCESSING COMPLETE
         CLI   JCTUSER4,X'00'           HAS THE CANCEL FLAG BEEN SET?
         BE    RETURN                   NO ALLOW PROCESSING TO CONTINUE
         B     CANJOB00                 ELSE SET TO CANCEL, NO PURGE
***********************************************************************
*        JES2 - SET RETURN CODE 8 TO CANCEL BUT NOT PURGE JOB
***********************************************************************
CANJOB00 EQU   *
         CLC   JCTUSEID+3(1),=C'R'      REGIONAL JOB?
         BNE   CANJOB20                 NO, CONTINUE
         LA    R2,NODETAB               NODE TRANSLATION TABLE
CANJOB10 EQU   *
         CLI   0(R2),C' '               END OF TABLE?
         BE    CANJOB30                 YES, USE DEST=FETCH
         CLC   0(1,R2),JCTUSEID+4       THIS NODE DEFINED?
         BE    CANJOB30                 YES, EXTRACT DEST FOR JCT
         LA    R2,5(R2)                 POINT AT NEXT ENTRY
         B     CANJOB10                 CONTINUE SEARCH
CANJOB20 EQU   *
         LA    R2,DFLTNODE              DEST=FETCH
CANJOB30 EQU   *
         MVC   JCTPROUT(4),1(R2)        UPDATE JCT
         LA    R15,8                    SET RETURN CODE 8
         B     RETURN                   GET OUT
***********************************************************************
*        JES2 - RESTORE REGISTERS AND RETURN
***********************************************************************
RETURN   EQU   *
         $RETURN RC=(R15)               RETURN TO CALLER
JOB      DC    C'JOB'                   JCTJOBID FOR BATCH JOB
**********************************************************************
*        JES2 - REGIONAL NODE TRANSLATION TABLE
**********************************************************************
NODETAB  EQU   *
         DC    CL1'1',X'000B0000'
         DC    CL1'2',X'000C0000'
         DC    CL1'3',X'000D0000'
         DC    CL1'4',X'000E0000'
         DC    CL1'5',X'000F0000'
         DC    CL1'6',X'00100000'
         DC    CL1'7',X'00110000'
         DC    CL1'8',X'00120000'
         DC    CL1'P',X'0001003B'
DFLTNODE DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         DC    CL1' ',X'00010096'
         $MODEND
         END  ,
