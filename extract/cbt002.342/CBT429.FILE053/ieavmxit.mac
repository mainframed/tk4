         TITLE 'IEAVMXIT - GENERAL USER WTO EXIT'
***********************************************************************
*                                                                     *
*              MODULE NAME = IEAVMXIT                                 *
*                                                                     *
*              DESCRIPTIVE NAME = GENERAL USER WTO EXIT               *
*                                                                     *
*              FUNCTION =                                             *
*                EXIT RESPONDS TO THE FOLLOWING CICS MESSAGES:        *
*                DFH4512, DFH4513, DFH4581, DFH4583 AND DFH4585       *
*                                                                     *
*              OPERATION =                                            *
*                 SAVE REGISTERS AND ESTABLISH ADDRESSABILITY         *
*                 IF MESSAGE ID IS NOT 'DFH4512', 'DFH4513',          *
*                 'DFH4581', 'DFH4583' OR 'DFH4585'                   *
*                     RESTORE REGISTERS                               *
*                     EXIT                                            *
*                 IF MESSAGE ID IS 'DFH4512', 'DFH4513',              *
*                 'DFH4581', 'DFH4583' OR 'DFH4585'                   *
*                     GETMAIN DYNAMIC STORAGE                         *
*                     GET JOBNAME FROM TIOT (TASK INPUT/OUTPUT TABLE) *
*                     IF MESSAGE ID IS 'DFH4512' OR 'DFH4513':        *
*                         ISSUE 'F CICSX,CEMT P SHUT' COMMAND FOR     *
*                             THE APPROPRIATE CICS                    *
*                         ISSUE THE FOLLOWING WTO:                    *
*                             'CICSX SYSTEM LOG / JOURNAL IS          *
*                             NO LONGER AVAILABLE. NOTIFY SYSTEMS     *
*                             PROGRAMMING BEFORE ATTEMPTING TO        *
*                             RESTART CICSX'                          *
*                     IF MESSAGE ID IS 'DFH4581', 'DFH4583',          *
*                         OR 'DFH4585':                               *
*                         SCAN MESSAGE TEXT TO DETERMINE WHICH        *
*                             DDNAME NEEDS TO BE DUMPED               *
*                         FOR MESSAGE 'DFH4583', PARM WILL ALSO       *
*                             CONTAIN THE APPROPRIATE REPLY ID (A     *
*                             REPLY OF 'GO' WILL BE ISSUED FROM       *
*                             CICSXLOG)                               *
*                         ISSUE 'S CICSXLOG,CICSDD=DFHJXXX,           *
*                             CICSREP=XX'.                            *
*                     FREEMAIN DYNAMIC STORAGE                        *
*                     RESTORE REGISTERS                               *
*                     EXIT                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                    REGISTER  1      = PARAMETER LIST POINTER        *
*                    REGISTER  2      = PARAMETER LIST WORK POINTER   *
*                    REGISTERS 3 - 10 = WORK REGISTERS                *
*                    REGISTER  11     = BRANCH ADDRESS FROM GETMAIN   *
*                    REGISTER  12     = BASE REGISTER                 *
*                    REGISTER  13     = SAVE AREA POINTER             *
*                    REGISTER  14     = RETURN                        *
*                    REGISTER  15     = ENTRY POINT ADDRESS AND       *
*                                       RETURN CODE REGISTER          *
*                                                                     *
*              MACROS =                                               *
*                SAVE, RETURN, EQUATE, GETMAIN, FREEMAIN,             *
*                MGCR, WTO                                            *
*                                                                     *
*              MAPPING MACROS USED =                                  *
*                IEZVX100, IEZMGCR, IHAPSA, IKJTCB, IEFTIOT1          *
*                                                                     *
*              ASSEMBLER OPTIONS = LIST,RENT                          *
*                                                                     *
*              LINKAGE EDITOR OPTIONS = LET,LIST,XREF,RENT,REUS,      *
*                                       REFR,AC=1                     *
*                                                                     *
*              LOAD LIBRARY = SYS1.LINKLIB (APF AUTHORIZED LIBRARY)   *
*                                                                     *
*                                                                     *
*              CHANGE ACTIVITY =                                      *
*                09/12/85  WRITTEN.    JWC.                           *
*                                                                     *
*                11/20/85  ADAPTED FOR USE WITH THE FOLLOWING CICS    *
*                          MESSAGES ONLY: DFH4512, DFH4513, DFH4581,  *
*                          DFH4583 AND DFH4585.                       *
*                                      JWC.                           *
*                                                                     *
*                07/29/86  WILL REQUEST 'DLXCICLG' TO USE 'IEBGENER'  *
*                          TO DUMP JOURNAL IF JOURNAL IS OTHER THAN   *
*                          DFHJ01.                                    *
*                                      JWC.                           *
*                                                                     *
*                08/21/86  WILL NOT DUMP JOURNAL ON DFH4585 MESSAGE   *
*                          (SHUTDOWN) IF JOURNAL IS OTHER THAN DFHJ01.*
*                                      JWC.                           *
*                                                                     *
*                09/12/86  UPDATED TO WORK WITH CICS 1.7 AS WELL      *
*                          AS CICS 1.6.                               *
*                                      JWC.                           *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        INITIALIZATION
***********************************************************************
IEAVMXIT CSECT                         GENERAL USER WTO EXIT
IEAVMXIT AMODE 31                      31-BIT ADDRESSING MODE
IEAVMXIT RMODE ANY                     31-BIT ADDRESSING MODE
         SAVE  (14,12),,IEAVMXIT_DELUXE_CHECK_&SYSDATE_&SYSTIME
*                                      SAVE REGISTERS
         LR    R12,R15                 R12 - BASE REGISTER
         USING IEAVMXIT,R12            ESTABLISH PGM ADDRESSABILITY
         L     R2,0(,R1)               PRESERVE PTR TO PARMS
         USING CTXT,R2                 ESTABLISH ADDRESSABILITY TO
*                                      COMMUNICATIONS TASK EXIT
*                                      PARAMETER LIST DSECT (CTXT)
***********************************************************************
*        MESSAGE ID CHECKING
***********************************************************************
CHKMSGID DS    0H
         L     R3,CTXTTXPJ             R3 - ADDRESS OF MESSAGE TEXT
         USING CTXTATTR,R3             ESTABLISH ADDRESSABILITY TO
*                                      COMMUNICATIONS TASK EXIT
*                                      MESSAGE ATTRIBUTES
         LA    R4,CTXTTMSG             R4 - ADDRESS OF TEXT AREA
         USING MSGTEXT,R4              ESTABLISH ADDRESSABILITY TO
*                                      TEXT OF MESSAGE
         LA    R11,MDFH4512            R11 - BRANCH ADDR AFTER GETMAIN
         CLC   MSGID,DFH4512           DFH4512 MESSAGE?
         BE    GETMAIN                 GETMAIN DYNAMIC STORAGE
         CLC   MSGID,DFH4513           DFH4513 MESSAGE?
         BE    GETMAIN                 GETMAIN DYNAMIC STORAGE
         LA    R11,MDFH4581            R11 - BRANCH ADDR AFTER GETMAIN
         CLC   MSGID,DFH4581           DFH4581 MESSAGE?
         BE    GETMAIN                 GETMAIN DYNAMIC STORAGE
         CLC   MSGID,DFH4583           DFH4583 MESSAGE?
         BE    GETMAIN                 GETMAIN DYNAMIC STORAGE
         CLC   MSGID,DFH4585           DFH4585 MESSAGE?
         BNE   FINISH                  NO, FINISH WITH NO GET/FREEMAIN
         EJECT
***********************************************************************
*        GETMAIN DYNAMIC STORAGE
*            BELOW 16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR
*            PARAMETER LIST TO BE IN 24-BIT ADDRESSABLE STORAGE
***********************************************************************
GETMAIN  DS    0H
         GETMAIN RU,LV=WORKLEN,SP=SUBPOOL,LOC=BELOW
*                                      GETMAIN DYNAMIC STORAGE
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(R13)               STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*        EXTRACT JOBNAME FROM TIOT (TASK INPUT/OUTPUT TABLE)
***********************************************************************
EXTRJOB  DS    0H
         LA    R6,0                    R6 - ADDRESS OF THE PSA
         USING PSA,R6                  ESTABLISH ADDRESSABILITY TO PSA
         L     R6,PSATOLD              R6 - ADDRESS OF CALLER'S TCB
         USING TCB,R6                  ESTABLISH ADDRESSABILITY TO TCB
         L     R6,TCBTIO               R6 - ADDRESS OF THE TIOT
         USING TIOT1,R6                ESTABLISH ADDRESSABILITY TO TIOT
         MVC   JOBNAME,TIOCNJOB        MOVE JOBNAME TO DYNAMIC WORK
         DROP  R6                      DROP ADDRESSABILITY TO TIOT
         BR    R11                     RETURN
         EJECT
***********************************************************************
*        REPLYING 'F CICSX,CEMT P SHUT' AFTER 'DFH4512' & 'DFH4513'
***********************************************************************
MDFH4512 DS    0H
         OI    CTXTRFB2,CTXTRHCO       SUPPRESS THE MSG (HARDCOPY ONLY)
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'          SET FLAG1 TO INDICATE THAT FLAG2
*                                      CONTAINS VALID INFORMATION.
*                                      FLAG2 = X'0000' (NO USER TOKEN).
         MVC   MGCRTEXT(L'CMDTXT1),CMDTXT1    MOVE COMMAND TEXT
         MVC   CICSJOB,JOBNAME         INDICATE APPROPRIATE CICS
         LA    R1,(MGCRTEXT-MGCRPL)+L'CMDTXT1    GET MGCRPL LENGTH
         STC   R1,MGCRLGTH             SAVE LENGTH IN MGCRPL
         SR    R0,R0                   CLEAR REG 0
         MGCR  MGCRPL                  ISSUE THE COMMAND
         MVC   WTOLIST_(LWTOLST1),WTOLIST1    MOVE WTO LIST FORM FROM
*                                      STATIC TO DYNAMIC
         MVC   WTOLIST_+4(5),JOBNAME   INDICATE APPROPRIATE CICS
         MVC   WTOLIST_+111(5),JOBNAME    INDICATE APPROPRIATE CICS
         WTO   MF=(E,WTOLIST_)         ISSUE THE MESSAGE
         B     FREEMAIN                FREE DYNAMIC STORAGE & RETURN
         EJECT
***********************************************************************
*        REPLYING
* 'S CICSXLOG,CICSDD=DFHJXXX,CICSREP=XX,REPLY=XXXX'
*                                 OR
* 'S CICSXLOG,CICSDD=DFHJXXX,LOGPGM=IEBGENER,SYSINDSN=DUMMY,CICSREP=XX,
*     REPLY=XXXX'
*                                 OR
* 'S CICSXLOG,REPONLY=YES,CICSREP=XX,REPLY=XXXX'
*        AFTER DFH4581, DFH4583 AND DFH4585
***********************************************************************
MDFH4581 DS    0H
         MVC   REPLID,NULLREPL         INITIALIZE MESSAGE REPLY-ID
         MVC   REPLY,GO                INITIALIZE MESSAGE REPLY
         CLC   MSGID,DFH4583           DO WE NEED TO REPLY?
         BNE   EXTRACT                 NO, EXTRACT DDNAME
         MVC   REPLID,CTXTRPID         YES, INSERT REPLY ID
EXTRACT  DS    0H
         SR    R6,R6                   CLEAR REGISTER 6
         LH    R6,CTXTTLEN             R6 - MAXIMUM TEXT LENGTH
         LR    R5,R4                   R5 - PRESEARVE PTR TO MSGTEXT
EXTLOOP0 DS    0H
         CLC   0(4,R5),DFHJ            FOUND DDNAME?
         BE    ENDLOOP0                YES, MOVE TO DYNAMIC
         LA    R5,1(R5)                NO, INCREMENT INDEX
         BCT   R6,EXTLOOP0             LOOK AGAIN
         MVC   WTOLIST_(LWTOLST2),WTOLIST2    MOVE WTO LIST FORM FROM
*                                      STATIC TO DYNAMIC
         MVC   WTOLIST_+19(5),JOBNAME  INDICATE APPROPRIATE CICS
         MVC   WTOLIST_+114(2),REPLID  INDICATE MESSAGE REPLY-ID
         WTO   MF=(E,WTOLIST_)         ISSUE THE MESSAGE (NO DDNAME)
         B     FREEMAIN                RETURN
ENDLOOP0 DS    0H
         LA    R8,DDNAME               R8 - INDEX TO DYNAMIC DDNAME
         LA    R7,8                    R7 - MAX LENGTH OF DDNAME+1
EXTLOOP1 DS    0H
         CLC   0(1,R5),BLANK           END OF DDNAME?
         BE    ENDLOOP1                YES, EXTRACT REPLY IF NEEDED
         CLC   0(1,R5),RPAREN          END OF DDNAME?
         BE    ENDLOOP1                YES, EXTRACT REPLY IF NEEDED
         MVC   0(1,R8),0(R5)           DDNAME TO DYNAMIC, 1 CHR / TIME
         LA    R5,1(R5)                INCREMENT MSG TEXT INDEX (INPUT)
         LA    R8,1(R8)                INCREMENT DYNAMIC INDEX (OUTPUT)
         BCTR  R6,0                    DECREMENT R6
         BCT   R7,EXTLOOP1             GET NEXT CHARACTER
         MVC   WTOLIST_(LWTOLST2),WTOLIST2    MOVE WTO LIST FORM FROM
*                                      STATIC TO DYNAMIC
         MVC   WTOLIST_+19(5),JOBNAME  INDICATE APPROPRIATE CICS
         MVC   WTOLIST_+114(2),REPLID  INDICATE MESSAGE REPLY-ID
         WTO   MF=(E,WTOLIST_)         ISSUE THE MESSAGE (NO DDNAME)
         B     FREEMAIN                RETURN
ENDLOOP1 DS    0H
         LA    R5,1(R5)                INCREMENT MSG TEXT INDEX (INPUT)
         BCTR  R6,0                    DECREMENT R6
EXTLOOP2 DS    0H
         CLC   0(2,R5),REPLYIND        REPLY FOUND?
         BE    ENDLOOP2                YES, EXTRACT REPLY
         LA    R5,1(R5)                INCREMENT MSG TEXT INDEX (INPUT)
         BCT   R6,EXTLOOP2             CONTINUE SEARCH
         B     CMD0                    USE DEFAULT REPLY (GO)
ENDLOOP2 DS    0H
         MVC   REPLY,1(R5)             EXTRACT REPLY
CMD0     DS    0H
         OI    CTXTRFB2,CTXTRHCO       SUPPRESS THE MSG (HARDCOPY ONLY)
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'          SET FLAG1 TO INDICATE THAT FLAG2
*                                      CONTAINS VALID INFORMATION.
*                                      FLAG2 = X'0000' (NO USER TOKEN).
         CLC   DFHJ01,DDNAME           IEBGENER OR DFSULTR0?
         BE    CMD2                    DFSULTR0
         CLC   MSGID,DFH4585           REPONLY=YES?
         BE    CMD4                    YES REPLY ONLY
CMD3     DS    0H
         MVC   MGCRTEXT(L'CMDTXT3),CMDTXT3    MOVE CMD TEXT SKELETON
         MVC   CICSLOGB,CICSID         INDICATE WHICH CICSLOG TO START
         MVC   CICSDDB,DDNAME          DDNAME FROM STATIC TO DYNAMIC
         MVC   CICSIDB,REPLID          REPLY-ID FROM STATIC TO DYNAMIC
         MVC   CICSREPB,REPLY          REPLY FROM STATIC TO DYNAMIC
         LA    R1,(MGCRTEXT-MGCRPL)+L'CMDTXT3    GET MGCRPL LENGTH
         STC   R1,MGCRLGTH             SAVE LENGTH IN MGCRPL
         B     MGCR                    ISSUE COMMAND
CMD2     DS    0H
         MVC   MGCRTEXT(L'CMDTXT2),CMDTXT2    MOVE CMD TEXT SKELETON
         MVC   CICSLOGA,CICSID         INDICATE WHICH CICSLOG TO START
         MVC   CICSDDA,DDNAME          DDNAME FROM STATIC TO DYNAMIC
         MVC   CICSIDA,REPLID          REPLY-ID FROM STATIC TO DYNAMIC
         MVC   CICSREPA,REPLY          REPLY FROM STATIC TO DYNAMIC
         LA    R1,(MGCRTEXT-MGCRPL)+L'CMDTXT2    GET MGCRPL LENGTH
         STC   R1,MGCRLGTH             SAVE LENGTH IN MGCRPL
         B     MGCR                    ISSUE COMMAND
CMD4     DS    0H
         MVC   MGCRTEXT(L'CMDTXT4),CMDTXT4    MOVE CMD TEXT SKELETON
         MVC   CICSLOGC,CICSID         INDICATE WHICH CICSLOG TO START
         MVC   CICSIDC,REPLID          REPLY-ID FROM STATIC TO DYNAMIC
         MVC   CICSREPC,REPLY          REPLY FROM STATIC TO DYNAMIC
         LA    R1,(MGCRTEXT-MGCRPL)+L'CMDTXT4    GET MGCRPL LENGTH
         STC   R1,MGCRLGTH             SAVE LENGTH IN MGCRPL
MGCR     DS    0H
         SR    R0,R0                   CLEAR REG 0
         MGCR  MGCRPL                  ISSUE THE COMMAND
         EJECT
***********************************************************************
*        FREEMAIN DYNAMIC STORAGE
*            BELOW 16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR
*            PARAMETER LIST TO BE IN 24-BIT ADDRESSABLE STORAGE
***********************************************************************
FREEMAIN DS    0H
         LR    R5,R13                  R5 - ADDRESS OF DYNAMIC WORKAREA
         L     R13,4(R13)              RESTORE REGISTER 13
         FREEMAIN RU,LV=WORKLEN,A=(R5),SP=SUBPOOL
*                                      FREEMAIN DYNAMIC STORAGE
***********************************************************************
*        WRAP THINGS UP AND RETURN
***********************************************************************
FINISH   DS    0H
         LM    R14,R12,12(R13)         RESTORE CALLER'S REGISTERS
         BR    R14                     RETURN
***********************************************************************
*        CONSTANTS
***********************************************************************
DFHJ     DC    CL4'DFHJ'               DDNAME INDICATOR / JOURNAL FILE
BLANK    DC    CL1' '                  END OF DDNAME INDICATOR
NULLREPL DC    CL2'XX'                 NULL REPLY-ID
GO       DC    CL4'GO'                 DEFAULT REPLY
REPLYIND DC    CL2'''Y'                INDICATES REPLY TEXT (YNNX)
RPAREN   DC    CL1')'                  END OF DDNAME INDICATOR
DFHJ01   DC    C'DFHJ01'               JOURNAL USES DFSULTR0 TO LOG
DFH4512  DC    C'DFH4512 '             MESSAGE DFH4512
DFH4513  DC    C'DFH4513 '             MESSAGE DFH4513
DFH4581  DC    C'DFH4581 '             MESSAGE DFH4581
DFH4583  DC    C'DFH4583 '             MESSAGE DFH4583
DFH4585  DC    C'DFH4585 '             MESSAGE DFH4585
CMDTXT1  DC    C'F CICSX,CEMT P SHUT,I'  CMD TEXT FOR DFH4512 & DFH4513
CMDTXT2  DC    C'S CICSXLOG,CICSDD=DFHJXXX,CICSREP=XX,REPLY=XXXX'
CMDTXT3  DC    C'S CICSXLOG,CICSDD=DFHJXXX,LOGPGM=IEBGENER,SYSINDS=DUMM+
               Y,CICSREP=XX,REPLY=XXXX'
CMDTXT4  DC    C'S CICSXLOG,REPONLY=YES,CICSREP=XX,REPLY=XXXX'
*                                      CMD TEXT DFH4581,DFH4583,DFH4585
         EJECT
***********************************************************************
*        LIST FORM OF WTO
***********************************************************************
WTOLIST1 WTO   'CICSX SYSTEM LOG / JOURNAL IS NO LONGER AVAILABLE. NOTI+
               FY SYSTEMS PROGRAMMING BEFORE ATTEMPTING TO RESTART CICS+
               X',ROUTCDE=(01),DESC=(02),MF=L
LWTOLST1 EQU   *-WTOLIST1
WTOLIST2 WTO   'UNABLE TO DUMP CICSX JOURNAL FILE. DDNAME UNKNOWN. NOTI+
               FY SYSTEMS PROGRAMMING BEFORE REPLYING TO MESSAGE ID - X+
               X',ROUTCDE=(01),DESC=(02),MF=L
LWTOLST2 EQU   *-WTOLIST2
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         EQUATE
SUBPOOL  EQU   230                     SUBPOOL FOR GET/FREEMAINS
         LTORG
         EJECT
***********************************************************************
*        DYNAMIC STORAGE AREA
***********************************************************************
WORKAREA DSECT
         DS    0F
SAVEAREA DS    18F                     IEAVMXIT SAVE AREA
         DS    0F
         EJECT
MGCR     IEZMGCR DSECT=NO
         EJECT
         ORG   MGCRTEXT
TXT4512  DS    0CL19                   CMD TEXT FOR DFH4512 & DFH4513
         DS    CL2
CICSJOB  DS    CL5
         DS    CL12
         ORG   MGCRTEXT
TXT4581A DS    0CL47                   CMD TEXT DFH4581,DFH4583,DFH4585
         DS    CL6
CICSLOGA DS    CL1                     CICSLOG JOB IDENTIFIER
         DS    CL11
CICSDDA  DS    CL7                     CICS JOURNAL DDNAME (DFHJXXX)
         DS    CL9
CICSIDA  DS    CL2                     REPLY-ID (FOR DFH4583 ONLY)
         DS    CL7
CICSREPA DS    CL4                     REPLY (FOR DFH4583 ONLY)
         ORG   MGCRTEXT
TXT4581B DS    0CL77                   CMD TEXT DFH4581,DFH4583,DFH4585
         DS    CL6
CICSLOGB DS    CL1                     CICSLOG JOB IDENTIFIER
         DS    CL11
CICSDDB  DS    CL7                     CICS JOURNAL DDNAME (DFHJXXX)
         DS    CL39
CICSIDB DS     CL2                     REPLY-ID (FOR DFH4583 ONLY)
         DS    CL7
CICSREPB DS    CL4                     REPLY (FOR DFH4583 ONLY)
         ORG   MGCRTEXT
TXT4581C DS    0CL44                   CMD TEXT DFH4581,DFH4583,DFH4585
         DS    CL6
CICSLOGC DS    CL1                     CICSLOG JOB IDENTIFIER
         DS    CL24
CICSIDC DS     CL2                     REPLY-ID (FOR DFH4583 ONLY)
         DS    CL7
CICSREPC DS    CL4                     REPLY (FOR DFH4583 ONLY)
         ORG
REPLID   DS    CL2                     REPLY-ID WORK AREA
REPLY    DS    CL4                     REPLY WORK AREA
JOBNAME  DS    0CL8                    CICS JOB NAME WORK AREA
         DS    CL4
CICSID   DS    CL1                     CICS IDENTIFIER (A,B,T,ETC.)
         DS    CL3
DDNAME   DS    CL7                     CICS JOURNAL DDNAME WORK AREA
WTOLIST_ DS    CL(LWTOLST1)            DYNAMIC LIST FORM OF WTO MACRO
WORKLEN  EQU   *-WORKAREA
***********************************************************************
*        MAPPING OF MESSAGE TEXT
***********************************************************************
MSGTEXT  DSECT
MSGID    DS    CL8                     MESSAGE ID
JOBDATA  DS    CL8                     JOBNAME
         EJECT
***********************************************************************
*        IEAVMXIT COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)
***********************************************************************
         IEZVX100
         EJECT
IEAVMXIT CSECT                         GENERAL USER WTO EXIT
***********************************************************************
*        PREFIXED SAVE AREA DSECT
***********************************************************************
         IHAPSA DSECT=YES
         EJECT
IEAVMXIT CSECT                         GENERAL USER WTO EXIT
***********************************************************************
*        TASK CONTROL BLOCK DSECT
***********************************************************************
         IKJTCB DSECT=YES
         EJECT
IEAVMXIT CSECT                         GENERAL USER WTO EXIT
***********************************************************************
*        TASK INPUT/OUTPUT TABLE DSECT
***********************************************************************
TIOT     DSECT
         IEFTIOT1
         EJECT
IEAVMXIT CSECT                         GENERAL USER WTO EXIT
         END   IEAVMXIT
