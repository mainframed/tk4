         TITLE 'DELUXE CHECK PRINTERS INC. SUBMIT EXIT - IKJEFF10'
*
*  ATTRIBUTES:  RENT REUS REFR
*
IKJEFF10 CSECT
         SAVE  (14,12),,IKJEFF10_DELUXE_SUBMIT_EXIT_&SYSDATE_&SYSTIME
         LR    R12,R15                  ESTABLISH R11 AS BASE REG
         USING IKJEFF10,R12             TELL ASSEMBLER
         L     R11,0(,R1)               ADDRESS OF 8 WORD PLIST
         USING IEDSECTD,R11             TELL ASSEMBLER
         L     R9,IESUBCTP              ADDRESS OF CONTROL SWITCHES
         USING IESUBCTD,R9              TELL ASSEMBLER
         GETMAIN R,LV=72,SP=0           GET A SAVEAREA
         ST    R13,4(R1)                SAVE OS SAVE AREA ADDRESS
         ST    R1,8(R13)                STORE OUR SAVEAREA ADDR IN OS
         LR    R13,R1                   POINT R13 AT OUR SAVE AREA
* END OF PROLOUGE                       SAVE AREA
         L     R10,IEEXITWD             LOAD WORKAREA ADDRESS
         LTR   R10,R10                  CHECK TO SEE IF IT EXISTS
         BNZ   GOTWORK1                 IT DOES SO SKIP GETMAIN
         GETMAIN R,SP=230,LV=ENDAREA1-WRKAREA1
         ST    R1,IEEXITWD              SAVE ADDRESS OF GOTTEN AREA
         LR    R10,R1                   ADDRESS TO REGISTER 10
         USING WRKAREA1,R10             ADDRESSABILITY TO WORKAREA
         MVI   CRDUSER,X'40'            INITIALIZE
         MVC   CRDUSER+1(7),CRDUSER     TO BLANKS
         XC    CARDPASS(4),CARDPASS     TO HEX ZEROS
GOTWORK1 EQU   *
         CLI   CRDUSER,C' '             USERID FOUND YET?
         BNE   PASSROUT                 YES GO CHECK FOR PASSWORD
USERROUT EQU   *
         L     R1,IECARDP               R1 PTS TO COL 1 OF CARD
         LA    R7,65(R1)                R7 PTS TO COL 66 OF CARD
         LA    R1,3(R1)                 R1 PTS TO COL 4 OF CARD
USERCHK  EQU   *
         LR    R4,R7                    R4 HAS ADDRESS OF COL 66
         SR    R4,R1                    R4 HAS NO OF COLS TO WORK WITH
         EX    R4,TRTINSTU              EXEC TRT, R4 HAS LENGTH
         BZ    RETURN1                  'U' NOT FOUND IN CARD
         CLC   0(5,R1),=C'USER='        R1 PTS TO LAST BYTE TRANSLATED
         BE    LOADUSER                 GO SAVE USERID
         CR    R7,R1                    HOW MANY COLS HAVE BEEN TRTD?
         BE    RETURN1                  USER NOT FOUND ON CARD
         LA    R1,1(R1)                 R1 POINTS TO NEXT COL RIGHT
         B     USERCHK                  TRY AGAIN
LOADUSER EQU   *                        LOAD USER INTO USER SAVE AREA
         LA    R1,5(R1)                 BUMP PAST USER=
         LR    R5,R1                    SAVE R1 FOR MOVE
         L     R7,IECARDP               GET ADDRESS OF LAST POSSIBLE
         LA    R7,70(R7)                CHARACTER IN R7
         LA    R4,0                     R4 IS LENGTH OF MOVE
LOADUSE1 EQU   *                        COUNT LOOP
         CLI   0(R1),C','               END OF USERID
         BE    LOADUSE2                 GO MOVE USERID
         CLI   0(R1),C' '               END OF USERID
         BE    LOADUSE2                 GO MOVE USERID
         CR    R7,R1                    END OF CARD
         BE    LOADUSE2                 GO MOVE USERID
         LA    R1,1(R1)                 BUMP R1
         LA    R4,1(R4)                 BUMP R4
         B     LOADUSE1                 CONTINUE
LOADUSE2 EQU   *                        MOVE USERID
         BCTR  R4,R0                    DECREMENT FOR EXECUTE
         EX    R4,MOVEUSER              EXECUTE MOVE LENGTH IN R4
PASSROUT EQU   *
         CLC   CARDPASS,=XL4'00000000'  CHECK TO SEE IF ALREADY
         BNE   RETURN1                  FOUND, IF SO WE ARE DONE
         L     R1,IEUSRIDP              ADDRESS OF USERID
         CLC   CRDUSER,=CL8'SYSPGM'     SPECIAL USERID?
         BNE   NOTSPEC                  NO CONTINUE
         CLC   0(6,R1),=C'T27611'       MIKE LOOS
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T53031'       MARTY KUNTZ
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T53043'       NANCY HAGSTROM
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T53039'       JIM TRETTER
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T60439'       RICK ANDERSON
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T04108'       RAY STOECKL
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T56714'       BARB COUTURE
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T68875'       MARK CARON
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T68878'       LOIS HOLLINGSWORTH
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T79157'       JOHN CARLSON
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T79186'       SUE RIGGI
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T53007'       MIKE REICHERT
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T53090'       ADY WICKSTROM
         BE    MOVEUIDS
         CLC   0(6,R1),=C'T68839'       CRAIG SPATAFORE
         BE    MOVEUIDS
         B     NOTSPEC
MOVEUIDS EQU   *
         MVC   CRDUSER,0(R1)
         MVC   0(6,R5),0(R1)
NOTSPEC  EQU   *
         CLC   CRDUSER,0(R1)            CHECK IF USER= LOGGED ON USERID
         BNE   RETURN1                  IF NOT PASS AS IS
         L     R1,IECARDP               R1 PTS TO COL 1 OF CARD
         LA    R7,61(R1)                R7 PTS TO COL 62 OF CARD
         LA    R1,3(R1)                 R1 PTS TO COL 4 OF CARD
PASSCHK  EQU   *
         LR    R4,R7                    R4 HAS ADDRESS OF COL 66
         SR    R4,R1                    R4 HAS NO OF COLS TO WORK WITH
         EX    R4,TRTINSTP              EXEC TRT, R4 HAS LENGTH
         BZ    RETURN1                  'P' NOT FOUND IN CARD
         CLC   0(9,R1),=C'PASSWORD='    R1 PTS TO LAST BYTE TRANSLATED
         BE    LOADPASS                 GO SAVE PASSWORD
         CR    R7,R1                    HOW MANY COLS HAVE BEEN TRTD?
         BE    RETURN1                  PW NOT FOUND
         LA    R1,1(R1)                 R1 POINTS TO NEXT COL RIGHT
         B     PASSCHK                  TRY AGAIN
LOADPASS EQU   *                        LOAD USER INTO USER SAVE AREA
         LA    R1,9(R1)                 BUMP PASS PASSWORD=
         ST    R1,CARDPASS              SAVE R1 FOR MOVE
         L     R7,IECARDP               GET ADDRESS OF LAST POSSIBLE
         LA    R7,70(R7)                CHARACTER IN R7
         LA    R4,0                     R4 IS LENGTH OF MOVE
LOADPAS1 EQU   *                        COUNT LOOP
         CLI   0(R1),C','               END OF PASSWORD
         BE    LOADPAS2                 GO MOVE PASSWORD
         CLI   0(R1),C' '               END OF PASSWORD
         BE    LOADPAS2                 GO MOVE PASSWORD
         CR    R7,R1                    END OF CARD
         BE    LOADPAS2                 GO MOVE PASSWORD
         LA    R1,1(R1)                 BUMP R1
         LA    R4,1(R4)                 BUMP R4
         B     LOADPAS1                 CONTINUE
LOADPAS2 EQU   *                        MOVE PASSWORD
         BCTR  R4,R0                    CARD PASSWORD LENGTH FOR EX
         L     R1,CARDPASS              GET ADDRESS OF PASSWORD
         EX    R4,PASSQUES              CHECK FOR QUESTION MARKS
         BNE   RETURN1
         L     R3,16                    ADDRESS OF CVT
         L     R3,0(R3)                 ADDRESS OF RCB/ASCB ADDRESSES
         L     R3,12(R3)                ADDRESS OF CURRENT ASCB
         L     R3,60(R3)                ADDRESS OF TSB
*                                       TSB IS IN FETCH PROTECTED
*                                       STORAGE SO WE NEED TO
*                                       DO A QUICK MODESET TO ACCESS IT
         MODESET EXTKEY=ZERO,SAVEKEY=(2)  GO TO KEY ZERO
         LA    R3,88(R3)                ADDRESS OF TSBPSWD
         EX    R4,PASSMOVE              MOVE TO THE CARD
         MODESET KEYADDR=(2)            RETURN TO SAVED KEY
* BEGINNING OF EPILOUGE
RETURN1  EQU   *                        RETURN SECTION
         SLR   R15,R15                  START WITH RETURN CODE 0
         TM    IESTMTYP,IESSCON         CHECK TO SEE IF STATEMENT
*                                       IS TO BE CONTINUED
         BO    RETURNX                  IF SO CONTINUE ELSE
*                                       ELSE WE ARE DONE
RETURN2  EQU   *                        FREE WORKAREA
         SLR   R1,R1                    ZERO REGISTER 1
         ST    R1,IEEXITWD              ZERO ADDRESS OF GOTTEN AREA
         LR    R1,R10                   POINT R1 AT ADDRESS OF WORKAREA
         FREEMAIN R,A=(1),SP=230,LV=ENDAREA1-WRKAREA1
RETURNX  EQU   *                        FREE SAVEAREA AND GET OUT
         LR    R1,R13                   POINT R1 AT OUR SAVEAREA
         L     R13,4(R13)               LOAD OS SAVE AREA ADDR TO R13
         FREEMAIN R,LV=72,A=(1),SP=0    FREE SAVEAREA
         RETURN (14,12),RC=(15)         RETURN TO OS RC IN R15
* END OF EPILOUGE
TRTINSTP TRT   0(0,R1),TRANTBLP         TRT INST IS USED VIA EX INST
TRTINSTU TRT   0(0,R1),TRANTBLU         TRT INST IS USED VIA EX INST
MOVEUSER MVC   CRDUSER(0),0(R5)         MVC INST USED TO MOVE USERID
PASSQUES CLC   0(0,R1),QUESMARK
PASSMOVE MVC   0(0,R1),0(R3)            MVC INST USED TO MOVE PASSWD
QUESMARK DC    CL8'????????'            FOR TEST
TRANTBLP DS    0CL256                   FIND EQUALS SIGN TABLE
         DC    215X'00'
LETTERP  DC    X'D7'                    THIS BYTE REPRESENTS A 'P'
         DC    40X'00'                  FILL OUT TABLE
TRANTBLU DS    0CL256                   FIND EQUALS SIGN TABLE
         DC    228X'00'
LETTERU  DC    X'E4'                    THIS BYTE REPRESENTS A 'U'
         DC    27X'00'                  FILL OUT TABLE
         LTORG
WRKAREA1 DSECT                          BEGINNING OF WORKAREA 1
CRDUSER  DS    CL8                      USERID FROM CARD
CARDPASS DS    A                        ADDRESS OF CARD PASSWORD
ENDAREA1 DS    0D                       END OF WORKAREA 1
         EQUATE                         REGISTER EQUATES
         IKJEFFIE IETYPE=SUBMIT
         END   IKJEFF10
