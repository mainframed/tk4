         TITLE 'USRUPD1 - DELUXE USER REGISTRATION (UPDATE A USER)'
***********************************************************************
*                                                                     *
*        MODULE NAME = USRUPD1                                        *
*                                                                     *
*        CHANGE ACTIVITY:                                             *
*              06/03/86  WRITTEN.                               JWC.  *
*                                                                     *
*              08/05/86  TSO & NCCF SPECIFICATIONS REMOVED.     JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'USRUPD1 - ISPCALL MACRO'
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         +
               &EP=ISPLINK,                                            +
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    +
               &EPADDR=,       LABEL FOR ADDRESS OF EP OF ISPLINK      +
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    +
               &WKREG=,        WORK REGISTER (DEFAULT 1)               +
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG,&WKEP
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1,&ISPEP
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHES+
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
         EJECT
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 +
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LIS+
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
         EJECT
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
.*       LA    1,&WKAR.+4                                       ISPCALL
.*       L     15,=V(&EP)                                       ISPCALL
.*       BALR  14,15                                            ISPCALL
         AIF   (T'&EPADDR EQ 'O').EP2
&ISPEP   SETC  '&EPADDR'
         LOAD  EP=ISPLINK
         ST    R0,&ISPEP
.EP2     ANOP
&WKEP    SETC  '&ISPEP'
         L     15,&WKEP
         LA    1,&WKAR.+4
         CALL  (15)
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
         EJECT
         TITLE 'USRUPD1 - INITIALIZATION SECTION'
USRUPD1  CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,USRUPD1_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING USRUPD1,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LA    R11,4095(R12)           LOAD REG 11 WITH ENTRY + 4095
         USING USRUPD1+4095,R11        ESTABLISH ADDRESSABILITY TO
*                                      REST OF PROGRAM
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*  INITIALIZATION SECTION - SET DIALOG CONTROL, DEFINE VARIABLES
***********************************************************************
CONTROL  EQU   *
*                                      SET DIALOG CONTROL
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        +
               WKAREA=PARMS,                                           +
               EPADDR=ISPADDR
VDEF1    EQU   *
         MVC   ISPFSERV,DEFID          SET ISPF DIALOG SERVICE
*                                      DEFINE VARIABLES
         ISPCALL ('VDEFINE',NAMELST1,USERID,'CHAR',8)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF2                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF2    EQU   *
         ISPCALL ('VDEFINE',NAMELST2,FNAME,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF3                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF3    EQU   *
         ISPCALL ('VDEFINE',NAMELST3,RACFATT0,'CHAR',12)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF4                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF4    EQU   *
         ISPCALL ('VDEFINE',NAMELST4,USERDATA,'CHAR',70)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF5                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF5    EQU   *
         ISPCALL ('VDEFINE',NAMELST5,RACF$,'CHAR',1)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF6                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF6    EQU   *
         ISPCALL ('VDEFINE',NAMELST6,ROOM,'CHAR',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF7                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF7    EQU   *
         ISPCALL ('VDEFINE',NAMELST7,CNCTGRP1,'CHAR',40)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF8                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF8    EQU   *
         ISPCALL ('VDEFINE',NAMELST8,USERMSGS,'CHAR',24)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF9                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF9    EQU   *
         ISPCALL ('VDEFINE',NAMELST9,USERMSGL,'CHAR',78)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFA                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFA    EQU   *
         ISPCALL ('VDEFINE',NAMELSTA,SAVTOPB,'FIXED',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFB                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFB    EQU   *
         ISPCALL ('VDEFINE',NAMELSTB,ZTDTOP,'CHAR',6)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFC                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFC    EQU   *
         ISPCALL ('VDEFINE',NAMELSTC,ZTEMPF,'CHAR',44)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFD                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFD    EQU   *
         ISPCALL ('VDEFINE',NAMELSTD,ZLLGJOB1,'CHAR',80)
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF10   EQU   *
         B     TBOPEN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VDEF)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VDEF)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - OPEN 'USERTAB'
***********************************************************************
TBOPEN   EQU   *
         MVC   ISPFSERV,OPNID          SET ISPF DIALOG SERVICE
*                                      OPEN A TABLE
         ISPCALL ('TBOPEN','USERTAB','WRITE','USRTLIB')
         B     TBOPN10(R15)            CHECK RETURN CODE
TBOPN10  EQU   *
         B     INITVAL                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBOPEN)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - INITIALIZE DIALOG VARIABLES
***********************************************************************
INITVAL  EQU   *
         MVC   SELCT,SPACES            INITIALIZATION
         MVC   USRUPD_,SKELNAM$        INITIALIZATION
         MVC   SUB,NO                  INITIALIZATION
         MVI   UPDFLAG,OFF             INITIALIZATION
         MVC   SUBCMD2,SUBCMD1         MOVE STATIC INFO TO DYNAMIC
         MVC   UPDCMD2,UPDCMD1         MOVE STATIC INFO TO DYNAMIC
         BAL   R8,SAVE                 SAVE LOGICAL SCREEN
***********************************************************************
*  PROCESSING SECTION - DISPLAY TABLE INFORMATION
***********************************************************************
TBDISPL1 EQU   *
         BAL   R8,RESTORE              RESTORE LOGICAL SCREEN
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','USERTAB','USRUPD1')
         B     TBDIS110(R15)           CHECK RETURN CODE
TBDIS110 EQU   *
         B     TBDIS120                RC = 00 (CONTINUE)
         B     TBDIS120                RC = 04 (CONTINUE)
         B     TBDIS130                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBDIS120 EQU   *
         BAL   R8,SAVE                 SAVE LOGICAL SCREEN
         B     CHKZCMD                 CHECK PRIMARY CMD FOR 'LOCATE'
TBDIS130 EQU   *
         BAL   R8,SAVE                 SAVE LOGICAL SCREEN
         B     CHKSELS1                CHECK FOR LINES TO PROCESS
***********************************************************************
*  PROCESSING SECTION - CHECK FOR 'LOCATE' PRIMARY COMMAND
***********************************************************************
CHKZCMD  EQU   *
         PACK  SAVTOPD,ZTDTOP          TRANSLATE ZTDTOP TO DECIMAL
         CVB   R2,SAVTOPD              CONVERT TO BINARY
         ST    R2,SAVTOPB              SAVE FOR FUTURE USE
         MVC   SAVLOC,SPACES           INITIALIZE SAVLOC
         LA    R2,ZCMD                 R2 - INDEX THROUGH ZCMD
         LA    R3,40                   R3 - MAXIMUM LENGTH OF ZCMD
         LA    R5,SAVLOC               R5 - INDEX THROUGH SAVLOC
CHKZCMD1 EQU   *
         LA    R4,2                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(2,R2),LOCATE1         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,3                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(3,R2),LOCATE2         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,4                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(4,R2),LOCATE3         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,5                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(5,R2),LOCATE4         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,6                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(6,R2),LOCATE5         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         LA    R4,7                    R4 - AMOUNT TO SKIP PAST LOCATE
         CLC   0(7,R2),LOCATE6         LOCATE COMMAND ENTERED?
         BE    CHKZCMD2                YES, SAVE LOCATE DATA
         CLC   0(1,R2),SPACES          ANY OTHER CHARACTER ENTERED?
         BNE   CHKLCMD                 YES, UNSUPPORTED COMMAND
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,CHKZCMD1             CONTINUE
         B     CHKLCMD                 CHECK FOR LINE COMMAND
CHKZCMD2 EQU   *
         AR    R2,R4                   POINT R2 PAST LOCATE COMMAND
CHKZCMD3 EQU   *
         CLC   0(1,R2),SPACES          START OF LOCATE DATA?
         BNE   CHKZCMD4                YES, EXTRACT DATA AND SAVE
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,CHKZCMD3             CONTINUE
         B     CHKLCMD                 NOTHING TO LOCATE
CHKZCMD4 EQU   *
         CLC   0(1,R2),SPACES          END OF LOCATE DATA?
         BE    CHKZCMD5                YES, ADD ASTERICK ON END
         MVC   0(1,R5),0(R2)           EXTRACT LOCATE DATA
         LA    R2,1(R2)                INCREMENT R2
         LA    R5,1(R5)                INCREMENT R5
         BCT   R3,CHKZCMD4             CONTINUE
         B     CHKLCMD                 CHECK LINE COMMANDS
CHKZCMD5 EQU   *
         MVC   0(1,R5),ASTERICK        ADD ASTERICK ON END
         B     CHKLCMD                 CHECK LINE COMMANDS
***********************************************************************
*  PROCESSING SECTION - CHECK LINE COMMAND
***********************************************************************
CHKLCMD  EQU   *
         CLC   ZTDSELS,CHAR00          NO LINES SELECTED?
         BE    CHKLOC                  CORRECT, PROCESS 'LOCATE' CMD
         CLC   ZTDSELS,CHAR01          ONE LINE SELECTED?
         BNE   CHKLCMD1                MORE, PROCESS
         CLC   LCMD,SELECT             LINE BEEN SELECTED?
         BNE   CHKLOC                  NO, PROCESS 'LOCATE' CMD
         MVI   UPDFLAG,ON              TURN ON UPDFLAG
         BAL   R8,TBGET                RETRIEVE INFO FOR UPDATED USER
         B     CHKLOC                  PROCESS 'LOCATE' COMMAND
CHKLCMD1 EQU   *
         CLC   LCMD,SELECT             LINE BEEN SELECTED?
         BNE   TBDISPL2                NO, PROCESS LCMD
         MVI   UPDFLAG,ON              TURN ON UPDFLAG
         BAL   R8,TBGET                RETRIEVE INFO FOR UPDATED USER
***********************************************************************
*  PROCESSING SECTION - DISPLAY TABLE TO PROCESS LCMD
***********************************************************************
TBDISPL2 EQU   *
         BAL   R8,RESTORE              RESTORE LOGICAL SCREEN
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','USERTAB')
         B     TBDIS210(R15)           CHECK RETURN CODE
TBDIS210 EQU   *
         B     TBDIS220                RC = 00 (CONTINUE)
         B     TBDIS220                RC = 04 (CONTINUE)
         B     TBDIS230                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBDIS220 EQU   *
         BAL   R8,SAVE                 SAVE LOGICAL SCREEN
         B     CHKLCMD                 CHECK FOR LINE COMMAND
TBDIS230 EQU   *
         BAL   R8,SAVE                 SAVE LOGICAL SCREEN
         B     CHKSELS2                CHECK FOR LINES TO PROCESS
***********************************************************************
*  PROCESSING SECTION - PROCESS LOCATE DATA
***********************************************************************
CHKLOC   EQU   *
         CLC   SAVLOC,SPACES           ANYTHING TO LOCATE?
         BNE   TBTOP1                  YES, ATTEMPT TO LOCATE
         B     TBDISPL1                RE-DISPLAY TABLE
***********************************************************************
*  PROCESSING SECTION - SET CRP TO TOP OF TABLE
***********************************************************************
TBTOP1   EQU   *
         MVC   ISPFSERV,TOPID          SET ISPF DIALOG SERVICE
*                                      SET ROW POINTER TO TOP
         ISPCALL ('TBTOP','USERTAB')
         B     TBTOP110(R15)           CHECK RETURN CODE
TBTOP110 EQU   *
         B     TBVCLEAR                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     USRERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CLEAR 'USERTAB' VARIABLES
***********************************************************************
TBVCLEAR EQU   *
         MVC   ISPFSERV,CLRID          SET ISPF DIALOG SERVICE
*                                      CLEAR TABLE VARIABLES
         ISPCALL ('TBVCLEAR','USERTAB')
         B     TBCLR10(R15)            CHECK RETURN CODE
TBCLR10  EQU   *
         B     TBSARG                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBVCLEAR)
         B     USRERR08                RC = 08 (NOT VALID FOR TBVCLEAR)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBVCLEAR)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - DEFINE SCAN ARGUMENTS FOR 'USERTAB'
***********************************************************************
TBSARG   EQU   *
         MVC   ISPFSERV,ARGID          SET ISPF DIALOG SERVICE
         MVC   USERID,SAVLOC           SET USERID WITH SELECTION VALUE
*                                      DEFINE SCAN ARGUMENTS
         ISPCALL ('TBSARG','USERTAB')
         B     TBARG10(R15)            CHECK RETURN CODE
TBARG10  EQU   *
         B     TBSCAN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSARG)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBSARG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SEARCH 'USERTAB' TABLE
***********************************************************************
TBSCAN   EQU   *
         MVC   ISPFSERV,SCNID          SET ISPF DIALOG SERVICE
*                                      SEARCH A TABLE
         ISPCALL ('TBSCAN','USERTAB')
         B     TBSCN10(R15)            CHECK RETURN CODE
TBSCN10  EQU   *
         B     TBDISPL1                RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSCAN)
         B     SCNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - END OR RETURN ENTERED. CHECK FOR SELECTED ROWS
***********************************************************************
CHKSELS1 EQU   *
         PACK  SAVTOPD,ZTDTOP          TRANSLATE ZTDTOP TO DECIMAL
         CVB   R2,SAVTOPD              CONVERT TO BINARY
         ST    R2,SAVTOPB              SAVE FOR FUTURE USE
         CLC   ZTDSELS,CHAR00          ANY ROWS SELECTED?
         BE    TBCLOSE                 NO, WE'RE FINISHED
         B     CHKLCMD                 YES, CHECK LINE COMMAND(S)
CHKSELS2 EQU   *
         CLC   ZTDSELS,CHAR01          SELECTED ROWS PROCESSED?
         BE    TBCLOSE                 YES, WE'RE FINISHED
         B     CHKLCMD                 NO, CHECK LINE COMMAND(S)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE INFO FROM TABLE FOR SELECTED USER
***********************************************************************
TBGET   EQU    *
         MVC   ISPFSERV,TBGID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE ROW FROM TABLE
         ISPCALL ('TBGET','USERTAB')
         B     TBGET10(R15)            CHECK RETURN CODE
TBGET10  EQU   *
         B     VPUT                    RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'USRUPD1 - PROCESSING SECTION'
***********************************************************************
*  PROCESSING SECTION - UPDATE VARIABLES IN POOL
***********************************************************************
VPUT     EQU   *
         MVC   ISPFSERV,PUTID          SET ISPF DIALOG SERVICE
*                                      UPDATE VARIABLES IN POOL
         ISPCALL ('VPUT',NAMELSTY)
         B     VPUT10(R15)             CHECK RETURN CODE
VPUT10   EQU   *
         B     SELECT1                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VPUT)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VPUT)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SELECT PROGRAM USRUPD2
***********************************************************************
SELECT1  EQU   *
         MVC   ISPFSERV,SELID          SET ISPF DIALOG SERVICE
         ISPCALL ('SELECT',FULL12,USRUPD2)    SELECT 'USRUPD2'
         B     SELCT110(R15)           CHECK RETURN CODE
SELCT110 EQU   *
         B     VGET1                   RC = 00 (CONTINUE)
         B     VGET1                   RC = 04 (CONTINUE)
         B     USRERR08                RC = 08 (NOT VALID FOR SELECT)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE UPDATED VARIABLES FROM POOL
***********************************************************************
VGET1    EQU   *
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE DIALOG VARIABLE
         ISPCALL ('VGET',NAMELSTY)
         B     VGET110(R15)            CHECK RETURN CODE
VGET110  EQU   *
         B     TBPUT                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - UPDATE TABLE WITH RACF INFORMATION
***********************************************************************
TBPUT    EQU   *
         MVC   ISPFSERV,PUTID          SET ISPF DIALOG SERVICE
         ISPCALL ('TBPUT','USERTAB',' ','ORDER')
         B     TBPUT10(R15)            CHECK RETURN CODE
TBPUT10  EQU   *
         B     TBPUT20                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBPUT)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBPUT20  EQU   *
         BR    R8                      RETURN
***********************************************************************
*  PROCESSING SECTION - CLOSE USER TABLE (USERTAB)
***********************************************************************
TBCLOSE  EQU   *
         MVC   ISPFSERV,CLOID          SET ISPF DIALOG SERVICE
*                                      CLOSE AND SAVE TABLE
         ISPCALL ('TBCLOSE','USERTAB','REPLCOPY',' ',' ','USRTLIB')
         B     TBCLO10(R15)            CHECK RETURN CODE
TBCLO10  EQU   *
         B     TBCLO20                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBCLOSE)
         B     USRERR08                RC = 08 (NOT VALID FOR TBCLOSE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBCLO20  EQU   *
         LA    R4,SKELNAM$             R4 - INDEX THRU SKELETON NAMES
***********************************************************************
*  PROCESSING SECTION - RETRIEVE 'ZLLGJOB1' & 'ZLLGJOB2'
***********************************************************************
VGET2    EQU   *
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE DIALOG VARIABLE
         ISPCALL ('VGET',NAMELSTD)
         B     VGET210(R15)            CHECK RETURN CODE
VGET210  EQU   *
         B     FTOPEN                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - OPEN TEMPORARY FILE TAILORING FILE
***********************************************************************
FTOPEN   EQU   *
         MVC   ISPFSERV,FTOID          SET ISPF DIALOG SERVICE
         TM    UPDFLAG,ON              ANY DATA PROCESSED?
         BNO   TBERASE                 NO, ERASE FILE TAILORING TABLE
*                                      OPEN TEMP FILE TAILORING FILE
         ISPCALL ('FTOPEN','TEMP')
         B     FTOPN10(R15)            CHECK RETURN CODE
FTOPN10  EQU   *
         B     FTINCL                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTOPEN)
         B     FTINCL                  RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - INCLUDE SKELETON FOR FILE TAILORING
***********************************************************************
FTINCL   EQU   *
         MVC   ISPFSERV,FTIID          SET ISPF DIALOG SERVICE
*                                      INCLUDE SKELETON FOR TAILORING
         ISPCALL ('FTINCL',USRUPD_)
         B     FTINC10(R15)            CHECK RETURN CODE
FTINC10  EQU   *
         B     FTCLOSE                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTINCL)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - CLOSE TEMPORARY FILE
***********************************************************************
FTCLOSE  EQU   *
         MVC   ISPFSERV,FTCID          SET ISPF DIALOG SERVICE
*                                      CLOSE TEMP FILE TAILORING FILE
         ISPCALL ('FTCLOSE')
         B     FTCLO10(R15)            CHECK RETURN CODE
FTCLO10  EQU   *
         B     VGET3                   RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR FTCLOSE)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - RETRIEVE 'ZTEMPF'
***********************************************************************
VGET3    EQU   *
         CLC   SUB,NO                  REGIONAL FILE TAILORING?
         BE    SELCT320                NO, PROCESS NEXT REGION
         MVC   ISPFSERV,GETID          SET ISPF DIALOG SERVICE
*                                      RETRIEVE DIALOG VARIABLE
         ISPCALL ('VGET',NAMELSTC)
         B     VGET310(R15)            CHECK RETURN CODE
VGET310  EQU   *
         B     SELECT3                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR VGET)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR VGET)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SELECT CMD ZTEMPF TO DEFINE USER(S)
***********************************************************************
SELECT3 EQU    *
         MVC   ISPFSERV,SELID          SET ISPF DIALOG SERVICE
         MVC   SUBCMD2+11(44),ZTEMPF   NO, MOVE ZTEMPF TO SUBCMD
         ISPCALL ('SELECT',FULL56,SUBCMD2)
         B     SELCT310(R15)           CHECK RETURN CODE
SELCT310 EQU   *
         B     SELCT320                RC = 00 (CONTINUE)
         B     SELCT320                RC = 04 (CONTINUE)
         B     USRERR08                RC = 08 (NOT VALID FOR SELECT)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
SELCT320 EQU   *
         CLC   0(8,R4),SKELNAM8        ARE WE DONE?
         BE    TBERASE                 YES, ERASE TABLE
         LA    R4,8(R4)                POINT TO NEXT SKELETON NAME
         MVC   USRUPD_,0(R4)           MOVE IT INTO USRUPD_
         MVC   SUB,NO                  RE-SET SUB
         B     FTOPEN                  CONTINUE WITH FILE TAILORING
***********************************************************************
*  PROCESSING SECTION - ERASE FILE TAILORING TABLE (USRUPD)
***********************************************************************
TBERASE  EQU   *
         MVC   ISPFSERV,ERAID          SET ISPF DIALOG SERVICE
*                                      CLOSE TABLE WITHOUT SAVING
         ISPCALL ('TBERASE','USRUPD')
         B     TBERA10(R15)            CHECK RETURN CODE
TBERA10  EQU   *
         B     FINISH                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBERASE)
         B     FINISH                  RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  PROCESSING SECTION - SAVE LOGICAL SCREEN
***********************************************************************
SAVE     EQU   *
         MVC   ISPFSERV,CTLID          SET ISPF DIALOG SERVICE
*                                      SAVE LOGICAL SCREEN
         ISPCALL ('CONTROL','DISPLAY','SAVE')
         B     SAVE10(R15)             CHECK RETURN CODE
SAVE10   EQU   *
         B     SAVE20                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR CONTROL)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR CONTROL)
         B     USRERR16                RC = 16 (NOT VALID FOR CONTROL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
SAVE20   EQU   *
         BR    R8                      RETURN
***********************************************************************
*  PROCESSING SECTION - RESTORE LOGICAL SCREEN
***********************************************************************
RESTORE  EQU   *
         MVC   ISPFSERV,CTLID          SET ISPF DIALOG SERVICE
*                                      RESTORE LOGICAL SCREEN
         ISPCALL ('CONTROL','DISPLAY','RESTORE')
         B     REST10(R15)             CHECK RETURN CODE
REST10   EQU   *
         B     REST20                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR CONTROL)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (NOT VALID FOR CONTROL)
         B     USRERR16                RC = 16 (NOT VALID FOR CONTROL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
REST20   EQU   *
         BR    R8                      RETURN
         TITLE 'USRUPD1 - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
USRERR04 EQU   *
         MVC   ISPFRC,FOUR             SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR08 EQU   *
         MVC   ISPFRC,EIGHT            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR12 EQU   *
         MVC   ISPFRC,TWELVE           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR16 EQU   *
         MVC   ISPFRC,SIXTEEN          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
USRERR20 EQU   *
         MVC   ISPFRC,TWENTY           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
MSGDISP1 EQU   *
         MVC   USERMSGS,USERERRS       SET SHORT MESSAGE
         MVC   USERMSGL,USERERRL       SET LONG MESSAGE
         MVC   USERMSGL+21(8),ISPFSERV    SET ISPF DIALOG SERVICE
         MVC   USERMSGL+47(2),ISPFRC   SET DIALOG SERVICE RETURN CODE
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','USRUPD1',USERMSG)
         B     FINISH
***********************************************************************
*  ERROR PROCESSING SECTION - USER NOT FOUND ERROR ('LOCATE' COMMAND)
***********************************************************************
SCNERR08 EQU   *
         MVC   USERMSGS,SPACES         INITIALIZE USERMSGS
         MVC   USERMSGL,SPACES         INITIALIZE USERMSGL
         MVC   USERMSGL(1),QUOTE       MOVE LEADING QUOTE TO MESSAGE
         MVC   USERMSGL+1(40),SAVLOC   MOVE IN LOCATION DATA
         LA    R2,USERMSGL             R2 - INDEX THROUGH SAVLOC
         LA    R3,41                   R3 - MAXIMUM LENGTH
SCNERR18 EQU   *
         CLC   0(1,R2),ASTERICK        END OF LOCATION DATA
         BE    SCNERR28                YES, ADD TRAILING QUOTE, ETC.
         LA    R2,1(R2)                INCREMENT R2
         BCT   R3,SCNERR18             CONTINUE
SCNERR28 EQU   *
         MVC   USERMSGS,USRERR1S       ISPF SHORT MESSAGE
         MVC   0(21,R2),USRERR1L       ADD TRAILING QUOTE, ETC.
         BAL   R8,TBTOP2               RE-POSITION CRP
         MVC   ISPFSERV,TDSID          SET ISPF DIALOG SERVICE
         ISPCALL ('TBDISPL','USERTAB','USRUPD1',USERMSG)
         B     SCNERR38(R15)           CHECK RETURN CODE
SCNERR38 EQU   *
         B     SCNERR48                RC = 00 (CONTINUE)
         B     SCNERR48                RC = 04 (CONTINUE)
         B     CHKSELS1                RC = 08 (CONTINUE)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
SCNERR48 EQU   *
         LA    R8,TBDISPL1             R8 - RETURN ADDRESS
***********************************************************************
*  ERROR PROCESSING SECTION - SET CRP TO TOP OF TABLE (AFTER 'LOCATE')
***********************************************************************
TBTOP2   EQU   *
         MVC   ISPFSERV,TOPID          SET ISPF DIALOG SERVICE
*                                      SET ROW POINTER TO TOP
         ISPCALL ('TBTOP','USERTAB')
         B     TBTOP210(R15)           CHECK RETURN CODE
TBTOP210 EQU   *
         B     TBSKIP                  RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     USRERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  ERROR PROCESSING SECTION - RESET ROWID (AFTER 'LOCATE')
***********************************************************************
TBSKIP   EQU   *
         MVC   ISPFSERV,SKPID          SET ISPF DIALOG SERVICE
*                                      RESET ROWID
         ISPCALL ('TBSKIP','USERTAB',SAVTOPB)
         B     TBSKP10(R15)            CHECK RETURN CODE
TBSKP10  EQU   *
         B     TBSKP20                 RC = 00 (CONTINUE)
         B     USRERR04                RC = 04 (NOT VALID FOR TBSKIP)
         B     USRERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     USRERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     USRERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     USRERR20                RC = 20 (DISPLAY PANEL / MSG)
TBSKP20  EQU   *
         BR    R8                      RC = 00 (CONTINUE)
         TITLE 'USRUPD1 - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'USRUPD1 - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
         EQUATE
ON       EQU   X'10'                   USER(S) HAVE BEEN UPDATED
OFF      EQU   X'00'                   USER(S) HAVE NOT BEEN UPDATED
         TITLE 'USRUPD1 - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
FULL12   DC    F'12'                   FULLWORD CONSTANT VALUE '12'
FULL56   DC    F'56'                   FULLWORD CONSTANT VALUE '56'
SPACES   DC    CL80' '                 SPACES
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
LOCATE1  DC    CL2'L '                 LOCATE COMMAND IDENTIFIER
LOCATE2  DC    CL3'LO '                LOCATE COMMAND IDENTIFIER
LOCATE3  DC    CL4'LOC '               LOCATE COMMAND IDENTIFIER
LOCATE4  DC    CL5'LOCA '              LOCATE COMMAND IDENTIFIER
LOCATE5  DC    CL6'LOCAT '             LOCATE COMMAND IDENTIFIER
LOCATE6  DC    CL7'LOCATE '            LOCATE COMMAND IDENTIFIER
NO       DC    CL1'N'                  NO
YES      DC    CL1'Y'                  YES
ASTERICK DC    CL1'*'                  ASTERICK
QUOTE    DC    CL1''''                 QUOTE
CHAR00   DC    CL4'0000'               CHARACTER CONSTANT VALUE '0000'
CHAR01   DC    CL4'0001'               CHARACTER CONSTANT VALUE '0001'
SELECT   DC    CL1'S'                  USERID HAS BEEN SELECTED
USERMSG  DC    CL8'USER000'            GENERIC USER MESSAGE
USRUPD2  DC    C'PGM(USRUPD2)'         FUNTION CALLED FROM USRUPD1
SUBCMD1  DC    CL56'CMD(SUBMIT                                         +
                    )'
UPDCMD1  DC    CL56'CMD(EXEC ''                                        +
                    )'
SKELNAM$ DC    CL8'USRUPD$'            ADMIN SKELETON
SKELNAM1 DC    CL8'USRUPD1'            REGION 1 SKELETON
SKELNAM2 DC    CL8'USRUPD2'            REGION 2 SKELETON
SKELNAM3 DC    CL8'USRUPD3'            REGION 3 SKELETON
SKELNAM4 DC    CL8'USRUPD4'            REGION 4 SKELETON
SKELNAM5 DC    CL8'USRUPD5'            REGION 5 SKELETON
SKELNAM6 DC    CL8'USRUPD6'            REGION 6 SKELETON
SKELNAM7 DC    CL8'USRUPD7'            REGION 7 SKELETON
SKELNAM8 DC    CL8'USRUPD8'            REGION 8 SKELETON
ADDID    DC    CL8'TBADD'              ISPF DIALOG SERVICE (TBADD)
CLOID    DC    CL8'TBCLOSE'            ISPF DIALOG SERVICE (TBCLOSE)
DEFID    DC    CL8'VDEFINE'            ISPF DIALOG SERVICE (VDEFINE)
DISID    DC    CL8'DISPLAY'            ISPF DIALOG SERVICE (DISPLAY)
ERAID    DC    CL8'TBERASE'            ISPF DIALOG SERVICE (TBERASE)
FTCID    DC    CL8'FTCLOSE'            ISPF DIALOG SERVICE (FTCLOSE)
FTIID    DC    CL8'FTINCL'             ISPF DIALOG SERVICE (FTINCL)
FTOID    DC    CL8'FTOPEN'             ISPF DIALOG SERVICE (FTOPEN)
GETID    DC    CL8'VGET'               ISPF DIALOG SERVICE (VGET)
PUTID    DC    CL8'VPUT'               ISPF DIALOG SERVICE (VPUT)
OPNID    DC    CL8'TBOPEN'             ISPF DIALOG SERVICE (TBOPEN)
SELID    DC    CL8'SELECT'             ISPF DIALOG SERVICE (SELECT)
TBGID    DC    CL8'TBGET'              ISPF DIALOG SERVICE (TBGET)
TDSID    DC    CL8'TBDISPL'            ISPF DIALOG SERVICE (TBDISPL)
TOPID    DC    CL8'TBTOP'              ISPF DIALOG SERVICE (TBTOP)
CLRID    DC    CL8'TBVCLEAR'           ISPF DIALOG SERVICE (TBVCLEAR)
ARGID    DC    CL8'TBSARG'             ISPF DIALOG SERVICE (TBSARG)
SCNID    DC    CL8'TBSCAN'             ISPF DIALOG SERVICE (TBSCAN)
SKPID    DC    CL8'TBSKIP'             ISPF DIALOG SERVICE (TBSKIP)
CTLID    DC    CL8'CONTROL'            ISPF DIALOG SERVICE (CONTROL)
USERERRS DC    CL24'USRUPD1 program error.  '
USERERRL DC    CL78'ISPF Dialog Service, 12345678, Ended with RC = 00. +
               Notify Systems Programming.'
USRERR1S DC    CL24'Not found in table.     '
USRERR1L DC    CL21''' not found in table.'
MSGTEXT  DC    CL72'Press ENTER key to view additional connect groups'
NAMELST1 DS    0F                      NAMES OF VARIABLES (LENGTH 8)
         DC    F'80'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'OWNER'
         DC    CL8'DFLTGRP'
         DC    CL8'REVOKE'
         DC    CL8'RESUME'
         DC    CL8'CGRP1'
         DC    CL8'CAUTH1'
         DC    CL8'CUACC1'
         DC    CL8'CREV1'
         DC    CL8'CRES1'
         DC    CL8'CGRP2'
         DC    CL8'CAUTH2'
         DC    CL8'CUACC2'
         DC    CL8'CREV2'
         DC    CL8'CRES2'
         DC    CL8'CGRP3'
         DC    CL8'CAUTH3'
         DC    CL8'CUACC3'
         DC    CL8'CREV3'
         DC    CL8'CRES3'
         DC    CL8'CGRP4'
         DC    CL8'CAUTH4'
         DC    CL8'CUACC4'
         DC    CL8'CREV4'
         DC    CL8'CRES4'
         DC    CL8'CGRP5'
         DC    CL8'CAUTH5'
         DC    CL8'CUACC5'
         DC    CL8'CREV5'
         DC    CL8'CRES5'
         DC    CL8'CGRP6'
         DC    CL8'CAUTH6'
         DC    CL8'CUACC6'
         DC    CL8'CREV6'
         DC    CL8'CRES6'
         DC    CL8'CGRP7'
         DC    CL8'CAUTH7'
         DC    CL8'CUACC7'
         DC    CL8'CREV7'
         DC    CL8'CRES7'
         DC    CL8'CGRP8'
         DC    CL8'CAUTH8'
         DC    CL8'CUACC8'
         DC    CL8'CREV8'
         DC    CL8'CRES8'
         DC    CL8'CGRP9'
         DC    CL8'CAUTH9'
         DC    CL8'CUACC9'
         DC    CL8'CREV9'
         DC    CL8'CRES9'
         DC    CL8'CGRPA'
         DC    CL8'CAUTHA'
         DC    CL8'CUACCA'
         DC    CL8'CREVA'
         DC    CL8'CRESA'
         DC    CL8'CGRPB'
         DC    CL8'CAUTHB'
         DC    CL8'CUACCB'
         DC    CL8'CREVB'
         DC    CL8'CRESB'
         DC    CL8'CGRPC'
         DC    CL8'CAUTHC'
         DC    CL8'CUACCC'
         DC    CL8'CREVC'
         DC    CL8'CRESC'
         DC    CL8'CGRPD'
         DC    CL8'CAUTHD'
         DC    CL8'CUACCD'
         DC    CL8'CREVD'
         DC    CL8'CRESD'
         DC    CL8'CGRPE'
         DC    CL8'CAUTHE'
         DC    CL8'CUACCE'
         DC    CL8'CREVE'
         DC    CL8'CRESE'
         DC    CL8'CGRPF'
         DC    CL8'CAUTHF'
         DC    CL8'CUACCF'
         DC    CL8'CREVF'
         DC    CL8'CRESF'
NAMELST2 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'03'
         DC    F'00'
         DC    CL8'FNAME'
         DC    CL8'LNAME'
         DC    CL8'NAME'
NAMELST3 DS    0F                      NAMES OF VARIABLES (LENGTH 12)
         DC    F'04'
         DC    F'00'
         DC    CL8'RACFATT0'
         DC    CL8'RACFATT1'
         DC    CL8'RACFATT2'
         DC    CL8'RACFATT3'
NAMELST4 DS    0F                      NAMES OF VARIABLES (LENGTH 70)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERDATA'
NAMELST5 DS    0F                      NAMES OF VARIABLES (LENGTH 1)
         DC    F'12'
         DC    F'00'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         DC    CL8'SELCT'
         DC    CL8'LCMD'
         DC    CL8'SUB'
NAMELST6 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'03'
         DC    F'00'
         DC    CL8'ROOM'
         DC    CL8'EXTENS'
         DC    CL8'ZTDSELS'
NAMELST7 DS    0F                      NAMES OF VARIABLES (LENGTH 40)
         DC    F'16'
         DC    F'00'
         DC    CL8'CNCTGRP1'
         DC    CL8'CNCTGRP2'
         DC    CL8'CNCTGRP3'
         DC    CL8'CNCTGRP4'
         DC    CL8'CNCTGRP5'
         DC    CL8'CNCTGRP6'
         DC    CL8'CNCTGRP7'
         DC    CL8'CNCTGRP8'
         DC    CL8'CNCTGRP9'
         DC    CL8'CNCTGRPA'
         DC    CL8'CNCTGRPB'
         DC    CL8'CNCTGRPC'
         DC    CL8'CNCTGRPD'
         DC    CL8'CNCTGRPE'
         DC    CL8'CNCTGRPF'
         DC    CL8'ZCMD'
NAMELST8 DS    0F                      NAMES OF VARIABLES (LENGTH 24)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGS'
NAMELST9 DS    0F                      NAMES OF VARIABLES (LENGTH 78)
         DC    F'01'
         DC    F'00'
         DC    CL8'USERMSGL'
NAMELSTA DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'01'
         DC    F'00'
         DC    CL8'SAVTOPB'
NAMELSTB DS    0F                      NAMES OF VARIABLES (LENGTH 6)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZTDTOP'
NAMELSTC DS    0F                      NAMES OF VARIABLES (LENGTH 44)
         DC    F'01'
         DC    F'00'
         DC    CL8'ZTEMPF'
NAMELSTD DS    0F                      NAMES OF VARIABLES (LENGTH 80)
         DC    F'02'
         DC    F'00'
         DC    CL8'ZLLGJOB1'
         DC    CL8'ZLLGJOB2'
NAMELSTY DS    0F                      NAMES OF VPUT VARIABLES
         DC    F'39'
         DC    F'00'
         DC    CL8'USERID'
         DC    CL8'FNAME'
         DC    CL8'LNAME'
         DC    CL8'NAME'
         DC    CL8'OWNER'
         DC    CL8'DFLTGRP'
         DC    CL8'RACFATT0'
         DC    CL8'RACFATT1'
         DC    CL8'RACFATT2'
         DC    CL8'RACFATT3'
         DC    CL8'REVOKE'
         DC    CL8'RESUME'
         DC    CL8'USERDATA'
         DC    CL8'CNCTGRP1'
         DC    CL8'CNCTGRP2'
         DC    CL8'CNCTGRP3'
         DC    CL8'CNCTGRP4'
         DC    CL8'CNCTGRP5'
         DC    CL8'CNCTGRP6'
         DC    CL8'CNCTGRP7'
         DC    CL8'CNCTGRP8'
         DC    CL8'CNCTGRP9'
         DC    CL8'CNCTGRPA'
         DC    CL8'CNCTGRPB'
         DC    CL8'CNCTGRPC'
         DC    CL8'CNCTGRPD'
         DC    CL8'CNCTGRPE'
         DC    CL8'CNCTGRPF'
         DC    CL8'RACF$'
         DC    CL8'RACF1'
         DC    CL8'RACF2'
         DC    CL8'RACF3'
         DC    CL8'RACF4'
         DC    CL8'RACF5'
         DC    CL8'RACF6'
         DC    CL8'RACF7'
         DC    CL8'RACF8'
         DC    CL8'EXTENS'
         DC    CL8'ROOM'
         TITLE 'USRUPD1 - LITERAL POOL'
         LTORG
         TITLE 'USRUPD1 - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
ISPADDR  DS    A                       ADDRESS OF EP TO ISPLINK
PARMS    DS    7A                      PARM LIST FOR CALL
SAVTOPB  DS    F                       SAVE ZTDTOP (BINARY)
SAVTOPD  DS    D                       SAVE ZTDTOP (FIXED DECIMAL)
UPDFLAG  DS    C                       FLAG SET WHEN USER DELETED
SUBCMD2  DS    CL56'CMD(SUBMIT                                         +
                    )'
UPDCMD2  DS    CL56'CMD(EXEC ''                                        +
                    )'
USERID   DS    CL8                     RACF USER ID
OWNER    DS    CL8                     RACF OWNER
DFLTGRP  DS    CL8                     RACF DFLTGRP
REVOKE   DS    CL8                     RACF REVOKE DATE
RESUME   DS    CL8                     RACF RESUME DATE
CNCTGRP1 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP1    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH1   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC1   DS    CL8                     USER'S CONNECT GROUP UACC
CREV1    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES1    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP2 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP2    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH2   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC2   DS    CL8                     USER'S CONNECT GROUP UACC
CREV2    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES2    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP3 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP3    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH3   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC3   DS    CL8                     USER'S CONNECT GROUP UACC
CREV3    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES3    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP4 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP4    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH4   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC4   DS    CL8                     USER'S CONNECT GROUP UACC
CREV4    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES4    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP5 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP5    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH5   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC5   DS    CL8                     USER'S CONNECT GROUP UACC
CREV5    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES5    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP6 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP6    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH6   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC6   DS    CL8                     USER'S CONNECT GROUP UACC
CREV6    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES6    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP7 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP7    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH7   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC7   DS    CL8                     USER'S CONNECT GROUP UACC
CREV7    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES7    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP8 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP8    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH8   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC8   DS    CL8                     USER'S CONNECT GROUP UACC
CREV8    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES8    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRP9 DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRP9    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTH9   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACC9   DS    CL8                     USER'S CONNECT GROUP UACC
CREV9    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRES9    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPA DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPA    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHA   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCA   DS    CL8                     USER'S CONNECT GROUP UACC
CREVA    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESA    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPB DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPB    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHB   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCB   DS    CL8                     USER'S CONNECT GROUP UACC
CREVB    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESB    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPC DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPC    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHC   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCC   DS    CL8                     USER'S CONNECT GROUP UACC
CREVC    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESC    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPD DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPD    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHD   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCD   DS    CL8                     USER'S CONNECT GROUP UACC
CREVD    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESD    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPE DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPE    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHE   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCE   DS    CL8                     USER'S CONNECT GROUP UACC
CREVE    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESE    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
CNCTGRPF DS    0CL40                   USER'S CONNECT GROUP INFORMATION
CGRPF    DS    CL8                     USER'S CONNECT GROUP NAME
CAUTHF   DS    CL8                     USER'S CONNECT GROUP AUTHORITY
CUACCF   DS    CL8                     USER'S CONNECT GROUP UACC
CREVF    DS    CL8                     USER'S CONNECT GROUP REVOKE DATE
CRESF    DS    CL8                     USER'S CONNECT GROUP RESUME DATE
ZCMD     DS    CL40                    ZCMD
FNAME    DS    CL20                    USER'S FIRST NAME
LNAME    DS    CL20                    USER'S LAST NAME
NAME     DS    CL20                    USER'S NAME
RACFATT0 DS    CL12                    SPECIAL / NOSPECIAL RACF ATTR
RACFATT1 DS    CL12                    OPERATIONS / NOOPERATIONS RACF
RACFATT2 DS    CL12                    AUDITOR / NOAUDITOR RACF ATTR
RACFATT3 DS    CL12                    INDICATES REVOKED USER
USERDATA DS    CL70                    RACF USER DEFINED DATA
RACF$    DS    CL1                     ADMIN SYSTEM USER
RACF1    DS    CL1                     REG 1 SYSTEM USER
RACF2    DS    CL1                     REG 2 SYSTEM USER
RACF3    DS    CL1                     REG 3 SYSTEM USER
RACF4    DS    CL1                     REG 4 SYSTEM USER
RACF5    DS    CL1                     REG 5 SYSTEM USER
RACF6    DS    CL1                     REG 6 SYSTEM USER
RACF7    DS    CL1                     REG 7 SYSTEM USER
RACF8    DS    CL1                     REG 8 SYSTEM USER
SELCT    DS    CL1                     SELCTION FROM 'USRUPD1' PANEL
LCMD     DS    CL1                     LINE COMMAND
SUB      DS    CL1                     INDICATES REGIONAL JOB TO SUBMIT
ROOM     DS    CL4                     USER'S ROOM NUMBER
EXTENS   DS    CL4                     USER'S PHONE EXTENSION
ZTDSELS  DS    CL4                     ZTDSELS
USERMSGS DS    CL24                    SHORT VERSION OF ISPF ERROR MSG
USERMSGL DS    CL78                    LONG VERSION OF ISPF ERROR MSG
ZLLGJOB1 DS    CL80                    JOB CARD 1 FROM OPTION 0.2
ZLLGJOB2 DS    CL80                    JOB CARD 2 FROM OPTION 0.2
USRUPD_  DS    CL8                     GENERIC SKELETON NAME
ZTDTOP   DS    CL6                     ZTDTOP
SAVLOC   DS    CL40                    SAVE LOCATE COMMAND
ZTEMPF   DS    CL44                    FILE TAILORING TEMPORARY FILE
MESSAGE  DS    CL72                    'ADDITIONAL CONNECT GROUPS' MSG
ISPFSERV DS    CL8                     ISPF SERVICE ROUTINE ID
ISPFRC   DS    CL2                     ISPF SERVICE ROUTINE RETURN CODE
WORKLEN  EQU   *-WORKAREA
         END   USRUPD1
