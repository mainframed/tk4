         MACRO
         LCBA  &REG,&CB
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      LCBA
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - LCBA
.*
.*  PURPOSE    MACRO USED TO LOAD ADDRESS OF CONTROL BLOCK
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   05/06/86 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   STRUCTURED MACRO TAG INDEX
         GBLA  &#MCVREG                 CONVERTED REGISTER (##CVREG)
         LCLC  &TAG1,&TAG2,&TAG3        WORK TAGS
&#MNDX   SETA  &#MNDX+1                 INCREMENT TAG INDEX
.*--------------------------------------------------------------------*
.*  INSURE THAT REG IS R1-R15
.*--------------------------------------------------------------------*
         ##CVREG &REG
         AIF   (&#MCVREG LE 15 AND &#MCVREG GT 0).CHK0100
         MNOTE 8,'&REG is invalid.  Register must be R1-R15.'
         MEXIT
.CHK0100 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF CURRENT ACEE
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'ACEE').ACE9999
&TAG1    SETC  '#LE&#MNDX'
&TAG2    SETC  '#LS&#MNDX'
         L     &REG,540                POINT TO CURRENT TCB
         TM    278(&REG),X'80'         TCB EXTENTION PRESENT?
         BZ    &TAG2                   NO, SKIP LOAD FROM TCB
         ICM   &REG,B'1111',340(&REG)  LOAD ACEE POINTER
         BNZ   &TAG1                   BRANCH IF PRESENT
&TAG2    L     &REG,548                POINT TO CURRENT ASCB
         L     &REG,108(,&REG)         POINT TO CURRENT ASXB
         ICM   &REG,B'1111',200(&REG)  POINT TO ACEE
&TAG1    DS    0H                      END OF LOAD ACEE CODE
         MEXIT
.ACE9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF ASCB
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'ASCB').ASC9999
         ICM   &REG,B'1111',548         POINT TO CURRENT ASCB
         MEXIT
.ASC9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF ASXB
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'ASXB').ASX9999
         L     &REG,548                 POINT TO CURRENT ASCB
         ICM   &REG,B'1111',108(&REG)   POINT TO CURRENT ASXB
         MEXIT
.ASX9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF CVT
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'CVT').CVT9999
         ICM   &REG,B'1111',16          POINT TO CVT
         MEXIT
.CVT9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF CURRENT TCB
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'TCB').TCB9999
         ICM   &REG,B'1111',540         POINT TO CURRENT TCB
         MEXIT
.TCB9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF UJT
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'UJT').UJT9999
&TAG1    SETC  '#LL&#MNDX'
&TAG2    SETC  '#LF&#MNDX'
&TAG3    SETC  '#LE&#MNDX'
         L     &REG,540                 POINT TO CURRENT TCB
&TAG1    TM    168(&REG),X'80'          DOES THIS TCB POINT TO UJT?
         BO    &TAG2                    YES, FOUND
         ICM   &REG,B'1111',132(&REG)   NO, POINT TO NEXT TCB
         BNZ   &TAG1                    LOOP UNTIL END OF TCB CHAIN
         B     &TAG3                    SKIP SUCCESS CODE
&TAG2    L     &REG,168(,&REG)          POINT TO UVT
         N     &REG,=A(X'00FFFFFF')     CLEAR HIGH BYTE AND SET CC
&TAG3    DS    0H                       END OF LOAD UVT CODE
         MEXIT
.UJT9999 ANOP
.*--------------------------------------------------------------------*
.*  LOAD ADDRESS OF UVT
.*--------------------------------------------------------------------*
         AIF   ('&CB' NE 'UVT').UVT9999
         L     &REG,16                  POINT TO CVT
         L     &REG,204(,&REG)          LOAD UVT ADDRESS
         N     &REG,=A(X'00FFFFFF')     CLEAR HIGH BYTE AND SET CC
         MEXIT
.UVT9999 ANOP
.*--------------------------------------------------------------------*
         MNOTE 8,'&CB is not a supported control block'
         MEND
