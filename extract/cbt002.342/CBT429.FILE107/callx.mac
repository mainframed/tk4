         MACRO
         CALLX &PGM,                    PROGRAM ADDRESS                $
               &P,                      PARAMETER LIST                 $
               &VL,                     VL                             $
               &PL=                     PARAMETER LIST STORAGE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1985.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      CALLX
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - CALLX
.*
.*  PURPOSE    STRUCTURED MACRO USED TO CALL EXTERNAL PROGRAMS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    6/27/85 MGG - NEW
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLB  &#PBXA                   XA OPTION
         GBLB  &#PB370                  370 OPTION
         LCLC  &TAG
&#MNDX   SETA  &#MNDX+1                 ADD ONE TO TAG INDEX
         PLIST &P,&VL,PL=&PL
.X100    AIF   ('&PGM' EQ '(R15)' OR '&PGM' EQ '(15)').X200
         L     R15,&PGM                 POINT R1 TO ROUTINE
.X200    AIF   (&#PBXA).X300            IF XA OPTION OFF, USE BALR INST
         BALR  R14,R15                  CALL EXTERNAL ROUTINE
         MEXIT
.X300    AIF   (&#PB370).X400           IF XA OPTION ON AND 370 OPTION
.*                                        OFF, USE BAS INST
         BASR  R14,R15                  CALL EXTERNAL ROUTINE
         MEXIT
.X400    ANOP                           IF XA AND 370 OPTION ON, USE
.*                                       BALR ON 370 AND BASR ON XA
         L     R14,16                   POINT R14 TO CVT
         TM    X'74'(R14),X'80'         RUNNING UNDER XA?
         BO    #CX&#MNDX                YES, DO BASR
         BALR  R14,R15                  CALL EXTERNAL ROUTINE
         B     #CP&#MNDX                BRANCH PAST BASR
&TAG     SETC  '#CX&#MNDX'
&TAG     BASR  R14,R15                  CALL EXTERNAL ROUTINE
&TAG     SETC  '#CP&#MNDX'
&TAG     DS    0H
         MEND
