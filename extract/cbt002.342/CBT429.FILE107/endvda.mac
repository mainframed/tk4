         MACRO
         ENDVDA &SIZE=,&STORAGE=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1991.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ENDVDA
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ENDVDA
.*
.*  PURPOSE    MACRO TO END A VARIABLE DEFINITION AREA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    8/06/91 MGG - NEW
.*
.*--------------------------------------------------------------------*
         GBLC  &VDA__ID(20)
         GBLC  &VDA__START(20)
         GBLC  &VDA__SIZE(20)
         GBLC  &VDA__STORAGE(20)
         GBLC  &VDA__GENTYPE
         GBLC  &VDA__CSECT
         GBLC  &VDA__EPILOG
         GBLC  &VDA__DC
         GBLC  &VDA__DS
         GBLC  &VDA__EQU
         GBLA  &VDA__LEVEL
         AIF   (&VDA__LEVEL GE 1).PRC010
         MNOTE 8,'ENDVDA does not have a corresponding VDA macro.'
         MEXIT
.PRC010  ANOP
&I       SETC  '&VDA__ID(&VDA__LEVEL)'
.*
.*  Determine tags for SIZE and storage
.*
&VDA__SIZE(&VDA__LEVEL) SETC ''
         AIF   ('&SIZE' EQ 'NO').PRC020
&VDA__SIZE(&VDA__LEVEL) SETC '&SIZE'
         AIF   ('&SIZE' NE '').PRC020
&VDA__SIZE(&VDA__LEVEL) SETC '&I.SIZE'
.PRC020  ANOP
&VDA__STORAGE(&VDA__LEVEL) SETC ''
         AIF   ('&STORAGE' EQ 'NO').PRC030
&VDA__STORAGE(&VDA__LEVEL) SETC '&STORAGE'
         AIF   ('&STORAGE' NE '').PRC030
&VDA__STORAGE(&VDA__LEVEL) SETC '&I.STORAGE'
.PRC030  ANOP
.*
.*  If there is a epilog macro, call it.
.*
         AIF   ('&VDA__EPILOG' EQ '').PRC040
         &VDA__EPILOG
.PRC040  ANOP
.*
.*  Redefine opcodes as necessary and decrement VDA level.
.*
.FINISH  AIF   (&VDA__LEVEL NE 1).FIN900
         AIF   ('&VDA__DC' EQ '').FIN010
DC       OPSYN ##DC
##DC     OPSYN
.FIN010  AIF   ('&VDA__DS' EQ '').FIN020
DS       OPSYN ##DS
##DS     OPSYN
.FIN020  AIF   ('&VDA__EQU' EQ '').FIN030
EQU      OPSYN ##EQU
##EQU    OPSYN
.FIN030  ANOP
.FIN900  AIF   (&VDA__LEVEL LT 1).FIN910
&VDA__LEVEL SETA &VDA__LEVEL-1
.FIN910  ANOP
         MEND
