         MACRO
&NAME    SBIT  ,                        SET BIT MACRO
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      SBIT
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - SBIT
.*
.*  PURPOSE    MACRO USED TO SET A BIT.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         LCLA  &POSFLAG                 POSITION OF FLAG
         LCLA  &CTR                     COUNTER
         LCLC  &BYTE                    BYTE NAME
         LCLC  &FLAGS                   CONCATINATION OF FLAGS
         AIF   (N'&SYSLIST NE 1).PRC0100
         AIF   (N'&SYSLIST(1) GT 8).ERR0001  ERROR IF MORE THEN 8 FLAGS
         AIF   (K'&SYSLIST(1,1) LT 6).ERR0003  ERROR IF FLAG < 6 CHARS
&BYTE    SETC  '&SYSLIST(1,1)'(1,5)     BYTE NAME IS FIRST FIVE CHARS
&FLAGS   SETC  '&SYSLIST(1,1)'          MOVE IN FIRST FLAG
&CTR     SETA  2                        LOOP CONCATINATING EACH FLAG
.PRC0050 AIF   (&CTR GT N'&SYSLIST(1)).PRC0060   LEAVE LOOP IF FINISHED
         AIF   (K'&SYSLIST(1,&CTR) LT 6).ERR0003    ERROR
         AIF   ('&SYSLIST(1,&CTR)'(1,5) NE '&BYTE').ERR0004 ERROR
&FLAGS   SETC  '&FLAGS+&SYSLIST(1,&CTR)' CONCATINATE
&CTR     SETA  &CTR+1                   ADD 1 TO COUNTER
         AGO   .PRC0050                 LOOP FOR ALL FLAGS
.PRC0060 ANOP
&NAME    OI    &BYTE,&FLAGS             SET FLAG(S)
         MEXIT
.PRC0100 AIF   (N'&SYSLIST NE 2).ERR0002     ERROR IF MORE THAN 2 PARMS
         AIF   (N'&SYSLIST(2) GT 8).ERR0001  ERROR IF MORE THAN 8 FLAGS
&BYTE    SETC  '&SYSLIST(1)'            SET BYTE NAME
&FLAGS   SETC  '&SYSLIST(2,1)'          MOVE IN FIRST FLAG
&CTR     SETA  2                        LOOP CONCATINATING EACH FLAG
.PRC0150 AIF   (&CTR GT N'&SYSLIST(2)).PRC0160 LEAVE LOOP IF FINISHED
&FLAGS   SETC  '&FLAGS+&SYSLIST(2,&CTR)' CONCATINATE
&CTR     SETA  &CTR+1                   ADD 1 TO COUNTER
         AGO   .PRC0150                 LOOP FOR ALL FLAGS
.PRC0160 ANOP
&NAME    OI    &BYTE,&FLAGS             SET FLAG(S)
         MEXIT
.ERR0001 MNOTE 8,'No more than eight flags can be specified'
         MEXIT
.ERR0002 MNOTE 8,'SBIT macro can have no more than two operands'
         MEXIT
.ERR0003 MNOTE 8,'If byte is not specified, flag name must be at least $
               six characters long'
         MEXIT
.ERR0004 MNOTE 8,'If byte is not specified, the first five characters o$
               f all flags must match'
         MEND
