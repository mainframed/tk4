         MACRO
         ##L   &REG,                    REGISTER TO LOAD               $
               &FIELD,                  FIELD TO LOAD                  $
               &WREG=                   WORK REGISTER
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1989.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##L
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##L
.*
.*  PURPOSE    INTERNAL MACRO TO LOAD A VALUE INTO A REGISTER.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    3/15/89 MGG - NEW
.*
.*--------------------------------------------------------------------*
         LCLC  &TAG                     WORK TAG
         LCLC  &TREG                    WORK FIELD
         AIF   ('&FIELD' NE '').X100
         SLR   &REG,&REG                NO PARAMETER SPECIFIED
         MEXIT
.X100    AIF   (T'&FIELD NE 'N').X200
         AIF   ('&FIELD'(1,1) LT '0' OR '&FIELD'(1,1) GT '9').ERR1
         L     &REG,=F'&FIELD'          LOAD NUMERIC CONSTANT
         MEXIT
.X200    AIF   (K'&FIELD GT 4).X300
         AIF   (K'&FIELD NE 4).X210
         AIF   ('&FIELD'(1,2) NE '*R').X300
         AIF   ('&FIELD'(3,1) NE '1').X300
         AIF   ('&FIELD'(4,1) LT '0').X300
         AIF   ('&FIELD'(4,1) GT '5').X300
&TREG    SETC  '&FIELD'(2,3)
         L     &REG,0(,&TREG)
         MEXIT
.X210    AIF   (K'&FIELD NE 3).X230
         AIF   ('&FIELD'(1,2) NE '*R').X220
         AIF   ('&FIELD'(3,1) LT '0').X300
&TREG    SETC  '&FIELD'(2,2)
         L     &REG,0(,&TREG)
         MEXIT
.X220    AIF   ('&FIELD'(1,1) NE 'R').X300
         AIF   ('&FIELD'(2,1) NE '1').X300
         AIF   ('&FIELD'(3,1) LT '0').X300
         AIF   ('&FIELD'(3,1) GT '5').X300
         AIF   ('&REG' EQ '&FIELD').MEXIT
         LR    &REG,&FIELD
         MEXIT
.X230    AIF   (K'&FIELD NE 2).X300
         AIF   ('&FIELD'(1,1) NE 'R').X300
         AIF   ('&FIELD'(2,1) LT '0').X300
         AIF   ('&REG' EQ '&FIELD').MEXIT
         LR    &REG,&FIELD
         MEXIT
.X300    AIF   ('&FIELD'(1,1) NE '*').X400
&TAG     SETC  '&FIELD'(2,K'&FIELD-1)
         AIF   ('&REG' NE 'R0').X310
         AIF   (T'&WREG EQ 'O').ERR2
         L     &WREG,&TAG
         L     &REG,0(,&WREG.)
         MEXIT
.X310    ANOP
         L     &REG,&TAG
         L     &REG,0(,&REG.)
         MEXIT
.X400    L     &REG,&FIELD              LOAD ADDR OF PARAMETER
         MEXIT
.ERR1    MNOTE 8,'Decimal self-defining terms are the only type of self$
               -defining terms allowed'
         MEXIT
.ERR2    MNOTE 8,'WREG is required if R0,*ADDR is used'
.MEXIT   MEND
