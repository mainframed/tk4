         MACRO
         ##UPSTD &TYPE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##UPSTD
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##UPSTD
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE PROLOG FOR A STANDARD
.*             SWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/06/87 MGG - NEW
.*              5/08/89 MGG - ADDED SWA TAG.                        @C1
.*              6/26/93 MGG - ADDED tags for saved registers.       @C2
.*
.*--------------------------------------------------------------------*
         GBLA  &#SB#C                   SUBROUTINE BLOCK NUMBER
         GBLB  &#SBSWA                  SUBROUTINE BLOCK SWA OPTION
         GBLB  &#SBPSWA                 PARTIAL SWA EXIST
         GBLB  &#PBDYN                  PROGRAM BLOCK DYNAMIC OPTION
         GBLA  &#SBPLMX                 MAX PARMS IN SUBROUTINE
         GBLA  &#SBWAMX                 MAX WORDS OF SMWA NEEDED
         GBLC  &SWA                     SWA TAG                     @C1
         LCLA  &N
.*--------------------------------------------------------------------*
.*  REQUIRE THAT SWA OPTION BE IN EFFECT TO ISSUE THIS MACRO.
.*--------------------------------------------------------------------*
         AIF   (&#SBSWA).PRC0010
         MNOTE 8,'SWA option was not specified on the subroutine block $
               macro or SWA already defined'
.PRC0010 ANOP
&N       SETA  &#SB#C
.*--------------------------------------------------------------------*
.*  IF NOT USING DYN OPTION, PLACE STORAGE FOR SWA IN PWA.
.*--------------------------------------------------------------------*
         AIF   (&#PBDYN).PRC0020
         PWA   TYPE=PARTIAL
         AIF   (&#SBPSWA).PRC0030
#SBD&N   DS    0F                       SWA STORAGE
         AGO   .PRC0030
.PRC0020 ANOP
.*--------------------------------------------------------------------*
.*  IF USING DYN OPTION, USE SEPERATE DSECT FOR SWA.
.*--------------------------------------------------------------------*
#SBD&N   DSECT                          SWA DSECT
.PRC0030 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS THE FIRST CALL FOR THIS SWA, GENERATE FIELDS THAT
.*  GO AT START OF THE SWA.
.*--------------------------------------------------------------------*
         AIF   (&#SBPSWA).PRC0200
&SWA     EQU   #SBD&N                                               @C1
         AIF   (NOT &#PBDYN).PRC0040
#SBN&N   DS    A                        NAB
.PRC0040 ANOP
#SBS&N   DS    0F                       SAVE AREA (R0-R14)
&SWA.SWA_SAVE_AREA_R0 DS F
&SWA.SWA_SAVE_AREA_R1 DS F
&SWA.SWA_SAVE_AREA_R2 DS F
&SWA.SWA_SAVE_AREA_R3 DS F
&SWA.SWA_SAVE_AREA_R4 DS F
&SWA.SWA_SAVE_AREA_R5 DS F
&SWA.SWA_SAVE_AREA_R6 DS F
&SWA.SWA_SAVE_AREA_R7 DS F
&SWA.SWA_SAVE_AREA_R8 DS F
&SWA.SWA_SAVE_AREA_R9 DS F
&SWA.SWA_SAVE_AREA_R10 DS F
&SWA.SWA_SAVE_AREA_R11 DS F
&SWA.SWA_SAVE_AREA_R12 DS F
&SWA.SWA_SAVE_AREA_R13 DS F
&SWA.SWA_SAVE_AREA_R14 DS F
&#SBPSWA SETB  1
.PRC0200 ANOP
.*--------------------------------------------------------------------*
.*  IF TYPE=PARTIAL WAS SPECIFIED, EXIT.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' EQ 'PARTIAL').PRC0900
.PRC0300 ANOP
.*--------------------------------------------------------------------*
.*  IF TYPE=LAST WAS SPECIFIED, GENERATE FIELDS THAT GO JUST BEFORE
.*  USER FIELDS.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0400
&#SBSWA  SETB  0
&#SBPSWA SETB  0
#SBW&N   DS    &#SBWAMX.A               SMWA
#SBP&N   DS    &#SBPLMX.A               PARAMETER LIST STORAGE
#SBU&N   DS    0X                       START OF USER STORAGE
         AGO   .PRC0900
.PRC0400 ANOP
.*--------------------------------------------------------------------*
.*  IT IS AN ERROR IF &TYPE IS OTHER THAN ONE OF THE ABOVE.
.*--------------------------------------------------------------------*
         MNOTE 8,'Invalid type &TYPE specified on SWA macro'
.PRC0900 ANOP
         MEND
