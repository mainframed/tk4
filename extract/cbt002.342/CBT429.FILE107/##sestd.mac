         MACRO
         ##SESTD &RC,                 RETURN CODE KEYWORD              $
               &R0,                     R0                             $
               &R1                      R1
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##SESTD
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##SESTD
.*
.*  PURPOSE    DEFAULT EPILOG MACRO FOR SUBROUTINE BLOCKS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   12/29/86 MGG - BROKEN APART FROM ENDBLK AND RESTRUCTURED
.*
.*--------------------------------------------------------------------*
         GBLA  &#BLVL                   BLOCK LEVEL
         GBLC  &#BAMODE(20)             ADDRESSING MODE OF BLOCK
         GBLB  &#PBPWA                  PROGRAM BLOCK HAS PWA
         GBLC  &#PBNAME                 PROGRAM BLOCK NAME
         GBLB  &#PBXA                   XA OPTION
         GBLB  &#PB370                  370 OPTION
         GBLB  &#PBDYN                  DYN OPTION
         GBLB  &#SBSWA                  SWA OPTION
         GBLB  &#PBLOCR                 LOCTR OPTION
         GBLB  &#SBSU                   SAVEUSING OPTION
         GBLB  &#SBUSWA                 USERSWA OPTION
         GBLA  &#SB#C                   CURRENT SUBROUTINE BLOCK NUM
         GBLA  &#SBPLMX                 MAXIMUM PARMS IN SUBROUTINE
         GBLB  &#SBCSTM                 CUSTOM SUBROUTINE BLOCK
         LCLC  &TAG                     WORK TAG
         LCLC  &TAG2                    WORK TAG
         LCLA  &CNT                     WORK COUNTER
.*--------------------------------------------------------------------*
.*  ISSUE MESSAGE FOR IGNORED KEYWORDS.
.*--------------------------------------------------------------------*
         ##IGMSG &R0,R0
         ##IGMSG &R1,R1
.*--------------------------------------------------------------------*
         AIF   (&#SBCSTM).PRC0700       GO DO CUSTOM STUFF
         AIF   (NOT &#PBXA).PRC0210
         AIF   ('&#BAMODE(&#BLVL)' EQ 'ANY').PRC0210
         AIF   ('&#BAMODE(&#BLVL)' EQ '').PRC0210
         AIF   (NOT &#PB370).PRC0210
         AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0010
         ##SWREG R15                    SAVE WORK REGISTER
.PRC0010 L     R15,16                   POINT R15 TO CVT
         TM    X'74'(R15),X'80'         RUNNING UNDER XA?
.PRC0200 AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0210
         ##RST R15=YES                  RESTORE REGISTERS
         ##RWREG R15                    RESTORE WORK REGISTER
         AGO   .PRC0300
.PRC0210 AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0220
&TAG     ##RST R15=NO                   RESTORE REGISTERS
         AGO   .PRC0300
.PRC0220 AIF   ('&RC'(1,1) NE '(').PRC0230
&TAG     LR    R15,&RC(1)               LOAD RETURN CODE
         ##RST R15=NO                   RESTORE REGISTERS
         AGO   .PRC0300
.PRC0230 ANOP
&TAG     ##RST R15=YES                  RESTORE REGISTERS
         AIF   ('&RC' NE '0').PRC0240
         SLR   R15,R15                  SET RETURN CODE TO 0
         AGO   .PRC0300
.PRC0240 LA    R15,&RC                  LOAD RETURN CODE
.PRC0300 AIF   (NOT &#PBXA).PRC0320
         AIF   ('&#BAMODE(&#BLVL)' EQ 'ANY').PRC0320
         AIF   ('&#BAMODE(&#BLVL)' EQ '').PRC0320
         AIF   (NOT &#PB370).PRC0310
         BZR   R14                      NO, RETURN WITH BR
.PRC0310 ANOP
         BSM   0,R14                    RETURN WITH PREVIOUS AMODE
         AGO   .PRC0390
.PRC0320 ANOP
         BR    R14                      RETURN
.PRC0390 ANOP
         AGO   .PRC0900
.PRC0700 AIF   ('&RC' EQ '(R15)').PRC0900
         AIF   ('&RC' EQ '').PRC0900
         AIF   ('&RC'(1,1) EQ '(').PRC0750
         AIF   ('&RC' EQ '0').PRC0760
         LA    R15,&RC                  LOAD RETURN CODE
         AGO   .PRC0900
.PRC0750 LR    R15,&RC(1)               LOAD RETURN CODE
         AGO   .PRC0900
.PRC0760 SLR   R15,R15                  SET ZERO RETURN CODE
.PRC0900 ANOP
         AIF   (NOT &#SBSU).PRC0950
         POP   USING
.PRC0950 AIF   (NOT &#PBPWA).PRC0965
         AIF   (NOT &#SBSWA).PRC0955
         AIF   (&#SBUSWA).PRC0965
         SWA   ,
         ENDSWA ,
         AGO   .PRC0965
.PRC0955 AIF   (NOT &#PBDYN).PRC0960
#SBL&#SB#C EQU 0                        NO SWA FOR THIS SUBROUTINE
.PRC0960 AIF   (&#SBPLMX EQ 0).PRC0965
         PWA   TYPE=PARTIAL
&TAG     SETC  '#SBP&#SB#C'
&CNT     SETA  &#SBPLMX*4
         DS    0F
&TAG     DS    XL&CNT                   SUBROUTINE PARAMETER STORAGE
         ENDPWA ,
.PRC0965 AIF   (NOT &#PBLOCR).PRC0970
&#PBNAME LOCTR ,
.PRC0970 ANOP
.EXIT    MEND
