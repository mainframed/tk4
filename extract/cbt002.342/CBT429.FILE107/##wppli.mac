         MACRO
         ##WPPLI &TYPE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##WPPLI
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##WPPLI
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE PROLOG FOR A PLI
.*             PWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/06/87 MGG - NEW
.*              5/08/89 MGG - CHANGED &#PBWAID TO &PWA.             @C1
.*
.*--------------------------------------------------------------------*
         GBLA  &#PB#C                   PROGRAM BLOCK NUMBER
         GBLA  &#PBPLMX                 MAX PARMS IN PROGRAM
         GBLB  &#PBPPWA                 PARTIAL PWA ENCOUNTERED
         GBLB  &#PBPWA                  PROGRAM BLOCK PWA OPTION
         GBLC  &PWA                     PWA DSECT TAG               @C1
         GBLA  &#PBWAMX                 MAX WORDS OF SMWA NEEDED
         GBLB  &#SBSWA                  SWA OPTION ON SUBROUTINE BLOCK
         LCLA  &N
         LCLC  &TAG
         LCLC  &D
&D       SETC  '&PWA'                                               @C1
.*--------------------------------------------------------------------*
.*  IF TYPE=LAST, SUPPLY SWA FOR PREVIOUS SUBROUTINE IF MISSING.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0000
         AIF   (NOT &#SBSWA).PRC0000
         SWA   TYPE=LAST                FINISH LAST SWA
         ENDSWA ,
.PRC0000 ANOP
.*--------------------------------------------------------------------*
.*  REQUIRE THAT PWA OPTION BE SPECIFIED FOR PROGRAM.
.*--------------------------------------------------------------------*
         AIF   (&#PBPWA).PRC0010
         MNOTE 8,'PWA option should be specified on the program block m$
               acro'
.PRC0010 ANOP
.*--------------------------------------------------------------------*
.*  GENERATE PWA DSECT CARD.
.*--------------------------------------------------------------------*
&N       SETA  &#PB#C
&PWA DSECT                              PWA DSECT                   @C1
.*--------------------------------------------------------------------*
.*  IF THIS IS THE FIRST CALL FOR THIS PWA, GENERATE DS'S FOR FIELDS
.*  THAT GO AT THE START OF THE PWA.
.*--------------------------------------------------------------------*
.PRC0100 AIF   (&#PBPPWA).PRC0200
&#PBPPWA SETB  1
*=====================================================================*
*  PL/I DYNAMIC STORAGE AREA (PL/I OPTIMIZING COMPILER)               *
*                                                                     *
*  MORE INFORMATION ABOUT THIS CONTROL BLOCK IS CONTAINED IN THE      *
*  OS PL/I OPTIMIZING COMPILER: EXECUTION LOGIC.                      *
*                                                                     *
*=====================================================================*
         SPACE
&D.XF1A  DS    B                        FLAGS (FIRST BYTE)
&D.XF1B  DS    B                        FLAGS (SECOND BYTE)
&D.XOF   DS    H                        OFFSET
&D.XBCH  DS    A                        BACK CHAIN
&D.XRS1  DS    A                        NOT USED
&D.XS14  DS    F                        SAVE AREA R14
&D.XS15  DS    F                        SAVE AREA R15
&D.XS0   DS    F                        SAVE AREA R0
&D.XS1   DS    F                        SAVE AREA R1
&D.XS2   DS    F                        SAVE AREA R2
&D.XS3   DS    F                        SAVE AREA R3
&D.XS4   DS    F                        SAVE AREA R4
&D.XS5   DS    F                        SAVE AREA R5
&D.XS6   DS    F                        SAVE AREA R6
&D.XS7   DS    F                        SAVE AREA R7
&D.XS8   DS    F                        SAVE AREA R8
&D.XS9   DS    F                        SAVE AREA R9
&D.XS10  DS    F                        SAVE AREA R10
&D.XS11  DS    F                        SAVE AREA R11
&D.XS12  DS    F                        SAVE AREA R12
&D.XALWS DS    A                        ADDRESS OF LWS
&D.XANAB DS    A                        ADDRESS OF NAB
&D.XEPNA DS    A                        END OF PROLOG NAB
&D.XBEC  DS    H                        BLOCK-ENABLE CELL
&D.XCEC  DS    H                        CURRENT-ENABLE CELL
&D.XSB   DS    A                        STATIC BACK CHAIN
&D.XFONC DS    A                        ADDRESS OF FIRST ONCB
&D.XCONC DS    A                        ADDRESS OF MOST RESENT ONCB
&D.XRSV2 DS    A                        RESERVED
&D.XRSV3 DS    A                        RESERVED
&D.XRSV4 DS    A                        RESERVED
&D.XAONC DS    A                        ADDRESS OF ONCELLS
&D.XCEXQ DS    X                        SAVE AREA FOR FIRST BYTE OF TCA
&D.XRSV5 DS    X                        RESERVED
&D.XF2   DS    B                        FLAGS
&D.XCT   DS    B                        CT FLAGS
.PRC0200 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=PARTIAL, ALL THAT NEEDS TO BE DONE HAS BEEN DONE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' EQ 'PARTIAL').PRC0900
.PRC0300 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=LAST, GENERATE FIELDS FOR SMWA AND PARAMETER LIST
.*  STORAGE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0400
&#PBPPWA SETB  0
#PBW&N   DS    &#PBWAMX.A               SMWA
#PBP&N   DS    &#PBPLMX.A               PARAMETER LIST STORAGE
#PBU&N   DS    0X                       START OF USER STORAGE
         AGO   .PRC0900
.PRC0400 ANOP
.*--------------------------------------------------------------------*
.*  IF TYPE IS NOT ONE OF THE ABOVE THEN IT IS INVALID.
.*--------------------------------------------------------------------*
         MNOTE 8,'Invalid type &TYPE specified on PWA macro'
.PRC0900 ANOP
         MEND
