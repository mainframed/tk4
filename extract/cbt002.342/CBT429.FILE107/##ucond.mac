         MACRO
&TAG     ##UCOND &EXPR,&S=,&F=,&L=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1989.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##UCOND
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##UCOND
.*
.*  PURPOSE    INTERNAL MACRO USED TO CALL MACRO TO SUPPORT USER
.*             DEFINED CONDITIONS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    9/13/89 MGG - NEW
.*
.*--------------------------------------------------------------------*
         LCLC  &UCOND                   USER CONDITION NAME
         LCLC  &UCOND_NAME
         LCLC  &UCOND_MACRO
         LCLA  &X                       WORK VARIABLE
&X       SETA  K'&EXPR(1)-1
         AIF   (&X LT 1).ERR0001
&UCOND   SETC  '&EXPR(1)'(2,&X)
&UCOND_DEFINED SETC '&UCOND.__DEFINED'
&UCOND_MACRO   SETC '&UCOND.__MACRO'
         GBLB  &(&UCOND_DEFINED)
         GBLC  &(&UCOND_MACRO)
         AIF   (NOT &(&UCOND_DEFINED)).ERR0001
&TAG     &(&UCOND_MACRO) &EXPR,S=&S,F=&F,L=&L
         MEXIT
.ERR0001 MNOTE 8,'Invalid or undefined user condition name: &UCOND'
         MEXIT
         MEND
