         MACRO
         ##WESTD
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##WESTD
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##WESTD
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE EPILOG FOR A STANDARD
.*             PWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/06/87 MGG - NEW
.*              5/08/89 MGG - CHANGED &#PBWAID TO &PWA              @C1
.*
.*--------------------------------------------------------------------*
         GBLA  &#CSF(500)               CALLSUB FROM
         GBLA  &#CSFF(99)               FIRST FROM INDEX
         GBLA  &#CSFT#N                 CALLSUB FROM/TO NEXT NUMBER
         GBLA  &#CST(500)               CALLSUB TO
         GBLA  &#PB#C                   PROGRAM BLOCK NUMBER
         GBLB  &#PBDSWA                 ENDPWA SHOULD DO DS
         GBLB  &#PBDYN                  PROGRAM BLOCK DYN OPTION
         GBLC  &#PBDYNS                 DYNSIZE KEYWORD
         GBLB  &#PBLOCR                 ENDPWA SHOULD DO DS
         GBLC  &#PBNAME                 CSECT NAME OF PROGRAM BLOCK
         GBLB  &#PBPPWA                 PARTIAL PWA ENCOUNTERED
         GBLC  &PWA                     PWA ID                      @C1
         GBLA  &#SB#CPB                 'SUB #' OF CURRENT PROG BLK
         GBLA  &#SB#M                   MAX SUBROUTINE NUMBER
         GBLC  &#SBNM(99)               SUBROUTINE NAMES
         LCLA  &CTR
         LCLA  &N
         LCLC  &TAG
&N       SETA  &#PB#C
.*--------------------------------------------------------------------*
.*  IF THIS IS A PARTIAL PWA, JUST SWITCH BACK TO CSECT.
.*--------------------------------------------------------------------*
.PRC0100 AIF   (&#PBPPWA).PRC1000
.*--------------------------------------------------------------------*
.*  IF DYN OPTION IN EFFECT, RESERVE ROOM FOR SUBROUTINE WORK AREAS.
.*--------------------------------------------------------------------*
         AIF   (NOT &#PBDYN).PRC0800
#PBX&N   DS    0D                   EXTRA STORAGE FOR SWA'S AND PWA'S
         AIF   ('&#PBDYNS' EQ '').PRC0300
         AIF   ('&#PBDYNS'(1,1) EQ '-').PRC0300
         AIF   ('&#PBDYNS'(1,1) EQ '+').PRC0300
         DS    (&#PBDYNS)X
         AGO   .PRC0800
.PRC0300 ANOP
&CTR     SETA  &#SB#CPB
.PRC0310 AIF   (&CTR GT &#SB#M).PRC0315
         MNOTE *,'&#SBNM(&CTR)(&CTR) first from indes is &#CSFF(&CTR)'
&CTR     SETA  &CTR+1
         AGO   .PRC0310
.PRC0315 ANOP
&CTR     SETA  &#SB#CPB
.PRC0320 AIF   (&CTR GE &#CSFT#N).PRC0330
         MNOTE *,'&CTR CALLSUB from &#SBNM(&#CSF(&CTR))(&#CSF(&CTR)) to$
                &#SBNM(&#CST(&CTR))(&#CST(&CTR))'
&CTR     SETA  &CTR+1
         AGO   .PRC0320
.PRC0330 ##ORGS &#SB#CPB,&#SB#CPB       DO ORGS
         ORG   ,
         AIF   ('&#PBDYNS' EQ '').PRC0700
         ORG   *&#PBDYNS
.PRC0700 DS    16F                      64 EXTRA BYTES
.*--------------------------------------------------------------------*
.*  GENERATE LENGTH TAG.
.*--------------------------------------------------------------------*
.PRC0800 DS    0D                       INSURE DOUBLEWORD ALIGNMENT
&TAG     SETC  '&PWA.PLLEN'                                         @C1
&TAG     EQU   *-&PWA                   LENGTH OF PWA               @C1
.*--------------------------------------------------------------------*
.*  GENERATE DS FOR PWA IF NEEDED.
.*--------------------------------------------------------------------*
         AIF   (NOT &#PBDSWA).PRC1000
&#PBNAME CSECT ,
         AIF   (NOT &#PBLOCR).PRC0820
#PBDL&N  LOCTR ,                        PROGRAM STATIC DATA
.PRC0820 ANOP
#PBJ&N   DS    0D                       ALIGN ON DOUBLE WORD
         DS    (&PWA.PLLEN)X            PWA STORAGE AREA            @C1
         AIF   (NOT &#PBLOCR).PRC9000
.PRC1000 ANOP
.*--------------------------------------------------------------------*
.*  SWITCH BACK TO CSECT.
.*--------------------------------------------------------------------*
&#PBNAME CSECT ,
.PRC9000 MEND
