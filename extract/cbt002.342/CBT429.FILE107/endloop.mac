         MACRO
         ENDLOOP &LOOP=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ENDLOOP
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ENDLOOP
.*
.*  PURPOSE    STRUCTURED MACRO TO END A LOOP STRUCTURE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#DMLVL(20)              STRUCTURAL LEVEL OF LOOP
         GBLA  &#DLVL                   LOOP NEXTING LEVEL
         GBLA  &#DNDX(20)               LOOP TAG INDEX
         GBLC  &#DNAME(20)              LOOP NAMES
         GBLC  &#DTYPE(20)              TYPE OF LOOP
         LCLA  &CTR                     WORK COUNT
         LCLC  &FTAG,&STAG,&TAG         FAIL TAG AND SUCCESS TAG
         AIF   (&#DLVL LT 1).ERR0001    IF NOT INSIDE LOOP, ERROR
         AIF   ('&LOOP' EQ '').PRC0010
         AIF   ('&#DNAME(&#DLVL)' NE '&LOOP').ERR0002
.PRC0010 AIF   (&#DMLVL(&#DLVL) NE &#MLVL).ERR0007
&#MLVL   SETA  &#MLVL-1
         AIF   ('&#DTYPE(&#DLVL)' NE '&SYSLIST(1)').ERR0003
&TAG     SETC  '#DB&#DNDX(&#DLVL)'      BEGIN TAG
&STAG    SETC  '#DE&#DNDX(&#DLVL)'      END TAG NAME
         AIF   ('&SYSLIST(1)' NE 'UNTIL').PRC2000
         AIF   ('&SYSLIST(2)' NE 'LEAVE').PRC2100
         B     &TAG                     LOOP UNTIL,LEAVE
&STAG    DS    0H                       END OF UNTIL,LEAVE LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AIF   (N'&SYSLIST NE 2).ERR0006
         AGO   .EXIT
.PRC2100 ANOP
&CTR     SETA  N'&SYSLIST-1             GET NUMBER OF POSITIONAL PARMS
         ##COND &SYSLIST(2),                                           $
               &SYSLIST(3),&SYSLIST(4),&SYSLIST(5),&SYSLIST(6),        $
               &SYSLIST(7),&SYSLIST(8),&SYSLIST(9),&SYSLIST(10),       $
               &SYSLIST(11),&SYSLIST(12),&SYSLIST(13),&SYSLIST(14),    $
               &SYSLIST(15),&SYSLIST(16),&SYSLIST(17),&SYSLIST(18),    $
               &SYSLIST(19),&SYSLIST(20),&SYSLIST(21),&SYSLIST(22),    $
               &SYSLIST(23),&SYSLIST(24),&SYSLIST(25),&SYSLIST(26),    $
               &SYSLIST(27),&SYSLIST(28),&SYSLIST(29),&SYSLIST(30),    $
               &SYSLIST(31),&SYSLIST(32),&SYSLIST(33),                 $
               &SYSLIST(34),&SYSLIST(35),&SYSLIST(36),                 $
               &SYSLIST(37),&SYSLIST(38),&SYSLIST(39),                 $
               &SYSLIST(40),&SYSLIST(41),&SYSLIST(42),                 $
               &SYSLIST(43),&SYSLIST(44),&SYSLIST(45),                 $
               &SYSLIST(46),&SYSLIST(47),&SYSLIST(48),                 $
               &SYSLIST(49),&SYSLIST(50),&SYSLIST(51),&SYSLIST(52),    $
               F=&TAG,S=&STAG,E=&CTR,B=1,T=B
&STAG    DS    0H                       END OF UNTIL LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AGO   .EXIT
.PRC2000 AIF   ('&SYSLIST(1)' NE 'WHILE').PRC3000
         B     &TAG                     LOOP WHILE
&STAG    DS    0H                       END OF WHILE LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AGO   .CHKONE
.PRC3000 AIF   ('&SYSLIST(1)' NE 'BCT').PRC4000
         BCT   &SYSLIST(2),&TAG         LOOP BCT
&STAG    DS    0H                       END OF BCT LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AIF   (N'&SYSLIST NE 2).ERR0004
         AGO   .EXIT
.PRC4000 AIF   ('&SYSLIST(1)' NE 'BXH').PRC5000
         B     &TAG                     LOOP BXH
&STAG    DS    0H                       END OF BXH LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AGO   .CHKONE
.PRC5000 AIF   ('&SYSLIST(1)' NE 'BXLE').ERR0005
&FTAG    SETC  '&SYSLIST(2),&SYSLIST(3),&TAG'
         BXLE  &FTAG                    LOOP BXLE
&STAG    DS    0H                       END OF BXLE LOOP
&#DLVL   SETA  &#DLVL-1                 DECREMENT LOOP LEVEL
         AIF   (N'&SYSLIST NE 3).ERR0004
         AGO   .EXIT
.CHKONE  AIF   (N'&SYSLIST EQ 1).EXIT
         MNOTE 8,'Additional positional parameters can not be specified$
                on ENDLOOP &SYSLIST(1)'
         AGO   .EXIT
.ERR0001 MNOTE 8,'Nesting level for loops is 0'
         AGO   .EXIT
.ERR0002 MNOTE 8,'LOOP= does not match corresponding LOOP'
         AGO   .EXIT
.ERR0003 MNOTE 8,'&SYSLIST(1) does not match type on corresponding LOOP$
               '
         AGO   .EXIT
.ERR0004 MNOTE 8,'Incorrect number of parameters specified for ENDLOOP $
               &SYSLIST(1)'
         AGO   .EXIT
.ERR0005 MNOTE 8,'LOOP type &SYSLIST(1) is invalid'
         AGO   .EXIT
.ERR0006 MNOTE 8,'Incorrect number of parameters specified on ENDLOOP U$
               NTIL,LEAVE'
         AGO   .EXIT
.ERR0007 MNOTE 8,'ENDLOOP structural level is not the same as the corre$
               ponding LOOP'
.EXIT    MEND
