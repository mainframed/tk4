         MACRO
         CASE  &CASEBLK=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      CASE
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - CASE
.*
.*  PURPOSE    INTERNAL MACRO USED TO START CODE FOR A CASE WITHIN
.*             A CASEBLK.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*              9/23/93 - mgg Allow case number up to 501*mult.
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#CMLVL(20)              STRUCTURAL LEVEL CASEBLK
         GBLA  &#CLVL                   CASEBLK LEVEL
         GBLA  &#CNDX(20)               CASEBLK BLOCK TAG INDEX
         GBLB  &#COTH(20)               CASEBLK OTHER ENCOUNTERED
         GBLB  &#CCAS(20)               CASEBLK ENCOUNTERED
         GBLC  &#CNAME(20)              CASEBLK NAMES
         GBLA  &#CMULT(20)              CASEBLK MULT FACTOR
         GBLA  &#CMAXC(20)              MAX CASE NUM IN THIS CASEBLK
         GBLA  &#CTAGS(10020)  20*501   CASE TAGS
         LCLA  &CTR,&NDX                WORK COUNTER & INDEX
         LCLC  &TAG                     WORK TAG
         AIF   ('&CASEBLK' EQ '').PRC0010
         AIF   ('&CASEBLK' NE '&#CNAME(&#CLVL)').ERR0006
.PRC0010 AIF   (&#CLVL EQ 0 OR &#CMLVL(&#CLVL) NE &#MLVL).ERR0007
&CTR     SETA  N'&SYSLIST
         AIF   (&CTR LE 0).ERR0001
         AIF   (NOT &#CCAS(&#CLVL)).PRC0090
         B     #CE&#CNDX(&#CLVL)        LEAVE CASE
.PRC0090 ANOP
&#CCAS(&#CLVL) SETB 1
         AIF   (&CTR NE 1).PRC0100
         AIF   ('&SYSLIST(1)' NE 'OTHER').PRC0100
         AIF   (&#COTH(&#CLVL)).ERR0002
&#COTH(&#CLVL) SETB  1
&TAG     SETC  '#CO&#CNDX(&#CLVL)'
&TAG     DS    0H                       OTHER CASES
         AGO   .EXIT
.PRC0100 ANOP
&#MNDX   SETA  &#MNDX+1
&TAG     SETC  '#CC&#MNDX'
&TAG     DS    0H                       START OF CASE
.PRC0110 ANOP
         AIF   (T'&SYSLIST(&CTR) NE 'N').ERR0003
         AIF   (&SYSLIST(&CTR) GT 500).ERR0004
&NDX     SETA  &SYSLIST(&CTR)/&#CMULT(&#CLVL)
         AIF   (&NDX*&#CMULT(&#CLVL) NE &SYSLIST(&CTR)).ERR0005
         AIF   (&#CTAGS((&#CLVL-1)*501+1+&NDX) EQ 0).PRC0120
         MNOTE 8,'CASE &SYSLIST(&CTR) already used'
.PRC0120 ANOP
&#CTAGS((&#CLVL-1)*501+1+&NDX) SETA &#MNDX
         AIF   (&NDX LE &#CMAXC(&#CLVL)).PRC0130
&#CMAXC(&#CLVL) SETA &NDX
.PRC0130 ANOP
&CTR     SETA  &CTR-1
         AIF   (&CTR GT 0).PRC0110
         AGO   .EXIT
.ERR0001 MNOTE 8,'No operand was specified on CASE statement'
         AGO   .EXIT
.ERR0002 MNOTE 8,'OTHER has already been specified in this CASEBLK'
         AGO   .EXIT
.ERR0003 MNOTE 8,'CASE is not numeric'
         AGO   .EXIT
.ERR0004 MNOTE 8,'CASE number cannot be larger than ''MULT'' * 500'
         AGO   .EXIT
.ERR0005 MNOTE 8,'CASE number must be multiple of ''MULT'''
         AGO   .EXIT
.ERR0006 MNOTE 8,'CASEBLK= does not match CASE name'
         AGO   .EXIT
.ERR0007 MNOTE 8,'CASE macro is not at same structural level as a CASEB$
               LK macro'
.EXIT    MEND
