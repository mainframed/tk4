         MACRO
         ##ASMH  &PASS=FIRST            MACRO TO TEST ASSEMBLER.
.*                                      SHOULD BE CALLED WITH FIELD
.*                                      OF THE FORM ((A))
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1985.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##ASMH
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##ASMH
.*
.*  PURPOSE    INTERNAL MACRO USED BY PLIST AND EXECUTE MACRO TO
.*             TEST TO SEE IF ASSEMBLER H IS ACTIVE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/31/85 PLH - NEW
.*
.*--------------------------------------------------------------------*
         GBLB  &##ASMH                  DECLARE GLOBAL VARIABLE.
&##ASMH  SETB  0                        ASSUME NOT ASSEMBLER H.
         AIF   ('&PASS' NE 'FIRST').X50
.*  PASS THE FIRST ITEM IN THE FIRST LIST BACK TO ##ASMH.  IF THE
.*  LEVEL H ASSEMBLER IS BEING USED, THIS WILL BE A LIST OF ONE
.*  ITEM -->  (A)
.*  IF THE F ASSEMBLER IS BEING USED, THIS WILL BE THE CHARACTER
.*  STRING "(A)"
         ##ASMH &SYSLIST(1,1),PASS=SECOND
         MEXIT
.*  IF THE FIRST CHARACTER IN THE FIRST ITEM OF THE FIRST LIST
.*  IS A PARENTHESIS, THEN THE "(A)" WAS PASSED AS A CHARACTER
.*  STRING AND NOT A LIST.  THEREFORE, THE F ASSEMBLER IS BEING
.*  USED.
.X50     AIF   ('&SYSLIST(1,1)'(1,1) EQ '(').X100
&##ASMH  SETB  1                        IF IS ASSEMBLER H,
.X100    MEND
