         MACRO
&LABEL   ##LDBS &NAME,&BREGS,&DEFREG,&FRSTLD=NO
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##LDBS
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##LDBS
.*
.*  PURPOSE    INTERNAL MACRO USED TO LOAD BASE REGISTERS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    2/14/86 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         GBLA  &#MCVREG
         GBLA  &#MNDX                   MASTER TAG INDEX
         GBLB  &#PBXA                   XA PROGRAM OPTION
         GBLB  &#PB370                  370 PROGRAM OPTION
         LCLC  &FRSTBS                  FIRST BASE REGISTER
         LCLA  &NDX                     LOCAL TAG INDEX
         LCLC  &TAG                     WORK TAG
&#MNDX   SETA  &#MNDX+1                 INCREMENT MASTER TAG INDEX
&NDX     SETA  &#MNDX                   SET MACROS LOCAL TAG INDEX
         AIF   ('&FRSTLD' EQ 'NO').PRC0130
         AIF   ('&NAME' EQ '*').PRC0200
         AIF   ('&BREGS' EQ '').PRC0150
&LABEL   LR    &BREGS(1),R15            LOAD SUBROUTINE BASE REGISTER
         AGO   .PRC1000
         MEXIT
.PRC0130 AIF   ('&BREGS' NE '').PRC1000
         USING &NAME,&DEFREG            ESTABLISH ADDRESSIBILITY
         MEXIT
.PRC0150 ANOP
&LABEL   LR    &DEFREG,R15              LOAD SUBROUTINE BASE REGISTER
         USING &NAME,&DEFREG            ESTABLISH SUBROUTINE BASE
         MEXIT
.PRC0200 AIF   ('&BREGS' EQ '').PRC0250
         AIF   (NOT &#PB370 AND &#PBXA).PRC0210
&LABEL   BALR  &BREGS(1),0              LOAD SUBROUTINE BASE REGISTER
         AGO   .PRC1000
.PRC0210 ANOP
&LABEL   BASR  &BREGS(1),0              LOAD SUBROUTINE BASE REGISTER
         AGO   .PRC1000
.PRC0250 AIF   (NOT &#PB370 AND &#PBXA).PRC0260
&LABEL   BALR  &DEFREG,0                LOAD SUBROUTINE BASE REGISTER
         USING *,&DEFREG                ESTABLISH SUBROUTINE BASE
&FRSTBS  SETC  '&DEFREG'
         AGO   .PRC2000
.PRC0260 ANOP
&LABEL   BASR  &DEFREG,0                LOAD SUBROUTINE BASE REGISTER
         USING *,&DEFREG                ESTABLISH SUBROUTINE BASE
         MEXIT
.PRC1000 ANOP
&CTR     SETA  1
&TAG     SETC  '&BREGS(1)'
         AIF   ('&TAG' NE '').PRC1050
&TAG     SETC  '&DEFREG'
.PRC1050 ANOP
&FRSTBS  SETC  '&BREGS(1)'
         AIF   ('&FRSTLD' EQ 'NO').PRC1110
.PRC1100 ##MREG USE,&BREGS(&CTR)
.PRC1110 AIF   (N'&BREGS LE &CTR).PRC1200
&TAG     SETC  '&TAG,&BREGS(&CTR+1)'
&CTR     SETA  &CTR+1
         AGO   .PRC1100
.PRC1200 USING &NAME,&TAG               ESTABLISH SUBROUTINE BASE
.PRC2000 AIF   (NOT &#PB370 OR NOT &#PBXA OR '&NAME' NE '*').PRC3000
         AIF   ('&FRSTLD' EQ 'NO').PRC3000
#LQ&NDX  ##SWREG R15
         L     R15,X'10'                POINT R15 TO CVT
         TM    X'74'(R15),X'80'         RUNNING ON XA?
         BZ    #LR&NDX                  NO, SKIP NEXT 3 INSTRUCTIONS
         BASR  &FRSTBS,0                LOAD FIRST BASE
         LA    R15,*-#LQ&NDX            ADJUST TO SAME ADDRESS AS BALR
         SLR   &FRSTBS,R15               INSTRUCTION PRODUCED
#LR&NDX  ##RWREG R15
.PRC3000 ANOP
&CTR     SETA  1
.PRC3100 AIF   (N'&BREGS LE &CTR).PRC3200
&TAG     SETC  '&BREGS(&CTR+1),X''800''(,&BREGS(&CTR))'
         LA    &TAG                     LOAD NEXT SUBROUTINE
&TAG     SETC  '&BREGS(&CTR+1),X''800''(,&BREGS(&CTR+1))'
         LA    &TAG                      BASE REGISTER
&CTR     SETA  &CTR+1
         AGO   .PRC3100
.PRC3200 ANOP
         MEND
