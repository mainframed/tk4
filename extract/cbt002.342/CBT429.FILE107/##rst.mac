         MACRO
&NAME    ##RST &R15=YES,&LVL=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##RST
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##RST
.*
.*  PURPOSE    INTERNAL MACRO USED TO RESTORE REGISTERS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*              7/14/84 MGG - DEFINED &CNT
.*              7/15/85 MGG - MADE IT GENERATE L INSTEAD OF LM
.*                            IF ONLY ONE REGISTER IS INVOLVED
.*              1/08/87 MGG - SOME CLEANUP.  REMOVED @CN.
.*
.*--------------------------------------------------------------------*
         GBLA  &#BLVL                   BLOCK LEVEL
         GBLC  &#BSV1(20)               BLOCK SAVE PARM 1
         GBLC  &#BSV2(20)               BLOCK SAVE PARM 2
         GBLC  &#BSV3(20)               BLOCK SAVE PARM 3
         GBLA  &#MCVREG                 CONVERTED REGISTER
         LCLC  &WRK                     WORK FIELD
         LCLC  &O1                      OPERAND 1 WORK FIELD
         LCLC  &O2                      OPERAND 2 WORK FIELD
         LCLC  &O3                      OPERAND 3 WORK FIELD
         LCLA  &WKLVL                   WORK BLOCK LEVEL
         LCLC  &TAG                     WORK TAG
         LCLA  &CNT                     WORK COUNTER
&TAG     SETC  '&NAME'                  SET TAG
&WKLVL   SETA  &#BLVL
         AIF   ('&LVL' EQ '').PRC0010
&WKLVL   SETA  &LVL
.PRC0010 ANOP
         AIF   ('&#BSV1(&WKLVL)' EQ '').PRC2000
         AIF   ('&#BSV3(&WKLVL)' EQ '').PRC1000
         AIF   ('&R15' EQ 'YES').PRC0100
         AIF   ('&#BSV1(&WKLVL)' EQ 'R15').PRC3000
         AIF   ('&#BSV2(&WKLVL)' EQ 'R15').PRC4000
         AIF   (K'&#BSV1(&WKLVL) GT K'&#BSV2(&WKLVL)).PRC5000
         AIF   ('&#BSV1(&WKLVL)' GT '&#BSV2(&WKLVL)').PRC5000
.PRC0100 ANOP
&O1      SETC  '&#BSV1(&WKLVL)'
&O2      SETC  '&#BSV2(&WKLVL)'
&O3      SETC  '&#BSV3(&WKLVL)'
         AGO   .LM
.PRC1000 AIF   ('&R15' EQ 'NO' AND '&#BSV1(&WKLVL)' EQ 'R15').PRC2000
&O1      SETC  '&#BSV1(&WKLVL)'
&O2      SETC  '&#BSV1(&WKLVL)'
&O3      SETC  '&#BSV2(&WKLVL)'
         AGO   .LM
.PRC2000 AIF   ('&TAG' EQ '').EXIT
&TAG     DS    0H                       NO REGISTERS RESTORED
         AGO   .EXIT
.PRC3000 ANOP
&O1      SETC  'R0'
&O2      SETC  '&#BSV2(&WKLVL)'
&O3      SETC  '4+&#BSV3(&WKLVL)'
         AGO   .LM
.PRC4000 ANOP
&O1      SETC  '&#BSV1(&WKLVL)'
&O2      SETC  'R14'
&O3      SETC  '&#BSV3(&WKLVL)'
         AGO   .LM
.PRC5000 ANOP
         AIF   ('&#BSV1(&WKLVL)' EQ 'R14').PRC5010
&WRK     SETC  '&#BSV1(&WKLVL),R14,&#BSV3(&WKLVL)'
&TAG     LM    &WRK                     RESTORE REGISTER(S)
         AGO   .PRC5020
.PRC5010 ANOP
&TAG     L     R14,&#BSV3(&WKLVL)       RESTORE R14
.PRC5020 ANOP
&TAG     SETC  ''
         ##CVREG &#BSV1(&WKLVL)
&CNT     SETA  (16-&#MCVREG)*4
&O1      SETC  'R0'
&O2      SETC  '&#BSV2(&WKLVL)'
&O3      SETC  '&CNT+&#BSV3(&WKLVL)'
.LM      ANOP
         AIF   ('&O1' EQ '&O2').L
&TAG     LM    &O1,&O2,&O3              RESTORE REGISTERS
         AGO   .EXIT
.L       ANOP
&TAG     L     &O1,&O3                  RESTORE REGISTER
.EXIT    MEND
