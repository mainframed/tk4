         MACRO
         ENDCASE  &CASEBLK=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ENDCASE
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ENDCASE
.*
.*  PURPOSE    STRUCTURED MACRO USED TO END A CASE BLOCK.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - mgg NEW
.*              9/23/93 - mgg Allow case number up to 501*mult.
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#CMLVL(20)              STRUCTURAL LEVEL FOR CASEBLK
         GBLA  &#CLVL                   CASEBLK LEVEL
         GBLA  &#CNDX(20)               CASEBLK BLOCK TAG INDEX
         GBLB  &#COTH(20)               CASEBLK OTHER ENCOUNTERED
         GBLB  &#CCAS(20)               CASEBLK ENCOUNTERED
         GBLC  &#CNAME(20)              CASEBLK NAMES
         GBLA  &#CMULT(20)              CASEBLK MULT FACTOR
         GBLA  &#CMAXC(20)              MAX CASE NUM IN THIS CASEBLK
         GBLA  &#CTAGS(10020)  20*501   CASE TAGS
         LCLA  &CTR                     WORK COUNTER
         LCLC  &TAG                     WORK TAG
         AIF   (&#CLVL LE 0).ERR0002
         AIF   (&#CMLVL(&#CLVL) NE &#MLVL).ERR0003
&#MLVL   SETA  &#MLVL-1
         AIF   ('&CASEBLK' EQ '').PRC0010
         AIF   ('&CASEBLK' NE '&#CNAME(&#CLVL)').ERR0001
.PRC0010 ANOP
         B     #CE&#CNDX(&#CLVL)
&TAG     SETC  '#CM&#CNDX(&#CLVL)'
&TAG     DC    AL2(&#CMAXC(&#CLVL)*2)
&TAG     SETC  '#CT&#CNDX(&#CLVL)'
&CTR     SETA  0
.PRC0100 AIF   (&CTR GT &#CMAXC(&#CLVL)).PRC0200
         AIF   (&#CTAGS((&#CLVL-1)*501+1+&CTR) NE 0).PRC0120
&TAG     DC    AL2(#CO&#CNDX(&#CLVL)-#CS&#CNDX(&#CLVL))
         AGO   .PRC0140
.PRC0120 ANOP
&TAG     DC    AL2(#CC&#CTAGS((&#CLVL-1)*501+1+&CTR)-#CS&#CNDX(&#CLVL))
.PRC0140 ANOP
&TAG     SETC  ''
&CTR     SETA  &CTR+1
         AGO   .PRC0100
.PRC0200 ANOP
         AIF   (&#COTH(&#CLVL)).PRC0210
&TAG     SETC  '#CO&#CNDX(&#CLVL)'
&TAG     DS    0H                       NO CASE OTHER
.PRC0210 ANOP
&TAG     SETC  '#CE&#CNDX(&#CLVL)'
&TAG     DS    0H                       END OF CASEBLK
&#CLVL   SETA  &#CLVL-1
         AGO   .EXIT
.ERR0001 MNOTE 8,'CASEBLK= does not match CASE name'
         MEXIT
.ERR0002 MNOTE 8,'CASEBLK nesting level is zero'
         MEXIT
.ERR0003 MNOTE 8,'Structural nesting level of ENDCASE is different than$
                corresponding CASEBLK macro'
.EXIT    MEND
