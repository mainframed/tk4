         MACRO
         VDA   &ID=,&GENTYPE=,&DSECT=,&START=,&TYPE=LAST,&ALIGN=F
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1991.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      VDA
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - VDA
.*
.*  PURPOSE    MACRO TO START A VARIABLE DEFINITION AREA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    8/06/91 MGG - NEW
.*
.*--------------------------------------------------------------------*
         GBLC  &VDA__ID(20)
         GBLC  &VDA__START(20)
         GBLC  &VDA__ALIGN(20)
         GBLC  &VDA__GENTYPE
         GBLC  &VDA__CSECT
         GBLA  &VDA__LEVEL
         GBLC  &VDA__PROLOG
         GBLC  &VDA__EPILOG
         GBLC  &VDA__DC
         GBLC  &VDA__DS
         GBLC  &VDA__EQU
.*
.*  Initialize variables.
.*
&VDA__LEVEL   SETA &VDA__LEVEL+1
&VDA__ID(&VDA__LEVEL)    SETC  '&ID'
.*
&VDA__START(&VDA__LEVEL) SETC  '&START'
&VDA__ALIGN(&VDA__LEVEL) SETC  '&ALIGN'
         AIF   ('&START' NE '').PRC010
&VDA__START(&VDA__LEVEL) SETC  '&ID.START'
.PRC010  AIF   (&VDA__LEVEL EQ 1).PRC020
         MNOTE *,'DSECT, GENTYPE and TYPE are ignored for nested VDAs.'
         AGO   .PROLOG
.PRC020  ANOP
&VDA__CSECT   SETC '&SYSECT'
&VDA__GENTYPE SETC ''
&VDA__PROLOG  SETC ''
&VDA__EPILOG  SETC ''
&VDA__DC      SETC ''
&VDA__DS      SETC ''
&VDA__EQU     SETC ''
.*
.*  Process based on GENTYPE and DSECT.
.*
         AIF   ('&DSECT' EQ '').SEL100
         AIF   ('&GENTYPE' EQ '').SEL050
         MNOTE *,'DSECT=&DSECT ignored because was GENTYPE specified.'
         AGO   .SEL100
.SEL050  AIF   ('&DSECT' EQ 'YES').DSECT
         AIF   ('&DSECT' EQ 'NO').INLINE
         MNOTE 4,'DSECT=&DSECT is not valid.  DSECT=NO assumed.'
         AGO   .INLINE
.SEL100  AIF   ('&GENTYPE' EQ '').INLINE
         AIF   ('&GENTYPE' EQ 'INLINE').INLINE
         AIF   ('&GENTYPE' EQ 'GENERATE').INLINE
         AIF   ('&GENTYPE' EQ 'DSECT').DSECT
         AIF   ('&GENTYPE(1)'(1,1) EQ '%').VGTYPE
         MNOTE 8,'GENTYPE=&GENTYPE is not valid.'
         MEXIT
.*
.*  Process user defined GENTYPE.
.*
.VGTYPE  ANOP
&X       SETA  K'&GENTYPE(1)-1
         AIF   (&X GE 1).VGT050
         MNOTE 8,'GENTYPE=&GENTYPE is not valid.'
         MEXIT
.VGT050  ANOP
&NAME    SETC  '&GENTYPE(1)'(2,&X)
&VGTYP_DEFINED SETC '&NAME.__DEFINED'
&VGTYP_PROLOG  SETC '&NAME.__PROLOG'
&VGTYP_EPILOG  SETC '&NAME.__EPILOG'
&VGTYP_DC      SETC '&NAME.__DC'
&VGTYP_DS      SETC '&NAME.__DS'
&VGTYP_EQU     SETC '&NAME.__EQU'
         GBLB  &(&VGTYP_DEFINED)
         GBLC  &(&VGTYP_PROLOG)
         GBLC  &(&VGTYP_EPILOG)
         GBLC  &(&VGTYP_DC)
         GBLC  &(&VGTYP_DS)
         GBLC  &(&VGTYP_EQU)
         AIF   (&(&VGTYP_DEFINED)).VGT100
         MNOTE 8,'No DEFDGTYP macro exists for GENTYPE=&GENTYPE.'
         MEXIT
.VGT100  ANOP
&VDA__PROLOG SETC '&(&VGTYP_PROLOG)'
&VDA__EPILOG SETC '&(&VGTYP_EPILOG)'
&VDA__DC     SETC '&(&VGTYP_DC)'
&VDA__DS     SETC '&(&VGTYP_DS)'
&VDA__EQU    SETC '&(&VGTYP_EQU)'
         AGO   .PROLOG
.*
.*  Process GENTYPE=INLINE.
.*
.INLINE  ANOP
&VDA__GENTYPE SETC 'INLINE'
&VDA__PROLOG  SETC '##VPINLN'
&VDA__EPILOG  SETC '##VEINLN'
         AGO   .PROLOG
.*
.*  Process GENTYPE=DSECT.
.*
.DSECT   ANOP
&VDA__GENTYPE SETC 'DSECT'
&VDA__PROLOG  SETC '##VPDSCT'
&VDA__EPILOG  SETC '##VEDSCT'
         AGO   .PROLOG
.*
.*  Do OPSYNs as necessary and call prolog macro.
.*
.PROLOG  AIF   (&VDA__LEVEL NE 1).PRO500
         AIF   ('&VDA__DC' EQ '').PRO110
##DC     OPSYN DC
DC       OPSYN
.PRO110  AIF   ('&VDA__DS' EQ '').PRO120
##DS     OPSYN DS
DS       OPSYN
.PRO120  AIF   ('&VDA__EQU' EQ '').PRO130
##EQU    OPSYN EQU
EQU      OPSYN
.PRO130  ANOP
.PRO500  AIF   ('&VDA__PROLOG' EQ '').PRO510
         &VDA__PROLOG GENTYPE=&GENTYPE
.PRO510  ANOP
.*
         MEND
