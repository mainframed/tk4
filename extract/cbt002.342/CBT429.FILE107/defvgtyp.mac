         MACRO
         DEFVGTYP &NAME=,&PROLOG=,&EPILOG=,&DS=,&DC=,&EQU=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1991.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      DEFVGTYP
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - DEFVGTYP
.*
.*  PURPOSE    STRUCTURED MACRO TO DEFINE A VDA GENTYPE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    8/05/91 MGG - NEW
.*
.*--------------------------------------------------------------------*
         LCLC  &VGTYP_DEFINED
         LCLC  &VGTYP_MACRO
         AIF   ('&NAME' EQ '').ERR0001
&VGTYP_DEFINED SETC '&NAME.__DEFINED'
&VGTYP_PROLOG  SETC '&NAME.__PROLOG'
&VGTYP_EPILOG  SETC '&NAME.__EPILOG'
&VGTYP_DC      SETC '&NAME.__DC'
&VGTYP_DS      SETC '&NAME.__DS'
&VGTYP_EQU     SETC '&NAME.__EQU'
         GBLB  &(&VGTYP_DEFINED)
         GBLC  &(&VGTYP_PROLOG)
         GBLC  &(&VGTYP_EPILOG)
         GBLC  &(&VGTYP_DC)
         GBLC  &(&VGTYP_DS)
         GBLC  &(&VGTYP_EQU)
&(&VGTYP_DEFINED) SETB 1
&(&VGTYP_PROLOG)  SETC '&PROLOG'
&(&VGTYP_EPILOG)  SETC '&EPILOG'
&(&VGTYP_DC)      SETC '&DC'
&(&VGTYP_DS)      SETC '&DS'
&(&VGTYP_EQU)     SETC '&EQU'
         MEXIT
.ERR0001 MNOTE 8,'NAME= must be specified'
         MEXIT
         MEND
