         MACRO
         ##SUBOPT &OPTIONS,             OPTIONS                        $
               &DEFAULT=NO              DO NOT SET DEFAULTS
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##SUBOPT
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##SUBOPT
.*
.*  PURPOSE    INTERNAL MACRO USED AS PARSE THE SUBROUTINE STANDARD
.*             SUBROUTINE OPTIONS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   12/18/86 MGG - BROKEN APART FROM THE BLOCK MACRO AND
.*                            RESTRUCURED.
.*              5/08/89 MGG - ADDED PARMS OPTION SUPPORT.           @C1
.*
.*--------------------------------------------------------------------*
         GBLB  &#SBCSTM                 CUSTOM SUBROUTINE
         GBLB  &#SBLDBS                 LOAD BASE OPTION
         GBLB  &#SBSU                   SAVEUSING OPTION
         GBLB  &#SBSWA                  SWA OPTION
         GBLB  &#SBUSWA                 USERSWA OPTION
         GBLB  &#SDCSTM                 DEFAULT CUSTOM OPTION
         GBLB  &#SDLDBS                 DEFAULT LOAD BASE OPTION
         GBLB  &#SDSU                   DEFAULT SAVEUSING OPTION
         GBLB  &#SDSWA                  DEFAULT SWA OPTION
         GBLB  &#SDUSWA                 DEFAULT USERSWA OPTION
         LCLB  &CSTM                    CUSTOM SUBROUTINE
         LCLB  &LDBS                    LOAD BASE OPTION
         LCLB  &SU                      SAVEUSING OPTION
         LCLB  &SWA                     SWA OPTION
         LCLB  &USWA                    USERSWA OPTION
.*------------------------------------------------------------------
.*  IF NOT DEFAULT=YES, SET OPTIONS TO DEFAULT; OTHERWISE, RESET.   @C1
.*------------------------------------------------------------------
         AIF   ('&DEFAULT' EQ 'YES').SDO0050                        @C1
&CSTM    SETB  (&#SDCSTM)               SET CUSTOM TO DEFAULT
&SWA     SETB  (&#SDSWA)                SET SWA TO DEFAULT
&SU      SETB  (&#SDSU)                 SET SAVEUSING TO DEFAULT
&USWA    SETB  (&#SDUSWA)               SET USERSWA TO DEFAULT
&LDBS    SETB  (&#SDLDBS)               SET LOADBASE TO DEFAULT
         ##PRMOPT PROCESS=SET_TO_SUBROUTINE_DEFAULTS,               @C1+
               TYPE=SUBROUTINE                                      @C1
         AGO   .SDO0100                                             @C1
.SDO0050 ANOP                                                       @C1
&CSTM    SETB  0                        SET NOCUSTOM                @C1
&SWA     SETB  0                        SET NOSWA                   @C1
&SU      SETB  0                        SET NOSAVEUSING             @C1
&USWA    SETB  0                        SET NOUSERSWA               @C1
&LDBS    SETB  0                        SET NOLOADBASE              @C1
         ##PRMOPT PROCESS=RESET,TYPE=SUBROUTINE SET NOPARMS         @C1
.SDO0100 ANOP
.*------------------------------------------------------------------
.*  SET SUBROUTINE OPTIONS BASE ON SUBOPT
.*------------------------------------------------------------------
&CTR     SETA  1                        SET COUNTER TO 1
.SDO1000 AIF   (&CTR GT N'&OPTIONS).SDO7000
.SDO1100 AIF   ('&OPTIONS(&CTR)' NE 'CUSTOM').SDO1110
&CSTM    SETB  1                        INDICATE CUSTOM SUBROUTINE
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1110 AIF   ('&OPTIONS(&CTR)' NE 'NOCUSTOM').SDO1120
&CSTM    SETB  0                        INDICATE NOT CUSTOM SUB
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1120 AIF   ('&OPTIONS(&CTR)' NE 'SWA').SDO1130
&SWA     SETB  1                        INDICATE SWA
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1130 AIF   ('&OPTIONS(&CTR)' NE 'NOSWA').SDO1140
&SWA     SETB  0                        INDICATE NOSWA
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1140 AIF   ('&OPTIONS(&CTR)' NE 'SAVEUSING').SDO1150
&SU      SETB  1                        INDICATE SAVEUSING
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1150 AIF   ('&OPTIONS(&CTR)' NE 'NOSAVEUSING').SDO1160
&SU      SETB  0                        INDICATE NOSAVEUSING
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1160 AIF   ('&OPTIONS(&CTR)' NE 'USERSWA').SDO1170
&USWA    SETB  1                        INDICATE USERSWA
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1170 AIF   ('&OPTIONS(&CTR)' NE 'NOUSERSWA').SDO1180
&USWA    SETB  0                        INDICATE NOUSERSWA
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1180 AIF   ('&OPTIONS(&CTR)' NE '*SWA').SDO1190
&SWA     SETB  1                        INDICATE SWA
&SU      SETB  1                        INDICATE SAVEUSING
&USWA    SETB  1                        INDICATE USERSWA
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1190 AIF   ('&OPTIONS(&CTR)' NE '*NOSUBOPTS').SDO1200
&SWA     SETB  0                        INDICATE NOSWA
&SU      SETB  0                        INDICATE NOSAVEUSING
&USWA    SETB  0                        INDICATE NOUSERSWA
&LDBS    SETB  0                        INDICATE NOLOADBASE
         ##PRMOPT PROCESS=RESET,TYPE=SUBROUTINE                     @C1
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1200 AIF   ('&OPTIONS(&CTR)' NE 'LOADBASE').SDO1210
&LDBS    SETB  1                        INDICATE LOADBASE
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1210 AIF   ('&OPTIONS(&CTR)' NE 'NOLOADBASE').SDO1220
&LDBS    SETB  0                        INDICATE NOLOADBASE
         AGO   .SDO1900                 CHECK NEXT OPTION
.SDO1220 AIF   ('&OPTIONS(&CTR,1)' NE 'PARMS').SDO1230              @C1
         ##PRMOPT PROCESS=PARSE_OPTIONS,OPTIONS=&OPTIONS(&CTR),     @C1+
               TYPE=SUBROUTINE                                      @C1
         AGO   .SDO1900                 CHECK NEXT OPTION           @C1
.SDO1230 AIF   ('&OPTIONS(&CTR)' NE 'NOPARMS').SDO1240              @C1
         ##PRMOPT PROCESS=RESET,TYPE=SUBROUTINE                     @C1
         AGO   .SDO1900                 CHECK NEXT OPTION           @C1
.SDO1240 ANOP                                                       @C1
         MNOTE 8,'&OPTIONS(&CTR) is an invalid option for subroutine bl$
               ock'
.SDO1900 ANOP
&CTR     SETA  &CTR+1                   INCREMENT COUNTER
         AGO   .SDO1000                 LOOP
.SDO7000 ANOP
.*--------------------------------------------------------------------*
.*  IF DEFAULT=YES SET DEFAULTS; OTHERWISE, SET ACTUAL VALUES.
.*--------------------------------------------------------------------*
         AIF   ('&DEFAULT' NE 'YES').SDO7010
&#SDCSTM SETB  (&CSTM)
&#SDUSWA SETB  (&USWA)
&#SDSU   SETB  (&SU)
&#SDSWA  SETB  (&SWA)
&#SDLDBS SETB  (&LDBS)
         ##PRMOPT PROCESS=SET_SUBROUTINE_DEFAULTS,TYPE=SUBROUTINE   @C1
         AGO   .SDO7020
.SDO7010 ANOP
&#SBCSTM SETB  (&CSTM)
&#SBUSWA SETB  (&USWA)
&#SBSU   SETB  (&SU)
&#SBSWA  SETB  (&SWA)
&#SBLDBS SETB  (&LDBS)
.SDO7020 ANOP
.*--------------------------------------------------------------------*
         MEND
