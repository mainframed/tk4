         MACRO
         LOOP  &NAME=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      LOOP
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - LOOP
.*
.*  PURPOSE    STRUCTURED MACRO USED TO START A LOOP STRUCTURE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#DMLVL(20)              STRUCTURAL LEVEL OF LOOP
         GBLA  &#DLVL                   LOOP NEXTING LEVEL
         GBLA  &#DNDX(20)               LOOP TAG INDEX
         GBLC  &#DNAME(20)              LOOP NAMES
         GBLC  &#DTYPE(20)              TYPE OF LOOP
         LCLA  &CTR                     WORK COUNT
         LCLC  &FTAG,&STAG,&TAG         FAIL TAG AND SUCCESS TAG
&#MNDX   SETA  &#MNDX+1                 ADD ONE TO TAG INDEX
         AIF   (&#DLVL GE 20).ERR0001
&#DLVL   SETA  &#DLVL+1                 ADD ONE TO NESTING LEVEL
&#MLVL   SETA  &#MLVL+1                 ADD ONE TO STRUCTURAL LEVEL
&#DMLVL(&#DLVL) SETA  &#MLVL            SAVE STRUCTURAL LEVEL
&#DNDX(&#DLVL)  SETA  &#MNDX            SET BLOCK TAG INDEX
&#DNAME(&#DLVL) SETC '&NAME'            SAVE NAME
&#DTYPE(&#DLVL) SETC '&SYSLIST(1)'      SAVE TYPE
&TAG     SETC  '#DB&#MNDX'              SUCCESS TAG NAME
         AIF   ('&SYSLIST(1)' NE 'WHILE').PRC2000
&FTAG    SETC  '#DE&#MNDX'              FAIL TAG NAME (END OF LOOP)
&STAG    SETC  '#DS&#MNDX'              SUCCESS TAG NAME
&CTR     SETA  N'&SYSLIST-1             GET NUMBER OF POSITIONAL PARMS
&TAG     ##COND &SYSLIST(2),                                           $
               &SYSLIST(3),&SYSLIST(4),&SYSLIST(5),&SYSLIST(6),        $
               &SYSLIST(7),&SYSLIST(8),&SYSLIST(9),&SYSLIST(10),       $
               &SYSLIST(11),&SYSLIST(12),&SYSLIST(13),&SYSLIST(14),    $
               &SYSLIST(15),&SYSLIST(16),&SYSLIST(17),&SYSLIST(18),    $
               &SYSLIST(19),&SYSLIST(20),&SYSLIST(21),&SYSLIST(22),    $
               &SYSLIST(23),&SYSLIST(24),&SYSLIST(25),&SYSLIST(26),    $
               &SYSLIST(27),&SYSLIST(28),&SYSLIST(29),&SYSLIST(30),    $
               &SYSLIST(31),&SYSLIST(32),&SYSLIST(33),                 $
               &SYSLIST(34),&SYSLIST(35),&SYSLIST(36),                 $
               &SYSLIST(37),&SYSLIST(38),&SYSLIST(39),                 $
               &SYSLIST(40),&SYSLIST(41),&SYSLIST(42),                 $
               &SYSLIST(43),&SYSLIST(44),&SYSLIST(45),                 $
               &SYSLIST(46),&SYSLIST(47),&SYSLIST(48),                 $
               &SYSLIST(49),&SYSLIST(50),&SYSLIST(51),&SYSLIST(52),    $
               F=&FTAG,S=&STAG,E=&CTR,B=1,T=B
&STAG    DS    0H                       START OF LOOP WHILE CODE
         AGO   .EXIT
.PRC2000 AIF   ('&SYSLIST(1)' NE 'UNTIL').PRC3000
&TAG     DS    0H                       START OF LOOP UNTIL
         AGO   .CHKONE
.PRC3000 AIF   ('&SYSLIST(1)' NE 'BCT').PRC4000
&TAG     DS    0H                       START OF LOOP BCT
         AGO   .CHKONE
.PRC4000 AIF   ('&SYSLIST(1)' NE 'BXH').PRC5000
&FTAG    SETC  '&SYSLIST(2),&SYSLIST(3),#DE&#MNDX'
&TAG     BXH   &FTAG                    START OF LOOP BXH
         AIF   (N'&SYSLIST NE 3).ERR0002
         AGO   .EXIT
.PRC5000 AIF   ('&SYSLIST(1)' NE 'BXLE').ERR0003
&TAG     DS    0H                       START OF LOOP BXLE
         AGO   .EXIT
.CHKONE  AIF   (N'&SYSLIST EQ 1).EXIT
         MNOTE 8,'Additional positional parameters can not be specified$
                on LOOP &SYSLIST(1)'
         AGO   .EXIT
.ERR0001 MNOTE 8,'LOOPs can only be nested 20 deep'
         AGO   .EXIT
.ERR0002 MNOTE 8,'Incorrect number of parameters specified for LOOP BXH$
               '
         AGO   .EXIT
.ERR0003 MNOTE 8,'LOOP type &SYSLIST(1) is invalid'
.EXIT    MEND
