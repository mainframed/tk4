         MACRO
         ##WPDC &TYPE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1986.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##WPDC
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##WPDC
.*
.*  PURPOSE    INTERNAL MACRO USED TO GENERATE PROLOG FOR AN IDMS/DC
.*             PWA.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/06/87 MGG - BROKE APART FROM PWA MACRO AND
.*                            REORGANIZED.
.*              5/08/89 MGG - CHANGED &#PBWAID TO &PWA.             @C1
.*
.*--------------------------------------------------------------------*
         GBLA  &#PB#C                   PROGRAM BLOCK NUMBER
         GBLB  &#PBDYN                  PROGRAM BLOCK DYN OPTION
         GBLB  &#PBPWA                  PROGRAM BLOCK PWA OPTION
         GBLB  &#PBPPWA                 PARTIAL PWA ENCOUNTERED
         GBLB  &#SBSWA                  SWA OPTION ON SUBROUTINE BLOCK
         GBLA  &#PBPLMX                 MAX PARMS IN PROGRAM
         GBLA  &#PBWAMX                 MAX WORDS OF SMWA NEEDED
         GBLC  &PWA                     PWA ID                      @C1
         LCLA  &N
         LCLC  &TAG
.*--------------------------------------------------------------------*
.*  IF TYPE=LAST, SUPPLY SWA FOR PREVIOUS SUBROUTINE IF MISSING.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0000
         AIF   (NOT &#SBSWA).PRC0000
         SWA   TYPE=LAST                FINISH LAST SWA
         ENDSWA ,
.PRC0000 ANOP
.*--------------------------------------------------------------------*
.*  REQUIRE THAT PWA OPTION BE SPECIFIED FOR PROGRAM.
.*--------------------------------------------------------------------*
         AIF   (&#PBPWA).PRC0010
         MNOTE 8,'PWA option should be specified on the program block m$
               acro'
.PRC0010 ANOP
.*--------------------------------------------------------------------*
.*  GENERATE PWA DSECT CARD.
.*--------------------------------------------------------------------*
&N       SETA  &#PB#C
&PWA     DSECT                          PWA DSECT                   @C1
.*--------------------------------------------------------------------*
.*  IF THIS IS THE FIRST CALL FOR THIS PWA, GENERATE DS'S FOR FIELDS
.*  THAT GO AT THE START OF THE PWA.
.*--------------------------------------------------------------------*
.PRC0100 AIF   (&#PBPPWA).PRC0200
         AIF   (NOT &#PBDYN).PRC0150
#PBE&N   DS    A                        EOS - 64
#PBR&N   DS    A                        RESERVED
#PBN&N   DS    A                        NAB (FIRST SWA)
.PRC0150 ANOP
&#PBPPWA SETB  1
.PRC0200 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=PARTIAL, ALL THAT NEEDS TO BE DONE HAS BEEN DONE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' EQ 'PARTIAL').PRC0900
.PRC0300 ANOP
.*--------------------------------------------------------------------*
.*  IF THIS IS TYPE=LAST, GENERATE FIELDS FOR SMWA AND PARAMETER LIST
.*  STORAGE.
.*--------------------------------------------------------------------*
         AIF   ('&TYPE' NE 'LAST').PRC0400
&#PBPPWA SETB  0
#PBW&N   DS    &#PBWAMX.A               SMWA
#PBP&N   DS    &#PBPLMX.A               PARAMETER LIST STORAGE
#PBU&N   DS    0X                       START OF USER STORAGE
         AGO   .PRC0900
.PRC0400 ANOP
.*--------------------------------------------------------------------*
.*  IF TYPE IS NOT ONE OF THE ABOVE THEN IT IS INVALID.
.*--------------------------------------------------------------------*
         MNOTE 8,'Invalid type &TYPE specified on PWA macro'
.PRC0900 ANOP
         MEND
