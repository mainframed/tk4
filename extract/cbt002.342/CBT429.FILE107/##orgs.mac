         MACRO
         ##ORGS &F,            ORGS FOR                                $
               &C              WHEN CALLED FROM
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1985.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##ORGS
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##ORGS
.*
.*  PURPOSE    INTERNAL MACRO USED PRODUCE ORGS TO DETERMINE MAXIMUM
.*             SWA STORAGE NEEDED
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    7/14/85 MGG - NEW
.*
.*--------------------------------------------------------------------*
         GBLA  &#CSF(500)                CALLSUB FROM
         GBLA  &#CSFF(99)                FIRST FOR SUBROUTINE FROM
         GBLA  &#CSFT#N                  CALLSUB FROM/TO NEXT NUMBER
         GBLA  &#CST(500)                CALLSUB TO
         GBLA  &#CSVST(99)               SUBROUTINES VISITED
         GBLC  &#SBNM(99)                SUBROUTINE NAMES
         GBLA  &#SBORGC                  ORG COUNTER
         GBLA  &#SBREC(99)               SUBROUTINE RECUR NUMBER
         LCLA  &CTR                      LOCAL WORK VARIABLE
         LCLC  &TAG                      LOCAL WORK TAG
         MNOTE *,'For subroutine &#SBNM(&F)(&F) when called from subrou$
               tine &#SBNM(&C)(&C)'
&#CSVST(&F) SETA &#CSVST(&F)+1
&CTR     SETA  &#CSFF(&F)
         AIF   (&CTR EQ 0).FINISH
&TAG     SETC  ''
.LOOP    AIF   (&CTR GE &#CSFT#N).FINISH
         AIF   (&#CSF(&CTR) NE &F).FINISH
         AIF   (&#CST(&CTR) EQ &F).NEXT
         AIF   (&#CSVST(&#CST(&CTR)) GT 1).NEXT
         AIF   (&#CSVST(&#CST(&CTR)) EQ 1).PASTORG
         AIF   ('&TAG' NE '').GENORG
&TAG     SETC  '#SB$&#SBORGC'
&#SBORGC SETA  &#SBORGC+1
&TAG     EQU   *
.GENORG  ORG   &TAG+#SBL&#CST(&CTR)*&#SBREC(&#CST(&CTR))
.PASTORG ##ORGS &#CST(&CTR),&F
.NEXT    ANOP
&CTR     SETA  &CTR+1
         AGO   .LOOP
.FINISH  ANOP
&#CSVST(&F) SETA &#CSVST(&F)-1
         MEND
