         MACRO
&NAME    SMCTRL &FREEPWA=               SET BIT MACRO
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      SMCTRL
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - SMCTRL
.*
.*  PURPOSE    MACRO USED TO SET CONTROL INFORMATION IN THE
.*             STRUCTURED MACROS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    4/23/87 - NEW  MGG
.*              3/28/91 - Added support for MANUALFREE option.      @C1
.*              2/19/92 - Fixed bug that ignored parm number for    @C2
.*                        reusable PWA.                             @C2
.*
.*--------------------------------------------------------------------*
         GBLB  &#PBRPWA                 REUSABLE PWA OPTION
         GBLA  &#PBPWAP                 REUSABLE PWA PARM NUMBER    @C2
         GBLB  &#PBMFRE                 MANUALFREE OPTION           @C1
         GBLC  &PWA                     PWA TAG                     @C1
.*====================================================================*
.*   FREEPWA KEYWORD
.*====================================================================*
         AIF  ('&FREEPWA' EQ '').FPW9999
         AIF  (&#PBRPWA OR &#PBMFRE).FPW0100                        @C1
         MNOTE 8,'The FREEPWA keyword is only valid if the REUSPWA or M$
               ANUALFREE option is in effect'                       @C1
.FPW0100 AIF  (&#PBMFRE).FPW1000                                    @C1
         ##SWREG R15
         L     R15,4(,R13)              LOAD R15 WITH BACKLINK
         L     R15,24(,R15)             LOAD R15 WITH R1 AT ENTRY
         L     R15,(&#PBPWAP-1)*4(,R15) LOAD R15 WITH PTR TO ANCHOR @C2
         AIF   ('&FREEPWA' NE 'YES').FPW0110
         OI    0(R15),X'80'             INDICATE PWA SHOULD BE FREED
         AGO   .FPW0200
.FPW0110 AIF   ('&FREEPWA' EQ 'NO').FPW0120
         MNOTE 8,'FREEPWA=&FREEPWA is not valid'
.FPW0120 NI    0(R15),X'7F'             INDICATE PWA SHOULD BE KEPT
.FPW0200 ##RWREG R15
         AGO   .FPW9999                                             @C1
.FPW1000 ANOP                                                       @C1
         AIF   ('&FREEPWA' NE 'YES').FPW1110                        @C1
         OI    &PWA.__F,&PWA.__F_FREEPWA                            @C1
         AGO   .FPW9999                                             @C1
.FPW1110 AIF   ('&FREEPWA' EQ 'NO').FPW1120                         @C1
         MNOTE 8,'FREEPWA=&FREEPWA is not valid'                    @C1
.FPW1120 NI    &PWA.__F,X'FF'-&PWA.__F_FREEPWA                      @C1
.FPW9999 ANOP
.*====================================================================*
         MEND
