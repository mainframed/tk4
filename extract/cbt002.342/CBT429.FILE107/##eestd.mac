         MACRO
         ##EESTD &RC,                 RETURN CODE KEYWORD              $
               &R0,                   R0 KEYWORD                       $
               &R1                    R1 KEYWORD
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##EESTD
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##EESTD
.*
.*  PURPOSE    DEFAULT EPILOG MACRO FOR ERREXIT BLOCKS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   12/29/86 MGG - BROKEN APART FROM ENDBLK AND RESTRUCTURED
.*
.*--------------------------------------------------------------------*
         GBLC  &#BAMODE(20)             ADDRESSING MODE OF BLOCK
         GBLA  &#BLVL                   BLOCK LEVEL
         GBLA  &#BNDX(20)               BLOCK TAG INDEXS
         GBLC  &#PBAMOD                 AMODE OF PROGRAM
         GBLA  &#PBNDX                  PROGRAM BLOCK INDEX
         GBLB  &#PBRTN0                 RETURN R0
         GBLB  &#PBRTN1                 RETURN R1
         GBLC  &#PBSMWA                 TAG OF SMWA
         GBLB  &#PBXA                   XA OPTION
         GBLB  &#PB370                  370 OPTION
         LCLC  &TAG,&TAG2               WORK TAG
.*--------------------------------------------------------------------*
.*  ISSUE MESSAGE FOR IGNORED KEYWORDS.
.*--------------------------------------------------------------------*
.* NONE
.*--------------------------------------------------------------------*
.*  LOAD R0 WITH VALUE TO RETURN IN R0
.*--------------------------------------------------------------------*
         AIF   ('&R0' EQ '').PKW0030
         AIF   (&#PBRTN0).PKW0021
         MNOTE 8,'R0 keyword can not be specified if option RTNR0 is no$
               t in effect'
.PKW0021 AIF   ('&R0' EQ '(R0)').PKW0030
         AIF   ('&R0'(1,1) NE '(').PKW0022
         LR    R0,&R0(1)                LOAD R0
         AGO   .PKW0030
.PKW0022 AIF   ('&R0' NE '0').PKW0025
         SLR   R0,R0                    SET R0 TO ZERO
         AGO   .PKW0030
.PKW0025 LA    R0,&R0                   LOAD R0
.PKW0030 ANOP
.*--------------------------------------------------------------------*
.*  LOAD R1 WITH VALUE TO RETURN IN R1
.*--------------------------------------------------------------------*
         AIF   ('&R1' EQ '').PKW0040
         AIF   (&#PBRTN1).PKW0031
         MNOTE 8,'R1 keyword can not be specified if option RTNR1 is no$
               t in effect'
.PKW0031 AIF   ('&R1' EQ '(R1)').PKW0040
         AIF   ('&R1'(1,1) NE '(').PKW0032
         LR    R1,&R1(1)                LOAD R1
         AGO   .PKW0040
.PKW0032 AIF   ('&R1' NE '0').PKW0035
         SLR   R1,R1                    SET R1 TO ZERO
         AGO   .PKW0040
.PKW0035 LA    R1,&R1                   LOAD R1
.*--------------------------------------------------------------------*
.PKW0040 ANOP
.*--------------------------------------------------------------------*
.*  GENERATE AMODE CHANGE CODE IF NECESSARY
.*--------------------------------------------------------------------*
&TAG     SETC  '#BV&#BNDX(&#BLVL)'
         AIF   (NOT &#PBXA).PRC0210
         AIF   ('&#PBAMOD' EQ 'ANY').PRC0010
         AIF   ('&#PBAMOD' EQ '&#BAMODE(&#BLVL)').PRC0210
.PRC0010 AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0020
         ##SWREG R15                    SAVE WORK REGISTER
.PRC0020 AIF   (NOT &#PB370).PRC0100
         L     R15,16                   POINT R15 TO CVT
         TM    X'74'(R15),X'80'         RUNNING UNDER XA?
         BZ    &TAG                     NO, SKIP AMODE SETTING
.PRC0100 AIF   ('&#PBAMOD' NE '31').PRC0110
&TAG2    SETC  '#BW&#BNDX(&#BLVL)'
         ICM   R15,B'1111',&TAG2        SWITCH TO
         BSM   0,R15                     AMODE=31.
&TAG2    DC    AL4(&TAG+X'80000000') AMODE 31
         AGO   .PRC0200
.PRC0110 AIF   ('&#PBAMOD' NE 'ANY' AND '&#PBAMOD' NE '').PRC0120
         LA    R15,&TAG                 SWITCH TO
         O     R15,&#PBSMWA+4            AMODE AT ENTRY
         BSM   0,R15                     TO THE PROGRAM.
         AGO   .PRC0200
.PRC0120 AIF   ('&#PBAMOD' NE '24').PRC0130
         LA    R15,&TAG                 SWITCH TO
         BSM   0,R15                     AMODE=24.
         AGO   .PRC0200
.PRC0130 ANOP INVALID AMODE (A PREVIOUS MACRO WILL ISSUE THE MESSAGE)
.PRC0200 AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0210
&TAG     ##RST R15=YES                  RESTORE REGISTERS
         ##RWREG R15                    RESTORE WORK REGISTER
         AGO   .PRC0300
.PRC0210 AIF   ('&RC' NE '(R15)' AND '&RC' NE '').PRC0220
&TAG     ##RST R15=NO                   RESTORE REGISTERS
         AGO   .PRC0300
.PRC0220 AIF   ('&RC'(1,1) NE '(').PRC0230
&TAG     LR    R15,&RC(1)               LOAD RETURN CODE
         ##RST R15=NO                   RESTORE REGISTERS
         AGO   .PRC0300
.PRC0230 ANOP
&TAG     ##RST R15=YES                  RESTORE REGISTERS
         AIF   ('&RC' NE '0').PRC0240
         SLR   R15,R15                  SET RETURN CODE TO 0
         AGO   .PRC0300
.PRC0240 LA    R15,&RC                  LOAD RETURN CODE
.PRC0300 ANOP
         B     #BQ&#PBNDX               LEAVE PROGRAM WITH RC IN R15
         MEND
