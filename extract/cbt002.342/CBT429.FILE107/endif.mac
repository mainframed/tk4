         MACRO
         ENDIF &IF=
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1983.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ENDIF
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ENDIF
.*
.*  PURPOSE    STRUCTURED MACRO TO END A IF STRUCTURE.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY   11/01/83 - NEW  MGG
.*
.*--------------------------------------------------------------------*
         GBLA  &#MLVL                   STRUCTURAL LEVEL
         GBLA  &#ILVL                   IF NEXTING LEVEL
         GBLA  &#IMLVL(20)              IF STURCTURAL LEVEL
         GBLA  &#INDX(20)               IF TAG INDEX
         GBLA  &#INDX2(20)              IF TAG INDEX 2
         GBLB  &#IELSE(20)              ELSE ENCOUNTERED
         GBLC  &#INAME(20)              IF NAMES
         LCLC  &TAG                     WORK TAG
         AIF   (&#ILVL LT 1).ERR0001
         AIF   ('&IF' EQ '').PRC0010
         AIF   ('&IF' NE '&#INAME(&#ILVL)').ERR0002
.PRC0010 AIF   (&#IMLVL(&#ILVL) NE &#MLVL).ERR0003
&#MLVL   SETA  &#MLVL-1                 SUBTRACT 1 FORM STRUCTURAL LVL
         AIF   (&#IELSE(&#ILVL)).PRC0020
&TAG     SETC  '#IF&#INDX2(&#ILVL)'     SET TAG TO PREVIOUS FAIL TAG
&TAG     DS    0H                       NO ELSE CODE
.PRC0020 ANOP
&TAG     SETC  '#IE&#INDX(&#ILVL)'      SET TAG TO END IF TAG
&TAG     DS    0H                       END OF IF CODE
&#ILVL   SETA  &#ILVL-1                 BUMP DOWN IF LEVEL
         AGO   .EXIT
.ERR0001 MNOTE 8,'IF nesting level is zero'
         AGO   .EXIT
.ERR0002 MNOTE 8,'IF= does not match corresponding IF'
         AGO   .EXIT
.ERR0003 MNOTE 8,'ENDIF is not at same structural level as correspondin$
               g IF'
.EXIT    MEND
