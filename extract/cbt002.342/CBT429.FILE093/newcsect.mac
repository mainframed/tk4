         MACRO
         NEWCSECT &NAME=GO
.**********************************************************************
.*                 N  E  W  C  S  E  C  T                             *
.*       THIS MACRO ESTABLISHED THE CSECT NAME WITH THE DEFAULT       *
.*       NAME OF "GO" UNLESS OTHERWISE SPECIFIED. R12 IS THE BASE     *
.*       AND A SECOND BASE REGISTER, R11 IS ALSO ESTABLISED. IF THE   *
.*       SECOND BASE IS NOT REQUIRED, SIMPLY USE R11. THE SAVE        *
.*       AREA IS INSIDE THE NEWCSECT MACRO.                           *
.*       NEWCSECT ALSO DOES ALL SAVE AREA CHAINING.                   *
.*         EXAMPLE -       NEWCSECT NAME=MYPROGM                      *
.**********************************************************************
         LCLA  &A
&NAME    CSECT
&A       SETA ((K'&NAME+2)/2)*2+4
         B &A.(0,15)         BRANCH AROUND NAME.
&A       SETA K'&NAME
         DC AL1(&A)          LENGTH OF NAME
         DC CL&A'&NAME'      NAME OF THIS CSECT
         STM 14,12,12(13)    SAVE REGISTERS.
         LR 12,15            REG 12 = ENTRY POINT ADDRESS
         LA 11,4095(12)
         LA 11,1(11)         REG 11 = ENTRY POINT ADDRESS + HEX 1000
         USING &NAME,12,11
         LR 15,13            REG 15 = OLD SAVE AREA ADDRESS
         LA 13,SAVE&SYSNDX   REG 13 = NEW SAVE AREA ADDRESS.
         ST 15,4(13)         LINK SAVE AREA.
         ST 13,8(15)         LINK SAVE AREA.
         B SAVE&SYSNDX+72    BRANCH AROUND THE NEW SAVE AREA.
SAVE&SYSNDX DC 18F'0'        NEW SAVE AREA
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
FR0      EQU 0
FR2      EQU 2
FR4      EQU 4
FR6      EQU 6
         MEND
