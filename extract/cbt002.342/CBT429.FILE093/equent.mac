         MACRO
&L      EQUENT &CSECT=,&ID=*,&BASEREG=12,&SA=*,                        +
               &ENTRY=NO,&SP=0,                                        +
               &XTRA=0,&XTRAREG=,&XTRAUSE=
.*
.* GENE CZARCINSKI,  05OCT73
.*
         AIF   ('&CSECT' EQ '').X0
&CSECT   CSECT ,
.X0      AIF   ('&L' NE '').X1
$$$&SYSNDX DC  0H'0'
.X1      AIF   ('&ENTRY' EQ 'NO').X1X
         AIF   ('&ENTRY' EQ 'YES').X1A
         MNOTE 8,'---ONLY ENTRY=YES/NO VALID'
         MEXIT
.X1A     AIF   ('&L' NE '').X1B
         MNOTE 8,'---A LABEL MUST BE SPECIFIED WHEN ENTRY=YES'
         MEXIT
.X1B     ENTRY &L
.X1X     ANOP
&L       SAVE  (14,12),,&ID
         LR    &BASEREG,15   ESTABLISH THE PGM BASE REGISTER
         AIF   ('&L' NE '').X3
         USING $$$&SYSNDX,&BASEREG     AND ADDRESSABILITY.
         AGO   .X4
.X3      USING &L,&BASEREG   AND ADDRESSABILITY.
.*
.X4      AIF   ('&SA' NE '').X5
         MNOTE *,'---REENTRANT ENTRY CODE GENERATED.'
         GETMAIN R,LV=72+&XTRA,SP=&SP
         AIF   ('&XTRAREG' EQ '').X7
         LA    &XTRAREG,72(0,1)        A(END OF SAVE AREA)
         AIF   ('&XTRAUSE' EQ '').X7
         USING &XTRAUSE,&XTRAREG
         AGO   .X7
.X5      MNOTE *,'---NON-REENTRANT ENTRY CODE BEING GENERATED.'
         AIF   ('&SA' EQ '*').X6
         LA    1,&SA         ADDR OF SAVE AREA
         AGO   .X7
.X6      CNOP  4,8
         BAL   1,$$$&SYSNDX.A          BRANCH AROUND
         DC    18F'0'        INLINE SAVE AREA
.*
$$$&SYSNDX.A DC 0H'0'
.X7      ANOP
         LR    15,13
         ST    1,8(0,13)     LINK THE SAVE AREAS
         ST    13,4(0,1)
         LR    13,1          ADDR OF NEW SAVE AREA
         LM    15,1,16(15)             RESTORE USED REGISTERS
         MEND  ENTER
