         MACRO
&NAME    STAE$ &END=STAE$END,&WRK=STAE$WRK,&MF=(E,STAE$LST),           +
               &LOAD=YES,&MSG=TPUT
.*
.*        THIS MACRO SETS UP AN ESTAE ENVIRONMENT FOR STAE$XIT.
.*
.*        WRITTEN BY BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
.*
.*        DATE UPDATED. APRIL 15 1981.
.*
.*        STAE$XIT IS AN ESTAE EXIT WHICH DISPLAYS (ON THE CONSOLE
.*        OR A TSO TERMINAL OR BOTH) INFORMATION ABOUT AN ABEND,
.*        INCLUDING THE PSW, REGISTERS, AND (IF THE PSW IS WITHIN
.*        THE CSECT THAT ISSUED THIS MACRO) THE CSECT NAME AND
.*        THE OFFSET OF THE PSW FROM THE BEGINNING OF THE CSECT.
.*
.*        TO USE STAE$XIT, YOU NEED TO ADD THE FOLLOWING 4 STATEMENTS
.*        TO YOUR PROGRAM.
.*
.*           1)  NEAR THE BEGINNING OF YOUR PROGRAM, AFTER YOU'VE
.*               SET UP YOUR BASE REGISTER AND (IN A RE-ENTRANT
.*               PROGRAM) GETMAINED A WORK AREA, CODE THE STAE$
.*               MACRO WITH NO OPERANDS.
.*           2)  ANYWHERE AFTER THE LAST EXECUTABLE INSTRUCTION
.*               (BUT WITHIN THE MAIN CSECT) INSERT:
.*                 STAE$END EQU   *
.*               THIS IS USED BY STAE$XIT TO DETERMINE IF THE PSW
.*               IS BETWEEN THE START OF THE CSECT AND HERE.
.*           3)  IN YOUR WORK AREA (WHICH IN A RE-ENTRANT PROGRAM
.*               IS USUALLY GETMAINED) INSERT:
.*                 STAE$WRK DS    32D
.*                 STAE$LST DS    16F
.*               THE FIRST IS A WORK AREA USED BY STAE$XIT WHEN
.*               AN ABEND OCCURS. THE SECOND IS A WORK AREA USED
.*               BY THE STAE$ MACRO AT THE TIME IT IS EXECUTED.
.*
.*        STAE$LST IS BROKEN DOWN AS FOLLOWS
.*               4F   -  ESTAE MF=L AREA
.*               2F   -  2 WORD PARAMETER LIST
.*               5F   -  OPTION LIST PREFIX (WORDS 3 AND 4 RESERVED)
.*               5F   -  FIRST CSECT DESCRIPTOR ENTRY
.*        THE STAE$ MACRO MAKES THESE 4 SECTIONS CONTIGUOUS,
.*        BUT THEY MAY IN FACT BE SCATTERED IF YOU GENERATE
.*        YOUR OWN PARAMETER LIST FOR STAE$XIT INSTEAD OF
.*        USING THIS MACRO.
.*
.*        THE STAE$XIT EXIT IS NORMALLY LOADED DYNAMICALLY BY THE
.*        STAE$ MACRO. THIS MAY BE UNACCEPABLE OVERHEAD FOR CERTAIN
.*        APPLICATIONS.  BY SPECIFYING LOAD=NO AS AN OPERAND OF
.*        THE MACRO, THE MACRO WILL REFERENCE THE EXIT WITH A
.*        V-TYPE ADDRESS CONSTANT INSTEAD OF LOADING IT, SO THE
.*        STAE$XIT EXIT CAN BE LINK-EDITED WITH THE ISSUING PROGRAM.
.*
.*        THERE ARE 2 ADVANTAGES TO LOADING THE EXIT DYNAMICALLY.
.*        FIRST, THE LINK-EDIT STEP NEED NOT BE CHANGED TO REFERENCE
.*        THE LIBRARY CONTAINING THE STAE$XIT EXIT.
.*        SECOND, IMPROVEMENTS MADE TO THE STAE$XIT EXIT WILL TAKE
.*        EFFECT AUTOMATICALLY, WITH NO RE-LINKING OF YOUR PROGRAM.
.*
.*        MESSAGES ARE NORMALLY DISPLAYED ON A TSO TERMINAL.
.*        FOR PROGRAMS NOT EXECUTING FROM TSO, THERE IS A MSG=WTO
.*        OPERAND WHICH CAUSES THE MESSAGES TO BE ISSUED VIA WTO.
.*        THERE IS ALSO A MSG=WTOT OPERAND, WHICH CAUSES MESSAGES
.*        TO BE ISSUED THRU WTO AND TSO BOTH.
.*
         AIF   ('&LOAD' EQ 'YES').LOAD
         AIF   ('&LOAD' EQ 'NO').VCON
         MNOTE 4,'LOAD=&LOAD INVALID, LOAD=YES ASSUMED'
.LOAD    LA    0,=CL8'STAE$XIT'    LOAD, EPLOC
         SR    1,1                 LOAD, NO DCB
         SVC   8                   ISSUE LOAD SVC
         AGO   .LOADED
.VCON    L     0,=V(STAE$XIT)
.LOADED  LA    1,&MF(2)            POINT TO ESTAE MF=L
         ST    0,0(,1)             STORE EXIT ADDRESS IN ESTAE MF=L
         MVI   0(1),22             OPTION BITS IN ESTAE MF=L
         XC    8(8,1),8(1)         CLEAR LAST 2 WORDS OF ESTAE MF=L
         LA    14,16(,1)           POINT TO 2-WORD PARAM LIST
         ST    14,4(,1)            STORE PARAM ADDRESS IN ESTAE MF=L
         LA    15,8(,14)           POINT TO 1ST PARAM (OPTIONS)
         ST    15,0(,14)           STORE 1ST PARAM ADDRESS
         LA    0,STAE$WRK          POINT TO 2ND PARAM (WORKAREA)
         ST    0,4(,14)            STORE 2ND PARAM ADDRESS
         XC    0(20,15),0(15)      CLEAR 1ST PARAM AREA
         AIF   ('&MSG' EQ 'TPUT').MSGT
         AIF   ('&MSG' EQ 'WTO').MSGW
         AIF   ('&MSG' EQ 'WTOT').MSGB
         MNOTE 4,'MSG=&MSG INVALID, MSG=TPUT ASSUMED'
.MSGT    MVI   0(15),64            MSG=TPUT
         AGO   .MSGX
.MSGW    MVI   0(15),128           MSG=WTO
         AGO   .MSGX
.MSGB    MVI   0(15),0             MSG=WTOT (WTO AND TPUT)
.MSGX    MVI   7(15),1             ONE CSECT IN LIST
         LA    14,20(,15)          ADDRESS OF FIRST CSECT ENTRY
         ST    14,16(,15)          STORE IN OPTION LIST WORD 5
         LA    0,&SYSECT           TOP OF CSECT
         ST    0,8(,14)            IN TABLE
         LA    0,&END              END OF CSECT
         ST    0,12(,14)           IN TABLE
         MVC   0(8,14),=CL8'&SYSECT' CSECT NAME
         XC    16(4,14),16(14)     ZERO POINTER TO NEXT CSECT
         SLR   0,0                 INDICATE ESTAE CREATE OPTION
         SVC   60                  ISSUE ESTAE SVC
         MEND
