SCAN     TITLE 'FIXPDS - SCAN MEMBER AND FIND OUT IF RIGHT ONE'
SCAN     CSECT
         ENTRY  BPAMEOD,BPAMSYN
         REGISTER
         EJECT
*     SYSTEM CONTROL BLOCKS
         SPACE 2
*        DATA CONTROL BLOCK
         SPACE 1
DCBRECFM EQU   36,1                    RECORD FORMAT
DCBRECF  EQU   B'10000000'             FIXED LENGTH
DCBRECV  EQU   B'01000000'             VARIABLE LENGTH
DCBRECU  EQU   B'11000000'             UNDEFINED LENGTH
DCBRECBR EQU   B'00010000'             BLOCKED RECORDS
DCBBLKSI EQU   62,2                    BLOCKSIZE
DCBLRECL EQU   82,2                    LOGICAL RECORD LENGTH
         SPACE 2
*        DATA EVENT CONTROL BLOCK
         SPACE 1
DECIOBPT EQU   16,4                    IOB POINTER
         SPACE 2
*        INPUT-OUTPUT BLOCK
         SPACE 1
IOBCSLN  EQU   14,2                    RESIDUAL BYTE COUNT
         EJECT
***********************************************************************
*  THIS ROUTINE ISSUES A POINT TO THE BEGINNING OF THE MEMBER,        *
*  AND WRITES OUT THE INITIAL MESSAGE TO THE TERMINAL.                *
***********************************************************************
         SPACE
         RBWENTER ,,RENT,LV=L'SCANCOR,DSECT=(SCANDSCT,R13)
         USING RENTCORE,R11            ADDRESSABILITY TO COMMON
         L     R10,0(,R1)              ADDRESS OF PARM = TTRN
         LA    R1,BPAMDCB              ADDRESS OF DCB
         POINT (1),(10)                POINT TO MEMBER
         OPEN  MF=(E,OPENBSAM)         OPEN OUTPUT FILE
         LA    R2,4
         CALL  ISPLINK,(VDEFINE,NAMELIS1,SPFMEMB,CHAR,F8),VL,          X
               MF=(E,ISPCALL)
         LTR   R15,R15
         BNZ   ERROR
         EJECT
***********************************************************************
*  THIS ROUTINE COPIES THE MEMBER TO A TEMPORARY DATASET              *
***********************************************************************
READ     DS    0Y
         L     R3,BUFRADDR             ADDRESS OF BUFFER
         READ  DECB,SF,BPAMDCB,(3),'S',MF=E READ BLOCK
         CHECK DECB                    CHECK I/O
         TM    DCBRECFM+BPAMDCB,DCBRECU IS IT RECFM U?
         BO    RECFMU                  YES
         TM    DCBRECFM+BPAMDCB,DCBRECV IS IT RECFM V?
         BO    RECFMV                  YES
         SPACE
*  RECFM F PROCESSING
         LH    R1,DCBBLKSI+BPAMDCB     LENGTH OF READ
         L     R2,DECIOBPT+DECB        ADDRESS OF IOB
         SH    R1,IOBCSLN(,R2)         SUBTRACT RESIDUAL COUNT FROM IOB
         STH   R1,DCBBLKSI+BSAMDCB     STORE IN OTHER DCB
         WRITE DECBS,SF,BSAMDCB,(3),MF=E WRITE BLOCK
         CHECK DECBS
         B     READ                    BRANCH TO RETURN RECORD
         SPACE
*  RECFM V PROCESSING
RECFMV   DS    0Y
         WRITE DECBS,SF,BSAMDCB,(3),MF=E WRITE BLOCK
         CHECK DECBS
         B     READ
         SPACE
*  RECFM U PROCESSING
RECFMU   DS    0Y
         LH    R1,DCBLRECL+BPAMDCB     GET BLOCK LENGTH FROM DCBLRECL
         STH   R1,DCBBLKSI+BSAMDCB     STORE IN BSAM DCB
         WRITE DECBS,SF,BSAMDCB,(3),'S',MF=E WRITE BLOCK
         CHECK DECBS
         B     READ
         EJECT
***********************************************************************
*  THIS ROUTINE IS EXECUTED AFTER THE ENTIRE MEMBER HAS BEEN COPIED   *
*  TO THE TEMPORARY FILE.  THE USER IS ALLOWED TO BROWSE THE MEMBER.  *
*  WHEN HE FINISHES, HE IS ASKED WHETHER THIS WAS THE RIGHT MEMBER.   *
***********************************************************************
BPAMEOD  DS    0Y
         MVC   DCBBLKSI+BSAMDCB,DCBBLKSI+BPAMDCB RESTORE BLKSIZE
         CLOSE MF=(E,CLOSBSAM)
         LA    R2,7
         CALL  ISPLINK,(BROWSE,TEMPDSN),VL,MF=(E,ISPCALL)
         LTR   R15,R15
         BNZ   ERROR
         LA    R2,8
         CALL  ISPLINK,(DISPLAY,PANNAME),VL,MF=(E,ISPCALL)
         CH    R15,=Y(8)     WAS PF3 HIT?
         BE    RET8
         LTR   R15,R15
         BNZ   ERROR
         CLC   SPFMEMB,=CL8' '
         BE    RET0
         B     RET4
RET0     DS    0Y
         XR    R2,R2
         B     RETURN
RET4     DS    0Y
         MVC   MEMBER,SPFMEMB
         LA    R2,4
         B     RETURN
RET8     DS    0Y
         LA    R2,8
         B     RETURN
ERROR    DS    0Y
         CVD   R2,SAVE2
         CVD   R15,SAVE15
         ABEND 100,DUMP
BPAMSYN  DS    0Y
         SYNADAF ACSMETH=BPAM
         LA    R1,50(,R1)              POINT PAST GARBAGE
         LA    R0,78                   LENGTH
         TPUT  (1),(0),R               PUT MESSAGE TO TERMINAL
         SYNADRLS                      FREE BUFFER
         CLOSE MF=(E,CLOSBSAM)
         B     RET0                    RETURN TO CHECK NEXT MEMBER
         SPACE 2
RETURN   DS    0Y
         TERME NOZOREST,RC=(R2)
         EJECT
*        CONSTANTS
FTOPEN   DC    CL8'FTOPEN'
VDEFINE  DC    CL8'VDEFINE'
FTINCL   DC    CL8'FTINCL'
FTCLOSE  DC    CL8'FTCLOSE'
VCOPY    DC    CL8'VCOPY'
BROWSE   DC    CL8'BROWSE'
DISPLAY  DC    CL8'DISPLAY'
TEMP     DC    CL8'TEMP'
CHAR     DC    CL8'CHAR'
MOVE     DC    CL8'MOVE'
NAMELIS1 DC    A(1),A(8),CL8'SPFMEMB'
SKELNAME DC    CL8'FPDSS0'
PANNAME  DC    CL8'FPDSP0'
F8       DC    F'8'
F255     DC    F'255'
TEMPDSN  DC    CL54'FPDS0001.CNTL '
         SPACE 2
         LTORG
         EJECT
*        DSECT FOR PROGRAM DYNAMIC STORAGE
         SPACE 2
SCANDSCT DSECT
SCANCOR@ DS    0Y
ISPCALL  CALL  ,(,,,,,,,),MF=L
DATALINE DS    CL259
SPFMEMB  DS    CL8
SAVE2    DS    D
SAVE15   DS    D
SCANCOR  EQU   SCANCOR@,*-SCANCOR@
         COPY  SPFDSCT
         END
