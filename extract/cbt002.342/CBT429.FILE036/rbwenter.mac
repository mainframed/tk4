     MACRO
&NAME    RBWENTER &BASE,&ID,&TRACEIT,                                  $
               &CPU=,                                                  ,
               &PL1=NO,&LV=,&SAVE=,&SP=,&DSECT=,&COMMENT=
.*
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*
.*                  RBWENTER MACRO                                   .*
.* PURPOSE - THIS MACRO WILL ASSUME REGISTER 15 TO HAVE AN ENTRY     .*
.* POINT ADDRESS  IT WILL SET UP OPERAND 1 AS A BASE REGISTER. IF NO .*
.* OPERAND 1, 12 WILL BE USED. IT WILL SET UP OPERAND 2 AS AN ENTRY  .*
.* POINT I.D. FIELD. IF OMITTED THEN THE INSTRUCTION NAME OR 'ENTRY  .*
.* POINT' WILL BE USED. IF OPERAND 3 IS 'TRACE'  A GETMAIN WILL BE   .*
.* ISSUED FOR A SAVE AREA EACH TIME THE ROUTINE IS ENTERED. IF THE   .*
.* THIRD OPERAND IS 'NOTRACE' THE GOTTEN SAVE AREA WILL BE FLAGGED   .*
.* SO THAT THE TERME MACRO WILL FREE IT. IF OPERAND 3 IS OMITTED,    .*
.* A SAVE AREA WILL BE ASSEMBLED IN LINE.                            .*
.* IF THE USER REQUESTS RENT CODING, A DSECT WILL BE OBTAINED, FOR THE
.* SAVE AREA AND THE $$SCR16 AREA FOR OTHER CBS MACROS. THESE TWO
.* FIELDS WILL BE ORGED BETWEEN THE USER DSECT IF ANY
.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.*.
         GBLA  &COUNTER
         GBLA  &RENTSW
         GBLA  &RENTSW2
         GBLA  &PL1SW,&SPVAL
         GBLB  &$$SCR16       DEFINES MACRO WORK AREA ONCE
         GBLC  &CPUTYPE      USED TO PASS CPU SPECIFICATION TO CBS MACS
         GBLC  &GPRG         REG. NOTATION SET BY REGISTER MACRO
         GBLC  &RELATED      SET BY 'GENAME' MACRO TO ARTIFICIAL NAME
         LCLA  &BASECTR,&USECTR,&USEDISP
         LCLB  &R13DSCT
         LCLA  &IDSIZE,&BSIZE
         LCLA  &SUBPOOL
         LCLC  &B
         LCLC  &EAR(4),&FAR(4),&GAR(4)
         LCLC  &SCRNM        NAME OF SCRATCH AREA IN DSECT
.* IF NO DSECT NAME WAS SPECIFIED, OBTAIN ONE AND REINVOKE MACRO
         AIF   ('&TRACEIT' NE 'RENT').OKGEN
         AIF   ('&DSECT(1)' NE '').OKGEN
         GENAME
&B       SETC  '&RELATED'(1,7)
         AIF   ('&DSECT(2)' EQ '').IG
&NAME    RBWENTER &BASE,&ID,&TRACEIT,CPU=&CPU,                         ,
               PL1=&PL1,LV=&LV,                                        ,
               SAVE=&SAVE,SP=&SP,DSECT=(&B,&DSECT(2)),                 $
               COMMENT=&COMMENT
         MEXIT
.IG      ANOP
&NAME    RBWENTER &BASE,&ID,&TRACEIT,                                  $
               DSECT=(&B,&GPRG.13),CPU=&CPU,PL1=&PL1,LV=&LV,SP=&SP,    $
               SAVE=&SAVE,COMMENT=&COMMENT
         MEXIT
.*
.OKGEN   ANOP
&COUNTER SETA  &COUNTER+1
         AIF   ('&CPU' EQ '').NULLCPU
&CPUTYPE SETC  '&CPU'
.NULLCPU ANOP
.*                           NOW TO GENERATE COMMENTS                .*
         AIF   ('&COMMENT' EQ 'NO').NOCOM
         SPACE
***********************************************************************
*                                                                     *
         AIF  (T'&BASE NE 'O').S1
         AIF   ('&PL1' EQ 'YES').S1A
*                  BASE REGISTER OMITTED, 12 ASSUMED                  *
         AGO   .S1
.S1A     ANOP
*        BASE REGISTER OMITTED, 11 ASSUMED                            *
.S1      AIF   (T'&ID NE 'O').S2
*          I.D. FIELD OMITTED -                                       *
         AIF   (T'&NAME NE 'O').S3
*              THE I.D. FIELD WILL HAVE THE CSECT NAME                *
*                         FOLLOWED BY A GENERATED NAME.               *
         AGO   .S2
.S3      ANOP
* THE I.D. FIELD WILL HAVE THE CSECTNAME FOLLOWED BY THE NAMEFIELD    *
*                                      OF THIS MACRO                  *
.S2      ANOP
         AIF   ('&PL1' NE 'YES').S2A
*      PL/1 INTERFACE WILL BE ESTABLISHED                             *
.S2A     ANOP
         AIF   ('&LV' EQ '').S2B
*      ADDITIONAL SAVE AREA WILL BE OBTAINED                          *
.S2B     ANOP
.S4      AIF   ('&TRACEIT' NE 'NOTRACE' AND '&TRACEIT' NE 'RENT').S5
* THIS CODE WILL GET A NEW SAVE AREA EACH TIME IT IS USED             *
         AGO   .S6
.V1      ANOP
&SUBPOOL SETA  1
         AGO   .V3
.V2      ANOP
&SUBPOOL SETA  &SP
         AGO   .V3
.ERROR7  ANOP
         MNOTE 4,'DSECT=&DSECT IS INVALID'
         MNOTE *,'VALID FORMAT IS DSECT=(DSECTNAME,DSECTBASEREG)'
         AGO   .S12B
.S5      AIF   (T'&TRACEIT NE 'O').S6
*  THIRD OPERAND OMITTED, THE SAVE AREA WILL BE GENERATED IN LINE.    *
.S6      ANOP
*                                                                     *
***********************************************************************
         SPACE
.NOCOM   ANOP
         AIF   ('&SP' NE '').V2
         AIF   ('&PL1' EQ 'YES').V1
&SUBPOOL SETA  2
.V3      ANOP
         AIF   ('&PL1' NE 'YES').V4
&PL1SW   SETA  &SUBPOOL+1
.V4      ANOP
         AIF   ('&TRACEIT' EQ 'RENT').VAL1
         AIF   ('&LV' EQ '' AND '&SP' EQ '' AND '&DSECT' EQ '').VALEXIT
         AIF   ('&TRACEIT' EQ '').VAL2
         ENTERROR ('THIRD OPERAND',&TRACEIT),                          $
               (LV,&LV,SP,&SP,DSECT,&DSECT),                           $
               (LV,&LV,SP,&SP,DSECT,&DSECT)
         AGO   .VALEXIT
.VAL2    ANOP
         ENTERROR ('THIRD OPERAND',(OMITTED)),                         $
               (LV,&LV,SP,&SP,DSECT,&DSECT),                           $
               (LV,&LV,SP,&SP,DSECT,&DSECT)
         AGO   .VALEXIT
.VAL1    ANOP
.VAL3    ANOP
         AIF   ('&LV' NE '').VALEXIT
         AIF   ('&DSECT' EQ '').VALEXIT
&GAR(3)  SETC  'DSECT'
&GAR(4)  SETC  '&DSECT'
&EAR(3)  SETC  'LV'
&EAR(4)  SETC  'OMITTED'
&FAR(3)  SETC  'DSECT'
&FAR(4)  SETC  '&DSECT'
         AIF   (('&DSECT(2)' EQ '13' OR '&DSECT(2)' EQ '&GPRG.13')     $
               AND '&TRACEIT' EQ 'RENT').VALEX2
.VALEXIT ANOP
         ENTERROR (&EAR(1),&EAR(2)),(&FAR(1),&FAR(2))
         ENTERROR (&EAR(3),&EAR(4)),(&FAR(3),&FAR(4)),                 $
               (&GAR(1),&GAR(2),&GAR(3),&GAR(4))
.VALEX2  ANOP
         AIF   (T'&BASE EQ 'O').S7
         AIF   (N'&BASE NE 1).SETBASE
         AIF   (T'&BASE NE 'N').ERROR2
         AIF   (&BASE GT 15).ERROR4
         AIF   (&BASE LT 1).ERROR4
.ERR4    ANOP
&B       SETC  '&BASE(1)'
.USING   ANOP
         USING  *,&B
.SETCHAR ANOP
         AIF   (T'&ID EQ 'O').SETO
         AIF   ('&ID'(1,1) EQ '''').SETBL
&IDSIZE  SETA  K'&ID/2*2+1
.SETBSIZ ANOP
&BSIZE   SETA  &IDSIZE+5
         AIF   ('&BASE' EQ '*').FIXDISP
&NAME    B     &BSIZE.(,15)       BRANCH AROUND ID
.DFIXRET ANOP
         DC    AL1(&IDSIZE)            CHARACTER COUNT OF I.D. FIELD
         AIF   (T'&ID NE 'O').S9
.*   S9 WILL USE THE GIVEN I.D. FIELD
         AIF   (T'&NAME NE 'O').S10
.* S10 WILL USE THE CSECT-MACRO NAME
.* THE FOLLOWING CODE WILL USE CSECT-GENERATED NAME
*    IDENTIFICATION FIELD FOLLOWS - -
         DC          CL19'&SYSECT - ENTRY &COUNTER'
*
         AGO   .S11
.FIXDISP ANOP
&NAME    B     *+&BSIZE           BRANCH AROUND ID
         AGO   .DFIXRET
.SETBL   ANOP
&IDSIZE  SETA  K'&ID/2*2-1
         AGO   .SETBSIZ
.SETO    ANOP
&IDSIZE  SETA  19
         AGO   .SETBSIZ
.S9      ANOP
         AIF   ('&ID'(1,1) NE '''').S9A
         DC    CL&IDSIZE.&ID
         AGO   .S11
.S9A     ANOP
         DC    CL&IDSIZE'&ID'
         AGO   .S11
.S10     ANOP
*    IDENTIFICATION FIELD FOLLOWS - -
         DC         CL19'&SYSECT - &NAME'
*
.S11     ANOP
         STM   &GPRG.14,&GPRG.12,12(&GPRG.13)
         AIF   ('&BASE' EQ '*').S8
         LA    &B,0(,&GPRG.15)
         AGO   .S8
.S7      ANOP
         AIF   ('&PL1' EQ 'YES').S7A
&B       SETC  '12'
         AGO   .USING
.S7A     ANOP
&B       SETC  '11'
         AGO   .USING
.S8      ANOP
.S12     AIF   ('&TRACEIT' NE 'NOTRACE' AND '&TRACEIT' NE 'RENT').S13
  AIF   ('&DSECT(2)' EQ '&GPRG.13' OR '&DSECT(2)' EQ '13').R13DSCT
.R13RET  ANOP
&RENTSW  SETA  &RENTSW+1
         AIF   ('&LV' NE '' AND &SUBPOOL EQ 0).SPGET1
         AIF   (&SUBPOOL EQ 0).SPGET2
         AIF   ('&LV' NE '').GETMORE
         LA    &GPRG.1,88          LENGTH OF SAVE AREA
         AGO   .GETMOR
.R13DSCT ANOP
&R13DSCT SETB  1
.* GENERATE THE SAVE AREA DSECT
&DSECT(1) DSECT
&SAVE    DS    18F                 REGISTER SAVE AREA
         AIF   (&$$SCR16).NULSCR
&$$SCR16 SETB   1            MARK FOR NO MORE WORK AREAS BY NAME
$$SCR16  DS    2D
.NULSCR  ANOP
&SYSECT  CSECT
         AGO   .R13RET        RETURN FROM DSECT DEFINITION HEADING
.EXTRA   ANOP
         AIF   (&R13DSCT).EXTRA2
         GENAME
&B       SETC  '&RELATED'(1,7)
         USING &B,&GPRG.13
&B       DSECT
&SAVE    DS    18F           WD1,BACKCHAIN,FWDCHAIN,REGS:14,15,0-12
         AIF   (&$$SCR16).NOLV4    IF $$SCR16 DEFINED, DON'T NEED IT
         AIF   ('&LV' EQ '').NOLV3
         ORG   *+&LV               FALL BEHIND USER'S DSECT
.NOLV3   ANOP
$$SCR16  DS    2D
&$$SCR16 SETB  1
         AIF   ('&LV' EQ '').NOLV4
         ORG   *-(&LV+16)
.NOLV4   ANOP
&SYSECT  CSECT
&$$SCR16 SETB  1
.EXTRA2  ANOP
         BAL   &GPRG.1,*+4         SET PARAMETER LIST ADDRESS
         SVC   10                      ISSUE GETMAIN (REGMAIN) SVC
         LA    &GPRG.1,0(,&GPRG.1) CLEAR HI ORDER BYTE
         XC    0(72,&GPRG.1),0(&GPRG.1)
         ST    &GPRG.0,0(,&GPRG.1) SAVE FIELD LENGTH
         AIF   (&SUBPOOL EQ 0).S12C
         MVI   0(&GPRG.1),&SUBPOOL SAVE SUBPOOL NUMBER
.S12C    ANOP
         ST    &GPRG.1,8(,&GPRG.13)
         ST    &GPRG.13,4(,&GPRG.1)
         MULTBASE &BASE
         LM    &GPRG.15,&GPRG.1,16(&GPRG.13)
         L     &GPRG.13,8(,&GPRG.13)
         AIF   ('&DSECT(1)' EQ '').S12A
         AIF   ('&DSECT(2)' EQ '').ERROR7
         USING &DSECT(1),&DSECT(2)
.S12A    ANOP
         AIF   ('&DSECT(2)' EQ '').S12B
   AIF   ('&DSECT(2)' EQ '13').S12B
         AIF   ('&DSECT(2)' EQ '&GPRG.13').S12B
         LA    &DSECT(2),72(,&GPRG.13) ESTABLISH DSECT ADDRESSABILITY
.S12B    ANOP
         AIF   ('&PL1' EQ 'YES').SETPL1
         MEXIT
.SPGET1  ANOP
         LA    &GPRG.0,88+&LV      SET LENGTH
         AGO   .EXTRA
.SPGET2  ANOP
         LA    &GPRG.0,88          LENGTH, NO LV SPECIFIED
         AGO   .EXTRA
.S13     AIF   (T'&TRACEIT NE 'O').ERROR1
.S13A    ANOP
         CNOP  0,4
         BAL   &GPRG.1,*+76        ADDRESS AND GO AROUND SAVE AREA
&SAVE    DC    18F'0'  DEFINE SAVE AREA
         ST    &GPRG.1,8(,&GPRG.13) SAVE FORWARD POINTER
         ST    &GPRG.13,4(,&GPRG.1) SAVE BACKWORD POINTER
         MULTBASE &BASE
         LM    &GPRG.0,&GPRG.1,20(&GPRG.13)
         L     &GPRG.13,8(,&GPRG.13)
         AIF   ('&PL1' EQ 'YES').SETPL1
         MEXIT
.SETPL1  ANOP
         MVI   0(&GPRG.13),X'80'   SET PL/1 FLAGS
         MEXIT
.SETBASE ANOP
&BASECTR SETA  N'&BASE
.SB2     ANOP
&USECTR  SETA  &USECTR+1
         USING *+&USEDISP,&BASE(&USECTR)
&USEDISP SETA  &USEDISP+4096
         AIF   (&USECTR NE &BASECTR).SB2
&B       SETC  '&BASE(1)'
         AGO   .SETCHAR
.GETMORE ANOP
         LA    &GPRG.1,88+&LV      LOAD FIELD SIZE
.GETMOR  ANOP
         LA    &GPRG.0,&SUBPOOL    INSERT SUBPOOL ID
         SLL   &GPRG.0,24
         OR    &GPRG.0,&GPRG.1
&RENTSW2 SETA  &RENTSW2+1
         AGO   .EXTRA
.ERROR1  MNOTE 4,'ILLEGAL ENTRY IN THIRD OPERAND (&TRACEIT)'
         SPACE 2
***********************************************************************
*              THE THIRD OPERAND MUST BE 'RENT' OR OMITTED            *
*              IT WILL BE ASSUMED THAT IT WAS OMITTED                 *
***********************************************************************
         SPACE 2
         AGO   .S13A
.ERROR2  ANOP
         AIF   ('&BASE' EQ '*').SETCHAR
         AIF   (T'&BASE EQ 'U').ERR4
.ERROR2B MNOTE 4,'FIRST OPERAND (&BASE) IS ILLEGAL'
         SPACE 2
***********************************************************************
* THE FIRST OPERAND IS THE BASE REGISTER IT MUST BE A NUMBER BETWEEN  *
*  ZERO AND FIFTEEN . IT WILL BE TREATED AS IF IT WAS OMITTED,        *
.ERR2B   ANOP
*      12 WILL BE ASSIGNED AS THE BASE.                               *
***********************************************************************
         SPACE 2
         AGO   .S7
.ERROR4  AIF   (&BASE NE 0).ERROR2
         MNOTE *,'W-A-R-N-I-N-G 0  WILL BE ASSIGNED AS THE BASE'
         SPACE 2
***********************************************************************
*   THIS CODE WILL HAVE NO BASE REGISTER                              *
***********************************************************************
         SPACE 2
         AGO  .ERR4
         MEND
