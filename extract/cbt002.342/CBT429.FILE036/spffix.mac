FIXD     TITLE 'FIXPDS - CORRECT PDS DIRECTORY TO POINT TO MEMBER'
FIXDIR   CSECT
         REGISTER
         EJECT
*     SYSTEM CONTROL BLOCKS
         SPACE 2
*        EVENT CONTROL BLOCK
         SPACE 1
ECBNORM  EQU   X'7F'                   NORMAL COMPLETION
ECBPERR  EQU   X'41'                   PERMANENT ERROR
ECBDAEA  EQU   X'42'                   END OF EXTENT
         SPACE 2
*        CHANNEL STATUS WORD
         SPACE 1
CSWUSTAT EQU   3,7                     UNIT STATUS
CSWUUEXC EQU   X'01'                   UNIT EXCEPTION
CSWUUCHK EQU   X'02'                   UNIT CHECK
         SPACE 2
*        DATA CONTROL BLOCK
         SPACE 1
DCBDEBAD EQU   44,4                    DATA EXTENT BLOCK ADDRESS
         SPACE 2
*        DATA EXTENT BLOCK
         SPACE 1
DEBNMEXT EQU   16,1                    NUMBER OF EXTENTS
DEBDASDS EQU   32,16                   DIR ACC DEVICE SECTION
         SPACE 2
*        DATA EXTENT BLOCK DIRECT ACCESS DEVICE SECTION
         SPACE 1
DEBBINUM EQU   4,2                     BIN NUMBER
DEBSTRCH EQU   6,4                     START OF EXTENT CYL AND HEAD
DEBSTRCC EQU   6,2                     START OF EXTENT CYLINDER
DEBSTRHH EQU   8,2                     START OF EXTENT HEAD
DEBENDCH EQU   10,4                    END OF EXTENT CYL AND HEAD
         EJECT
***********************************************************************
* THIS ROUTINE STOWS THE APPROPRIATE MEMBER NAME IN THE DIRECTORY AS  *
* AN ALIAS, SINCE THIS IS THE ONLY WAY BPAM ALLOWS YOU TO SUPPLY YOUR *
* OWN TTR TO THE STOW.  IT STOWS WITH REPLACE, SO THAT IT DOESN'T CARE*
* WHETHER THERE IS ALREADY A DIRECTORY ENTRY FOR THAT MEMBER.         *
***********************************************************************
         SPACE 2
         RBWENTER ,,RENT,LV=L'FIXCORE,DSECT=(FIXDSCT,R13)
         USING RENTCORE,R11
         L     R10,0(,R1)              PICK UP ADDRESS OF TTRC
         OPEN  (BPAMDCB,(OUTPUT)),MF=(E,OPENBPAM) OPEN BPAM FOR OUTPUT
         MVC   FIXSMEMB,MEMBER         MOVE MEMBER NAME FOR STOW
         MVC   FIXSTTRC,0(R10)         MOVE TTRC FOR STOW
         OI    FIXSTTRC+3,X'80'        SET FLAG FOR ALIAS
         STOW  BPAMDCB,FIXSTOW,R       STOW MEMBER NAME
         CLOSE MF=(E,CLOSBPAM)         CLOSE DATASET AGAIN
         EJECT
***********************************************************************
*  THIS ROUTINE READS THE DIRECTORY USING EXCP, FINDING THE CORRECT   *
*  BLOCK, AND TURNS OFF THE ALIAS BIT IN THE DIRECTORY ENTRY.         *
***********************************************************************
         SPACE 2
         L     R7,DCBDEBAD+EXCPDCB     GET ADDRESS OF DEB
         XR    R3,R3                   CLEAR REGISTER
         IC    R3,DEBNMEXT(,R7)        NUMBER OF EXTENTS
         LA    R7,DEBDASDS(,R7)        POINT AT FIRST EXTENT ENTRY
         XR    R4,R4
DEBLOOP  DS    0Y
         STC   R4,IOBSM                SET M = EXTENT NUMBER
         MVC   IOBSBB,DEBBINUM(R7)     SET BB FROM DEB
         MVC   IOBSCC,DEBSTRCC(R7)     SET CC FROM DEB
         MVC   IOBSHH,DEBSTRHH(R7)     SET HH FROM DEB
         MVI   IOBSR,X'01'             SET R = 1
         XC    ECB,ECB                 CLEAR ECB
         LA    R1,CPRDDIR              ADDRESS OF CHANNEL PROGRAM
         STCM  R1,B'0111',IOBSTART     STORE IT IN IOB
         EXCP  IOB                     READ APPROPRIATE DIRECTORY BLK
         WAIT  ECB=ECB                 WAIT FOR COMPLETION
         CLI   IOBECBCC,ECBNORM        NORMAL COMPLETION?
         BE    FOUNDBLK                YES
         CLI   IOBECBCC,ECBDAEA        END OF EXTENT ?
         BE    ENDOFEXT                YES
         CLI   IOBECBCC,ECBPERR        PERMANENT ERROR ?
         BNE   IOERROR                 NO  - LET SYNADAF HANDLE IT
         TM    CSWUSTAT+IOBCSW,CSWUUEXC END OF FILE?
         BO    EOF                     YES - PROBLEM
         B     IOERROR                 OTHERWISE, LET SYNADAF HANDLE IT
         SPACE
ENDOFEXT DS    0Y
*  AT END OF EXTENT, RESTART AT NEXT EXTENT IF IT EXISTS
         LA    R4,1(,R1)               BUMP EXTENT NUMBER
         CR    R4,R3                   COMPARE WITH # OF EXTENTS
         BNL   EOF                     EQUAL OR HIGH - END OF FILE
         LA    R7,L'DEBDASDS(,R7)      BUMP TO NEXT EXTENT IN DEB
         B     DEBLOOP
         EJECT
IOERROR  DS    0Y
*  I/O ERROR HANDLING ROUTINE
         LA    R1,IOB                  POINT AT IOB
         SYNADAF ACSMETH=EXCP          GET ERROR MESSAGE
         LA    R1,50(,R1)              POINT PAST GARBAGE
         LA    R0,78                   LENGTH
         TPUT  (1),(0),R               PUT MESSAGE TO TERMINAL
         SYNADRLS                      FREE BUFFER
         ABEND X'001000',DUMP          SYSTEM 001 ABEND
         SPACE 2
EOF      DS    0Y
*  ABEND IF DIRECTORY EXHAUSTED AND MEMBER NOT FOUND
         ABEND 1980,DUMP
         EJECT
FOUNDBLK DS    0Y
***********************************************************************
*  HAVING FOUND THE RIGHT DIRECTORY BLOCK, WE NOW HAVE TO FIND THE    *
*  ENTRY FOR THE MEMBER, AND THEN TURN OF THE ALIAS BIT.              *
***********************************************************************
         SPACE 2
         XR    R5,R5
         ICM   R5,B'0011',DIRBUF       PICK UP LENGTH FROM BLOCK
         LA    R6,DIRBUF+2             POINT PAST LENGTH
         SH    R5,=Y(2)                DECREMENT LENGTH
MEMLOOP  DS    0Y
         CLC   0(8,R6),MEMBER          IS THIS THE MEMBER
         BE    FOUNDMEM                YES - FOUND IT
         IC    R1,11(,R6)              PICK UP C BYTE
         N     R1,=A(X'0000001F')      # OF HALFWORDS OF USER DATA
         SLA   R1,1                    CONVERT TO # OF BYTES OF DATA
         LA    R6,12(,R6)              BUMP POINTER PAST FIXED DATA
         AR    R6,R1                   BUMP POINTER PAST USER DATA
         SH    R5,=Y(12)               DECREMENT LENGTH BY FIXED DATA
         SR    R5,R1                   DECREMENT LENGTH BY USER DATA
         BNZ   MEMLOOP                 IF ANY MORE, LOOK AT NEXT
         ABEND 1981,DUMP               MEMBER NOT FOUND - ERROR
         SPACE
FOUNDMEM DS    0Y
         NI    11(R6),X'7F'            TURN OF ALIAS BIT
         EJECT
***********************************************************************
*  THIS ROUTINE WRITES BACK THE CHANGED DIRECTORY BLOCK, AND RETURNS  *
***********************************************************************
         SPACE 2
         CLOSE MF=(E,CLOSEXCP)         CLOSE EXCP DCB
         OPEN  (EXCPDCB,(OUTPUT)),MF=(E,OPENEXCP) REOPEN DCB FOR OUTPUT
         MVC   IOBCCHHR,CNTCCHHR       MOVE ADDRESS OF RECORD TO IOB
         XC    ECB,ECB                 CLEAR ECB
         LA    R1,CPWTDIR              ADDRESS OF CHANNEL PROGRAM
         STCM  R1,B'0111',IOBSTART     STORE IT IN IOB
         EXCP  IOB                     WRITE BACK DIRECTORY BLOCK
         WAIT  ECB=ECB                 WAIT FOR COMPLETION
         CLI   IOBECBCC,ECBNORM        NORMAL COMPLETION
         BNE   IOERROR                 NO - GO TO SYNAD
         TERME NOZOREST,RC=0           RETURN
         EJECT
         LTORG                         EJECT
*        DSECT FOR PROGRAM DYNAMIC STORAGE
         SPACE 2
FIXDSCT  DSECT
FIXCOR$  DS    0X
FIXSTOW  DS    0XL12
FIXSMEMB DS    CL8
FIXSTTRC DS    XL4
FIXCORE  EQU   FIXCOR$,*-FIXCOR$
         COPY  SPFDSCT
         END
