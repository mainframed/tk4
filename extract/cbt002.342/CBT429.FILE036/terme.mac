         MACRO
&NAME    TERME &OP,&RC=
         GBLA  &RENTSW
         GBLA  &RENTSW2
         GBLA  &PL1SW
         GBLC  &GPRG
.*  THIS MACRO WILL RESTORE REGISTERS, FREE SAVE AREAS, AND EXECUTE
.*  THE NECESSARY EXIT LINKAGE.
.*  IF XCTL IS SPECIFIED THE MACRO WILL NOT RELINQUISH CONTROL, INSTEAD
.*  INSTRUCTIONS WILL BE GENERATED WHICH WILL ALLOW THE XCTL MACRO TO
.*  BE GIVEN IMMEDIATLY FOLLOWING THE TERM MACRO.
.*  REGISTERS 0 AND 1      WILL NOT BE RESTORED BY THIS MACRO.
         AIF   (&RENTSW EQ 0).NORENT1
&NAME    LR    &GPRG.1,&GPRG.13       GET ADDRESS OF AREA TO BE FREED
         L     &GPRG.13,4(,&GPRG.13)  GET HIGHER SAVE POINTER
         AGO   .NORENT2
.NORENT1 ANOP
&NAME    L     &GPRG.13,4(,&GPRG.13)    GET HIGHER SAVE POINTER
.NORENT2 ANOP
         AIF   ('&OP' EQ 'NOZOREST').NOZRST
         STM   &GPRG.0,&GPRG.1,20(&GPRG.13)    TEMP. SAVE OF 0 AND 1
.NOZRST  ANOP
         AIF   (&RENTSW EQ 0).NORENT3
*  THIS EXIT LINKAGE IS REENTRANT.
&RENTSW  SETA  &RENTSW-1
         AIF   (&PL1SW NE 0).SUBPOOL
         AIF   (&RENTSW2 EQ 0).SUBSET
&RENTSW2 SETA  &RENTSW2-1
.SUBSET  ANOP
         L     &GPRG.0,0(,&GPRG.1)  GET SUBPOOL AND LENGTH FOR FREEMAIN
         AIF   ('&RC' EQ '').FMAIN
         AIF   ('&RC'(1,1) NE '(').FMAIN
         AIF   ('&RC(1)' NE '15' AND '&RC(1)' NE '&GPRG.15').FMAIN
         LR    &GPRG.12,&GPRG.15        SAVE RETURN CODE
.FMAIN   ANOP
         SVC   10    ISSUE  FREEMAIN  (REGMAIN)  SVC
         AIF   ('&RC' EQ '').NORENT4
         AIF   ('&RC'(1,1) NE '(').NORENT4
         AIF   ('&RC(1)' NE '15' AND '&RC(1)' NE '&GPRG.15').NORENT4
         LR    &GPRG.15,&GPRG.12        RESTORE RETURN CODE
         AGO   .NORENT4
.SUBPOOL ANOP
&PL1SW   SETA  &PL1SW-1
         LA    &GPRG.0,&PL1SW        SET SUBPOOL NUMBER
         STC   &GPRG.0,0(,&GPRG.1)   PLACE SUBPOOL # IN HI ORDER BYTE
         AGO   .SUBSET
.NORENT3 ANOP
*  THIS EXIT LINKAGE IS SERIALLY REUSABLE.
.NORENT4 ANOP
         AIF   ('&OP' EQ 'XCTL').OUT
         AIF   ('&RC' EQ '').OUT
         AIF   ('&RC'(1,1) EQ '(').RCREG
         LA    &GPRG.15,&RC          SET RETURN CODE
         AGO   .OUT
.RCREG   ANOP
         AIF   ('&RC(1)' EQ '15' OR '&RC(1)' EQ '&GPRG.15').OUT
         LR    &GPRG.15,&RC(1)       SET RETURN CODE
.OUT     ANOP
         L     &GPRG.14,12(,&GPRG.13)             RESTORE REG 14
         LM    &GPRG.0,&GPRG.12,20(&GPRG.13)      RESTORE 0 THRU 12
         XC    8(4,&GPRG.13),8(&GPRG.13)
         AIF   ('&OP' EQ 'XCTL').C
&PL1SW   SETA  0
         BR    &GPRG.14              RETURN TO NEXT HIGHER LEVEL.
         MEXIT
.C       ANOP
         BALR  &GPRG.15,&GPRG.0      SET UP NEW BASE REGISTER
         USING *,&GPRG.15            GIVE IT ADDRESSABILITY
         MEND
