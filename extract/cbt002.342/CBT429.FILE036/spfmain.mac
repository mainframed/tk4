MAIN     TITLE 'PDS FIX - MAIN LINE CALLING PROGRAM'
MAINLINE CSECT
         REGISTER
         EJECT
*     SYSTEM CONTROL BLOCKS
         SPACE 2
*        DATA CONTROL BLOCK
         SPACE 1
DCBDEBAD EQU    44,4                    DATA EXTENT BLOCK ADDRESS
         SPACE 2
*        DATA EXTENT BLOCK
         SPACE 1
DEBNMEXT EQU   16,1                     NUMBER OF EXTENTS
DEBDASDS EQU   32,16                    DIR ACC DEVICE SECTION
         SPACE 2
*        DATA EXTENT BLOCK DIRECT ACCESS DEVICE SECTION
         SPACE 1
DEBBINUM EQU   4,2                     BIN NUMBER
DEBSTRCH EQU   6,4                      START OF EXT CYL AND HEAD
DEBSTRCC EQU   6,2                     START OF EXTENT CYLINDER
DEBSTRHH EQU   8,2                     START OF EXTENT HEAD
DEBENDCH EQU   10,4                     END OF EXT CYL AND HEAD
         SPACE 2
*        FULL DISK ADDRESS
         SPACE 1
FDAM     EQU   0,1                      EXTENT NUMBER
FDABB    EQU   1,2                      BIN NUMBER
FDACCHH  EQU   3,4                      CYLINDER AND HEAD
FDACC    EQU   3,2                      CYLINDER NUMBER
FDAHH    EQU   5,2                      HEAD NUMBER
FDAR     EQU   7,1                      RECORD NUMBER
         EJECT
***********************************************************************
*     THIS ROUTINE PERFORMS INITIALIZATION, OBTAINS THE ADDRESS OF    *
*     THE DEB FROM THE DCB, AND SETS UP VALUES IN REGISTERS FOR A     *
*     BXH LOOP THROUGH THE DEB CHAIN FROM THE LAST EXTENT TO THE FIRST*
***********************************************************************
         SPACE 2
         RBWENTER ,,RENT,LV=L'MAINCOR,DSECT=(MAINDSCT,R13)
         USING RENTCORE,R11            ADDRESSABILITY TO COMMON AREA
         XC    MAINSWIT,MAINSWIT       CLEAR SWITCH BYTE
         L     R7,DCBDEBAD+EXCPDCB     DEB ADDRESS IN R7
         XR    R3,R3
         IC    R3,DEBNMEXT(R7)         NUMBER OF EXTENTS IN R3
         BCTR  R3,0                    SUBTRACT 1 FROM # OF EXTENTS
         LA    R7,DEBDASDS(R7)         A(FIRST EXTENT ENTRY) IN R7
         LR    R9,R7                   A(FIRST EXTENT ENTRY) IN R9
         LR    R8,R3                   # OF EXTENTS (- 1) IN R8
         MH    R8,=Y(L'DEBDASDS)       PUT A(LAST EXTENT ENTRY) ....
         AR    R7,R8                   .... IN R7
         LH    R8,=Y(0-L'DEBDASDS)     -(LENGTH OF DASD ENTRY) IN R8
         AR    R9,R8                   R9 < START OF DASD SECTION
         OI    MAINSWIT,MAINSWLS       SET FLAG FOR LAST EXTENT
         EJECT
***********************************************************************
*       THIS LOOP WILL CHAIN THROUGH ALL EXTENTS OF THE DATASET       *
*       CALLING SEARCH ROUTINE FOR EACH ONE. SEARCH WILL RETURN       *
*       THE ADDRESS OF A CHAIN OF TTR'S OF THE BEGINNING OF MEMBERS   *
*       AT POINTER.  THE ROUTINE THEN CALLS SCAN FOR EACH MEMBER      *
*       AND DEPENDING ON THE RETURN CODE, EITHER PROCESSES THE NEXT   *
*       MEMBER, OR DECIDES THAT THIS IS THE ONE TO RESTORE.           *
*                                                                     *
*                 RETURN CODES FROM SCAN:                             *
*           0   -     PROCESS NEXT MEMBER                             *
*           4   -     THIS IS DESIRED MEMBER                          *
***********************************************************************
         SPACE 2
DEBLOOP  DS    0Y
         TM    MAINSWIT,MAINSWLS       IS IT LAST EXTENT
         BNO   MOVEFDA                 NO - PROCESS NORMALLY
         CLC   DEBSTRCH(,R7),FDACCHH+LSTARFDA IS LSTAR IN THIS EXTENT?
         BH    ENDCHAIN                NO - THIS EXTENT IS UNUSED
         CLC   DEBENDCH(,R7),FDACCHH+LSTARFDA
         BL    ENDCHAIN
MOVEFDA  DS    0Y
         MVC   FDABB+MAINEXAD,DEBBINUM(R7) GET BB FROM DEB
         MVC   FDACC+MAINEXAD,DEBSTRCC(R7) GET CC FROM DEB
         MVC   FDAHH+MAINEXAD,DEBSTRHH(R7) GET HH FROM DEB
         MVC   FDAR+MAINEXAD,=X'01'    SET R TO 1
         STC   R3,FDAM+MAINEXAD        SET M TO EXTENT NO
         TM    MAINSWIT,MAINSWLS       IS IT LAST EXTENT?
         BNO   CALLSRCH                NO
         OI    FDAM+MAINEXAD,MAINSWLS  SET INDICATOR FOR SEARCH
         NI    MAINSWIT,255-MAINSWLS   CLEAR SWITCH
CALLSRCH DS    0Y
         LA    R2,MAINEXAD              PARM ADDRESS IN R2
         CALL  SEARCH,((R2)),MF=(E,MAINCALL)
         USING EOFELEM,R6
POINLOOP DS    0Y
         OC    POINTER,POINTER        CHECK FOR ZERO POINTER
         BZ    ENDCHAIN               YES - END OF CHAIN
         L     R6,POINTER             ADDRESS.TO NEXT ITEM ON CHAIN
         LA    R2,EOFTTRN             ADDRESS OF TTRN IN R2
         CALL  SCAN,((R2)),MF=(E,MAINCALL)  CALL SCAN
         LTR   R15,R15                CHECK RETURN CODE
         BNZ   CHECKRTN               IF NOT ZERO, FURTHER CHECK
RESCAN   DS    0Y
         L     R1,POINTER              POINTER ADDR IN R1 FOR FREE
         MVC   POINTER,EOFNEXT         NEXT ELEM ADDRESS IN POINTER
         FREEMAIN R,LV=L'EOFELEM,A=(1)
         B     POINLOOP               ZERO RETURN, NEXT ITEM OF CHAIN
ENDCHAIN DS    0Y
         BCTR  R3,0                    DECREASE EXTENT NUMBER
         BXH   R7,R8,DEBLOOP           LOOP
         LA    R4,8                    SET RETURN CODE
ABORT    DS    0Y
         LA    R2,BPAMDCB              ADDRESS OF DCB
         LA    R3,BUFRADDR             ADDRESS OF BUFFER
         FREEBUF (2),(3)               FREE BUFFER
         CLOSE MF=(E,CLOSBPAM)         CLOSE BPAM DCB
         FREEPOOL (2)                  FREE BUFFER POOL
         B     RETURN
CHECKRTN DS    0Y
         CH    R15,=H'4'              CHECK FOR VALID RETURN
         BE    FIXDIR                 RC = 4 : STOW MEMBER
         CH    R15,=H'8'
         BNE   ABEND
         LA    R4,4
         B     ABORT
FIXDIR   DS    0Y
         LA    R2,BPAMDCB              ADDRESS OF DCB
         LA    R3,BUFRADDR             ADDRESS OF BUFFER
         FREEBUF (2),(3)               FREE BUFFER
         CLOSE MF=(E,CLOSBPAM)         CLOSE BPAM DCB
         FREEPOOL (2)                  FREE BUFFER POOL
         LA    R2,EOFTTRN             ADDRESS OF TTRN IN R2
         CALL  FIXDIR,((R2)),MF=(E,MAINCALL)  CODE 4 - THIS IS MEMBER
         OPEN  (BPAMDCB,(INPUT)),MF=(E,OPENBPAM)  REOPEN BPAM DCB
         LA    R1,BPAMDCB
         GETBUF (1),(2)
         ST    R2,BUFRADDR
         B     RESCAN
*        LA    R4,0
*        B     RETURN                  RETURN TO TSO CONTROL CSECT
         EJECT
***********************************************************************
*   THIS ROUTINE IS THE EXIT FROM THIS MODULE.  IT IS EXECUTED EITHER *
*   WHEN A MEMBER HAS BEEN SELECTED AND FIXED, OR WHEN THE LIBRARY    *
*   HAS BEEN EXHAUSTED WITH NO MEMBER SELECTED.  IT FREES UP ALL      *
*   UNFREED STORAGE AND RETURNS TO THE TSO CONTROL MODULE.            *
***********************************************************************
         SPACE 2
RETURN   DS    0Y
RLSELOOP DS    0Y
         L     R6,POINTER               ADDRESSABILITY TO EOFELEM
         OC    POINTER,POINTER         IS POINTER ZERO?
         BZ    TERME                   YES - ALL STORAGE FREED
         L     R1,POINTER              ADDRESS IN R1 FOR FREEMAIN
         MVC   POINTER,EOFNEXT         NEXT ELEMENT TO POINTER
         FREEMAIN R,LV=L'EOFELEM,A=(1)
         B     RLSELOOP
TERME    DS    0Y
         TERME NOZOREST,RC=(R4)
         SPACE 3
***********************************************************************
*   IF SCAN RETURNS WITH A CODE THAT IS NOT 0 OR 4, THIS CODE CAUSES  *
*   A U1975 ABEND.                                                    *
***********************************************************************
         SPACE 2
ABEND    DS    0Y
         ABEND 1975,DUMP
         EJECT
*        CONSTANTS
         SPACE 2
SORRYMSG DC    C'END OF LIBRARY - NO MEMBER SELECTED FOR RECOVERY'
         EJECT
         LTORG
         EJECT
*        DSECT FOR PROGRAM DYNAMIC STORAGE
         SPACE 1
MAINDSCT DSECT
MAINCOR$ DS    0X
MAINEXAD DS    XL8                     ADDRESS OF EXTENT
MAINSWIT DS    X                       PROGRAM SWITCHES
MAINSWLS EQU   B'10000000'             LAST EXTENT
MAINCALL CALL  ,(*-*),MF=L
MAINCOR  EQU   MAINCOR$,*-MAINCOR$
         COPY  SPFDSCT
         COPY  EOFELEM
         END
