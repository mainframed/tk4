TSOC     TITLE 'PERFORM TSO PARSE AND DYNAMIC ALLOCATAION'
TSOCNTL  CSECT
         REGISTER
         EJECT
*     SYSTEM CONTROL BLOCKS
         SPACE 2
*        DATA SET CONTROL BLOCK
         SPACE 1
DS1LSTAR EQU   54,3                    ADDRESS OF LAST RECORD
         SPACE 2
*        FIXED LOW CORE
         SPACE 1
FLCCVT   EQU   16,4                    ADDRESS OF CVT
         SPACE 2
*        COMMUNICATIONS VECTOR TABLE
         SPACE 1
CVTPCNVT EQU   28,4                    ADDRESS OF TTR TO MBCCHHRR RTN
CVTPRLTV EQU   32,4                    ADDRESS OF MBCCHHR TO TTR RTN
         SPACE 2
*        DATA CONTROL BLOCK
         SPACE 1
DCBEODAD EQU   33,3                    END OF DATA ADDRESS
DCBRECFM EQU   36,2                    RECORD FORMAT
DCBDDNAM EQU   40,8                    DDNAME
DCBDEBAD EQU   44,4                    DATA EXTENT BLOCK ADDRESS
DCBSYNAD EQU   57,3                    SYNCHRONUS ERROR ROUTINE ADDR
DCBBLKSI EQU   62,2                    BLOCKSIZE
DCBLRECL EQU   82,2                    LOGICAL RECORD LENGTH
         SPACE 2
*        JOB FILE CONTROL BLOCK
         SPACE 1
JFCBDSNM EQU   0,44                    DATASET NAME
JFCBVOLS EQU   118,6                   VOLUME SERIAL NUMBER
         EJECT
***********************************************************************
*  THIS ROUTINE PERFORMS INITIALIZATION AND LINKS TO TSO PARSE TO     *
*  PROCESS TSO COMMAND.                                               *
***********************************************************************
         SPACE 2
         RBWENTER ,,RENT,LV=L'TSOCCOR,DSECT=(TSOCDSCT,R13)
         L     R10,0(,R1)              SAVE PARM ADDRESS
*        LOAD  EP=RENTCORE             LOAD NON-REENTRANT CSECT
*        LR    R11,R0                  ADDRESS IN R0
         L     R11,=V(RENTCORE)        LOAD COMMON CSECT ADDRESS
         USING RENTCORE,R11            ADDRESSABILITY
***********************************************************************
*   THIS ROUTINE OPENS THE EXCP AND BPAM DCB'S FOR THE REQUESTED      *
*   DATASET, READS THE JFCB AND FORMAT 1 DSCB FOR THE DATASET,        *
*   AND CALLS THE MAINLINE ROUTINE TO PROCESS THE DATASET.            *
***********************************************************************
         SPACE 2
*        MVC   MEMBER,2(R10)           MOVE MEMBER NAME TO AREA
         MVC   DCBDDNAM+EXCPDCB,FPDSDDN MOVE DDNAME TO EXCPDCB
         MVC   DCBDDNAM+BPAMDCB,FPDSDDN MOVE DDNAME TO BPAMDCB
         OPEN  MF=(E,OPENEXCP)         OPEN EXCP DCB
         L     R15,=V(BPAMEOD)         GET EOD ROUTINE ADDRESS
         STCM  R15,B'0111',DCBEODAD+BPAMDCB STORE IT IN BPAM DCB
         L     R15,=V(BPAMSYN)         GET SYNAD ROUTINE ADDRESS
         STCM  R15,B'0111',DCBSYNAD+BPAMDCB STORE IT IN BPAM DCB
         OPEN  MF=(E,OPENBPAM)         OPEN BPAM DCB
         LA    R1,BPAMDCB              ADDRESS OF BPAM DCB
         MVC   DCBRECFM+BSAMDCB,DCBRECFM+BPAMDCB MOVE RECFM
         MVC   DCBBLKSI+BSAMDCB,DCBBLKSI+BPAMDCB MOVE BLKSIZE
         MVC   DCBLRECL+BSAMDCB,DCBLRECL+BPAMDCB MOVE LRECL
         GETBUF (1),(2)                GET I/O BUFFER FOR BPAM
         ST    R2,BUFRADDR             STORE BUFFER ADDRESS
         RDJFCB MF=(E,OPENEXCP)        GET JFCB
         MVC   CAMDSN,JFCBDSNM+JFCB    MOVE DSNAME TO CAMLIST
         MVC   CAMVOL,JFCBVOLS+JFCB    MOVE VOLSER TO CAMLIST
         OBTAIN DSCBCAM                GET FORMAT 1 DSCB
         XR    R0,R0                   CLEAR REG 0
         ICM   R0,B'1110',DS1LSTAR+DSCB PUT TTR IN R0
         L     R1,EXCPDCB+DCBDEBAD     ADDRESS OF DEB
         LA    R2,LSTARFDA             ADDRESS OF ANSWER AREA
        STM   R8,R13,TSOCSVRG         SAVE REGS
        L     R15,FLCCVT              CVT ADDRESS
        L     R15,CVTPCNVT(R15)       PACK CONVERT RTN ADDRESS
        LR   R8,R13                  PRESERVE R13
        BALR  R14,R15                 LINK TO GET LSTAR MBBCCHHR
        LR    R13,R8                  RESTORE R13
        LM    R8,R13,TSOCSVRG       RESTORE OTHER REGS
         CALL   MAINLINE,MF=(E,TSOCCALL) CALL MAINLINE ROUTINE
        LR    R4,R15                SAVE RETURN CODE
         EJECT
***********************************************************************
*   UPON RETURN FROM THE MAINLINE ROUTINE, THIS ROUTINE CLOSES AND    *
*   FREES THE DATASET, FREES UP STORAGE, AND RETURNS.                 *
***********************************************************************
         SPACE 2
         CLOSE MF=(E,CLOSEXCP)         CLOSE EXCP DCB
*        DELETE  EP=RENTCORE          DELETE NON-REENTRANT CSECT
         TERME NOZOREST,RC=(R4)   RETURN
         EJECT
*        CONSTANTS
FPDSDDN  DC    CL8'FPDS0000'
         SPACE 2
         EJECT
         LTORG
         EJECT
*        DSECT FOR PROGRAM DYNAMIC STORAGE
         SPACE 2
TSOCDSCT DSECT
TSOCCOR$ DS    0X
TSOCSVRG DS    6F
TSOCCALL CALL  ,(*-*),MF=L
TSOCDALR DS    A
TSOCEF02 DS    A
TSOCDID  DS    H
TSOCCOR  EQU   TSOCCOR$,*-TSOCCOR$
         COPY  SPFDSCT
         END
