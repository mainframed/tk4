./ CHANGE      SEQFLD=738,NAME=COPYCAT
         EJECT  ,                                               ESTAE01
         IHASDWA ,                                              ESTAE01
         ESTAE STAEEXIT,CT,                                     ESTAE01X
               PURGE=QUIESCE       QUIESCE I/O REQUESTS         ESTAE01
TERMAB01 NOP   TERM007             SET TO BR IF ABEND           ESTAE01
STAEEXIT DS    0H                  ENTRY TO ESTAE EXIT ROUTINE  ESTAE01
         STM   R0,R15,STAEXRGS-STAEEXIT(R15) FOR DEBUG          ESTAE01
         CH    R15,STAEY12-STAEEXIT(,R15)   SDWA PRESENT??      ESTAE01
         BC    NE,STAE010-STAEEXIT(,R15)    BR IF YES           ESTAE01
*                                                               ESTAE01
*        IF AN SDWA IS NOT PRESENT, SET RETURN CODE ZERO TO     ESTAE01
*        ALLOW THE ABEND TO PROCEED AN DO NO MORE PROCESSING.   ESTAE01
         SR    R15,R15             CONTINUE WITH ABEND          ESTAE01
         BR    R14                 RETURN                       ESTAE01
*                                                               ESTAE01
*        AN SDWA IS PRESENT, CONTINUE PROCESSING.               ESTAE01
STAE010  DS    0H                  CONTINUE WITH STAE EXIT      ESTAE01
         STM   R14,R12,12(R13)     SAVE CALLING REGISTERS       ESTAE01
         LR    R4,R1               SAVE SDWA ADDRESS            ESTAE01
         MVC   STAEWK(2),SDWACMPC-SDWA(R4)   ABEND CODE         ESTAE01
STAE100  DS    0H                                               ESTAE01
         SETRP RC=0,               CONTINUE WITH ABEND          ESTAE01X
               REGS=(14,12)        RESTORE REGISTERS            ESTAE01
STAE150  DS    0H                                               ESTAE01
         SETRP RC=4,               REQUEST RETRY ROUTINE        ESTAE01X
               DUMP=NO,            SUPPRESS ERROR DUMP          ESTAE01X
               RETADDR=STAERTRY,   -> RETRY ROUTINE             ESTAE01X
               REGS=(14,12)        RESTORE REGISTERS            ESTAE01
STAERTRY DS    0H                  START RETRY ROUTINE          ESTAE01
         LM    R10,R13,STAEBASE-STAERTRY(R15)  RELOAD BASE REGS ESTAE01
         LR    R4,R1               SAVE SDWA ADDRESS            ESTAE01
         ESTAE 0                   CANCEL ESTAE EXIT            ESTAE01
         L     R1,SDWAIOBR-SDWA(,R4)    -> IOB RESTORE CHAIN    ESTAE01
         LTR   R1,R1               IS RESTORE POINTER THERE??   ESTAE01
         BC    ZERO,STAE210        BR AROUND SVC IF NOT         ESTAE01
         SVC   17                  RESTORE I/O                  ESTAE01
STAE210  DS    0H                  BYPASS RESTORE SVC           ESTAE01
         SPACE 1                                                ESTAE01
         L     R0,SDWASPID-SDWA(,R4)    SP & LN FOR FREEMAIN    ESTAE01
         LR    R1,R4                    SDWA ADDRESS            ESTAE01
         FREEMAIN R,LV=(0),A=(1)        RELEASE SDWA            ESTAE01
         OI    TERMAB01+1,X'F0'    CHANGE NOP TO BR             ESTAE01
         SPACE 1                                                ESTAE01
         SPACE 2                                                ESTAE01
STAEXRGS DC  16A(0)                REGS ON ENTRY TO EXIT        ESTAE01
STAEY12  DC    Y(12)               STAE EXIT ENTRY CODE TEST    ESTAE01
