*          DATA SET WTOER301   AT LEVEL 003 AS OF 10/10/84
WTOER301 TITLE 'WTOER301 -- WTOR EXIT TO REPLY "JOBNAME" TO IEC301A'
WTOER301 CSECT
WTOER301 AMODE 31
WTOER301 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOER301'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BNO   RETURN          NO, FORGET IT
         CLC   =C'IEC301A S JOB ',CTXTTMSG
         BNE   RETURN          NOT CORRECT MESSAGE, FORGET IT
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY),REPLY MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+6(L'CTXTRPID),CTXTRPID
         MVC   MGCRTEXT+10(8),CTXTTMSG+14
         SR    R1,R1
         SR    R2,R2
         TRT   MGCRTEXT+10(9),BLANKTBL
         LR    R9,R2
         LTR   R8,R1          DID IT TRAP?
         BZ    ABEND0C3
         MVI   0(R8),C''''
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
ABEND0C3 EX    6,*
REPLY    DC    C'REPLY NN,''XXXXXXXX  BY WTOER301'
BLANKTBL DC    XL64'00'
         DC    C''''
         DC    XL191'00'
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOER301
