*          DATA SET WTOE362A   AT LEVEL 005 AS OF 09/26/86
WTOE362A TITLE 'WTOE362A -- WTO EXIT TO DUMP THE SMF MANX DATASETS'
WTOE362A CSECT
WTOE362A AMODE 31
WTOE362A RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOE362A'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         CLC   =C'IEE362A ',CTXTTMSG
         BNE   RETURN          NOT CORRECT MESSAGE, FORGET IT
         CLC   =C' SYS1.MAN',CTXTTMSG+26 HAS THE RECORD LAYOUT CHANGED?
         BE    PROCESS                    NO, PROCESS IT
         EX    4,*                        YES, DUMP IT
PROCESS  GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY),REPLY MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+10(4),CTXTTMSG+32
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         TIME  BIN            GET CURRENT TIME OF DAY IN R0
         SRDA  R0,32          CONVERT TO MINUTES
         D     R0,=F'6000'
         SR    R0,R0          CALCULATE THE HOURS
         D     R0,=F'60'       R0=MINUTES  R1=HOURS
         LR    R3,R0           R3=MINUTES
         M     R0,=F'100'
         AR    R3,R1          R3=HOURS AND MINUTES
         A     R3,=F'2'
         CVD   R3,DWORD       CONVERT IT TO DISPLAY
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY1),REPLY1 MOVE REPLY TO MACRO AREA
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY1
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         UNPK  MGCRTEXT+6(5),DWORD+5(3)
         OI    MGCRTEXT+10,C'0'
         MVC   MGCRTEXT+6(2),MGCRTEXT+7
         MVI   MGCRTEXT+8,C'.'
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY2),REPLY2 MOVE REPLY TO MACRO AREA
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY2
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         UNPK  MGCRTEXT+6(5),DWORD+5(3)
         OI    MGCRTEXT+10,C'0'
         MVC   MGCRTEXT+6(2),MGCRTEXT+7
         MVI   MGCRTEXT+8,C'.'
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
         CLC   CTXTDCLN,=H'2' ANY DESCRIPTOR CODES?
         BL    RETURN          NO, FORGET IT
         L     R10,CTXTDCP    LOAD POINTER TO THE DESCRIPTOR CODES
         USING CTXTDESC,R10
         MVC   CTXTDC1(L'CTXTDC1+L'CTXTDC2),SETSTATS
         OI    CTXTRFB1,CTXTRCRC+CTXTRCDC  CHANGE CODES NOW
         DROP  R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
SETSTATS DC    AL1(CTXTDC04),X'00'       SYSTEM STATUS WILL DO NICELY
REPLY    DC    C'START @,J=MANx  BY WTOE362A'
REPLY1   DC    C'$TA,T=HH.MM,''$T''''A9291AA'''',P=12''  BY WTOE362A'
REPLY2   DC    C'$TA,T=HH.MM,''$T''''A9291AB'''',P=12''  BY WTOE362A'
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
DWORD    DS    D
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOE362A
