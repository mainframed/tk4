*          DATA SET WTOEDB30   AT LEVEL 005 AS OF 06/04/86
WTOEDB30 TITLE 'WTOEDB30 -- WTO EXIT TO DUMP THE DATACOM/DB LOG'
WTOEDB30 CSECT
WTOEDB30 AMODE 31
WTOEDB30 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEDB30'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         CLC   =C'DB00301I',CTXTTMSG
         BNE   RETURN          NOT CORRECT MESSAGE, FORGET IT
*        CLC   =C' SYS1.MAN',CTXTTMSG+26 HAS THE RECORD LAYOUT CHANGED?
*        BE    PROCESS                    NO, PROCESS IT
*        EX    4,*                        YES, DUMP IT
PROCESS  GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         CLC   =C'PROD',CTXTTMSG+11  CHECK PRODUCTION OR TEST
         BNE   TESTMOVE
         MVC   MGCRTEXT(L'REPLY),PREPLY MOVE PROD REPLY TO MGCR AREA
         B     *+10
TESTMOVE MVC   MGCRTEXT(L'REPLY),TREPLY MOVE TEST REPLY TO MGCR AREA
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
         CLC   CTXTDCLN,=H'2' ANY DESCRIPTOR CODES?
         BL    RETURN          NO, FORGET IT
         L     R10,CTXTDCP    LOAD POINTER TO THE DESCRIPTOR CODES
         USING CTXTDESC,R10
         MVC   CTXTDC1(L'CTXTDC1+L'CTXTDC2),SETSTATS
         OI    CTXTRFB1,CTXTRCRC+CTXTRCDC  CHANGE CODES NOW
         DROP  R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
SETSTATS DC    AL1(CTXTDC04),X'00'       SYSTEM STATUS WILL DO NICELY
REPLY    DC    C'START XXXXXXXX                '
TREPLY   DC    C'START DCMTSPIL     BY WTOEDB30'
PREPLY   DC    C'START DCMPSPIL     BY WTOEDB30'
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
DWORD    DS    D
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOEDB30
