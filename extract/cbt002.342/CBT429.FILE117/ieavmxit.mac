*          DATA SET IEAVMXIT   AT LEVEL 006 AS OF 12/21/84
IEAVMXIT TITLE 'IEAVMXIT -- GENERAL PURPOSE WTO EXIT'
IEAVMXIT CSECT
IEAVMXIT AMODE 31
IEAVMXIT RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'IEAVMXIT'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)      PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         CLC   CTXTRCLN,=H'2' ANY ROUTING CODES?
         BL    RETURN          NO, FORGET IT
         L     R10,CTXTRCP    LOAD POINTER TO THE ROUTING CODES
         USING CTXTROUT,R10
         CLC   TESTROUT,CTXTR001 IS THIS ROUTCDE=11?
         BNE   RETURN             NO, SKIP IT
         DROP  R10
         OI    CTXTRFB2,CTXTRHCO FORCE HARDCOPY ONLY
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
TESTROUT DC    AL2(CTXTR11)   ROUTCDE=11
         LTORG
         SPACE 3
         PRINT   GEN
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   IEAVMXIT
