*          DATA SET WTOEI554   AT LEVEL 009 AS OF 05/06/85
WTOEI554 TITLE 'WTOEI554 -- WTO EXIT TO HANDLE THE IMS DFS554 MESSAGE'
WTOEI554 CSECT
WTOEI554 AMODE 31
WTOEI554 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEI554'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)      PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R10
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVI   MGCRTEXT,C' '
         MVC   MGCRTEXT+1(L'MGCRTEXT-1),MGCRTEXT
         MVC   MGCRTEXT(L'SENDHDR),SENDHDR REPLY TO MACRO AREA
         LH    R9,CTXTTLEN    CALCULATE THE LENGTH FOR THE MOVE
         CH    R9,SENDLENG    IS THIS MESSAGE SHORT ENOUGH?
         BNH   SEND0           YES, PROCESS IT
         LH    R9,SENDLENG     NO, USE A SHORTER PART OF THE MESSAGE
SEND0    BCTR  R9,0
         EX    R9,SENDEX      MOVE THE MESSAGE INTO THE BUFFER
         LA    R8,L'SENDHDR+1(R9)
         LA    R7,MGCRTEXT(R8)
         MVI   0(R7),C''''
         LA    R7,1(R7)
         LA    R8,1(R8)
         CLC   =C'DFS554',CTXTTMSG DO NOT HOLD FOR LOGON
         BNE   SEND1
         MVC   0(L'SENDUIDS,R7),SENDUIDS
         LA    R6,L'SENDUIDS
         B     SEND2
SEND1    MVC   0(L'SENDLGN+L'SENDUIDS,R7),SENDLGN
         LA    R6,L'SENDUIDS+L'SENDLGN
SEND2    LA    R3,4(R8,R6)
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
SENDHDR  DC    C'SEND '''
SENDLGN  DC    C',LOGON' THIS MUST BE IMMEDIATELY IN FRONT OF SENDUIDS
SENDUIDS DC    C',USER=(O9080H,A00292,A00345)'
SENDEX   MVC   MGCRTEXT+L'SENDHDR(0),CTXTTMSG MOVE THE MESSAGE
SENDLENG DC    AL2(L'MGCRTEXT-L'SENDHDR-L'SENDLGN-L'SENDUIDS)
         LTORG
         SPACE 3
         PRINT   GEN
         COPY  USERCVT
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOEI554
