WTOEUCVT TITLE 'WTOEUCVT -- WTO EXIT TO CREATE THE USERCVT IN ECSA'
*
*  THIS MPF EXIT IS DESIGNED TO ALLOCATE A USERCVT FOR INSTALLATIONS
*  THAT DO NOT HAVE ONE AND YET WISH TO RUN THE WTOETPSX EXITS INCLUDED
*  IN THIS COLLECTION OF EXITS.  IT HAS BEEN RUN ON ONE OF OUR SYSTEMS.
WTOEUCVT CSECT
WTOEUCVT AMODE 31
WTOEUCVT RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEUCVT'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R8,CVTPTR      FIND USERCVT
         USING CVT,R8
         L     R9,CVTUSER
         LTR   R9,R9          IS THE USERCVT VALID?
         BNZ   OK              YES, DO NOT DO ANYTHING DUMB
         GETMAIN RU,LV=USERCVTL,LOC=ANY,SP=241,RELATED=GETCSA
         LR    R11,R1         SAVE THE ADDRESS FOR LATER
         USING USERCVT,R11
         LA    R2,=XL6'00'    CLEAR THE AREA TO HEX ZEROES
         SR    R3,R3
         LR    R4,R11
         LA    R5,USERCVTL
         BCTR  R5,0
         MVCL  R4,R2
         MVC   CSAEYECT,=C'WTOEUCVT' SET UP THE TABLE HEADER
         CS    R9,R11,CVTUSER USE "CS" INSTRUCTION TO UPDATE MVS CVT
         BE    LOOKGOOD       LOOKS GOOD TO ME
         LA    R2,USERCVTL    OOPS, SOMEBODY BEAT ME TO IT
         MVC   0(4,R11),=C'OLD ' CHANGE THE EYE CATCHER
         FREEMAIN RU,LV=(R2),A=(R11),SP=241,RELATED=GETCSA
OK       WTO   'WTOEUCVT2 USERCVT NOT CREATED BECAUSE IT EXISTS'
         B     RETURN
LOOKGOOD WTO   'WTOEUCVT1 USERCVT CREATED SUCCESSFULLY'
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
         LTORG
         PRINT GEN
         COPY  USERCVT
         SPACE 3
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOEUCVT
