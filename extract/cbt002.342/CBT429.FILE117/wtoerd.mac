*          DATA SET WTOERD     AT LEVEL 002 AS OF 08/09/85
WTOERD   TITLE 'WTOERD -- WTOR EXIT TO REPLY "D" TO MESSAGE'
WTOERD   CSECT
WTOERD   AMODE 31
WTOERD   RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOERD  '
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BNO   RETURN          NO, FORGET IT
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLYD),REPLYD MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+6(L'CTXTRPID),CTXTRPID
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLYD
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
REPLYD   DC    C'REPLY NN,''D'' BY WTOERD'
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOERD
