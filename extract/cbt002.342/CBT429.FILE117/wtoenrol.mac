*          DATA SET WTOENROL   AT LEVEL 004 AS OF 10/10/84
WTOENROL TITLE 'WTOENROL -- WTOR EXIT TO STOP A MESSAGE FROM ROLLING'
WTOENROL CSECT
WTOENROL AMODE 31
WTOENROL RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOENROL'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)      PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         OI    CTXTRFB1,CTXTRCRC+CTXTRCDC  CHANGE CODES NOW
         CLC   CTXTDCLN,=H'2' ANY DESCRIPTOR CODES?
         BL    DESCRPT         NO, MAKE SOME
         L     R10,CTXTDCP    LOAD POINTER TO THE DESCRIPTOR CODES
         USING CTXTDESC,R10
         MVC   CTXTDC1(L'CTXTDC1+L'CTXTDC2),NOROLL
         B     ROUTCDE
         DROP  R10
DESCRPT  LA    R10,NOROLL     MAKE SOME DESCRIPTOR CODES
         ST    R10,CTXTDCP
         LH    R4,=H'2'
         STH   R4,CTXTDCLN
ROUTCDE  CLC   CTXTRCLN,=H'2' ANY ROUTING CODES?
         BL    ROUTCDE1        NO, MAKE SOME
         L     R10,CTXTRCP    LOAD POINTER TO THE ROUTING CODES
         USING CTXTROUT,R10
         MVC   CTXTR001(L'CTXTR001+L'CTXTR002),MASTERI
         B     RETURN
         DROP  R10
ROUTCDE1 LA    R10,MASTERI    MAKE SOME DESCRIPTOR CODES
         ST    R10,CTXTRCP
         LH    R4,=H'2'
         STH   R4,CTXTRCLN
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
NOROLL   DC    AL1(CTXTDC02),X'00' IMMEDIATE ACTION REQUIRED
MASTERI  DC    AL1(CTXTR02),X'00' MASTER CONSOLE INFORMATION
         LTORG
         SPACE 3
         PRINT   GEN
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOENROL
