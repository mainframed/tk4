*          DATA SET WTOEI000   AT LEVEL 008 AS OF 06/17/86
WTOEI000 TITLE 'WTOEI000 -- WTO EXIT TO PROCESS AN IMS /DISPLAY ACTIVE'
WTOEI000 CSECT
WTOEI000 AMODE 31
WTOEI000 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEI000'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R8,CVTPTR      FIND USERCVT
         USING CVT,R8
         L     R9,CVTUSER
         LTR   R9,R9          IS THE USERCVT VALID?
         BNZ   START1          YES, CONTINUE
         EX    6,*
         USING USERCVT,R9
         DROP  R8
START1   CLC   BMPJOBN,=XL8'00' ANY JOB TO CANCEL?
         BE    RETURN            NO, FORGET IT
         L     R11,0(R1)      PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BO    RETURN          YES, SOMETHING IS WRONG
         CLC   =C'DFS000I',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BNE   RETURN          NO, DON'T KNOW WHY I WAS CALLED
         CLC   =C' REGID JOBNAME ',CTXTTMSG+11 NOT INTERESTED IN THIS
         BE    RETURN                             LINE.
         LH    R7,CTXTTLEN    LOAD TEXT LENGTH
         LA    R6,CTXTTMSG-3(R7) ADDRESS OF THE END OF MESSAGE
         S     R7,=F'5'
IMSVER0  CLC   IMS1,0(R6)     WHICH VERSION OF IMS IS THIS?
         BE    IMSVER1         THIS ONE, HANDLE IT.
         CLC   IMS2,0(R6)     WHICH VERSION OF IMS IS THIS?
         BE    IMSVER2         THIS ONE, HANDLE IT.
         BCTR  R6,0           BACK UP ONE
         BCT   R7,IMSVER0      AND TRY AGAIN
         EX    R6,*           IMS VERSION NOT FOUND, FIX IT
IMSVER1  LA    R8,IMSAREA1    THIS IS THE IMS VERSION
         B     IMSVER3
IMSVER2  LA    R8,IMSAREA2     THIS IS THE IMS VERSION
         USING IMSAREA,R8
IMSVER3  CLC   =C' VTAM ACB ',CTXTTMSG+11 FINISHED WITH JOB NAMES?
         BE    VTAMACB        JOB NAME NOT FOUND
         CLC   BMPJOBN,CTXTTMSG+18 CHECK FOR THE JOB NAME
         BNE   RETURN         JOB NAME NOT FOUND
         CLC   =C' BMP ',CTXTTMSG+27 MAKE SURE WE FOUND A BMP
         BNE   RETURN
         L     R5,CTXTTMSG+13 LOAD THE REGION ID FOR STORAGE
         SR    R4,R4
         IC    R4,CTXTTMSG+12
         LM    R6,R7,IMSREPLY SAVE THE REGION ID FOR LATER
         CDS   R6,R4,IMSREPLY
         LM    R6,R7,BMPJOBN  WIPE OUT THE JOBNAME TO SHORTEN PROCESS
         SR    R4,R4
         LR    R5,R4
         CDS   R6,R4,BMPJOBN
         CLC   =C' WAITING FOR BLOCK MOVER ',CTXTTMSG+33 ?????
         BNE   RETURN
         DROP  R10
         OI    CTXTRFB1,CTXTRCRC+CTXTRCDC  CHANGE CODES NOW
         CLC   CTXTDCLN,=H'2' ANY DESCRIPTOR CODES?
         BL    DESCRPT         NO, MAKE SOME
         L     R10,CTXTDCP    LOAD POINTER TO THE DESCRIPTOR CODES
         USING CTXTDESC,R10
         MVC   CTXTDC1(L'CTXTDC1+L'CTXTDC2),NOROLL
         B     ROUTCDE
         DROP  R10
DESCRPT  LA    R10,NOROLL     MAKE SOME DESCRIPTOR CODES
         ST    R10,CTXTDCP
         LH    R4,=H'2'
         STH   R4,CTXTDCLN
ROUTCDE  CLC   CTXTRCLN,=H'2' ANY ROUTING CODES?
         BL    ROUTCDE1        NO, MAKE SOME
         L     R10,CTXTRCP    LOAD POINTER TO THE ROUTING CODES
         USING CTXTROUT,R10
         OC    CTXTR001(L'CTXTR001+L'CTXTR002),MASTERI
         B     MOVEMSG
         DROP  R10
ROUTCDE1 LA    R10,MASTERI    MAKE SOME DESCRIPTOR CODES
         ST    R10,CTXTRCP
         LH    R4,=H'2'
         STH   R4,CTXTRCLN
MOVEMSG  L     R10,CTXTTXPJ   LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R10
         CLC   =H'120',CTXTTLMX ENOUGH ROOM FOR THE MESSAGE?
         BNH   MOVEMSG1           YES, PROCESS IT
         EX    5,*                NO, WHAT HAPPENED?
MOVEMSG1 MVC   CTXTTMSG+(120-L'LMTDINIT)(L'LMTDINIT),LMTDINIT
         MVC   CTXTTLEN,CTXTTLMX   GIVE THE MESSAGE ENOUGH ROOM
         OI    CTXTRFB1,CTXTRCMT      TELL COMMTASK TO CHANGE THE MSG
         B     RETURN
         DROP  R10
VTAMACB  TS    IMSFLAG        THE GUY MAY BE MASQUERADING AS A BMP
         LA    R8,IMSAREA1
         TM    IMSFLAG,X'80'
         BNO   RETURN
         LA    R8,IMSAREA2
         TM    IMSFLAG,X'80'
         BNO   RETURN          THIS GUY CHEATS, CANCEL HIM
         DROP  R8
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVI   MGCRTEXT,C' '
         MVC   MGCRTEXT+1(L'MGCRTEXT-1),MGCRTEXT
         MVC   MGCRTEXT(L'CANCELMS),CANCELMS REPLY TO MACRO AREA
         MVC   MGCRTEXT+7(L'BMPJOBN),BMPJOBN
         LA    R3,(MGCRTEXT-MGCRPL)+L'CANCELMS
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         LM    R6,R7,BMPJOBN  WIPE OUT THE JOBNAME TO SHORTEN PROCESS
         SR    R1,R1
         CDS   R6,R0,BMPJOBN
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
NOROLL   DC    AL1(CTXTDC02),X'00' IMMEDIATE ACTION REQUIRED
MASTERI  DC    AL1(CTXTR02),X'00' MASTER CONSOLE INFORMATION
CANCELMS DC    C'CANCEL XXXXXXXX BY WTOEI000'
LMTDINIT DC    C'AND IN A LIMITED INITIATOR'
         LTORG
         SPACE 3
         PRINT   GEN
         COPY  USERCVT
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOEI000
