*          DATA SET WTOE176I   AT LEVEL 002 AS OF 07/07/86
//O9080RL  JOB (0052,ACCC),
//         'ASM I. R.',
//         NOTIFY=O9080R,
//         CLASS=I,
//         MSGCLASS=Q,
//         MSGLEVEL=1
//R  EXEC A9294A07,USER='O9080R',SYSOUT='(D,,AA01)'
//AS5 EXEC ACFASM,SOUT='(D,,LA01)',MEMBER=WTOE176I
//SYSLIB DD DISP=SHR,DSN=&ACFX..&ACFMAC
//       DD DISP=SHR,DSN=SYS1.MACLIB
//       DD DISP=SHR,DSN=COKE.MACLIB
//       DD DISP=SHR,DSN=SYS1.HASPSRC
//       DD DISP=SHR,DSN=SYS1.AMODGEN
//       DD DISP=SHR,DSN=TMS.TMSGEN47.ASM
//       DD DISP=SHR,DSN=O9080R.CMPAT.ASM
//SYSIN  DD  *
WTOE176I TITLE 'WTOE176I -- WTO EXIT TO STOP THE EXT WRITER 0,1'
WTOE176I CSECT
WTOE176I AMODE 31
WTOE176I RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEDB30'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         CLC   =C'IEF176I',CTXTTMSG
         BNE   RETURN          NOT CORRECT MESSAGE, FORGET IT
PROCESS  GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY),REPLY MOVE  REPLY TO MGCR AREA
         MVC   MGCRTEXT+5(1),CTXTTMSG+40
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
         CLC   CTXTDCLN,=H'2' ANY DESCRIPTOR CODES?
         BL    RETURN          NO, FORGET IT
         L     R10,CTXTDCP    LOAD POINTER TO THE DESCRIPTOR CODES
         USING CTXTDESC,R10
         MVC   CTXTDC1(L'CTXTDC1+L'CTXTDC2),SETSTATS
         OI    CTXTRFB1,CTXTRCRC+CTXTRCDC  CHANGE CODES NOW
         DROP  R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
SETSTATS DC    AL1(CTXTDC04),X'00'       SYSTEM STATUS WILL DO NICELY
REPLY    DC    C'P WTRX'
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
DWORD    DS    D
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOE176I
//L  EXEC ACFLINK,SOUT='*',SYSX='SYS2',LIB=LINKLIB,C=' '
//* RENT='RENT,'
//SYSLIN DD *
  INCLUDE ACFOBJ(WTOE176I)
  NAME   WTOE176I(R)
//ZZDATA DD  DATA,DLM=ZZ
ZZ
