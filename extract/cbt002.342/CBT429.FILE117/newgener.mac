*          DATA SET O9061S13   AT LEVEL 003 AS OF 05/06/81
GENER    TITLE 'IEBGENER TYPE UTILITY FOR REMOTE CENTER TRANSMISSIONS'
****************
*** PARM VALUES - TSO - ROUTES THE WTO RECORD CNT TO TSO USER
****************
*  MODIFIED 01/26/81 FOR THE FOLLOWING:                              RS
*  (1) MADE REENTRANT.                                               RS
*  (2) TO CORRECTLY HANDLE RECFM=VBS, A SYSUT1 OPEN EXIT ADDS        RS
*      BFTEK=A TO THE DCB IF RECORDS ARE SPANNED.                    RS
*  (3) IF SYSUT1 IS FS/FBS, THE STANDARD BIT IS TURNED OFF, SO THAT  RS
*      CONCATENATION OF INPUT WILL WORK.                             RS
*  (4) COPYING OF SYSUT1'S RECFM/LRECL/BLKSIZE INTO THE SYSUT2 DCB,  RS
*      WHEN THE LATTER IS ZERO, IS MOVED FROM BEFORE THE SYSUT2      RS
*      OPEN (PRECEDED BY A RDJFCB) TO A SYSUT2 OPEN EXIT; THIS       RS
*      ALLOWS VALUES IN A PREEXISTING DSCB TO BE HONORED.  (RDJFCB   RS
*      SHOWED WHAT WAS ON THE DD CARD, BUT NOT WHAT WAS IN THE       RS
*      LABEL OF AN OLD SYSUT2 DATA SET.)                             RS
*  (5) ADDITIONAL SYSUTN DD'S (N=3 TO 9) ARE TREATED AS ADDITIONAL   RS
*      OUTPUT FILES TO WHICH SYSUT1 IS ALSO COPIED.  THIS ONE WAS    RS
*      REQUESTED BY BOB.                                             RS
*  (6) IF SYSUT1 HAS RECFM=U, OR IF SYSUT1 AND SYSUT2-9 ARE NOT      RS
*      BOTH RECFM=F OR BOTH RECFM=V, THE PROGRAM TERMINATES WITH     RS
*      RETURN CODE 16.  ADDING CODE TO GO FROM F TO V OR VICE VERSA  RS
*      OR TO CHECK RESIDUALS FROM READING U'S WOULD RELINQUISH       RS
*      TRNGENER'S LAST CLAIM TO SIMPLICITY.                          RS
*  (7) REMOVED "PRIME" AND "TERME" MACROS SINCE I'M NOT FAMILIAR     RS
*      WITH THEM.                                                    RS
*  (8) REMOVED THE "EOF" OPTION, WHICH DIDN'T WORK ANYWAY.           RS
*  (9) USED STANDARD MACROS FOR DCB, PSA, TCB, AND TIOT.  WHO KNOWS, RS
*      THOSE DISPLACEMENTS MIGHT CHANGE SOMEDAY.                     RS
*  MODIFIED 05/05/81 TO FIX VBS BUG ON CONCATENATED INPUT.       RS0581
****************                                                     RS
TRNGENER CSECT                                                       RS
         SAVE  (14,12)             SAVE REGISTERS                    RS
         BALR  R12,0               LOAD BASE REGISTER                RS
         USING *,R12               ADDRESSABILITY                    RS
         L     R2,0(,R1)           ADDRESS OF PARM                   RS
         L     R0,GETMPARM         SUBPOOL AND LENGTH                RS
         GETMAIN R,LV=(0)          GET DYNAMIC AREA                  RS
         XC    0(72,R1),0(R1)      CLEAR FRONT FOR SAVE AREA         RS
         ST    R13,4(,R1)          SAVE AREA                         RS
         ST    R1,8(,R13)            CHAINING                        RS
         LR    R13,R1              POINT TO NEW AREA                 RS
         USING WORKAREA,R13        USE DSECT FOR IT                  RS
         MVC   SYSUT1(DCBLEN),DCBSKEL1  SET UP DCB'S IN              RS
         MVC   SYSUT2(DCBLEN),DCBSKEL2   DYNAMIC AREA                RS
         MVC   SYSUT3(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT3+(DCBDDNAM+5-IHADCB),C'3'  SET DDNAME           RS
         MVC   SYSUT4(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT4+(DCBDDNAM+5-IHADCB),C'4'  SET DDNAME           RS
         MVC   SYSUT5(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT5+(DCBDDNAM+5-IHADCB),C'5'  SET DDNAME           RS
         MVC   SYSUT6(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT6+(DCBDDNAM+5-IHADCB),C'6'  SET DDNAME           RS
         MVC   SYSUT7(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT7+(DCBDDNAM+5-IHADCB),C'7'  SET DDNAME           RS
         MVC   SYSUT8(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT8+(DCBDDNAM+5-IHADCB),C'8'  SET DDNAME           RS
         MVC   SYSUT9(DCBLEN),DCBSKEL2                               RS
         MVI   SYSUT9+(DCBDDNAM+5-IHADCB),C'9'  SET DDNAME           RS
         MVC   WTOMSG(WTOMSGL),WTOSKEL  MOVE WTO LIST FORM           RS
         MVI   EXITSW,X'00'        INITIALIZE                        RS
         XC    FLAG,FLAG             DYNAMIC FIELDS                  RS
         XC    RTNCODE,RTNCODE                                       RS
         MVC   EXL2(8),EXL2SKEL    SET UP EXIT LIST FOR OUTPUT DCBS  RS
         LA    R1,JFCBBUF           IN DYNAMIC AREA (BECAUSE IT HAS  RS
         STCM  R1,7,EXL2+5          DYNAMIC JFCB BUFFER ADDRESS)     RS
         SR    R5,R5               ZERO RECORD COUNTER               RS
         CLC   0(2,R2),=H'3'       IS THERE A PARM (OF RIGHT LENGTH) RS
         BNE   *+10                FORGET IT IF NOT                  RS
         MVC   FLAG,2(R2)          SAVE PARM                         RS
         L     R1,PSATOLD-PSA      OUR TCB ADDRESS                   RS
         L     R1,TCBTIO-TCB(,R1)  TIOT ADDRESS                      RS
         MVC   WTOJOB,TIOCNJOB-TIOT1(R1)   PUT OUR JOBNAME-STEPNAME  RS
         MVC   WTOSTEP,TIOCSTEP-TIOT1(R1)   INTO MESSAGE             RS
         LA    R2,SYSUT1           POINT TO SYSUT1 DCB               RS
         ST    R2,LISTFORM         PUT DCB ADDRESS INTO OPEN LIST    RS
         MVI   LISTFORM,X'80'      FLAG INPUT AND END-OF-LIST        RS
         LA    R1,LISTFORM         POINT TO LIST FORM                RS
         OPEN  MF=(E,(1))          OPEN SYSUT1                       RS
         TM    SYSUT1+(DCBRECFM-IHADCB),DCBRECU  RECFM=U?            RS
         BNO   GETRECFM            BRANCH IF NOT RECFM=U             RS
         MVC   RTNCODE,=F'16'      SET RC=16                         RS
         B     CLOSEIN             QUIT (RECFM=U NOT SUPPORTED)      RS
GETRECFM SLR   R6,R6                                                 RS
         IC    R6,SYSUT1+(DCBRECFM-IHADCB)  GET INPUT RECFM          RS
         SRL   R6,6                SHIFT OUT ALL BUT FIRST 2 BITS    RS
*   NOW R6=1 FOR VARIABLE, 2 FOR FIXED; WILL COMPARE WITH OUTPUTS.   RS
OPENOUT  LA    R2,SYSUT2           POINT TO 1ST OUTPUT DCB (SYSUT2)  RS
         LA    R3,8                LOOP COUNTER                      RS
OPENLOOP LA    R1,EXL2             PUT ADDRESS OF EXIT LIST          RS
         STCM  R1,7,DCBEXLSA-IHADCB(R2)  (DYNAMIC) INTO DCB          RS
         ST    R2,LISTFORM         PUT DCB ADDRESS INTO LIST FORM    RS
         MVI   LISTFORM,X'80'      FLAG END OF LIST                  RS
         LA    R1,LISTFORM         POINT TO LIST FORM                RS
         RDJFCB MF=(E,(1))         SEE IF DD CARD WAS PRESENT        RS
         LTR   R15,R15             RC = 0 MEANS IT WAS               RS
         BNZ   OPENNEXT            SKIP OPEN IF NO DD CARD           RS
         MVI   LISTFORM,X'8F'      FLAG OUTPUT AND END-OF-LIST       RS
         LA    R1,LISTFORM         POINT TO LIST FORM                RS
         OPEN  MF=(E,(1))          OPEN AN OUTPUT DCB                RS
         SLR   R7,R7                                                 RS
         IC    R7,DCBRECFM-IHADCB(,R2)  GET OUTPUT RECFM             RS
         SRL   R7,6                SHIFT OUT ALL BUT FIRST 2 BITS    RS
         CLR   R6,R7               BRANCH IF COPYING RECFM=F TO F    RS
         BE    OPENNEXT             OR V TO V                        RS
         MVC   RTNCODE,=F'16'      OTHERWISE SET RC=16               RS
         B     EOJHALT              AND QUIT                         RS
OPENNEXT LA    R2,DCBLEN(,R2)      BUMP TO NEXT OUTPUT DCB           RS
         BCT   R3,OPENLOOP         REPEAT FOR SYSUT2 THRU 9          RS
*  MAINLINE (SEE HOW SHORT IT IS!)                                   RS
GETRECD  GET   SYSUT1         READ ANOTHER RECORD
         TM    EXITSW,X'0F'   IS THE REREAD SWITCH ON?
         BO    REREAD          YES, REREAD THE RECORD
         LR    R4,R1               ADDRESS OF INPUT RECORD           RS
         LA    R2,SYSUT2           POINT TO 1ST OUTPUT DCB (SYSUT2)  RS
         LA    R3,8                LOOP COUNTER                      RS
WRITLOOP TM    DCBOFLGS-IHADCB(R2),DCBOFOPN  DID WE OPEN THIS ONE?   RS
         BNO   WRITNEXT            IF NOT, DON'T WRITE TO IT         RS
         LR    R1,R2               POINT TO OUTPUT DCB               RS
         LR    R0,R4               POINT TO RECORD                   RS
         PUT   (1),(0)             WRITE A RECORD                    RS
WRITNEXT LA    R2,DCBLEN(,R2)      BUMP TO NEXT OUTPUT DCB           RS
         BCT   R3,WRITLOOP         REPEAT FOR SYSUT2 THRU 9          RS
         LA    R5,1(,R5)      ADD ONE TO RECORD COUNTER              RS
         B     GETRECD        GO TO READ ANOTHER RECORD
REREAD   NI    EXITSW,X'F0'   LOAD ZERO INTO THE REREAD SWITCH
         B     GETRECD        GO TO READ ANOTHER RECORD
*  TERMINATION                                                       RS
EOJHALT  LA    R2,SYSUT2           POINT TO 1ST OUTPUT DCB (SYSUT2)  RS
         LA    R3,8                LOOP COUNTER                      RS
CLOSLOOP TM    DCBOFLGS-IHADCB(R2),DCBOFOPN  DID WE OPEN THIS ONE?   RS
         BNO   CLOSNEXT            IF NOT, DON'T CLOSE IT            RS
         ST    R2,LISTFORM         PUT DCB ADDRESS INTO LIST         RS
         MVI   LISTFORM,X'80'      FLAG END OF LIST                  RS
         LA    R1,LISTFORM         POINT TO LIST FORM                RS
         CLOSE MF=(E,(1))          CLOSE AN OUTPUT DCB               RS
CLOSNEXT LA    R2,DCBLEN(,R2)      BUMP TO NEXT OUTPUT DCB           RS
         BCT   R3,CLOSLOOP         REPEAT FOR SYSUT2 THRU 9          RS
CLOSEIN  LA    R2,SYSUT1           POINT TO SYSUT1 DCB               RS
         ST    R2,LISTFORM         PUT DCB ADDRESS INTO LIST         RS
         MVI   LISTFORM,X'80'      FLAG END OF LIST                  RS
         LA    R1,LISTFORM         POINT TO LIST FORM                RS
         CLOSE MF=(E,(1))          CLOSE SYSUT1                      RS
         CVD   R5,WORKDW           CONVERT RECORD COUNT TO DECIMAL   RS
         MVC   WTOADDR(L'EDCNTR),EDCNTR     MOVE EDIT PATTERN TO WTO
         ED    WTOADDR(L'EDCNTR),WORKDW+1   EDIT COUNT INTO WTO      RS
* WTO
         LH    R0,WTOMSG
         LA    R1,WTOJOB
         CLC   =C'TSO',FLAG
         BNE   WTOSVC
         TPUT  (1),(0),R
         B     TERME
WTOSVC   WTO   MF=(E,WTOMSG)  PRINT IT TO MESSAGE CLASS
TERME    L     R2,RTNCODE          LOAD RETURN CODE (BUT NOT IN R15) RS
         L     R0,GETMPARM         SUBPOOL AND LENGTH OF DYNAM AREA  RS
         LR    R1,R13              POINT TO START OF DYNAMIC AREA    RS
         L     R13,4(,R13)         RESTORE CALLER'S S.A. POINTER     RS
         FREEMAIN R,LV=(0),A=(1)   FREE OUR LAST PIECE OF CORE       RS
         L     R14,12(,R13)        RESTORE R14                       RS
         LR    R15,R2              SET RETURN CODE IN R15            RS
         LM    R0,R12,20(R13)      RESTORE R0-R12                    RS
         BR    R14                 EXIT                              RS
         SPACE 2
EXL      DS    0F             EXIT LIST FOR SYSUT1
         DC    X'85'          INFORMING THE EXIT THAT THIS IS A DCBEXIT
         DC    AL3(DCB1EXIT)   THE ADDRESS OF THE EXIT
         DS    0H             FORCE HALFWORD ALLIGNMENT
DCB1EXIT TM    EXITSW,X'F0'   DCB EXIT--TEST FOR FIRST TIME THROUGH
         BZ    DCBEXIT1        YES, FIRST TIME THROUGH
         OI    EXITSW,X'0F'   TURN ON THE REREAD SWITCH, DCB CLOSED
         B     CKSPAN         CHECK FOR VBS ON SECOND FILE       RS0581
DCBEXIT1 MVI   EXITSW,X'F0'   SET THE FIRST SWITCH OFF
         OI    SYSUT1+(DCBOFLGS-IHADCB),DCBOFPPC  SET CONCAT SWITCH  RS
CKSPAN   DS    0H                                                RS0581
         TM    SYSUT1+(DCBRECFM-IHADCB),DCBRECSB  "S" BIT ON?        RS
         BCR   14,R14         (BNOR)  RETURN IF NOT                  RS
         TM    SYSUT1+(DCBRECFM-IHADCB),DCBRECV  VARIABLE?           RS
         BO    SPANNED        YES--"S" MEANS SPANNED                 RS
         NI    SYSUT1+(DCBRECFM-IHADCB),255-DCBRECSB  NO--"S" MEANS  RS
*                                             STANDARD--TURN IT OFF  RS
         BR    R14            RETURN                                 RS
SPANNED  OI    SYSUT1+(DCBBFTEK-IHADCB),DCBBFTA  SET BFTEK=A         RS
         BR    R14            RETURN
         SPACE 1
EXL2SKEL DS    0F             SKELETON EXIT LIST FOR SYSUT2-9        RS
         DC    X'05'          DCB OPEN EXIT IDENTIFIER               RS
         DC    AL3(DCB2EXIT)   THE ADDRESS OF THE EXIT               RS
         DC    X'87'          RDJFCB EXIT IDENTIFIER                 RS
         DC    AL3(0)         JFCB BUFFER ADDRESS PLUGS IN HERE      RS
         DS    0H             FORCE HALFWORD ALIGNMENT               RS
         USING IHADCB,R1           R1 POINTS TO THE DCB              RS
DCB2EXIT CLC   DCBBLKSI,HZERO      IF NO OUTPUT BLKSIZE,             RS
         BNE   *+10                                                  RS
         MVC   DCBBLKSI,SYSUT1+(DCBBLKSI-IHADCB)  COPY FROM INPUT    RS
         CLC   DCBLRECL,HZERO      IF NO OUTPUT LRECL,               RS
         BNE   *+10                                                  RS
         MVC   DCBLRECL,SYSUT1+(DCBLRECL-IHADCB)  COPY FROM INPUT    RS
         CLI   DCBRECFM,X'00'      IF NO OUTPUT RECFM,               RS
         BCR   7,R14          (BNER)                                 RS
         MVC   DCBRECFM,SYSUT1+(DCBRECFM-IHADCB)  COPY FROM INPUT    RS
         BR    R14            RETURN                                 RS
         DROP  R1                                                    RS
         SPACE 1
*  STATIC AREA (UNCHANGEABLE)                                        RS
DCBSKEL1 DCB   DDNAME=SYSUT1,DSORG=PS,EODAD=EOJHALT,MACRF=GL,EXLST=EXL
         DS    0F                  ALIGN TO FULLWORD                 RS
DCBLEN   EQU   *-DCBSKEL1          LENGTH OF A DCB                   RS
DCBSKEL2 DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,EXLST=EXL2SKEL        RS
SKELOPEN OPEN  (,),MF=L       SKELETON OPEN LIST FORM                RS
WTOSKEL  WTO   'JOBNAMEX/NOSTEPNM N,NNN,NNN,NNN,NNN RECORDS COPIED',   X
               ROUTCDE=11,MF=L
WTOMSGL  EQU   *-WTOSKEL                                             RS
GETMPARM DC    0F'0',AL1(001),AL3(WRKAREAL)  GETMAIN SP AND LENGTH   RS
***** NOTE--CHANGE THIS SUBPOOL IF THIS PROGRAM IS PUT IN LPA. ******RS
HZERO    DC    H'0'
EDCNTR   DC    X'40206B2020206B2020206B2020206B202120'
         LTORG
*  DYNAMIC AREA                                                      RS
WORKAREA DSECT                                                       RS
         DS    18F            STANDARD SAVE AREA                     RS
SYSUT1   DS    CL(DCBLEN)     SYSUT1 DCB BUILT HERE                  RS
SYSUT2   DS    CL(DCBLEN)     SYSUT2 DCB BUILT HERE                  RS
SYSUT3   DS    CL(DCBLEN)     SYSUT3 DCB BUILT HERE                  RS
SYSUT4   DS    CL(DCBLEN)     SYSUT4 DCB BUILT HERE                  RS
SYSUT5   DS    CL(DCBLEN)     SYSUT5 DCB BUILT HERE                  RS
SYSUT6   DS    CL(DCBLEN)     SYSUT6 DCB BUILT HERE                  RS
SYSUT7   DS    CL(DCBLEN)     SYSUT7 DCB BUILT HERE                  RS
SYSUT8   DS    CL(DCBLEN)     SYSUT8 DCB BUILT HERE                  RS
SYSUT9   DS    CL(DCBLEN)     SYSUT9 DCB BUILT HERE                  RS
LISTFORM DS    F              OPEN/CLOSE/RDJFCB LIST FORM BUILT HERE RS
EXL2     DS    2F             OUTPUT DCB EXIT LIST BUILT HERE        RS
FLAG     DS    CL3            PARM IS STORED HERE                    RS
EXITSW   DS    X              EXIT CONTROL SWITCH CONSTANT           RS
WORKDW   DS    D              USED FOR CVD                           RS
RTNCODE  DS    F              RETURN CODE AT TERMINATION             RS
WTOMSG   DS    0F,CL(WTOMSGL)  WTO LIST FORM BUILT HERE              RS
WTOJOB   EQU   WTOMSG+4,8                                            RS
WTOSTEP  EQU   WTOMSG+13,8                                           RS
WTOADDR  EQU   WTOMSG+21,18                                          RS
JFCBBUF  DS    0F,CL176       JFCB BUFFER (WE NEVER LOOK AT IT)      RS
WRKAREAL EQU   *-WORKAREA     LENGTH OF DYNAMIC AREA                 RS
         PRINT NOGEN
         DCBD  DSORG=PS
TIOT     DSECT
         IEFTIOT1
         IHAPSA
         IKJTCB
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
