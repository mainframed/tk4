*          DATA SET WTODB103   AT LEVEL 002 AS OF 05/08/86
//O9080RL  JOB (0052,ACCC),
//         'ASM I. R.',
//         NOTIFY=O9080R,
//         CLASS=I,
//         MSGCLASS=Q,
//         MSGLEVEL=1
//R  EXEC A9294A07,USER='O9080R',SYSOUT='(D,,AA01)'
//AS5 EXEC ACFASM,SOUT='(D,,LA01)',MEMBER=WTODB103
//SYSLIB DD DISP=SHR,DSN=&ACFX..&ACFMAC
//       DD DISP=SHR,DSN=SYS1.MACLIB
//       DD DISP=SHR,DSN=COKE.MACLIB
//       DD DISP=SHR,DSN=SYS1.HASPSRC
//       DD DISP=SHR,DSN=SYS1.AMODGEN
//SYSIN  DD  *
WTODB103 TITLE 'WTODB103 -- WTO EXIT TO HANDLE DATACOM DB00103I MSG'
*
*     THIS IS A TRANSACTIION BACKOUT MESSAGE WHICH CAN AFFECT
*     VALIDITY OF A BACKUP TAKEN AT THIS TIME.
*
*
WTODB103 CSECT
WTODB103 AMODE 31
WTODB103 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTODB103'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)      PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R10
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVI   MGCRTEXT,C' '
         MVC   MGCRTEXT+1(L'MGCRTEXT-1),MGCRTEXT
         MVC   MGCRTEXT(L'SENDHDR),SENDHDR REPLY TO MACRO AREA
         LH    R9,CTXTTLEN    CALCULATE THE LENGTH FOR THE MOVE
         CH    R9,SENDLENG    IS THIS MESSAGE SHORT ENOUGH?
         BNH   SEND0           YES, PROCESS IT
         LH    R9,SENDLENG     NO, USE A SHORTER PART OF THE MESSAGE
SEND0    BCTR  R9,0
         EX    R9,SENDEX      MOVE THE MESSAGE INTO THE BUFFER
         LA    R8,L'SENDHDR+1(R9)
         LA    R7,MGCRTEXT(R8)
         MVI   0(R7),C''''
         LA    R7,1(R7)
         LA    R8,1(R8)
         CLC   =C'JOB-CICS',CTXTTMSG+19  DO NOT HOLD FOR LOGON
         BNE   SEND1
         MVC   0(L'SENDUIDS,R7),SENDUIDS
         LA    R6,L'SENDUIDS
         B     SEND2
SEND1    MVC   0(L'SENDLGN+L'SENDUIDS,R7),SENDLGN
         LA    R6,L'SENDUIDS+L'SENDLGN
SEND2    LA    R3,4(R8,R6)
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
SENDHDR  DC    C'SEND '''
SENDLGN  DC    C',LOGON' THIS MUST BE IMMEDIATELY IN FRONT OF SENDUIDS
SENDUIDS DC    C',USER=(O06376,A00273,O9080R)'
SENDEX   MVC   MGCRTEXT+L'SENDHDR(0),CTXTTMSG MOVE THE MESSAGE
SENDLENG DC    AL2(L'MGCRTEXT-L'SENDHDR-L'SENDLGN-L'SENDUIDS)
         LTORG
         SPACE 3
         PRINT   GEN
         COPY  USERCVT
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTODB103
//L  EXEC ACFLINK,SOUT='*',SYSX='SYS2',LIB=LINKLIB,C=' '
//SYSLIN DD *
  INCLUDE ACFOBJ(WTODB103)
  NAME   WTODB103(R)
