*          DATA SET WTOEI996   AT LEVEL 004 AS OF 11/12/84
WTOEI996 TITLE 'WTOEI996 -- WTOR EXIT TO STORE IMS REPLY NUMBER'
WTOEI996 CSECT
WTOEI996 AMODE 31
WTOEI996 RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEI996'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   ADDRESS OF THE MESSAGE TEXT
         USING CTXTATTR,R10
         CLC   =C'DFS996I',CTXTTMSG IS THIS THE CORRECT MESSAGE?
         BE    START1           YES, PROCESS IT
         CLC   =C'IEE400I',CTXTTMSG MVS MESSAGES CANCELLED
         BE    START1           YES, PROCESS IT
         EX    6,*            NOT SURE WHY I GOT CALLED
START1   L     R8,CVTPTR      LOAD ADDRESS OF USERCVT
         USING CVT,R8
         L     R9,CVTUSER
         LTR   R9,R9          IS IT VALID?
         BNZ   START2          YES
         EX    6,*
         USING USERCVT,R9
         DROP  R8
START2   CLC   =C'IEE400I',CTXTTMSG MVS MESSAGES CANCELLED
         BE    IEE400I          YES, PROCESS IT
         CLC   IMS1,CTXTTMSG+21 WHICH VERSION OF IMS IS THIS?
         BNE   START3          NOT THIS ONE
         LA    R8,IMSAREA1     THIS ONE
         B     START5
START3   CLC   IMS2,CTXTTMSG+21 WHICH VERSION OF IMS IS THIS?
         BE    START4          THIS ONE
         EX    6,*
START4   LA    R8,IMSAREA2     THIS ONE
         USING IMSAREA,R8
START5   SR    R4,R4          CLEAR THE RESULT REGISTERS
         LR    R5,R4
         LM    R6,R7,IMSREPLY LOAD THE "CDS" AREA
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BNO   RETURN          NO, FORGET IT
         LH    R4,CTXTRPID    YES, SAVE IT FOR LATER
         SLL   R4,8           LEAVE ROOM FOR THE FLAG
         DROP  R10
         CLC   =XL5'00',IMSBMPRG IS THERE A BMP TO CANCEL?
         BE    RETURN             NO, SKIP IT
         TM    IMSBMPRG,C' '
         BNO   RETURN             NO, SKIP IT
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLYU),REPLYU MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+6(L'CTXTRPID),CTXTRPID
         MVC   MGCRTEXT+23(L'IMSBMPRG),IMSBMPRG
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLYU
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
         SR    R4,R4
         SR    R5,R5
         B     RETURN1
IEE400I  LA    R8,IMSAREA1    DELETE MESSAGE IDS FROM USERCVT
         USING CTXTATTR,R10
         CLC   IMSREPLY,CTXTTMSG+35
         BE    IEE400I1
         LA    R8,IMSAREA2
         CLC   IMSREPLY,CTXTTMSG+35
         BNE   RETURN2         NOT MINE, FORGET IT
         DROP  R10
IEE400I1 LM    R6,R7,IMSREPLY OVERWRITE THE AREA
         SR    R5,R5
         L     R4,=X'0000C000'
         B     RETURN1
RETURN   IC    R4,IMSFLAG     SAVE THE FLAG FOR LATER
         SLL   R4,8
RETURN1  CDS   R6,R4,IMSREPLY SAVE THE RESULTS
RETURN2  LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
REPLYU   DC    C'REPLY NN,''/STOP REGION XXXXX,ABDUMP'' BY WTOEI996'
         LTORG
         SPACE 3
         PRINT   GEN
         COPY  USERCVT
WORKAREA DSECT
         DS    18F            SAVE AREA
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOEI996
