*          DATA SET WTOEIUTL   AT LEVEL 005 AS OF 11/12/84
WTOEIUTL TITLE 'WTOEIUTL -- WTO EXIT TO ISSUE AN IMS /DISPLAY ACTIVE'
WTOEIUTL CSECT
WTOEIUTL AMODE 31
WTOEIUTL RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOEIUTL'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         CLC   =C'UTL005I',CTXTTMSG IS THIS MESSAGE FROM IEFUTL?
         BNE   START1                NO, TRY AGAIN
         LM    R4,R5,CTXTTMSG+12     YES, PROCESS IT
         B     START2
START1   CLC   =C'UTL006I',CTXTTMSG IS THIS MESSAGE FROM JES2?
         BNE   RETURN                NO, FORGET IT
         LM    R4,R5,CTXTTMSG+12     YES, PROCESS IT
         DROP  R10,R11
START2   L     R10,CVTPTR      FIND THE USERCVT
         USING CVT,R10
         L     R11,CVTUSER
         USING USERCVT,R11
         DROP  R10
         LM    R6,R7,BMPJOBN  STORE JOBNAME INTO USERCVT
         CDS   R6,R4,BMPJOBN
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         MVC   IMSRPLY2,=C'NN' SET THE SECOND REPLY
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'IMSDISA),IMSDISA MOVE REPLY TO MACRO AREA
         LA    R3,(MGCRTEXT-MGCRPL)+L'IMSDISA
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         LA    R10,IMSAREA1   REPLY TO IMS1
         USING IMSAREA,R10
         LM    R2,R3,IMSREPLY
         TM    IMSREPLY,X'F0' IS THIS A REPLY ID?
         BNO   REPLY1          NO, SKIP IT
         TM    IMSREPLY+1,X'F0'
         BNO   REPLY1          NO, SKIP IT
         MVC   IMSRPLY2,IMSREPLY
         SR    R0,R0          DELETE REPLYID
         SR    R1,R1
         CDS   R2,R0,IMSREPLY
REPLY1   LA    R10,IMSAREA2   REPLY TO IMS2
         LM    R2,R3,IMSREPLY
         TM    IMSREPLY,X'F0' IS THIS A REPLY ID?
         BNO   REPLY2          NO, SKIP IT
         TM    IMSREPLY+1,X'F0'
         BNO   REPLY2          NO, SKIP IT
         MVC   MGCRTEXT+6(L'IMSREPLY),IMSREPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         SR    R1,R1          DELETE REPLYID
         CDS   R2,R0,IMSREPLY
         DROP  R10,R11
REPLY2   CLC   MGCRTEXT+6(L'IMSREPLY),=C'NN' IS THERE A VALID REPLY?
         BE    REPLY3          NO, SKIP IT
         MGCR  MGCRPL
REPLY3   CLC   IMSRPLY2,=C'NN' IS THERE A VALID REPLY?
         BE    REPLYEND        NO, SKIP IT
         MVC   MGCRTEXT+6(L'IMSRPLY2),IMSRPLY2
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
REPLYEND L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
IMSDISA  DC    C'REPLY NN,''/DISPLAY ACTIVE'' BY WTOEIUTL'
         LTORG
         SPACE 3
         PRINT   GEN
         COPY  USERCVT
WORKAREA DSECT
         DS    18F            SAVE AREA
IMSRPLY2 DS    CL2            HOLD THE SECOND REPLY
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOEIUTL
