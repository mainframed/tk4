*          DATA SET WTOERNHL   AT LEVEL 004 AS OF 11/30/84
WTOERNHL TITLE 'WTOERNHL -- WTOR EXIT TO REPLY "NOHOLD" TO MESSAGE'
WTOERNHL CSECT
WTOERNHL AMODE 31
WTOERNHL RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOERNHL'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BNO   RETURN          NO, FORGET IT
         GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY),REPLY MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+25(L'CTXTRPID),CTXTRPID
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         TIME  BIN            GET CURRENT TIME OF DAY IN R0
         SRDA  R0,32          CONVERT TO MINUTES
         D     R0,=F'6000'
         SR    R0,R0          CALCULATE THE HOURS
         D     R0,=F'60'       R0=MINUTES  R1=HOURS
         LR    R3,R0           R3=MINUTES
         M     R0,=F'100'
         AR    R3,R1          R3=HOURS AND MINUTES
         A     R3,=F'2' ***** WAIT 'N' MINUTES TO REPLY ************
         CVD   R3,DWORD       CONVERT IT TO DISPLAY
         UNPK  MGCRTEXT+6(5),DWORD+5(3)
         OI    MGCRTEXT+10,C'0'
         MVC   MGCRTEXT+6(2),MGCRTEXT+7
         MVI   MGCRTEXT+8,C'.'
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
REPLY    DC C'$TA,T=HH.MM,''$VS,''''REPLY NN,''''''''NOHOLD'''''''' BY W
               WTOERNHL'''''''
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
DWORD    DS    D
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOERNHL
