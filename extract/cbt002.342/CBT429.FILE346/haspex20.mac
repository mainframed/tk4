*********************************************************************
*                                                                   *
*                        HASP EXIT 20                               *
*                                                                   *
*     This exit gets control at end of job input and performs       *
*     the following functions:                                      *
*                                                                   *
*     > Sets the JES2 reader time and date in the JQE and           *
*       checkpoints the JQE.                                        *
*     > Forces batch jobs to output class "E".                      *
*                                                                   *
*     Entry point: EXIT20                                           *
*                                                                   *
*     Input registers:                                              *
*        R0     Code indicating                                     *
*         - 0 = Normal end of input                                 *
*         - 4 = Job has JECL error                                  *
*        R1-9   N/A                                                 *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    N/A                                                 *
*        R13    PCE address                                         *
*        R14    Return address                                      *
*        R15    Entry address                                       *
*                                                                   *
*     Output registers:                                             *
*        R0-14  Unchanged                                           *
*        R15:   Return code                                         *
*                                                                   *
*     Register usage(internal):                                     *
*        R0-8   Work registers                                      *
*        R9     EXIT20 workarea address                             *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    Base register                                       *
*        R13    PCE address                                         *
*        R14-15 Work and linkage registers                          *
*                                                                   *
*     Note: This module is for use with JES2 Version 6.0.4          *
*                                                                   *
*     Author: Bob Break                                             *
*                                                                   *
*********************************************************************

         COPY  $HASPGBL                  Copy HASP global symbols

HASPEX20 $MODULE ENVIRON=JES2,                                         X
               RMODE=ANY,                                              X
               SPLEVEL=NOCHECK,                                        X
               CVT,                                                    X
               PSA,                                                    X
               $BUFFER,                                                X
               $CADDR,                                                 X
               $DCT,                                                   X
               $HASPEQU,                                               X
               $HCT,                                                   X
               $HCCT,                                                  X
               $JCT,                                                   X
               $JQE,                                                   X
               $MIT,                                                   X
               $MITETBL,                                               X
               $PCE,                                                   X
               $PSV,                                                   X
               $RDRWORK,                                               X
               $SQD,                                                   X
               $TRE,                                                   X
               $USERCBS,                                               X
               $XECB

EXIT20  $ENTRY BASE=R12            Exit routine entry point

*********************************************************************
*                                                                   *
*        Save caller's registers and establish addressabilities.    *
*                                                                   *
*********************************************************************

         USING JCT,R10
EXIT000 $SAVE  NAME=EXIT20         Save caller's registers
         LR    R12,R15             Set base register
         LA    R1,EXIT20WL         Get workarea length
         LA    R1,3(,R1)           Round up to word boundary
         SRL   R1,2                Convert to number of words
         LA    R1,1(,R1)           Add one for identifier
        $GETWORK WORDS=(R1),USE=EX20
         LA    R1,4(,R1)           Point past identifier
         LR    R9,R1               Set workarea address
         LA    R15,EXIT20WL        Set move to length
         XR    R1,R1               Set move from length and pad
         MVCL  R14,R0              Clear workarea
         USING EXIT20W,R9

*********************************************************************
*                                                                   *
*        Obtain the current time of day and date, and then set      *
*        the values into the JQE.                                   *
*                                                                   *
*********************************************************************

EXIT100 $DOGJQE ACTION=(FETCH,READ),JQE=PCEJQE
         LR    R1,R0               Get JQA address
         USING JQE,R1
         CLC   JQERDRON,$ZEROS     Time previously set in JQE?
         BNE   EXIT199             Yes - branch, continue
         DROP  R1                  JQE
         L     R2,RDWSQD           Get SQD address
         USING SQD,R2
         XC    SQDLEVEL+1(SQDEND-(SQDLEVEL+1)),SQDLEVEL+1  Clear SQD
         DROP  R2                  SQD
         LR    R0,R9               Set EXIT20 workarea as parm
        $SUBIT RDRTIME,SQDADDR=(R2),PARM0=(R0)
         LTR   R15,R15             $SUBIT successful?
         BNZ   EXIT199             No - branch, bypass ckpt update

*********************************************************************
*                                                                   *
*        Obtain access to the ckpt queues and checkpoint the JQE.   *
*                                                                   *
*********************************************************************

        $QSUSE                     Request access to checkpoint queues
        $DOGJQE ACTION=(FETCH,UPDATE),JQE=PCEJQE
         LR    R1,R0               Get JQA address
         USING JQE,R1
         MVC   JQERDRON,EX20TIME   Set reader time in JQE
         MVC   JQERDTON,EX20DATE   Set reader date in JQE
        $DOGJQE ACTION=RETURN,CBADDR=JQE
         DROP  R1                  JQE
EXIT199  DS    0H

*********************************************************************
*                                                                   *
*        Force batch jobs to msgclass "E".                          *
*                                                                   *
*********************************************************************

EXIT200  CLI   JCTJOBID,C'J'       Batch job?
         BNE   EXIT299             No - branch, continue
         MVI   JCTMCLAS,C'E'       Reset to msgclass 'E'
EXIT299  DS    0H

*********************************************************************
*                                                                   *
*        Return to caller.                                          *
*                                                                   *
*********************************************************************

RETURN   S     R9,$F4               Point to EXIT20 workarea
        $RETWORK (R9)               Return workarea
         XR     R15,R15             Set routine return code
        $RETURN RC=(R15)            Return to caller

         DROP  R9,R10               EXIT20W, JCT

         LTORG


***************************************************************
*                                                             *
*        RDRTIME - Obtain time of day and date.               *
*                                                             *
*   Function: This routine is called by $SUBIT.  It issues    *
*             the MVS TIME macro to obtain the current time   *
*             of day and date.                                *
*                                                             *
*   Linkage: Via $CALL from the general subtask facility      *
*                                                             *
*   Environment: Subtask                                      *
*                                                             *
*   Recovery: General purpose subtask recovery only.          *
*                                                             *
*   Register usage (entry/exit):                              *
*                                                             *
*    Reg       Value on entry             Value on exit       *
*    ---       --------------             -------------       *
*    R0        EXIT20 workarea address    Unchanged           *
*    R1-9      N/A                        Unchanged           *
*    R10       JCT address                Unchanged           *
*    R11       HCT address                Unchanged           *
*    R12       HASPEX20 base register     Unchanged           *
*    R13       PCE address                Unchanged           *
*    R14       Return address             Unchanged           *
*    R15       Entry address              Unchanged           *
*                                                             *
*   Register usage (internal):                                *
*                                                             *
*    R0-8      Work                                           *
*    R9        RDRTIME workarea address                       *
*    R10       JCT                                            *
*    R11       HCT                                            *
*    R12       RDRTIME base register                          *
*    R13       Save area for MVS services                     *
*    R14-15    Work and linkage                               *
*                                                             *
*   Output from RDRTIME subtask:                              *
*        EX20TIME - Current time of day (hhmmssht)            *
*        EX20DATE - Current date (mmddyyyy)                   *
*                                                             *
***************************************************************

&ANVIRON SETC  'SUBTASK'          Indicate subtask environment
         USING RDRTIME,R12        Set RDRTIME addressability
RDRTIME  BAKR  R14,0              Stack caller's psw/registers
         LR    R12,R15            Set RDRTIME base register
         LR    R3,R0              Set EXIT20 workarea address
        $GETHP TYPE=GET,VERSIZE=RDRTIMWL  Obtain workarea
         LR    R9,R1              Save workarea address
         USING RDRTIMW,R9
         LR    R14,R1             Get address into work reg
         LA    R15,RDRTIMWL       Set move to length
         XR    R1,R1              Set move from length and pad
         MVCL  R14,R0             Clear workarea
         ST    R13,RDRTR13        Save PCE address
         LA    R13,RDRTSAVE       Set save area address for TIME
         TIME  DEC,RDRTIMWT,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM,          X
               MF=(E,TIMELIST)
         L     R13,RDRTR13        Restore PCE address
         USING EXIT20W,R3
         MVC   EX20TIME,RDRTIMWT  Set time in return area
         MVC   EX20DATE,RDRTIMWT+8  Set date in return area
         DROP  R3                 EXIT20W
        $GETHP TYPE=FREE,CELL=(R9)  Free RDRTIME workarea
         PR                       Restore environment/return
         DROP  R9,R12             RDRTIMW, RDRTIME base
&ANVIRON SETC  'JES2'             Cancel subtask environment

         LTORG

        $MODEND

RDRTIMW  DSECT                    RDRTIME subtask workarea
RDRTR13  DS    A                  R13 save area
RDRTSAVE DS    18F                RDRTIME register save area
RDRTIMWT DS    CL16               Return area for time and date
TIMELIST TIME  LINKAGE=SYSTEM,MF=L  Parm list for TIME macro
RDRTIMWL EQU   *-RDRTIMW          RDRTIME workarea length

EXIT20W  DSECT                    EXIT20 workarea
EX20TIME DS    CL8                Reader time
EX20DATE DS    CL8                Reader date
EXIT20WL EQU   *-EXIT20W          EXIT20 workarea length

         END
