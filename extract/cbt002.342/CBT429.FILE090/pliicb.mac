         MACRO
&L       PLIICB &COND,&QUAL,&TYPE=FULL
.* PL/I OPTIMIZER INTERRUPT CONTROL BLOCK (ICB).
         LCLA  &LN
         LCLC  &HEX1,&HEX2
         AIF   (T'&L EQ 'O' AND '&TYPE' EQ 'FULL').NONAME
         AIF   (T'&COND EQ 'O').NOCOND
&HEX2    SETC  '00'
&HEX1    SETC  '02'
         AIF   ('&COND' EQ 'ZDIV' OR '&COND' EQ 'ZERODIVIDE').QUALOK
&HEX1    SETC  '03'
         AIF   ('&COND' EQ 'FOFL' OR '&COND' EQ 'FIXEDOVERFLOW').QUALOK
&HEX1    SETC  '04'
         AIF   ('&COND' EQ 'SIZE').SUBTYPE
&HEX1    SETC  '05'
         AIF   ('&COND' EQ 'CONV' OR '&COND' EQ 'CONVERSION').SUBTYPE
&HEX1    SETC  '06'
         AIF   ('&COND' EQ 'OFL' OR '&COND' EQ 'OVERFLOW').QUALOK
&HEX1    SETC  '07'
         AIF   ('&COND' EQ 'UFL' OR '&COND' EQ 'UNDERFLOW').QUALOK
&HEX1    SETC  '08'
         AIF   ('&COND' EQ 'STRZ' OR '&COND' EQ 'STRINGSIZE').QUALOK
&HEX1    SETC  '09'
         AIF   ('&COND' EQ 'STRG' OR '&COND' EQ 'STRINGRANGE').QUALOK
&HEX1    SETC  '0A'
         AIF   ('&COND' EQ 'SUBG' OR                                   *
               '&COND' EQ 'SUBSCRIPTRANGE').SUBTYPE
&HEX1    SETC  '0B'
         AIF   ('&COND' EQ 'AREA').SUBTYPE
         AIF   ('&COND' EQ 'ERR' OR '&COND' EQ 'ERROR').ERROR
&HEX1    SETC  '0D'
         AIF   ('&COND' EQ 'FIN' OR '&COND' EQ 'FINISH').QUALOK
&HEX1    SETC  '0E'
         AIF   ('&COND' EQ 'COND' OR '&COND' EQ 'CONDITION').COND
&HEX1    SETC  '10'
         AIF   ('&COND' EQ 'KEY').SUBTYPE
&HEX1    SETC  '11'
         AIF   ('&COND' EQ 'RECORD' OR '&COND' EQ 'REC').SUBTYPE
&HEX1    SETC  '12'
         AIF   ('&COND' EQ 'UNDEFINEDFILE').SUBTYPE
         AIF   ('&COND' EQ 'UNDF').SUBTYPE
&HEX1    SETC  '13'
         AIF   ('&COND' EQ 'ENDFILE' OR '&COND' EQ 'ENDF').QUALOK
&HEX1    SETC  '14'
         AIF   ('&COND' EQ 'TRANSMIT' OR '&COND' EQ 'TMIT').SUBTYPE
         MNOTE 8,'&COND IS NOT A VALID PL/1 CONDITION'
         MNOTE 0,'ERROR ASSUMED'
         AGO    .ERROR
.SUBTYPE AIF   (T'&QUAL EQ 'O').NOQUAL
         AIF   (K'&QUAL NE 2).NOTHEX
&HEX2    SETC  '&QUAL'
.QUALOK  AIF   ('&TYPE' NE 'FULL').SNODS
         DS    0F
.SNODS   ANOP
&L       DC    X'&HEX1&HEX2'
         MEXIT
         MNOTE 12,'&COND IS NOT A VALID CONDITION TYPE'
         MEXIT
.NONAME  MNOTE 12,'A NAME MUST BE BE SUPPLIED IN THE LABEL FIELD'
         MEXIT
.NOCOND  MNOTE 12,'THE TYPE OF CONDITION MUST BE SUPPLIED'
         MEXIT
.NOTHEX  MNOTE 8,'QUALIFIER FOR &COND MUST BE TWO HEX DIGITS'
         MEXIT
.COND    AIF   (T'&QUAL EQ 'O').NOQUAL
&LN      SETA  K'&QUAL
&L       DC    0F'0',X'0F00',X'0000',A(&QUAL)
&QUAL    CSECT
&QUAL    AMODE ANY
&QUAL    RMODE ANY
         DC    AL1(&LN),C'&QUAL'
&SYSECT  CSECT
         MEXIT
.*
.NOQUAL  MNOTE 12,'CONDITION QUALIFYIER IS REQUIRED FOR ''&COND'''
         MEXIT
.*
.ERROR   ANOP
&L       DC    0F'0',X'0C00',X'0000'
         MEND
