         MACRO
&ENTRY   DWNMPRLG &DSASIZE=88,&STATIC=
         LCLA  &LEN,&LENFILL
&LEN     SETA  K'&ENTRY
         AIF   (&LEN LE 7).LENOK
         MNOTE 12,'PL/I ENTRY POINT MUST BE NO MORE THAN 7 CHARS.'
         MEXIT
.LENOK   ANOP
         DC    0H'0'
         AIF   (&LEN EQ 7).NOFILL
&LENFILL SETA  7-&LEN
         DC    CL&LENFILL' '
.NOFILL  ANOP
         DC    C'&ENTRY'
         DC    AL1(&LEN)
         ENTRY &ENTRY
&ENTRY   STM   14,12,12(13)
         BALR  11,0
         USING *,11
         LR    4,1
         AIF   (T'&STATIC EQ 'O').NOSTAT
         ICM   3,B'1111',*+76
         USING &STATIC,3
.NOSTAT  ANOP
         ICM   0,B'1111',*+64
         L     1,76(0,13)
         ALR   0,1
         CL    0,12(0,12)
         BNH   *+10
         L     15,116(0,12)
         BALR  14,15
         ST    0,76(0,1)
         ST    1,8(0,13)
         ST    13,4(0,1)
         MVC   72(4,1),72(13)
         LR    13,1
         LR    1,4
         MVI   0(13),X'80'
         MVI   1(13),X'00'
         MVC   84(4,13),*+14
         AIF   (T'&STATIC NE 'O').B16
         B     *+12
         AGO   .DOCONS
.B16     ANOP
         B     *+16
.DOCONS  ANOP
         DC    AL4(&DSASIZE)
         DC    2X'91C0'
         AIF   (T'&STATIC EQ 'O').NOASTAT
         DC    AL4(&STATIC)
.NOASTAT ANOP
         MEND
