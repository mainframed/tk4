//$A1041UB JOB 'MEIER,L.A.SH28205610*110A044         015  1001007037',
//  MSGLEVEL=1,MSGCLASS=H,
//  REGION=1024K,TIME=010,
//  NOTIFY=$A1041
//*MAIN ORG=RMY24,CLASS=L
//*********************************  ($A1041.IMA.RECORD(DFSDVBH0))  ***
//DFSDVBH0 EXEC IMSUPDTE,P=IMA,DISPR=SHR,
//   SYMI=SOURCEDB,SYMO=SOURCEDB,
//   SMEMBER=DFSDVBH0,LMEMBER=DFSDVBHM,
//   RE='RENT,REFR',
//   LOAD='PVT.MODS',
//   SOUT='*',
//   OSLIBS=AMVSXA,
//   ACCT=0
//U.SYSIN  DD  *   RW0026A   ABORT DL/I CALL IN AN I/O LOOP
./  CHANGE  NAME=DFSDVBH0
./ NUMBER INSERT=YES,SEQ1=01800000,NEW1=01800010,INCR=10            #26
         SPACE 3                                                    #26
***********************************************************************
*        MODIFICATION # 26                                            *
*        PURPOSE: TO PERMIT ABORTING A DL/I CALL BEFORE IT COMPLETES. *
*            (AN ILL-CONCEIVED GU WITH 3 SSA'S CAUSED 133,000 I/O'S)  *
*            (AND TOOK 1 HR 45 MIN.                                )  *
*        SUSPECTING SUCH A SITUATION, MASTER TERMINAL OPERATOR ISSUES *
*        A "/STOP REGION ABDUMP TRAN" COMMAND.  THE FOLLOWING CODING  *
*        DETECTS THAT THE SAPSABDP BIT IS ON IN THE SAP AND SETS UP   *
*        FOR IMS TO ABORT THE CALL AND THE TRANSACTION.  IF ANY FORM  *
*        OF DATABASE BACKOUT IS INDICATED, THEN THE ABORT ACTION IS   *
*        NOT TAKEN.                                                   *
***********************************************************************
         LA    R15,SCD             GET THE ADDRESS OF THE SCD.      #26
         NARSCDF R15,NARSCFB0,KNOWN=SCD,FAILX=NOTSTOP               #26
*                                                                   #26
         TM    025*8(R15),X'80'    FEAT=25 SET ON?                  #26
         BZ    NOTSTOP             NO, IGNORE THE FOLLOWING CODE.   #26
*                                                                   #26
         L     R15,PSTSAV1         GET ADDRESS OF SAP.              #26
         USING SAP,R15             TELL THE ASSEMBLER.              #26
         TM    SAPSFLG0,SAPSABDP   "/STOP REGION ABDUMP" PENDING?   #26
         BNO   NOTSTOP             NO.                              #26
*                                                                   #26
         TM    PSTDLSA,PSTDLSA1+PSTDLSA7  IF ANY KIND OF DATABASE   #26
         BNZ   NOTSTOP             BACKOUT IS GOING ON, THEN DO NOT #26
         TM    PSTSYNC,PSTSYNC1    SET THINGS UP TO ABORT THE CALL  #26
         BNZ   NOTSTOP             AND THE TRAN.                    #26
*                                                                   #26
**       ABORT RUNAWAY CALL,  LET CPY00 MAKE IT A U474 PSEUDO ABEND #26
         MVI   PSTSUBCD,X'04'      FOR R15 RETURN CODE.             #26
         MVI   PSTRTCDE,PSTBDCAL   SET "ILLEGAL CALL".              #26
NOTSTOP  DS    0H                                                   #26
         DROP  R15                                                  #26
********************************************************************#26
         SPACE 3                                                    #26
         NARSCD                                                     #26
