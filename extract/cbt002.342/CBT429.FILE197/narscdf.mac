//AIMS31MS JOB 'IMSGROUP  SH28205610*110A044         999  3000007037',
//  REGION=999K,TIME=5,MSGLEVEL=1,MSGCLASS=H,NOTIFY=$A1016
//*MAIN ORG=LOCAL,CLASS=L5
//UPDTE EXEC IMSUPDTE,P=IMA,MEMBER=NARSCDF,DISPR=SHR,
//    SYMI=GENLIB,SYMO=MACLIB,USYSUT2=PERM,
//    SOUT='*',CONDL=0
//SYSIN  DD *
./ ADD NAME=NARSCDF
         MACRO
&N       NARSCDF &B,&TAG,&KNOWN=VCON,&FAILX=
         LCLC  &VCON#,&VCON0,&VCONID
         LCLC  &TAG17,&TAG88,&TAGA,&TAGB
         LCLC  &NOFND
         LCLA  &KIND
.*  BASIC FORM IS-   NARSCDF  REG,SYMBOLNAME
.*   DESIRED PTR IS RETURNED IN REGISTER SPECIFIED BY "REG".
.*    "SYMBOLNAME" = NAME OF A SYMBOL IN THE MACRO=NARSCD.
.*   IF  "KNOWN=SCD"   IS SPECIFIED, TRACK BEGINS IN SCD AND SCD ADDR
.*                                        MUST BE IN SPECIFIED  "REG".
.*   IF  "KNOWN=NARSCD"   SPECIFIED, TRACK BEGINS IN NARSCD & NARSCD
.*                                        MUST BE IN SPECIFIED  "REG".
.*   IF  "KNOWN=PST"   IS SPECIFIED, TRACK BEGINS IN PST AND PST ADDR
.*                                        MUST BE IN SPECIFIED  "REG".
.*      ELSE                         TRACK BEGINS WITH V(NARSCD10).
.*   IF  "FAILX=TAG"   IS SPECIFIED, THEN MACRO BRANCHES TO TAG IF
.*                                 LOCAL BLOCKS NOT FOUND.
.*      ELSE           NOT-FOUND CAUSES PROGRAM CHECK ABEND S/0C6.
.**********************************************************
&NOFND   SETC  '1(&B'.')'
         AIF  ('&FAILX' EQ '').FAILDUN
&NOFND   SETC  '&FAILX'
.FAILDUN ANOP
.**********************************************************
         AIF   ('&TAG'   EQ 'NARSCD10').KIND0
&TAG17   SETC  '&TAG'(1,7)
&TAG88   SETC  '&TAG'(8,1)
&TAGA    SETC  '&TAG'
         AIF   ('&TAG88' EQ '#').KIND1
         AIF   ('&TAG88' EQ '0').KIND2
         AGO                    .KIND3    ELSE
.*      *******
.KIND0   ANOP
&KIND    SETA  0
         AGO   .SPOTJ
.*      *******
.KIND1   ANOP
&KIND    SETA  1
         AGO   .SPOTJ
.*      *******
.KIND3   ANOP
&TAGB    SETC  '&TAG17'.'0'
&KIND    SETA  3
         AGO   .SPOTJ
.*      *******
.KIND2   ANOP
&KIND    SETA  2
.*    **************
.SPOTJ   ANOP
&TAGA    SETC  '&TAG17'.'#'
.**********************************************************
&VCON#    SETC  'NARSCD1#'
&VCON0    SETC  'NARSCD10'
&VCONID   SETC        '1'
.CHKKNOW AIF   ('&KNOWN' EQ     'SCD').SCD
         AIF   ('&KNOWN' EQ  'NARSCD').CHKFIND
         AIF   ('&KNOWN' EQ     'PST').PST
.*                               ELSE .VCON
.*****************************
.VCON    L     &B,=V(&VCON#)
         L     &B,0(&B)
         AGO   .CHKFIND
.*****************************
.SCD     ANOP
         LA    &B,SCDESCDQ-SCD(&B)
RIF&SYSNDX.S ICM  &B,15,0(&B)
         BZ    &NOFND
         CLC   =C'RIS&VCONID',4(&B)
         BNE   RIF&SYSNDX.S
         AGO   .CHKFIND
.*****************************
.PST     ANOP
         LA    &B,PSTEPST-PST(&B)
RIF&SYSNDX.P ICM  &B,15,0(&B)
         BZ    &NOFND
         CLC   =C'RIP1',4(&B)
         BNE   RIF&SYSNDX.P
         L     &B,NARPRS10-NARPST#0(&B)
.***************************************
.CHKFIND ANOP
         AIF   (&KIND EQ 0).MEND
.*       ******
         AIF   (&KIND NE 1).DO23
*                            POINTER TO TAG....# RECKONED FROM NARSCD..
.DO1     LA    &B,&TAGA-&VCON0.(&B)    POINTER TO THE ADDRESS.
         AGO   .MEND
.*       ******
.DO23    ANOP
*                            POINTER TO TAG....0  LOADED  FROM TAG....#
         L     &B,&TAGA-&VCON0.(&B)    POINTER TO THE SUB-AREA.
         AIF   (&KIND NE 3).MEND
.*       ******
.DO3     ANOP
*                            POINTER TO TAG....X RECKONED FROM TAG....0
         LA    &B,&TAG-&TAGB.(&B)      POINTER TO THE SUB-AREA'S FIELD.
.***************************************
.MEND    ANOP
         MEND
