//YA267820 JOB 'LEE       SH28205610*110A044         015  1001007037',
//  MSGLEVEL=1,MSGCLASS=H,
//  REGION=5120K,TIME=010,NOTIFY=YA2678
//*MAIN ORG=RMY24,CLASS=L
//*********************************  (YA2678.IMA.RECORD(DFSIDP20))  ***
//STEP1    EXEC IMSUPDTE,P=IMA,DISPR=SHR,
//   SYMI=SOURCESV,SYMO=SOURCESV,
//   SMEMBER=DFSIDP20,LMEMBER=DFSIDP2G,      <=========
//   RE='RENT,REFR',
//   LOAD='PVT.MODS',
//** CONDL=0,
//   LINEC=54,     USE THIS IF PRINTING ON XEROX 3700
//   SOUT='*',
//   OSLIBS=AMVSXA,
//   ACCT=0
//U.SYSIN1 DD  *          RW0151A  MODS TO THE OUTPUT OF /DIS A COMMAND
./ CHANGE NAME=DFSIDP20
DFSIDP2X DS    0H                                                  #151
*                                                                  #151
         B     TOTALS          14. GO PUT OUT PST TOTAL VALUES     #151
         B     DBRCHK1         15. NOW REALLY GO CHECK DBRC REGION #151
*                                (WE USED NO. 11 TO PUT OUT DASHES)#151
*                                                                  #151
TOTALS   DS    0H                                                  #151
         L     R14,=A(TOTLINE)                                     #151
         BR    R14                                                 #151
         BO    HEADERX                                             #151
         TM    SPADCBFL,SPADREG+SPADDC   SHOW HISTORY + CURRENT?   #151
         BNZ   VTAMHDR                                             #151
HEADERX  DS    0H                                                  #151
         USING H70,R2              ADDRESSABILITY TO DISP DSECT    #151
         MVI   H70NL,NEWLINE       MOVE IN NEWLINE CHAR            #151
         MVC   H70FID(L'A70FID),A70FMT MOVE IN A70 FORMAT ID       #151
         MVC   H70REG(L'REGID),REGID MOVE IN REGID LITERAL         #151
         MVC   H70JOB(L'JOBNAME),JOBNAME MOVE IN JOBNAME LITERAL   #151
         MVC   H70TYPE(L'TYPE),TYPE MOVE IN TYPE LITERAL           #151
         MVC   H70TRAN(L'TRAN),TRAN MOVE IN TRAN LITERAL           #151
         MVC   H70PGM(L'PROGRAM),PROGRAM MOVE IN PROGRAM LITERAL   #151
         MVC   H70STAT(L'STATUS),STATUS MOVE IN STATUS LITERAL     #151
         MVC   H70CLASS(L'CLASS),CLASS MOVE IN CLASS LITERAL       #151
         MVC   H70SCHD(L'SCHD),SCHD    MOVE IN SCHD  LITERAL       #151
         MVC   H70DBCAL(L'DBCAL),DBCAL MOVE IN DBCAL LITERAL       #151
         MVC   H70IWAIT(L'IWAIT),IWAIT MOVE IN IWAIT LITERAL       #151
         MVC   H70MSGS(L'MSGS),MSGS    MOVE IN MSGS  LITERAL       #151
         MVC   H70MSCAL(L'MSCAL),MSCAL MOVE IN MSCAL LITERAL       #151
         LA    R3,H70LEN           INDICATE TOTAL HDR LENGTH.      #151
         TM    SPADCBFL,SPADREG+SPADDC   SHOW HISTORY + CURRENT?   #151
         BNM   BUFFRET             YES, BRANCH.                    #151
         MVC   63(4,R2),=C'ENQS'   CHANGE HEADER FOR JUST CURRENT. #151
         BINHEX PKAREA,PSTPRTY     IF REGN WAITING,                #151
         MVC   27(2,R2),PKAREA+2   GET CUT-OFF PRIORITY.           #151
         BINHEX PKAREA,SMBQCBPR    IF TRAN PRESENT,                #151
         MVC   28(1,R2),PKAREA+3     GET ITS CURRENT PRIORITY.     #151
         BINHEX PKAREA,SMBCLASS    GET ITS CLASS.                  #151
         MVC   27(1,R2),PKAREA+3   MOVE CLASS TO PRINT LINE.       #151
         L     R4,=A(FPCODE)                                       #151
         BR    R4                                                  #151
./ DELETE  SEQ1=08770000,SEQ2=10510000
*                                                                  #151
*                                                                  #151
*                                                                  #151
*                                                                  #151
*                                                                  #151
./ NUMBER INSERT=YES,SEQ1=10860000,NEW1=10860005,INCR=005 10860000 ---
*                                                                  #152
         TM    SCDSTMN2,SCDMSTRT   IS THE MONITOR ACTIVE?          #152
         BZ    OVMONACT            NO, DON'T ADD TO MESSAGE        #152
         MVC   48(21,R2),MNACTMSG  YES, ADD MONITOR ACTIVE MESSAGE #152
         LA    R3,21(,R3)          ADD IN MESSAGE DELTA            #152
OVMONACT EQU   *                                                   #152
         TM    SPADCBFL,SPADDC     /DIS ACTIVE DC?                 #151
         BO    LINECK2             YES, GO PROCESS.                #151
         MVI   SPADNTRY,8          NO, SETUP TIME AND DATE.        #151
         B     TIME8               BRANCH TO PUT OUT TIMESTAMP.    #151
LINECK2  DS    0H                                                  #151
./ NUMBER INSERT=YES,SEQ1=13860000,NEW1=13860005,INCR=001 13860000 ---
NARDASH  DS    0H                                                  #151
         TM    SPADFLAG,X'C0'     AT LEAST 1 MSG OR BMP REG ACTIVE?#151
         BZ    DBRCHK1            NO, IGNORE DASHES & TOTALS LINES.#151
         TM    SPADCBFL,SPADREG+SPADDC  HIST. + CURR. SPECIFIED?   #151
         BM    DBRCHK1            NO, DON'T SET UP FOR PST TOTALS. #151
         MVI   SPADNTRY,14        SET NEXT ENTRY TO DO PST TOTALS. #151
         MVC   4(3,R2),=C'A98'    SET LINE ID = 'A98'              #151
         MVC   9(L'DASHLINE,R2),DASHLINE MOVE DASHES TO OUTPUT AREA#151
         LA    R3,L'DASHLINE+11   SET TOTAL LINE LENGTH.           #151
         B     BUFFRET            GO TO OUTPUT MSG.                #151
         SPACE 3                                                   #151
         B     NARDASH            OUTPUT DASH LINE AND PST TOTALS. #151
DBRCHK1  EQU   *                  NOW HANDLE THE DBRC MESSAGE.     #151
DOUBLE   DC    D'0'                ALIGNMENT.                      #151
*HDRLINE DC    C'REGID JOBNAME  TYPE  TRAN/STEP PROGRAM  CLASS SCHD  MS
*              GS DBCAL MSCAL IWAIT'   PRIOR LINE COL 72 NOW BLANK #151
DASHLINE DC    C'                                                     -*
               ---- ----- -----    -----  -----'                   #151
BLNKS    DC    50X'40'    FOR INIT.                                #151
SCHD     DC    C'SCHD'                SCHD LITERAL                 #151
MSGS     DC    C'MSGS'                MSGS LITERAL                 #151
DBCAL    DC    C'DBCAL'              DBCAL LITERAL                 #151
MSCAL    DC    C'MSCAL'              MSCAL LITERAL                 #151
IWAIT    DC    C'IWAIT'              IWAIT LITERAL                 #151
P1000    DC    P'1000'                                             #151
P500     DC    P'500'                                              #151
         LR    R4,R12                                              #151
         LA    R3,56               INDICATE OUTPUT POS USED.       #151
         L     R14,=A(NARWAY1)                                     #151
         BR    R14                 BUT LET'S DO IT OUR WAY.        #151
         AGO   .OVNAR1                                             #151
.OVNAR1  ANOP                                                      #151
         DROP  R12                                                 #151
         USING DFSIDP2X,R12                                        #151
         PRINT GEN                                                 #151
         A70RW151 ROCKWELL HEADER LINE                             #151
***      ISAP      MOVED DOWN FOR ADDR REASONS                     #151
./ NUMBER INSERT=YES,SEQ1=19960000,NEW1=19960010,INCR=010 19960000 ---
         PRINT GEN                                                 #151
DFSIDP20 CSECT                                                     #151
**************************************************                 #151
**************************************************                 #151
***CHANGED STUFF HAS '****' IN COLS. 1-4 *********                 #151
**************************************************                 #151
*   EARL :   REPL R4 WITH R12 ???*****************                 #151
**************************************************                 #151
**************************************************                 #151
**************************************************                 #151
**************************************************                 #151
*                        **  IDENTITY                              #151
R#S      EQU   2    R2   ** PTR TO ORG OF P LINE.                  #151
R#L      EQU   3    R3   ** LENGTH OF PRINT LINE.                  #151
R#P      EQU   7    R7   ** BASE OF IBM'S PST.                     #151
R#M      EQU   8    R8      LENGTH OF DATA PRINTED FOR THIS ENTRY. #151
R#N      EQU   8    R8      BASE OF NAR'S PST.                     #151
R#A      EQU   9    R9      ACCUMULATOR.                           #151
R#X      EQU   10   R10     PTR TO DRIVING TABLE ENTRY.            #151
R#D      EQU   11   R11     PTR TO VALUE BEING PROCESSED.          #151
R#Z      EQU   14   R14                                            #151
R#W      EQU   15   R15                                            #151
******************************                                     #151
NARWAY1  DC    0Y(0)                                               #151
         USING NARWAY1,R14                                         #151
         LR    R12,R4              POINT R12 TO VERY TOP           #151
         STM   R0,R15,STM0X15      SAVE REGISTERS.                 #151
         LR    R4,R14                                              #151
         DROP  R14                                                 #151
         USING NARWAY1,R4                                          #151
         LA    R#Z,0(R#L,R#S)      BLANK-OUT                       #151
         MVC   0(35,R#Z),BLNKS       OUR PART.                     #151
*        ***                                                       #151
         XC    PKAREA,PKAREA       CLEAR WORK AREA TO ZERO.        #151
         MVN   PKAREA,PSTCLASS     MOVE PST CLASSES TO WORK AREA   #151
         TR    PKAREA,=C'0123456789ABCDEF' AND MAKE THEM PRINTABLE.#151
         MVC   1(4,R#Z),PKAREA     MOVE CLASSES TO PRINT LINE.     #151
         LA    R#L,05(,R#L)        LGTH TO OUR PART.               #151
********************                                               #151
         TPROT 0(R#P),X'70'        CAN WE ACCESS THE PST ADDRESS?  #151
         BNZ   ENDY                NO, AVOID A S/0C4 ABEND.        #151
         LR    R#N,R#P             A(PST) TO R#N                   #151
         NARPSTF R#N,FAILX=ENDY    FIND LOCAL AUXILIARY PST.       #151
         L     R#W,NARPSTY0-NARPST#0(R#N)  FIND HUGH'S ACCT AREA.  #151
         ST    R#W,XPSTBAS         STO AS BASE FOR TABLE.          #151
         L     R#W,NARPSTJ0-NARPST#0(R#N)  FIND LOU'S ACCT AREA.   #151
         ST    R#W,KPSTBAS         STO AS BASE FOR TABLE.          #151
******************************                                     #151
*        MVC   RISPCBFL,SPADCBFL   SAVE SPADCBFL                   #151
         TM    SPADCBFL,SPADREG+SPADDC   SHOW HIST. + CURR. ?      #151
         BNM   SETR6ALL            YES, SET R6 TO 'ALL' LIST.      #151
         L     R6,PSTSUPNQ         CALCULATE NUMBER OF UPDATE      #151
         S     R6,PSTSUPDQ          ENQUEUES - # OF DEQUEUES.      #151
         ST    R6,NQLESSDQ         SAVE VALUE FOR DISPLAY.         #151
         LA    R6,DRVLIST2         POINT TO REG-ONLY DRIVE LIST.   #151
         B     LOOPSPST                                            #151
*                                                                  #151
SETR6ALL DS    0H                                                  #151
         LA    R6,DRIVTADD         POINT TO 'SYS' DRIVE LIST.      #151
********************                                               #151
LOOPSPST XR    R#A,R#A             CLEAR ACCUM.                    #151
         L     R10,0(,R6)          GET PTR TO TABLE ENTRY.         #151
*        ***********                                               #151
LOOPXPST DS    0H                                                  #151
         SR    R#D,R#D             CLEAR R11.                      #151
         ICM   R#D,7,1(R#X)        GET DATA PTR, OFFSET, OR ZERO.  #151
         BZ    OVACC               IF ZERO, NO VALUE TO PROCESS.   #151
*         *                                                        #151
         CLI   1(R#X),0            POINTER OR OFFSET?              #151
         BNE   CUMMDATA            POINTER; SKIP ADDING BASE.      #151
         TM    4(R#X),X'01'        OFFSET; WHICH BASE TO BE USED?  #151
         BZ    ISITXBAS                                            #151
         A     R#D,KPSTBAS         ADD-ON THE KBASE.               #151
         B     CUMMDATA                                            #151
ISITXBAS EQU   *                                                   #151
         TM    4(R#X),X'02'        IS IT A(PST)                    #151
         BNZ   ADDPST              YES, BRANCH                     #151
         A     R#D,XPSTBAS         ADD-ON THE XBASE.               #151
         B     CUMMDATA                                            #151
ADDPST   EQU   *                                                   #151
         AR    R#D,R#P             ADD-ON A(CURRENT PST)           #151
*        ***                                                       #151
CUMMDATA EQU   *                   HERE TO ACCUMULATE DATA.        #151
         TM    4(R#X),X'20'        HALF-WORD ADD?                  #151
         BZ    FULLADD             NO, BRANCH                      #151
         AH    R#A,0(,R#D)         ACCUMULATE DATA.                #151
         B     OVACC                                               #151
FULLADD  EQU   *                                                   #151
         A     R#A,0(,R#D)         ACCUMULATE DATA.                #151
OVACC    EQU   *                                                   #151
*        ***                                                       #151
         CLI   0(R#X),0            Q. CONTINUE OR PRINT.           #151
         BNE   PRNTRTN1            PRINT.                          #151
         LA    R#X,8(,R#X)         BUMP TABLE PTR.                 #151
         B     LOOPXPST            CONTINUE ACCUMULATING.          #151
*        ******                                                    #151
PRNTRTN1 DS    0H                                                  #151
         CVD   R#A,PKAREA          PRINT THE RESULT.               #151
         TM    4(R#X),X'80'        SHOULD WE DIVIDE BY 1000?       #151
         BZ    OVDIVK1             NO                              #151
         CP    PKAREA(8),P1000     HAS VALUE REACHED 1000 YET?     #151
         BL    OVDIVK1             NO, DON'T DIVIDE                #151
         OI    4(R#X),X'40'        SHOW THAT WE DID DIVIDE BY 1000 #151
         AP    PKAREA(8),P500      ROUND VALUE TO NEAREST 1000     #151
         DP    PKAREA(8),P1000     DIVIDE VALUE BY 1000            #151
         UNPK  DOUBLE,PKAREA(5)                                    #151
         B     OVDIVK2                                             #151
OVDIVK1  EQU   *                                                   #151
         UNPK  DOUBLE,PKAREA(8)                                    #151
OVDIVK2  EQU   *                                                   #151
         OI    DOUBLE+L'DOUBLE-1,X'F0'                             #151
*        ***                                                       #151
         LA    R#Z,0(R#L,R#S)      CALC FBA.  (TO)                 #151
         IC    R#M,0(,R#X)         NBR OF CHARS.                   #151
         BCTR  R#M,0                (MACH CODE)                    #151
         N     R#M,=A(7)           MAX.                            #151
         LA    R#W,DOUBLE+L'DOUBLE-1   CALC. END OF AREA ADDRESS   #151
         LR    R#A,R#W             CALC.                           #151
         SR    R#A,R#M               FBA.                          #151
EDITLOOP EQU   *                                                   #151
         CLI   0(R#A),X'F0'        LEADING ZERO?                   #151
         BNE   ENDEDIT             NO, STOP THE EDIT LOOP          #151
         MVI   0(R#A),X'40'        REPLACE ZERO WITH A BLANK       #151
         LA    R#A,1(,R#A)         BUMP TO NEXT POSITION           #151
         CR    R#A,R#W             ARE WE AT LAST POSITION?        #151
         BL    EDITLOOP            NO, CONTINUE TO EDIT LEADING 0'S#151
ENDEDIT  EQU   *                                                   #151
         SR    R#W,R#M             CALC. FBA.  (FROM)              #151
         MVI   0(R#Z),C' '                                         #151
         TM    4(R#X),X'40'        HAVE WE DIVIDED BY 1000?        #151
         BZ    OVDIVK3             NO                              #151
         BCTR  R#M,0                                               #151
         LA    R#W,1(,R#W)                                         #151
OVDIVK3  EQU   *                                                   #151
         EX    R#M,MOVEXP          MOVE PRINTABLES.                #151
************                                                       #151
         TM    4(R#X),X'40'        HAVE WE DIVIDED BY 1000?        #151
         BZ    OVDIVK4             NO                              #151
         LA    R#M,1(,R#M)                                         #151
         LA    R#W,1(R#Z,R#M)      FIND K COLUMN                   #151
         MVI   0(R#W),C'K'                                         #151
OVDIVK4  EQU   *                                                   #151
         NI    4(R#X),X'BF'        TURN OFF DIV-BY-1000 INDICATOR  #151
         LA    R#L,2(R#L,R#M)      BUMP LENGTH.                    #151
         LA    R6,4(,R6)           BUMP TO NEXT LIST ENTRY.        #151
         CLI   0(R6),X'FF'         AT END OF LIST?                 #151
         BE    ENDX                YES, CLEAN UP AND LEAVE.        #151
         B     LOOPSPST            NEXT ACCUMULATION GROUP.        #151
***************************************                            #151
ENDX     EQU   *                                                   #151
         LM    R4,R15,STM0X15+(4*R4)   RESTORE REGS R4 - R15       #151
         LA    R3,1(,R3)                                           #151
         B     INITMSG3                                            #151
************                                                       #151
MOVEXP   MVC   1(0,R#Z),0(R#W)                                     #151
****************************************                           #151
ENDY     EQU   *                                                   #151
         LA    R#Z,0(R#L,R#S)      CALC FBA.  (TO).                #151
         MVC   0(22,R#Z),=C' NARXPST NOT AVAILABLE'                #151
         LA    R#L,22(,R#L)        BUMP LENGTH.                    #151
         B     ENDX                RETURN.                         #151
         EJECT                                                     #151
***************************************                            #151
** THE FOLLOWING TABLE OF DBLE WRDS CONTROLS THE ADD'L PST DISPLAY.#151
**       XX00JKLM   ADD-IN VALUE IN WORD @ NARPSTX0 + JKLM         #151
**       XXADDRES   ADD-IN VALUE IN WORD @ LOCATION   ADDRES       #151
**       00XXXXXX   CONTINUE ACCUM AFTER ADDING-IN THIS VALUE.     #151
**       NNXXXXXX   THIS IS LAST VALUE. CONV TO NN DIGITS & PRINT. #151
**       XX000000   NO VALUE TO BE PROCESSED.                      #151
**       FFFFFFFF   END-OF-LIST ID IS X'FF'.                       #151
**                                                                 #151
**               XX   FLAG BYTE                                    #151
**               80   1: VALUE CAN BE DIVIDED BY 1000              #151
**                    0: VALUE CANNOT BE DIVIDED BY 1000           #151
**               40   1: VALUE HAS BEEN DIVIDED BY 1000            #151
**                    0: VALUE HAS NOT BEEN DIVIDED BY 1000        #151
**               20   1: PERFORM HALF-WORD ADDITION                #151
**                    0: PERFORM FULL-WORD ADDITION                #151
**               10      RESERVED                                  #151
**               08      RESERVED                                  #151
**               04      RESERVED                                  #151
**               02   1: ADD A(PST)  TO OFFSET JKLM                #151
**               01   1: ADD KPSTBAS TO OFFSET JKLM                #151
**                    0: ADD XPSTBAS TO OFFSET JKLM                #151
**                                                                 #151
**                                                                 #151
**                                                                 #151
         PRINT DATA                                                #151
DRIVTABL DC    0D'0'                                               #151
DRIVTAB1 DC    AL1(0),AL3(NARPSTL0-NARPSTX0),A(0)      # PGM LOADS #151
         DC    AL1(5),AL3(0),X'00',AL3(0)                          #151
**                                                                 #151
DRIVTB1A DC    AL1(0),AL3(NQLESSDQ),A(0)               # ENQS      #151
         DC    AL1(5),AL3(0),X'00',AL3(0)                          #151
**                                                                 #151
**                                                                 #151
N$1      EQU   NARPSTN0-NARPSTX0                                   #151
DRIVTAB4 DC          (((PSTDREPL+0-PSTDGU+000)/4)+1)A(((*-DRIVTAB4)/2)+*
               N$1+PSTDGU+000-PSTACCT+00,0)            # DB CALLS  #151
DRIVTB4A DC          (((PSTDREPL+0-PSTDGU+000)/4)+1)A(((*-DRIVTB4A)/2)+*
               PSTDGU+000-PST+00,2*16777216)           # DB CALLS  #151
         DC    AL1(5),AL3(0),X'80',AL3(0)                          #151
**                                                                 #151
**                                                                 #151
DRIVTAB6 DC    AL1(0),AL3(NARPSTI0-NARPSTX0),X'00',AL3(0) # IWAITS #151
DRIVTB6A DC    AL1(0),AL3(NARPIWT0-NARPSTK0),X'01',AL3(0) # IWAITS #151
         DC    AL1(5),AL3(0),X'80',AL3(0)                          #151
DRIVTAB2 DC    AL1(0),AL3(NARPSTM0-NARPSTX0),A(0)      # MSGS      #151
DRIVTB2A DC    AL1(0),AL3(PSTDQCNT-PST),X'22',AL3(0)   # MSGS      #151
         DC    AL1(8),AL3(0),X'00',AL3(0)                          #151
DRIVTAB5 DC          (((PSTMPURG+0-PSTMGU+000)/4)+1)A(((*-DRIVTAB5)/2)+*
               N$1+PSTMGU+000-PSTACCT+00,0)            # TP CALLS  #151
DRIVTB5A DC          (((PSTMPURG+0-PSTMGU+000)/4)+1)A(((*-DRIVTB5A)/2)+*
               PSTMGU+000-PST+00,2*16777216)           # TP CALLS  #151
         DC    AL1(6),AL3(0),X'80',AL3(0)                          #151
**                                                                 #151
         DC   2F'-1'                                               #151
         PRINT NODATA                                              #151
******************************                                     #151
XPSTBAS  DC    A(0)                                                #151
KPSTBAS  DC    A(0)                                                #151
NQLESSDQ DC    A(0)                                                #151
DRIVTADD DC    A(DRIVTAB1)                                         #151
         DC    A(DRIVTAB4)                                         #151
         DC    A(DRIVTAB6)                                         #151
         DC    A(DRIVTAB2)                                         #151
         DC    A(DRIVTAB5)                                         #151
         DC    F'-1'                                               #151
DRVLIST2 DC    A(DRIVTB1A)                                         #151
         DC    A(DRIVTB4A)                                         #151
         DC    A(DRIVTB6A)                                         #151
         DC    A(DRIVTB2A)                                         #151
         DC    A(DRIVTB5A)                                         #151
         DC    F'-1'                                               #151
**************************************************                 #151
         EJECT                                                     #151
TOTLINE  DS    0H                                                  #151
         DROP  R4                                                  #151
         USING TOTLINE,R14                                         #151
         STM   R0,R15,STM0X15      SAVE REGS                       #151
         LR    R4,R14                                              #151
         LA    R14,TOTLINE-NARWAY1                                 #151
         SR    R4,R14                                              #151
         USING NARWAY1,R4                                          #151
         DROP  R14                                                 #151
         LA    R10,DRIVTADD        POINT TO TABLE OF ADCONS        #151
         LA    R3,61               PRESET CHAR POSITION IN LINE    #151
         SR    R8,R8                                               #151
*                                                                  #151
A        EQU   *                                                   #151
         SR    R9,R9               SET ACCUMULATOR TO ZERO         #151
         LR    R5,R11              PUT A(SCD) INTO R5              #151
         NARSCDF R5,NARPSTSL,KNOWN=SCD   FIND NAR PST/EXT LIST.    #151
         L     R7,4(,R5)           GET FIRST A(PST) FROM LIST.     #151
         LTR   R7,R7                                               #151
         BZ    F                   IF ADR = 0, GO LOOK FOR ANOTHER.#151
*                                                                  #151
B        EQU   *                                                   #151
         TPROT 0(R7),X'70'         CAN WE ACCESS THE PST ADDRESS?  #151
         BNZ   F                   NO, AVOID A S/0C4 ABEND.        #151
         LR    R8,R7               ADDR OF PST TO R8               #151
         NARPSTF R8,FAILX=F        FIND NAR'S AUXILIARY PST        #151
*                                                                  #151
         L     R15,NARPSTY0-NARPST#0(R8)  NOW FIND ACCTNG AREA     #151
         ST    R15,XPSTBAS         STORE AS BASE FOR HUGH'S CUMMS  #151
         L     R15,NARPSTJ0-NARPST#0(R8)  NOW FIND ACCTNG AREA     #151
         ST    R15,KPSTBAS         STORE AS BASE FOR LOU'S ACCTNG  #151
         L     R8,0(,R10)          POINT TO START OF INNER TABLE   #151
*                                                                  #151
C        EQU   *                                                   #151
         SR    R6,R6               CLEAR R6                        #151
         ICM   R6,7,1(R8)          GET DATA PTR, OFFSET, OR ZERO.  #151
         BZ    E                   IF ZERO, NO VALUE TO PROCESS.   #151
         CLI   1(R8),0             POINTER OR OFFSET?              #151
         BNE   D                   POINTER, SKIP ADDING BASE       #151
         TM    4(R8),X'01'         OFFSET, WHICH BASE DO WE USE?   #151
         BZ    C1                  GO SEE IF IT IS XBASE           #151
         A     R6,KPSTBAS          ADD-ON THE KBASE                #151
         B     D                                                   #151
C1       EQU   *                                                   #151
         TM    4(R8),X'02'         IS IT A(CURRENT PST)?           #151
         BNZ   C2                  YES, BRANCH                     #151
         A     R6,XPSTBAS          ADD-ON THE XBASE                #151
         B     D                                                   #151
C2       EQU   *                                                   #151
         AR    R6,R7               ADD-ON A(CURRENT PST)           #151
*                                                                  #151
D        EQU   *                                                   #151
         TM    4(R8),X'20'         IS IT HALF-WORD ADD?            #151
         BZ    D1                  NO, BRANCH                      #151
         AH    R9,0(,R6)           ACCUMULATE THE DATA             #151
         B     E                                                   #151
D1       EQU   *                                                   #151
         A     R9,0(,R6)           ACCUMULATE THE DATA             #151
*                                                                  #151
E        EQU   *                                                   #151
         CLI   0(R8),0             CONTINUE OR SHIFT TO NEXT PST?  #151
         BNE   F                   SHIFT TO NEXT PST               #151
         LA    R8,8(,R8)           BUMP TO NXT ENTRY IN INNER TABLE#151
         B     C                   CONTINUE WITH CURRENT PST       #151
*                                                                  #151
F        EQU   *                                                   #151
         LA    R5,08(,R5)          BUMP TO NXT ENTRY IN PSTEXT LIST#151
         CLI   0(R5),X'FF'         AT END OF LIST?                 #151
         BE    F1                  YES, BRANCH.                    #151
         L     R7,4(,R5)           GET ADDR OF NEXT PST FROM LIST. #151
         LTR   R7,R7               IF ZERO, GO BACK THROUGH LOOP.  #151
         BZ    F                   IF ADR = 0, GO LOOK FOR ANOTHER.#151
         B     B                   CONTINUE WITH NEXT PST.         #151
*                                                                  #151
F1       EQU   *                                                   #151
         LTR   R8,R8                                               #151
         BZ    M                                                   #151
         CVD   R9,PKAREA           PREPARE THE CUMM VALUE FOR PRINT#151
         CP    PKAREA(8),P1000     HAS VALUE REACHED 1000 YET?     #151
         BL    G                   NO, DON'T DIVIDE BY 1000        #151
         OI    4(R8),X'40'         SHOW THAT WE DID DIVIDE BY 1000 #151
         AP    PKAREA(8),P500      ROUND VALUE UP TO NEAREST 1000  #151
         DP    PKAREA(8),P1000     DIV ROUNDED CUMM VALUE BY 1000  #151
         UNPK  DOUBLE,PKAREA(5)    UNPACK QUOTIENT                 #151
         B     H                                                   #151
*                                                                  #151
G        EQU   *                                                   #151
         UNPK  DOUBLE,PKAREA(8)    UNPACK WHOLE VALUE              #151
*                                                                  #151
H        EQU   *                                                   #151
         OI    DOUBLE+7,X'F0'      MAKE SURE SIGN IS PLUS          #151
         LA    R14,0(R2,R3)        CALCULATE FBA.  (TO)            #151
         IC    R7,0(,R8)           PICK UP NUMBER OF CHARS TO PRINT#151
         BCTR  R7,0                MAKE IT EXECUTABLE              #151
         N     R7,=A(7)            MAKE SURE IT'S NOT LARGER THAN 7#151
         LA    R15,DOUBLE+7        CALC. END OF AREA ADDRESS       #151
         LR    R9,R15                                              #151
         SR    R9,R7                                               #151
*                                                                  #151
I        EQU   *                                                   #151
         CLI   0(R9),X'F0'         LEADING ZERO?                   #151
         BNE   J                   NO, STOP THE EDIT LOOP          #151
         MVI   0(R9),X'40'         REPLACE ZERO WITH A BLANK       #151
         LA    R9,1(,R9)           BUMP TO NEXT POSITION           #151
         CR    R9,R15              ARE WE AT LAST POSITION?        #151
         BL    I                   NO, CONTINUE EDITING LEADING 0'S#151
*                                                                  #151
J        EQU   *                                                   #151
         SR    R15,R7              CALC. FBA.   (FROM)             #151
         MVI   0(R14),X'40'                                        #151
         TM    4(R8),X'40'         HAVE WE DIVIDED BY 1000?        #151
         BZ    K                   NO, DON'T ADJUST REGS           #151
         BCTR  R7,0                MOVE ONE LESS CHARACTER         #151
         LA    R15,1(,R15)          FROM DOUBLE ARRAY              #151
*                                                                  #151
K        EQU   *                                                   #151
         EX    R7,MOVEXP           MOVE PRINTABLES                 #151
         TM    4(R8),X'40'         DID WE DIVIDE BY 1000?          #151
         BZ    L                   NO                              #151
         LA    R7,1(,R7)           YES, RESET CHAR COUNT           #151
         LA    R15,1(R14,R7)       FIND K COLUMN                   #151
         MVI   0(R15),C'K'                                         #151
*                                                                  #151
L        EQU   *                                                   #151
         NI    4(R8),X'BF'         TURN OFF DIV-BY-1000 FLAG       #151
         LA    R3,2(R7,R3)         BUMP COUNT OF CHARACTERS        #151
*                                                                  #151
M        EQU   *                                                   #151
         LA    R10,4(,R10)         BUMP TO NEXT ADCON ADDRESS      #151
         CLI   0(R10),X'FF'        ARE WE THRU THIS LIST?          #151
         BNE   A                   NO, LOOP BACK                   #151
         LM    R4,R15,STM0X15+(4*R4)  RESTORE REGS R4 THRU R15     #151
         LA    R3,1(,R3)                                           #151
         MVI   SPADNTRY,15         SET RETURN TO PROCESS DBRC MSG. #151
         B     BUFFRET                                             #151
         SPACE 2                                                   #151
***********************************************************************
*                                                                     *
*  -------                                                            *
*  ENTRY 3   - PROCESS ALL FAST PATH REGIONS                          *
*  -------                                                            *
*                                                                     *
***********************************************************************
***********************************************************************
******** FAST PATH CODE MOVED DOWN HERE FOR ADDR. REASONS *************
***********************************************************************
FPCODE   DS    0H                                              @BM60942
         USING FPCODE,R4                                        #151
         L     R15,SCDFRB          A(FRB)                      @PL52295
         USING FRB,R15             SET BASE                    @PL52295
         LTR   R15,R15             XRF CAPABLE SYSTEM?         @PL52295
         BNM   FPCHKA              NO, USE IMSID FROM SCD      @PL52295
         LA    R14,FRBRSENM        YES, USE RSENAME FROM FRB   @PL52295
         B     FPCHKB              GO SCAN FOR LCRE            @PL52295
         DROP  R15                                             @PL52295
FPCHKA   DS    0H                                              @PL52295
         LA    R14,SSCDIMID        ADDRESS OF IMSID            @PL52295
FPCHKB   DS    0H                                              @PL52295
         BAL   R5,SCANLCRE        FIND NEXT LCRE OFF SIDX      @BIM8USS
         LTR   R15,R15             DID WE FIND AN LCRE?        @PL52295
         BNZ   FPEND              NO, DONE WITH FP DISPLAYS    @BIHC9AS
         L     R1,SPADLBLK        RESTORE PTR TO NEW LCRE      @BM60618
         USING DFSLCRE,R1         ADDRESSABILITY.              @BM60618
         L     R7,LCREPSTA        GET ADDR ACTIVE PST(THERE IS @BM60618
         DROP  R1                 AN LCRE FOR EACH ACTIVE PST).@BM60618
**             DELETED BY PL52295                              @PL52295
**             DELETED BY PL52295                              @PL52295
**             DELETED BY PL52295                              @PL52295
**             DELETED BY PL52295                              @PL52295
**             DELETED BY PL52295                              @PL52295
*----------------------------------------------------------------------
*  A51 FP REGION JOB TERMINATING
*----------------------------------------------------------------------
FPCHK1   DS    0H                                              @BIM8USS
         USING A51,R2                                          @BIM8USS
**             DELETED BY PL52295                              @PL52295
         TM    PSTTERM,PSTFPA     IS THIS A FAST PATH REGION   @BIM8USS
****     BZ    FPCHK              NO: SCAN OUT NEXT PST        @BIHC9AS
         BZ    FPCODE             NO: SCAN OUT NEXT PST        #151
         TM    PSTABTRM,PSTIQABN+PSTRGTRM  JOB TERMINATING     @BIL8TCH
         BZ    FPCHK1A            NO, SKIP TERMINATE MSG       @BIM8USS
         BAL   R1,INITMSGA        YES, SET UP STANDARD MSG     @BIM8USS
         MVC   A51TERM(L'TERM),TERM TERMINATE MESSAGE          @BIM8USS
         MVC   A51FID(L'A51FID),=C'A51' FORMAT ID ='A51'       @BIM8USS
         B     FP03               GO OUTPUT MESSAGE.           @BIM8USS
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  CHECK FOR HOTSTANDBY BACKUP DISPLAY
*  A60 FP REGION WAITING FOR SWITCHOVER
*----------------------------------------------------------------------
FPCHK1A  DS    0H                                              @BIM8USS
         USING A60,R2                                          @BIM8USS
         L     R10,SCDFRB         GET A(FAST RESTART BLOCK).   @BIJ1SJH
         LTR   R10,R10            SYSTEM 'HOTSTANDBY CAPABLE'? @BIJ1SJH
         BNM   FPCHK2             NO... BYPASS SPECIAL MESSAGE @BMK0088
         PUSH  USING                                           @BIJ1SJH
         USING FRB,R10            ADDRESSABILITY TO FRB.       @BIJ1SJH
         TM    FRBFLAG1,FRBACTV+FRBRSTRT    BACKUP SYSTEM?     @BIJ1SJH
         BNO   FPCHK2             NO..BYPASS SPECIAL MESSAGE.  @BIJ1SJH
         BAL   R1,INITMSGA        GO SET UP STANDARD MESSAGE.  @BIJ1SJH
         MVC   A60FID(L'A60FID),=C'A60' FORMAT ID = 'A60'      @BIM8USS
         MVC   A60TP(L'FP),FP     REGION TYPE, WAITING FOR     @BIM8USS
         MVC   A60WAIT(L'SWITCH),SWITCH WAITING FOR SWITCHOVER @BM20126
         B     FP03               PROCESS NEXT REGION.         @BIJ1SJH
         POP   USING                                           @BIJ1SJH
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  A58 FP REGION SCHEDULING
*----------------------------------------------------------------------
FPCHK2   DS    0H                                              @BIM8USS
         USING A58,R2                                          @BIM8USS
         L     R10,PSTPSB         GET ADRS OF PSBD             @BIJ1SJH
         LTR   R10,R10            DOUBLE CHECK PDIR POINTER    @BIJ1SJH
         BNZ   CKWIFP             IF NON-ZERO, BRANCH          @BIJ1SJH
         BAL   R1,INITMSGA        GO TO SET UP STANDARD MSG    @BIJ1SJH
         MVC   A58FID(L'A58FID),=C'A58' FORMAT ID = 'A58'      @BIM8USS
         MVC   A58TP(L'FP),FP     SET 'FP' REGION TYPE         @BIM8USS
         MVC   A58WAIT(L'SCHED),SCHED   'SCHEDULING'           @BIM8USS
         B     FP03                                            @BM10348
*----------------------------------------------------------------------
*  A58 FP REGION WAITING
*----------------------------------------------------------------------
CKWIFP   DS    0H                                              @BM10348
         L     R14,PSTSAV1        GET SAP ADDR                 @BM10348
         USING SAP,R14                                    #151
         TM    SAPSFLG0,SAPSWAIT  IS THE PST IN IWAIT          @BM10348
         BZ    CKWIFP2            NO - BRANCH                  @BM10348
         BAL   R1,INITMSGA        GO TO SET UP STANDARD MSG    @BM10348
         TM    PDIRFLG3,PDIRIFPR  FP APPLICATION PROGRAM?      @BMV0082
****     BNO   FPCHK              NO... OOPS, LOOK AT NEXT PST @BMV0082
         BNO   FPCODE             NO... OOPS, LOOK AT NEXT PST #151
         MVC   A58TP(L'FPM),FPM   PRIME WITH FPM.              @BIM8USS
         TM    PDIRFLG3,PDIRIFPM  FP MESSAGE DRIVEN PROGRAM?   @BMV0082
         BO    CKWIFPA            YES... LEAVE TYPE AS IS.     @BMV0082
         TM    PDIRFLG3,PDIRIFPU  FP UTILITY PROGRAM?          @BMV0082
****     BNO   FPCHK              NO... OOPS, LOOK AT NEXT PST @BMV0082
         BNO   FPCODE             NO... OOPS, LOOK AT NEXT PST #151
         MVC   A58TP(L'FPU),FPU   SHOW FPU.                    @BIM8USS
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  A59 FP REGION WAITING FOR INTENT OR POOL SPACE
*----------------------------------------------------------------------
CKWIFPA  DS    0H                                              @BMV0082
         USING A59,R2                                          @BIM8USS
         L     R15,PSTSTRAC+4     GET LAST 8 TRACE ENTRIES     @BM10348
         N     R15,MASK000F       MASK OFF HIGHEST 28 BITS     @BM10348
         C     R15,TRACE1         IS WAIT FOR INTENT OR POOL   @BMK0088
         BNE   CKWIFP1            NO - BRANCH                  @BM10348
         MVC   A59FID(L'A59FID),=C'A59' FORMAT ID = 'A59'      @BIM8USS
         MVC   A59WAIT(L'FINPLSP),FINPLSP WAITING FOR INTENT   @BIM8USS
         B     FP03                                            @BM10348
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  A57 FP REGION WAITING FOR BLOCK MOVER
*----------------------------------------------------------------------
CKWIFP1  DS    0H                                              @BM10348
         USING A57,R2                                          @BIM8USS
         MVC   A57FID(L'A57FID),=C'A57' FORMAT ID = 'A57'      @BIM8USS
         MVC   A57WAIT(L'WBLKMVR),WBLKMVR WAITING FOR BLOCK MVR@BM20126
         B     FP03                                            @BM10348
         DROP  R14                                             @BM10348
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  A00 CHECK IF FAST PATH UTILITY
*----------------------------------------------------------------------
CKWIFP2  DS    0H                 FP REGION NOT IWAITING..     @BIHC9AS
         USING A00,R2                                          @BIM8USS
         TM    PDIRFLG3,PDIRIFPR  FP APPLICATION PROGRAM?      @BMV0082
****     BNO   FPCHK              NO... OOPS, LOOK AT NEXT PST @BMV0082
         BNO   FPCODE             NO... OOPS, LOOK AT NEXT PST #151
         TM    PDIRFLG3,PDIRIFPM   FP MESSAGE DRIVEN PROGRAM?  @BMV0082
         BO    FP01                YES... GO SHOW IT.          @BMV0082
         TM    PDIRFLG3,PDIRIFPU   FP UTILITY PROGRAM?         @BMV0082
****     BNO   FPCHK               NO... OOPS, LOOK AT NEXT PST@BMV0082
         BNO   FPCODE              NO... OOPS, LOOK AT NEXT PST#151
         MVC   A00TYPE(L'FPU),FPU  SHOW FPU.                   @BIM8USS
         MVC   A00STEP(L'PSTNSTEP),PSTNSTEP STEP NAME.         @BIM8USS
         B     FP02                CONTINUE.                   @BMV0082
*----------------------------------------------------------------------
*  MESSAGE DRIVEN FP PROGRAM
*----------------------------------------------------------------------
FP01     DS    0H                                              @BMV0082
         MVC   A00TYPE(L'FPM),FPM SHOW MESSAGE DRIVEN.         @BIM8USS
         MVC   A00STEP(8),=C'NO MSG. ' PRIME TRAN CODE FIELD   @BIM8USS
         L     R8,PSTSMB          GET SMB ADDRESS.             @BMV0082
         LTR   R8,R8              IS THERE ONE?                @BMV0082
         BZ    FP02               NO... LEAVE 'NO MSG.'.       @BMV0082
         MVC   A00STEP(L'SMBTRNCD),SMBTRNCD TRAN CODE.         @BIM8USS
FP02     DS    0H                                              @BMV0082
         BAL   R1,INITMSG         SET STANDARD MSG             @BMV0082
         MVC   A00FID(L'A00FID),=C'A00' FORMAT ID = 'A00'      @BIM8USS
         DROP  R2                                              @BIM8USS
*----------------------------------------------------------------------
*  CHECK IF FP REGION HAS THREAD.  IF SO, PUT OUT THREAD DISPLAY
*----------------------------------------------------------------------
FP03     DS    0H                                              @BM10348
         OI    SPADFLAG,FPACTIVE  IND AT LEAST 1 ACT FP REGION @BMR0278
         L     R5,PSTRRE          GET ADDR(1ST RRE OFF PST).   @BMR0011
         USING DFSRRE,R5                                       @BM80324
         CLC   RRETYPE,=AL2(RRETYESS)  IS IT AN ESS BMP?       @BM80324
         BNE   FP04               NO, SKIP ESS CODE            @BM80324
         DROP  R5                                              @BM80324
         L     R15,=A(FINDTHRD)   PREPARE TO FIND THREAD       @BIM8USS
         BALR  R14,R15            CHECK FOR THREADS FROM PST.  @BIM8USS
         LTR   R5,R5              ARE THERE ANY THREADS?       @BMR0011
         BZ    BUFFRET            NO... CONTINUE AS USUAL      @BMR0011
*----------------------------------------------------------------------
*  SET UP FOR THREAD DISPLAY
*----------------------------------------------------------------------
         ST    R5,SPADRREA        SAVE POINTER TO RRE AND      @BMR0011
         ST    R6,SPADEZSE        EZSE.                        @BMR0011
         ST    R7,SPADPSAV        SAVE CURRENT PST POINTER AND @BIHC9
         MVC   SPAD6SAV,SPADNTRY  ENTRY WHILE DISPLAYING       @BME0799
*                                 THREADS FROM REGIONS.
         MVI   SPADNTRY,X'09'     SET ENTRY FOR THREAD HEADER  @BIHC9
*                                 NEXT TIME THROUGH.
*
FP04     DS    0H                                              @BM80324
         B     BUFFRET            GO OUTPUT MESSAGE.           @BIM8USS
*----------------------------------------------------------------------
*  A55 FP REGION NOT ACTIVE
*----------------------------------------------------------------------
FPEND    DS    0H                                              @BIM8USS
         USING A55,R2                                          @BIM8USS
         MVI   SPADNTRY,13         SETUP FOR DBT REGIONS NEXT  @PL52295
         XC    SPADLBLK,SPADLBLK  ZERO IN CASE USED.           @BM61166
         TM    SPADFLAG,FPACTIVE  ANY ACTIVE FP REGIONS?       @BM61166
         BO    BRSETUP            YES... CONTINUE.             @BM61166
         MVC   A55FPR(L'FPMSG),FPMSG FP NOT ACTIVE MSG.        @BIM8USS
         MVC   A55FID(L'A55FID),=C'A55' FORMAT ID = 'A55'      @BIM8USS
         LA    R3,A55LEN          MSG LENGTH.                  @BIHC9
         B     BUFFRET            PUT OUT MSG                  @BIHC9
         DROP  R2                                              @BIM8USS
         EJECT
STM0X15  DC    16A(0)                                              #151
         DROP  R4                                                  #151
         SPACE 2                                                   #151
         LTORG                                                     #151
         PRINT NOGEN                                               #151
         NARSCD                                                    #151
         SPACE 2                                                   #151
         ISAP                                                      #151
//U.SYSIN2 DD  *          RW0152A  MODS TO SHOW MONITOR IS ACTIVE.
./ CHANGE NAME=DFSIDP20
MNACTMSG DC    X'15',C'  MONITOR IS ACTIVE',X'15'                  #152
//
//********************** EOJ ******************************************
//STEP2  EXEC PGM=IEWL,
//             PARM='NCAL,LET,RENT,XREF,LIST'
//SYSPRINT DD  SYSOUT=*
//MODS    DD   DSN=IMA.PVT.MODS,DISP=SHR
//SYSLMOD  DD  DSN=IMS.SY7N.NUCLEU7,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,1))
//SYSLIN   DD  *
  INCLUDE MODS(DFSIDP2Z)
  INCLUDE SYSLMOD(DFSVNUC0)
  ENTRY DFSIOPL0
  NAME DFSVNUC0(R)
