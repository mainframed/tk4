PSUTABND CSECT
**
**  FOR INFORMATION CALL: GEORGE RAMAS
**                        OAKLAND, CALIFORNIA 94610
**                        (415)-763-0540
**          STARTING 1990 (510)
**
**  THE PURPOSE OF THIS UTILITY IS TO HALT PROCESSING IN A PROCEDURE
**  IF A SELECTED CONDITION CODE IS RECEIVED.  IT WILL DISPLAY A
**  MESSAGE TO THE CONSOLE AND ASK THE OPERATOR TO REPLY 'Y' TO
**  CONTINUE OR 'N' TO FLUSH.  THE JOB 'NAME' WILL ALSO BE DISPLAY
**  IN THIS MESSAGE.  THIS ALERTS THE OPERATOR WHEN A JOB ABENDS
**  WITHOUT ROLLING OFF THE CONSOLE SCREEN.
**
**  YOU CAN DISPLAY AN ADDITIONAL MESSAGE WITH THE ABOVE MESSAGE
**  BY KEYING UP 45 CHARACTERS IN A 'PARM='XXXXXXXX'' FIELD IN THE
**  JCL ON THE EXEC LINE'
**
**      //************
**      //ABENDCHK EXEC PGM=PSUTABND,COND=((0,EQ,DLSD4020),EVEN),
**      //  PARM='THE PIVOT PROCESSING HAS ABENDED'
**      //************
**      //STEPLIB DD DSN=OSSP.UTILITY.LOADLIB,
**      //           DISP=SHR
**
**
**
**
         SPACE
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
         SAVE  (14,12),,*
         L     2,0(R1)            LOAD PARM ADDRESS IN REG 2
         LR    R11,R15
         USING PSUTABND,R11
         ST    R13,SAVEREG+4
         LA    R3,SAVEREG
         ST    R3,8(R13)
         LR    R13,R3
SETUP    EXTRACT ADRTIOT,FIELDS=TIOT   EXTRACT TIOT ADDRESS
         L     R7,ADRTIOT         LOAD TIOT ADDRESS
         MVC   JOBNAME(8),0(R7)   MOVE JOBNAME TO WTOR
         MVC   GIVE+1(1),1(R2)    INSERT PARM LENGTH
******* THE ABOVE INSTRUCTION MODIFIES THE LENGTH ATTRIBUTE OF THE ***
******* FOLLOWING INSTRUCTION.                                     ***
GIVE     MVC   MESSAGE,2(R2)      MOVE PARM MESSAGE TO WTO
WTO      WTO   '********************************************************
               *********************************************************
               ************',ROUTCDE=(2,11),DESC=(2)
WTOR     WTOR  '*** REPLY Y TO CONTINUE JOB----- -REPLY N TO ABEND    **
               *****************',REPLY,1,ECB,ROUTCDE=(2,11)
         WAIT  ECB=ECB            WAIT FOR REPLY
         CLI   REPLY,C'Y'         OK TO CONTINUE ??
         BE    EOJ                IF SO, NORMAL END
         MVC   ECB,RESET          CLEAR ECB
         CLI   REPLY,C'N'         VALID REPLY ??
         BNE   WTOR               BAD REPLY-DISPLAY AGAIN
         ABEND 999                ABEND
EOJ      L     R13,SAVEREG+4      RESTORE REG 13
         RETURN (14,12),RC=0      RETURN
         EJECT
SAVEREG  DS    18F                ALIGN ON FULLWORD
         SPACE 3
ECB      DC    F'0'
RESET    DC    F'0'
ADRTIOT  DC    F'0'
JOBNAME  EQU   WTOR+16+24,8
MESSAGE  EQU   WTO+8+4
REPLY    DS    CL1
         END
