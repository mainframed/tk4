000100******************************************************************
000200*** I D E N T I F I C A T I O N                 D I V I S I O N **
000300** *                          PSUTGDG                           **
000400***                                                             **
000500***    THE PURPOSE OF THIS PROGRAM IS TO LIST THE NUMBER OF GDG **
000600***    ENTRIES FOR A SELECT GROUP OF DATA SETS OR NODE LEVELS.  **
000700***    IT SELECTS CERTAIN FIELDS FROM A IDCAMS LISTCAST OUTPUT. **
000800***                                                             **
000803***    FOR INFORMATION: GEORGE RAMAS                            **
000804***                     OAKLAND, CALIFORNIA 94610               **
000805***                     (415) 763-0540                          **
000806***          AFTER 1990 (510)                                   **
000810***                                                             **
000900***    THE INPUT TO THIS PROGRAM IS A LISTCAT USING THE         **
001000***    UTILITY PGM=IDCAMS                                       **
001100***                                                             **
001110***  //STEP1  EXEC PGM=IDCAMS                                   **
001120***  //*                                                        **
001130***  //SYSPRINT DD DSN=&&LISTING,                               **
001140***  //            DISP=(,PASS,DELETE),                         **
001150***  //            UNIT=SYSDA,                                  **
001160***  //            SPACE=(CYL,(2,1),RLSE),                      **
001170***  //            DCB=(RECFM=VBA,LRECL=125,BLKSIZE=23476)      **
001180***  //*                                                        **
001190***  //* USE LEVEL FOR NODE SELECTION      DLSP                 **
001200***  //* USE ENTRY FOR FULL DATA SET NAME  DLSP.TRAN1050        **
001201***  //* YOU MUST USE THE 'ALL' PARAMETER                       **
001202***  //*                                                        **
001203***  //SYSIN    DD *                                            **
001204***    LISTC LEVEL(DLSP) ALL                                    **
001205***  /*                                                         **
001206***  //*                                                        **
001207***  //SYSOUT   DD SYSOUT=*                                     **
001208***                                                             **
001209***    EXECUTION JCL FOR THIS PROGRAM IS AS FOLLOWS:            **
001300***                                                             **
001400***  //STEP2  EXEC PGM=PSUTGDG,COND=(0,NE)                      **
001500***  //*                                                        **
001600***  //STEPLIB DD DSN=OSSP.UTILITY.LOADLIB,                     **
001700***  //*                                                        **
001800***  //GDGLIST DD DSN=&&LISTING,                                **
001900***  //           DISP=(OLD,DELETE),                            **
002000***  //           DCB=(RECFM=VBA,LRECL=125,BLKSIZE=23476)       **
002100***  //*                                                        **
002200***  //GDGRPT  DD SYSOUT=A,                                     **
002300***  //           DCB=(RECFM=FBA,LRECL=133,BLKSIZE=23408)       **
002400***  //*                                                        **
002500***  //SYSOUT  DD SYSOUT=*                                      **
002600***  //SYSUDMP DD SYSOUT=*                                      **
002700***                                                             **
002800***                                                             **
002900******************************************************************
003000 IDENTIFICATION DIVISION.
003100 PROGRAM-ID.    PSUTGDG.
003200 INSTALLATION.
003300 AUTHOR.
003400 DATE-WRITTEN.    OCT. 1988.
003500 DATE-COMPILED.
003600 REMARKS.
003700  PURPOSE:
003800
003900
004000  INPUT:
004100   1) FILE NAME:  GDG-LIST                  DD NAME:    GDGLST
004200      REC LENGTH: 80                        KEY LENGTH: --
004300      DSORG:      SEQ                       RECFMT:     FB
004400
004500  OUTPUT:
004600   1) FILE NAME:  GDG-REPORT                DD NAME:    GDGRPT
004700      REC LENGTH: 133                       KEY LENGTH: --
004800      DSORG:      SEQ                       RECFMT:     FB
004900
005000*
005100******************************************************************
005200** * * * * * *  M O D I F I C A T I O N      L O G  * * * * * * **
005300**            COMMENT IN                                         *
005400**  DATE      COL 73-80   DESCRIPTION OF CHANGE & NAME           *
005500**  --------  ----------- ---------------------------------------*
005600**  99/99/99  FNB001      TEXT...   PROGRAMMER NAME.             *
005700**                                                               *
005800******************************************************************
005900     EJECT
006000 ENVIRONMENT DIVISION.
006100 CONFIGURATION SECTION.
006200 SOURCE-COMPUTER. IBM-3090.
006300 OBJECT-COMPUTER. IBM-3090.
006400 INPUT-OUTPUT SECTION.
006500 FILE-CONTROL.
006600     SELECT GDG-LIST            ASSIGN TO UT-S-GDGLST.
006700     SELECT GDG-REPORT          ASSIGN TO UT-S-GDGRPT.
006800     EJECT
006900 DATA DIVISION.
007000 FILE SECTION.
007100******************************************************************
007200*** INPUT FILE                           INPUT                  **
007300******************************************************************
007400 FD  GDG-LIST
007500     RECORDING MODE IS V
007600     LABEL RECORDS ARE STANDARD
007700     BLOCK CONTAINS 0 RECORDS
007710     RECORD CONTAINS 0 TO 121 CHARACTERS.
007800 01  GDG-LIST-RCD.
008000     05  GDG-LIST-DATA.
008100         10  TITLE-FLD                PIC X(08).
008200             88  ENTRY-BRK                  VALUE '0GDG BAS'
008300                                                  '0NONVSAM'.
008400             88  GDG-BASE-HDR               VALUE '0GDG BAS'.
008500             88  NONVSAM-HDR                VALUE '0NONVSAM'.
008510             88  LST-CC-CHAR-ONLY           VALUE '0       '.
008520             88  LST-PG-HDR                 VALUE '1IDCAMS '.
008600         10  GDG-LIST-FILLER          PIC X(113).
008700
008800
008900     EJECT
009000******************************************************************
009100*** REPORT FILE                          OUTPUT/REPORT          **
009200******************************************************************
009300 FD  GDG-REPORT
009400     LABEL RECORDS ARE STANDARD
009500     BLOCK CONTAINS 0 RECORDS.
009600 01  GDG-RPT-RCD.
009700     05  G-R-CC                       PIC X(01).
009800     05  G-R-FILLER                   PIC X(132).
009900     EJECT
010000******************************************************************
010100*** W O R K I N G                                 S T O R A G E **
010200***                       PSUTGDG                               **
010300******************************************************************
010400 WORKING-STORAGE SECTION.
010405 77  PAN-VALET PICTURE X(24) VALUE '002CBT1898   11/01/89'.
010500 01  FILLER              PIC X(45) VALUE
010600       '**** PSUTGDG WORKING STORAGE STARTS HERE ****'.
010700
010800
010900 01  LIST-RCD-AREA.
011000     05  LST-RCD-CHRCTR          OCCURS 80 TIMES
011100                                 INDEXED BY LC1 LC2.
011200         10  LST-CHAR                 PIC X(01).
011300             88  SPACE-CHAR                  VALUE SPACE.
011400             88  HYPHEN-CHAR                 VALUE '-'.
011500             88  ALPH-NMRIC       VALUE '0' THRU '9'
011600                                        'A' THRU 'Z'
011601                                         '.'.
011602
011603 01  LIST-FLD-AREA.
011700     05  LST-FLD-DATA                 PIC X(80).
012000             88  GDG-HDR-FLD              VALUE '0GDG'.
012100             88  NONVSAM-HDR-FLD          VALUE '0NONVSAM '.
012200             88  GDG-FLD                  VALUE 'GDG'.
012400             88  LIMIT-FLD                VALUE 'LIMIT'.
012800             88  ASSOCIATIONS-FLD         VALUE 'ASSOCIATIONS'.
013200             88  NUL-FLD                  VALUE '(NULL)'.
013600             88  VOLSER-FLD               VALUE 'VOLSER'.
014000             88  NONVSAM-FLD              VALUE 'NONVSAM'.
014100             88  SPACE-FLD                VALUE '0'.
014200     05  LST-FLD-CHRCTR  REDEFINES LST-FLD-DATA
014300                                 OCCURS 80  TIMES
014400                                 INDEXED BY FC1 FC2.
014500         10  LST-FLD-CHAR             PIC X(01).
014600
014610 01  WORK-TEXT-AREA.
014620     05  WS-HOLD-GDG-DSN-GRP.
014630         10  WS-HOLD-GDG-DSN-FLD
014651                              JUSTIFIED RIGHT
014660                                      PIC X(40).
014670     05  WS-TEST-GDG-DSN-GRP.
014680         10  WS-TEST-DSN-FLD          PIC X(40).
014690         10  WS-TEST-DSN-GNVOL        PIC X(09).
014691     05  WS-RDF-TEST-GDG-DSN  REDEFINES WS-TEST-GDG-DSN-GRP
014692                              JUSTIFIED RIGHT
014693                                      PIC X(49).
014700
014800
014900 01  FILLER              PIC X(45) VALUE
015000       '**  PUSTGDG  OUTPUT WORK RECORDS START HERE *'.
015100
015200 01  PR-LINE-HDR1.
015300     05  PR-CC-HDR1                  PIC X(01) VALUE '1'.
015400     05  PR-HDR-FILL1                PIC X(60) VALUE
015500         '         GENERATION DATA GROUPS   '.
015510     05  PR-HDR-FILL2                PIC X(07) VALUE ' DATE:'.
015600     05  PR-HDR1-RN-DATE             PIC X(10).
015601     05  PR-HDR-FILL3                PIC X(07) VALUE ' TIME:'.
015602     05  PR-HDR1-RN-TIME             PIC X(10).
015603     05  PR-HDR1-FILL4               PIC X(05) VALUE 'PAGE '.
015604     05  PR-HDR1-PAGE-NO             PIC ZZZZZZ9.
015605
015700 01  PR-LINE-HDR2.
015800     05  PR-CC-HDR2                  PIC X(01) VALUE SPACE.
015900     05  PR-HDR2-FILL1               PIC X(40) VALUE
016000        '  GDG BASE DSN '.
016100     05  PR-HDR2-FILL2               PIC X(07) VALUE
016200       'LIMIT'.
016300     05  PR-HDR2-FILL3               PIC X(10) VALUE
016400       ' VOLSER'.
016500     05  PR-HDR2-FILL4               PIC X(40) VALUE
016600       '    ASSOCIATIONS (NONVSAM) '.
016700 01  PR-LINE-HDR3.
016800     05  PR-CC-HDR3                  PIC X(01) VALUE SPACE.
016900     05  PR-HDR3-FILL1               PIC X(80) VALUE SPACE.
017000 01  PR-LINE-DTL.
017100     05  PR-CC1                      PIC X(01) VALUE SPACE.
017200     05  FILLER                      PIC X(01) VALUE SPACE.
017300     05  PR-GDG-DSN                  PIC X(40).
017400     05  PR-LIMIT                    PIC X(07).
017500     05  PR-VOLSER                   PIC X(10).
017600     05  PR-ASSOC-FILES              PIC X(70).
017700
017800*
017900     EJECT
018000******************************************************************
018100*** SAVE/COMPARE AREAS                                          **
018200******************************************************************
018300 01  FILLER              PIC X(45) VALUE
018400       '** PSUTGDG   SAVE/COMPARE AREAS START HERE **'.
018500 01  SAVE-FIELDS.
018600     03  HIGHEST-SEVERITY         PIC 9      VALUE 0.
018700     03  PREV-KEY                 PIC X(40)  VALUE SPACES.
018800     EJECT
018900******************************************************************
019000*** WORK/DISPLAY/CONSTANT FIELDS                                **
019100******************************************************************
019200 01  FILLER              PIC X(45) VALUE
019300       '** PSUTGDG   WORK FIELDS ****** START HERE **'.
019400*
019500 01  WORK-FIELDS.
019600     03  WORK-TIME.
019700         05  WORK-HOURS      PIC 99.
019800         05  WORK-MINUTES    PIC 99.
019900         05  WORK-SECS       PIC 99.
020000*
020100     03  DISPLAY-TIME.
020200         05  DISP-HOURS      PIC 99.
020300         05  FILLER          PIC X VALUE ':'.
020400         05  DISP-MINUTES    PIC 99.
020500         05  FILLER          PIC X VALUE ':'.
020600         05  DISP-SECS       PIC 99.
020700*
020800     03  DISP-CURRENT-DATE   PIC X(08)   VALUE SPACES.
020900*
021000     03  WK-UNPACK-DATE      PIC 9(07)   VALUE ZERO.
021100     03  WK-UNPACK-DATE-R    REDEFINES  WK-UNPACK-DATE.
021200         05  FILLER          PIC 9.
021300         05  WK-UNPACK-MM    PIC 99.
021400         05  WK-UNPACK-DD    PIC 99.
021500         05  WK-UNPACK-YY    PIC 99.
021600*
021700     03  WK-DATE-0YYMMDD     PIC 9(07)       VALUE 0.
021800     03  WK-DATE-0YYMMDD-R  REDEFINES  WK-DATE-0YYMMDD.
021900       04  FILLER            PIC 9.
022000       04  WK-DATE-YYMMDD.
022100         05  WK-YYMMDD-YY    PIC 99.
022200         05  WK-YYMMDD-MM    PIC 99.
022300         05  WK-YYMMDD-DD    PIC 99.
022400*
022500     03  WK-DATE-MMDDYY.
022600         05  WK-MMDDYY-MM    PIC 99          VALUE 0.
022700         05  WK-MMDDYY-DD    PIC 99          VALUE 0.
022800         05  WK-MMDDYY-YY    PIC 99          VALUE 0.
022900*
023000     03  WK-FORMAT-DATE.
023100         05  WK-FORMAT-MM    PIC 99          VALUE ZERO.
023200         05  FILLER          PIC X VALUE '-'.
023300         05  WK-FORMAT-DD    PIC 99          VALUE ZERO.
023400         05  FILLER          PIC X VALUE '-'.
023500         05  WK-FORMAT-YY    PIC 99          VALUE ZERO.
023600*
023700     03  DISPLAY-AMT-EDIT    PIC Z,ZZZ,ZZ9-  VALUE ZERO.
023800     03  DISPLAY-AMT-NUM     PIC S9(07)    VALUE +0.
023900     03  DISPLAY-WHEN-COMPILED.
024000         05  DISPLAY-COMPILED-TIME    PIC X(08) VALUE SPACES.
024001         05  DISPLAY-COMPILED-DATE    PIC X(12) VALUE SPACES.
024002     EJECT
024003******************************************************************
024100*** SUBSCRIPTS & SWITCHES                                       **
024200******************************************************************
024300 01  FILLER              PIC X(45) VALUE
024400       '** PSUTGDG   SUBSCRIPTS/SWITCHES START HERE *'.
024500 01  SWITCHES.
024600     03  READ-SW                     PIC X VALUE LOW-VALUE.
024700         88  FIRST-READ                  VALUE LOW-VALUE.
024800         88  NORMAL-READ                 VALUE SPACE.
024900         88  GDG-EOF                     VALUE HIGH-VALUE.
025000     03  EDIT-DATA-SW                PIC X VALUE SPACE.
025100         88  DATA-OK                     VALUE 'Y'.
025110         88  PRINT-LINE                  VALUE 'P'.
025200         88  BY-PASS-GROUP               VALUE 'B'.
025300
025400 01  SUBSCRIPTS        COMP SYNC.
025500     03  PAGE-SUB            PIC S9(03)   VALUE +0.
025600     03  LINE-SUB            PIC S9(03)   VALUE +0.
025700     03  MSG-SUB             PIC S9(03)   VALUE +0.
025800     EJECT
025900******************************************************************
026000*** COUNTERS                                                    **
026100******************************************************************
026200 01  FILLER              PIC X(45) VALUE
026300       '** PSUTGDG   COUNTERS START HERE ************'.
026400
026500 01  COUNTERS    COMP-3.
026600     03  CTR-RECS-READ          PIC S9(7)   VALUE +0.
026700     03  CTR-RECS-PROCESSED     PIC S9(7)   VALUE +0.
026800     03  CTR-RECS-REJECTED      PIC S9(7)   VALUE +0.
026900     03  CTR-PAGES-WRITTEN      PIC S9(7)   VALUE +0.
027000     03  CTR-RECS-WRITTEN       PIC S9(7)   VALUE +0.
027100     03  CTR-RECS-WRITTEN-PAGE  PIC S9(7)   VALUE +0.
027200             88  PAGE-BREAK         VALUE +54 THRU +99999.
027300     03  CTR-PGM-ERRORS         PIC S9(3)   VALUE +0.
027400     EJECT
027500******************************************************************
027600*** MESSAGES                                                    **
027700******************************************************************
027800 01  FILLER              PIC X(45) VALUE
027900       '** PSUTGDG   MESSAGES START HERE ************'.
028000
028100 01  MESSAGES.
028200     03  MSG-PGM-START          PIC X(31)   VALUE
028300         '*  PSUTGDG  START EXECUTION AT '.
028301     03  MSG-PGM-COMPILED       PIC X(31)   VALUE
028302         '*  PSUTGDG  LAST COMPILED   -  '.
028400     03  MSG-CONTROL-TOTALS     PIC X(60)   VALUE
028500         '* * * * * PSUTGDG  CONTROL TOTALS * * * * * * * * * * *
028600-        '* *'.
028700     03  MSG-ASTERISKS          PIC X(60)   VALUE ALL '*'.
028800     03  MSG-RECS-READ          PIC X(40)   VALUE
028900         '* TOTAL      RECORDS READ             = '.
029000     03  MSG-RECS-PROCESSED     PIC X(40)   VALUE
029100         '*      GDG RECORDS PROCESSED          = '.
029200     03  MSG-RECS-REJECTED      PIC X(40)   VALUE
029300         '*       DATA SETS REJECTED            = '.
029400     03  MSG-PAGES-WRITTEN      PIC X(40)   VALUE
029500         '* TOTAL           PAGES WRITTEN       = '.
029600     03  MSG-RECS-WRITTEN       PIC X(40)   VALUE
029700         '* TOTAL GDG ENTRY LIMITS WRITTEN      = '.
029800     03  MSG-PGM-ERRORS         PIC X(40)   VALUE
029900         '* TOTAL PROGRAM ERRORS ENCOUNTERED    = '.
030000     03  MSG-HIGHEST-SEVERITY   PIC X(40)   VALUE
030100         '*       HIGHEST SEVERITY WAS          = '.
030200     03  MSG-PGM-END            PIC X(31)   VALUE
030300         '* PSUTGDG     END EXECUTION AT '.
030400     03  MSG-BILL-FILE-EMPTY    PIC X(52)   VALUE
030500         '* INPUT FILE IS EMPTY - NO PROCESSING PERFORMED'.
030600     EJECT
030700******************************************************************
030800*** INTERNAL TABLES                                             **
030900******************************************************************
031000 01  FILLER              PIC X(45) VALUE
031100       '** PSUTGDG   INTERNAL TABLES START HERE *****'.
031200
031300     EJECT
031400******************************************************************
031500*** P R O C E D U R E                           D I V I S I O N **
031600***                          PSUTGDG                            **
031700******************************************************************
031800 PROCEDURE DIVISION.
031900*
032000******************************************************************
032100 00000-MAIN-LINE.
032200******************************************************************
032300
032400***
032500***  READY TRACE.
032600***
032700     PERFORM  A0000-INIT.
032800     PERFORM  B0000-PROCESS-FILE
032900              UNTIL    GDG-EOF.
033000     PERFORM  C0000-EOJ.
033100     STOP RUN.
033200
033300
033400
033500
033600******************************************************************
033700     EJECT
033800******************************************************************
033900 A0000-INIT.
034000******************************************************************
034100* 1) DISPLAYS PROGRAM START INFORMATION
034200* 2) OPENS FILES
034300* 3) DOES FIRST READ OF INPUT FILE
034400*
034500*
034600     DISPLAY SPACES.
034700     DISPLAY  MSG-ASTERISKS.
034800     MOVE  TIME-OF-DAY     TO  WORK-TIME.
034900     MOVE  WORK-HOURS      TO  DISP-HOURS.
035000     MOVE  WORK-MINUTES    TO  DISP-MINUTES.
035100     MOVE  WORK-SECS       TO  DISP-SECS.
035200     MOVE  CURRENT-DATE    TO  DISP-CURRENT-DATE
035300                               PR-HDR1-RN-DATE.
035400     MOVE  DISPLAY-TIME    TO  PR-HDR1-RN-TIME.
035401     MOVE  WHEN-COMPILED   TO DISPLAY-WHEN-COMPILED.
035402     DISPLAY  MSG-PGM-START
035403              DISPLAY-TIME  ' ON '
035500              DISP-CURRENT-DATE.
035600     DISPLAY  MSG-PGM-COMPILED
035700              DISPLAY-COMPILED-DATE
035800              '  '
035801              DISPLAY-COMPILED-TIME.
035802     OPEN INPUT   GDG-LIST
035803          OUTPUT  GDG-REPORT.
035804     PERFORM  IO3000-WRITE-HEADER.
035900
036000
036100     EJECT
036200 B0000-PROCESS-FILE.
036300******************************************************************
036400* 1) PROCESSES INPUT FILE RECORDS
036500* 2) EDITS ALL DATA FIELDS
036600******************************************************************
036700     PERFORM IO1000-READ-GDG-LIST.
036800     IF  GDG-EOF
036900             NEXT SENTENCE
037000     ELSE IF  GDG-BASE-HDR
037100             PERFORM  B0200-CHECK-GDG-HDR
037200     ELSE IF BY-PASS-GROUP
037300             NEXT SENTENCE
037400     ELSE IF LST-PG-HDR
037500             NEXT SENTENCE
037600     ELSE IF GDG-LIST-FILLER  EQUAL SPACES
037601             NEXT SENTENCE
037602     ELSE
037603             PERFORM  B1000-EDIT-FIELDS.
037604
037700     EJECT
037800 B0200-CHECK-GDG-HDR.
037900******************************************************************
038000*           TEST FOR GDG HEADER RECORD                           *
038100*         IF GDG RECORD SET UP NEW GROUP.                        *
038200******************************************************************
038300
038400     MOVE  SPACES TO  LIST-FLD-AREA
038500                      PR-LINE-DTL
038600                      EDIT-DATA-SW
038601                      WS-HOLD-GDG-DSN-FLD
038610                      LIST-RCD-AREA.
038611     MOVE GDG-LIST-DATA TO LIST-RCD-AREA.
038620     PERFORM B2000-MOVE-DSN-FLD.
039100
039200     EJECT
039300 B1000-EDIT-FIELDS.
039400******************************************************************
039500* 1) EDITS ALL FIELDS                                            *
039600* 2)                                                             *
039700******************************************************************
039800
039900     SET FC1  TO 1.
040000     MOVE SPACES TO LST-FLD-DATA
040100     MOVE GDG-LIST-DATA  TO  LIST-RCD-AREA.
040110     IF  LST-CC-CHAR-ONLY
040120             MOVE SPACE TO LST-CHAR (1).
040200     PERFORM B3100-POSITION-CHAR
040300                VARYING LC1 FROM 1 BY 1
040400                UNTIL LC1 > 80
040500                   OR (LST-CHAR (LC1) NOT = SPACE
040600                       AND LST-CHAR (LC1) NOT =  '-').
040700     PERFORM  B3200-MOVE-CHAR
040800                 VARYING  LC1 FROM LC1 BY 1
040900                 UNTIL LC1 > 80
041000                    OR LST-CHAR (LC1) = SPACE
041100                    OR LST-CHAR (LC1) =   '-'.
041500     IF  LIMIT-FLD
041600             MOVE SPACES TO LIST-FLD-AREA
041700             PERFORM B2200-MOVE-LIMIT-FLD
041701     ELSE IF ASSOCIATIONS-FLD
041702             MOVE SPACES TO LIST-FLD-AREA
041703             PERFORM B2400-CHK-NULL
041710     ELSE IF NONVSAM-HDR-FLD
041720             MOVE SPACES TO LIST-FLD-AREA
041730             PERFORM B2600-MOVE-ASSOC-FLD
041800     ELSE IF VOLSER-FLD
041900             MOVE SPACES TO LIST-FLD-AREA
042000             PERFORM B2300-MOVE-VOLSER-FLD
042700     ELSE
042800             NEXT SENTENCE.
042900     MOVE  SPACES TO LIST-FLD-AREA.
043000
043100     EJECT
043200 B2000-MOVE-DSN-FLD.
043300******************************************************************
043400*           MOVES THE DSN ON GDG LINE TO THE PRINT LINE.
043500*
043600******************************************************************
043700
043800      PERFORM  B3100-POSITION-CHAR
043900                   VARYING LC1  FROM 10  BY 1
044000                   UNTIL LC1 > 80
044100                      OR (NOT SPACE-CHAR (LC1)
044200                          AND NOT HYPHEN-CHAR (LC1)).
044210      SET  LC2  TO  LC1.
044300      SET  FC1  TO  1.
044400      PERFORM  B3200-MOVE-CHAR
044500                   VARYING LC1  FROM LC1 BY 1
044600                   UNTIL LC1 > 80
044700                      OR FC1 > 80
044800                      OR NOT ALPH-NMRIC (LC1).
045000      MOVE  LST-FLD-DATA  TO  PR-GDG-DSN.
045001      MOVE  SPACES  TO    LST-FLD-DATA.
045003      SET  FC2  TO  80.
045004      PERFORM  B3100-POSITION-CHAR
045005                   VARYING LC1  FROM LC1 BY -1
045006                   UNTIL LC1 < LC2
045007                      OR ALPH-NMRIC (LC1).
045008      PERFORM  B3300-MOVE-CHAR-RIGHT
045009                   VARYING LC1  FROM LC1 BY -1
045010                   UNTIL LC1 < LC2
045011                      OR FC2 < 1
045012                      OR LST-CHAR (LC1) = SPACE
045013                      OR LST-CHAR (LC1) = '-'.
045014      MOVE  LST-FLD-DATA  TO  WS-HOLD-GDG-DSN-FLD.
045100      MOVE   '0'  TO PR-CC1.
045200
045300
045400     EJECT
045500 B2200-MOVE-LIMIT-FLD.
045600******************************************************************
045700*           MOVE THE GDG LIMIT TO THE PRINT LINE.                *
045800******************************************************************
045900
046000      PERFORM  B3100-POSITION-CHAR
046100                   VARYING LC1  FROM LC1 BY 1
046200                   UNTIL LC1 > 80
046300                     OR (LST-CHAR (LC1) NOT = SPACES
046400                        AND LST-CHAR (LC1) NOT = '-').
046500      SET  FC1  TO 1.
046600      PERFORM  B3200-MOVE-CHAR
046700                   VARYING LC1  FROM LC1 BY 1
046800                   UNTIL LC1 > 80
046900                      OR FC1 > 80
047000                      OR LST-CHAR (LC1) = SPACE
047100                      OR LST-CHAR (LC1) = '-'.
047200      MOVE  LST-FLD-DATA  TO  PR-LIMIT.
047210      PERFORM IO2000-WRITE-A-RPT-LINE.
047220      MOVE SPACES TO PR-LINE-DTL.
047300
047400     EJECT
047500 B2300-MOVE-VOLSER-FLD.
047600******************************************************************
047700*           MOVE THE VOLSER    TO THE PRINT LINE.                *
047800******************************************************************
047900
048000      PERFORM  B3100-POSITION-CHAR
048100                   VARYING LC1  FROM LC1 BY 1
048200                   UNTIL LC1 > 80
048300                     OR (LST-CHAR (LC1) NOT = SPACES
048400                        AND LST-CHAR (LC1) NOT = '-').
048500      SET  FC1  TO 1.
048600      PERFORM  B3200-MOVE-CHAR
048700                   VARYING LC1  FROM LC1 BY 1
048800                   UNTIL LC1 > 80
048900                      OR FC1 > 80
049000                      OR LST-CHAR (LC1) = SPACE
049100                      OR LST-CHAR (LC1) = '-'.
049200      MOVE  LST-FLD-DATA  TO  PR-VOLSER.
049210      IF  PRINT-LINE
049220             PERFORM  IO2000-WRITE-A-RPT-LINE
049230             MOVE SPACES TO PR-LINE-DTL.
049300
049400     EJECT
049500 B2400-CHK-NULL.
049600******************************************************************
049700*           IF (NULL) ON ASSOCIATION BY PASS THE GROUP.          *
049800******************************************************************
049900
050000      PERFORM  B3100-POSITION-CHAR
050100                   VARYING LC1  FROM LC1 BY 1
050200                   UNTIL LC1 > 80
050300                      OR (LST-CHAR (LC1)     NOT = SPACES
050400                          AND LST-CHAR (LC1) NOT = '-').
050500      SET  FC1 TO 1.
050600      PERFORM  B3200-MOVE-CHAR
050700                   VARYING LC1  FROM LC1 BY 1
050800                   UNTIL LC1 > 80
050900                      OR FC1 > 80
051000                      OR LST-CHAR (LC1) = SPACE
051100                      OR LST-CHAR (LC1) = '-'.
051200      IF  NUL-FLD
051300              MOVE  'B'  TO EDIT-DATA-SW.
051400
051500
053800
053900     EJECT
053910 B2600-MOVE-ASSOC-FLD.
053920******************************************************************
053930*          TEST THE DSN AND NONVSAM DSN TO MATCH AND PRINT.
053940*
053950******************************************************************
053960
053970      PERFORM  B3100-POSITION-CHAR
053980                   VARYING LC1  FROM LC1 BY 1
053990                   UNTIL LC1 > 80
053991                      OR (NOT SPACE-CHAR (LC1)
053992                          AND NOT HYPHEN-CHAR (LC1)).
053993      SET  LC2  TO LC1.
053994      SET  FC1  TO  1.
053995      PERFORM  B3200-MOVE-CHAR
053996                   VARYING LC1  FROM LC1 BY 1
053997                   UNTIL LC1 > 80
053998                      OR FC1 > 80
053999                      OR NOT ALPH-NMRIC (LC1).
054000      MOVE  LST-FLD-DATA  TO  PR-ASSOC-FILES.
054001      MOVE  SPACES  TO    LST-FLD-DATA.
054003      SET  FC2  TO  80.
054004      PERFORM  B3100-POSITION-CHAR
054005                   VARYING LC1  FROM LC1 BY -1
054006                   UNTIL LC1 < LC2
054007                      OR ALPH-NMRIC (LC1).
054008      PERFORM  B3300-MOVE-CHAR-RIGHT
054009                   VARYING LC1  FROM LC1 BY -1
054010                   UNTIL LC1 < LC2
054020                      OR FC2 < 1
054030                      OR SPACE-CHAR (LC1)
054031                      OR HYPHEN-CHAR (LC1).
054032      MOVE  LST-FLD-DATA  TO  WS-RDF-TEST-GDG-DSN.
054033      IF  WS-HOLD-GDG-DSN-FLD  =  WS-TEST-DSN-FLD
054034              MOVE  'P' TO  EDIT-DATA-SW
054035      ELSE
054036              MOVE  'B' TO  EDIT-DATA-SW.
054037
054038
054039      EJECT
054040 B3000-SET-FOR-MOVE.
054100******************************************************************
054200* 1)  SETS UP THE CHARACTER BY CHARACTER MOVE FROM THE GDG RECORD*
054300*          TO THE REPORT AREA.                                   *
054400******************************************************************
054500
054600     MOVE SPACES TO LIST-FLD-AREA.
054700
054800 B3100-POSITION-CHAR.
054900******************************************************************
055000* 1)  POSITIONS THE POINTER TO THE NEXT FIELD TO MOVE.           *
055100*          TO THE TEST AREA.                                     *
055200******************************************************************
055300
055400     IF  LC1 < 80
055500               NEXT SENTENCE.
055600
055700
055800 B3200-MOVE-CHAR.
055900******************************************************************
056000* 1)  MOVES THE CHARACTER TO THE RECEIVING FIELD.                *
056100*                                                                *
056200******************************************************************
056300
056400     MOVE LST-CHAR (LC1)  TO  LST-FLD-CHAR (FC1).
056500     SET  FC1 UP BY 1.
056600
056700     EJECT
056710 B3300-MOVE-CHAR-RIGHT.
056720******************************************************************
056730* 1)  MOVE THE CHARACTER TO THE RIGHT MOST POSITION              *
056740*          OF WORK AREA                                          *
056750******************************************************************
056760
056770     MOVE LST-CHAR (LC1)  TO  LST-FLD-CHAR (FC2).
056780     SET  FC2 DOWN BY  1.
056790
056791     EJECT
056900 C0000-EOJ.
057000******************************************************************
057100* 1) CLOSES FILES
057200* 2) DISPLAYS CONTROL TOTALS
057300*
057400*
057410******************************************************************
057500     CLOSE   GDG-LIST
057600             GDG-REPORT.
057700
057800     DISPLAY  SPACES.
057900     DISPLAY  MSG-CONTROL-TOTALS.
058000     MOVE     CTR-RECS-READ         TO  DISPLAY-AMT-NUM.
058100     MOVE     DISPLAY-AMT-NUM       TO  DISPLAY-AMT-EDIT.
058200     MOVE  CTR-RECS-PROCESSED       TO  DISPLAY-AMT-NUM.
058300     DISPLAY  MSG-RECS-PROCESSED        DISPLAY-AMT-EDIT.
058400     MOVE  CTR-RECS-REJECTED        TO  DISPLAY-AMT-NUM.
058500     MOVE     DISPLAY-AMT-NUM       TO  DISPLAY-AMT-EDIT.
058600     DISPLAY  MSG-RECS-REJECTED         DISPLAY-AMT-EDIT.
058700     MOVE  CTR-PAGES-WRITTEN        TO  DISPLAY-AMT-NUM.
058800     MOVE     DISPLAY-AMT-NUM       TO  DISPLAY-AMT-EDIT.
058900     DISPLAY  MSG-PAGES-WRITTEN         DISPLAY-AMT-EDIT.
059000     MOVE  CTR-RECS-WRITTEN         TO  DISPLAY-AMT-NUM.
059100     MOVE     DISPLAY-AMT-NUM       TO  DISPLAY-AMT-EDIT.
059200     DISPLAY  MSG-RECS-WRITTEN          DISPLAY-AMT-EDIT.
059300     MOVE  CTR-PGM-ERRORS           TO  DISPLAY-AMT-NUM.
059400     MOVE     DISPLAY-AMT-NUM       TO  DISPLAY-AMT-EDIT.
059500     DISPLAY  MSG-PGM-ERRORS            DISPLAY-AMT-EDIT.
059600     DISPLAY  MSG-HIGHEST-SEVERITY      HIGHEST-SEVERITY.
059700     MOVE  TIME-OF-DAY     TO  WORK-TIME.
059800     MOVE  WORK-HOURS      TO  DISP-HOURS.
059900     MOVE  WORK-MINUTES    TO  DISP-MINUTES.
060000     MOVE  WORK-SECS       TO  DISP-SECS.
060100     MOVE  CURRENT-DATE    TO  DISP-CURRENT-DATE.
060200     DISPLAY  MSG-PGM-END
060300              DISPLAY-TIME  ' ON '
060400              DISP-CURRENT-DATE.
060500     DISPLAY  MSG-ASTERISKS.
060600*
060700 C0000-EXIT.
060800     EXIT.
060900     EJECT
061000******************************************************************
061100** INPUT/OUTPUT ROUTINES
061200******************************************************************
061300*
061400*---------------------
061500 IO1000-READ-GDG-LIST.
061600*---------------------
061700*
061800*
061900     READ  GDG-LIST     AT END
062000         MOVE  HIGH-VALUE TO READ-SW.
062100     IF  GDG-EOF
062200             NEXT SENTENCE
062300     ELSE
062400             MOVE SPACE TO READ-SW
062500             ADD  +1  TO  CTR-RECS-READ.
062600
062700     EJECT
062800 IO2000-WRITE-A-RPT-LINE.
062900******************************************************************
063000*       READ AND CONTROL THE INPUT FILE.                         *
063100******************************************************************
063200     WRITE  GDG-RPT-RCD  FROM   PR-LINE-DTL
063300                 AFTER POSITIONING  PR-CC1.
063400     ADD  +1   TO  CTR-RECS-WRITTEN.
063500     IF  PR-CC1 = '0'
063600             ADD  +2  TO CTR-RECS-WRITTEN-PAGE
063700     ELSE
063800             ADD  +1   TO  CTR-RECS-WRITTEN-PAGE.
063900     IF  PAGE-BREAK
064000             PERFORM IO3000-WRITE-HEADER.
064100
064200
064300
064400 IO3000-WRITE-HEADER.
064500******************************************************************
064600*        WRITES NEW PAGE AND PAGE HEADERS.                       *
064700******************************************************************
064800
064900     ADD  1  TO CTR-PAGES-WRITTEN.
064910     MOVE  CTR-PAGES-WRITTEN TO PR-HDR1-PAGE-NO.
065000     WRITE GDG-RPT-RCD
065100            FROM PR-LINE-HDR1
065200            AFTER POSITIONING PR-CC-HDR1.
065300     WRITE GDG-RPT-RCD
065400            FROM PR-LINE-HDR2
065500            AFTER POSITIONING PR-CC-HDR2.
065600     WRITE GDG-RPT-RCD
065700            FROM PR-LINE-HDR3
065800            AFTER POSITIONING PR-CC-HDR3.
065900     MOVE  ZERO  TO CTR-RECS-WRITTEN-PAGE.
066000
066100
066200******************************************************************
066300** ERROR ROUTINES
066400******************************************************************
066500*
066600*---------------------
066700 X1000-DATA-ERROR.
066800*---------------------
066900*
067000*
067100*
067200     DISPLAY  'X1000-DATA-ERROR'.
067300*
067400
067500
067600*************************************************************
067700*** END OF SOURCE CODE FOR PROGRAM PSUTGDG                ***
067800*************************************************************
