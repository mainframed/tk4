         PRINT GEN
*
**********************************************************************
***                                                                ***
***  SET THE FOLLOWING LOCAL CONSTANTS                             ***
***  (See text further down.)                                      ***
***                                                                ***
**********************************************************************
         LCLC  &MYNAME,&PRIMPNL,&APPL   LOCAL CONSTANTS
*
&MYNAME  SETC  'XXXXXXX'                COMMAND NAME (CSECT NAME)
*
&PRIMPNL SETC  'XXX@PRIM'               PRIMARY OPTION PANEL NAME
&APPL    SETC  'XXX'                    3-CHAR APPLICATION NAME
*
&PROFCMD SETC  'ISPFPROF'               'NO' OR NAME OF TSO COMMAND
*                                          OR PROGRAM (NOT CLIST) TO
*                                          ALLOCATE THE PROFILE DATASET
*
*
*
         TITLE '&MYNAME - COMMAND PROCESSOR TO EXECUTE DIALOG'
         PRINT NOGEN
**********************************************************************
***                                                                ***
***   ATTRIBUTES - REENTERABLE, REUSABLE, REFRESHABLE              ***
***                                                                ***
**********************************************************************
*
         EJECT
**********************************************************************
***                                                                ***
***  Background -                                                  ***
***    When an ISPF dialog has a primary option panel, and when    ***
***    you want it to be executable from ISPF or from READY,       ***
***    often you write a CLIST like the one below.  This will      ***
***    get you to the primary option menu of the dialog.           ***
***                                                                ***
***       PROC 0                                                   ***
***       IF &SYSISPF EQ ACTIVE THEN +                             ***
***         ISPEXEC SELECT PANEL(XXX@PRIM) NEWAPPL(XXX)            ***
***       ELSE +                                                   ***
***         DO                                                     ***
***           ISPFPROF                 /* ALLOCATE PROFILE DSN */  ***
***           ISPSTART PANEL(XXX@PRIM) NEWAPPL(XXX)                ***
***         END                                                    ***
***       EXIT CODE(&MAXCC)                                        ***
***                                                                ***
***    Unfortunately, CLISTs have no way of giving you an          ***
***    optional, positional parameter to put into an OPT parm      ***
***    to branch the user directly to a panel below the primary    ***
***    option menu.  That is, if the CLIST is named 'ABC', there   ***
***    is no way to optionally go to panel 3.1 by typing in        ***
***    'ABC  3.1' and still be able to get to the primary option   ***
***    menu by just typing in 'ABC'.                               ***
***                                                                ***
***    NOTE:  If you don't have a command like 'ISPFPROF', you     ***
***           can get it from the CBT Mods tape.  If you have no   ***
***           need for it, use 'NO' as the command name in the     ***
***           local constants.                                     ***
***                                                                ***
***                                                                ***
***  Function -                                                    ***
***    When the user types in:                                     ***
***                                                                ***
***          &MYNAME   2.3                                         ***
***                                                                ***
***                                                                ***
***   If this command is running in a non-ISPF environment, it     ***
***   will result in the equivalent of the following TSO commands: ***
***                                                                ***
***       ISPFPROF                                                 ***
***       ISPSTART PANEL(&PRIMPNL) NEWAPPL(&APPL) OPT(2.3)         ***
***                                                                ***
***   If the program is running in an ISPF environment, it will    ***
***   result in the equivalent of the following CLIST command:     ***
***                                                                ***
***       ISPEXEC SELECT PANEL(&PRIMPNL) NEWAPPL(&APPL) OPT(2.3)   ***
***                                                                ***
***   If the user did not use the positional parameter '2.3',      ***
***   OPT(2.3) would not be added to the command buffer.           ***
***                                                                ***
***                                                                ***
***  ISPF Interface -                                              ***
***    Add the name which you select for this TSO command to the   ***
***    TSO command list for ISPF, module ISRTCM.  With ISPF V2R3M0 ***
***    or above, there is a macro for doing this.  For V2R2M0 or   ***
***    below, ZAP the name (and optional ALIAS) into ISRTCM or add ***
***    the following lines into the source.  This will allow the   ***
***    command to be executed from TSO Commands Option 6, or from  ***
***    a COMMAND or OPTION line.                                   ***
***                                                                ***
***          DC    CL8'name    ',B'00000010'                       ***
***          DC    CL8'alias   ',B'00000010'                       ***
***                                                                ***
***    NOTE -                                                      ***
***      If you also want the application to execute from the      ***
***      ISPF/PDF Primary Option Menu, ISR@PRIM, modify ISR@PRIM   ***
***      to invoke the application with the PANEL and NEWAPPL      ***
***      options, rather than referring to this command with the   ***
***      CMD option.  CMD interferes with the function of .ZTRAIL. ***
***      That is, using option 2 would work, but 2.3 would not.    ***
***      You could only select options actually on the             ***
***      application's primary option menu, and not subsequent     ***
***      panels below that.                                        ***
***                                                                ***
**********************************************************************
*
         EJECT
**********************************************************************
***                                                                ***
***  Author -                                                      ***
***                                                                ***
***    Chuck Hoffman                                               ***
***    Supervisor, IBM Systems Programming                         ***
***    GTE Laboratories, Technical Computation Center              ***
***    40 Sylvan Road                                              ***
***    Waltham, MA  02254                                          ***
***                                                                ***
***    Work:  617-466-2131                                         ***
***    Home:  617-551-0185                                         ***
***    Time:  617-637-1234                                         ***
***                                                                ***
**********************************************************************
*
         EJECT
**********************************************************************
***                                                                ***
***   PROLOG                                                       ***
***                                                                ***
***     1.  Linkage conventions and addressability.                ***
***     2.  Clear the completion code                              ***
***     3.  Save the pointer to the CPPL.                          ***
***                                                                ***
**********************************************************************
*
&MYNAME CSECT
&MYNAME RMODE 24
&MYNAME AMODE 24
         STM   R14,R12,12(R13)          SAVE INTO CALLERS S.A.
         B     (BASE-&MYNAME)(0,R15)    BRANCH TO AROUND EYECATCHER
         DC    AL1(L'NAME)                LENGTH OF NAME
NAME     DC    C'&MYNAME'                 MODULE NAME
         DC    C' &SYSDATE &SYSTIME '     DD.MM.YY HH.MM
BASE     LR    RBASE,R15                RBASE IS BASE REGISTER
         USING &MYNAME,RBASE              TELL ASSEMBLER
         GETMAIN  R,LV=WORKDLEN,SP=0    GET AREA FOR MYSAVE AND WORK
         ST    R13,4(0,R1)              CALLERS S.A. ADDR TO MY S.A.
         ST    R1,8(0,R13)              MY S.A. ADDR TO CALLERS S.A.
         LM    R15,R1,16(R13)           RESTORE REGS USED BY GETMAIN
         L     R13,8(0,R13)             R13 POINTS TO MY S.A.
         USING WORKD,R13                  TELL ASSEMBLER
*
         USING PARMPDL,R9               ADDRESSABILITY OF PDE LIST
*
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE TO ZERO
         ST    R1,CPPLPTR               SAVE CPPL POINTER
*
         B     MAINLINE
*
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
*
MAINLINE BAL   RBAL,ABEND               SET UP ABEND EXIT
         BAL   RBAL,PPLSETUP            SET UP PARSE PARM LIST
*
MAPARSE  BAL   RBAL,PARSE               PARSE THE INPUT PARAMETERS
         CLC   RETCDE(4),F0             IF RETURN CODE IS BAD
         BE    *+14                     THEN
         MVC   COMPCODE(4),F12            SET RETURN CODE TO 12
         B     ENDING                     AND GO TO ENDING
*
MAXISPF  BAL   RBAL,XISPF               PERFORM XISPF
         CLC   RETCDE,F0                IF RETURN CODE NOT ZERO
         BNE   ENDING04                   BRANCH TO ERROR
*
MAISPF   CLC   XISPFWA,F0               IF IN ISPF ENVIRONMENT
         BNE   MAEXEC                     EXECUTE ISPEXEC
         B     MASTART                  ELSE EXECUTE ISPSTART
*
MAEXEC   BAL   RBAL,EXEC                PERFORM EXEC
         CLC   RETCDE,F0                IF RETURN CODE NOT ZERO
         BNE   ENDING04                   BRANCH TO ERROR
         B     MAEND
*
MASTART  BAL   RBAL,START               PERFORM START
         CLC   RETCDE,F0                IF RETURN CODE NOT ZERO
         BNE   ENDING04                   BRANCH TO ERROR
         B     MAEND
*
MA#####  BAL   RBAL,#####               PERFORM #####
         CLC   RETCDE,F0                IF RETURN CODE NOT ZERO
         BNE   ENDING04                   BRANCH TO ERROR
*
MAEND    B     ENDING                   BRANCH TO ENDING
         EJECT
**********************************************************************
***                                                                ***
***   EPILOG                                                       ***
***                                                                ***
***     1.  Release storage used by IKJPARS.                       ***
***     2.  Linkage conventions.                                   ***
***                                                                ***
**********************************************************************
ENDING04 LA       R15,X'04'(0,0)          RETURN CODE TO R15
         ST       R15,COMPCODE              SAVE IN COMPLETION CODE
*
ENDING   LA       R2,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R2,0(0,R2)              R4 POINTS TO PDL
         IKJRLSA  (R2)                    FREE STORAGE OF PDL
*
ENDABEND ESTAE    0                       CANCEL THE ABEND EXIT
*
         L        R14,COMPCODE            R14 TEMPORARILY HAS COMP CODE
         LR       R15,R13                 R15 HAS MY SAVE AREA ADDRESS
         L        R13,4(0,R13)            R13 RESTORE, PNT TO CALLER SA
         FREEMAIN R,LV=WORKDLEN,SP=0,A=(R15)  FREE MYSAVE,COMPCODE,ETC
         LM       R0,R12,20(R13)          R0-R12 RESTORE FROM CALLER SA
         LR       R15,R14                 R15 GETS COMP CODE
         L        R14,12(0,R13)           R14 RESTORE FROM CALLERS S.A.
         MVI      12(R13),X'FF'           SIGNAL MODULE COMPLETE
         BR       R14                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***           SET UP EXIT FOR ABENDS                               ***
***                                                                ***
***  Use ESTAE to set up an environment to trap ABENDs.            ***
***                                                                ***
***  1.  The exit is ESTXIT.                                       ***
***  2.  The return point is at label ENDING04 in main routine.    ***
***  3.  The parm list passed to the exit will be:                 ***
***                                                                ***
***         ESTUPL                                                 ***
***         +-----------+                                          ***
***         ×           ×==> Return point                          ***
***         +-----------+                                          ***
***         ×           ×==> User work area                        ***
***         +-----------+    +-----------+                         ***
***                          × Register  ×                         ***
***                          × save area ×                         ***
***                         ---         ---                        ***
***                         ---         ---                        ***
***                          ×           ×                         ***
***                          +-----------+                         ***
***                          × Other data×                         ***
***                          +-----------+                         ***
***                                                                ***
**********************************************************************
ABEND    ST    RBAL,ABRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   ESTAEL(ESTAELLN),ESTAED  INITIALIZE ESTAE PARM LIST
         LA    R15,ENDING04             ADDRESS OF RETURN POINT
         ST    R15,ESTUPL                 INTO USER PARM LIST
         LA    R15,ESTUWK               ADDRESS OF USER WORK AREA
         ST    R15,ESTUPL+4               INTO USER PARM LIST
         LA    R4,ESTUPL                R4 HAS ADDRESS OF USER PARM LST
         L     R5,VESTXIT               R5 HAS ADDRESS OF ESTAE EXIT
*
ABESTAE  ESTAE (R5),PARAM=(R4),MF=(E,ESTAEL)  EXECUTE ESTAE
*
ABENDEND L     RBAL,ABRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST AND I/O PARM LIST           ***
***                                                                ***
***        PARSE                                                   ***
***          1.  Copy addresses of UPT, ECT and CBUF from CPPL.    ***
***          2.  Add addresses of own ECB, ANS and UWA, and        ***
***              address of PCE List (PCL) created by macros.      ***
***                                                                ***
***        I/O                                                     ***
***          1.  Copy addresses of UPD and ECT from CPPL.          ***
***          2.  Add address of own I/O ECB.                       ***
***          3.  Zero the IOPLIOPB pointer.  This will point to    ***
***              the parm block for the appropriate I/O routine.   ***
***              It will be filled in by the execute form of the   ***
***              STACK, GETLINE, PUTLINE or PUTGET macro when      ***
***              executed.                                         ***
**********************************************************************
PPLSETUP ST    RBAL,PPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         LA    R6,MYPPL                 ADDRESS OF MY PARSE PARM LIST
         USING PPL,R6                     ADDRESSABILITY
*
         MVC   PPLUPT(4),CPPLUPT        UPT  (CPPL)
         MVC   PPLECT(4),CPPLECT        ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,PPLECB                ECB  (MINE)
         MVC   PPLPCL(4),VPARMPCL       PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,PPLANS                ANS  (MINE)
         MVC   PPLCBUF(4),CPPLCBUF      CBUF (CPPL)
         LA    R5,MYUWA
         ST    R5,PPLUWA                UWA  (MINE)
         DROP  R6                       DROP ADDRESSABILITY
*
         LA    R6,MYIOPL                ADDRESS OF MY IO PARM LIST
         USING IOPL,R6                    ADDRESSABILITY
         MVC   IOPLUPT(4),CPPLUPT       UPT  (CPPL)
         MVC   IOPLECT(4),CPPLECT       ECT  (CPPL)
         LA    R15,MYIOECB
         ST    R15,IOPLECB              ECB  (MINE)
         XC    IOPLIOPB(4),IOPLIOPB     IOPB (0000)
*
         DROP  R4,R6
*
PPEND    L     RBAL,PPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
***      1.  Execute IKJPARS with CALLTSSR using own PPL.          ***
***      2.  Load address of Parm Descriptor Element List (PDL)    ***
***          from own ANS word into R9 for addressability.         ***
***                                                                ***
**********************************************************************
PARSE    ST    RBAL,PARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     RBAL,PARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   WRITE MESSAGE WITH PUTLINE                                   ***
***                                                                ***
***    1.  IOPL was initialized at beginning of program, IOPLIOPB  ***
***        field will be filled in by PUTLINE execute macro.       ***
***    2.  PTPB will be filled in by PUTLINE execute macro.        ***
***    3.  OLD will be filled in by this procedure.                ***
***                                                                ***
***    Message number in R15 is multiplied by four and used as     ***
***    offset into PUTABLE.  PUTABLE contains branch addresses     ***
***    for processing appropriate message.                         ***
**********************************************************************
PUTLPROC ST    RBAL,PURBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         SLL   R15,2                    R15 IS NOW OFFSET INTO PUTABLE
         L     R15,PUTABLE(R15)         R15 NOW POINTS TO PROCESSING
         BR    R15                      GO TO APPROPRIATE PROCESSING
*
PUTABLE  DC    A(PU0000)                PROCESS ADDR FOR BLANK LINE
*
PU0000   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0000              ADDR OF BLANK LINE
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PUOUTPUT XC    MYPTPB(MYPTPBLN),MYPTPB  ZERO THE PUTLINE PARM BLOCK
         XC    MYIOECB,MYIOECB          ZERO THE ECB
         PUTLINE PARM=MYPTPB,OUTPUT=MYOLD,MF=(E,MYIOPL)
         LTR   R15,R15                  IF RETURN IS ZERO
         BZ    PUEND                      GO TO END
         B     PUERR04                  ELSE GO TO ERROR
*
PUERR04  LA    R15,X'04'(0,0)           SET VALUE
         ST    R15,RETCDE                 INTO INTERNAL RETURN CODE
*
PUEND    L     RBAL,PURBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  FIND THE ADDRESS OF THE ISPF WORK AREA.  IF ZERO, WE ARE NOT  ***
***  IN AN ISPF ENVIRONMENT.                                       ***
***                                                                ***
**********************************************************************
XISPF    ST    RBAL,XIRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XR    R2,R2                    R2 IS BASE OF PSA
         USING PSA,R2                     ADDRESSABILITY
         L     R3,PSAANEW               R3 IS BASE OF ASCB
         USING ASCB,R3                    ADDRESSABILITY
         L     R4,ASCBASXB              R4 IS BASE OF ASXB
         USING ASXB,R4                    ADDRESSABILITY
         L     R5,ASXBLWA               R5 IS BASE OF LWA
         USING LWA,R5                     ADDRESSABILITY
*
         L     R6,LWASPF                R6 POINTS TO ISPF WORK AREA
*
         STM   R2,R6,XIADDS             STORE ADDRESSES
         DROP  R2,R3,R4,R5
*
         B     XIEND
*
XIRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     XIEND                    BRANCH TO ENDING
*
XIEND    L     RBAL,XIRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  IF NOT IN AN ISPF ENVIRONMENT, EXECUTE THE ISPFPROF COMMAND.  ***
***  THEN EXECUTE THE ISPSTART COMMAND.                            ***
***                                                                ***
**********************************************************************
START    ST    RBAL,STRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R2,CPPLPTR               POINT TO FIRST CPPL
         MVC   MYCPPL(16),0(R2)         COPY CPPL TO MY CPPL
         MVC   MYCBUF+0,=H'12'          LENGTH OF BUFFER TO BUFFER
         XC    MYCBUF+2(2),MYCBUF+2     ZERO THE OFFSET IN BUFFER
         MVC   MYCBUF+4(8),=C'ISPFPROF' ISPFPROF COMMAND TO BUFFER
         LA    R1,MYCBUF                POINTER TO MY CBUF
         ST    R1,MYCPPL+0                INTO MY CPPL
*
.PROF1   AIF   ('&PROFCMD' EQ 'NO').PROF2
         LA    R1,MYCPPL                POINT TO MYCPPL
         LINK  EP=&PROFCMD              EXECUTE TSO COMMAND
.PROF2   ANOP
*
         MVC   MYCBUF(80),BLANKS        BLANK OUT THE BUFFER
         MVC   MYCBUF+0,=H'80'          LENGTH OF BUFFER TO BUFFER
         MVC   MYCBUF+2(2),=H'9'        OPERAND OFFSET INTO BUFFER
*
    MVC   MYCBUF+4(38),=CL38'ISPSTART PANEL(&PRIMPNL) NEWAPPL(&APPL) '
*                            ....+....1....+....2....+....3....+.  ...4
         LA    R4,MYCBUF+4+38           R4 POINTS INTO BUFFER FOR OPT
         BAL   RBAL,OPT                 PERFORM OPT PROC
         LA    R1,MYCPPL                POINT TO MYCPPL
         LINK  EP=ISPSTART              EXECUTE TSO COMMAND
*
         B     STEND
*
STRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     STEND                    BRANCH TO ENDING
*
STEND    L     RBAL,STRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  EXECUTE ISPLINK TO THE SELECT SERVICE FOR THE PRIMARY MENU    ***
***                                                                ***
**********************************************************************
EXEC     ST    RBAL,EXRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   EXSERVCE(8),=CL8'SELECT' SET SERVICE
         MVC   EXBUFLN,=F'80'           SET BUFFER LENGTH
         MVC   EXBUFFER(80),BLANKS      CLEAR THE BUFFER
*
      MVC   EXBUFFER(29),=CL29'PANEL(&PRIMPNL) NEWAPPL(&APPL) '
*                              ....+....1....+....2....+..  ..3
         LA    R4,EXBUFFER+29           R4 POINTS INTO BUFFER FOR OPT
         BAL   RBAL,OPT                 PERFORM OPT PROC
         LA    R1,EXSERVCE              ADDR OF SERVICE
         ST    R1,EXLIST+00               TO PARM LIST
         LA    R1,EXBUFLN               ADDR OF BUFFER LENGTH
         ST    R1,EXLIST+04               TO PARM LIST
         LA    R1,EXBUFFER              ADDR OF BUFFER
         ST    R1,EXLIST+8                TO PARM LIST
         OI    EXLIST+8,X'80'           MARK THE END OF LIST
         LA    R1,EXLIST                POINT TO THE PARM LIST
         LINK  EP=ISPLINK               EXECUTE ISPLINK
*
         B     EXEND
*
EXRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     EXEND                    BRANCH TO ENDING
*
EXEND    L     RBAL,EXRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  ADD 'OPT' OPERAND INTO BUFFER.                                ***
***  R4 POINTS TO LOCATION IN BUFFER.                              ***
***                                                                ***
**********************************************************************
OPT      ST    RBAL,OPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         TM    POSOPT+6,X'80'           IF OPT NOT USED
         BNO   OPEND                      BYPASS THIS
*
         MVC   0(4,R4),=C'OPT('         PUT 'OPT(' INTO BUFFER
         LA    R4,4(0,R4)               R4 POINTS TO NEXT BUFF POSITN
         L     R2,POSOPT                R2 POINTS TO OPTION CHARACTERS
         LH    R3,POSOPT+4              R3 IS LENGTH OF OPTION
         BCTR  R3,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   0(*-*,R4),0(R2)            MOVE OPT CHARS TO BUFFER
         EX    R3,*-6                   EXECUTE THE MOVE
         LA    R4,1(R3,R4)              R4 POINTS TO NEXT BUFF POSITN
         MVI   0(R4),C')'               MOVE ')' TO BUFFER
*
         B     OPEND
*
OPRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     OPEND                    BRANCH TO ENDING
*
OPEND    L     RBAL,OPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***                                                                ***
***                                                                ***
**********************************************************************
#####    ST    RBAL,##RBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
*
         B     ##END
*
##RET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     ##END                    BRANCH TO ENDING
*
##END    L     RBAL,##RBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
CONSTDTA DS    0D                       AREA FOR DATA CONSTANTS
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
VESTXIT  DC    V(ESTXIT)                ADDR OF ABEND EXIT
*
F0       DC    F'0'                     CONSTANT
F12      DC    F'12'                    CONSTANT
H2       DC    H'2'                     CONSTANT
BLANKS   DC    CL80' '                  BLANK LINE
*                              0....+....1....+....2....+....3....+
MSG0000  DC    H'06',H'00',CL02'  '
*
ESTAED   ESTAE MF=L
*
CONSTEND DS    0D
CONSTLEN EQU   *-CONSTDTA               TOTAL LENGTH OF CONSTANTS
*
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
***    See syntax description at beginning of program.             ***
***                                                                ***
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
POSOPT   IKJIDENT 'Initial option.',                                   X
               MAXLNTH=40,                                             X
               FIRST=ANY,OTHER=ANY,                                    X
               HELP=('Option name for the primary option menu.')
*
         IKJENDP
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA IN SUBPOOL 000                                     ***
***                                                                ***
**********************************************************************
WORKD    DSECT                          AREA-13 FOR VARIABLES
MYSAVE   DS    18F                      REGISTER SAVE AREA
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
*
ABRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
PURBALSV DS    F                        RETURN ADDRESS SAVE AREA
XIRBALSV DS    F                        RETURN ADDRESS SAVE AREA
STRBALSV DS    F                        RETURN ADDRESS SAVE AREA
EXRBALSV DS    F                        RETURN ADDRESS SAVE AREA
OPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
##RBALSV DS    F                        RETURN ADDRESS SAVE AREA
*
MYCPPL   DS    4A                       MY COMMAND PROCESSOR PARM LIST
MYCBUF   DS    2H,CL251                 MY COMMAND BUFFER
*
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
*
MYUWA    DS    0D                       WORK/SAVE AREA FOR PARSE EXITS
MYUWASV  DS    18F                      SAVE AREA FOR EXITS
MYUWAWRK DS    14F                      WORK AREA FOR EXITS
*
MYIOPL   DS    4F                       IO PARM LIST
MYIOECB  DS    F                        ECB FOR I/O ROUTINES
MYPTPB   PUTLINE MF=L                   PUTLINE PARM BLOCK (PTPB)
MYPTPBLN EQU   *-MYPTPB                   LENGTH OF PTPB
*
*                                       OUTPUT LINE DESCRIPTOR, 1 LEVEL
MYOLD    DS    F                          NUMBER OF SEGMENTS
         DS    5A                         ADDRS OF UP TO FIVE SEGMENTS
*
ESTAEL   ESTAE MF=L
ESTAELLN EQU   *-ESTAEL
ESTUPL   DS    0D                       ESTAE USER PARM LIST
         DS    A                          ADDRESS OF RETURN POINT
         DS    A                          ADDRESS OF USER WORK AREA
ESTUWK   DS    0D                       ESTAE USER WORK AREA
         DS    18F                        REGISTER SAVE AREA
         DS    D                          DOUBLE WORD FOR UNPACK
         DS    F                          BAL REGISTER SAVE AREA
         WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
*
XIADDS   DS    0F
XIPSA    DS    A                        PSA ADDRESS
XIASCB   DS    A                        ASCB ADDRESS
XIASXB   DS    A                        ASXB ADDRESS
XILWA    DS    A                        LWA ADDRESS
XISPFWA  DS    A                        ISPF WORK AREA ADDRESS, OR ZERO
*
EXLIST   DS    3A                       PARM LIST FOR ISPLINK
EXSERVCE DS    CL8                      SERVICE
EXBUFLN  DS    F                        BUFFER LENGTH
EXBUFFER DS    CL80
*
WORKDEND DS    0D
WORKDLEN EQU   *-WORKD                  TOTAL LENGTH OF WORK-13 AREA
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING DSECTS                                               ***
***                                                                ***
**********************************************************************
*
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         IKJIOPL                        I/O PARM LIST
*
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES  MAPS DAIRFAIL CONTROL BLOCKS
         IEFZB4D0                       MAPS SVC99 CONTROL BLOCKS
*
         IHASDWA                      , SDWA FOR ESTAE EXIT
         EJECT
         IHAPSA   DSECT=YES,LIST=NO    PREFIXED STORAGE AREA
         EJECT
         IHAASCB  DSECT=YES,LIST=NO    ADDRESS SPACE CONTROL BLOCK
         EJECT
         IHAASXB  DSECT=YES            ASCB EXTENSION BLOCK
         EJECT
         IKJEFLWA                      LOGON WORK AREA
         EJECT
**********************************************************************
***                                                                ***
***   EQUATES                                                      ***
***                                                                ***
**********************************************************************
RBASE    EQU   12                       BASE REGISTER
RBAL     EQU   10                       BAL REGISTER
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
**********************************************************************
***                  ESTAE (ABEND) EXIT                            ***
***                                                                ***
***   This exit will receive control from RTM during ABEND         ***
***   processing.  Data areas available to this exit will be       ***
***   the System Diagnostic Work Area (SDWA) provided by the       ***
***   RTM, and the user parm list and the areas it points to       ***
***   which are provided by the main routine.                      ***
***                                                                ***
***   This exit will format an ABEND message, and write it         ***
***   with a WTO.  It will then return to RTM.                     ***
***                                                                ***
***   The register save area used by this exit has already been    ***
***   obtained by a GETMAIN by the main program, prior to the      ***
***   execution of the ESTAE macro.                                ***
***                                                                ***
***   This exit is entered by RTM with standard MVS linkage        ***
***   conventions.  Upon entry, the following relationships        ***
***   exist:                                                       ***
***                                                                ***
*** R1              SDWA (IHASDWA)   ESTUSRPL                      ***
*** +-----------+   +------------+   +-----------+                 ***
*** ×           ×==>× SDWAPARM   ×==>× ESURETA   ×==> Return point ***
*** +-----------+   +------------+   +-----------+                 ***
***                 × SDWAABCC   ×   × ESUWRKA   ×==> ESUWKD       ***
***                 +------------+   +-----------+   +-----------+ ***
***                 ×            ×                   × Save area × ***
***                ---          ---                 ---         ---***
***                ---          ---                 ---         ---***
***                 ×            ×                   ×           × ***
***                 +------------+                   +-----------+ ***
***                 × SDWAGR15   ×                   × Other work× ***
***                 +------------+                   ×   areas   × ***
***                 ×            ×                   +-----------+ ***
***                ---          ---                                ***
***                ---          ---                                ***
***                 ×            ×                                 ***
***                 +------------+                                 ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***          SEE DOCUMENTATION ON PREVIOUS PAGE                    ***
***                                                                ***
**********************************************************************
ESTXIT   CSECT
         STM   R14,R12,12(R13)          SAVE REGS INTO CALLER'S S.A.
         LR    R12,R15                  R12 HAS BASE ADDRESS
         USING ESTXIT,R12                 ADDRESSABILITY
         LR    R11,R1                   R11 POINTS TO SDWA
         USING SDWA,R11                   ADDRESSABILITY
         L     R10,SDWAPARM             R10 POINTS TO USER PARM LIST
         USING ESTUSRPL,R10               ADDRESSABILITY
         L     R15,ESUSRWKA             R15 POINTS TO USER WORK AREA
         ST    R13,4(0,R15)             CALLER'S S.A. ADDR TO OWN S.A.
         ST    R15,8(0,R13)             OWN S.A. ADDR TO CALLER'S S.A.
         LR    R13,R15                  R13 POINTS TO OWN S.A.
         USING ESUWKD,R13                 ADDRESSABILITY
*
ESTMESSG BAL   R9,ESMSG                 FORMAT AND WRITE A MESSAGE
*
         PRINT GEN
         L     R4,ESURETA               R4 CONTAINS RETURN ADDRESS
ESTSETRP SETRP WKAREA=(R11),RC=4,RETADDR=(R4),RETREGS=YES,FRESDWA=YES
         PRINT NOGEN
*
ESTEND   XR    R15,R15                  CLEAR RETURN CODE
         L     R13,4(0,R13)             RESTORE R13, PNT TO CALLER'S SA
         LM    R0,R12,20(R13)           RESTORE R0-R12 FROM CALLER'S SA
         L     R14,12(0,R13)            RESTORE R14 FROM CALLER'S S.A.
         BR    R14                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***                  WRITE ABEND MESSAGE                           ***
***                                                                ***
**********************************************************************
ESMSG    ST    R9,ESUBALS1              SAVE BAL REGISTER
*
         MVC   WTOL(WTOLLEN),WTOD       INITIALIZE WTO PARM LIST
*
ESSYSCDE L     R3,SDWAABCC              R3 HAS ABEND CODES
         SLL   R3,8                     LEFT JUSTIFY SYSTEM CODE
         LA    R4,WTOL+26               R4 POINTS INTO MESSAGE
         LA    R5,3                     R5 COUNTER SET TO 3
         BAL   R9,ESHEX                 CONVERT TO DISPLAYABLE HEX
*
ESR15CDE L     R3,SDWAGR15              R3 HAS ABEND R15 CONTENTS
         LA    R4,WTOL+42               R4 POINTS INTO MESSAGE
         LA    R5,8                     R5 COUNTER SET TO 8
         BAL   R9,ESHEX                 CONVERT TO DISPLAYABLE HEX
*
ESUSRCDE L     R3,SDWAABCC              R3 HAS ABEND CODES
         N     R3,ESXFFF                R3 HAS ONLY USER CODE
         CVD   R3,ESUWKDBL              CONVERT TO DECIMAL
         OI    ESUWKDBL+7,X'0F'         KILL THE SIGN
         UNPK  WTOL+33(4),ESUWKDBL(8)   UNPACK INTO MESSAGE
*
ESWTO    WTO   MF=(E,WTOL)              WRITE THE MESSAGE
*
ESMSGEND L     R9,ESUBALS1              RESTORE THE BAL REGISTER
         BR    R9                       RETURN
*
*
ESHEX    XR    R2,R2                    CLEAR EVEN REG OF EVEN/ODD
         SLDL  R2,4                     R2 RECEIVES ONE HEX DIGIT
         LA    R4,1(0,R4)               R4 POINTS TO NEXT MESSAGE CHAR
         LA    R2,ESHEXTBL(R2)          R2 POINTS TO CHAR IN TABLE
         MVC   0(1,R4),0(R2)            MOVE CHAR FROM TABLE TO MESSAGE
         BCT   R5,ESHEX                 LOOP BACK UP
         BR    R9                       RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***          DATA AREAS AND DSECTS FOR ESTAE EXIT                  ***
***                                                                ***
**********************************************************************
ESHEXTBL DC    CL16'0123456789ABCDEF'   TRANSLATE TABLE
         DS    0F                       ALIGN
ESXFFF   DC    X'00000FFF'              MASK TO CLEAR BITS 0-19
WTOD     WTO   '*** ABEND ***  Codes: SXXX, U9999, R15=XXXXXXXX',      X
               ROUTCDE=(11),DESC=(7),MF=L
*
*
*
ESTUSRPL DSECT                          USER PARM LIST
ESURETA  DS    A                          RETURN ADDRESS FOR RTM
ESUSRWKA DS    A                          WORK AREA ADDRESS FOR ME
*
*
*
ESUWKD   DSECT                          USER WORK AREA
ESUWKSAV DS    18F                        REGISTER SAVE AREA
ESUWKDBL DS    D                          DOUBLE WORD FOR UNPACK
ESUBALS1 DS    F                          BAL REGISTER SAVE AREA
WTOL     WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
WTOLLEN  EQU   *-WTOL                     LENGTH OF WTO PARM LIST
*
         PRINT GEN
         END   &MYNAME
