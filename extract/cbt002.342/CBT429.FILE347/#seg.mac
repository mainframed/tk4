         MACRO
&L       #SEG  &DEST,&STR,&LEN,&NOFIT=
.*********************************************************************
.*
.*       #SEG
.*
.*       STR --DEFINES THE STRING WHICH IS MOVED INTO THE BUFFER.
.*       LEN --LENGTH OF INPUT STRING.
.*       DEST--DESTINATION OF STRING.  THIS FIELD IS A VARIABLE
.*             LENGTH STRING WITH A FULLWORD CONTAINING A HALFWORD
.*             FOR CURRENT LENGTH AND MAXIMUM LENGTH.
.*
.*********************************************************************
         LCLC  &LBL
         LCLA  &LN                 LENGTH OF VARIABLE/STRING.
         LCLC  &LENSYM             SYMBOL FOR LENGTH OF VARIABLE.
&LBL     SETC  '&L'
         AIF   ('&LBL' EQ '').NOLBL
&L       DS    0H
.NOLBL   ANOP
         AIF   (T'&STR NE 'O').OKSTR
         MNOTE 8,'SEG100E STRING NOT SPECIFIED'
         MEXIT
.OKSTR   ANOP
         SR    0,0
         LA    0,&STR              POINT TO STRING.
         AIF   (T'&LEN EQ 'O').NOLEN
         LA    1,&LEN              LOAD LENGTH INTO R1
         AGO   .DEST
.NOLEN   ANOP
         AIF   ('&STR'(1,1) EQ '''').STR
&LENSYM  SETC  'L'''               SETUP THE LENGTH ATTRIBUTE.
         LA    1,&LENSYM&STR       USE VARIABLE DEFINED LENGTH.
         AGO   .DEST
.STR     ANOP
&LN      SETA  K'&STR-2            COMPUTE STRING LENGTH.
         LA    1,&LN               LOAD LENGTH INTO R0.
.DEST    ANOP
         SR    14,14
         LA    14,&DEST            VARIABLE STRING FIELD.
         LA    15,0                FOR LENGTH COMPUTATIONS
         LH    15,0(,14)           LOAD CURRENT LENGTH
         AR    15,1                ADD NEW LENGTH
         AIF   ('&NOFIT' EQ '').NOCHK
         CH    15,2(,14)           LONGER THAN MAX LENGTH ??
         BH    &NOFIT              YES; ERROR EXIT
.NOCHK   ANOP
         STH   15,0(,14)
         SR    15,1
         LA    14,4(15,14)         POINT TO NEXT BYTE
         LR    15,1                COPY LENGTH TO MOVE
         MVCL  14,0                AND INSERT THE STRING
         MEND
