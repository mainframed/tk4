         MACRO
&NAME    HEXCNVT &OUT,&IN,&LN
         GBLC  &HEXSECT,&HEXTAB,&HEXWRD
         LCLA  &A,&B,&C,&D,&E,&F,&G
         LCLB  &REGSW,&RLNSW,&DFALTSW
         LCLB  &HEXSW
         LCLC  &GNAME
&GNAME   SETC  'HEX'.'&SYSNDX'
&DFALTSW SETB  (T'&LN EQ 'O')
         AIF   (&DFALTSW EQ 1).NEXT2
&RLNSW   SETB  ('&LN'(1,1) EQ '(')
.NEXT2   AIF   ('&OUT' EQ '' OR '&IN' EQ '').ERROR2
&REGSW   SETB  ('&OUT'(1,1) EQ '(')
         AIF   (&DFALTSW EQ 0 AND T'&LN NE 'N').ERROR4
         AIF   (&DFALTSW EQ 1).NEXT3
         AIF   (N'&LN NE 1).ERROR5
.NEXT3   AIF   (&RLNSW EQ 1 AND T'&LN(1) NE 'N').ERROR4
         AIF   (&DFALTSW EQ 1).NEXT4
         AIF   (&RLNSW EQ 1 AND (&LN(1) LT 2)).ERROR3
         AIF   (&RLNSW EQ 0 AND T'&LN(1) NE 'N').ERROR4
         AIF   (&RLNSW EQ 1).NEXT4
         AIF   (&LN LT 1 OR &LN GT 112).ERROR6 LN MUST BE FROM 1-112
.NEXT4   AIF   (&REGSW EQ 0).NEXT9
         AIF   ('&IN'(1,1) NE '(').ERROR1 &IN MUST ALSO BE A REG
         AIF   (T'&OUT NE 'N').ERROR4
         AIF   (N'&OUT GT 1).ERROR3
         AIF   (&OUT(1) LE 1).ERROR3
         AIF   (T'&IN NE 'N').ERROR4
         AIF   (N'&IN GT 1).ERROR3
         AIF   (&IN(1) LE 1).ERROR3
         AGO   .NEXT5
.NEXT9   AIF   ('&IN'(1,1) EQ '(').ERROR1
         AIF   (T'&OUT EQ 'N' OR T'&IN EQ 'N').ERROR4
.NEXT5   ANOP
&HEXSW   SETB  ('&HEXSECT' EQ '&SYSECT')
         AIF   (&HEXSW EQ 1).NEXT6
&HEXSECT SETC  '&SYSECT'
&HEXWRD  SETC  '&GNAME'
&HEXTAB  SETC  '&GNAME.A'
.NEXT6   ANOP
         AIF   (&DFALTSW EQ 1).DFALT
         AIF   (&RLNSW EQ 1).REGLN
         AIF   (&LN GT 7).MULTI
&A       SETA  &LN*2+1             LENGTH FOR R1 OF UNPK
&B       SETA  &LN+1               LENGTH FOR R2 OF UNPK
.CONTA   AIF   (&REGSW EQ 1).ISAREG
&NAME    UNPK  &OUT.(&A),&IN.(&B)  UNPACK HEX DIGITS
&A       SETA &A-1
         MVI   &OUT+&A,X'40'       CLEAR SIGN BYTE
.CONTB   L     1,&GNAME.B          SET ORIGIN OF TABLE
         S     1,0(1)              BACK 240 BYTES
         AIF   (&RLNSW EQ 1).CONTG
         AIF   (&REGSW EQ 1).CONTD
         TR    &OUT.(&A),0(1)      TRANSLATE FA-FF
.CONTC   B     &GNAME.H            BR AROUND CONSTANTS
         AIF   (&HEXSW EQ 1).ENDD  DON'T GENERATE TABLE IF SW ON
         AGO   .ENDC
.ISAREG  ANOP
&NAME    UNPK  0(&A,&OUT(1)),0(&B,&IN(1)) UNPACK HEX DIGITS
&A       SETA  &A-1
         MVI   &A.(&OUT(1)),X'40'  CLEAR SIGN BYTE
         AGO   .CONTB              SET REG 1 FOR TR
.CONTD   TR    0(&A,&OUT(1)),0(1)  TRANSLATE FA-FF
         AGO   .CONTC
.DFALT   ANOP
&A       SETA  9                   DFAULT TO 4 BYTES
&B       SETA  5
         AGO   .CONTA
.MULTI   ANOP
&C       SETA  (&LN/4)             TO UNPACK 4 AT A TIME
&D       SETA  &LN-&C*4            REMAINDER
&A       SETA  9
&B       SETA  5
         AIF   (&REGSW EQ 0).MULTIB
&E       SETA  &OUT(1)
&F       SETA  &IN(1)
&G       SETA  1
&NAME    LA    &G,&C               SET COUNT
.MULTIA  ANOP
&GNAME.E UNPK  0(&A,&E),0(&B,&F)   UNPACK HEX DIGITS
&A       SETA  &A-1
         MVI   &A.(&E),X'EF'       CLEAR SIGN BYTE
         LA    &E,9(&E)            INCREMENT ADDRESSES
         LA    &F,4(&F)
         BCT   &G,&GNAME.E         LOOP
         AIF   (&D EQ 0).CONTH     NO REMAINDER
&A       SETA  &D*2+1
&B       SETA  &D+1
         UNPK  0(&A,&E),0(&B,&F)   UNPACK HEX DIGITS
&A       SETA  &A-1
         MVI   &A.(&E),X'40'       CLEAR SIGN BYTE
.CONTH   ANOP
&C       SETA  &C+&LN*2
&G       SETA  &C+&D*2             LENGTH OF FIELD TO BE TRANSLATED
         LA    1,&C                RE-ESTABLISH BEGINING
         SR    &E,1                OF FIELD
         L     1,&GNAME.B          SET ORIGIN OF TABLE
         S     1,0(1)              BACK 240 BYTES
         TR    0(&G,&E),0(1)       TRANSLATE FA-FF
         AGO   .CONTC
.MULTIB  ANOP
&NAME    LA    2,&OUT
         LA    1,&IN
         LA    3,&C                SET COUNT
&E       SETA  2
&F       SETA  1
&G       SETA  3
         AGO   .MULTIA
.REGLN   ANOP
         AIF   (&REGSW EQ 0).CONTI
&E       SETA  &OUT(1)
&F       SETA  &IN(1)
         AGO   .CONTJ
.CONTI   ANOP
&E       SETA  2
&F       SETA  3
&NAME    LA    2,&OUT
         LA    3,&IN
         LR    1,&LN(1)
         AGO   .CONTK
.CONTJ   ANOP
&NAME    LTR   1,&LN(1)            LOAD LN TO REG 1 AND TEST
         BZ    &GNAME.H            DO NOTHING IF ZERO
.CONTK   LA    0,3                 SET UP MASK
         NR    0,1                 AND SAVE 2 LOW ORDER BITS
         SRL   1,2                 DIVIDE BY 4
         LTR   1,1                 LENGTH LESS THAN 4
         BZ    &GNAME.F            IF YES--BRANCH
&GNAME.E UNPK  0(9,&E),0(5,&F)     UNPACK HEX DIGITS
         MVI   8(&E),X'EF'         CLEAR SIGN BYTE
         LA    &E,9(&E)            INCREMENT REGS
         LA    &F,4(&F)
         BCT   1,&GNAME.E          LOOP
&GNAME.F LTR   0,0
         BZ    &GNAME.G            BRANCH IF SAVED BITS ARE 0
         LR    1,0                 ADJUST REG 1
         AR    1,0                 FOR EXECUTE INSTRUCTION
         SLL   1,4
         OR    1,0
         EX    1,&GNAME.C          EXECUTE UNPK
         SRL   1,4
         AR    &E,1
         MVI   0(&E),X'40'         CLEAR SIGN BYTE
&GNAME.G LR    1,&LN(1)
         SRL   1,2                 DIVIDE BY 4
         AR    &LN(1),&LN(1)       SET UP LENGTH FOR
         AR    &LN(1),1            TRANSLATE INSTRUCTION
         SR    &E,&LN(1)           RE-ESTABLISH BEGINING OF FIELD
         AGO   .CONTB              SET REG 1 FOR TR
.CONTG   BCTR  &LN(1),0            DECR LN 1 FOR EXECUTE INST
         EX    &LN(1),&GNAME.D     EXECUTE TR
         B     &GNAME.H            BRANCH AROUND CONSTANTS
         AGO   .ENDA
.ERROR1  MNOTE 12,'INCONSISTANT PARAMETERS'
         MEXIT
.ERROR2  MNOTE 12,'PARAMETER MISSING'
         MEXIT
.ERROR3  MNOTE 12,'REG PARMS CANNOT BE 0 OR 1 '
         MEXIT
.ERROR4  MNOTE 12,'INVALID TYPE ATTRIBUTE SPECIFIED IN PARAMETERS'
         MEXIT
.ERROR5  MNOTE 12,'&LN--MULTIPLE PARAMETERS INVALID'
         MEXIT
.ERROR6  MNOTE 12,'&LN INVALID--MUST BE FROM 1-112'
         MEXIT
.ENDA    ANOP
&GNAME.C UNPK  0(0,&E),0(0,&F)     UNPACK HEX DIGITS
&GNAME.D TR    0(0,&E),0(1)        TRANSLATE FA-FF
         AIF   (&HEXSW EQ 0).ENDC
.ENDD    ANOP
&GNAME.B DC    A(&HEXWRD)
         AGO   .ENDB
.ENDC    ANOP
&GNAME.B DC    A(&HEXWRD)
&GNAME   DC    F'234'
         DC    C'  '
&GNAME.A DC    C'0123456789ABCDEF'
.ENDB    ANOP
&GNAME.H DS    0H
         MEND
