         MACRO
         ENDDATA#
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLC  &REENT,&MYCSECT
         GBLA  &GIFLVL,&GDOLVL                   IF/DO LEVEL
         GBLA  &GDWCNT
         GBLB  &IFORSW#,&UNORSW#,&DWORSW#
.*
.**********************************************************************
.*       CHECK INPUT PARAMETERS                                       *
.**********************************************************************
.*
         AIF   (&GIFLVL EQ 0).L10
         MNOTE 8,'YS000016-MISSING ENDIF STATEMENT(S)'
&GIFLVL  SETA  0
.L10     ANOP
         AIF   (&GDOLVL EQ 0).L15
         MNOTE 8,'YS000017-MISSING ENDDO/UNTIL STATEMENT(S)'
&GDOLVL  SETA  0
&GDWCNT  SETA  0
.L15     ANOP
         AIF   (&IFORSW# EQ 0).L17
         MNOTE 8,'YS000018-MISSING IF FOLLOWING IFOR(S)'
&IFORSW# SETB  0
.L17     ANOP
         AIF   (&UNORSW# EQ 0).L17A
         MNOTE 8,'YS000019-MISSING UNTIL FOLLOWING UNTILOR(S)'
&UNORSW# SETB  0
.L17A    ANOP
         AIF   (&DWORSW# EQ 0).L17B
         MNOTE 8,'YS000020-MISSING DOWHILE FOLLOWING DOWHLOR(S)'
&DWORSW# SETB  0
.L17B    ANOP
.*
         SEQCK EXIT                              PRED/SUCCESSOR CHECK
.*
         AIF   ('&REENT' EQ 'N').CODE001
         SPACE
&MYCSECT CSECT                                   CSECT CONTINUATION
.CODE001 ANOP
         SPACE
         MEND
