PROC 0 DEBUG IX('SYS3.XFER.DATA')
IF &DEBUG EQ DEBUG THEN CONTROL LIST CONLIST SYMLIST NOFLUSH MSG PROMPT
   ELSE CONTROL NOFLUSH NOMSG NOPROMPT NOLIST
/*
/* >>> COPYRIGHT (C) 1985, 1986 HOWARD M. DEAN
/* >>> INSTALLED 06/25/85 - HOWARD M. DEAN
/* >>> UPDATED   06/25/85 - HOWARD M. DEAN
/*
/**
/** CHECK FOR PROPER ENVIRONMENT
/**
IF  &SYSENV EQ FORE THEN -
    DO
      WRITE *-------------------------------------------*
      WRITE *  THIS CLIST CAN ONLY BE EXECUTED IN THE   *
      WRITE *  BACKGROUND AS PART OF THE XFERDUMP PROC  *
      WRITE *-------------------------------------------*
      CONTROL FLUSH
      EXIT CODE(99)
    END
/**
/** SET UP VARIABLES
/**
SET &SYSD2 EQ &SUBSTR(4:5,&STR(&SYSDATE))
SET &SW     EQ 0
SET &CALL   EQ &STR(#)
SET &NAME   EQ &STR(')&IX(DAY&SYSD2)&STR(')
SET &PREFIX EQ &STR(SYS3)
SET &HNAME  EQ &STR(')&PREFIX..TEMP.DATA&STR(')
SET &INAME  EQ &STR(')&PREFIX..WTO.DATA&STR(')
/**
/**  SET UP FILES
/**
DEL &HNAME PURGE
FREE  FI(HOLD INDEX)
ALLOC FI(INDEX) DA(&NAME)  SHR
ALLOC FI(HOLD)  DA(&HNAME) NEW CATALOG -
      SP(100,10) TRACKS RELEASE UNIT(SYSDA)
/**
/**  OPEN FILES AND PRIME INPUT
/**
OPENFILE INDEX INPUT
OPENFILE TEMP  INPUT               /* ALLOCATED IN XFERDUMP PROC */
OPENFILE HOLD  OUTPUT
GETFILE  INDEX
/**
/**  SET UP ERROR HANDLING ROUTINE
/**
ERROR -
   DO
     IF &LASTCC = 400 THEN SET &SW EQ 1
     RETURN
   END
/**
/** BEGIN PROCESSING
/**
IF &SUBSTR(1:18,&INDEX) EQ &STR(DUMPS FOR &SYSDATE) THEN -
   DO
     SET &SW   EQ 0
     SET &HOLD EQ &NSTR(&INDEX)       /* DON'T DROP HEADER RECORD */
     PUTFILE HOLD
     DO  WHILE &SW EQ 0               /* READ TO END OF FILE */
       GETFILE INDEX                  /* AND WRITE TO HOLD FILE */
       IF &SW EQ 0 THEN -
         DO
           SET &HOLD EQ &NSTR(&INDEX)
           PUTFILE HOLD
         END
     END
     SET &HOLD = &STR(*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-)
     SET &HOLD = &HOLD&STR(-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*)
     PUTFILE HOLD                     /* PUT OUT A SEPERATOR LINE */
     SET &HOLD = &STR(DUMPS FOR &SYSDATE)
     PUTFILE HOLD
     SET &SW EQ 0
       DO WHILE &SW EQ 0
         GETFILE TEMP
         IF &SW EQ 0 THEN -
           DO
             SET &HOLD EQ &NSTR(&TEMP)
             IF  &SUBSTR(1:3,&HOLD) EQ &STR(END)   THEN SET &SW EQ 1
             IF  &SUBSTR(1:5,&HOLD) EQ &STR(READY) THEN SET &SW EQ 1
             IF  &SW EQ 0 THEN PUTFILE HOLD
           END
       END
   END
/*
ELSE -
   DO
     SET &HOLD EQ &STR(DUMPS FOR &SYSDATE)
     PUTFILE  HOLD
     SET &SW EQ 0
     DO WHILE &SW EQ 0
       GETFILE TEMP
       IF &SW EQ 0 THEN -
         DO
          SET &HOLD EQ &NSTR(&TEMP)
          IF  &SUBSTR(1:3,&HOLD) EQ &STR(END)   THEN SET &SW EQ 1
          IF  &SUBSTR(1:5,&HOLD) EQ &STR(READY) THEN SET &SW EQ 1
          IF  &SW EQ 0 THEN PUTFILE HOLD
         END
     END
   END
/**
/** UPDATE THE PDS
/**
CLOSFILE TEMP
CLOSFILE INDEX
CLOSFILE HOLD
/*
OPENFILE INDEX OUTPUT
OPENFILE HOLD  INPUT
SET &SW EQ 0
DO WHILE &SW EQ 0
   GETFILE HOLD
   IF &SW EQ 0 THEN -
     DO
       SET &INDEX EQ &NSTR(&HOLD)
       PUTFILE INDEX
     END
END
/*
CLOSFILE INDEX
CLOSFILE HOLD
/**
/** WRITE TO THE OPERATOR
/**
DEL &INAME PURGE
FREE  FI(SYSIN)
ALLOC FI(SYSIN) DA(&INAME) NEW CATALOG -
      SP(100,10) TRACKS RELEASE UNIT(SYSDA)
/**
CLOSFILE TEMP                    /* RESET TO BEGINNING OF FILE */
OPENFILE TEMP  INPUT
OPENFILE SYSIN OUTPUT
/**
SET &SW EQ 0
DO WHILE &SW EQ 0
  GETFILE TEMP
  IF &SW EQ 0 THEN -
    DO
      SET &SYSIN EQ &NSTR(&TEMP)
      IF  &SUBSTR(1:3,&SYSIN) EQ &STR(END)   THEN SET &SW EQ 1
      IF  &SUBSTR(1:5,&SYSIN) EQ &STR(READY) THEN SET &SW EQ 1
      IF  &SW EQ 0 THEN PUTFILE SYSIN
    END
END
CLOSFILE TEMP
CLOSFILE SYSIN
&CALL XFERWTO
/**
/** CLEAN UP AND EXIT
/**
DEL  &HNAME PURGE
DEL  &INAME PURGE
FREE FI(HOLD INDEX SYSIN)
EXIT CODE(0)
