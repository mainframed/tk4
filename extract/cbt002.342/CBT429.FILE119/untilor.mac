         MACRO
         UNTILOR &OPND1,&RELA,&OPND2,&OPCD
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GDOCNT,&GDOLVL,&GDOSTK(16)
         GBLB  &UNORSW#                       'UNTILOR' INDICATOR
         GBLC  &GTYPE
         LCLA  &I,&J,&K                          'AND' LOOP COUNTERS
         LCLB  &ANDSW
         LCLC  &ORLOC
         LCLA  &WKA1
         LCLC  &LOC
.*
         SEQCK UNTILOR
.*
         SPACE
         AIF   (&GDOLVL EQ 0).ER1
&WKA1    SETA  &GDOSTK(&GDOLVL)+1
&LOC     SETC  'ZLDO'.'&WKA1'
.*
.**********************************************************************
.*       GENERATE CODE TO TEST CONDITION AND BRANCH                   *
.**********************************************************************
.*
&ORLOC   SETC  'ZLAD&SYSNDX'
&ANDSW   SETB  0                                 ASSUME - NO *AND*
&J       SETA  0                                 FIRST DELIMITER NUMBER
&I       SETA  1                                 FIRST PARM NUMBER
.*
.LOOPAND ANOP                                    LOOP THRU PARMS
&J       SETA  &J+1                        TRY NEXT PARM AS DELIMITER
         AIF   (&J GT N'&SYSLIST).ENDAND         IS IT END OF PARMS ?
         AIF   ('&SYSLIST(&J)' NE '*AND*').LOOPAND  IS IT DELIMITER ?
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN 2 DELIMITERS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND1       MUST BE 4 OR 1 PARM
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND1  ANOP                                    GEN. TEST AND BRANCH
&GTYPE   SETC  'UNTIL'                           CHANGE FLAG FOR GENC
&ANDSW   SETB  1                                 INDICATE - *AND* FOUND
         AIF   (&K EQ 1).GENOP1                  DO WE HAVE 1 PARM ?
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&ORLOC
         AGO   .GENOK
.GENOP1  ANOP                                    GEN CODE WITH 1 PARM
         GENC  &SYSLIST(&I),,,,&ORLOC
.GENOK   ANOP                                    GEN IS DONE
&GTYPE   SETC  'UNTILOR'                         RESTORE FLAG FOR GENC
.*
&I       SETA  &J+1                  SET POINTER TO NEXT SET OF PARMS
         AGO   .LOOPAND                          REPEAT LOOP
.*
.ENDAND  ANOP                                    END OF LOOP
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN LAST DELIMTRS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND2       MUST BE 4 OR 1
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND2  ANOP                                  GEN LAST TEST & BRANCH
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
.*
         AIF   (&ANDSW EQ 0).CODE040             IS THERE *AND* PARM ?
&ORLOC   DS    0H                                CONDITIONS ARE FALSE
.CODE040 ANOP
.*
&UNORSW# SETB  1
.*
         SPACE
.*
         MEXIT
.ER1     MNOTE 8,'UNTILOR NOT PRECEDED BY DO'
         SPACE
         MEND
