*****
*
*   UPDATED 10OCT84, WILLIAM SMITH, SYNTEX TECHNICAL SUPPORT
*   -  AUTHOR UNKNOWN; CONTRIBUTED BY SAM BASS, OCCIDENTAL SYSTEMS,
*      HOUSTON, TEXAS (713) 840-2178
*   -  THE FUNCTION OF THIS PROGRAM IS TO CLEAR A 327X TYPE TERMINAL.
*   -  ASCII DEVICES ARE PROPERLY HANDLED TO ELIMINATE DISPLAY OF AN
*      UN-SUPPORTED DATA STREAM
*   -  IF THE TSO SESSION MANAGER IS ACTIVE, THE EFFECT OF THIS COMMAND
*      IS ESSENTIALLY A NO OPERATION TO ELIMINATE A SCREEN FLICKER
*
*   UPDATED 30JUN86, HOWARD M. DEAN, SYNTEX TECHNICAL SUPPORT
*
*   -  CHANGED ENTRY AND EXIT MACROS
*   -  USE SYSTEM MAPPING MACROS
*   -  SWITCH BACK TO 24BIT MODE FOR TPUT/STLINENO MACROS
*   -  THIS COMMAND IS ENTERED IN 31 BIT MODE, BUT SWITCHES TO 24BIT
*
*****
CLEAR    TITLE 'TSO UTILITY TO CLEAR A 327X TYPE TERMINAL DISPLAY'
CLEAR    CSECT                                                 HD JUN86
CLEAR    AMODE 31                                              HD JUN86
CLEAR    RMODE 24                                              HD JUN86
         SETREG 12,CODE=RENT,CPR=NO                            HD JUN86
         GTSIZE ,                  GET TERMINAL DIMENSIONS
         LTR   R0,R0               IS THIS A 327X TYPE TERMINAL?
         BP    DISPLAY             YES, GO ISSUE SCREEN CLEAR
         C     R1,=F'80'           IS THIS A ASCII TUBE?
         BE    TTY35SCR            YES, GO ISSUE SCREEN CLEAR
         B     EXIT                MUST BE HARDCOPY, GO EXIT
DISPLAY  DS    0H
         L     R2,PSAAOLD-PSA(R0)   GET ASCB ADDRESS
         L     R2,ASCBASXB-ASCB(R2) GET ASXB ADDRESS
         L     R2,ASXBLWA-ASXB(R2)  GET LWA ADDRESS
         LTR   R2,R2                DOES LWA EXIST?
         BZ    EXIT                 NO, OOPS
         L     R2,LWAXXXX-LWA(R2)   GET RDF ADDRESS
         LTR   R2,R2                DOES RDF EXIST?
         BZ    NONSMGR             NO, SESSION MANAGER NOT ACTIVE
         TM    RDFFLGS(R2),RDFFSCR TEST RDFFSCR BIT OF RDFFLAGS
         BZ    EXIT                EXIT IF SESSION MANAGER ACTIVE
NONSMGR  DS    0H
         SPACE 2                                               HD JUN86
         XABSM MODE=24             GET 24BIT MODE              HD JUN86
         STFSMODE ON,INITIAL=YES   SETUP SCREEN FOR CLEAR
         TPUT  DCLR,DCLRL,FULLSCR  ISSUE 327X TYPE SCREEN CLEAR
         STLINENO LINE=1,MODE=OFF  TURN OFF FULL SCREEN MODE
         XABSM MODE=31             GET 31BIT MODE              HD JUN86
         SPACE 2                                               HD JUN86
         B     EXIT                GO EXIT
TTY35SCR DS    0H
         SPACE 2                                               HD JUN86
         XABSM MODE=24             GET 24BIT MODE              HD JUN86
         TPUT  TCLR,TCLRL,ASIS     ISSUE ASCII TYPE SCREEN CLEAR
         XABSM MODE=31             GET 31BIT MODE              HD JUN86
         SPACE 2                                               HD JUN86
         B     EXIT                GO EXIT
EXIT     DS    0H
         EOJ RC=0
         EJECT
SPACE    DC    C' '                A SPACE CHARACTER
DCLR     DS    0H                  327X TYPE SCREEN CLEAR
*              WCC   SBA   R 24  C 79
         DC    X'40',X'11',X'5D',X'7E'
*              SBA   R 01  C 01
         DC    X'11',X'40',X'40'
*              SF    AID - UNPROTECTED,ALPHA,DISPLAY,NOT-MDFYD
         DC    X'1D',X'40'
*              IC
         DC    X'13'
*              RA    R 07  C 01  NULL
         DC    X'3C',X'C7',X'60',X'00'
*              RA    R 13  C 01  NULL
         DC    X'3C',X'4F',X'40',X'00'
*              RA    R 19  C 01  NULL
         DC    X'3C',X'D6',X'60',X'00'
*              RA    R 01  C 01  NULL
         DC    X'3C',X'40',X'40',X'00'
*              IC
         DC    X'13'
DCLRL    EQU   *-DCLR
TCLR     DC    23X'25'             ASCII TYPE SCREEN CLEAR
         DC    X'15'
TCLRL    EQU   *-TCLR
RDFFLGS  EQU   X'90'               OFFSET OF RDFFLGS IN RDFBLOCK
RDFFSCR  EQU   X'40'               SESSION MANAGER FULLSCREEN FLAG
         EJECT
         IHAPSA
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         IKJEFLWA
         END
