         MACRO
         GENC  &P1,&REL,&P2,&OP,&LABEL
         GBLC  &GTYPE
         LCLB  &TRUEB#
         LCLC  &WKC1
&TRUEB#  SETB  1
         AIF   ('&GTYPE' EQ 'WHILE' OR '&GTYPE' EQ 'IFOR').L05
         AIF   ('&GTYPE' EQ 'UNTILOR' OR '&GTYPE' EQ 'WHILEOR').L05
         AIF   ('&GTYPE' EQ 'DOWHLOR').L05
&TRUEB#  SETB  0
.L05     ANOP
         AIF   ('&OP' EQ '').L100
         AIF   ('&P1' EQ '' OR '&REL' EQ '' OR '&P2' EQ '').ER1
&WKC1    SETC  'BNE'
         AIF   ('&REL' EQ 'EQ' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'NE' AND &TRUEB# EQ 1).L10
&WKC1    SETC  'BNH'
         AIF   ('&REL' EQ 'GT' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'LE' AND &TRUEB# EQ 1).L10
&WKC1    SETC  'BNL'
         AIF   ('&REL' EQ 'LT' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'GE' AND &TRUEB# EQ 1).L10
&WKC1    SETC  'BE'
         AIF   ('&REL' EQ 'NE' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'EQ' AND &TRUEB# EQ 1).L10
&WKC1    SETC  'BL'
         AIF   ('&REL' EQ 'GE' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'LT' AND &TRUEB# EQ 1).L10
&WKC1    SETC  'BH'
         AIF   ('&REL' EQ 'LE' AND &TRUEB# EQ 0).L10
         AIF   ('&REL' EQ 'GT' AND &TRUEB# EQ 1).L10
         AGO   .ER2
.L10     ANOP
         AIF   ('&OP' EQ 'BCT').L40
         AIF   ('&OP' EQ 'BXLE').L50
         AIF   ('&OP' EQ 'BXH').L50
         AIF   ('&OP' EQ 'TM').L20
         AIF   ('&OP' EQ 'CLC').L30
         AIF   ('&OP' EQ 'CLI').L30
         AIF   ('&OP' EQ 'C').L30
         AIF   ('&OP' EQ 'CH').L30
         AIF   ('&OP' EQ 'CL').L30
         AIF   ('&OP' EQ 'CR').L30
         AIF   ('&OP' EQ 'CLR').L30
         AIF   ('&OP' EQ 'CP').L30
         AIF   ('&OP' EQ 'EX').L30
         AIF   ('&OP' EQ 'LTR').L70
         AGO   .ER3
.L20     ANOP
         AIF   ('&WKC1' EQ 'BNE').L25
         AIF   ('&WKC1' NE 'BE').ER2
&WKC1    SETC  'BO'
         AGO   .L30
.L25     ANOP
&WKC1    SETC  'BNO'
.L30     ANOP
         &OP   &P1,&P2
         &WKC1 &LABEL
         MEXIT
.L40     ANOP
         AIF   ('&WKC1' NE 'BNE').ER2
         AIF   ('&P2' NE '0').ER4
         BCT   &P1,&LABEL
         MEXIT
.L50     ANOP
         AIF   ('&OP' EQ 'BXLE' AND '&WKC1' NE 'BNH').ER2
         AIF   ('&OP' EQ 'BXH' AND '&WKC1' NE 'BH').ER2
         &OP   &P1,&P2,&LABEL
         MEXIT
.L70     ANOP
         AIF   ('&P2' NE '0').ER4
         &OP   &P1,&P1
         &WKC1 &LABEL
         MEXIT
.L100    ANOP
         AIF   ('&REL' NE '' OR '&P2' NE '').ER4
         AIF   (&TRUEB# EQ 1).L105
&WKC1    SETC  'BNE'
         AIF   ('&P1' EQ 'EQUAL').L110
&WKC1    SETC  'BE'
         AIF   ('&P1' EQ 'NOTEQUAL').L110
&WKC1    SETC  'BNL'
         AIF   ('&P1' EQ 'LOW').L110
&WKC1    SETC  'BL'
         AIF   ('&P1' EQ 'NOTLOW').L110
&WKC1    SETC  'BNH'
         AIF   ('&P1' EQ 'HIGH').L110
&WKC1    SETC  'BH'
         AIF   ('&P1' EQ 'NOTHIGH').L110
&WKC1    SETC  'BNZ'
         AIF   ('&P1' EQ 'ZERO').L110
         AIF   ('&P1' EQ 'ZEROS').L110
&WKC1    SETC  'BZ'
         AIF   ('&P1' EQ 'NOTZERO').L110
&WKC1    SETC  'BNO'
         AIF   ('&P1' EQ 'ONES').L110
         AIF   ('&P1' EQ 'OVERFLOW').L110
&WKC1    SETC  'BO'
         AIF   ('&P1' EQ 'NOTONES').L110
&WKC1    SETC  'BNP'
         AIF   ('&P1' EQ 'PLUS').L110
&WKC1    SETC  'BP'
         AIF   ('&P1' EQ 'NOTPLUS').L110
&WKC1    SETC  'BNM'
         AIF   ('&P1' EQ 'MINUS').L110
         AIF   ('&P1' EQ 'MIXED').L110
&WKC1    SETC  'BM'
         AIF   ('&P1' EQ 'NOTMINUS').L110
         AGO   .ER4
.L105    ANOP
&WKC1    SETC  'BE'
         AIF   ('&P1' EQ 'EQUAL').L110
&WKC1    SETC  'BNE'
         AIF   ('&P1' EQ 'NOTEQUAL').L110
&WKC1    SETC  'BL'
         AIF   ('&P1' EQ 'LOW').L110
&WKC1    SETC  'BNL'
         AIF   ('&P1' EQ 'NOTLOW').L110
&WKC1    SETC  'BH'
         AIF   ('&P1' EQ 'HIGH').L110
&WKC1    SETC  'BNH'
         AIF   ('&P1' EQ 'NOTHIGH').L110
&WKC1    SETC  'BZ'
         AIF   ('&P1' EQ 'ZERO').L110
         AIF   ('&P1' EQ 'ZEROS').L110
&WKC1    SETC  'BNZ'
         AIF   ('&P1' EQ 'NOTZERO').L110
&WKC1    SETC  'BO'
         AIF   ('&P1' EQ 'ONES').L110
         AIF   ('&P1' EQ 'OVERFLOW').L110
&WKC1    SETC  'BNO'
         AIF   ('&P1' EQ 'NOTONES').L110
&WKC1    SETC  'BP'
         AIF   ('&P1' EQ 'PLUS').L110
&WKC1    SETC  'BNP'
         AIF   ('&P1' EQ 'NOTPLUS').L110
&WKC1    SETC  'BM'
         AIF   ('&P1' EQ 'MINUS').L110
         AIF   ('&P1' EQ 'MIXED').L110
&WKC1    SETC  'BNM'
         AIF   ('&P1' EQ 'NOTMINUS').L110
         AGO   .ER4
.L110    ANOP
         &WKC1 &LABEL
         MEXIT
.ER1     MNOTE 8,'REQUIRED OPERAND MISSING'
         MEXIT
.ER2     MNOTE 8,'INVALID RELATION'
         MEXIT
.ER3     MNOTE 8,'INVALID OPCODE'
         MEXIT
.ER4     MNOTE 8,'INVALID OPERAND'
         MEND
