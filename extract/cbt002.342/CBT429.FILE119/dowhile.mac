         MACRO
         DOWHILE &OPND1,&RELA,&OPND2,&OPCD
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GDOCNT,&GDOLVL,&GIFLVL,&GDOSTK(16),&GDOIFST(16),&GDWCNT
         GBLB  &DWORSW#
         LCLC  &LOC
         LCLC  &LBLYSS
         LCLA  &I,&J,&K                          'AND' LOOP COUNTERS
.*
         SEQCK DOWHILE
.*
         SPACE
         AIF   (&DWORSW# EQ 1).L10
&GDOCNT  SETA  &GDOCNT+2
.*
&LBLYSS  SETC  'ZLDO&GDOCNT'                   TO GET OPCODE IN COL 10
&LBLYSS  DS    0H                                LOOP START
.*
&GDOCNT  SETA  &GDOCNT+1
&GDOLVL  SETA  &GDOLVL+1
&GDOSTK(&GDOLVL) SETA &GDOCNT
&GDOIFST(&GDOLVL) SETA &GIFLVL
.*
.L10     ANOP
&GDOCNT  SETA  &GDOCNT+1
&LOC     SETC  'ZLDO'.'&GDOCNT'
&GDOCNT  SETA  &GDOCNT-1
.*
.**********************************************************************
.*       GENERATE CODE TO TEST CONDITION AND BRANCH                   *
.**********************************************************************
.*
&J       SETA  0                                 FIRST DELIMITER NUMBER
&I       SETA  1                                 FIRST PARM NUMBER
.*
.LOOPAND ANOP                                    LOOP THRU PARMS
&J       SETA  &J+1                        TRY NEXT PARM AS DELIMITER
         AIF   (&J GT N'&SYSLIST).ENDAND         IS IT END OF PARMS ?
         AIF   ('&SYSLIST(&J)' NE '*AND*').LOOPAND  IS IT DELIMITER ?
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN 2 DELIMITERS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND1       MUST BE 4 OR 1 PARM
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND1  ANOP                                    GEN. TEST AND BRANCH
         AIF   (&K EQ 1).GENOP1                  DO WE HAVE 1 PARM ?
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
         AGO   .GENOK
.GENOP1  ANOP                                    GEN CODE WITH 1 PARM
         GENC  &SYSLIST(&I),,,,&LOC
.GENOK   ANOP                                    GEN IS DONE
.*
&I       SETA  &J+1                  SET POINTER TO NEXT SET OF PARMS
         AGO   .LOOPAND                          REPEAT LOOP
.*
.ENDAND  ANOP                                    END OF LOOP
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN LAST DELIMTRS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND2       MUST BE 4 OR 1
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND2  ANOP                                  GEN LAST TEST & BRANCH
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
.*
.**********************************************************************
.*       ANALIZE 'OR' CONDITION                                       *
.**********************************************************************
.*
         AIF   (&DWORSW# EQ 0).CODE020         IF NO DOWHLOR,SKIP NEXT
&LBLYSS  SETC  'ZLDO&GDOCNT'                   TO GET OPCODE IN COL 10
&LBLYSS  DS    0H                                BODY OF LOOP FOLLOWS
.CODE020 ANOP
.*
&GDWCNT  SETA  &GDWCNT+1
&DWORSW# SETB  0
         SPACE
         MEND
