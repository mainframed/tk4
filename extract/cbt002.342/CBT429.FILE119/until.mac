         MACRO
         UNTIL &OPND1,&RELA,&OPND2,&OPCD
.*
.**********************************************************************
.*       DEFINE VARIABLES                                             *
.**********************************************************************
.*
         GBLA  &GDOCNT,&GDOLVL,&GIFLVL,&GDOSTK(16),&GDOIFST(16)
         GBLB  &UNORSW#                       'UNTILOR' INDICATOR
         LCLC  &LOC
         LCLC  &LBLYSS
         LCLA  &I,&J,&K                          'AND' LOOP COUNTERS
.*
         SEQCK UNTIL
.*
.**********************************************************************
.*       CHECK INPUT PARAMETERS                                       *
.**********************************************************************
.*
         SPACE
         AIF   (&GDOLVL EQ 0).ER1
         AIF   (&GDOIFST(&GDOLVL) EQ &GIFLVL).L10
         MNOTE 8,'ENDIF STATEMENT(S) MISSING WITHIN DO LOOP'
.L10     ANOP
.*
.**********************************************************************
.*       GENERATE CODE TO TEST CONDITION AND BRANCH                   *
.**********************************************************************
.*
&GDOSTK(&GDOLVL) SETA &GDOSTK(&GDOLVL)-1
&LOC     SETC  'ZLDO'.'&GDOSTK(&GDOLVL)'
.*
&J       SETA  0                                 FIRST DELIMITER NUMBER
&I       SETA  1                                 FIRST PARM NUMBER
.*
.LOOPAND ANOP                                    LOOP THRU PARMS
&J       SETA  &J+1                        TRY NEXT PARM AS DELIMITER
         AIF   (&J GT N'&SYSLIST).ENDAND         IS IT END OF PARMS ?
         AIF   ('&SYSLIST(&J)' NE '*AND*').LOOPAND  IS IT DELIMITER ?
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN 2 DELIMITERS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND1       MUST BE 4 OR 1 PARM
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND1  ANOP                                    GEN. TEST AND BRANCH
         AIF   (&K EQ 1).GENOP1                  DO WE HAVE 1 PARM ?
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
         AGO   .GENOK
.GENOP1  ANOP                                    GEN CODE WITH 1 PARM
         GENC  &SYSLIST(&I),,,,&LOC
.GENOK   ANOP                                    GEN IS DONE
.*
&I       SETA  &J+1                  SET POINTER TO NEXT SET OF PARMS
         AGO   .LOOPAND                          REPEAT LOOP
.*
.ENDAND  ANOP                                    END OF LOOP
&K       SETA  &J-&I       * SET NUMBER OF PARMS BETWEEN LAST DELIMTRS
         AIF   (&K EQ 4 OR &K EQ 1).OKAND2       MUST BE 4 OR 1
         MNOTE 8,'REQUIRED OPERAND INVALID OR MISSING'
         MEXIT
.*
.OKAND2  ANOP                                  GEN LAST TEST & BRANCH
         GENC  &SYSLIST(&I),&SYSLIST(&I+1),&SYSLIST(&I+2),             X
               &SYSLIST(&I+3),&LOC
.*
&GDOSTK(&GDOLVL) SETA &GDOSTK(&GDOLVL)+2
.*
.**********************************************************************
.*       ANALIZE 'OR' CONDITION                                       *
.**********************************************************************
.*
         AIF   (&UNORSW# EQ 0).CODE020        IF NO UNTILOR,SKIP NEXT
&LBLYSS  SETC  'ZLDO&GDOSTK(&GDOLVL)'          TO GET OPCODE IN COL 10
&LBLYSS  DS    0H                                LOOP END
.*
.CODE020 ANOP
&UNORSW# SETB  0
&GDOLVL  SETA  &GDOLVL-1
         SPACE
         MEXIT
.ER1     MNOTE 8,'UNTIL NOT PRECEDED BY DO'
&UNORSW# SETB  0
         SPACE
         MEND
