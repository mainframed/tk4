PROC 1 DSNAME DEBUG
/* ***************************************************************** */
/*                                                                   */
/* "BROWSE" RECURSIVE CLIST                                          */
/*  WRITTEN 13MAR86, WILLIAM SMITH, SYNTEX TECHNICAL SUPPORT         */
/*    . SIMPLE RECURSIVE CLIST TO DIRECTLY INVOKE THE PDF BROWSE     */
/*      SERVICE FROM WITHIN ISPF OR FROM READY MODE TSO              */
/*    . EXISTENCE OF DATA SET IS VERIFIED PRIOR TO EDIT ENTRY        */
/*                                                                   */
/* ***************************************************************** */
IF &DEBUG = DEBUG THEN -
   CONTROL MSG LIST CONLIST SYMLIST
ELSE -
   CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST
IF &SYSDSN(&DSNAME.) = OK THEN -
   DO
     IF &SYSISPF = ACTIVE THEN -
        DO
          ISPEXEC BROWSE DATASET(&DSNAME.)
          EXIT
        END
     ELSE -
        ISPSTART CMD(%B &DSNAME. &DEBUG)
   END
ELSE -
   DO
     CLR3270
     WRITE ***** &DSNAME. DOES NOT EXIST... TRY AGAIN *****
     EXIT
   END
