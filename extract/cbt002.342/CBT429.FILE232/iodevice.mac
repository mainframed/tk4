         MACRO
&L       IODEVICE &UNIT=,&CUNUMBR=,&ADDRESS=,&MODEL=,&FEATURE=,&TCU=,  *
               &ADAPTER=,&OPTCHAN=
.* IODEVICE: MACRO FOR MVS IOGEN ==> HCPRIO
.* AUTHOR:   JOE VEILLEUX
.* HISTORY:  15NOV85-JJV-1.00-ORIGINAL RELEASE
.*           16APR86-JJV-1.01-PROCESS 3791L, SKIP
.*              UNIT=DUMMY DEVICES, ENHANCE TAPE (DUALDENS), ENHANCE
.*              3270 (CORRECT MODEL), GENERATE DASD "SHARE=YES"
.*              CORRECTLY, ADD ADAPTER/TYPE TO 3705 (HARDCODED),
.*              PROCESS MODEL AND OPTIONAL FEATURES ON 3800
.* USAGE:    THIS MACRO REFORMATS THE MVS "IODEVICE" INFORMATION INTO
.*           VM/XA SF "RDEVICE" MACROS. OUTPUT IS VIA ASSEMBLER "PUNCH"
.*           STATEMENTS.
.* LIMITS:   (1) SOME REQUIRED INFORMATION FOR RDEVICE MACROS IS NOT ON
.*           THE MVS IODEVICE MACROS. IN SOME CASES, REASONABLE
.*           DEFAULTS (FOR MY ENVIRONMENT ONLY!!) HAVE BEEN HARDCODED
.*           BELOW. FOR EXAMPLES, SEE 3800 AND 3705 SECTIONS.
.*           (2) NO ATTEMPT IS MADE TO RETAIN CONTROL UNIT INFO FOR USE
.*           IN THIS MACRO, SO DASD CONTROL UNITS SUCH AS 3880-23 THAT
.*           MUST BE MENTIONED ON RDEVICE DO NOT GET CODED CORRECTLY.
.*           YOU MUST DO THESE BY HAND.
         LCLC  &OPT,&DEVT,&FLAG,&M
         LCLB  &FFLAG
         LCLA  &N
&DEVT    SETC  '&UNIT'
.* 1403 OR 3211 PRINTERS:
         AIF   (('&UNIT' NE '1403') AND ('&UNIT' NE '3211')).C1403
         AIF   ('&FEATURE' EQ '').S14031
&OPT     SETC  ',FEATURE=&FEATURE'
.S14031  AGO   .RDEV
.C1403   ANOP
.* 3800 PRINTER:
         AIF   ('&UNIT' NE '3800').C3800
&OPT     SETC  ',MODEL=&MODEL'
         AIF   ('&MODEL' EQ '3').S38001
.* ADD CHARS/FCB/FORM DEFAULTS.. NOTE: CAN'T DO THIS ON MODEL=3
&OPT     SETC  ',MODEL=1,CHARS=GF12,FCB=8,FORM=*'
.S38001  AIF   ('&FEATURE' NE 'CGS4').S38002
&OPT     SETC  '&OPT,FEATURE=4WCGMS'
.S38002  AGO   .RDEV
.C3800   ANOP
.* CARD READERS/PUNCHES:
         AIF   (('&UNIT' NE '2501') AND ('&UNIT' NE '2540R') AND       *
               ('&UNIT' NE '3505') AND ('&UNIT' NE '2540P') AND        *
               ('&UNIT' NE '3525')).C2501
         AGO   .RDEV
.C2501   ANOP
.* 3277/3278/3279 TERMINALS:
         AIF   (('&UNIT' NE '3277') AND ('&UNIT' NE '3278') AND        *
               ('&UNIT' NE '3279')).C3270
         AIF   ('&MODEL' EQ '').S32703
&M       SETC  '&MODEL'
.* CHECK FOR ALLOWABLE MODEL NUMBERS, CHANGE IF NECESSARY
         AIF   ('&UNIT' NE '3277').S3277
         AIF   ('&MODEL' EQ '2').S32702
&M       SETC  '2'
         AGO   .S32701
.S3277   AIF   ('&UNIT' NE '3278').S3278
         AIF   (('&MODEL' EQ '2') OR ('&MODEL' EQ '2A') OR             *
               ('&MODEL' EQ '3') OR ('&MODEL' EQ '4')).S32702
&M       SETC  '2'
         AGO   .S32701
.S3278   ANOP  * 3279'S:
         AIF   (('&MODEL' EQ '2') OR ('&MODEL' EQ '2C') OR             *
               ('&MODEL' EQ '3')).S32702
&M       SETC  '3'
         AGO   .S32701
.S32701  MNOTE 4,'MODEL NUMBER "&MODEL" WILL BE CHANGED TO "&M".'
.S32702  ANOP
&OPT     SETC  ',MODEL=&M'
.* NOTE NO SUPPORT HERE FOR FEATURE=OPRDR
.S32703  AGO   .RDEV
.C3270   ANOP
.* 328X PRINTERS:
         AIF   ('&UNIT'(1,3) NE '328').C328X
         AGO   .RDEV
.C328X   ANOP
.* 3420/3430 TAPE DRIVES:
         AIF   (('&UNIT' NE '3420' AND '&UNIT' NE '3430')).C3420
&FFLAG   SETB  0
         AIF   ('&MODEL' EQ '').S34201
&OPT     SETC  ',MODEL=&MODEL'
.S34201  AIF   ('&FEATURE' EQ '').S34204
&N       SETA  1
.S34202  AIF   ('&FEATURE(&N)' NE 'DUALDENS').S34203
&FFLAG   SETB  1
.S34203  ANOP
&N       SETA  &N+1
         AIF   (&N LE N'&FEATURE).S34202
.S34204  ANOP
.* IF DUALDENS SPECIFIED OR IF IT'S A MODEL 8 SET FEATURE=DUALDENS
         AIF   ((&FFLAG NE 1) AND ('&MODEL' NE '8')).S34205
&OPT     SETC  '&OPT,FEATURE=DUALDENS'
.S34205  ANOP
         AGO   .RDEV
.C3420   ANOP
.* DASD: 2305, 3330, 3350, 3380
         AIF   (('&UNIT' NE '2305' AND '&UNIT' NE '3330') AND          *
               ('&UNIT' NE '3350' AND '&UNIT' NE '3380')).C2305
         AIF   ('&MODEL' EQ '').S23051
&OPT     SETC  ',MODEL=&MODEL'
.S23051  AIF   ('&FEATURE' EQ '').S23054
&N       SETA  1
.S23052  AIF   ('&FEATURE(&N)' NE 'SHARED').S23053
&OPT     SETC  '&OPT,SHARED=YES'
.S23053  ANOP
&N       SETA  &N+1
         AIF   (&N LE N'&FEATURE).S23052
.S23054  AGO   .RDEV
.C2305   ANOP
.* 2701 DATA ADAPTER:
         AIF   ('&TCU' NE '2701').C2701
&DEVT    SETC  '2701'
         AIF   ('&ADAPTER' EQ '').S27011
&OPT     SETC  ',ADAPTER=&ADAPTER'
.S27011  AGO   .RDEV
.C2701   ANOP
.* 3704/3705/3725 COMMUNICATIONS CONTROLLERS:
         AIF   (('&UNIT' NE '3704' AND '&UNIT' NE '3705') AND          *
               ('&UNIT' NE '3725')).C3704
.* ADAPTER AND MODEL ARE NOT AVAILABLE BUT ARE REQUIRED... SUPPLY
.* HARDCODED VALUES
&OPT     SETC  ',ADAPTER=TYPE4,MODEL=2'
         AIF   ('&UNIT' NE '3725').S37041
&DEVT    SETC  '3705'
.S37041  AGO   .RDEV
.C3704   ANOP
.* 3791L LOCAL SNA CONTROL UNIT:
         AIF   ('&UNIT' NE '3791L').C3791L
&DEVT    SETC  '3791L'
&FLAG    SETC  '**UNSUPPORTED**'
&OPT     SETC  ',CLASS=DISPLAY'
         AGO   .RDEV
.C3791L  ANOP
.* UNIT=DUMMY... IGNORE
         AIF   ('&UNIT' NE 'DUMMY').CDUMMY
* IODEVICE MACRO WITH UNIT=DUMMY WAS IGNORED
         MEXIT
.CDUMMY  ANOP
.* UNKNOWN DEVICE TYPE
         MNOTE 4,'UNKNOWN DEVICE TYPE "&UNIT"'
&FLAG    SETC  '**UNKNOWN**'
.* PUNCH RDEVICE MACRO FOR THIS DEVICE
.RDEV    ANOP
         PUNCH ' RDEVICE DEVNO=&ADDRESS,DEVTYPE=&DEVT.&OPT &FLAG'
         MEND
