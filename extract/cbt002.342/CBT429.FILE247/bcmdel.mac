BCMDEL   TITLE 'BCMDEL - DRAIN BROADCAST MESSAGES FOR A USERID.'
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  DOCUMENTATION:   THIS PROGRAM IS MEANT TO BE RUN AS A TSO-IN-BATCH *
*                   COMMAND.   MUST BE RUN AUTHORIZED.                *
*                                                                     *
*         SYNTAX:   BCMDEL USERID                                     *
*                                                                     *
*       FUNCTION:   TO DISPLAY AND DELETE UN-SENT BROADCAST MESSAGES  *
*                   FOR A TSO USERID.  USUALLY USED TO CLEAR MESSAGES *
*                   FROM ID'S WHICH CAN'T BE LOGGED ON TO.            *
*                                                                     *
*          AUTHOR:  SAM GOLOB               VERSION:   1.2            *
*                                                                     *
*            DATE:  MAY 19, 95                                        *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
SP000    EQU   0
         SPACE 1
BCMDEL   CSECT
         USING BCMDEL,R12
         B     BEGINN-*(,R15)
         DC    AL1(16),CL16'BCMDEL VER 1.2'
         DC    CL16' &SYSDATE &SYSTIME '
         DS    0H
BEGINN   STM   R14,R12,12(R13)
         LR    R12,R15
         LR    R11,R1              PRESERVE CPPL POINTER.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         SPACE 1
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R10,12(,R1)         POINT TO ASCB.
         L     R9,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R9)       POINT TO JSCB.
         L     R2,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R2)           CLEAR HIGH BYTE.
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                GET OUT.  NOT TSO.
         USING PSCB,R2
CHEKAUTH DS    0H                                            PARS 06/99
         B     CONT0          BYPASS AUTHORIZATION           PARS 06/99
         TM    PSCBATR1,PSCBACCT   ACCT USER?
         BO    CONT0               YES.  CONTINUE.
         TM    PSCBATR1,PSCBCTRL   OPER USER?
         BZ    EOJ4                NO.  GET OUT.
CONT0    LTR   R11,R11             ANY CPPL?
         BZ    EOJ8                NO.  GET OUT
         L     R1,0(,R11)          PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    EOJ4                GET OUT IF NONE
         LH    R3,0(,R1)           LOAD LENGTH.
         CH    R3,=H'4'            ANYTHING?
         BNH   EOJ4                NO.  GET OUT.
         LR    R5,R3               LENGTH OF COMMAND + 4
         ICM   R5,14,FULLZERO      CLEAR HI ORDER 3 BYTES
*        ST    R1,CMBUFAD          STORE COMMAND BUFFER ADDRESS
*        L     R1,CMBUFAD          RELOAD COMMAND BUFFER ADDRESS
         LH    R3,2(,R1)           LOAD OFFSET.
         SR    R5,R3               SUBTRACT OFFSET FROM FULL LENGTH+4
         SH    R5,=H'4'            CORRECT FOR LENGTH OF CMDBUFR HEADER
         BC    8,NOUSERID          ZERO. MESSAGE
         BC    5,EOJ8              LESS THAN ZERO OR OVERFLOW, OUT.
         AR    R1,R3               POINT ...
         LA    R1,4(,R1)           ... TO CHAR.
         LA    R6,7                LOAD REG FOR BCT LOOP
         LR    R7,R1               INITIALIZE POINTER
         LA    R7,0(,R7)                TO FIRST CHARACTER OF ID
         LA    R8,0                INITIALIZE CHARACTER COUNT
CUTIDLEN CLI   0(R7),X'40'         IS THIS CHARACTER A BLANK?
         BE    NOMORCUT            YES, WE ARE AT THE END OF THE ID.
         LA    R7,1(,R7)           BUMP ANOTHER CHARACTER
         LA    R8,1(,R8)           COUNT ANOTHER CHARACTER
         CR    R8,R5               END OF COMMAND BUFFER ?
         BNL   NOMORCUT            YES. GET OUT NOW.
         BCT   R6,CUTIDLEN         TRY FOR BLANK AGAIN
NOMORCUT DS    0H
         MVC   NEWID(8),BLANKS     CLEAR FIELD
         BCTR  R8,0                SUBTRACT 1 FOR EXECUTE
         EX    R8,MOVEID           MAKE SUBCOMMAND INTO NEW ID
         OC    NEWID(8),BLANKS     UPPERCASE IT.
         MVC   OLDID(8),BLANKS     CLEAR FIELD
         MVC   OLDID(7),PSCBUSER   SAVE ORIGINAL USERID
         MVI   OLDPSCBL,X'00'      INITIALIZE FIELD
         MVC   OLDPSCBL(1),PSCBUSRL  SAVE ORIGINAL USERID LENGTH
KZ       MODESET KEY=ZERO
BATCHCNT MVC   PSCBUSER(7),NEWID   SET USERID TO PARAMETER VALUE
         LA    R8,1(,R8)           BUMP REG BACK TO WHERE IT WAS
         STC   R8,PSCBUSRL         PUT USERID LENGTH INTO PSCB
         MVC   0(4,R11),=A(BPARM)  MOVE PARM POINTER TO CPPL.
         LR    R1,R11              POINT R1 TO CPPL.
         LINK  EP=LISTBC           DO LISTBC COMMAND.
         MVC   PSCBUSER(7),OLDID   RESTORE OLD USERID TO PSCB
         MVC   PSCBUSRL(1),OLDPSCBL  RESTORE SAVED USERID LENGTH
NZ       MODESET KEY=NZERO
         B     EOJ0                NORMAL END
NOUSERID LA    R15,0                                         PARS 06/99
         TPUT  MESSAGE1,L'MESSAGE1                           PARS 06/99
EOJ4     LA    R15,4               SET CC = 4.
EOJ      DS    0H                  BYE.
         LR    R1,R13
         L     R13,4(,R13)
         ST    R15,16(,R13)        PUT RC INTO PASSED SAVE AREA
         L     R14,12(,R13)
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LM    R0,R12,20(R13)
         BR    R14
EOJ0     SR    R15,R15             SET CC = 0.
         B     EOJ                 CONT.
EOJ8     LA    R15,8               SET CC = 8.
         B     EOJ                 CONT
         EJECT
FULLZERO DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - *
MOVEID   MVC   NEWID(*-*),0(R1)    EXECUTED
* - - - - - - - - - - - - - - - - - - - - - - - - *
BLANKS   DC    C'        '         8 BLANKS
BPARM    DC    AL2(BPARME-BPARM,0),C'NON'
BPARME   EQU   *
MESSAGE1 DC    C'PLEASE ENTER THE USERID NAME AFTER COMMAND NAME'
         SPACE 3
         LTORG
         SPACE 3
DATAAREA DSECT
SAVE     DS    9D
CMBUFAD  DS    F                   COMMAND BUFFER ADDRESS
NEWID    DC    C'        '         STORE NEW USERID HERE
         DS    CL72                PADDING
OLDID    DC    C'        '         STORE OLD USERID HERE
OLDPSCBL DC    X'00'               STORE OLD PSCBUSRL HERE
         DS    0F                  ALIGN ON FULLWORD
         SPACE 1
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJUPT
         IKJPSCB
         END
