*      M Y I D   P R O G R A M    -    A   T S O   C O M M A N D
*
*      TSO COMMAND PROCESSOR TO DISPLAY THE USERID OF THE INVOKER.
*
*        REGISTER EQUATES
         YREGS                ,    IBM'S REGISTER EQUATE MACRO
SP000    EQU   0                   DEFINE SUBPOOL TO BE 0
*
MYID     CSECT
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS R14 THRU R12
         LR    R12,R15             LOAD ENTRY POINT INTO BASE REGISTER
         USING MYID,R12            TELL THE ASSEMBLER, R12 IS THE BASE
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
*  The address of the obtained storage is placed into Register 1.
         ST    R13,4(,R1)          SAVE CALLER'S SAVEAREA ADDRESS
         ST    R1,8(,R13)          STORE OUR SAVEAREA ADDRESS IN HIS
         LR    R13,R1              POINT REGISTER 13 TO OUR SAVE AREA
         USING SAVEAREA,R13        TELL ASSEMBLER
RUNCHAIN L     R3,16               POINT TO CVT. ADDR IS IN LOW STORAGE
         USING CVT,R3
         L     R3,CVTTCBP          POINT TO TCB/ASCB WORDS, "0" OFF CVT
         L     R3,4(,R3)           POINT TO TCB, "4" OFF TCB/ASCB WORDS
         DROP  R3
         USING TCB,R3
         L     R3,TCBJSCB          POINT TO JSCB. X'B4' OFF CURRENT TCB
         DROP  R3
         USING IEZJSCB,R3
         L     R3,JSCBPSCB         POINT TO PSCB. X'108' OFF THE JSCB
         DROP  R3
         USING PSCB,R3
         MVC   MESSAGE(20),MSGLINE        MOVE TEXT TO VARIABLE AREA
         MVC   MESSAGE+13(7),PSCBUSER     MOVE MY USERID INTO MESSAGE
         DROP  R3
         TPUT  MESSAGE,L'MESSAGE  DISPLAY THE WHOLE MESSAGE ON THE TUBE
RETURN   DS    0H
         LR    R1,R13              SET UP FOR SAVEAREA FREEMAIN
         L     R13,4(,R13)         POINT TO CALLER'S SAVEAREA
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LM    R14,R12,12(R13)     RELOAD THE CALLER'S REGISTERS
         BR    R14                 RETURN TO CALLER
MSGLINE  DC    CL20'MY USERID IS        '      CONSTANT PART OF MESSAGE
*
SAVEAREA DSECT
         DS    18F                 DEFINE MY SAVEAREA - 18 FULLWORDS
MESSAGE  DS    CL20                VARIABLE MESSAGE AREA
         DS    0D                  ALIGN ON DOUBLEWORD
DATALEN  EQU   *-SAVEAREA          DEFINE LENGTH OF VARIABLE STORAGE
*
         CVT   DSECT=YES           CVT mapping macro
         IKJTCB                    TCB mapping macro
         IEZJSCB                   JSCB mapping macro
         IKJPSCB                   PSCB mapping macro
         END
