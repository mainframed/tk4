//XKZS015N JOB (SD,A,SPM000,DZK),'SMF TYPE 32',
//          CLASS=M,MSGCLASS=T,NOTIFY=XKZS,TIME=(20,0)
/*JOBPARM R=131
/*SETUP DSN=WB.A.ABL001T1(-1)
//STEP1     EXEC  SAS,OPTIONS='TIME=MAX'
//*SMFIN    DD DSN=WB.A.ABL010D1,DISP=SHR
//*SMFIN    DD DSN=XKZS.ABLDATA.WEEK1,DISP=SHR
//SMFIN    DD DSN=WB.A.ABL001T1(-1),DISP=SHR
//SYSIN    DD   *
  DATA SASIN ;
  INFILE SMFIN CLOSE=FREE MISSOVER;
  KEEP  TTIME TDATE USERID CMDNAME NOTIME TCBI SRBI TRANCNT EXCPCNT
         TERMIO CPUTIME ;
  INPUT @2 RECNO PIB1. @3 TIMEDATE SMFSTAMP8. @43 NOCMD PIB2. @;
  IF RECNO = 32 AND NOCMD >= 2 THEN DO;
    TTIME = TIMEPART(TIMEDATE) ;
    TDATE = DATEPART(TIMEDATE) ;
    INPUT @57 USERID $CHAR8. @221 CMDNAME $CHAR8. @229 NOTIME PIB4.
          @233 TCBI PIB4.2 @237 SRBI PIB4.2 @241 TGET PIB4.
          @245 TPUT PIB4. @;
          TERMIO = TGET + TPUT;
          CPUTIME = TCBI + SRBI ;
    INPUT @249 TRANCNT PIB4. @253 EXCPCNT PIB4. ;
    IF 'DYNA' = SUBSTR(CMDNAME,1,4) THEN OUTPUT;
  END;
  RETURN;
*______________________________________________________________________*
*                                                                      *
*         RECORD TYPE 32  : TSO/E WORK ACCOUNTING RECORD               *
*______________________________________________________________________;
  PROC SORT DATA=SASIN ;
       BY USERID TDATE TTIME CMDNAME ;
  TITLE 'DYNASOFT USAGE DETAIL REPORT' ;
  PROC PRINT DATA=SASIN LABEL UNIFORM N;
   LABEL TDATE='DATE' TTIME='TIME' CMDNAME='COMMAND'
         TCBI='TCB TIME' SRBI='SRB TIME' EXCPCNT='# EXCPS'
         TERMIO='TERM I/O' NOTIME='TIMES EXECUTED' ;
   BY USERID;
   SUM TCBI SRBI EXCPCNT TERMIO NOTIME ;
   FORMAT TCBI SRBI COMMA8.2 ;
   FORMAT TDATE MMDDYY8. TTIME TIME. ;
   ID TDATE TTIME USERID CMDNAME ;
   VAR TCBI SRBI EXCPCNT TERMIO NOTIME ;
  PROC SUMMARY DATA=SASIN ;
   CLASS USERID ;
   VAR TCBI SRBI CPUTIME EXCPCNT TERMIO NOTIME ;
   OUTPUT OUT=TSUM SUM= ;
  TITLE 'DYNASOFT USAGE SUMMARY REPORT' ;
  PROC PRINT DATA=TSUM LABEL UNIFORM N;
   LABEL CMDNAME='COMMAND' TCBI='TCB TIME' SRBI='SRB TIME'
         CPUTIME='CPU TIME' EXCPCNT='# EXCPS'
         TERMIO='TERM I/O' NOTIME='TIMES EXECUTED' ;
   FORMAT CPUTIME TCBI SRBI COMMA8.2 ;
   ID USERID ;
   VAR CPUTIME TCBI SRBI EXCPCNT TERMIO NOTIME ;
/*
