*          DATA SET JES2XIT4V9 AT LEVEL 014 AS OF 11/16/87
*          DATA SET JES2XIT4V8 AT LEVEL 011 AS OF 06/05/85
        TITLE 'JES SETUP CARD TRANSLATION'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    FIX TO CHECK FOR GENERATED SYSIN DD * TO PREVENT LOOP YARF *
*    XRRN  THIS EXIT WAS CHANGED TO SUPPORT JES2 2.1.5.         *
*    XKZS  THIS EXIT WAS CHANGED TO SUPPORT JES2 1.3.4.         *
*                                            HJE2330            *
*                                                               *
*    PURPOSE:                                                   *
*     TO FACILATE THE USE OF THE SETUP CARD BY TRANSLATING      *
*     ANY "DSN=XXXX" TO  THE ACTUAL VOL=SER THAT THE DSN IS     *
*     ON AND REPLACING THAT IN THE JES STATEMENT. IF THE DATA   *
*     SET NAME IS NOT CATALOGED 'NOT CATLG' WILL BE PUT IN      *
*     COLS 63-71.                                               *
*                                                               *
*     MODIFIED TO HANDLE SETUPS FOR CIS JOBS                    *
*                                                               *
*     MODIFIED TO INSERT /*ROUTE CARDS FOR CIS                  *
*                                                               *
*     MODIFIED TO INSERT //JOBLIB CARDS FOR CIS                 *
*                                                               *
*     MODIFIED TO PREVENT S0C4 ISSUING TMSSVC                   *
*                                                               *
*     ENTRY POINT = UEXIT4                                      *
*                                                               *
*     MODULE NAME = $XIT4                                       *
*                                                               *
*     INPUT (REGISTERED):                                       *
*       R0    =0 JES2 COMMAND                                   *
*             =4 JCL STATEMENT                                  *
*       R1    POINTER TO 3WORD PERAMETER LIST                   *
*             WORD 1 (+0) ADDRESS OF CONTROL STATEMENT          *
*                         IMAGE BUFFER                          *
*             WORD 2 (+4) ADDRESS OF EXIT FLAG BYTE             *
*             WORD 3 (+8) ADDRESS OF JCTXWRK FIELD IN JCT       *
*       R2-9  N/A                                               *
*       R10   ADDRESS OF JCT                                    *
*       R11   ADDRESS OF HCT                                    *
*       R12   BASE REGISTURE                                    *
*       R13   ADDRESS OF PCE                                    *
*       R14   RETURN ADDRESS                                    *
*       R15   ENTRY ADDRESS                                     *
*                                                               *
*     RETURN (REGISTERS):                                       *
*       R0-14 N/A                                               *
*       R15   RETURN CODE                                       *
*             =0  PROCESS NEXT EXIT OR RETURN                   *
*             =4  IGNORE NEXT EXIT                              *
*             =8  IGNORE HASPRCCS PROCESSING                    *
*             =12 CANCEL JOB                                    *
*             =16 PURGE THE JOB                                 *
*                                                               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT OFF
         COPY  $HASPGBL       COPY JES GLOBAL PARMS
         PRINT ON,NOGEN
**   CREATE THE MIT TABLE  **
$XIT4   $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                   X
               $HASPEQU,                                               X
               $BUFFER,                                                X
               $RDRWORK,                                               X
               $JCT,                                                   X
               $JQE,                                                   X
               $PCE,                                                   X
               $CAT,                                                   X
               $HCT,                                                   X
               $MIT,                                                   X
               RPL
         TITLE 'JES SETUP CARD TRANSLATION'
         PRINT ON,GEN,NODATA
UEXIT4  $ENTRY BASE=R12          EXIT ROUTINE ENTRY POINT
        $SAVE
         LR    R12,R15                     LOAD BASE REGISTER
         SLR   R15,R15                     CLEAR R15
         LR    R7,R1               SAVE ADDRESS PARAMETERS
         LR    R9,R0               SAVE CARD TYPE INDICATOR
         L     R4,0(R7)
         USING JCT,R10
*        MVC   #CARD,0(R4)
*        WTO   MF=(E,WTOLIST)
         BAL   R14,#CHKJOB
         LTR   R15,R15             Q. CIS JOB
         BNZ   CHKSETUP            A. NOT ZERO - NO
         CLC   =C'//*',0(R4)       Q. COMMENT
         BE    #EXIT               A. EQUAL - YES
         CLC   =C'//SYSIN',0(R4)   Q. GENERATERED SYSIN
         BE    #EXIT               A. EQUAL - YES
         TM    JCTUSER0+1,HOLDBIT  Q. STILL NEED TO GO THERE
         BZ    CARDSCAN            A. ZERO - YES
CHKSETUP DS    0H
         LTR   R9,R9               Q. JES2 CONTROL STATEMENT
         BNZ   #EXIT               A. NOT ZERO - NO
         L     R2,0(R7)            LOAD RECORD TO WORK WITH
         L     R4,0(R7)            LOAD RECORD START ADDRESS
         CLC   SETUP,0(R4)         Q. JES2 CARD
         BNE   #EXIT               A. NOT EQUAL - NO
         LA    R3,25
BGNSRC   DS    0H
         CLC   #KEYWORD,0(R2)      Q.IS IT HERE
         BE    GOON                A. YES
         LA    R2,1(R2)            INCREMENT
         BCT   R3,BGNSRC              ONLY 25 TIMES
         B     #MOVECLS            RETURN
GOON     CLI   4(R2),C' '          Q. ANY DSNAME THERE
         BE    #MOVECLS            A. EQUAL - NO
         MVC   #DSNAME,BLANKS      CLEAR TO BLANKS
         LA    R1,5(R2)            SET START OF SCAN
         LA    R5,4(R2)            SAVE ADDRESS OF START OF DSN
         LA    R3,44               SET MAX SCAN
DSNLOOP  DS    0H                  FIND FIRST BLANK
         CLI   0(R1),C' '          Q. BLANK HERE
         BE    FOUND               A. EQUAL - YES
         LA    R1,1(,R1)
         BCT   R3,DSNLOOP
FOUND    DS    0H
*        MVC   0(2,R2),=C'C='
*        MVC   2(1,R2),JCTJCLAS    MOVE IN JOB CLASS
         SR    R1,R5               SET TO LENGTH
         BCTR  R1,0                SUBTRACT 1
         EX    R1,MOVEDSN          MOVE IN DSNAME FOR LOCATE
         LOCATE #NAME              GET VOLSERS
         LTR   R15,R15             Q. CATALOGUED
         BZ    #CATLG              A. ZERO - YES
         MVC   62(9,R4),CATMSG     MOVE =C'NOT CATLG'
         B     #MOVECLS            NOW GO EXIT
#CATLG   DS    0H
         LH    R3,#VOLIST          GET # OF ENTRIES
         LA    R1,#VOLIST+6
*        LA    R5,4(,R2)           POINT PAST 'C=? '
         LA    R5,0(,R2)           POINT TO START
         LA    R6,62(,R2)          POINT TO END OF CARD
         SR    R6,R5               GET LENGTH
         MVI   0(R5),C' '          CLEAR REST OF CARD
         EX    R6,CLEAR            CLEAR WITH LENGTH IN R6
         LA    R6,67(,R4)          POINT TO END OF CARD - 3
         SH    R6,SIX              MINUS SIX
#VOLLOOP DS    0H
         CR    R5,R6               Q. ANY MORE ROOM
         BH    #MOVECLS            A. HIGH - NO
         MVC   0(6,R5),0(R1)
         CR    R6,R5               Q. ANY MORE ROOM
         BNH   #CHKVOLS            A. NOT HIGH - NO
         MVI   6(R5),C','
         LA    R5,7(,R5)
         LA    R1,12(R1)
         BCT   R3,#VOLLOOP
         BCTR  R5,0
         MVI   0(R5),C' '
#CHKVOLS DS    0H
         LA    R1,JCTJNAME
         BAL   R14,#CHKJOB          CHECK JOBNAME
         LTR   R15,R15              Q. NEED TO CHECK
         BNZ   #MOVECLS             A. NOT EQUAL - NO
         ST    R12,0(R13)           FOR UCC
         OPENTMC NOTACT=#EXIT
         LR    R3,R2                POINT TO START OF VOLS
#CHKLOOP DS    0H
         CLI   0(R3),C' '           Q. END OF THE TESTS
         BE    #MOVECLS             A. EQUAL - YES
         CLI   0(R3),C'0'           Q. NEED TO CHECK
         BNE   #NXTVOL              A. NOT EQUAL - NO
         MVC   TMVOLSER,0(R3)       COPY VOLSER
         MVI   TMVOLSER,C'C'        FORCE A 'C'
         GETTMC TMVOLSER,TMRECORD,#NXTVOL,ENQ=NO
         CLC   TMDSN,#DSNAME        Q. SAME DATASET
         BNE   #NXTVOL              A. NOT EQUAL - NO
         MVI   0(R3),C'C'           CHANGE IT TO A 'C'
#NXTVOL  DS    0H
         LA    R3,7(,R3)            A. SKIP OVER XXXXXX,
         B     #CHKLOOP
#MOVECLS DS    0H
         MVC   67(2,R4),=C'C='
         MVC   69(1,R4),JCTJCLAS
         B     #EXIT
*CHKJCLS DS    0H
*        TM    JCTUSER0+1,CLSBIT   Q. INSERTED JOBCLASS CARD
*        BO    #EXIT               A. ONE - YES
*        MVC   JCTXWRK,SETUPM
*        MVC   JCTXWRK+18(1),JCTJCLAS      POST JOB CLASS
*        OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
*        OI    JCTUSER0+1,CLSBIT           SET AS INSERTED
*        LA    R15,8
*        B     #ALTEXIT
#EXIT    DS    0H
         LA    R15,0                       SET RETURN CODE
#ALTEXIT DS    0H
        $RETURN RC=(R15)                   RETURN TO HASPPRPU
CARDSCAN DS    0H
*        CLC   =C'$XIT4',44(R4)            Q. HAVE THESE CARDS BEEN ADD
*        BNE   CHKROUT                     A. NO- GO ADD THEM
*        MVI   JCTUSER0+1,X'FF'            TURN ALL BITS ON TO SAY DONE
*        B     #EXIT                       GO EXIT
*HKROUT  DS    0H
         TM    JCTUSER0+1,ROUTEBIT         Q. ROUTE CARD NEEDED
         BO    CHKPUNCH                    A. ONE - NO
         L     R1,0(R7)                    POINT TO JCL IMAGE
         MVC   #HOLD,0(R1)                 COPY ORIGINAL STATEMENT
         MVI   2(R1),C'*'                  MAKE IT A COMMENT
         MVC   JCTXWRK,ROUTE               MOVE IN ROUTE CARD
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
         OI    JCTUSER0+1,ROUTEBIT         SET AS INSERTED
         LA    R15,8                       MAKE A COMMENT
         CLI   JCTJNAME+2,C'T'             Q. TEST JOB
         BNE   #ALTEXIT                    A. NOT EQUAL - NO
         MVC   JCTXWRK,PUNCH               MOVE IN /*ROUTE PUNCH
         OI    JCTUSER0+1,PUNCHBIT+OUTBIT  ROUTE ONLY PUNCH
         LA    R15,8                       MAKE A COMMENT
         B     #ALTEXIT                    EXIT
CHKPUNCH DS    0H
         TM    JCTUSER0+1,PUNCHBIT         Q. ALREADY DONE
         BO    CHKOUT                      A. ONE - YES
         MVC   JCTXWRK,PUNCH               MOVE IN OUTPUT CARD
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
         OI    JCTUSER0+1,PUNCHBIT         SET AS INSERTED
         B     #EXIT                       EXIT
CHKOUT   DS    0H
         TM    JCTUSER0+1,OUTBIT           Q. ALREADY DONE
         BO    CHKLIB                      A. ONE - YES
         MVC   JCTXWRK,OUTPUT              MOVE IN OUTPUT CARD
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
         OI    JCTUSER0+1,OUTBIT           SET AS INSERTED
         B     #EXIT                       EXIT
CHKLIB   DS    0H
         TM    JCTUSER0+1,LIBBIT           Q. ALREADY DONE
         BO    CHKHOLD                     A. ONE - YES
         MVC   JCTXWRK,JOBLIB              MOVE IN OUTPUT CARD
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
         OI    JCTUSER0+1,LIBBIT           SET AS INSERTED
         B     #EXIT                       EXIT
CHKHOLD  DS    0H
         MVC   JCTXWRK,#HOLD               MOVE IN HELD CARD
         OI    RDWFLAGX,RDWXXSNC           SET TO ACCEPT NEW STATEMENT
         OI    JCTUSER0+1,HOLDBIT          SET AS INSERTED
         B     #EXIT                       EXIT
#CHKJOB  DS    0H
         LA    R15,0
         CLI   JCTUSER0,X'FF'             Q. CIS JOB
         BER   R14                        A. EQUAL - YES
         LA    R15,4
         BR    R14
* FLAGS
JOBBIT   EQU   B'00000001'                JOB CARD FOUND
CONTBIT  EQU   B'00000010'                LAST CARD CONTINUED
ROUTEBIT EQU   B'00000100'                ROUTE PRINT CARD INSERTED
OUTBIT   EQU   B'00001000'                OUTPUT CARD INSERTED
PUNCHBIT EQU   B'00010000'                ROUTE PUNCH CARD INSERTED
HOLDBIT  EQU   B'00100000'                CARD NEEDS TO BE RE-INSERTED
LIBBIT   EQU   B'01000000'                JOBLIB CARD HAS BEEN INSERTED
CLSBIT   EQU   B'10000000'                JOBCLASS CARD INSERTED
MOVEDSN  MVC   #DSNAME(0),4(R2)            MOVE IN DSNAME
CLEAR    MVC   1(0,R5),0(R5)
#NAME    CAMLST NAME,#DSNAME,,#VOLIST
SAVEJOB  DS    A
#VOLIST  DS    0H,265C
TMRECORD TMRECORD
WTOLIST  DC    AL2(L'PREFIX+L'#CARD+4,0)
PREFIX   DC    C'$XIT4 - '
#CARD    DS    CL80
#HOLD    DS    CL80
PUNCH    DC    CL80'/*ROUTE PUNCH WBWEST                        $XIT4'
ROUTE    DC    CL80'/*ROUTE PRINT WBEAST                        $XIT4'
OUTPUT   DC    CL80'//JESDS OUTPUT JESDS=ALL,DEST=LOCAL,CLASS=W $XIT4'
JOBLIB   DC    CL80'//JOBLIB DD DSN=WDS.LINKLIB,DISP=SHR        $XIT4'
SETUPM   DC    CL80'/*SETUP JOB CLASS=                          $XIT4'
#DSNAME  DS    CL44
CATMSG   DC    CL9'NOT CATLG'
SETUP    DC    C'/*SETUP'
JOB      DC    C' JOB '
#KEYWORD DC    CL4'DSN='
BLANKS   DC    CL44' '
TWO      DC    H'2'
SIX      DC    H'6'
         LTORG
        $MODEND
         END    ,
