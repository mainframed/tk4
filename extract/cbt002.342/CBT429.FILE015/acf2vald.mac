*          DATA SET ACF2VALDV1 AT LEVEL 019 AS OF 07/03/86
***********************************************************************
* ACF2 RESOURCE VALIDATION CALL FOR BATCH                             *
*                                                                     *
*      CALL ACF2VALD(TYPE: CHAR(4),     /* NOT USED   */              *
*                    P2  : PTR,         /* NOT USED   */              *
*                    RES : CHAR(40),    /*RESOURCE    */              *
*                    ACCS: CHAR(1);     /*ACCESS REQ  */              *
*                                                                     *
***********************************************************************
ACF2VALD  CSECT
          SAVE   (14,12),,*
          LR     R3,R15
          USING  ACF2VALD,R3
          L      R4,8(,R1)              POINT TO RESOURCE
          L      R5,12(,R1)             POINT TO ACCESS
          GETMAIN RU,LV=@LWORK
          ST     R1,8(,R13)
          ST     R13,4(,R1)
          LR     R13,R1
*
*       BUILD ACF2 INTERFACE CB'S AND CALL ACF2
*
          LA     R6,@SAVEL(,R13)
          USING  ACDSV,R6
          MVI    0(R6),X'00'
          MVC    1(ACFSPRML-1,R6),0(R6)    CLEAR DSN
          MVI    ACFSPID1,ACFSPIUR         USER CALL
          MVI    ACFSPREQ,ACFSPRDS         DATASET VALIDATION
          CLI    0(R5),C'R'
          BNE    CHKWR
          MVI    ACFSPAC1,ACFSPAIN
          B      DSN
CHKWR     CLI    0(R5),C'W'
          BNE    CHKAL
          MVI    ACFSPAC1,ACFSPAOU
          B      DSN
CHKAL     CLI    0(R5),C'L'
          BNE    PARMERR
          MVI    ACFSPAC1,ACFSPAOT
DSN       EQU    *
          ST     R4,ACFSPDSN
          LA     R1,@SAVEL+ACFSPRML(,R13)
          ST     R1,ACFSPMSG
          ACFSVC 0(R6),TYPE=S,CVT=FIND,NONE=NOACF2
          LTR    R15,R15
          BZ     RETURN0
*
*       HERE IF WE ARE TO WASTE THE REQUEST
*
PARMERR   EQU    *
ERRRTN    EQU    *
          LR     R4,R13
          L      R13,4(,R13)
          FREEMAIN RU,LV=@LWORK,A=(R4)
          RETURN (14,12),RC=8
          SPACE  4
RETURN0   EQU   *                             HERE IF OK TO KEEP
          LR     R4,R13
          L      R13,4(,R13)
          FREEMAIN RU,LV=@LWORK,A=(R4)
          RETURN (14,12),RC=0                 CHECKING
NOACF2    EQU   *                             HERE IF OK TO KEEP
          LR     R4,R13
          L      R13,4(,R13)
          FREEMAIN RU,LV=@LWORK,A=(R4)
          RETURN (14,12),RC=64                CHECKING
          EJECT
          PRINT NOGEN
          ACCVT
          ACDSV
          ACSXP
          ACFREGS
@LWORK    EQU     80+ACFSPRML+133
@SAVEL    EQU     80
          END
