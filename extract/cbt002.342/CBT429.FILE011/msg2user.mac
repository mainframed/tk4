MSG2USER TITLE 'INTEL EXIT TO INFORM TSO USERS ENQUED ON A DATASET'
MSG2USER CSECT
MSG2USER AMODE 31                 31 BIT ADDRESSING MODE
MSG2USER RMODE ANY                31 BIT RESIDENCY MODE
*   02/23/87       MILTON SOONG    AUTHOR OF THIS PROGRAM.            *
*   03/08/90       MORRIS KARLIN   REVISED FOR ESA 3.1.0E.        @MK *
*****  T A B L E   O F   C O N T E N T S                          *****
* PARAGRAPH    NARRATIVE DESCRIPTION                                  *
* 100          PROGRAM ENTRY AND EXIT.                                *
* 200          SUBROUTINES                                            *
* 200-010      GET THIS TASK'S ASID                         (GTMYASID)*
* 200-020      BUILD DSNAME TO SCAN                         (BLDDSN  )*
* 200-030      BUILD ASID TABLE USING GQSCAN                (BLDASIDT)*
* 200-040      SEND MESSAGES TO TSO USER                    (SENDMSGS)*
* 800          CONSTANTS, LITERALS AND LTORG DEFINITIONS.             *
* 900          DSECT DEFINITIONS.                                     *
* 900-060      WTO USER EXIT PARAMETER LIST DSECT                     *
* 900-070      TPUT MESSAGE WORK AREA DSECT                           *
* 900-080      WTO MESSAGE TEXT DSECT                                 *
* 900-090      DYNAMIC WORK AREA DSECT                                *
***********************************************************************
*              P R O G R A M   D E S C R I P T I O N                  *
* NAME         MSG2USER                                               *
* ENTRY POINT  MSG2USER                                               *
* PURPOSE      THE PURPOSE OF THIS EXIT IS TO ISSUE MESSAGES TO TSO   *
*              USERS WHO ARE ENQUED ON A DATASET CAUSING A BATCH JOB  *
*              TO WAIT.                                               *
* OPERATION    THIS WTO EXIT GAINS CONTROL WHENEVER MESSAGE 'IEF863I' *
*              IS ISSUED ON THE OPERATOR' CONSOLE.  THIS EXIT WILL    *
*              THEN USE THE DSNAME PASSED IN THE IEF863I MESSAGE AND  *
*              ISSUE A ''GQSCAN'' FOR ONLY THAT DSNAME.   ''GQSCAN''  *
*              RETURNS ''RESOURCE INFORMATION BLOCK EXTENSIONS(RIBE)''*
*              FOR EVERY TSO USER OR BATCH JOB ENQUED ON THIS DSNAME. *
*              THE EXIT WILL THEN DETERMINE THE JOBNAME OF THE BATCH  *
*              JOB THAT IS WAITING AND WILL ALSO DETERMINE THE TSO    *
*              USERS WHO HAVE THE DATASET ENQUED.  THE FOLLOWING      *
*              MESSAGES WILL BE SENT TO EACH TSO USER ENQUED ON THE   *
*              DATASET:                                               *
*               HRA861I FOLLOWING RESERVED DATASET UNAVAILABLE TO JJJ *
*               HRA863I DSN = DDDDDDDDDD                              *
*                                                                     *
*              IF YOU RECEIVE THIS MESSAGE YOU SHOULD FREE THE DATASET*
*              TO ALLOW THE BATCH JOB TO COMPLETE.                    *
* RESTRICTIONS THIS EXIT RECEIVES CONTROL IN 31-BIT ADDRESSING MODE   *
*              RMODE=ANY.  TPUT WHICH IS THE METHOD USED TO SEND THE  *
*              MESSAGES TO TSO USERS CAN BE ISSUED IN 31-BIT MODE BUT *
*              IT'S WORKAREA MUST RESIDE BELOW THE LINE.              *
*                                                                     *
*              THIS EXIT ROUTINE MUST RESIDE IN AN AUTHORIZED LIBRARY *
*              AND ON THE ''LNKLST'' CONCATENATION.                   *
*                                                                     *
*              THE FOLLOWING ENTRY MUST BE ADDED TO THE ACTIVE        *
*              ''MPFLSTXX'' MEMBER OF SYS1.PARMLIB':                  *
*                      IEF863I,SUP(NO),USEREXIT(MSG2USER)             *
* INPUT        R1   POINTS TO THE ADDRESS OF THE CTXT(WTO TEXT MSG.)  *
*              R13  POINTS ADDRESS OF STANDARD SAVE AREA              *
*              R14  RETURN ADDRESS                                    *
*              R15  ENTRY POINT ADDRESS                               *
* OUTPUT       THE FOLLOWING MESSAGES IS ISSUED TO THE TSO USER ENQUED*
*              ON THE DATASET CAUSING JOB TO WAIT:                    *
*               HRA861I FOLLOWING RESERVED DATASET UNAVAILABLE TO JJJ *
*               HRA863I DSN = DDDDDDDDDD                              *
***********************************************************************
MSG2USER CSECT
R0       EQU   0   PARAMETER REGISTER
R1       EQU   1   PARAMETER REGISTER OR PARAMETER LIST REGISTER
R2       EQU   2   USE TO BRANCH AND LINK TO SUBROUTINES
R3       EQU   3   WORK REGISTER
R4       EQU   4   WORK REGISTER
R5       EQU   5   WORK REGISTER
R6       EQU   6   WORK REGISTER
R7       EQU   7   WORK REGISTER
R8       EQU   8   WORK REGISTER
R9       EQU   9   BASE REGISTER FOR WTO EXIT PARAMETER LIST
R10      EQU   10  BASE REGISTER FOR OUR DYNAMIC STORAGE AREA.
R11      EQU   11  RESERVED FOR SECOND BASE REGISTER IF NEEDED.
R12      EQU   12  $$$$$$$$ B A S E   R E G I S T E R $$$$$$$$
R13      EQU   13  SAVEAREA REGISTER     (LINKAGE REGISTER)
R14      EQU   14  RETURN REGISTER       (LINKAGE REGISTER)
R15      EQU   15  ENTRY POINT REGISTER  (LINKAGE REGISTER)
         PRINT NOGEN
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         BALR R12,R0              ESTABLISH MODULES BASE REGISTER
         USING *,R12              R12 IS NOW OUR BASE REGISTER
         L     R9,0(R1)           R9==> WTO EXIT PARAMETER LIST
         USING CTXT,R9            ESTABLISH ADDRESSABLITY TO IT
         B     MAINLINE           NOW GO DO YOUR PROCESSING
ALLDONE  EQU   *
         LM    R14,R12,12(R13)    RESTORE CALLERS REGISTERS
         BR    R14                CLEAN UP AND RETURN
         TITLE  'PROGRAM MAIN LINE LOGIC ROUTINE'
MAINLINE EQU   *
         SLR   R10,R10            CLEAR R10
         GETMAIN R,LV=DYNLEN,SP=230    GET STORAGE SP 230        @MK
         LTR   R15,R15            Q. DID WE GET IT?
         BNZ   ALLDONE            A. NO.  NO SENSE ON CONTINUING
         LR    R10,R1             ESTABLISH ADDRESSABILITY TO DYNAREA
         USING DYNAREA,R10
         BAL   R2,GTMYASID        GET MY ASID
         BAL   R2,BLDDSN          BUILD DSNAME TO SCAN
         BAL   R2,BLDASIDT        BUILD ASID TABLE USING GQSCAN
         BAL   R2,SENDMSGS        SEND MESSAGES TO TSO USERS
PREDONE  FREEMAIN R,LV=DYNLEN,A=(10),SP=230 R10==> DYNAMIC WORKAREA
         B     ALLDONE
 TITLE '***SEC. 200-010*** - GET THIS TASKS ASID'
*********************************************************************
*               G T M Y A S I D   S U B R O U T I N E               *
* THIS SUBROUTINE WILL EXTRACT THE ASID OF THE CURRENTLY RUNING TASK*
* THAT IS   W A I T I N G   FOR THE DATASET NAME WHICH IS DISPLAYED *
* IN THE MESSAGE TEXT: IEF863I DSN = DSNAME.                        *
*                                                                   *
* WE DO IT THIS WAY BECAUSE THIS EXIT GETS CONTROL FIRST AND THEN   *
* THE DISPATCHER GETS CONTROL.  THEREFORE PSAAOLD WILL CONTAIN THE  *
* THE ASCB OF THE TASK WAITING FOR THE DATASET.                     *
*********************************************************************
GTMYASID EQU   *
         L     R1,PSAAOLD-PSA(0,0)     EXTRACT WAITING TASK'S ASID
         MVC   ALCMASID,ASCBASID-ASCB(R1)   AND SAVE IT FOR LATER USE
         BR    R2                 RETURN TO MAINLINE ROUTINE
 TITLE '***SEC. 200-020*** - BUILD DSNAME TO SCAN'
*********************************************************************
*                   B L D D S N   S U B R O U T I N E               *
* AT ENTRY TO THIS SUBROUTINE REGISTER 9 POINTS TO THE WTO EXIT     *
* INPUT PARAMETER LIST.  FROM THIS PARAMETER LIST REGISTER 3 IS     *
* LOADED WITH THE ADDRESS OF THE MESSAGE ATTRIBUTES.  THEN THE      *
* ADDRESS OF MESSAGE (IEF863I DSN= XXXX) IS LOADED INTO REGISTER 4. *
* THE LENGTH OF THE DSNAME IS COMPUTED AND SAVED FOR LATER USE.     *
* THE DSNAME IS SAVED ALSO FOR LATER USE.                           *
*********************************************************************
BLDDSN   EQU   *
         L     R3,CTXTTXPJ        ESTABLISH ADDRESSABILITY TO
         USING CTXTATTR,R3        THE MESSAGE ATTRIBUTES.
         LA    R4,CTXTTMSG        R4===> A(IEF863I DSN= ) TEXT
         USING MSGTEXT,R4         ESTABLISH ADDRESSABILITY TO IT
         MVI   ALCDSNM,C' '       CLEAR DSNAME WORKAREA
         MVC   ALCDSNM+1(L'ALCDSNM-1),ALCDSNM
         SLR   R5,R5              CLEAR R5
         SLR   R6,R6              CLEAR R6
         LH    R5,CTXTTLEN        R5= L'(WTO MESSAGE TEXT)
         LA    R6,MSGFXLEN        R6= L('FIXED PORTION OF MESSAGE)
*                                       'IEF863I DSN = '
         SR    R5,R6              COMPUTE LENGTH OF DSNAME
         ST    R5,ALCDSNLN        SAVE LENGTH OF DSNAME FOR LATER USE
         BCTR  R5,0               MINUS 1 FOR EXECUTED MOVE
         EX    R5,SAVDSN          MOVE MSGDSN TO ALCDSNM
         BR    R2                 RETURN TO MAINLINE
SAVDSN   MVC   ALCDSNM(0),MSGDSN  SAVE THE DSNAME FROM MESSAGE TEXT
         SPACE 3
         DROP  R3,R4
 TITLE '***SEC. 200-030*** - BUILD ASID TABLE USING GQSCAN'
*********************************************************************
*               B L D A S I D T   S U B R O U T I N E               *
* THIS SUBROUTINE WILL INVOKE THE 'GQSCAN' MACRO TO SEARCH FOR ALL  *
* JOBS OR TSO USERS THAT ARE ENQUED TO A DATASET.  THE DATASET NAME *
* WAS PASSED IN THE WTO INPUT PARAMETER LIST AND SAVED IN AN AREA   *
* LABELED 'ALCDSNM'. 'GQSCAN' RETURNS RIBES OF EACH TSO USER OR JOB *
* ENQUED TO THIS DATASET.  THIS SUBROUTINE WILL INSPECT EACH RIBE   *
* RETURNED AND SAVE THE ASID IN A TABLE.  THIS TABLE WILL BE USED   *
* BY THE 'SENDMSG' SUBROUTINE TO SEND MESSAGES.  IF THE ASID BELONGS*
* TO THIS TASK IT WILL NOT BE SAVED IN THE TABLE.  IF THE ASID IS   *
* WAITING FOR A RESOURCE THEN IT WILL BECOME THE JOBNAME AND HIS    *
* ASID WILL NOT BE SAVED IN THE TABLE.                              *
*********************************************************************
BLDASIDT EQU   *
         STM   R0,R15,ALCSAVE     SAVE ALL MY REGISTERS
         LA    R3,ALCSVIDS        R3===> A(TABLE TO HOLD ASIDS)
         ST    R3,ALCENIDS        SAVE END OF TABLE ADDRESS
         L     R6,ALCDSNLN        R6= L'(ENQUED DSNAME)
         LA    R5,ALCDSNM         R5===> A(ENQUEUED DSNAME)
         L     R0,SCANGM          R0= L'(GQSCAN STORAGE)
         GETMAIN R,LV=(0)                                       @MK
         LTR   R15,R15            Q. DID WE GET OUR STORAGE?
         BNZ   TSOEND             A. NO.  DON'T CONTINUE
         ST    R1,SCANPTR         A. YES. SAVE ADDRESS OF GETMAIN CORE
         LR    R7,R1              TELL GQSCAN WHERE THE STORAGE STARTS
         LA    R13,ALCSAVE2       GIVE GQSCAN A 18 FULLWORD SAVEAREA
         GQSCAN AREA=((R7),8192),REQLIM=10,SCOPE=ALL,                  X
               RESNAME=(MAJNAM,(R5),(R6)),MF=(E,GQPARMS)
         LTR   R15,R15            Q. GQSCAN SUCCESSFUL?
         BNZ   TSOEND             A. NO. DONT CONTINUE
         ST    R0,RIBLNGTH        STORE L'RIB AND L'RIBE
         ST    R1,NUMRIBS         SAVE NUMBER OF RIBS RETURNED
         LTR   R8,R1              Q. ANY RIBS RETURNED?
         BZ    TSOEND             A. NO. DONT CONTINUE
         L     R7,SCANPTR         R7===> A(RIBS RETURNED)
         USING RIB,R7             ESTABLISH ADDRESSABILITY TO RIB
RIBLOOP  EQU   *
         LR    R3,R7
         AH    R3,RIBLNGTH        R3===> A(RIB VARIABLE SECTION)
         AH    R3,RIBVLEN         R3===> A(FIRST RIBE) OR RIB EXTENSION
         USING RIBE,R3            ESTABLISH ADDRESSABILITY TO RIBE
         L     R1,RIBNRIBE        R1= NUMBER OF RIB EXTENSIONS RETURNED
         LA    R15,ALCSVIDS       R15===> START OF ASID TABLE
         LA    R14,ALCSVIDE       R14===> END OF ASID TABLE
RIBELOOP EQU   *
         TM    RIBESFLG,RIBESTAT  Q. IS THIS THE OWNER OF THE RESOURCE?
         BO    RIBOWNER           A. YES HE IS AND HE IS NOT WAITING.
         MVC   ALCJOBNM,RIBEJBNM  A. NO. THIS GUY IS WAITING FOR DSNAME
         B     BUMPRIBE              SAVE HIS NAME BUT NOT HIS ASID.
RIBOWNER EQU   *
         CLC   ALCMASID,RIBEASID  Q. IS IT THE TASK'S ASID?
         BE    BUMPRIBE           A. YES. JUST IGNORE IT
         MVC   0(2,R15),RIBEASID  ELSE KEEP THE ASID
         LA    R15,2(R15)         BUMP TABLE POINTER
         CR    R15,R14            Q. IS THE ASID TABLE FULL?
         BNL   RIBEOUT            A. YES.
BUMPRIBE EQU   *
         AH    R3,RIBELENG        POINT TO NEXT RIB EXTENSION
         BCT   R1,RIBELOOP        AND INSPECT IT.
         SPACE 1
BUMPRIB  EQU   *
         SLR   R4,R4              CLEAR R4 FOR WORK
         L     R5,RIBNRIBE        NUMBER OF RIBES
BUMPLOOP EQU   *
         AH    R4,RIBELENG        ADD LENGTH OF A RIBE
         BCT   R5,BUMPLOOP        ADD TIMES NUMBER OF RIBES
         AH    R4,RIBLNGTH        ADD LENGTH OF RIB
         AH    R4,RIBVLEN         ADD LENGTH OF VARIABLE SECTION
         AR    R7,R4              R7===> POINTS TO NEXT RIB
         BCT   R8,RIBLOOP         START WITH NEXT RIB
         SPACE 1
RIBEOUT  EQU   *
         ST    R15,ALCENIDS       SAVE THE END OF TABLE ADDRESS.
         L     R0,SCANGM          GRAB GQSCAN WORKAREA LENGTH & SP
         L     R1,SCANPTR         R1===> A(GQSCAN WORKAREA)
         FREEMAIN R,LV=(0),A=(1)  FREE GQSCAN WORKAREA
         LM    R0,R15,ALCSAVE     RESTORE REGISTERS
         BR    R2                 RETURN TO MAINLINE ROUTINE
TSOEND   EQU   *
         LM    R0,R15,ALCSAVE     RESTORE REGISTERS
         B     PREDONE            AND GET OUT OF THIS EXIT.
 TITLE '***SEC. 200-040*** - SEND MESSAGES TO TSO USER'
*********************************************************************
*               S E N D M S G S   S U B R O U T I N E               *
* THIS SUBROUTINE WILL SEND THE FOLLOWING MESSAGES TO ALL TSO USERS *
* WHO HAVE THE DATASET ENQUED CAUSING A BATCH JOB TO WAIT:          *
*        HRA861I FOLLOWING RESERVED DATA SET IS UNAVAILABLE TO JJJ  *
*        HRA863I DSN= DSNAME ENQUED                                 *
* THIS SUBROUTINE WILL DO A GETMAIN FOR STORAGE BELOW THE LINE.  WE *
* HAVE TO DO IT THIS WAY BECAUSE THIS EXIT RECEIVES CONTROL IN 31   *
* BIT ADDRESSING MODE AND CAN RESIDE ABOVE OR BELOW THE LINE.  TPUT *
* CAN BE ISSUED IN 31 BIT ADDRESSING MODE BUT THE INPUT TO IT MUST  *
* BE BELOW THE LINE.                                                *
* TPUT IS THE METHOD USED TO SEND THE MESSAGES TO THE TSO USER.     *
*********************************************************************
SENDMSGS EQU   *
         STM   R0,R15,ALCSAVE     SAVE ALL MY REGISTERS
         GETMAIN R,LV=TPUTALEN,LOC=BELOW,SP=230  GET STORAGE BELOW LINE
         LTR   R15,R15            Q. DID WE GET STORAGE?
         BNZ   SENDEXT            A. NO. DONT CONTINE
         ST    R1,TPUTPTR         A. YES. SAVE ITS ADDRESS
         LR    R11,R1             USE R11 AS THE
         USING TPUTAREA,R11         BASE FOR TPUT WORKAREA.
********************************************************
* BUILD TEXT MESSAGES 1 AND 2.  THE MESSAGES ARE BUILT *
* BUILT IN A GETMAINED AREA BELOW THE LINE BECAUSE THE *
* TPUT MACRO REQUIRES THAT IT'S INPUT BE IN 24 BIT MODE*
********************************************************
         MVC   TPUTTXT1(64),TMSGTXT1 BUILD MESSAGE TEXT 1
         MVC   TPUTTXT2(57),TMSGTXT2 BUILD MESSAGE TEXT 2
         MVC   TPUTJBNM,ALCJOBNM  MOVE JOBNAME TO MESSAGE
         L     R3,ALCDSNLN        R3= L(ENQUED DSNAME)
         BCTR  R3,0               MINUS 1 FOR EXECUTED MVC
         EX    R3,MVCDSN          MVC   TPUTDSN(0),MSGDSN
         LA    R3,1(R3)           RESTORE ORIGINAL LENGTH VALUE
         LA    R3,ALCSVIDS        R3===> A(ASID TABLE)
         L     R4,CVTPTR          R4===> A(CVT)
         L     R4,CVTASVT-CVTMAP(,R4)  R4===> ASVT
         SLR   R14,R14
SENDMSG1 EQU   *
         C     R3,ALCENIDS        Q. END OF ASID TABLE?
         BNL   SENDEND            A. YES IT IS.
         LH    R5,0(R3)           PICKUP ASID TABLE ENTRY
         BCTR  R5,0               COMPUTE ENTRY IN ASVT
         SLL   R5,2
         L     R5,ASVTENTY-ASVT(R5,R4)
         CLC   ASCBTSB-ASCB(4,R5),=A(0)  Q. IS THIS A TSO ASCB?
         BE    SENDMSG2                  A. NO.  IT MUST BE BATCH.
********************************************************
* SEND MESSAGES TO THE TSO USERS WHO HAVE THE RESOURCE ENQED.
********************************************************
         LA    R6,TMSGT1L         R6= L'(TPUT TEXT1)
         STH   R6,TPUTMSGL        SAVE IT
         ICM   R0,12,0(3)         ASID OF TSO USER
         ICM   R0,3,TPUTMSGL      MESSAGE LENGTH OF TEXT 1
         LA    R1,TPUTTXT1        ADDRESS OF MESSAGE TEXT 1
         TPUT  (1),(0),R          SEND IT TO THE TSO USER
         SPACE 1
         LA    R6,TMSGT2L         R6= L'(TPUT TEXT1)
         STH   R6,TPUTMSGL        SAVE IT
         ICM   R0,12,0(3)         ASID OF TSO USER
         ICM   R0,3,TPUTMSGL      MESSAGE LENGTH OF TEXT 2
         LA    R1,TPUTTXT2        ADDRESS OF MESSAGE TEXT 2
         TPUT  (1),(0),R          SEND IT TO THE TSO USER
SENDMSG2 LA    R3,2(R3)           R3===> A(NEXT ASID IN TABLE)
         B     SENDMSG1           CONTINUE UNTIL END OF ASID TABLE.
         SPACE 1
SENDEND  L     R11,TPUTPTR        R11===> A(STORAGE TO FREE)
         FREEMAIN R,LV=TPUTALEN,A=(11),SP=230
SENDEXT  LM    R0,R15,ALCSAVE     RESTORE ALL REGISTERS
         BR    R2                 RETURN TO MAINLINE
MVCDSN   MVC   TPUTDSN(0),ALCDSNM
 TITLE '***SEC. 800*** - CONSTANTS, LITERALS AND LTORG DEFINITIONS'
         DS    0D
MAJNAM   DC    CL8'SYSDSN  '      MAJOR RESOURCE NAME FOR GQSCAN
SCANGM   DC    AL1(230),AL3(16384) GETMAIN SIZE FOR GQSCAN IN SP 230
         DS    0D
TMSGTXT1 DC    CL08'HRA861I '     MESSAGE ID
         DC    CL48'FOLLOWING RESERVED DATA SET NAME UNAVAILABLE TO '
         DC    CL08'        '
TMSGT1L  EQU   *-TMSGTXT1
TMSGTXT2 EQU   *
         DC    CL08'HRA863I '
         DC    CL05'DSN= '
         DC    CL44'                                            '
TMSGT2L  EQU   *-TMSGTXT2
         LTORG
 TITLE '***SEC. 900-010*** - COMMUNICATIONS VECTOR TABLE(CVT) DSECT'
*********************************************************************
* THIS DSECT IS A MAP THE MVS COMMUNICATIONS VECTOR TABLE WHICH IS  *
* REFERRED TO AS THE CVT.  IT CONTAINS POINTERS TO MANY OF THE MVS  *
* CONTROL BLOCKS.                                                   *
*********************************************************************
         PRINT NOGEN
CVTMAP   CVT   LIST=YES,DSECT=YES
PSA      IHAPSA          ,   PREFIX SAVE AREA
ASVT     IHAASVT         ,  ADDRESS SPACE VECTOR TABLE
ASCB     IHAASCB         ,  ADDRESS SPACE CONTROL BLOCK
RIB      ISGRIB          ,  RESOURCE INFO BLOCK
         IEZVX100        ,  WTO USER EXIT PARM LIST
 TITLE '***SEC. 900-070 - TSO TPUT MESSAGE WORK AREA DSECT'
**********************************************************************
* THIS DSECT IS USED TO ESTABLISH ADDRESSABILITY TO STORAGE OBTAINED *
* BY A GETMAIN INSTRUCTION.  THIS STORAGE AREA MUST RESIDE BELOW THE *
* 16 MEG LINE BECAUSE THE TPUT SVC REQUIRES IT TO BE BELOW THE LINE. *
**********************************************************************
TPUTAREA DSECT
         DS    0D
TPUTTXT1 EQU   *
         DS    CL08               MESSAGE ID='HRA861I '
         DS    CL48               MESSAGE TEXT1
TPUTJBNM DS    CL08               JOBNAME (MAX EIGHT CHARACTERS)
         SPACE 2
TPUTTXT2 DS    CL08               MESSAGE ID='HRA863I '
         DS    CL05               MESSAGE TEXT2 PART 1= 'DSN= '
TPUTDSN  DS    CL44               DSNAME
         SPACE 2
TSOASID  DS    H                  TSO USER ASID TO SEND THE MESSAGES TO
TPUTMSGL DS    H                  LENGTH OF MESSAGE TO TPUT TO USER
TPUTALEN EQU   ((*-TPUTAREA+7)/8)*8 LENGTH OF TPUT WORK AREAS
 TITLE '***SEC. 900-080 - WTO MESSAGE TEXT DSECT'
**********************************************************************
* THIS DSECT IS USED TO ESTABLISH ADDRESSABILITY TO THE WTO MESSAGE  *
* PASSED TO THIS EXIT AT ENTRY TO THIS EXIT.                         *
**********************************************************************
MSGTEXT  DSECT
MSGID    DS    CL14             MESSAGE ID = 'IEF863I DSN = '
MSGFXLEN EQU   *-MSGTEXT        LENGTH OF FIXED PORTION OF MESSAGE
MSGDSN   DS    CL44             DSNAME. (MAX 44 BYTES)
MSGDSNL  EQU   *-MSGDSN         MAX LENGTH OF ANY DSNAME
MSGTEXTL EQU   *-MSGTEXT        MAX LENGTH OF MESSAGE
 TITLE '***SEC. 900-090*** - DYNAMIC STORAGE AREA DSECT'
*********************************************************************
* THIS DSECT IS A MAP OF ALL OUR WORKING STORAGE FOR THIS PROGRAM.  *
* REG. 10 IS THE BASE REGISTER USED.  THIS IS TO MAKE THIS PROGRAM RENT
*       NOTE: THIS STORAGE AREA HAS RMODE=ANY AND WILL PROBABLY     *
*             RESIDE ABOVE THE LINE.                                *
*********************************************************************
DYNAREA  DSECT
ALCSAVE  DS    18F                AREA FOR SUBROUTINES TO SAVE REGS.
ALCSAVE2 DS    18F                AREA FOR GQSCAN TO SAVE REGS.
GQPARMS  GQSCAN AREA=(*-*,8192),REQLIM=10,SCOPE=ALL,                   X
               RESNAME=(*-*,*-*,0),MF=L
SCANPTR  DS    A                  A(GQSCAN WORKAREA)
TPUTPTR  DS    A                  FULLWORD TO SAVE GETMAINED CORE ADDR.
RIBLNGTH DS    H                  L'RIB(MUST PRECEED RIBELENG)
RIBELENG DS    H                  L'RIBE(MUST PROCEED RIBLENGTH)
NUMRIBS  DS    F                  NUMBER OF RIBS RETURNED BY GQSCAN
ALCMASID DS    H                  OUR ASID
ALCJOBNM DS    CL8                JOBNAME OF JOB WAITING FOR RESOURCE
ALCDSNM  DS    CL44               AREA TO SAVE DSNAME WTO TEXT
ALCDSNLN DS    F                  LENGTH OF DSNAME THAT IS ENQUED.
**********************************************************************
* NOTE: DO NOT CHANGE THE ORDER OF 'ALCENIDS' 'ALCSVIDS' 'ALCSVIDE'. *
**********************************************************************
ALCENIDS DS    A                  ADDRESS OF THE END OF ASID TABLE
ALCSVIDS DS    128H               ASID TABLE (128 ASIDS)
ALCSVIDE EQU   *                  END OF ASID TABLE
DYNLEN   EQU   *-DYNAREA          LENGTH OF DYNAMIC STORAGE AREA
         END   MSG2USER
