DCMCONVD CSECT
DCMCONVD AMODE 31
DCMCONVD RMODE 24
         $BOX   HDR='DCMCONVD - ',TITLE='DSECTS'
         $DAB
         EJECT
         $STATSD
         EJECT
         PRINT  NOGEN
         $IOB
         $CCW
         $DCB
         PRINT  GEN
         $BOX  'CONVERT TIME',                                         *
               TITLE='CONVERT TIME AND DATE'
DCMCONVD CSECT
         $MODID BR=YES
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING DCMCONVD,R12
         USING DAB,R11
                                       SPACE
*   R0 =  TIME  100'S SECS FROM MIDNIG HT
*   R1 =  DATE  YYDDDF PACKED)
                                       SPACE
         LA    R10,100
         LR    R9,R0
         XR    R8,R8
         LA    R7,60
         DR    R8,R10                  R9 = SECONDS
         XR    R8,R8
         DR    R8,R7                   R8 = SS, R9 = MM
         LR    R6,R8                   R6 = SS, R9 = MM
         XR    R8,R8
         DR    R8,R7                   R8 = MM, R9 = HH
         LR    R5,R8                   R5 = MM
         MR    R8,R10                  R9 = 100*HH
         AR    R9,R5                   R9 = 100*HH + MM
         MR    R8,R10                  R9 = 10000*HH + 100*MM
         AR    R9,R6                   R9 = 10000*HH + 100*MM + SS
         CVD   R9,WA
         MVC   TIMEF(10),EDMASKT
         ED    TIMEF(10),WA+4
         CLI   TIMEF+3,C'0'
         BNE   CONVDATE
         OI    TIMEF+2,C'0'
                                       SPACE
         $BOX  'CONVERT DATE',CTL1=EJECT
*  Algorithm from R Stone, Bell Labs
CONVDATE EQU   *
         ST    R1,WA
         ZAP   WA2(8),WA+2(2)
         CVB   R9,WA2                  R9 = DAY IN BINARY
         LA    R8,59                   R8= DAYS IN JAN AND FEB NOLEAP
         TM    WA+1,X'01'              LEAP YEAR?
         BO    NO_LEAP                 BRANCH IF ODD YEAR
         TM    WA+1,X'12'              IS YEAR DIVISIBLE BY 4?
         BM    NO_LEAP                 BRANCH IF NOT
         LA    R8,60                   R8 = DAYS IN JAN + FEB LEAP YR
NO_LEAP  CR    R9,R8                   DATE IN JAN OR FEB?
         BNH   JAN_FEB                 BRANCH IF YES
         SR    R9,R8                   FORCE FEB TO HAVE 30 DAYS
         LA    R9,61(,R9)
JAN_FEB  LA    R9,91(,R9)              R9 = DAY + 91
         LR    R10,R9                  R10= DAY + 91
         LA    R2,100
         LA    R3,3055
         MR    R8,R2
         DR    R8,R3                   R9= (DAY + 91)/30.55
         LR    R7,R9
         BCTR  R7,0
         BCTR  R7,0                    R7= MONTH NUMBER
         MR    R8,R3
         DR    R8,R2
         SR    R10,R9                  R10= DAY OF MONTH
         CVD   R10,WA
         MVC   DATEDF+1(4),EDMASKD
         ED    DATEDF+1(4),WA+6
         BCTR  R7,0                    R7= MONTH NUMBER - 1
         LA    R6,0(R7,R7)             MULTIPLY
         LA    R6,0(R6,R7)               BY 3
         LA    R6,MONTHS(R6)
         MVC   DATEDF(3),0(R6)         GET MONTH
                                       SPACE
DONE     EQU   *
         RETURN (14,12)                AND RETURN
                                       SPACE
         $BOX  'CONSTANTS',TITLE='CONSTANTS'
         DS    0D
EDMASKT  DC    X'402021207A20207A2020'
EDMASKD  DC    X'40212020'
         DS    0D
MONTHS   DC    C'JanFebMarAprMayJun'
         DC    C'JulAugSepOctNovDec'
         SPACE 3
         $REGS
         END
