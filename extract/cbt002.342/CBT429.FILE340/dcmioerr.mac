DCMIOERR CSECT
DCMIOERR AMODE 31
DCMIOERR RMODE 24
         $BOX   HDR='DCMIOERR - ',TITLE='DSECTS'
         $DAB
         EJECT
         $STATSD
         EJECT
         $IOB
         EJECT
         $CCW
         EJECT
         PRINT  NOGEN
         $DCB
         DCBD   DSORG=(XA,XE),DEVD=DA
         PRINT  GEN
         $ERRCODE
         $BOX  'INITIALIZATION',TITLE='INITIALIZATION'
DCMIOERR CSECT
         $MODID BR=YES
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING DCMIOERR,R12
         USING DAB,R11
                                       SPACE
         LA    R2,$ERR_IO              SHOW I/O FAILED
         MVC   D_ERRMSG(PMSGLEN),PERRMSG MOVE PROTOTYPE MESSAGE
         OI    D_FLG1,$IOERR
         ST    R2,D_ERROR
         LA    R10,IOBAREA
         USING IOB,R10
                                       SPACE
         $BOX  'FORMAT ECB',                                           *
               TITLE='FORMAT  CMD, ECB and SENSE'
         UNPK  WA(3),IOBECB(2)         FORMAT
         TR    WA(2),TRTAB-C'0'          COMPLETION CODE
         MVC   D_ERRMSG+PCCD(2),WA     AND MOVE IT
                                       SPACE
         $BOX  'FORMAT SENSE'
         UNPK  WA(5),IOBSENSE(3)       FORMAT
         TR    WA(4),TRTAB-C'0'        SENSE CODE
         MVC   D_ERRMSG+PSENSED(4),WA  AND MOVE IT
                                       SPACE
         $BOX  'FORMAT CMD'
         L     R8,IOBCSW               GET CCW ADDRESS
         USING CCW,R8
                                       SPACE
         LA    R0,8
         N     R8,M0FFF
         BZ    WRITE_IT
                                       SPACE
         SLR   R8,R0                   R8 -> CCW
         UNPK  WA(3),CCWCC(2)          FORMAT
         TR    WA(2),TRTAB-C'0'          CMD ADDR
         MVC   D_ERRMSG+PCMDD(2),WA    AND MOVE IT
                                       SPACE
         $BOX  'WRITE MESSAGE',TITLE='WRITE MESSAGE'
WRITE_IT EQU   *
         LA    R1,D_ERRMSG
         LA    R0,PMSGLEN
         O     R1,NOWAIT
         TPUT  (1),(0),R
                                       SPACE
         RETURN (14,12)
                                   SPACE
         $BOX  'CONSTANTS',TITLE='CONSTANTS'
         DS    0D
M0FFF    DC    X'00FFFFFF'
NOWAIT   DC    X'10000000'
*                      111111111122222222223333333333444444444455555555
*                456789012345678901234567890123456789012345678901234567
PERRMSG  DC    C'I/O Error. Cmd = '
PCMD     DC    CL2' '
         DC    C', Sense = '
PSENSE   DC    CL4' '
         DC    C', Completion code = '
PCC      DC    CL2' '
PMSGLEN  EQU   *-PERRMSG
                                   SPACE
PCMDD    EQU   PCMD-PERRMSG
PSENSED  EQU   PSENSE-PERRMSG
PCCD     EQU   PCC-PERRMSG
                                       SPACE
         DS    0D
TRTAB    DC    C'0123456789ABCDEF'
         SPACE 3
         $REGS
         END
