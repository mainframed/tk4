DCMSTATS CSECT
DCMSTATS AMODE 31
DCMSTATS RMODE 24
         $BOX   HDR='DCMSTATS - ',TITLE='DSECTS'
         $DAB
         EJECT
         $STATSD
         EJECT
         $SSRD
         EJECT
         $SSSD
         EJECT
         $RDCD
         EJECT
         $MONREC
         $UCMBS
         $CMB
         PRINT  NOGEN
         $IOB
         $CCW
         $OPEN
         $DCB
         PRINT  GEN
         $SCREEN
         $BOX  'INITIALIZATION',TITLE='INITIALIZATION'
DCMSTATS CSECT
         $MODID BR=YES
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING DCMSTATS,R12
         USING DAB,R11
                                       SPACE
         LA    R10,72(,R13)
         ST    R13,4(,R10)             CHAIN
         ST    R10,8(,R13)               THOSE
         LR    R13,R10                     SAVEAREAS
                                       SPACE
         L     R10,D_SCRN@             GET SCREEN POINTER
         USING SCREEN,R10
                                       SPACE
         L     R9,D_NEW@
         USING MONREC,R9
                                       SPACE
         LA    R15,CLONG
         TM    IN_FLGS,$DELTA          DELTA STATS?
         BZ    *+4+4
         LA    R15,CSHORT
         MVC   SDELTAP,0(R15)
                                       SPACE
         LA    R15,CLONG
         TM    IN_FLGS,$HIST           LONG TERM RATES
         BO    *+4+4
         LA    R15,CSHORT
         MVC   SDELTAR,0(R15)
                                       SPACE
         NI    D_FLG1,255-$OFFLINE     RESET OFFLINE FLAG
         TM    IN_FLGS,$RUN            Running online devices?
         BO    FORM_CACHE              Yes..don't update date
                                       SPACE
         $BOX  'FORMAT DATE/TIME'
         TM    IN_FLGS,$HIST
         BZ    NEW_DATES
                                       SPACE
         L     R8,D_FILE@
         L     R0,MON_TME-MONREC(,R8)
         L     R1,MON_DTE-MONREC(,R8)
         L     R15,ACONDATE
         BASR  R14,R15
                                       SPACE
NEW_DATES      EQU *
         MVC   SODATE(L'SCDATE+L'SCTIME),DATEF  MOVE OLD DATE+TIME
         L     R0,MON_TME
         L     R1,MON_DTE
         L     R15,ACONDATE
         BASR  R14,R15
                                       SPACE
         MVC   SCDATE(L'SCDATE+L'SCTIME),DATEF  MOVE NEW DATE+TIME
         DROP  R9
         XC    SCREF(8),SCREF
         TM    IN_FLGS,$AUTO
         BZ    FORM_CACHE
                                       SPACE
         L     R15,D_REPREM
         BCTR  R15,0
         LTR   R15,R15
         BZ    FORM_CACHE
                                       SPACE
         LA    R0,10
         MVC   SCREF,=C'Auto'
         MVC   SCREF#,=C' >10'
         CR    R15,R0
         BH    FORM_CACHE
                                       SPACE
         CVD   R15,WA
         MVC   SCREF#,EDMASKA
         ED    SCREF#,WA+6
                                       SPACE
         $BOX  'FORMAT CACHE CAPACITY',                                *
               TITLE='COLLECT STATISTICS'
FORM_CACHE     EQU *
         LA    R9,SSSAREA
         USING SSS,R9
                                       SPACE
         UNPK  WA(5),SSSSSID(3)        SSID
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SCSSID,WA
         ICM   R15,B'1111',CFCACHE     GET CONFIGURED CACHE
         CLI   SSSMODEL,0
         BNE   *+4+4
         SRL   R15,10                  CONVERT TO K
         CVD   R15,WA
         MVC   SCAINST,EDMASKK
         ED    SCAINST,WA+4
                                       SPACE
         ICM   R15,B'1111',AVCACHE     GET AVAILABLE  CACHE
         CLI   SSSMODEL,0
         BNE   *+4+4
         SRL   R15,10                  CONVERT TO K
         CVD   R15,WA
         MVC   SCAAVAIL,EDMASKK
         ED    SCAAVAIL,WA+4
                                       SPACE
         ICM   R15,B'1111',CFNVS       GET INSTALLED NVS
         CLI   SSSMODEL,0
         BNE   *+4+4
         SRL   R15,10                  CONVERT TO K
         CVD   R15,WA
         MVC   SNVSINST,EDMASKK
         ED    SNVSINST,WA+4
                                       SPACE
         ICM   R15,B'1111',PINNVS      GET PINNED DATA
         CLI   SSSMODEL,0
         BNE   *+4+4
         SRL   R15,10                  CONVERT TO K
         CVD   R15,WA
         MVC   SPINNVS,EDMASKK
         ED    SPINNVS,WA+4
                                       SPACE
         $BOX  'FORMAT SWITCH STATUS',                                 *
               TITLE='SWITCH STATUS'
         TM    D_FLAGS,$SWITCH
         BO    FORM_DEVS
                                       SPACE
         XR    R0,R0
         ICM   R0,B'0001',D_SWITCH     SP0 BIT MAP
         LA    R2,SSWCH0               OUTPUT AREA
         BAS   R14,FORM_SWCH
                                       SPACE
         ICM   R0,B'0001',D_SWITCH+1   SP1 BIT MAP
         LA    R2,SSWCH1               OUTPUT AREA
         BAS   R14,FORM_SWCH
                                       SPACE
         ICM   R0,B'0001',D_SWITCH+2   SP2 BIT MAP
         LA    R2,SSWCH2               OUTPUT AREA
         BAS   R14,FORM_SWCH
                                       SPACE
         ICM   R0,B'0001',D_SWITCH+3   SP3 BIT MAP
         LA    R2,SSWCH3               OUTPUT AREA
         BAS   R14,FORM_SWCH
                                       SPACE
         OI    D_FLAGS,$SWITCH         INDICATE SWITCHES FORMATTED
         MVC   SSERIAL#,D_SERIAL       MOVE CU SERIAL#
         $BOX  'FORMAT DEVICES'
FORM_DEVS      EQU *
         UNPK  WA(5),D_DEVF+2(3)       STARTING ADDR
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SDEVICES(4),WA
         MVI   SDEVICES+4,C'-'
         UNPK  WA(5),D_DEVL+2(3)       ENDING   ADDR
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SDEVICES+5(4),WA
                                       SPACE
         $BOX  'FORMAT DEVICE ADDRESS',                                *
               TITLE='FORMAT DEVICE ADDRESS'
         XC    SVOL,SVOL
         XC    SDEV0,SDEV0
         MVC   SDEV0(5),=C'*ALL*'
         L     R14,D_DEVOFF
         L     R15,D_UCMBS@
         L     R7,D_DEV#
         SLL   R14,7
         AR    R15,R14
         BCTR  R7,0
         L     R8,U_UCB@-UCMBS(,R15)   R8  -> UCB
         TM    IN_FLGS,$ALL
         BO    FORM_PATHS
                                       SPACE
         XC    SDEV0,SDEV0
         MVC   SVOL,$UCBVOLI(R8)       MOVE VOLUME NAME
         UNPK  WA(5),D_DEV+2(3)        UNPACK DEVICE ADDRESS
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SDEV0(4),WA
         TM    $UCBSTAT(R8),$UCBONLI   ONLINE?
         BO    FORM_DEV1
                                       SPACE
         MVC   SDEV1,OFFLINE
         OI    D_FLG1,$OFFLINE
                                       SPACE
FORM_DEV1      EQU *
         L     R15,D_DEV
         LTR   R7,R7                   MORE THAN 1 DEVICE?
         BZ    FORM_PATHS
                                       SPACE
         XC    SDEV1,SDEV1
         XC    SVOL,SVOL
         AR    R15,R7
         ST    R15,WA2
         MVI   SDEV0+4,C'-'
         UNPK  WA(5),WA2+2(3)          ENDING   ADDR
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SDEV1(4),WA
                                       SPACE
         $BOX  'FORMAT PATHS',                                         *
               TITLE='FORMAT PATHS'
*    R8  -> UCB
*    R7  =  #DEVICES - 1
FORM_PATHS     EQU *
         XC    SPATHS,SPATHS
*        LTR   R7,R7                   MORE THAN 1 DEVICE?
*        BNZ   CACHE_STAT              PATH STATUS PROBABLY INCORRECT
                                       SPACE
         L     R15,XFF
         LA    R2,$UCBLPM
         XR    R3,R3                   R3 = 0
         XR    R4,R4                   R3 = 0
         SR    R8,R2                   R8 -> LPM
         ST    R15,D_PATHS
         ST    R15,D_PATHS+4
         ICM   R2,B'0001',0(R8)        R2 = LPM
         LA    R8,$UCBLPM-$UCBCHPS+7(,R8) R8 -> LAST IN UCBCHPS
         LA    R9,D_PATHS+7            LAST ENTRY IN D_PATHS
         LA    R0,8                    AT MOST 8 CHPIDS
                                       SPACE
NEXT_CHP EQU  *
         SRDL  R2,1
         LTR   R3,R3
         BZ    DECREMENT
         ICM   R4,B'0001',0(R8)        FETCH CHPID
         STCM  R4,B'0001',0(R9)        AND SAVE
                                       SPACE
DECREMENT      EQU *
         XR    R3,R3
         BCTR  R9,0
         BCTR  R8,0
         BCT   R0,NEXT_CHP
                                       SPACE
         LA    R0,8
         LA    R1,D_PATHS
         XR    R2,R2
         LA    R3,SPATHS
         LA    R4,X'FF'
                                       SPACE
NEXT_PATH      EQU *
         ICM   R2,B'0001',0(R1)
         CR    R2,R4
         BE    CACHE_STAT
         STCM  R2,B'0001',WA2
         UNPK  WA(3),WA2(2)
         TR    WA(2),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   0(2,R3),WA
         MVI   2(R3),C' '
         LA    R1,1(,R1)
         LA    R3,3(,R3)
         BCT   R0,NEXT_PATH
                                       SPACE
         $BOX  'FORMAT CACHE STATUS',                                  *
               TITLE='FORMAT CACHE ST  ATUS'
CACHE_STAT     EQU *
         XC    SDPLX(SDPLLN),SDPLX
         XC    SPINST,SPINST
         LTR   R7,R7                   MORE THAN 1 DEVICE?
         BNZ   ALL_STATS
                                       SPACE
         LA    R15,ACTIV
         TM    D_DEVST,$DCASHAC        CACHE ACTIVE?
         BZ    *+4+4
         LA    R15,NOTACTIV
         MVC   SCASHST,0(R15)
                                       SPACE
         LA    R15,ACTIV
         TM    D_DEVST,$DDFWAC         DFW ACTIVE?
         BZ    *+4+4
         LA    R15,NOTACTIV
         MVC   SDFWST,0(R15)
                                       SPACE
         TM    D_DEVST+1,$PINST        PINNED DATA?
         BZ    *+4+6
         MVC   SPINST,PINNED
                                       SPACE
         TM    D_DEVST,$DDPLXP+$DDPLXS DUPLEXED?
         BZ    IO_TIME
                                       SPACE
         MVC   SDPLX,DPLXING
         LA    R15,DPLX2ND
         TM    D_DEVST,$DDPLXP         PRIMARY DUPLEX VOL?
         BO    *+4+4                   GUESSED RIGHT
         LA    R15,DPLX1ST
         MVC   SDPLPS,0(R15)
                                       SPACE
         L     R15,D_CU
         LA    R0,X'3F'
         LH    R1,D_DEVST
         NR    R1,R0
         AR    R15,R1
         ST    R15,WA2
         UNPK  WA(5),WA2+2(3)          DEVICE ADDRESS
         TR    WA(4),HEXTAB-C'0'       TRANSLATE TO HEX
         MVC   SDPLDEV,WA+1
         B     IO_TIME
                                       SPACE
ALL_STATS      EQU *
         LA    R9,SSSAREA
         USING SSS,R9
                                       SPACE
         LA    R15,AVAIL
         TM    SSSOCHE,$CACHEAC        CACHE ACTIVE?
         BZ    *+4+4
         LA    R15,NOTAVAIL
         MVC   SCASHST,0(R15)
                                       SPACE
         LA    R15,AVAIL
         TM    SSSONVS,$NVSAC          DFW ACTIVE?
         BZ    *+4+4
         LA    R15,NOTAVAIL
         MVC   SDFWST,0(R15)
                                       SPACE
         ICM   R15,B'1111',PINCACHE    PINNED DATA?
         BZ    IO_TIME
                                       SPACE
         MVC   SPINST,PINNED
         DROP  R9
                                       SPACE
         $BOX  'FORMAT I/O TIMES',                                     *
               TITLE='FORMAT I/O STAT  ISTICS'
IO_TIME        EQU *
         L     R9,D_NEW@
         L     R8,D_OLD@
         TM    IN_FLGS,$HIST
         BZ    *+4+4
         L     R8,D_FILE@
         LM    R0,R1,MON_TOD-MONREC(R9)    R0,R1 = NEW TOD
         LM    R2,R3,MON_TOD-MONREC(R8)    R2,31 = OLD TOD
         SRDL  R0,12+10+4
         SRDL  R2,12+10+4
         SLR   R1,R3                   R1 = DURATION
         XR    R0,R0
         SLDL  R0,10+4                 R0,R1 = DURATION IN USECS
         D     R0,F100000              R1 = DURATION IN TENTHS
         ST    R1,D_DUR                SAVE DURATION
                                       SPACE
         XC    SIOTT,SIOTT             CLEAR I/O TIME
         XC    SIOPT,SIOPT             CLEAR PEND TIME
         XC    SIODT,SIODT             CLEAR DISC TIME
         XC    SIOCT,SIOCT             CLEAR CONN TIME
                                       SPACE
         LA    R0,100
         L     R9,D_SAMPC              R9 = TOTAL SAMPLED I/O COUNT
         LTR   R8,R1                   R8 = DURATION IN TENTHS
         BZ    HIT_STATS               Strange...duration is zero
                                       SPACE
         LR    R7,R9                   R7 = TOTAL SAMPLED I/O COUNT
         MR    R6,R0
         DR    R6,R8                   R7 = I/O RATE (CHAN SUBSYSTEM)
         CVD   R7,WA
         MVC   SIOCH,EDMASKT
         ED    SIOCH,WA+5
                                       SPACE
         LTR   R9,R9                   R9 = TOTAL SAMPLED I/O COUNT
         BZ    IO_STATS
                                       SPACE
         MR    R8,R0                   R9 = 100*SAMPLED I/O COUNT
         L     R7,D_PENDT              R7  = TOTAL PEND TIME
         XR    R6,R6
         SLDL  R6,7                    TIME IN MICROS
         DR    R6,R9                   R7 = AVG PEND TIME(MS)
         CVD   R7,WA
         LR    R1,R7                   ACCUMULATE
         MVC   SIOPT,EDMASKT
         ED    SIOPT,WA+5
                                       SPACE
         L     R7,D_DISCT              R7  = TOTAL DISCO TIME
         XR    R6,R6
         SLDL  R6,7                    TIME IN MICROS
         DR    R6,R9                   R7 = AVG DISCO TIME(MS)
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE
         MVC   SIODT,EDMASKT
         ED    SIODT,WA+5
                                       SPACE
         L     R7,D_CONNT              R7  = TOTAL CONNECT TIME
         XR    R6,R6
         SLDL  R6,7                    TIME IN MICROS
         DR    R6,R9                   R7 = AVG CONN TIME(MS)
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE
         MVC   SIOCT,EDMASKT
         ED    SIOCT,WA+5
                                       SPACE
         CVD   R1,WA                   R1 = TOTAL I/O TIME
         MVC   SIOTT,EDMASKT
         ED    SIOTT,WA+5
                                       SPACE
         $BOX  'FORMAT I/O RATES',                                     *
               TITLE='FORMAT I/O RATES'
IO_STATS       EQU *
         LA    R9,DSSR
         USING STATSD,R9
                                       SPACE
         LA    R0,100
         L     R15,D_DUR               DURATION IN TENTHS
         L     R7,CRD#                 R7 = NORMAL READS
         MR    R6,R0
         DR    R6,R15                  R7 = NORMAL READ RATE
         CVD   R7,WA
         LR    R1,R7                   ACCUMULATE READS
         LR    R3,R7                   ACCUMULATE READS
         MVC   SNRD,EDMASKT
         ED    SNRD,WA+5
                                       SPACE
         L     R7,CWR#                 R7 = NORMAL WRITES
         MR    R6,R0
         DR    R6,R15                  R7 = NORMAL WRITE RATE
         CVD   R7,WA
         LR    R2,R7                   ACCUMULATE WRITES
         LR    R4,R7                   ACCUMULATE WRITES
         MVC   SNWR,EDMASKT
         ED    SNWR,WA+5
                                       SPACE
         AR    R4,R3                   R4 = NORMAL I/O
         CVD   R4,WA
         AR    R3,R4                   ACCUMULATE I/O
         MVC   SNIO,EDMASKT
         ED    SNIO,WA+5
                                       SPACE
         L     R7,CRDSQ#               R7 = SEQL   READS
         MR    R6,R0
         DR    R6,R15                  R7 = SEQL   READ RATE
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE READS
         LR    R3,R7                   ACCUMULATE READS
         MVC   SSQRD,EDMASKT
         ED    SSQRD,WA+5
                                       SPACE
         L     R7,CWRSQ#               R7 = SEQL   WRITES
         MR    R6,R0
         DR    R6,R15                  R7 = SEQL   WRITE RATE
         CVD   R7,WA
         AR    R2,R7                   ACCUMULATE WRITES
         LR    R4,R7                   ACCUMULATE READS
         MVC   SSQWR,EDMASKT
         ED    SSQWR,WA+5
                                       SPACE
         AR    R4,R3                   R4 = SEQL   I/O
         CVD   R4,WA
         MVC   SSQ,EDMASKT
         ED    SSQ,WA+5
                                       SPACE
         L     R7,CRDCFW#              R7 = CFW    READS
         MR    R6,R0
         DR    R6,R15                  R9 = CFW    READ RATE
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE READS
         LR    R3,R7                   ACCUMULATE READS
         MVC   SCFWRD,EDMASKT
         ED    SCFWRD,WA+5
                                       SPACE
         L     R7,CWRCFW#              R7 = CFW    WRITES
         MR    R6,R0
         DR    R6,R15                  R7 = CFW    WRITE RATE
         CVD   R7,WA
         AR    R2,R7                   ACCUMULATE WRITES
         LR    R4,R7                   ACCUMULATE WRITES
         MVC   SCFWWR,EDMASKT
         ED    SCFWWR,WA+5
                                       SPACE
         AR    R4,R3                   R4 = CFW    I/O
         CVD   R4,WA
         MVC   SCFWIO,EDMASKT
         ED    SCFWIO,WA+5
                                       SPACE
         CVD   R1,WA                   TOTAL READS
         MVC   STRD,EDMASKT
         ED    STRD,WA+5
                                       SPACE
         CVD   R2,WA                   TOTAL WRITES
         MVC   STWR,EDMASKT
         ED    STWR,WA+5
         AR    R2,R1                   ACCUMULATE
                                       SPACE
         L     R7,CINH#                INHIBITS
         MR    R6,R0
         DR    R6,R15                  R7 = INHIBIT RATE
         CVD   R7,WA
         AR    R2,R7                   ACCUMULATE
         MVC   SINHIB,EDMASKT
         ED    SINHIB,WA+5
                                       SPACE
         L     R7,CBYP#                BYPASS
         MR    R6,R0
         DR    R6,R15                  R7 = BYPASS RATE
         CVD   R7,WA
         AR    R2,R7                   ACCUMULATE
         MVC   SBYPASS,EDMASKT
         ED    SBYPASS,WA+5
                                       SPACE
         CVD   R2,WA                   TOTAL I/O
         MVC   STIO,EDMASKT
         ED    STIO,WA+5
                                       SPACE
         L     R7,CDFWWR#              R7 = DFW    WRITES
         MR    R6,R0
         DR    R6,R15                  R7 = DFW    WRITE RATE
         CVD   R7,WA
         LR    R1,R7                   ACCUMULATE
         MVC   SDFWWRN,EDMASKT
         ED    SDFWWRN,WA+5
                                       SPACE
         L     R7,CDFWWRSQ#            R7 = DFW SEQL WRITES
         MR    R6,R0
         DR    R6,R15                  R7 = DFW SEQL WRITE RATE
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE
         MVC   SDFWSQ,EDMASKT
         ED    SDFWSQ,WA+5
                                       SPACE
         CVD   R1,WA                   DFW I/O RATE
         MVC   SDFWIO,EDMASKT
         ED    SDFWIO,WA+5
                                       SPACE
         $BOX  'FORMAT HITS',                                          *
               TITLE='FORMAT HITS'
HIT_STATS      EQU *
         LA    R9,DSSR                 DELTA STATS
         TM    IN_FLGS,$DELTA          Lucky guess?
         BO    *+4+4
         LA    R9,CSSR                 NO...CUMULATIVE STATS
         LA    R0,1000
         L     R7,CRD#H                R7 = NORMAL READ HITS
         LR    R3,R7                   R3 = NORMAL READ HITS
         MR    R6,R0
         XR    R15,R15
         L     R4,CRD#                 R4 = NORMAL READ
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R6,R4                   R7 = NORMAL READ%
         LR    R15,R7                  R15= NORMAL READ%
         CVD   R15,WA
         MVC   SNRMRDH,EDMASKP
         ED    SNRMRDH,WA+5
                                       SPACE
         L     R7,CRDSQ#H              R7 = READ SEQL HITS
         AR    R3,R7                   R3 = NORMAL READ +SEQL HITS
         MR    R6,R0
         L     R5,CRDSQ#               R5 = SEQL   READ
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R6,R5                   R7 = READ SEQL%
         LR    R15,R7                  R15= READ SEQL%
         CVD   R15,WA
         MVC   SSQLRDH,EDMASKP
         ED    SSQLRDH,WA+5
                                       SPACE
         MR    R2,R0
         AR    R5,R4
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R2,R5
         LR    R15,R3
         CVD   R15,WA
         MVC   STRDH,EDMASKP
         ED    STRDH,WA+5
                                       SPACE
         L     R7,CWR#H                R7 = NORMAL WRITE HITS
         LR    R3,R7                   R3 = NORMAL WRITE HITS
         MR    R6,R0
         L     R4,CWR#                 R4 = NORMAL WRITES
         XR    R15,R15
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R6,R4                   R7 = NORMAL WRITE%
         LR    R15,R7
         CVD   R15,WA
         MVC   SNRMDFH,EDMASKP
         ED    SNRMDFH,WA+5
                                       SPACE
         L     R7,CWRSQ#H              R7 = WRITE SEQL HITS
         AR    R3,R7                   R3 = NORMAL WRITE+SEQL HITS
         MR    R6,R0
         L     R5,CWRSQ#               R5 = SEQL   READ
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R6,R5                   R7 = WRITE SEQL%
         LR    R15,R7                  R15= WRITE SEQL%
         CVD   R15,WA
         MVC   SSQLDFH,EDMASKP
         ED    SSQLDFH,WA+5
                                       SPACE
         MR    R2,R0
         AR    R5,R4
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R2,R5
         LR    R15,R3                  R15= WRITE%
         CVD   R15,WA
         MVC   STDFH,EDMASKP
         ED    STDFH,WA+5
                                       SPACE
         L     R7,CRDCFW#H             R7 = CFW READ HITS
         LR    R3,R7                   R3 = CFW READ HITS
         MR    R6,R0
         L     R4,CRDCFW#              R4 = CFW READS
         XR    R15,R15
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R6,R4                   R7 = CFW READ%
         LR    R15,R7                  R15= CFW READ%
         CVD   R15,WA
         MVC   SCFWRDH,EDMASKP
         ED    SCFWRDH,WA+5
                                       SPACE
         L     R7,CWRCFW#H             R7 = WRITE CFW HITS
         AR    R3,R7                   R3 = READ + WRITE CFW HITS
         MR    R6,R0
         L     R5,CWRCFW#              R5 = CFW WRITES
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R6,R5                   R7 = WRITE CFW%
         LR    R15,R7                  R15= WRITE CFW%
         CVD   R15,WA
         MVC   SCFWWRH,EDMASKP
         ED    SCFWWRH,WA+5
                                       SPACE
         MR    R2,R0
         AR    R5,R4
         XR    R15,R15
         LTR   R5,R5
         BZ    *+4+2+2
         DR    R2,R5
         LR    R15,R3
         CVD   R15,WA
         MVC   STCFH,EDMASKP
         ED    STCFH,WA+5
                                       SPACE
         $BOX  'FORMAT MISCELLANEOUS',                                 *
               TITLE='FORMAT MISCELLANEOUS'
         LA    R9,DSSR
         LA    R0,100
         L     R15,D_DUR               DURATION IN TENTHS
         L     R7,CDACHE#              R7 = NORMAL STAGES
         MR    R6,R0
         DR    R6,R15                  R7 = NORMAL STAGE RATE
         CVD   R7,WA
         LR    R1,R7                   ACCUMULATE
         MVC   SDTCNRM,EDMASKT
         ED    SDTCNRM,WA+5
                                       SPACE
         L     R7,CSQCHE#              R7 = SEQL   STAGES
         MR    R6,R0
         DR    R6,R15                  R7 = SEQL   STAGES RATE
         CVD   R7,WA
         AR    R1,R7                   ACCUMULATE
         MVC   SDTCSQ,EDMASKT
         ED    SDTCSQ,WA+5
                                       SPACE
         CVD   R1,WA                   TOTAL STAGE RATE
         MVC   STDTC,EDMASKT
         ED    STDTC,WA+5
                                       SPACE
         L     R7,CCHEDA#              R7 = DESTAGES
         MR    R6,R0
         DR    R6,R15                  R7 = DESTAGE RATE
         CVD   R7,WA
         MVC   STCTD,EDMASKT
         ED    STCTD,WA+5
                                       SPACE
         L     R7,CSDSQRD#             R7 = PREREADS
         MR    R6,R0
         DR    R6,R15                  R7 = PREREAD RATE
         CVD   R7,WA
         MVC   STPRD,EDMASKT
         ED    STPRD,WA+5
                                       SPACE
         LA    R9,CSSR
         TM    IN_FLGS,$DELTA
         BZ    *+4+4
         LA    R9,DSSR
         LA    R0,10
         L     R3,CRD#                 READS
         L     R4,CWR#                 WRITES
         A     R3,CRDSQ#               READ SEQL
         A     R4,CWRSQ#               WRITE SEQL
         A     R3,CRDCFW#              CFW READS
         A     R4,CWRCFW#              CFW WRITES
         MR    R2,R0                   10*TOTAL READS
         L     R5,F9999
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R2,R4                   R3 = READ/WRITE RATIO
         LR    R5,R3                   R5 = READ/WRITE RATIO
         CVD   R5,WA
         MVC   STRW,EDMASKH
         ED    STRW,WA+5
         C     R5,F9999
         BL    *+4+6
         MVC   STRW,OVFLO
                                       SPACE
         L     R3,CRD#H                READ HITS
         A     R3,CWR#H                WRITE HITS
         A     R3,CRDSQ#H              READ SEQL HITS
         A     R3,CWRSQ#H              WRITE SEQL HITS
         A     R3,CWR#H                WRITE HITS
         L     R4,CSQCHE#              SEQL STAGES
         MR    R2,R0                   TIMES 10
         A     R4,CDACHE#              NORMAL STAGES
         L     R5,F9999
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R2,R4                   R3 = HITS/STAGE
         LR    R5,R3                   R5 = HITS/STAGE
         CVD   R5,WA
         MVC   SHTSTG,EDMASKH
         ED    SHTSTG,WA+5
         C     R5,F9999
         BL    *+4+6
         MVC   SHTSTG,OVFLO
                                       SPACE
         LA    R0,100
         L     R4,CWR#                 NORMAL WRITES
         L     R3,CRTRY#               RETRYS
         A     R4,CWRSQ#               SEQL WRITES
         MR    R2,R0
         XR    R15,R15
         LTR   R4,R4
         BZ    *+4+2+2
         DR    R2,R4                   RETRY%
         LR    R15,R3                  RETRY%
         CVD   R15,WA
         MVC   SRTRYS,EDMASKP
         ED    SRTRYS,WA+5
                                       SPACE
DONE     EQU  *
         XR    R15,R15
         TM    D_FLG1,$OFFLINE         OFFLINE DEVICE
         BZ    *+4+4
         LA    R15,99                  SHOW OFFLINE
                                       SPACE
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)        AND RETURN
                                       SPACE
         $BOX  'FORMAT SWITCHES',TITLE='FORMAT SWITCHES'
         DS    0D
FORM_SWCH      EQU *
         XR    R1,R1
         LA    R15,8
         SRDL  R0,8                    BIT MASK IN R1
         LA    R3,CSWITCH
         XR    R4,R4
                                       SPACE
NEXT_SWCH      EQU *
         SLDL  R0,1
         LTR   R0,R0
         BZ    BUMP
                                       SPACE
         ICM   R4,B'0001',0(R3)
         STCM  R4,B'0001',0(R2)
                                       SPACE
BUMP     EQU   *
         XR    R0,R0
         LA    R2,1(,R2)
         LA    R3,1(,R3)
         BCT   R15,NEXT_SWCH
                                       SPACE
         BR    R14                     AND RETURN
                                       SPACE
         $BOX  'CONSTANTS',TITLE='CONSTANTS'
         DS    0D
XFF      DC    F'-1'
F100000  DC    F'100000'
F9999    DC    F'99999'
                                       SPACE
$CVT     EQU   16
$CVTUCBA EQU   X'4E4'
$UCBDEV  EQU   4
$UCBF@   EQU   8
$UCBVOLI EQU   28
$UCBCHPS EQU   24
$UCBLPM  EQU   28
$UCBSTAT EQU   3
$UCBONLI EQU   B'10000000'
                                       SPACE
HEXTAB   DC    C'0123456789ABCDEF'
CSWITCH  DC    C'ABCDEFGH'
ACTIV    DC    C'Active       '
NOTACTIV DC    C'Not active   '
AVAIL    DC    C'Available    '
NOTAVAIL DC    C'Not available'
DPLXING  DC    C'Duplexed: '
DPLX1ST  DC    C' Primary  device'
DPLX2ND  DC    C'Secondary device'
PINNED   DC    C'Pinned data'
OFFLINE  DC    C'Offline'
CSHORT   DC    C'Short'
CLONG    DC    C' Long'
OVFLO    DC    CL7'   ****'
         DS    0D
ACONDATE DC    V(DCMCONVD)
EDMASKT  DC    X'40202021204B20'
EDMASKP  DC    X'40202021204B20'
EDMASKH  DC    X'40202021204B20'
EDMASKR  DC    X'40202020202120'
EDMASKK  DC    X'4020202020202120'
EDMASKA  DC    X'40202120'
         SPACE 3
         $REGS
         END
