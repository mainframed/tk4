REPLAYP  CSECT
REPLAYP  AMODE 24
REPLAYP  RMODE 24
         $BOX   HDR='REPLAYP - ',TITLE='DSECTS'
         PRINT NOGEN
         DCBD  DSORG=QS
         PRINT GEN
         SPACE 3
LINED    DSECT
LINE     DS    0CL128
CC       DS    CL1
PAD1     DS    CL14
DATA     DS    CL80
PAD2     DS    CL33
$LINELEN EQU   *-LINE
         SPACE 3
PAGED    DSECT
PAGE     DS    24CL($LINELEN)
$PAGELEN EQU   *-PAGE
         SPACE 3
         $BOX  'INITIALIZATION',TITLE='INITIALIZATION'
SBA      EQU   X'11'
SF       EQU   X'1D'
$FF      EQU   C'1'
$DS      EQU   C'0'
$SS      EQU   C' '
$OFFSET  EQU   20
                                       SPACE
REPLAYP  CSECT
         SAVE  (14,12),,RELAYP-&SYSDATE
         LR    R12,R15
         USING REPLAYP,R12
                                       SPACE
         LA    R10,SAVEAREA
         ST    R13,4(,R10)             CHAIN
         ST    R10,8(,R13)               THOSE
         LR    R13,R10                     SAVEAREAS
                                       SPACE
         LR    R9,R1                   SAVE PARM POINTER
         LA    R0,$PAGELEN
         LR    R2,R0
                                       SPACE
         GETMAIN RU,LV=(0),LOC=BELOW
                                       SPACE
         LR    R11,R1
         USING PAGE,R11
                                       SPACE
         LR    R4,R1
         LR    R5,R2
         XR    R14,R14
         XR    R15,R15
         ICM   R15,B'1000',BLANKS
         MVCL  R4,R14                  CLEAR
                                       SPACE
         L     R1,0(,R9)
         LH    R2,0(,R1)
         LTR   R2,R2                   ANY PARMS?
         BZ    OPEN                    NO..USE DEFAULTS
                                       SPACE
         MVC   VALFLD,ZEROS            INIT VALFLD
         BCTR  R2,0                    REDUCE FOR EXECUTE
         EX    R2,VALMVN               -> MVN VALFLD(*-*),2(R1)
         EX    R2,VALCLC               -> CLC VALFLD(*-*),2(R1)
         BNE   OPEN
                                       SPACE
         EX    R2,VALPACK              -> PACK WA,2(*-*,R1)
         CVB   R3,WA
         ST    R3,BLKPP                SAVE BLOCKS/PAGE
                                       EJECT
OPEN     EQU   *
         OPEN  (CAPTURE,(INPUT),SYSPRINT,(OUTPUT))
                                       SPACE
         LA    R10,CAPTURE
         USING IHADCB,R10
                                       SPACE
         TM    DCBOFLGS,DCBOFOPN       OPEN OK?
         BZ    ERR_OPN                 NO..ERROR
                                       SPACE
         LH    R0,DCBLRECL
         ST    R0,RECORDLN             Save record length
         LA    R10,SYSPRINT
         TM    DCBOFLGS,DCBOFOPN       OPEN OK?
         BZ    ERR_OPN                 NO..ERROR
                                       SPACE
         $BOX  'Main Processing',TITLE='Main Processing'
GETMORE  EQU   *
         GET   CAPTURE
                                       SPACE
         L     R15,COUNT
         L     R8,RECORDLN             R8 = Max search
         LA    R15,1(,R15)
         ST    R15,COUNT
         LR    R10,R1
         LR    R9,R1
         AR    R9,R8                   R9 -> LAST CHARACTER
         BAS   R14,SEARCH              SEARCH FOR 1ST SBA
                                       SPACE
         LTR   R15,R15
         BZ    PRINT_EM
                                       SPACE
         $BOX  'Convert 3270 coordinates to row + column'
NEXT_FIELD     EQU *
         LA    R10,1(,R10)             R10= START
         BCTR  R8,0                    Decrement remaining count
         LR    R6,R10                  R6 -> sba + 1
         BAS   R14,SEARCH              SEARCH FOR 1ST SBA
                                       SPACE
         LR    R7,R10                  R7 -> sba
         LTR   R15,R15
         BZ    PRINT_EM
                                       SPACE
         ICM   R0,B'0001',0(R6)        R0 = 1ST COORDINATE
         ICM   R1,B'0001',1(R6)        R1 = 2ND COORDINATE
         LA    R15,X'3F'
         LA    R14,80
         NR    R0,R15
         NR    R1,R15
         SLL   R0,6                    TIMES 64
         AR    R1,R0                   ADD ROW
         XR    R0,R0
         DR    R0,R14                  R0 = C-1; R1 = R-1
         SLL   R1,7
         AR    R1,R0
         LA    R1,$OFFSET(R1,R11)      R1 = PAGE OFFSET
                                       SPACE
         LA    R6,2(,R6)
         CLI   0(R6),SF                SF ATTRIBUTE?
         BNE   *+4+4
         LA    R6,2(,R6)
         LR    R15,R7                  R15 -> END + 1
         SR    R15,R6                  R15 = LENGTH
         BCTR  R15,0
         LTR   R15,R15
         BM    *+4+4
         EX    R15,MVCPAGE
         LR    R8,R9
         LR    R10,R7
         SR    R8,R10
         B     NEXT_FIELD
                                       SPACE
         $BOX  'Search for SBA',CTL1=EJECT
         DS    0D
SEARCH   EQU   *
         LA    R15,1
                                       SPACE
CONTINUE EQU   *
         CLI   0(R10),SBA
         BER   R14
                                       SPACE
         LA    R10,1(,R10)
         BCT   R8,CONTINUE
                                       SPACE
         XR    R15,R15
         BR    R14
                                       SPACE
         DROP  R11,R10
                                       SPACE
         $BOX  'Print Processing',TITLE='Print Processing'
         DS    0D
PRINT_EM EQU   *
         LR    R10,R11
         USING LINED,R10
                                       SPACE
         MVI   CC,$DS                  Double SPACE
         L     R15,BLK#
         BCT   R15,NEW_BLOCK
                                       SPACE
         MVI   CC,$FF                  SKIP TO NEW PAGE
         L     R15,BLKPP
                                       SPACE
NEW_BLOCK      EQU *
         ST    R15,BLK#
         L     R14,COUNT               GET SCREEN COUNT
         CVD   R14,WA
         MVC   COUNTP,EDMASKP
         ED    COUNTP,WA+5
         MVC   PAD1(SEQLEN),SEQUENCE
         PUT   SYSPRINT,(R10)
                                       SPACE
         LA    R10,$LINELEN(,R10)
         MVI   CC,$SS                  Single SPACE
         MVC   PAD1,BLANKS
         LA    R9,19
                                       SPACE
PRINT_LINE     EQU *
         PUT   SYSPRINT,(R10)
                                       SPACE
         LA    R10,$LINELEN(,R10)
         BCT   R9,PRINT_LINE
                                       SPACE
         B     GETMORE
                                       SPACE
         $BOX  'Close files',TITLE='Cleanup'
DONE     EQU   *
         CLOSE (CAPTURE,,SYSPRINT)
                                       SPACE
RETURN   EQU   *
         LA    R0,$PAGELEN
         $BOX  'Free storage'
         FREEMAIN RU,LV=(0),A=(R11)
                                       SPACE
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
                                       SPACE
         $BOX  'ERRORS',TITLE='ERRORS'
ERR_OPN  EQU   *
         WTO   MF=(E,OPENMSG)
         LA    R15,1
         B     RETURN
                                       SPACE
         $BOX  'CONSTANTS',TITLE='CONSTANTS'
         DS    0D
SAVEAREA DS    18F
WA       DS    D
BLKPP    DC    F'3'                    BLOCKS/PAGE
BLK#     DC    F'0'
COUNT    DC    F'0'
RECORDLN DC    F'0'
ZEROS    DC    C'00000000'
VALFLD   DS    CL8
BLANKS   DC    CL16' '
         PRINT NOGEN
CAPTURE  DCB   DSORG=PS,MACRF=GL,DDNAME=CAPTURE,EODAD=DONE,RECFM=FB
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      *
               RECFM=FBA,LRECL=100,BLKSIZE=100
OPENMSG  WTO   'Error: OPEN failed',ROUTCDE=11,MF=L
         DS    0H
VALMVN   MVN   VALFLD(*-*),2(R1)
VALCLC   CLC   VALFLD(*-*),2(R1)
VALPACK  PACK  WA,2(*-*,R1)
MVCPAGE  MVC   0(*-*,R1),0(R6)
EDMASKP  DC    X'402020202121'
SEQUENCE DC    C'Screen: '
COUNTP   DS    CL6
SEQLEN   EQU   *-SEQUENCE
         SPACE 3
         $REGS
         END
