DCMDSPLY CSECT
DCMDSPLY AMODE 31
DCMDSPLY RMODE 24
         $BOX   HDR='DCMDSPLY - ',TITLE='DSECTS'
         $DAB
         EJECT
         $STATSD
         EJECT
         PRINT  NOGEN
         $IOB
         $CCW
         $OPEN
         $DCB
         $SCREEN
         PRINT  GEN
*  TPUT/TGET ---
*    R0 - Length
*    R1 - flags / buffer address
*        1... ....  - TGET
*        .xx. ....  - reserved
*        ...1 ....  - nowait
*        .... 1...  - hold
*        .... .1..  - break
*        .... ..00  - edit
*        .... ..01  - asis
*        .... ..10  - control
*        .... ..11  - fullscr
*
* Return Codes --- TGET
* 00  - all ok, R1 = input length
* 04  - nowait and no input was available
* 08  - attention interupt occured during TGET
* 0c  - buffer not large enougth (next TGET will get next chunk)
* 10  - invalid parms
* 14  - terminal disconnected
*              --- TPUT
* 00  - all ok
* 04  - nowait and no buffers available
* 08  - attention interupt occured during TPUT
* 0c  - TPUT with tjid refused by user
* 10  - invalid parms
* 14  - terminal disconnected
*
         $BOX  'INITIALIZATION',TITLE='INITIALIZATION'
*
*  DCMDSPLY really runs in 24 bit since PUT may be used.
*
DCMDSPLY CSECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING DCMDSPLY,R12
         USING DAB,R11
                                       SPACE
         LA    R10,72(,R13)
         ST    R13,4(,R10)
         ST    R10,8(,R13)
         LR    R13,R10
                                       SPACE
         L     R1,D_SCRN@
         LR    R2,R1
         USING SCREEN,R1
                                       SPACE
         MVC   SERRMSG,D_ERRMSG        MOVE ERROR MESSAGE
         LA    R0,$SCRNLEN
         OI    D_FLG1,$FSCRN
         O     R1,TPUTFLG
         TPUT  (1),(0),R
                                       SPACE
         LTR   R15,R15
         BNZ   DONE
                                       SPACE
         TM    D_FLG1,$CAPTURE
         BZ    DONE
                                       SPACE
         $AMODE 24
                                       SPACE
         PUT   CAPDCB,(R2)             SAVE SCREEN IMAGE
                                       SPACE
         XR    R15,R15
                                       SPACE
DONE     EQU   *
         XC    D_ERRMSG(L'D_ERRMSG),D_ERRMSG
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BSM   0,R14                   R15 contains TPUT return code
                                       SPACE
         $BOX  'CONSTANTS',TITLE='CONSTANTS'
         DS    0D
TPUTFLG  DC    X'13000000'             TPUT FULLSCREEN + NOWAIT
         SPACE 3
         $REGS
         END
