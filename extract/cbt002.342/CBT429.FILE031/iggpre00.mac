IGGPRE00 TITLE 'DADSM PRE-PROCESSING EXIT'
*
*
*        DOCUMENTATION NOTE:
*
*        THIS EXIT WILL ALLOW DASD ALLOCATION OF NEW DATASETS ONLY ON
*        VOLUMES MOUNTED STORAGE OR PUBLIC UNLESS THE USER HAS BEEN
*        GIVEN RACF "OPERATIONS".
*
*
*
         EJECT
*
*        FOR DOCUMENTATION, SEE GC26-4149, MVS/XA SYSTEM-DATA
*        ADMINISTRATION, CHAPTER 5 ON EXIT ROUTINES.
*
*
*        ASSEMBLY REQUIRES SYS1.MACLIB AND SYS1.AMODGEN
*
*
*        LINKEDIT ATTRIBUTES: RENT
*
*
*        CHANGES:
*        1.0 MVS 10-02-86 REJECT ANY NEW ALLOCATION REQUESTS NOT
*                         DIRECTED TO A VOLUME WHICH IS MOUNTED
*                         AS STORAGE OR PUBLIC.
*
*
*        REGISTERS AT ENTRY TO DADSM EXIT
*
* REG1  =>  ADDRESS OF PARAMETER LIST
* REG13 =>  ADDRESS OF AN 18 WORD SAVE AREA
* REG14 =>  RETURN ADDRESS TO DADSM
* REG15 =>  ADDRESS OF EXIT ROUTINE
*
*        REGISTERS AT RETURN FROM DADSM EXIT
*
* REG0  =>
*  THRU     SAME AS ON ENTRY TO YOUR EXIT ROUTINE
* REG14 =>
* REG15 =>  A RETURN CODE FROM IGGPRE00
*
*        RETURN CODES FROM DADSM EXITS
*
* X'00' =>  PROCESS REQUEST
* X'04' =>  DO NOT PROCESS REQUEST FOR CURRENT VOLUME
* X'08' =>  DO NOT PROCESS REQUEST AT ALL
         EJECT
*        IN ORDER TO TEST IGGPRE00 THE FOLLOWING MUST BE DONE:
*              1). LINK THE NEW IGGPRE00 INTO SYS1.LINKLIB.
*              2). BUILD LPA MEMBER IN PARMLIB.
*              3). IPL WITH A MLPA.
*              4). TEST OUT THE NEW IGGPRE00.
*        IF IT WORKS, THEN ALL IS WELL.
*              1). LINK THE NEW IGGPRE00 INTO SYS1.LPALIB.
*              2). DELETE THE NEW IGGPRE00 FORM SYS1.LINKLIB.
*              3). IPL WITH A CLPA.
*        IF IT DOES NOT WORK, THEN:
*              1). IPL
         EJECT
**********************************************************************
*        SET UP NORMAL LINKAGE AND USE REGISTER 12 AS THE BASE REG.
*        AND REGISTER 10 AS THE POINTER TO THE LIST OF ADDRESSES
*        PASSED TO IGGPRE00.
**********************************************************************
         EJECT
IGGPRE00 CSECT
         SAVE  (14,12),,IGGPRE00_&SYSTIME_&SYSDATE
         LR    R12,R15             SET UP R12 AS BASE REGISTER
         USING IGGPRE00,R12        ESTABLISH PROGRAM BASE REGISTER
         LR    R10,R1              FETCH PARM ADDRESS
         USING IEXID,R10           ESTABLISH PARM LIST BASE REGISTER
         TM    IEXFLAG,IEXVIO      IF THIS IS A VIO ALLOCATION
         BO    OKALLOC             THEN ALLOW ALLOCATION
         L     R4,IEXUCB           POINT TO UCB
         USING UCBCMSEG,R4         ADDRESS DSECT
         L     R5,IEXPTR1          POINT TO JFCB
         USING INFMJFCB,R5         ADDRESS DSECT
TEST     DS    0H
         CLI   IEXFUNC,IEXALL      IS REQUEST FOR ALLOCATION
         BE    CHECK               LETS VERIFY THIS REQUEST
         CLI   IEXFUNC,IEXEXT      IS REQUEST FOR EXTENSION
         BNE   OKALLOC             THEN ALLOW ALLOCATION
CHECK    DS    0H
         TM    UCBTYP+2,UCB3DACC   IS ALLOCATION FOR DASD
         BNO   OKALLOC             IF NOT ALLOW ALLOCATION
         TM    UCBSTAB,X'10'       IF VOLUME IS NOT MOUNTED PRIVATE
         BNO   OKALLOC             THEN ALLOW ALLOCATION
         TM    JFCBIND2,JFCNEW     IF DISP NEW WAS SPECIFIED
         BNO   OKALLOC             IF NOT ALLOW ALLOCATION
         CLC   0(3,R5),=C'SYS'     ALLOCATING A SYSXXXX DATASET
         BE    OKALLOC             THEN ALLOW ALLOCATION
         LA    R6,0                POINT TO PSA
         L     R6,548(R6)          POINT TO ASCB
         USING ASCB,R6             ADDRESS  ASCB
         L     R6,ASCBASXB         POINT TO ASXB
         USING ASXB,R6             ADDRESS  ASXB
         L     R6,ASXBSENV         POINT TO ACEE
         USING ACEE,R6             ADDRESS  ACEE
         TM    ACEEFLG1,ACEEOPER   DOES THIS USER HAVE OPERATIONS
         BNO   NOALLOC             IF NOT THEN DO NOT ALLOW ALLOCATION
         EJECT
OKALLOC  DS    0H
         LA    R15,0               ALLOW THE REQUEST
         B     EXIT
         SPACE 2
NOALLOC  DS    0H
         LA    R15,8               REJECT THE REQUEST
         SPACE 2
EXIT     DS    0H
*        L     R13,4(R13)          GET PREVIOUS SAVEAREA @
         RETURN (14,12),T,RC=(15)  RETURN TO CALLER
         EJECT
         TITLE 'REGISTER EQUATES'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'PARM LIST DSECT'
         IECIEXPL
         TITLE 'ACEE DSECT'
         IHAACEE
         TITLE 'ASCB DSECT'
         IHAASCB
         TITLE 'ASXB DSECT'
         IHAASXB
         TITLE 'JFCB DSECT'
         IEFJFCBN
         TITLE 'UCB DSECT'
         IEFUCBOB
         END
