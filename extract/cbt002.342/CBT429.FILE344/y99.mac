         /*Copyright 7 Dec 1999 by JBG REXX Y99                      */
         /*----------------------------------------------------------*/
         /* The author can be reached at joerg.berning(at)gmx.net    */
         /*----------------------------------------------------------*/
         trace off
         address isredit
         "MACRO"
         "(MYDSN) = DATASET"
         "(MYMEM) = MEMBER"
         myDSN = strip(myDSN,b,"'")
         address ispexec
         "LMINIT DATAID(MYDATAID) DATASET('"myDSN"')"
         if RC <> 0 then
           do
             say zErrLM
             exit
           end
         "LMOPEN DATAID("myDataID") OPTION(INPUT)"
         if RC <> 0 then
           do
             say zErrLM
             exit
           end
         "LMMFIND DATAID("myDataID") MEMBER("myMem") STATS(YES)"
         if RC <> 0 then
           do
             say zErrLM
             exit
           end
         myZlVers = zlVers
         myZlMod = zlMod
         myZlCDate = zlCDate
         myZlC4Date = zlC4Date
         myZlMDate = "99/01/01"
         myZlM4Date = "1999/01/01"
         myZlMTime = "11:11:11"
         myZlCNoRc = zlCNoRc
         myZlINoRC = zlINoRC
         myZlMNoRC = zlMNoRC
         myZlUser = zlUser
         "LMMSTATS",
           "MEMBER("myMem")",
           "DATAID("myDataID")",
           "VERSION("myZlVers")",
           "MODLEVEL("myZlMod")",
           "CREATED("myZlCDate")",
           "MODDATE("myZlMDate")",
           "MODTIME("myZlMTime")",
           "CURSIZE("myZlCNoRC")",
           "INITSIZE("myZlINoRC")",
           "MODRECS("myZlMNoRC")",
           "USER("myZlUser")",
           "CREATED4("myZlC4Date")",
           "MODDATE4("myZlM4Date")"
         "LMCLOSE DATAID("myDataID")"
         "LMFREE DATAID("myDataID")"
