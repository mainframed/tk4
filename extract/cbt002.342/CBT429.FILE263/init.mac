         MACRO
&NAME    INIT  &AREA,&LV=8,&FILL=00
         LCLA  &L
         AIF   (T'&AREA EQ 'O').ERR1
         AIF   ('&LV'(1,1) EQ '(').A
&L       SETA  &LV
         AIF   (&L LT 513).MVC
.A       AIF   ('&AREA'(1,1) EQ '(').B
&NAME    LA    0,&AREA             LOAD AREA ADDRESS
         AGO   .C
.B       AIF   ('&AREA' EQ '(0)').NAMEIT
&NAME    LR    0,&AREA(1)          LOAD AREA ADDRESS
         AGO   .C
.NAMEIT  ANOP
&NAME    DS    0H
.C       LR    14,0                LOAD AREA ADDRESS
         AIF   ('&LV'(1,1) EQ '(').D
         AIF   (&L LT 4096).E
         L     1,=F'&L'            LOAD LENGTH
         AGO   .F
.D       AIF   ('&LV' EQ '(1)').F
         LR    1,&LV(1)            LOAD LENGTH
         AGO   .F
.E       LA    1,&L.(0,0)          LOAD LENGTH
.F       AIF   ('&FILL' EQ '00').X
         IC    15,=X'&FILL'        LOAD PADDING CHARACTER
         SLL   15,24               SHIFT TO HIGH ORDER BYTE
         AGO   .Y
.X       SR    15,15               SET PADDING CHAR. TO X'00'
.Y       MVCL  0,14                INITIALISE STORAGE
         MEXIT
.MVC     AIF   ('&FILL' EQ '00').XOR
         AIF   ('&AREA'(1,1) EQ '(').G
&NAME    MVI   &AREA,X'&FILL'      MOVE PADDING CHAR. TO FIRST BYTE
&L       SETA  &L-1
         AGO   .H
.G       ANOP
&NAME    MVI   0&AREA,X'&FILL'     MOVE PADDING CHAR. TO FIRST BYTE
&L       SETA  &L-1
.H       AIF   (&L GT 256).I
         AIF   ('&LV' EQ '1').END
         AIF   ('&AREA'(1,1) EQ '(').J
         MVC   &AREA+1(&L),&AREA   INITIALISE STORAGE
         MEXIT
.J       MVC   1(&L,&AREA(1)),0&AREA    INITIALISE STORAGE
         MEXIT
.I       AIF   ('&AREA'(1,1) EQ '(').K
         MVC   &AREA+1(256),&AREA  INITIALISE FIRST 256 BYTES
&L       SETA  &L-256
         MVC   &AREA+256(&L),&AREA+255  INITIALISE REST OF STORAGE
         MEXIT
.K       MVC   1(256,&AREA(1)),0&AREA   INITIALISE FIRST 256 BYTES
&L       SETA  &L-256
         MVC   256(&L,&AREA(1)),255&AREA    INITIALISE REST OF STORAGE
         MEXIT
.XOR     AIF   (&L LT 257).Z
         AIF   ('&AREA'(1,1) EQ '(').M
&NAME    XC    &AREA.(256),&AREA   CLEAR FIRST 256 BYTES
         AGO   .N
.M       ANOP
&NAME    XC    0(256,&AREA(1)),0&AREA   CLEAR FIRST 256 BYTES
.N       ANOP
&L       SETA  &L-256
         AIF   ('&AREA'(1,1) EQ '(').O
         XC    &AREA+256(&L),&AREA+256  CLEAR REST OF AREA
         MEXIT
.O       XC    256(&L,&AREA(1)),256&AREA  CLEAR REST OF AREA
         MEXIT
.Z       AIF   (&L EQ 1).P
         AIF   ('&AREA'(1,1) EQ '(').Q
&NAME    XC    &AREA.(&L),&AREA    CLEAR STORAGE
         MEXIT
.Q       ANOP
&NAME    XC    0(&L,&AREA(1)),0&AREA    CLEAR STORAGE
         MEXIT
.P       AIF   ('&AREA'(1,1) EQ '(').R
&NAME    MVI   &AREA,X'00'         MOVE LOW VALUES INTO STORAGE
         MEXIT
.R       ANOP
&NAME    MVI   0&AREA,X'00'        MOVE LOW VALUES INTO STORAGE
         MEXIT
.ERR1    MNOTE 8,'AREA ADDRESS NOT SPECIFIED'
.END     MEND
