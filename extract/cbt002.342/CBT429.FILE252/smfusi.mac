//SMFUSI   JOB 5304010530000000,'TECH.SUPP.DANBOWEN',NOTIFY=TECH103,
//             CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),REGION=4096K
/*JOBPARM SYSAFF=SYSA
//*
//* DOC: THIS JOB WILL ASSEMBLE THE SMF ( IEFUSI ) INSTALLATION EXIT
//*      INTO ( SYS1.TECH.COMMON.LPALIB ).
//*      THIS SMF EXIT ROUTINE WILL SET THE DEFAULT/LIMIT REGION SIZE
//*      AND GETMAIN REQUESTS BELOW AND ABOVE 16MB FOR ALL JOBS.
//*      THE REGION AND GETMAIN LIMITS BELOW 16MB WILL BE DEFINED
//*      AS FOLLOWS:
//*      . BATCH, TSO, AND STC - SET TO REGION SIZE REQUESTED, BUT NOT
//*        TO EXCEED THE SIZE OF THE SYSTEM'S PRIVATE AREA.
//*      THE REGION AND GETMAIN LIMITS ABOVE 16MB WILL BE DEFINED
//*      AS FOLLOWS:
//*      . BATCH = 128MB   ( BATMAXA )
//*      . STC   = 256MB   ( STCMAXA )
//*      . TSO   = 32MB    ( TSOMAXA )
//*
//ASSEM  EXEC  PGM=IEV90,
//        PARM='OBJECT,XREF(SHORT),NODECK,LIST,RENT'
//SYSLIB   DD  DSN=SYSM.MVSESA.SMPSTS,DISP=SHR
//         DD  DSN=SYSM.MVSESA.SMPMTS,DISP=SHR
//         DD  DSN=SYSM.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(3,1)),DSN=&SYSUT1
//SYSPUNCH DD  DUMMY
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(1,1,0)),
//         DCB=(BLKSIZE=400),DSN=&&LOADSET
//SYSIN    DD  *
IEFUSI   TITLE 'SMF STEP INITIATION EXIT ROUTINE                '
         SPACE 3
IEFUSI   CSECT ,                       CSECT NAME DECLARED
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
         EJECT
         SAVE  (14,12),,IEFUSI_&SYSDATE._&SYSTIME
         LR    R12,R15                 LOAD BASE REG WITH ENTRY POINT
         USING IEFUSI,R12              SET UP BASE ADDRESSABILITY
*
* NOTE THAT NO SAVE_AREA WAS GETMAINED SINCE THIS ROUTINE
*      CALLS NO OTHER ROUTINE OR ISSUES ANY SVC'S
*
         LR    R08,R01                 LOAD ADDRESS PARM LIST
         USING EXTPARM,R08           ADDRESSABILITY TO PARM LIST DSECT
         L     R11,EXTAEPA           LOAD ADDRESS OF EXIT PARM AREA
         USING JMR,R11               MAP SMF EXIT PARM AREA TO JMR
         CLC   EXTASTPN(8),=C'TECHSERV'  CHECK FOR MAINT STEP NAME
         BE    EXITRET                   IF MAINT STEP NAME THEN EXIT
**********************************************************************
*  HANDLE REGION / GETMAIN LIMITS - CHECK ASID REGION TYPE           *
**********************************************************************
         ICM   R07,15,EXTAVSMP       FETCH POINTER TO VSM PARM LIST
         USING VSMPARM,R07           ADDRESSABILITY TO VSM PARMS DSECT
         BZ    EXITRET               BYPASS IF NOT PROVIDED
         ICM   R01,15,EXTASRMI       FETCH POINTER TO SRM INFO WORD
         BZ    EXITRET               BYPASS IF NOT PROVIDED
         TM    0(R01),X'80'          TEST IF V=R JOB?
         BO    EXITRET               BYPASS IF V=R JOB
         L     R05,CVTPTR            LOAD ADDRESS OF CVT
         USING CVTMAP,R05            ADDRESSABILITY TO CVT
*
*        FETCH PRIVATE AREA SIZE FROM GDA.
*
         L     R14,CVTGDA              LOAD ADDRESS OF GDA
         L     R14,GDAPVTSZ-GDA(,R14)
         DROP  R05                     RELEASE CVT BASE REG
         L     R05,PSATOLD-PSA         LOAD ADDRESS OF OUR ASCB
         USING ASCB,R05                ADDRESSABILITY TO ASCB
         L     R05,ASCBOUCB            LOAD ADDRESS OF OUCB
         DROP  R05                     RELEASE ASCB BASE REG
         USING OUCB,R05                ADDRESSABILITY TO OUCB
         LA    R15,1                   PRESET STC SWITCH (R15 > 0)
         CLC   OUCBSUBN(3),=C'STC'     TEST IF STARTED TASK?
         BE    STCRGNB1                YES, GO SET STC LIMITS
         LNR   R15,R15                 PRESET TSO SWITCH (R15 < 0)
         CLC   OUCBSUBN(3),=C'TSO'     TEST IF TSO SESSION?
         BE    TSORGNB1                YES, GO SET TSO LIMITS
         SR    R15,R15                 SET BATCH SWITCH (R15 = 0)
         DROP  R05                     RELEASE OUCB BASE REG
         B     BATRGNB1                GO SET BATCH LIMITS
**********************************************************************
*  HANDLE STC REGION LIMITS BELOW 16MB                               *
*  -----------------------------------                               *
*  . SET REGION SIZE EQUAL TO SYSTEM'S PRIVATE AREA SIZE MINUS 64KB. *
*  . SET GETMAIN LIMIT EQUAL TO SYSTEM'S PRIVATE AREA SIZE.          *
**********************************************************************
STCRGNB1 DS    0H
         LTR   R01,R14                FETCH SYSTEM'S PRIVATE AREA SIZE
         BZ    STCRGNB2               BYPASS IF 0 OR NOT AVAILABLE
         S     R01,K64                SUBTRACT 64KB FOR REGION SIZE
         LR    R02,R14                SET GETMAIN TO LIMIT PRIVATE AREA
         ST    R01,VSMRLIMB           STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB           STORE GETMAIN LIMIT BELOW 16M
         B     STCRGNA1               GO SET LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE STC REGION LIMITS BELOW 16MB                               *
*  -----------------------------------                               *
*  . SET REGION SIZE BELOW 16M TO INSTALLATION DEFINED VALUE - 8MB.  *
*  . SET GETMAIN LIMIT TO REGION SIZE LIMIT + 64KB.                  *
**********************************************************************
STCRGNB2 DS    0H
         L     R00,MAXBELOW          LOAD INSTALLATION MAX BELOW 16MB
         LR    R01,R00               SET REGION SIZE BELOW 16MB
         LR    R02,R00               SET GETMAIN LIMIT BELOW 16MB
         A     R02,K64               ADD 64KB TO GETMAIN LIMIT
         ST    R01,VSMRLIMB          STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB          STORE GETMAIN LIMIT BELOW 16M
**********************************************************************
*  HANDLE STC REGION LIMITS ABOVE 16MB                               *
*  -----------------------------------                               *
*  . SET REGION SIZE ABOVE 16M TO INSTALLATION DEFINED VALUE - 96MB. *
*  . SET GETMAIN LIMIT TO REGION SIZE LIMIT.                         *
**********************************************************************
STCRGNA1 DS    0H
         L     R03,STCMAXA            LOAD STC MAX ABOVE 16MB
         LR    R01,R03                SET REGION SIZE ABOVE 16MB
         ST    R01,VSMRLIMA           STORE EXTENDED REGION LIMIT
         ST    R01,VSMGLIMA           USE SAME VALUE FOR EXT. GETMAIN
         OI    VSMFLAGW,X'80'         SET FLAGS TO USE DEFINED LIMITS
         NI    VSMFLAGW,255-X'40'     CHECK IF REGION < 16MB FITS
         OI    VSMFLAGW,X'20'         CHECK IF REGION > 16MB FITS
         B     EXITRET                EXIT, USE DEFINED VALUES
**********************************************************************
*  HANDLE BATCH REGION LIMITS BELOW 16MB                             *
*  -------------------------------------                             *
*  . SET REGION SIZE EQUAL TO SYSTEM'S PRIVATE AREA SIZE MINUS 64KB. *
*  . SET GETMAIN LIMIT EQUAL TO SYSTEM'S PRIVATE AREA SIZE.          *
**********************************************************************
BATRGNB1 DS    0H
         ICM   R00,15,VSMREGRQ        FETCH REQUESTED REGION SIZE
         BNZ   BATRGNB2               BYPASS IF REGION > 0
         LTR   R01,R14                FETCH SYSTEM'S PRIVATE AREA SIZE
         BZ    BATRGNB4               BYPASS IF 0 OR NOT AVAILABLE
         S     R01,K64                SUBTRACT 64KB FOR REGION SIZE
         LR    R02,R14                SET GETMAIN TO LIMIT PRIVATE AREA
         ST    R01,VSMRLIMB           STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB           STORE GETMAIN LIMIT BELOW 16M
         B     BATRGNA1               GO SET LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE BATCH REGION LIMITS BELOW 16MB                             *
*  -------------------------------------                             *
*  . USE REQUESTED REGION SIZE IF > MINBELOW, AND < MAXBELOW.        *
*  . IF < MINBELOW, SET TO INSTALLATION DEFINED MINIMUM.             *
*  . IF > MAXBELOW, SET TO INSTALLATION DEFINED MAXIMUM.             *
*  . ADD 64KB FOR THE GETMAIN LIMIT.                                 *
**********************************************************************
BATRGNB2 DS    0H
         C     R00,MINBELOW            IS REGION REQUEST < MINBELOW?
         BNL   BATRGNB3                NOT LESS, GO CHECK LIMITS
         L     R00,MINBELOW            USE INSTALLATION MINBELOW
         LR    R01,R00                 SET REGION SIZE BELOW 16MB
         LR    R02,R00                 SET GETMAIN LIMIT BELOW 16MB
         A     R02,K64                 ADD 64KB TO GETMAIN LIMIT
         ST    R01,VSMRLIMB            STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB            STORE GETMAIN LIMIT BELOW 16M
         B     BATRGNA1                GO SET LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE BATCH REGION LIMITS BELOW 16MB                             *
*  -------------------------------------                             *
*  . CHECK REQUESTED REGION SIZE.                                    *
*  . IF < PRIVATE AREA SIZE, USE REQUESTED REGION SIZE.              *
*  . IF > PRIVATE AREA SIZE, SET TO PRIVATE AREA SIZE.               *
*  . IF PRIVATE AREA SIZE NOT AVAILABLE, USE INSTALLATION MAXBELOW.  *
**********************************************************************
BATRGNB3 DS    0H
         LR    R02,R00                SET GETMAIN TO REQUESTED SIZE
         LTR   R01,R14                FETCH SYSTEM'S PRIVATE AREA SIZE
         BZ    BATRGNB4               BYPASS IF 0 OR NOT AVAILABLE
         CR    R02,R14                IS GETMAIN LIMIT > PRIVATE AREA?
         BNH   BATRGNB5               NOT HIGH, GO USE REQUESTED SIZE
**********************************************************************
*  HANDLE BATCH REGION LIMITS BELOW 16MB                             *
*  -------------------------------------                             *
*  . SET REGION SIZE TO MAXBELOW.                                    *
*  . SET GETMAIN LIMIT EQUAL TO REGION SIZE + 64KB.                  *
**********************************************************************
BATRGNB4 DS    0H
         L     R00,MAXBELOW          LOAD INSTALLATION MAX BELOW 16MB
         LR    R01,R00               SET REGION SIZE BELOW 16MB
         LR    R02,R00               SET GETMAIN LIMIT BELOW 16MB
         A     R02,K64               ADD 64KB TO GETMAIN LIMIT
         ST    R01,VSMRLIMB          STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB          STORE GETMAIN LIMIT BELOW 16M
         B     BATRGNA1              GO SET BATCH LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE BATCH REGION LIMITS BELOW 16MB                             *
*  -------------------------------------                             *
*  . USE REQUESTED REGION SIZE.                                      *
*  . SET GETMAIN LIMIT TO REGION SIZE REQUESTED.                     *
**********************************************************************
BATRGNB5 DS    0H
         LR    R01,R00                 SET REGION SIZE BELOW 16MB
         LR    R02,R00                 SET GETMAIN LIMIT BELOW 16MB
         ST    R01,VSMRLIMB            STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB            STORE GETMAIN LIMIT BELOW 16M
**********************************************************************
*  HANDLE BATCH REGION LIMITS ABOVE 16MB                             *
*  -------------------------------------                             *
*  . SET REGION SIZE ABOVE 16M TO INSTALLATION DEFINED VALUE - 32MB. *
*  . SET GETMAIN LIMIT TO REGION SIZE LIMIT.                         *
**********************************************************************
BATRGNA1 DS    0H
         L     R03,BATMAXA            LOAD BATCH MAX ABOVE 16MB
         LR    R01,R03                SET REGION SIZE ABOVE 16MB
         ST    R01,VSMRLIMA           STORE EXTENDED REGION LIMIT
         ST    R01,VSMGLIMA           USE SAME VALUE FOR EXT. GETMAIN
         OI    VSMFLAGW,X'80'         SET FLAGS TO USE DEFINED LIMITS
         NI    VSMFLAGW,255-X'40'     CHECK IF REGION < 16MB FITS
         OI    VSMFLAGW,X'20'         CHECK IF REGION > 16MB FITS
         B     EXITRET                EXIT, USE DEFINED VALUES
**********************************************************************
*  HANDLE TSO REGION LIMITS BELOW 16MB                               *
*  -------------------------------------                             *
*  . SET REGION SIZE EQUAL TO SYSTEM'S PRIVATE AREA SIZE MINUS 64KB. *
*  . SET GETMAIN LIMIT EQUAL TO SYSTEM'S PRIVATE AREA SIZE.          *
**********************************************************************
TSORGNB1 DS    0H
         ICM   R00,15,VSMREGRQ        FETCH REQUESTED REGION SIZE
         BNZ   TSORGNB2               BYPASS IF REGION > 0
         LTR   R01,R14                FETCH SYSTEM'S PRIVATE AREA SIZE
         BZ    TSORGNB4               BYPASS IF 0 OR NOT AVAILABLE
         S     R01,K64                SUBTRACT 64KB FOR REGION SIZE
         LR    R02,R14                SET GETMAIN TO LIMIT PRIVATE AREA
         ST    R01,VSMRLIMB           STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB           STORE GETMAIN LIMIT BELOW 16M
         B     TSORGNA1               GO SET LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE TSO REGION LIMITS BELOW 16MB                               *
*  -------------------------------------                             *
*  . USE REQUESTED REGION SIZE IF > MINBELOW, AND < MAXBELOW.        *
*  . IF < MINBELOW, SET TO INSTALLATION DEFINED MINIMUM.             *
*  . IF > MAXBELOW, SET TO INSTALLATION DEFINED MAXIMUM.             *
*  . ADD 64KB FOR THE GETMAIN LIMIT.                                 *
**********************************************************************
TSORGNB2 DS    0H
         C     R00,MINBELOW            IS REGION REQUEST < MINBELOW?
         BNL   TSORGNB3                NOT LESS, GO CHECK LIMITS
         L     R00,MINBELOW            USE INSTALLATION MINBELOW
         LR    R01,R00                 SET REGION SIZE BELOW 16MB
         LR    R02,R00                 SET GETMAIN LIMIT BELOW 16MB
         A     R02,K64                 ADD 64KB TO GETMAIN LIMIT
         ST    R01,VSMRLIMB            STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB            STORE GETMAIN LIMIT BELOW 16M
         B     TSORGNA1                GO SET LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE TSO REGION LIMITS BELOW 16MB                               *
*  -------------------------------------                             *
*  . CHECK REQUESTED REGION SIZE.                                    *
*  . IF < PRIVATE AREA SIZE, USE REQUESTED REGION SIZE.              *
*  . IF > PRIVATE AREA SIZE, SET TO PRIVATE AREA SIZE.               *
*  . IF PRIVATE AREA SIZE NOT AVAILABLE, USE INSTALLATION MAXBELOW.  *
**********************************************************************
TSORGNB3 DS    0H
         LR    R02,R00                SET GETMAIN TO REQUESTED SIZE
         LTR   R01,R14                FETCH SYSTEM'S PRIVATE AREA SIZE
         BZ    TSORGNB4               BYPASS IF 0 OR NOT AVAILABLE
         CR    R02,R14                IS GETMAIN LIMIT > PRIVATE AREA?
         BNH   TSORGNB5               NOT HIGH, GO USE REQUESTED SIZE
**********************************************************************
*  HANDLE TSO REGION LIMITS BELOW 16MB                               *
*  -------------------------------------                             *
*  . SET REGION SIZE TO MAXBELOW.                                    *
*  . SET GETMAIN LIMIT EQUAL TO REGION SIZE + 64KB.                  *
**********************************************************************
TSORGNB4 DS    0H
         L     R00,MAXBELOW          LOAD INSTALLATION MAX BELOW 16MB
         LR    R01,R00               SET REGION SIZE BELOW 16MB
         LR    R02,R00               SET GETMAIN LIMIT BELOW 16MB
         A     R02,K64               ADD 64KB TO GETMAIN LIMIT
         ST    R01,VSMRLIMB          STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB          STORE GETMAIN LIMIT BELOW 16M
         B     TSORGNA1              GO SET TSO LIMITS ABOVE 16MB
**********************************************************************
*  HANDLE TSO REGION LIMITS BELOW 16MB                               *
*  -------------------------------------                             *
*  . USE REQUESTED REGION SIZE.                                      *
*  . SET GETMAIN LIMIT TO REGION SIZE REQUESTED.                     *
**********************************************************************
TSORGNB5 DS    0H
         LR    R01,R00                 SET REGION SIZE BELOW 16MB
         LR    R02,R00                 SET GETMAIN LIMIT BELOW 16MB
         ST    R01,VSMRLIMB            STORE REGION LIMIT BELOW 16M
         ST    R02,VSMGLIMB            STORE GETMAIN LIMIT BELOW 16M
**********************************************************************
*  HANDLE TSO REGION LIMITS ABOVE 16MB                               *
*  -------------------------------------                             *
*  . SET REGION SIZE ABOVE 16M TO INSTALLATION DEFINED VALUE - 32MB. *
*  . SET GETMAIN LIMIT TO REGION SIZE LIMIT.                         *
**********************************************************************
TSORGNA1 DS    0H
         L     R03,TSOMAXA            LOAD TSO MAX ABOVE 16MB
         LR    R01,R03                SET REGION SIZE ABOVE 16MB
         ST    R01,VSMRLIMA           STORE EXTENDED REGION LIMIT
         ST    R01,VSMGLIMA           USE SAME VALUE FOR EXT. GETMAIN
         OI    VSMFLAGW,X'80'         SET FLAGS TO USE DEFINED LIMITS
         NI    VSMFLAGW,255-X'40'     CHECK IF REGION < 16MB FITS
         OI    VSMFLAGW,X'20'         CHECK IF REGION > 16MB FITS
*
* RETURN TO CALLER
*
EXITRET  DS    0H
         LM    R14,R12,12(R13)         RESTORE CALLER REGISTERS
         SLR   R15,R15                 SET RETURN CODE TO ZERO
         BSM   0,14                    RETURN TO CALLER IN ITS MODE
         EJECT
*
*   CONSTANTS TO DEFINE INSTALLATION DEFAULT MINIMUM AND MAXIMUM SIZES
*
K64      DC    A(64*1024)              64KB CONSTANT
MINBELOW DC    A(6*1024*1024-64*1024)  MAX BELOW 16MB = 6MB MINUS 64KB
MAXBELOW DC    A(8*1024*1024-64*1024)  MAX BELOW 16MB = 8MB MINUS 64KB
BATMAXA  DC    A(128*1024*1024)        BATCH MAX EXTENDED ABOVE = 128MB
STCMAXA  DC    A(256*1024*1024)        STC MAX EXTENDED ABOVE = 256MB
TSOMAXA  DC    A(32*1024*1024)         TSO MAX EXTENDED ABOVE = 32MB
         LTORG
*
*   REGISTER EQUATES
*
R00      EQU   00                      REGISTER 0
R01      EQU   01                      REGISTER 1
R02      EQU   02                      REGISTER 2
R03      EQU   03                      REGISTER 3
R04      EQU   04                      REGISTER 4
R05      EQU   05                      REGISTER 5
R06      EQU   06                      REGISTER 6
R07      EQU   07                      REGISTER 7
R08      EQU   08                      REGISTER 8
R09      EQU   09                      REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
R12      EQU   12                      REGISTER 12
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
*
*   EXIT PARAMATER LIST DSECT
*
EXTPARM  DSECT                     POINTED BY REGISTER ONE (R1)
EXTAEPA  DS    A                   ADDRESS OF SMF EXIT PARAMETER LIST
EXTASTPN DS    A                   POINTER TO JOB STEP NAME
EXTAPGM  DS    A                   POINTER TO PROGRAM NAME FROM EXEC
EXTASACI DS    A                   POINTER TO STEP ACCOUNTING INFO
EXTAVSMP DS    A                   POINTER TO VSM PARM LIST (VSMPARM)
EXTASRMI DS    A                   POINTER TO SRM INFO FULLWORD
EXTARSMP DS    A                   POINTER TO RSM PARM LIST (RSMPARM)
*
*   VIRTUAL STORAGE MANAGEMENT (VSM) PARM LIST DSECT
*
VSMPARM  DSECT                     POINTED TO BY EXTAVSMP
VSMFLAGW DS    F                   VSM FLAG WORD
VSMREGRQ DS    F                   REGION SIZE REQUESTED
VSMGLIMB DS    F                   GETMAIN LIMIT BELOW 16MB
VSMRLIMB DS    F                   REGION LIMIT BELOW 16MB
VSMGLIMA DS    F                   GETMAIN LIMIT ABOVE 16MB
VSMRLIMA DS    F                   REGION LIMIT ABOVE 16MB
*
*   DSECT FOR COMMUNICATION VECTOR TABLE (CVT)
*
         PUSH  PRINT
         PRINT OFF
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         POP   PRINT
*
*   DSECT FOR ADDRESS SPACE CONTROL BLOCK (ASCB)
*
         PUSH  PRINT
         PRINT OFF
         IHAASCB
         POP   PRINT
*
*   DSECT FOR RESOURCES MANAGER USER CONTROL BLOCK (OUCB)
*
         PUSH  PRINT
         PRINT OFF
         IRAOUCB
         POP   PRINT
*
*   DSECT FOR PREFIXED SAVE AREA (PSA)
*
         PUSH  PRINT
         PRINT OFF
         IHAPSA
         POP   PRINT
*
*   DSECT FOR VSM GLOBAL DATA AREA (GDA)
*
         PUSH  PRINT
         PRINT OFF
         IHAGDA
         POP   PRINT
*
*   DSECT FOR SMF JOB MANAGEMENT RECORD (JMR)
*
         PUSH  PRINT
         PRINT OFF
         IEFJMR
         POP   PRINT
         END   IEFUSI
/*
//LNKEDT   EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL,XREF,RENT',
//           COND=(4,LT,ASSEM),REGION=256K
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLIB   DD DSN=SYS1.TECH.COMMON.LPALIB,DISP=SHR
//SYSLMOD  DD DSN=SYS1.TECH.COMMON.LPALIB(IEFUSI),DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  *
  ENTRY   IEFUSI
  NAME    IEFUSI(R)
/*
//
