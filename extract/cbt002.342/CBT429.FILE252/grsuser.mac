//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL,PARM.L='XREF,LIST,RENT,REUS,REFR,AC=1'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN  DD *
GRS      TITLE 'GQSCAN SERVICES'
GRSUSER  CSECT , /* GRSUSER */
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K24      EQU   24
K28      EQU   28
K32      EQU   32
K36      EQU   36
K40      EQU   40
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K255     EQU   255
K256     EQU   256
K264     EQU   264
K424     EQU   424
K512     EQU   512
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
GRSUSER  AMODE 24
GRSUSER  RMODE 24
         SAVE  (14,12),,GRSUSER*&SYSDATE*&SYSTIME
         LR    R3,R1 /* CPPL */
         USING CPPL,R3
         LR    RA,RF /*ADDRESSABILITY*/
         USING GRSUSER,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    R2,RD /*SAVEAREA HOUSEKEEPING*/
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         MODESET KEY=ZERO,MODE=SUP
         SLR   R9,R9 RC
         L     R0,=A(DWORDS)
         GETMAIN R,LV=(0)
         LR    RD,R1
         ST    RD,K8(,R2)
         ST    R2,K4(,RD)
         USING GRSWMAP,RD
         XC    PARSEPPL(K28),PARSEPPL /* PREP PARAM SCAN */
         XC    CMDPANS,CMDPANS
         XC    CMDPECB,CMDPECB
         LA    R8,PARSEPPL MAP
         USING PPL,R8
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    R0,CMDPECB
         ST    R0,PPLECB
         LA    R0,CMDPANS
         ST    R0,PPLANS
         L     R0,=A(IKJPARM)
         ST    R0,PPLPCL
         DROP  R8
         CALLTSSR EP=IKJPARS,MF=(E,(R8))
         LTR   R9,RF
         BNZ   MAINEXIT
         L     R4,CMDPANS /* RESPONSE */
         USING IKJPARMD,R4
         TM    USERPARM+K6,X80 DSN FOUND
         BZ    DSNERROR
         L     R5,USERPARM DSN PTR
*        TM    USERPARM+K6,X40 DSN IN QUOTES
*        BZ    NODSNQ    NO
*        LA    R5,K1(,R5) POSITION DSN PTR PAST FIRST QUOTE
*NODSNQ  EQU   * *
         LH    R6,USERPARM+K4 DSN LEN - NEVER INCL QUOTES
         MVC   GQSCANLX(GQSCANLL),GQSCANL
         GQSCAN AREA=(GQRESP,K4095),                                   X
               RESNAME=(QNAME,(R5),(R6),SPECIFIC,K8),MF=(E,GQSCANLX)
         LR    R9,RF SAVE RC
         LA    R1,CMDPANS
         IKJRLSA (R1)
         DROP  R4
         LTR   R9,R9 CHEK RC
         BZ    GQ0
         CH    R9,=H'8' TOOO MUCH DATA
         BE    GQ0
         B     NODATA   ERROR
GQ0      MVC   MSGRESP,=CL24' ' FILLER
         LA    R7,GQRESP
         USING RIB,R7
         L     R4,RIBNRIBE RIBE COUNT RETURNED
         LTR   R4,R4 CHECK
         BZ    NODATA
         LH    R5,RIBVLEN VAR RIB SECTION LEN
         LA    R7,RIBEND-RIB(R5,R7) /* START OF RIBES */
         DROP  R7
         USING RIBE,R7
DORIBE   TM    RIBESFLG,RIBESTAT RES OWNER
         BO    ISOWNER YES
NEXTRIBE LA    R7,RIBEEND-RIBE(,R7) NEXT RIBE
         BCT   R4,DORIBE LOOP
         B     MAINEXIT ALL DONE
ISOWNER  MVC   MSGRESP(L'RIBEJBNM),RIBEJBNM OWNER NAME
         MVC   MSGRESP+L'RIBEJBNM+K2(K9),=CL9'Exclusive' ASSUME
         TM    RIBERFLG,RIBETYPE CHECK SHR OR EXCL
         BZ    ISEXCL OK
         MVC   MSGRESP+L'RIBEJBNM+K2(K9),=CL9'Share'
ISEXCL   LA    R1,MSGRESP
         LA    R0,K24
         TPUT  (1),(0)
         LTR   R9,RF
         BZ    NEXTRIBE CONTINUE
MAINEXIT L     R2,K4(,RD)
         LR    R1,RD
         L     R0,=A(DWORDS)
         FREEMAIN R,A=(1),LV=(0)
         LR    RD,R2
         LR    RF,R9
         RETURN (14,12),RC=(15)
DSNERROR LA    R9,K255 DSN NOT FOUND
         B     MAINEXIT
NODATA   TPUT  NODMSG,L'NODMSG
         B     MAINEXIT
NODMSG   DC    C'Dsname is not currently being used; not serialized.'
         LTORG
IKJPARM  IKJPARM
USERPARM IKJPOSIT DSNAME,UPPERCASE,PROMPT='Required dsname ?'
         IKJENDP
GQSCANL  GQSCAN SCOPE=ALL,MF=L
GQSCANLL EQU    *-GQSCANL
QNAME    DC    CL8'SYSDSN'
GRSWMAP  DSECT
SAVEAREA DC    18F'0'
CMDPECB  DC    A(K0)
CMDPANS  DC    A(K0)
PARSEPPL DC    (K28)AL1(K0) PARSE PPL
GQSCANLX GQSCAN SCOPE=ALL,MF=L
MSGRESP  DC    (K24)C' '
GQRESP   DC    (K4095)AL1(K0)
DWORDS   EQU   ((((*-SAVEAREA)+7)/8)*8)
         PRINT NOGEN
         IKJPPL
         IKJCPPL
         CVT   DSECT=YES
         ISGRIB
         END
/*
//L.SYSLMOD DD DSN=IPO1.LINKLIB(WHOHAS),DISP=SHR,
//  UNIT=,SPACE=
