//TECH110Z JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
//S EXEC ASMHCL,PARM.L='XREF,LIST,AC=1'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN  DD *
RCF      TITLE 'REXX RACFUSER PROFILE RETRIEVAL'
RACFUSER CSECT , /* RACFUSER */
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K24      EQU   24
K28      EQU   28
K32      EQU   32
K36      EQU   36
K40      EQU   40
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K255     EQU   255
K256     EQU   256
K264     EQU   264
K424     EQU   424
K512     EQU   512
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
RACFUSER AMODE 31
RACFUSER RMODE 24
         SAVE  (14,12),,RACFUSER*&SYSDATE*&SYSTIME
         LR    R3,R1 /* CPPL */
         USING CPPL,R3
         LR    RA,RF /*ADDRESSABILITY*/
         USING RACFUSER,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    R2,RD /*SAVEAREA HOUSEKEEPING*/
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         SLR   R9,R9 RC
         LA    R0,DWORDS
         GETMAIN R,LV=(0)
         LR    RD,R1
         ST    RD,K8(,R2)
         ST    R2,K4(,RD)
         USING RACFWMAP,RD
         XC    PARSEPPL(K28),PARSEPPL /* PREP PARAM SCAN */
         XC    CMDPANS,CMDPANS
         XC    CMDPECB,CMDPECB
         LA    R8,PARSEPPL MAP
         USING PPL,R8
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    R0,CMDPECB
         ST    R0,PPLECB
         LA    R0,CMDPANS
         ST    R0,PPLANS
         L     R0,=A(IKJPARM)
         ST    R0,PPLPCL
         DROP  R8
         CALLTSSR EP=IKJPARS,MF=(E,(R8))
         LTR   R9,RF
         BNZ   MAINEXIT
         L     R4,CMDPANS /* RESPONSE */
         USING IKJPARMD,R4
         L     R5,USERPARM
         LH    R6,USERPARM+K4
         TM    USERPARM+K6,X80 UID FOUND
         BZ    UIDERROR
         STC   R6,USERID /* FOR RACF */
         BCTR  R6,K0
         EX    R6,MOVEUID
         LA    R1,CMDPANS
         IKJRLSA (R1)
         LA    R5,RACFWORK
         MVC   RACFWORK(K4),=A(K96)
         LA    R6,LXTEINTY
         LA    R7,LXTFNAME
         MVC   K0(LLLEINTY,R6),LSTEINTY MAKE RENT
         MVC   K0(LLLFNAME,R7),FNAME
         DROP  R4
         LA    R4,USERID
         ICHEINTY LOCATE,TYPE='USR',ENTRY=(R4),RELEASE=1.8.1,          X
               WKAREA=(R5),ACTIONS=(FNAME),OPTIONS=(ACTION),MF=(E,(R6))
         LTR   R9,RF
         BNZ   NOUSER
         LA    R1,RACFWORK+K32
         CLI   K0(R1),C' ' ANYTHING THERE
         BE    NODATA
         LA    R0,K20
         STM   R0,R1,VALUELEN ...
*        TPUT  (1),(0)
MAINEXT1 LINK  EP=IKJCT441,PARAM=(ECODE,NAMEPTR,NAMELEN,VALUEPTR,      X
               VALUELEN,TOKEN),VL=1
*        LTR   R9,RF ignore this RC
         B     MAINEXIT
*OUSER   TPUT  NOUSERM,L'NOUSERM
NOUSER   LA    R0,L'NOUSERM
         LA    R1,NOUSERM
         STM   R0,R1,VALUELEN ...
         B     MAINEXT1
*ODATA   TPUT  NODATAM,L'NODATAM
NODATA   LA    R0,L'NODATAM
         LA    R1,NODATAM
         STM   R0,R1,VALUELEN ...
         B     MAINEXT1
UIDERROR LA    R9,K255 /* UID NOT PRESENT */
MAINEXIT L     R2,K4(,RD)
         LR    R1,RD
         LA    R0,DWORDS
         FREEMAIN R,A=(1),LV=(0)
         LR    RD,R2
         LR    RF,R9
         RETURN (14,12),RC=(15)
MOVEUID  MVC   USERID+K1(*-*),K0(R5) ACQUIRE USERID
NOUSERM  DC    C'Invalid TSO/E userid entered.'
NODATAM  DC    C'RACF database programmer name field missing.'
         LTORG
ECODE    DC    A(TSVEUPDT) Set value
NAMEPTR  DC    A(NAME)
NAMELEN  DC    F'5'
VALUELEN DC    A(*-*) Keep together
VALUEPTR DC    A(*-*)
TOKEN    DC    F'0'
NAME     DC    C'WHOIS' Rexx var name
FNAME    ICHEACTN FIELD=PGMRNAME,RELEASE=1.8.1
LLLFNAME EQU   *-FNAME
LSTEINTY ICHEINTY LOCATE,TYPE='USR',ENTRY=*-*,RELEASE=1.8.1,           X
               WKAREA=*-*,ACTIONS=(FNAME),MF=L
LLLEINTY EQU   *-LSTEINTY
IKJPARM  IKJPARM
USERPARM IKJPOSIT USERID,UPPERCASE,PROMPT='Required TSO/E userid ?'
         IKJENDP
RACFWMAP DSECT
SAVEAREA DC    18F'0'
CMDPECB  DC    A(K0)
CMDPANS  DC    A(K0)
PARSEPPL DC    (K28)AL1(K0) PARSE PPL
LXTEINTY DC    (LLLEINTY)AL1(K0)
RACFWORK DC    A(K96),(K92)AL1(K0)
LXTFNAME DC    (LLLFNAME)AL1(K0)
USERID   DC    AL1(*-*),CL8' '
DWORDS   EQU   ((((*-SAVEAREA)+7)/8)*8)
         PRINT NOGEN
         IKJPPL
         IKJCPPL
         CVT   DSECT=YES
         IKJTSVT
         END
/*
//L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(WHOISX),DISP=SHR,
// UNIT=,SPACE=
