//TECH110L JOB (5304010530000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110
//S EXEC ASMHCL,PARM.L='AC=1,XREF,LIST'
//C.SYSIN DD *
CATEDIT  CSECT , /* CATEDIT EDIT AND UPDATE */
         SAVE  (14,12),,CATEDIT*&SYSDATE
         LR    12,15
         USING CATEDIT,12
         LR    11,13 /* CHAIN */
         CNOP  0,4
         BAL   13,*+76
         DC    18F'0'
         ST    13,8(,11)
         ST    11,4(,13)
         OPEN  (CATALOG,(UPDAT))
         TM    CATALOG+48,X'10' OPEN OK
         BZ    FAILOPEN NO
CATSCAN  GET   CATALOG
         LR    4,1 RCD PTR TO TRANSLATE
         LH    5,CATALOG+82 DCBLRECL
         LA    6,0(5,4) END POINT
         SH    6,=H'4' WE MUST NOT LOOK PAST END OF BUFFER, DO -4
         LR    8,4 STARTING SCAN POINT
         LR    9,5 WORKING LEN
NEXTLCHK LTR   9,9 ANY LEFT
         BNP   DOPUT NO
         C     9,=F'256' MAX AT A TIME
         BL    MINLOOK NEAR END
         TRT   0(256,8),SCANLOOK
TRTDONE  BZ    NEXTSECT LOOP
         CLC   =X'78008080',0(1) MATCH
         BE    REPL
         CLC   =X'78048080',0(1) MATCH
         BE    REPL
         LA    8,1(,1) NEXT START SCAN
CALCWLEN LR    9,6 WORK LEN
         SR    9,8
         B     NEXTLCHK
REPL     MVC   0(4,1),=X'78048081' 3490E DEVT
         LA    8,4(,1) FOR NEXT SCAN
         B     CALCWLEN ...
MINLOOK  BCTR  9,0 FOR EX
         EX    9,EXTRT DO
         B     TRTDONE
EXTRT    TRT   0(*-*,8),SCANLOOK
NEXTSECT LA    8,256(,8) NEXT FULL SECT
         B     CALCWLEN
DOPUT    PUTX  CATALOG UPDATE CAT IN PLACE
         B     CATSCAN
CATEND   CLOSE (CATALOG)
         FREEPOOL CATALOG
         SLR   15,15
RETURN   L     13,4(,13)
         RETURN (14,12),RC=(15)
FAILOPEN LA    15,24 /* OPEN FAILED */
         B     RETURN
         LTORG
CATALOG  DCB   DDNAME=CATALOG,MACRF=(GL,PL),EODAD=CATEND,RECFM=U,      X
               BLKSIZE=32760,DSORG=PS
SCANLOOK DC    256X'00'
         ORG   SCANLOOK+X'78'
         DC    X'FF'
         ORG
         END
/*
//L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(CATEDIT),DISP=SHR,UNIT=
//*G.CATALOG DD DSN=SYS1.UCATTSM.CLUSTER,DISP=SHR
//*G.SYSUDUMP DD SYSOUT=*
