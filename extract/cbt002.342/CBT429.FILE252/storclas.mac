/* BUILDING DEPOSIT, INC. SMS ACS STORCLASS ROUTINE                 */
/*                                                                  */
PROC &STORCLAS
/*                                                                  */
/* LIST OF VALID STORAGE CLASSES THAT MAY BE CODED WITHOUT RESTRICTN*/
FILTLIST &VALID_PUBLIC_STORAGE_CLASSES INCLUDE(
   'ANYWHERE',
   'PREALLOC',
   'NONSMS',
   '')
/* LIST OF VALID RESTRICTED STORAGE CLASSES                         */
FILTLIST &VALID_NONPUBLIC_STORAGE_CLASSES INCLUDE(
   'FASTREAD',
   'FASTWRIT')
/* COMPATABILITY MODE                                               */
FILTLIST &COMPAT_ANYWHERE INCLUDE(
   'SCSTNRD')
FILTLIST &COMPAT_PREALLOC INCLUDE(
   'SCSTNRDG','CICSR','CICSW','SYSSHARE','SYSMAINT')
/* DASD UNITS                                                       */
FILTLIST DASD_UNITS INCLUDE(
    '3380','3390','DISK','PAGEIO','SYSDA','SYSSQ','SYSTEST',
    'SMS','QA','PARTH',
    'VIO','SYSALLDA','APPLTA','SEQDA','SYSTA','USRDA','')
/*                                                                  */
/* TECHNOLOGY SERVICES TSO/E USERIDS AND PROD ZEKE SUBMITTOR        */
FILTLIST &TECH_SERV INCLUDE(TECH*,'ZEKE')
/*                                                                  */
/* SYSTEM DATASETS THAT DEFAULT TO PREALLOC                         */
FILTLIST &SYSHLQ INCLUDE(SYS%,DB2%,FDR*)
/*                                                                  */
/* NEW ALLOC HAVE ACSENVIR OF ALLOC OR RECOVER                      */
FILTLIST &ALLOC INCLUDE('ALLOC','RECOVER')
/*                                                                  */
/* ASSIGN A NEW STORAGE CLASS ONLY IF THIS IS NEW SMS ALLOCATION    */
/* I.E. IF RECALL THEN KEEP PREV ASSIGNED VALUE                     */
/* IF CONVERT PROCESS IN PLACE THEN SET STORCLAS=NULL; SO THAT      */
/* THIS WILL IN EFFECT PREVENT USAGE OF THIS FACILITY               */
/* ALL CONVERSIONS TO SMS SHOULD BE DONE BY DATASET                 */
/*                                                                  */
SELECT (&ACSENVIR)
/*                                                                  */
/* ALLOC=NEW DATASET ALLOC; RECOVER=DFDSS/FDR DATASET RESTORES      */
      WHEN (&ALLOC) DO
/* IGNORE IF NON-DASD ALLOCATION AND SET STORCLASS TO NULL          */
               IF &UNIT NE &DASD_UNITS THEN DO
                  SET &STORCLAS = ''
                  EXIT CODE(0)
                  END
/* FORCE ANYWHERE OR PREALLOC FOR ALL TRUE TEMP DATASETS            */
               ELSE IF &DSTYPE = 'TEMP' THEN DO
                  IF &ALLVOL = '' THEN SET &STORCLAS = 'PREALLOC'
                       ELSE SET &STORCLAS = 'ANYWHERE'
                  EXIT CODE(0)
                  END
/* RACF DFP RESOWNER INDICATES NONSMS                               */
                         ELSE IF &DSOWNER = 'NONSMS' THEN DO
                                 SET &STORCLAS = ''
                                 EXIT CODE(0)
                                 END
/* PERFORM AUTHORIZATION CHECKS IF RESTRICTED STORCLAS VALUE CODED  */
           ELSE IF &STORCLAS EQ &VALID_NONPUBLIC_STORAGE_CLASSES AND +
                  &XMODE NE 'TASK' AND +
                  &USER  NE &TECH_SERV THEN DO
                           SET &STORCLAS = 'ANYWHERE'
                           EXIT CODE(0)
                           END
/* PERMIT UNAUTH USERS TO CODE PREALLOC ONLY IF NO VOLSERS CODED    */
               ELSE IF &STORCLAS = 'PREALLOC' AND +
                  &ALLVOL NE '' AND +
                  &XMODE NE 'TASK' AND +
                  &USER  NE &TECH_SERV THEN DO
                           SET &STORCLAS = 'ANYWHERE'
                           EXIT CODE(0)
                           END
/*                                                                  */
/* PROCESS THE CODED STORCLAS VALUE                                 */
               ELSE SELECT (&STORCLAS)
/* FIRST PROCESS ALL NON-SMS DATASETS                               */
                     WHEN ('NONSMS') DO
                           SET &STORCLAS = ''
                           EXIT CODE(0)
                           END
/* ASSIGN SOMETHING IF STORCLAS NOT CODED                           */
                     WHEN ('') DO
/*      GIVE A BASIC DEFAULT OF ANYWHERE OR PREALLOC AS STORCLAS    */
                            IF &HLQ = &SYSHLQ THEN DO
                                   SET &STORCLAS = 'PREALLOC'
                                   EXIT CODE(0)
                                 END
                              ELSE DO
                                   IF &ALLVOL = '' THEN +
                                     SET &STORCLAS = 'PREALLOC'
                                   ELSE SET &STORCLAS = 'ANYWHERE'
                                 EXIT CODE(0)
                               END
                           END
/* ELSE PROCESS COMPATABILITY MODE                                  */
                     WHEN (&COMPAT_ANYWHERE) DO
                              SET &STORCLAS = 'ANYWHERE'
                              EXIT CODE(0)
                           END
                     WHEN (&COMPAT_PREALLOC) DO
                              SET &STORCLAS = 'PREALLOC'
                              EXIT CODE(0)
                           END
/* ELSE PROCESS VALID REMAINING STORAGE CLASSES                     */
                     WHEN (&VALID_PUBLIC_STORAGE_CLASSES) DO
                              SET &STORCLAS = &STORCLAS
                              EXIT CODE(0)
                           END
                     WHEN (&VALID_NONPUBLIC_STORAGE_CLASSES) DO
                              SET &STORCLAS = &STORCLAS
                              EXIT CODE(0)
                           END
/* OTHERWISE GARBAGE STORAGE CLASS WAS CODED; WE WILL OVERRIDE IT   */
                     OTHERWISE DO
                           SET &STORCLAS = 'ANYWHERE'
                           EXIT CODE(0)
                           END
                 END
           END
/*                                                                  */
/* IGNORE CONVERT REQUESTS                                          */
      WHEN ('CONVERT') DO
           SET &STORCLAS = ''
           EXIT CODE(0)
           END
/*                                                                  */
/* FALL THROUGH FOR RECALLS, ETC.                                   */
      OTHERWISE DO
           SET &STORCLAS = &STORCLAS
           EXIT CODE(0)
           END
END
/*                                                                  */
END
