DISPEXIT CSECT
         USING *,R15
         B     ENTRY
*
         DC    C'INPUTV1.0-&SYSDATE-&SYSTIME'  EYE CATCHER
*
*  ENTRY LOGIC
*
ENTRY     STM   R14,R12,12(R13)   STORE REGS
          LR    R12,R15
          USING DISPEXIT,R12
          DROP  R15
          LA    R14,SAVEAREA
          ST    R13,4(R14)
          ST    R14,8(R13)
          LR    R13,R14
*
*  THESE ARE THE PASSED PARMS
*  SEE THE SYSPROG GD FOR MORE INFO
*
* R2 = A(SMF RECORD)
* R3 = A(1 CHAR DDISP INDIC)
* R4 = A(1 CHAR INSERTION INDIC)
* R5 = A(USER DATA AREA)
*
          LM    R2,R5,0(R1)   GET PASSED PARMS
*
*  IF R3 = 'FF' THIS IS EOF
*
          CLI    0(R3),X'FF'    IS THIS EOF CALL?
          BE     FINISH   YES - FIN
*  SEE IF RECORD TYPE = 6 (PRINT RECORD)
*
          USING SMFDSECT,R2   ADDRESS THE SMF REC
          CLI   SMFTYPE,X'06'   IS IT A TYPE 6 REC?
          BNE   FINISH   NO- LEAVE EXIT
*
*   GOT A TYPE 6 REC
*
          CLC   SM6SBS,=X'0010'   DISPATCH REC?
          BE    DISPATCH    YES - GO PROCESS IT
          CLC   SM6JBN,=C'DISPATCH'    JES RECORD FOR DISPATCH?
          BE    DISPREC
          CLC   SM6JBN,=C'CADSXTD1'   EXTERNAL WRITER FROM DISPATCH?
          BE    DISPREC
          CLC   SM6JBN,=C'CADSXTD2'            "
          BE    DISPREC
          CLC   SM6JBN,=C'CADSXTD3'            "
          BE    DISPREC
          CLC   SM6JBN,=C'CADSXTD4'            "
          BE    DISPREC
          B     FINISH    NOT A DISPATCH-PRODUCED RECORD--KEEP IT
DISPREC   CNOP  2,4
          MVI   0(R3),X'FF'    YES - TELL JARS TO KILL THIS REC
          B     FINISH     FINISHED
*
*   CHECK HERE IF DISPATHC IS CREATING USER RECS INSTEAD OF TYPE 6'S
*
DISPATCH  EQU   *
*
*  MOVE WHAT IS DESIRED TO USER AREA HERE
*
*  FINISHED
FINISH    L     R13,4(R13)   GET ADDR OF CALLING SA
          LM    R14,R12,12(R13)    RESTORE CALLING REGS TO CALLING PGM
          BR    R14
          EJECT
*
*  CONSTANTS AND WORK AREAS
*
SAVEAREA  DC    18F'0'
R0        EQU   0
R1        EQU   1
R2        EQU   2
R3        EQU   3
R4        EQU   4
R5        EQU   5
R6        EQU   6
R7        EQU   7
R8        EQU   8
R9        EQU   9
R10       EQU   10
R11       EQU   11
R12       EQU   12
R13       EQU   13
R14       EQU   14
R15       EQU   15
          EJECT
          LTORG
*
*   CA DISPATCH SMF RECORD DSECT
*
SMFDSECT  DSECT
          SPACE  2
SM6LEN    DS     H        RECORD LENGTH
SM6SEG    DS     H        SEG DESCRIPTOR
SM6FLG    DS     XL1      X'02' FOR VS2 SYS
SMFTYPE   DS     XL1      REC TYPE
SM6TIMRP  DS     XL4      TIME REC DUMPED TO SMF BUFFER
SM6DATRP  DS     PL4      DATE REC DUMPED TO SMF BUFFER
SM6SID    DS     CL4      SYSTEM ID - FROM CVT
SM6JBLOG  DS     0CL16    JOB LOG FROM ORIG JOB
SM6JBN    DS     CL8      JOB NAME
SM6RST    DS     XL4      READER START NAME
SM6RSD    DS     PL4      READER START TIME  - 100S OF SEC
SM6UIF    DS     CL8      NOT USED - SPACES
SM6WC     DS     CL1      SYSOUT CLASS - USER DEFINED
SM6WST    DS     XL4      SAME AS SM6TIMRP
SM6WSD    DS     PL4      SAME AS SM6DATRP
SM6NLR    DS     XL4      NUMBER OF LINES - CALCULATED
SM6IOE    DS     XL1      NOT USED - BIN 0
SM6NDS    DS     XL1      SET TO X'01'
SM6FMN    DS     CL4      SYSOUT FORM # - USER DEFINED
SM6PAD1   DS     XL1      NOT USED -  BIN 0
SM6SBS    DS     XL2      X'0010'  - INDICATE DISPATCH
SM6LN1    DS     XL2      LENGTH OF REST OF RECORD
SM6DCI    DS     XL2      X'0010' - PLACED IN A BUNDLE
SM6JESNM  DS     CL4      JES JB#  FOR ORIGINAL JOB
SM6OUT    DS     CL8      NOT USED - SPACES
SM6FCB    DS     CL4      FCB - USER DEFINED
SM6UCS    DS     CL4      NOT USED - SPACES
SM6PGE    DS     XL4      PAGE COUNT- CALCULATED
SM6RTE    DS     XL2      NOT USED (X'0100')
SM6COPY   DS     PL2      NUMBER OF REPORT COPIES
*                         FOR 4.1 AND HIGHER, THIS NUMBER WILL
*                         ALWAYS BE 1 AND MULTIPLE TYPE 6'S WILL
*                         BE PRODUCED LIKE IBM DOES IT.
*
*
*  THE REMAINDER OF THE REC IS SPECIFIC TODCDS AND CONTAINS
*  DATA SPECIFIC TO A RECIPIENT
*
SM6DROP   DS     CL8      MAILDROP
SM6DEST   DS     CL8      DESTINATION
SM6JBNAM  DS     CL8      JOB NAME
SM6RTNAM  DS     CL8      REPORT NAME
SM6RCPNM  DS     CL16     RECIPIENT ID
SM6DESC   DS     CL64     RECIPIENT DESCRIPTION
SM6PHONE  DS     CL10     RECIPIENT PHNE NUM
SM6MINOR  DS     CL32     MINOR CHARGE CODE
SM6RPTDS  DS     CL32     REPORT DESCRIPTION
SMSIZE    EQU    *-SM6LEN SM6 LENGTH
          END
