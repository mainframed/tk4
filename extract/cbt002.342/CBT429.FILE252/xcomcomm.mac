//TECH110Z JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
//S EXEC ASMHCL,PARM.L='LIST,LET,XREF,MAP'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSIN  DD *
XCM      TITLE 'XCOM SYSA/SYSB NEGOTIATOR'
XCOMCOMM CSECT ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K21      EQU   21
K23      EQU   23
K24      EQU   24
K26      EQU   26
K27      EQU   27
K28      EQU   28
K29      EQU   29
K30      EQU   30
K32      EQU   32
K34      EQU   34
K36      EQU   36
K38      EQU   38
K40      EQU   40
K43      EQU   43
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K57      EQU   57
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K133     EQU   133
K136     EQU   136
K144     EQU   144
K152     EQU   152
K176     EQU   176
K196     EQU   196
K208     EQU   208
K212     EQU   212
K255     EQU   255
K256     EQU   256
K272     EQU   272
K320     EQU   320
K550     EQU   550
K2047    EQU   2047
K2048    EQU   2048
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
XCOMCOMM AMODE 24
XCOMCOMM RMODE 24
         SAVE  (14,12),,XCOMCOMM*&SYSDATE*&SYSTIME
         LR    RA,RF /*ADDRESSABILITY*/
         USING XCOMCOMM,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    RF,RD /*SAVEAREA HOUSEKEEPING*/
         CNOP  K0,K4
         BAL   RD,*+K76
         DC    (K18)AL4(K0)
         ST    RD,K8(,RF)
         ST    RF,K4(,RD)
         LTR   R1,R1 TEST PARAM
         BZ    FPARAM1
         TM    K0(R1),X'80' LAST PARAM
         BNO   FPARAM2
         L     R3,K0(,R1) PASSED PARAM PTR
         LH    R4,K0(,R3) LEN OF PARM
         LTR   R4,R4
         BZ    FPARAM3
         CH    R4,=H'9' MIN LEN = XCO?APPL/ TRANSMITTING XCOM
         BL    FPARAM4
         CLC   =C'XCO',K2(R3)
         BNE   FPARAM5
         CLC   =C'APPL',K6(R3)
         BNE   FPARAM6
         CLI   K10(R3),C'/' MUST BE
         BNE   FPARAM7
         SLR   R2,R2
         TRT   K5(K1,R3),SYSPARM
         LTR   R5,R2 SAVE TRANSM XCOM SYSNAME
         BZ    FPARAM8 ERROR
         STC   R5,XCOMSKEL+K11 PREP STCAPPL PARAM
         LOAD  EPLOC=XCOMMEP
         LR    RF,R0 /* EP */
         L     RE,K16 CVT
         ICM   RE,K7,197(RE) CVTSMCA
         LA    RE,K0(,RE) CLEAR HI
         MVI   XCOMSKEL+K28,C'M' ACBNAME/JOBACB
         CLC   =C'SYSA',K16(RE) IS SYSA
         BE    GOSCHED YES
         MVI   XCOMSKEL+K28,C'T' ACBNAME/JOBACB
         CLC   =C'SYSM',K16(RE) IS SYSM
         BE    GOSCHED YES
         MVI   XCOMSKEL+K28,C'E' ACBNAME/JOBACB
         CLC   =C'SYSE',K16(RE) IS SYSE
         BE    GOSCHED YES
         MVI   XCOMSKEL+K28,C'B' ACBNAME/JOBACB
         CLC   =C'SYSB',K16(RE) IS SYSB
         BNE   FSMCA  UNKNOWN SYSTEM
GOSCHED  LA    R1,L'XCOMSKEL GET LEN OF OUR PARAM
         STH   R1,XCOMPRM SAVE OUR LEN
         MVC   XCOMPRML(L'XCOMSKEL),XCOMSKEL MOVE BASE PARAM
         SH    R4,=H'9' OFF OUR FIXED PARM
         LA    R3,K11(,R3) PTR TO USER PARM
         LTR   R4,R4 ANY USER PARM
         BNP   GOXCOM PROCESS
         LA    R1,K1(R4,R1) NEW COMBINED LEN INCL COMMA WE ADD
         CH    R1,=H'100' MAX PARM LEN
         BH    FPARAM9 ERROR
         STH   R1,XCOMPRM SAVE OUR LEN
         MVI   XCOMPRML+K29,C',' EXTEND OUR PARM
         LA    R5,XCOMPRML+K30 POSITION FOR USER PARM
         BCTR  R4,K0 FOR EXEC
         EX    R4,MOVEUPRM MOVE USER PARM
GOXCOM   LA    R1,=A(XCOMPRM) /* EXEC PARM */
         BALR  RE,RF /* GO SCHEDULE */
         LR    R9,RF /* RC */
         DELETE EPLOC=XCOMMEP
MAINEXIT LR    RF,R9 RESTORE RC
         L     RD,K4(,RD)
         RETURN (14,12),RC=(15)
MOVEUPRM MVC   K0(*-*,R5),K0(R3) MOVE USER PARAM
FSMCA    LA    R9,K32 /* SMCA   FAILURE */
         B     MAINEXIT
FPARAM1  LA    R9,K36 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM2  LA    R9,K40 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM3  LA    R9,K44 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM4  LA    R9,K48 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM5  LA    R9,K52 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM6  LA    R9,K56 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM7  LA    R9,K60 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM8  LA    R9,K64 /* PARM   FAILURE */
         B     MAINEXIT
FPARAM9  LA    R9,K68 /* PARM   FAILURE */
         B     MAINEXIT
XCOMMEP  DC    0F'0',CL8'XCOMJOB'
XCOMPRM  DC    AL2(*-*)
XCOMPRML DC    100C' ' BUILD XCOMJOB EXEC PARAM HERE
XCOMSKEL DC    C'STCAPPL=XCO?APPL,ACBNAME=XCO?'
SYSPARM  DC    256X'00'
         ORG   SYSPARM+C'T'
         DC    C'T'
         ORG   SYSPARM+C'B'
         DC    C'B'
         ORG   SYSPARM+C'M'
         DC    C'M'
         ORG   SYSPARM+C'E'
         DC    C'E'
         ORG
         LTORG
         END
/*
//*L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(XCOMCOMM),DISP=SHR,
//* UNIT=,SPACE=
//L.SYSLMOD DD DSN=TECH110.USER.LOADLIB(XCOMCOMM),DISP=SHR,
// UNIT=,SPACE=
