/* BUILDING DEPOSIT, INC. SMS ACS STORGRP ROUTINE                   */
PROC &STORGRP
FILTLIST &ALLPOOLS INCLUDE(FDR*.**)
FILTLIST &HIPERF   INCLUDE('FASTREAD','FASTWRIT')
FILTLIST &LIBRARY  INCLUDE(SYS%.**.MASTER)
FILTLIST &SYSPARTH INCLUDE(PPLAN.**)
FILTLIST &SYSQARTH INCLUDE(QPLAN.**)
FILTLIST &SYSTARTH INCLUDE(TPLAN.**)
FILTLIST &SYSPDB2  INCLUDE(DB2A.**)
FILTLIST &SYSQDB2  INCLUDE(DB2X.**)
FILTLIST &SYSTDB2  INCLUDE(DB2Y.**)
FILTLIST &SYSPSYS  INCLUDE(SYSP.**)
FILTLIST &SYSTSYS  INCLUDE(SYST.**)
FILTLIST &SYSMSYS  INCLUDE(SYSM.**)
FILTLIST &SYSPOOL  INCLUDE(SYS%.**)
SELECT
/* TRUE TEMPORARY DATASETS                                          */
   WHEN (&DSTYPE = 'TEMP') DO
      SET &STORGRP = 'VIOPOOL','SYSPUSER','SYSTUSER'
      EXIT CODE(0)
      END
/* CA LIBRARIAN MASTER FILES THAT ARE SHARED AMONG SYSP AND SYST    */
   WHEN (&DSN = &LIBRARY) DO
      SET &STORGRP = 'LIBRARY'
      EXIT CODE(0)
      END
/* HIGH PERF (FASTREAD/FASTWRIT) DATASETS ON SYSP ONLY FOR CICS/SYS */
   WHEN (&STORCLAS = &HIPERF) DO
      SET &STORGRP = 'SYSPPERF','SYSPUSER','SYSTUSER'
      EXIT CODE(0)
      END
/* SYSTEM DATASETS                                                  */
   WHEN (&DSN = &SYSPSYS) DO
      SET &STORGRP = 'SYSPSYS'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSTSYS) DO
      SET &STORGRP = 'SYSTSYS'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSMSYS) DO
      SET &STORGRP = 'SYSPSYS'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSPOOL) DO
      SET &STORGRP = 'SYSPSYS'
      EXIT CODE(0)
      END
/* MERC PLANNING ARTHUR DATABASE                                    */
   WHEN (&DSN = &SYSPARTH) DO
      SET &STORGRP = 'SYSPARTH'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSQARTH) DO
      SET &STORGRP = 'SYSQARTH'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSTARTH) DO
      SET &STORGRP = 'SYSTARTH'
      EXIT CODE(0)
      END
/* DB2 APPLICATION DATABASE                                         */
   WHEN (&DSN = &SYSPDB2) DO
      SET &STORGRP = 'SYSPDB2'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSQDB2) DO
      SET &STORGRP = 'SYSQDB2'
      EXIT CODE(0)
      END
   WHEN (&DSN = &SYSTDB2) DO
      SET &STORGRP = 'SYSTDB2'
      EXIT CODE(0)
      END
/* SYSTEM UTIL,LIKE FDR, FILES TO BE ALLOC ON ANY VOLUME            */
   WHEN (&DSN = &ALLPOOLS) DO
      SET &STORGRP='SYSPUSER','SYSTUSER',  +
                   'SYSPARTH','SYSQARTH','SYSTARTH',  +
                   'SYSPDB2','SYSQDB2','SYSTDB2',  +
                   'SYSPSYS','SYSTSYS','SYSMSYS',  +
                   'SYSPPERF','LIBRARY'
      EXIT CODE(0)
      END
/* ELSE GENERAL POOL                                                */
   OTHERWISE DO
      SET &STORGRP = 'SYSPUSER','SYSTUSER'
      EXIT CODE(0)
      END
END
END
