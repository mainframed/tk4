//TECH110Y JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
//S EXEC ASMHCL
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//         DD DSN=SYSM.AMACLIB,DISP=SHR
//C.SYSIN  DD *
CDRSSCAN CSECT , /*DASD ANALYSIS*/
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
CDRSSCAN AMODE 24
CDRSSCAN RMODE 24
         SAVE  (14,12),,CDRSSCAN*&SYSDATE*&SYSTIME
         LR    RA,RF ADDRESSABILITY
         USING CDRSSCAN,RA,RB,RC
         LA    RB,4095(RA)
         LA    RB,1(RB)
         LA    RC,4095(RB)
         LA    RC,1(RC)
         LR    RF,RD SAVEAREA HOUSEKEEPING
         CNOP  0,4
         BAL   RD,*+76
         DC    18F'0'
         ST    RD,8(RF)
         ST    RF,4(RD)
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         OPEN  (WORKDCB,(OUTPUT))
         LA    R2,WORKDCB
         USING IHADCB,R2
         TM    DCBOFLGS,DCBOFOPN OPEN OKAY
         BZ    FWORK
         DROP  R2
IOSVSCAN EQU   * *
         UCBSCAN COPY,WORKAREA=UCBWORK,UCBAREA=UCBAREA,CMXTAREA=TOKEN, X
               DYNAMIC=YES,RANGE=ALL,DEVNCHAR=DEVN,DEVCLASS=DASD,      X
               IOCTOKEN=IOCTOKEN
*IOSVSCAN LA    R1,IOSVPARM /* SCAN UCBS */
*        L     RF,CVTPTR
*        USING CVTMAP,RF
*        L     RF,CVTUCBSC /* IOSVSUCB EP PTR */
*        DROP  RF
*        BALR  RE,RF /* SCAN FOR DASD ONLY */
*        LTR   RF,RF /* SCAN END */
*        BNZ   ENDIOSV
*        L     R8,IOSVPARM+12 /* PICKUP UCB PTR */
         B     *+4(RF) INDEX
         B     UOK
         B     ENDIOSV
         B     ERRUCBS
         B     ERRUCBS
         B     ERRUCBS
         B     ERRUCBS
         USING UCBOB,R8
UOK      LA    R8,UCBAREA
         TM    UCBSTAT,UCBONLI ONLINE
         BZ    IOSVSCAN NO
         MVC   WORKOUT(6),UCBVOLI  /* VOLSER */
         MVC   FDEVT+8(6),UCBVOLI /* IFF ERROR */
         MVC   FLSPACE+8(6),UCBVOLI /* IFF ERROR */
         ST    R8,UCBLIST FOR DEVTYPE
         DEVTYPE ,(DEVTYPE,24),UCBLIST=(UCBLIST,1)
         LTR   RF,RF
         BNZ   FDEVT
         LH    RF,DEVTYPE+8 DASD PHYS CYL COUNT
         BCTR  RF,0 SUBTRACT CE CYL
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+7(4),WORK+1
         LSPACE UCB=((R8)),DATA=LSPACE
         LTR   RF,RF
         BNZ   FLSPACE
         L     RF,LSPACE+8 TOTAL FREE CYL
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+12(4),WORK+1
         L     RF,LSPACE+32 FRAGMENTATION INDEX 0-999 (HIGH=BAD FRAG)
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+17(4),WORK+1
         PUT   WORKDCB,WORKOUT SAVE DATA
         B     IOSVSCAN CONTINUE
         DROP  R8
ENDIOSV  EQU   * *
BREAKOUT CLOSE (WORKDCB)
         L     RD,4(RD)
         RETURN (14,12),RC=0
FWORK    L     RD,4(RD)
         RETURN (14,12),RC=28
FDEVT    WTO   'VOLSER ******** DEVTYPE FAILURE ********'
         L     RD,4(RD)
         RETURN (14,12),RC=32
FLSPACE  WTO   'VOLSER ******** LSPACE FAILURE ********'
         L     RD,4(RD)
         RETURN (14,12),RC=36
ERRUCBS  L     RD,4(RD)
         SLL   RF,4 *16
         AR    RF,R0
         RETURN (14,12),RC=(15)
WORK     DC    D'0'
LSPACE   DC    36X'00'
UCBWORK  DC    100X'00'
UCBAREA  DC    48X'00'
TOKEN    DC    32X'00'
DEVN     DC    4X'00'
IOCTOKEN DC    48X'00'
*IOSVPARM DC    A(IOSVWORK,IOSVDEVT),X'80',AL3(IOSVPARM+12),A(0)
*IOSVWORK DC    512X'00'
UCBLIST  DC    A(*-*) FOR DEVTYPE
DEVTYPE  DC    24X'00' DEVTYPE RESPONSE AREA
WORKOUT  DC    CL26' '
IOSVDEVT DC    AL1(UCB3DACC) DASD CLASS
         LTORG
         PRINT NOGEN
WORKDCB  DCB   DDNAME=WORK,DSORG=PS,MACRF=(GM,PM),RECFM=FB,LRECL=26
         CVT   DSECT=YES
         DSECT
         IEFUCBOB
         DSECT
         IEZCOM
         DSECT
         IEZCIB
         DCBD  DSORG=QS,DEVD=DA
         END
/*
//*L.SYSLMOD DD DSN=TECH110.USER.LOADLIB(DASDSCAN),DISP=SHR,
//L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(DASDSCAN),DISP=SHR,
// UNIT=,SPACE=
//*G.WORK DD SYSOUT=*
//*G.SYSUDUMP DD SYSOUT=*
