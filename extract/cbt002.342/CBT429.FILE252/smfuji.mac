//SMFUJI   JOB 5304010530000000,'TECH.SUPP.DANBOWEN',NOTIFY=TECH103,
//             CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),REGION=4096K
//*
//*********************************************************************
//* DOC: THIS JOB WILL ASSEMBLE THE SMF ( IEFUJI ) INSTALLATION EXIT
//*      INTO ( SYS1.TECH.COMMON.LPALIB ).
//*      THIS SMF EXIT ROUTINE WILL PERFORM THE FOLLOWING FUNCTION:
//*      1. BUILD THE RACROUTE PARAMETER LIST TO PERFORM AUTHORIZATION
//*         VERIFICATION TO DETERMINE IF THE JOB SUBMITTOR IS
//*         AUTHORIZED TO USE THE SPECIFIED INITIATOR JOB CLASS.
//*
//*      THE RACROUTE SAF IS USED FOR USER AUTHORIZATION CHECKING VIA
//*      RACF DEFINED GENERAL RESOURCE ( FACILITY ).
//*      EACH INITIATOR JOBCLASS IS A 1 BYTE MATCHED PROFILE WITH A
//*      UACC OF ( NONE ).
//*      THE PROFILE IS DEFINED WITH THE PREFIX ( $JOBCLAS ) AND THE
//*      THE JMR JOB CLASS: ( $JOBCLAS.J ).
//*      THE USER MUST BE ON THE PROFILE ACCESS LIST WITH AT LEAST
//*      ( READ ) AUTHORITY.
//*
//*      REGISTER 1 PARAMETER LIST:
//*      . WORD 1 0(0)    ( ADDRESS OF COMMON SMF PARM LIST )
//*      . WORD 2 4(4)    ( ADDRESS OF PROGRAMMER'S NAME 20 BYTES )
//*      . WORD 3 8(8)    ( ADDRESS OF 1 BYTE JOB PRTY IN BIN )
//*      . WORD 4 12(C)   ( ADDRESS OF JOB ACCOUNTING INFO )
//*
//*      EXIT RETURN CODES:
//*      . R15 = 0        ( USER JOB CLASS OK, ALLOW JOB TO PROCESS )
//*      . R15 = 4        ( USER NOT AUTH FOR JOB CLASS, CANCEL JOB )
//*********************************************************************
//*
//ASSEM  EXEC  PGM=IEV90,
//        PARM='OBJECT,XREF(SHORT),NODECK,LIST,RENT'
//SYSLIB   DD  DSN=SYSM.MVSESA.SMPSTS,DISP=SHR
//         DD  DSN=SYSM.MVSESA.SMPMTS,DISP=SHR
//         DD  DSN=SYSM.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(3,1)),DSN=&SYSUT1
//SYSPUNCH DD  DUMMY
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(1,1,0)),
//         DCB=(BLKSIZE=400),DSN=&&LOADSET
//SYSIN    DD  *
IEFUJI   TITLE 'SMF JOB INITIATION EXIT ROUTINE                '
         SPACE 3
IEFUJI   CSECT ,                       CSECT NAME DECLARED
IEFUJI   AMODE 31
IEFUJI   RMODE ANY
         EJECT
         SAVE  (14,12),,IEFUJI_&SYSDATE._&SYSTIME
         LR    R12,R15               LOAD BASE REG WITH ENTRY POINT
         USING IEFUJI,R12            SET UP BASE ADDRESSABILITY
         L     R2,0(,R1)             GET PARM LIST ADDRESSES
         USING JMR,R2                JMR DSECT ADDRESSABILITY PARM LIST
*
* ISSUE RACROUTE TO CHECK AUTHORITY TO USE INITIATOR JOB CLASS
*
RACFCHK  DS    0H
         L     R0,WRKSIZE          SAF WORK AREA SIZE TO R0
         GETMAIN RU,LV=(0),SP=252  GETMAIN FOR SAF WORK AREA
         LTR   R15,R15             GETMAIN SUCCESSFUL?
         BNZ   GETMERR             NO, EXIT WITH WTO ERROR MSG
         LR    R11,R1              SAF WORK AREA ADDRESSABILITY
         USING RACDSECT,R11
         ST    R13,SAVEAREA+4      SAVE CALLER'S SAVEAREA ADDRESS
         ST    R11,8(,R13)         SAVE OUR SAVEAREA ADDRESS
         LR    R13,R11             LOAD OUR SAVE AREA ADDRESS IN R13
         MVC   AUTHCHK(LRACROUT),RACROUTL    INIT RACROUTE MACRO
         MVC   PROFILE,RACPROF     INIT RACROUTE PROFILE
         MVC   RJOBCLAS(1),JMRCLASS  RACROUTE PROFILE JOB CLASS
         RACROUTE REQUEST=AUTH,    RACHECK REQUEST.                    X
               CLASS=RACLASS,      RESOURCE CLASS.                     X
               ATTR=READ,          INSURE AT LEAST READ AUTHORITY.     X
               ENTITY=PROFILE,     RESOURCE PROFILE.                   X
               LOG=ASIS,           LOG ACCESS ATTEMPT PER PROFILE.     X
               WORKA=SAFWORK,      SAF WORK AREA.                      X
               MF=(E,AUTHCHK)
*
*  PROCESS RACHECK RETURN CODE
*
         LR    R4,R15              SAVE RACROUTE SAF RETURN CODE
         C     R4,=F'0'            USER AUTHORIZED FOR JOB CLASS?
         BE    ALLOWJOB            YES, ALLOW JOB, EXIT RC=0
*
         C     R4,=F'4'            PROFILE NOT FOUND
         BE    ALLOWJOB            NO PROFILE, ALLOW JOB, EXIT RC=0
*
         C     R4,=F'8'          USER NOT AUTHORIZED FOR JOB CLASS?
         BE    SETFAIL           USER NOT AUTH, CANCEL JOB, EXIT RC=4
         B     ALLOWJOB          UNEXPECTED RETURN CODE, ALLOW JOB
*
SETFAIL  DS    0H                   REJECT USER JOBCLASS - NOT AUTH
         LA    R5,4                 SET RETURN CODE TO FAIL REQUEST
         WTO   'CHRM100E - JOB CANCELLED, USER NOT AUTH FOR JOBCLASS.',X
               ROUTCDE=(2,11)
         B     FINAL
*
GETMERR  DS    0H                      ALLOW JOB TO RUN - EXIT RC=0
         WTO   'CHRM101I - GETMAIN FAILED FOR RACROUTE SAF BUFFER. ',  X
               ROUTCDE=(2,11)
         LM    R14,R12,12(R13)         RESTORE CALLER REGISTERS
         SLR   R15,R15                 SET RETURN CODE TO ZERO
         BSM   0,14                    RETURN TO CALLER IN ITS MODE
*
ALLOWJOB SLR    R5,R5               SET RETURN CODE TO ALLOW REQUEST
*
FINAL    DS    0H
         L     R13,SAVEAREA+4      RESTORE CALLER'S REG 13
         L     R0,WRKSIZE          SAF WORK AREA SIZE TO R0
         LR    R1,R11              DYNAMIC DATA ADDRESS TO R1
         FREEMAIN RU,LV=(0),A=(1),SP=252  FREE SAF WORK AREA
         LR    R15,R5              LOAD RETURN CODE
         RETURN (14,12),T,RC=(15)  RESTORE CALLER REGISTERS, RETURN
         EJECT
*
*   REGISTER EQUATES
*
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                       REGISTER 2
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
R5       EQU   5                       REGISTER 5
R6       EQU   6                       REGISTER 6
R7       EQU   7                       REGISTER 7
R8       EQU   8                       REGISTER 8
R9       EQU   9                       REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
R12      EQU   12                      REGISTER 12
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
*
*  DATA AREAS FOR RACROUTE SAF MACRO
*
         DS    0D
WRKSIZE  DC    AL4(SIZEDATD)       DYN AREA SIZE
RACLASS  DC    AL1(L'RACLASSN)     CLASS NAME FOR RACROUTE
RACLASSN DC    C'FACILITY'         CLASS NAME FOR RACROUTE
RACPROF  DC    CL39'$JOBCLAS. '    PROFILE JOBCLASS
RACROUTL RACROUTE REQUEST=AUTH,WORKA=*-*,MF=L
LRACROUT EQU   *-RACROUTL          LENGTH OF RACROUTE MACRO.
         LTORG
*
*  DSECT DATA AREAS FOR RACROUTE SAF MACRO
*
RACDSECT DSECT
SAVEAREA DS    18F                 REGISTER SAVE AREA.
SAFWORK  DS    CL512               SAF WORK AREA.
         DS    0D
DOUBLE   DC    D'0'
PROFILE  DS    CL39                PROFILE FOR RACROUTE
         ORG   PROFILE
RACFPRFX DC    C'$JOBCLAS.'        PROFILE PREFIX
RJOBCLAS DS    CL1                 JOB CLASS
         ORG
         DS    0D
AUTHCHK  RACROUTE REQUEST=AUTH,MF=L,WORKA=*-*
SIZEDATD EQU   *-RACDSECT          LENGTH OF DSECT
         IEFJMR  ,                 JMR DSECT MAPPING
         END   IEFUJI
/*
//LNKEDT   EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL,XREF,RENT',
//           COND=(4,LT,ASSEM),REGION=256K
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLIB   DD DSN=SYS1.TECH.COMMON.LPALIB,DISP=SHR
//SYSLMOD  DD DSN=SYS1.TECH.COMMON.LPALIB(IEFUJI),DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  *
  ENTRY   IEFUJI
  NAME    IEFUJI(R)
/*
//
