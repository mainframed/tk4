//TECH110F JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
//S EXEC ASMHCL,PARM.L='LIST,LET,XREF,MAP,AC=1'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN  DD *
IGD      TITLE 'IGDACSDC DATACLAS INSTALLATION EXIT'
IGDACSDC CSECT ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K21      EQU   21
K23      EQU   23
K24      EQU   24
K27      EQU   27
K28      EQU   28
K32      EQU   32
K34      EQU   34
K36      EQU   36
K38      EQU   38
K40      EQU   40
K43      EQU   43
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K57      EQU   57
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K136     EQU   136
K144     EQU   144
K152     EQU   152
K176     EQU   176
K196     EQU   196
K208     EQU   208
K212     EQU   212
K255     EQU   255
K256     EQU   256
K272     EQU   272
K320     EQU   320
K550     EQU   550
K2047    EQU   2047
K2048    EQU   2048
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
IGDACSDC AMODE 31
IGDACSDC RMODE ANY
         SAVE  (14,12),,IGDACSDC*&SYSDATE*&SYSTIME
         USING ACSPMD,R3
         L     R3,K0(R1) ADDRESS PASSED PARAM LIST
         USING ACSWORK,R4
         L     R4,ACSPWORK MAP PASSED WORKAREA
         LR    RA,RF /*ADDRESSABILITY*/
         USING IGDACSDC,RA
         LR    RF,RD /*SAVEAREA HOUSEKEEPING*/
         CNOP  K0,K4
         LA    RD,SAVEAREA
         ST    RD,K8(,RF)
         ST    RF,K4(,RD)
         USING ACERO,R5
         L     R5,ACSPERO MAP READ ONLY VARS
         USING ACERW,R6
         L     R6,ACSPERW MAP R/W VARS
         CLC   =AL2(K0),ACERODCL LEN OF CLASS NAME ASSIGNED
         BE    EXIT LEAVE, NULL ASSIGNED
         CLI   ACERODCV,C'$' REPLACEABLE VALUE ASSIGNED
         BNE   EXIT NO
         CLC   =AL2(K8),ACERODCL $DSOWNER
         BNE   CHEK$5  CONTINUE
         CLC   =C'$DSOWNER',ACERODCV
         BNE   EXIT LEAVE
         MVC   ACERWNCS(K4),=A(K1) 1 CLASS VAL
         MVC   ACERWVAL(L'ACERODSO),ACERODSO RACF DSOWNER
         LA    R7,K8 MAX LEN
         LA    R8,ACERWVAL+K7 LAST POSSIBLE CHAR
CHEK$2   CLI   K0(R8),C' ' BLANK
         BNE   CHEK$3 NO
         BCTR  R8,K0 -1
         BCT   R7,CHEK$2 LOOP BACKWARDS
CHEK$3   STCM  R7,K3,ACERWVLN STORE LEN
         B     EXIT OKAY
CHEK$5   CLC   =AL2(K5),ACERODCL $USER
         BNE   EXIT  CONTINUE
         CLC   =C'$USER',ACERODCV
         BNE   EXIT LEAVE
         MVC   ACERWNCS(K4),=A(K1) 1 CLASS VAL
         MVC   ACERWVAL(L'ACERODSO),ACEROUSR USERID
         LA    R7,K8 MAX LEN
         LA    R8,ACERWVAL+K7 LAST POSSIBLE CHAR
CHEK$7   CLI   K0(R8),C' ' BLANK
         BNE   CHEK$8 NO
         BCTR  R8,K0 -1
         BCT   R7,CHEK$7 LOOP BACKWARDS
CHEK$8   STCM  R7,K3,ACERWVLN STORE LEN
EXIT     L     RD,K4(,RD) EXIT PROCESSING
         RETURN (14,12),RC=0
         LTORG
ACSWORK  DSECT , MAP ACS PASSED WORKAREA
SAVEAREA DS    18F SAVEAREA
IGDACSPM IGDACSPM ACS EXIT PARAM LIST
IGDACERO IGDACERO ACS READ ONLY VARIABLES
IGDACERW IGDACERW ACS READ WRITE VARIABLES
         END
/*
//L.SYSLMOD DD DSN=SYST.LPALIB(IGDACSDC),DISP=SHR,
// UNIT=,SPACE=
