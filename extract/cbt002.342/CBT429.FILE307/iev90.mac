IEV90    AMODE 31
IEV90    RMODE ANY
IEV90    CSECT
         SAVE  (14,12),,'IEV90 &SYSDATE &SYSTIME '
         USING IEV90,15
*--------------------------------------------------------
*        replace "LOAD" with "OBJ "
*--------------------------------------------------------
         LR    10,1               copy parm= reg
         N     10,=A(X'7FFFFFFF') turn off hi bit
         BZ    A90XCTL            xctl if no parm=
         L     10,0(,10)          point to length, data
         N     10,=A(X'7FFFFFFF') turn off hi bit
         BZ    A90XCTL            xctl if no parm=
         LH    9,0(,10)           load length
         LTR   9,9                test length
         BNP   A90XCTL            xctl if no parm=
         LA    10,2(,10)          point past length
         LA    9,0(9,10)          point to end of parms
         SH    9,=Y(4)            backup 4 bytes
CHKLOOP  CR    10,9               past end of parm=
         BH    A90XCTL            xctl if yes
         CLC   =C'LOAD',0(10)     pointing to "LOAD" ?
         BNE   CHKNEXT             no, continue
         MVC   0(4,10),=C'OBJ '   else change to "OBJ"
CHKNEXT  LA    10,1(,10)          point to next character
         B     CHKLOOP            loop back
*--------------------------------------------------------
*        xctl to hlasm
*--------------------------------------------------------
A90XCTL  LM    9,10,56(13)        restore regs 9 & 10
         XCTL  EP=ASMA90
         END
