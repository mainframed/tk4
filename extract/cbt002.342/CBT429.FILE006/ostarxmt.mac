/* * Rexx proc: OSTARXMT ** Author: RMC20 ***** Date: 7 Mar 1997 *** */
/* ----------------------------------------------------------------- */
/* Purpose: Compress multiple datasets into a format suitable for    */
/* transmission via pc, internet, etc, and reconstitution via        */
/* OSTARREC procedure.                                               */
/* ----------------------------------------------------------------- */
/*                                                                   */
/*    This software is supplied by Amdahl Corp. for the transmission */
/* of materials between ObjectStar support and it's customers. Any   */
/* other use of this software is at the user's discretion and is not */
/* supported in any way by Amdahl Corp.                              */
/*                                                                   */
/*   The software is supplied as 'freeware' and may be used/modified */
/* by anyone provided they do not then sell it on for profit or      */
/* expect support from Amdahl Corp.                                  */
/*                                                                   */
/*   Limited support MAY in some cases be available from the author. */
/*                                                                   */
/* ----------------------------------------------------------------- */
/* V 1.02 remove chars which won't ascii transmit.                   */
/* V 1.03 remove chars which won't ascii transmit.                   */
/* V 1.04 Allow single dataset quick option.                         */
/* V 1.05 Fix bug introduced by 1.04.                                */
/* V 1.06 Add data counts.                                           */
/* V 1.07 Remove excessive trailing blanks in dsnames.               */
/* V 1.08 Improve dataset size & blocking calculations.              */
/* V 1.09 remove #s from dataset names.                              */
/* V 1.0A Add support for TRSMAIN program and PAK files.             */
/* V 1.0B Add support for XM1 & XM2 files.                           */
/* V 1.0C Correct error in PHASE11 (compression) process.            */
/* V 1.0D Allow XM2 format only for PS files not PO and add help.    */
/* V 1.0E IMPROVE PERFORMANCE VIA OSTAREDC MODULE.                   */
/* V 1.0F Synchonize versions with ostarrec. (no change)             */
/* V 1.0G Add disclaimer.                                            */
/*                                                                   */
/* ***************************************************************** */
/* Phase 1 - Identify environment and input                          */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
TRSMAIN = "'CSUPP.SHARED.LOADLIB(TRSMAIN)'" /* MODIFY IF AVAILABLE */
OSTAREDC  = "'CSUPP.SHARED.LOADLIB(OSTAREDC)'" /* MODIFY IF AVAILABLE */
IF SYSDSN(TRSMAIN) = "OK"
THEN DO
     COMPRESS = 1
     "ALLOCATE FI(SYSPRINT) DA(*) SHR"
     END
ELSE DO
     COMPRESS = 0
     SAY "Compression not available."
     END
IF SYSDSN(OSTAREDC) = "OK"
THEN DO
     EDC = 1
     "ALLOCATE FI(SYSOUT) DA(*) SHR"
     END
ELSE DO
     EDC = 0
     SAY "OSTAREDC program not available."
     END
ARG DSN .
ADDRESS "TSO"
HEADER = COPIES("OSTARXMT10",4)
VERSION = "V1.0C"
IF DSN = "?" THEN CALL HELP
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Phase 2 - Identify datasets                                       */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
MAXRC = 0
IF SYSDSN(DSN) = "OK"
THEN DO
     WORK = LISTDSI(DSN "DIRECTORY")
     "ALLOCATE FI(SYSUT1) DA("DSN") SHR REUSE"
     IF SYSDSORG = "PO" & POS("(",DSN) = 0
     THEN NOP
     ELSE EXECIO "1 DISKR SYSUT1 ( STEM IN. FINIS"
     IF IN.1 = "OSTARXMT CONTROL"
     THEN DO
          SAY "Processing" DSN "as a control file."
          EXECIO "* DISKR SYSUT1 ( STEM IN. FINIS"
          ODSN = WORD(IN.2,1)
          DSNS = 0
          DO I = 3 TO IN.0
             X = I-2
             WORK = WORD(IN.I,1)
             IF SYSDSN(WORK) = "OK"
             THEN DO
                  DSNS = DSNS+1
                  DSN.DSNS = WORK
                  SAY WORK "accepted."
                  END
             ELSE DO
                  CALL WARN WORK "not found. Rejected."
                  IF MAXRC < 8 THEN MAXRC = 8
                  END
             END
          END
     ELSE DO
          SAY "Processing" DSN "as a single input file."
          DSNS = 1
          DSN.1 = DSN
          X = LASTPOS('.',DSN)
          ODSN = SUBSTR(DSN,1,X-1)".XMT"
          IF SUBSTR(ODSN,1,1) = "'" THEN ODSN = ODSN"'"
          IF POS("#",ODSN) > 0
          THEN DO
               ODSN = TRANSLATE(ODSN,"P","#")
               SAY "Output dataset renamed to "ODSN"."
               END
          END
     CALL CHK_ODSNS
     DROP IN.
     END
ELSE DO
     SAY "";SAY "";SAY ""
     SAY "Enter output dataset name."
     PULL ODSN
     CALL CHK_ODSNS
     SAY ODSN "accepted."
     DSNS = 0
     DO FOREVER
        SAY "Enter input DSN, 'QUIT' to abort, or null to end list."
        PULL DSN .
        IF DSN = "" THEN LEAVE
        IF DSN = "QUIT" THEN EXIT 4
        IF SYSDSN(DSN) = "OK"
        THEN DO
             DSNS = DSNS+1
             DSN.DSNS = DSN
             SAY DSN "accepted."
             END
        ELSE CALL WARN DSN "not found. Rejected."
        END
     END
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Phase 3 - allocate work file and output file(s)                   */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
IF DSNS = 1
THEN SAY "1 dataset accepted."
ELSE SAY DSNS "datasets accepted."
TOTSIZE = 0
MAXSIZE = 0
DO I = 1 TO DSNS
   X = LISTDSI(DSN.I)
   SELECT
     WHEN SYSUNITS = "CYLINDER" THEN X = SYSUSED*SYSTRKSCYL
     WHEN SYSUNITS = "TRACK" THEN X = SYSUSED
     WHEN SYSUNITS = "BLOCK" THEN X = SYSUSED%SYSBLKSTRK+1
     OTHERWISE
     CALL ERROR "Invalid space units for file" DSN.I"."
     END
   SIZE.I = X
   TOTSIZE = TOTSIZE + X
   IF X > MAXSIZE THEN MAXSIZE = X
   END
IF DSNS = 1 & TOTSIZE > 300  & COMPRESS & SYSDSORG = "PS"
THEN SIGNAL PHASE11
"ALLOCATE FI(OSTARXMT) DA("ODSN") NEW REUSE TRACKS",
       "SPACE("TOTSIZE","TOTSIZE%10+1")",
       "CATALOG"
IF RC = 0
THEN DO
     WORK = LISTDSI(ODSN)
     SELECT
       WHEN SYSUNIT = "3390" THEN WORK = "27984"
       WHEN SYSUNIT = "3380" THEN WORK = "23408"
       OTHERWISE
       WORK = 0
       END
     "ALLOCATE FI(OSTARXMT) DA("ODSN") MOD REUSE",
       "LRECL(88) RECFM(F B) BLKSIZE("WORK") RELEASE"
     END
IF RC = 0
THEN SAY ODSN "Allocated, tracks="TOTSIZE", blksize="WORK", DISP=NEW."
ELSE CALL ERROR "Unable to allocate file" ODSN "with DISP=NEW."
SELECT
  WHEN SYSUNIT = "3390" THEN WORK = "27920"
  WHEN SYSUNIT = "3380" THEN WORK = "23440"
  OTHERWISE
  WORK = 0
  END
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Phase 4 - create XMT file.                                        */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
OUT1 = HEADER VERSION DSNS DATE("S") TIME()
EXECIO "1 DISKW OSTARXMT ( STEM OUT"
IF RC = 0
THEN RECS = 1
ELSE CALL ERROR "error writing to" ODSN"."
IF EDC
THEN DO
     EXECIO "0 DISKW OSTARXMT ( FINIS"
     DO I = 1 TO DSNS
        CALL XMITDSNE I DSN.I SIZE.I WORK
        END
     "ALLOCATE FI(SYSUT2) DUMMY"
     PARMS = "'"RIGHT(DSNS,3,"0")"AD"HEADER"'"
     "CALL" OSTAREDC PARMS
     IF RC = 0
     THEN NOP
     ELSE CALL ERROR "Failure adding EDC INFO. rc="RC
     DO I = 1 TO DSNS
        "FREE FI(OSTAR"RIGHT(I,3,"0")")"
        END
     END
ELSE DO
     "ALLOCATE FI(SYSUT2) NEW DELETE REUSE TRACKS",
           "SPACE("MAXSIZE","MAXSIZE%10+1")",
           "LRECL(80) RECFM(F B) BLKSIZE("WORK")"
     IF RC = 0
     THEN SAY "Workfile allocated, tracks="MAXSIZE", blksize="WORK"."
     ELSE CALL ERROR "Workfile 'SYSUT2' allocation failure."
     DO I = 1 TO DSNS
        TOKEN = XMITDSN(DSN.I HEADER)
        OUT1 = OVERLAY(TOKEN,HEADER,41)
        EXECIO "1 DISKW OSTARXMT ( STEM OUT"
        IF RC = 0
        THEN RECS = RECS + 1
        ELSE CALL ERROR "error writing to" ODSN"."
        END
     SAY "XMT file" ODSN "contains" RECS "records,",
          RECS*88%1024 "KiloBytes."
     END
EXECIO "0 DISKW OSTARXMT (FINIS"
ADDRESS "TSO" "FREE FI(OSTARXMT,SYSUT2,SYSUT1)"
IF MAXRC = 8
THEN CALL WARN "All datasets were not processed but" ODSN "is valid."
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Phase 5 - create PAK file.                                        */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
IF COMPRESS
THEN DO
     SAY "Compressing" ODSN "into" PDSN"."
     "ALLOCATE FI(INFILE) DA("ODSN") SHR REUSE"
     IF RC <> 0 THEN CALL ERROR "ERROR RE-ALLOCATING XMT FILE" ODSN"."
     "ALLOCATE FI(OUTFILE) DA("PDSN") NEW REUSE TRACKS",
           "SPACE("TOTSIZE%3+1","TOTSIZE%10+1")",
            "CATALOG"
     "CALL" TRSMAIN "'PACK'"
     IF RC > MAXRC THEN MAXRC = RC
     IF RC = 0
     THEN SAY "Dataset" PDSN "contains a compressed version of" ODSN"."
     "FREE FI(INFILE,OUTFILE)"
     END
EXIT MAXRC
XMITDSNE: PROCEDURE
ARG COUNT INDSN TRACKS BLKSIZE .
SAY "Processing" INDSN"."
DDNAME = "OSTAR"RIGHT(COUNT,3,"0")
"ALLOCATE FI("DDNAME") NEW DELETE REUSE TRACKS",
           "SPACE("TRACKS","TRACKS%10+1")",
           "LRECL(80) RECFM(F B) BLKSIZE("BLKSIZE")"
IF RC = 0
THEN SAY "Workfile allocated, tracks="TRACKS", blksize="BLKSIZE"."
ELSE CALL ERROR "Workfile 'SYSUT2' allocation failure."
ADDRESS "TSO"
"TRANSMIT COMPLEX2."USERID() "DATASET("INDSN") NOLOG",
                               "NONOTIFY OUTDD("DDNAME")"
IF RC = 0
THEN NOP
ELSE CALL ERROR "Unable to copy" INDSN "to workfile."
RETURN 0
XMITDSN: PROCEDURE EXPOSE RECS
ARG INDSN TOKEN .
SAY "Processing" INDSN"."
ADDRESS "TSO"
"TRANSMIT COMPLEX2."USERID() "DATASET("INDSN") NOLOG",
                               "NONOTIFY OUTDD(SYSUT2)"
IF RC = 0
THEN NOP
ELSE CALL ERROR "Unable to copy" INDSN "to workfile."
SAY "Now adding error detection info for" INDSN". Please be patient."
DO FOREVER
   EXECIO "1 DISKR SYSUT2 (STEM IN"
   IF RC = 2 THEN LEAVE
   IF RC = 0
   THEN NOP
   ELSE CALL ERROR "Read error on temp file."
   TOKEN = BITXOR(TOKEN,SUBSTR(IN1,1,40))
   TOKEN = BITXOR(TOKEN,SUBSTR(IN1,41,40))
   TOKEN2 = SUBSTR(IN1,1,8)
   DO I = 9 TO 73 BY 8
      TOKEN2 = BITXOR(TOKEN2,SUBSTR(IN1,I,8))
      END
   OUT1 = OVERLAY(TOKEN2,IN1,81)
   EXECIO "1 DISKW OSTARXMT (STEM OUT"
   IF RC = 0
   THEN RECS = RECS + 1
   ELSE CALL ERROR "error writing to output file."
   END
EXECIO "0 DISKR SYSUT2 (FINIS"
RETURN TOKEN
WARN: PROCEDURE
PARSE ARG MSG
SAY "!! WARNING !! -" MSG
SAY "Press 'ENTER' to continue."
PULL .
RETURN 0
ERROR: PROCEDURE EXPOSE RC
PARSE ARG MSG
SAY " "
SAY "                           !! ERROR !! "
SAY "====>" MSG "<===="
SAY " "
IF RC = "RC" THEN EXIT 16
EXIT RC
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Phase 11 - Create a xm2 file.                                     */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
PHASE11:
SAY "Compressing" DSN.1 "into" QDSN"."
"ALLOCATE FI(INFILE) DA("DSN.1") SHR REUSE"
IF RC <> 0 THEN CALL ERROR "ERROR ALLOCATING DATASET" DSN.1"."
"ALLOCATE FI(OUTFILE) DA("QDSN") NEW REUSE TRACKS",
      "SPACE("TOTSIZE%3+1","TOTSIZE%10+1")",
       "CATALOG"
"CALL" TRSMAIN "'SPACK'"
IF RC > MAXRC THEN MAXRC = RC
IF RC = 0
THEN SAY "Dataset" QDSN "contains a compressed version of" DSN.1"."
"FREE FI(INFILE,OUTFILE)"
EXIT MAXRC
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
/* Subroutin to ensure output datasets do not exit.                  */
/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
CHK_ODSNS: PROCEDURE EXPOSE ODSN PDSN QDSN COMPRESS
IF SYSDSN(ODSN) = "OK"
THEN CALL ERROR "Dataset" ODSN "already exists."
IF COMPRESS = 0 THEN RETURN 0
X = LASTPOS('.',ODSN)
PDSN = SUBSTR(ODSN,1,X-1)".XM1"
IF SUBSTR(PDSN,1,1) = "'" THEN PDSN = PDSN"'"
IF SYSDSN(PDSN) = "OK"
THEN CALL ERROR "Dataset" PDSN "already exists."
QDSN = SUBSTR(ODSN,1,X-1)".XM2"
IF SUBSTR(QDSN,1,1) = "'" THEN QDSN = QDSN"'"
IF SYSDSN(QDSN) = "OK"
THEN CALL ERROR "Dataset" QDSN "already exists."
RETURN 0
HELP: PROCEDURE
SAY "The OSTARXMT REXX procedure combines 1 or more MVS sequential or"
SAY "partitioned datasets into a single file for simple tranmission "
SAY "over the internet."
SAY ""
SAY "OSTARXMT creates 3 possible formats of file."
SAY ""
SAY " An XMT file is a single MVS FB LRECL=88 file containing 1 or more"
SAY "MVS sequential or partitioned datasets with error detection data"
SAY "added."
SAY ""
SAY " An XM1 file is a compressed version of an XMT file to allow"
SAY "faster data transmission over the internet.  It has RECFM=FB and"
SAY "LRECL=1024."
SAY ""
SAY " An XM2 file is a HIGHLY compressed version of a single large"
SAY "seqential dataset, e.g. a dump. It is quick to create and send"
SAY "via the internet but has no error detection information.  It has"
SAY "RECFM=FB and LRECL=1024."
SAY ""
SAY "OSTARXMT can be envoked in 3 ways :-"
SAY ""
SAY "1) With no parameters at any TSO command or READY prompt. You will"
SAY "  be prompted firstly for an output dataset which must not exist,"
SAY "  and then for an input datset. The input dataset prompt will be"
SAY "  repeated until you reply a blank line."
SAY ""
SAY "2) With as single parameter which is the name of a sequential file"
SAY "  or PDS member containing a control file of the format -"
SAY "  OSTARXMT CONTROL   - line 1 indicates a control file."
SAY "  data.set.name      - line 2 names the output file."
SAY "  data.set.name      - line 3 names an  input  file."
SAY "            repeat line 3 as required for multiple input files."
SAY ""
SAY "3) With as single parameter which is the name of a sequential file"
SAY "  or PDS  to be formated. The output file name will be the same as"
SAY "  the input file but with the last qualifier changed to reflect "
SAY "  the OSTARXMT format."
SAY ""
SAY " Options 2 or 3 can be run in batch under TSO if large files are"
SAY "involved which would tie up your session for a long time."
SAY ""
SAY " If you specify a single large sequential file AND compression is"
SAY "enabled then an XM2 file is created. Otherwise an XMT file will"
SAY "be produced and also an XM1 file if compression is enabled."
SAY ""
SAY " For info on how to rebuild the original files from the OSTARXMT"
SAY "file see the OSTARREC help via the '%OSTARREC ?' command."
SAY ""
SAY " If you have any problems, or encounter error messages you don't"
SAY "understand then contact the Support Centre."
SAY ""
exit 0
