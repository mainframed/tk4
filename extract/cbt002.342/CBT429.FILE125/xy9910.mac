XY9910   CSECT
         SPACE 3
***    .
***    .    START OF CODE. BASE       REGS 12 9
***    .    WORK AREA                 REG 11
***    .    SYSIN RECORD AREA         REG 2
***    .    SMF RECORD AREA           REG 2
***    .    RETURN FROM BRANCH        REG 7
***    .
***    .    CONTROL CARDS THAT ARE VALID ARE:
***    .      SDATE=YYDDD YEAR AND DAY. THE SIGNIFICANT DIGITS ARE
***    .                  FROM LEFT TO RIGHT. E.G. YOU CAN CODE
***    .                  SDATE=85 FOR 1985 AND TAKE THE DEFAULT DAY
***    .                  DEFAULT IS 00000
***    .      EDATE=YYDDD  SAME AS ABOVE. DEFAULT IS 99366.
***    .      STIME=HHMMSS SIGNIFICANT DIGITS ARE LEFT TO RIGHT. IF
***    .                   YOU DONT TAKE THE DEFAULT THE YOU MUST
***    .                   SUPPLY AT LEAST 2 DIGITS FOR THE HOUR.
***    .                  DEFAULT IS 00000
***    .
         STM   14,12,12(13)
         LR    12,15           1ST BASE REG
         USING XY9910,12,9
         LA    R9,4095(R12)
         LA    R9,1(R9)        2ND BASE REG
         ST    R13,SAVERET+4   MUST SAVE REG13
         LA    R13,SAVERET     POINT TO SAVE AREA
         B     BEGIN           BRANCH AROUND ID
         DC    C'XY9910        JULY 6, 1987'
         DS    0H
BEGIN    EQU   *
         L     R4,0(1)            GET ADDR OF PARMS
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R11,R1             REG 11 POINTS TO WORK AREA
         USING WORK,R11
         LA    R0,WORKSTRT          POINT TO START OF CLEAR
         LA    R1,WORKEND-WORKSTRT  AND SET THE LENGTH
         SR    R15,R15            SET REG WITH PADDING CHAR
         MVCL  R0,R14             CLEAR THE AREA
         LR    R10,R11            LOAD SAVE AREA ADDR
         ST    R4,PARMAREA        NOW SAVE ADDR OF PARMS
SETTBLS  EQU   *                  GET STORAGE FOR TABLES
         L     R0,SIZETAB         SIZE AND SUBPOOL OF SMF REC
         GETMAIN R,LV=(0)
         ST    R1,@TYPETAB        SAVE ADDR OF SMF RECORD AREA
         MVC   0(1,R1),EFFS
         L     R0,SIZESMF         SIZE AND SUBPOOL OF SMF REC
         GETMAIN R,LV=(0)
         ST    R1,@SMFIN          SAVE ADDR OF SMF RECORD AREA
OPENPRT  EQU   *
         OPEN  (PRINT1,OUTPUT)    OPEN SYSPRINT DD
         OPEN  (SYSIN,INPUT)
GETAREC  EQU   *                  GET PARMS FROM SYSIN
         GET   SYSIN,INAREA
         LA    R2,INAREA          POINT TO INPUT AREA
         LA    R3,79              LIMIT-1 OF CARD AREA
CHKDEL   EQU   *                  PROCESS PARMS FROM SYSIN
         CLI   0(R2),X'40'        CHECK FOR DELIMETER OF BLANK
         BE    ECHKDEL
         CLI   0(R2),C','         CHECK FOR DELIMITER OF COMMA
         BE    ECHKDEL
         CLC   0(6,R2),=C'SDATE=' STARTING DATE KEYWORD
         BE    SDATERT            YES, GO TO ROUTINE
         CLC   0(6,R2),=C'EDATE=' ENDING DATE KEYWORD
         BE    EDATERT            YES, GO TO ROUTINE
         CLC   0(6,R2),=C'STIME=' STARTING TIME KEYWORD
         BE    STIMERT            YES, GO TO ROUTINE
         CLC   0(6,R2),=C'ETIME=' ENDING TIME KEYWORD
         BE    ETIMERT            YES, GO TO ROUTINE
         OI    ERRORSW,CNTLCARD   INDICATE CONTROL CARD ERROR
         B     ERROR1             NO, INVALID CONTROL CARD
SDATERT  EQU   *
         PACK  SDATE(4),6(5,R2)   PACK INPUT STARTING DATE YYDDD
         OI    SDATE+3,X'0F'      FORCE SIGN IN CASE OF BLANKS
         B     GETAREC
EDATERT  EQU   *
         PACK  EDATE(4),6(5,R2)   PACK INPUT ENDING DATE
         OI    EDATE+3,X'0F'      FORCE SIGN IN CASE OF BLANKS
         B     GETAREC
STIMERT  EQU   *
         PACK  STIME(4),6(6,R2)   PACK INPUT STARTING TIME-HHMMSS
         OI    STIME+3,X'0F'      FORCE SIGN IN CASE OF BLANKS
*                                 ALLOWS HH INSTEAD OF HH0000
*                                 MUST SUPPLY AT LEAST HH
         B     GETAREC
ETIMERT  EQU   *
         PACK  ETIME(4),6(6,R2)   PACK INPUT ENDING TIME
         OI    ETIME+3,X'0F'      FORCE SIGN IN CASE OF BLANKS
         B     GETAREC
ECHKDEL  EQU   *
         LA    R2,1(R2)           NEXT POSTION
         BCT   R3,CHKDEL          KEEP CHECKING SAME RECORD
         B     GETAREC            GET NEXT RECORD
ENDIN    EQU   *
         CLOSE (SYSIN)            CLOSE IT HERE FOR NOW
*        ALL DONE WITH REG 2 AND 3
OPENSMF  EQU   *
         UNPK  L4STIME(6),STIME(4) MOVE TO TITLE LINE
         UNPK  L4ETIME(6),ETIME(4)
         UNPK  L4SDATE(5),SDATE(4)
         UNPK  L4EDATE(5),EDATE(4)
         PUT   PRINT1,PLINEA
         PUT   PRINT1,PLINEB
         PUT   PRINT1,PLINEC
         PUT   PRINT1,PLINED
         PUT   PRINT1,PLINEE
         PUT   PRINT1,PLINEF
         PUT   PRINT1,PLINE4
         OPEN  (SMFOUT,OUTPUT)    OPEN THE OUTPUT FILE
         OPEN  (SMFIN,INPUT)      OPEN THE SMF FILE
         MVC   TOTALRMF,ZEDS      ZERO OUT TOTAL FIELD
**       SHOULD BE ALL DONE WITH REG 2
         USING SMF74HDR,R2
READSMF  EQU   *
         L     R2,@SMFIN          RE-ESTABLISH ADDR OF SMF REC AREA
         GET   SMFIN,SMF74HDR     GET A SMF REC
CHECKSMF EQU   *
         CLC   SMF74RTY,=X'46'    IS THIS A 70 REC
         BL    READSMF            IF LESS THAN GET ANOTHER REC
         CLC   SMF74RTY,=X'4F'    IS THIS A 79 REC
         BH    READSMF            IF GREATER THAN GET ANOTHER REC
         MVC   S74PRS,SMF74PRS    SAVE OFFSET TO PROD SECTION
         MVC   S74TYPE,SMF74RTY
         DROP  R2
         A     R2,S74PRS
         USING SMF74PRO,R2
         CLC   SMF74DAT,SDATE     INTERVAL DATE
         BL    READSMF            SMF74DAT LT SDATE, GET A REC
         CLC   SMF74DAT,EDATE     INTERVAL DATE
         BH    READSMF            SMF74DAT GT EDATE, GET A REC
         CLC   SMF74IST,STIME     INTERVAL START TIME
         BL    READSMF            SMF74IST LT STIME, GET A REC
         CLC   SMF74IST,ETIME     INTERVAL START TIME
*                                  DOESNOT ACCOUNT FOR DURATION
         BH    READSMF            SMF74IST GT ETIME, GET A REC
PROCREC  EQU   *                  NOW HAVE A GOOD SMF REC
         L     R2,@SMFIN
         USING SMF74HDR,R2
         PUT   SMFOUT,SMF74HDR    WRITE OUTPUT REC
DDSLOOP  EQU   *                  NOW HAVE A GOOD SMF REC
         L     R5,@TYPETAB        ADDR OF TYPE TABLE
         USING TYPETAB,R5
         LA    R6,10              MAX AMOUNT OF ENTRIES IN TABLE -1
TTABLOOP EQU   *                  NOW HAVE A GOOD SMF REC
         CLC   TYPEREC(1),EFFS    START OF TABLE
         BE    ADDTAB             NO, KEEP LOOKING FOR REC TYPE
         CLC   TYPEREC,S74TYPE    IS THIS THE REC TYPE
         BE    TYPEHIT            YES,
         LA    R5,5(R5)           NO, POINT TO NEXT ENTRY
         BCT   R6,TTABLOOP
ADDTAB   EQU   *
         MVC   TYPEREC,S74TYPE    MOVE IN NEW TYPE REC
         MVC   NUMRECS,ZEDS       MOVE ZEROS TO NUMRECS
         MVC   5(1,R5),EFFS       MARK END OF TABLE
TYPEHIT  EQU   *
         AP    1(4,R5),=P'1'      ADD 1 TO REC COUNTER IN TABLE
         AP    TOTALRMF,=P'1'     ADD 1 TO TOTAL REC COUNTER
         B     READSMF            READ ANOTHER REC
ENDSMF   EQU   *
         PUT   PRINT1,PLINE3
PRTRECS  EQU   *
         DROP  R5
         L     R3,@TYPETAB         ADDR OF TYPETAB
PRTTABR  EQU   *                   PRINT INFO IN TABLES
         USING TYPETAB,R3
         CLC   0(1,R3),EFFS        END OF TABLE
         BE    PTOTAL              YES, ALL DONE
         SR    6,6
         IC    6,TYPEREC
         CVD   6,DOUBLEWK
         UNPK  L2RECTYP,DOUBLEWK
         OI    L2RECTYP+1,X'F0'
         MVC   L2NUMREC,MASK1
         ED    L2NUMREC,NUMRECS
         PUT   PRINT1,PLINE2       PRINT A LINE
         LA    R3,5(R3)           NEXT ENTRY
         B     PRTTABR             PRINT NEXT ENTRY
PTOTAL   EQU   *
         MVC   L5TOTREC,MASK1
         ED    L5TOTREC,TOTALRMF
         PUT   PRINT1,PLINE5       PRINT A LINE
RETURN   EQU   *
         CLOSE (PRINT1)
         CLOSE (SMFOUT)
         CLOSE (SMFIN)
         L     R0,SIZESMF                        .FREE SMF REC AREA
         L     R1,@SMFIN                   ADDR OF SMFIN
         FREEMAIN R,LV=(0),A=(1)           SAVE AREA
         L     R0,SIZE                           .FREE THIS WORK AREA
         LR    R1,R11                      ADDR OF WORK AREA..
         FREEMAIN R,LV=(0),A=(1)           SAVE AREA
         L     R13,SAVERET+4
         LM    14,12,12(13)
         SR    15,15           COND CODE
         BR    14
ERROR1   EQU   *                       RTN TO CONVERT ERROR FLAG1
         EJECT
*                             WORK DSECT LATER AS DS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         DS    0F
SIZE     DC    AL1(000),AL3(WORKEND-WORKSTRT)  SIZE OF GET MAIN
SIZESMF  DC    AL1(000),AL3(32767)    SIZE OF INPUT AREA FOR SMF
SIZETAB  DC    AL1(000),AL3(5*10)     SIZE OF TYPE TABLE
SAVERET  DS    18F
SDATE    DC    X'0000000F'     00YYDDDF   DEFAULTS
EDATE    DC    X'0099366F'     00YYDDDF   DEFAULTS
STIME    DC    X'0000000F'     0HHMMSSF   DEFAULTS
ETIME    DC    X'0250000F'     0HHMMSSF   DEFAULTS
DOUBLEWK DC    D'0'            CVB/CVD WORK AREA
MASK1    DC    X'40206B2020206B202120' D=7 L=11  EDIT MASK
EFFS     DC    X'FF'         HEX EFFS
ZEDS     DC    X'0000000F'  PACKED ZERO
PLINE5   DS    0CL133
         DC    X'F0'           CAR. CONTROL F0=DOUBLE
         DC    CL2' '
         DC    C'TOTAL RECORDS'
         DC    CL2' '
L5TOTREC DS    CL11
         DC    CL104' '
PLINE4   DS    0CL133          PRINTLINE
L4CC     DC    X'F1'           CAR. CONTROL F1=TOP        1
         DC    C'SUMMARY OF SMF RECORDS             '    35
         DC    4C' '                                '     4
         DC    C' FROM '                                  6
L4STIME  DS    CL6     HHMMSS                             6
         DC    C' ON '                                    4   56
L4SDATE  DS    CL5     YYDDD                              5
         DC    C' UNTIL '                                 7
L4ETIME  DS    CL6     HHMMSS                             6
         DC    C' ON '                                    4
L4EDATE  DS    CL5     YYDDD                              5   83
         DC    CL50' '                                   50  133
PLINE2   DS    0CL133          PRINTLINE
L2CC     DC    X'40'           CAR. CONTROL 40=SINGLE
         DC    CL8' '
L2RECTYP DS    CL2
         DC    CL7' '
L2NUMREC DS    CL11
         DC    CL104' '
PLINE3   DS    0CL133          PRINTLINE
L3CC     DC    X'F0'           CAR. CONTROL F0=DOUBLE     1
         DC    CL3' '
         DC    C'RECORD TYPE'
         DC    CL5' '
         DC    C'NUMBER OF RECORDS'
         DC    CL96' '
PLINEA   DS    0CL133
LACC     DC    X'F1'
         DC    CL30' '
         DC    C'THIS PROGRAM PULLS OFF SMF RECORDS 70-79'
         DC    CL62' '
PLINEB   DS    0CL133
LBCC     DC    X'F0'
         DC    CL30' '
         DC    C'THESE ARE THE VALID CONTROL CARDS       '
         DC    CL62' '
PLINEC   DS    0CL133
LCCC     DC    X'40'
         DC    CL30' '
         DC    C'SDATE=YYDDD   DEFAULT IS 00000          '
         DC    CL62' '
PLINED   DS    0CL133
LDCC     DC    X'40'
         DC    CL30' '
         DC    C'EDATE=YYDDD   DEFAULT IS 99366          '
         DC    CL62' '
PLINEE   DS    0CL133
LECE     DC    X'40'
         DC    CL30' '
         DC    C'STIME=HHMMSS  DEFAULT IS 000000         '
         DC    CL62' '
PLINEF   DS    0CL133
LFCE     DC    X'40'
         DC    CL30' '
         DC    C'ETIME=HHMMSS  DEFAULT IS 250000         '
         DC    CL62' '
SMFOUT   DCB   DDNAME=SMFOUT,DSORG=PS,MACRF=(PM),                      X
               LRECL=32767,RECFM=VBS
PRINT1   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    X
               BLKSIZE=133,LRECL=133,RECFM=FBA
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),EODAD=ENDIN,           X
               BLKSIZE=80,LRECL=80,RECFM=FB
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=(GM),EODAD=ENDSMF,          X
               LRECL=32767,RECFM=VBS
****           BLKSIZE=4096,LRECL=32767,RECFM=VBS
         EJECT
WORK     DSECT
WORKSTRT DS    0D
         DS    F
         DS    F
PARMAREA DS    F               ADDR OF PARM AREA
@SMFIN   DS    F               ADDR OF SMFREC AFTER READ
@TYPETAB DS    F               ADDR OF TYPETAB
S74PRS   DS    F
S74TYPE  DS    CL1
INAREA   DS    CL80            CNTL CARD INPUT AREA
ERRORSW  DS    X               SYSIN
CNTLCARD EQU   X'01'           ERROR ON CONTROL CARD
TOTALRMF DS    PL4
WORKEND  DS    0D
         EJECT
         ERBSMF74
         EJECT
TYPETAB  DSECT
TTENTRY  DS    0CL5
TYPEREC  DS    BL1
NUMRECS  DS    PL4
         EJECT
         END
