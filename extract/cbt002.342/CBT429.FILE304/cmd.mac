./ ADD NAME=LVXOP
 PROC 1 LEVEL   /* LEVEL UNDER WHICH USER WANTS D/S LIST */ -
 OP('LISTD ') /* OPERATION USER WANTS TO DO TO D/S LIST */ -
 IGNORE()       /* HIGH-LEVEL INDEX STRING TO IGNORE ON LEFT */ -
 OUTINDEX()     /* HIGH-LEVEL INDEX STRING TO INSERT ON LEFT */ -
 MASK()         /* SELECTED FILES WILL CONTAIN THIS MASK     */ -
 PREOP()        /* STRING TO PREFIX TO 2ND DSN */ -
 POSTOP()       /* STRING TO SUFFIX TO 2ND DSN */ -
 NOAPOST        /* REMOVE APOSTROPHES ON OUTPUT */
 CONTROL NOFLUSH NOMSG NOPROMPT
 CONTROL END(OD)
/* CONTROL   LIST   CONLIST   SYMLIST
  /* ************************************************************* */
  /* BHX 91-03-19 UPDATED TO ADD FUNCTION OF CREATING SIDE-BY-SIDE */
  /*     LIST OF DATASET NAMES.                                    */
  /* BHX 91-03-25 CORRECTED FOR TAPE DATASETS.                     */
  /*                                                               */
  /* BHX 91-03-01 IMPORTED TO DEPT OF ENERGY                       */
  /* BQH 83-03-23 B.HOGMAN                                         */
  /*     THIS CLIST ASSEMBLES A LIST OF DATASET NAMES TO DELETE.   */
  /*     IT USES "LC" COMMAND TO ASSEMBLE THE LIST, THEN INVOKES   */
  /*     SPF EDIT DATASET FUNCTION SO THAT THE USER CAN EXAMINE    */
  /*     THE CANDIDATE LIST OF DATASETS.  THE USER REMOVES FROM    */
  /*     THE LIST THOSE NAMES OF DATASETS TO KEEP AND HITS THE     */
  /*     "END" KEY.  THEN, THE USER HAS THE OPTION OF CONTINUING   */
  /*     WITH DELETIONS, RETURNING TO RE-CHECK THE NAMES FILE      */
  /*     UNDER SPF EDIT DATASET, OR TO CANCEL THE LVXOP FUNCTION   */
  /*     WITH NO OPERATION.                                        */
  /*                                                               */
  /* ************************************************************* */

/*===================================================================*/
/* DEFINE SOME CLIST VARIABLES USED TO CONSTRUCT DATA STRINGS.       */
/*===================================================================*/
 SET AST = &STR(*)
 SET PL  = &STR(+)
 SET SL  = &STR(/)
 IF X NE X&STR(&NOAPOST) THEN SET APOST =
 ELSE SET APOST = &STR(')

/*===================================================================*/
/* DEFINE TEMPORARY FILE TO USE FOR OUTPUT AND EDIT.                 */
/*===================================================================*/
SET D =D&SUBSTR(7:8,&SYSDATE)&SUBSTR(1:2,&SYSDATE)&SUBSTR(4:5,&SYSDATE)
SET T =T&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)&SUBSTR(7:8,&SYSTIME)
 SET F = LVXOP.&D..&T
 FREE DD(LVXOPEFW LVXOPEFE)
 ALLOC DS(&F..WORK.CNTL) NEW RECFM(F B) BLKSIZE(3120) LRECL(80) +
    SPACE(1 10) TRACKS DD(LVXOPEFW) REUSE RELEASE
 ALLOC DS(&F..EDIT.CNTL) NEW RECFM(V B) BLKSIZE(6300) LRECL(255) +
    SPACE(1 10) TRACKS DD(LVXOPEFE) REUSE RELEASE

/*===================================================================*/
/* INVOKE TSO COMMAND 'LC' TO RETURN A LISTING OF DATASETS UNDER     */
/* GIVEN HIGH-LEVEL INDEX NAME.                                      */
/*===================================================================*/
 IF X EQ X&STR(&IGNORE) THEN +
    IF X NE X&STR(&OUTINDEX) THEN DO
       SET IGNORE = &LEVEL
       SET IGNORE = &STR(&IGNORE)&STR(.)
    OD
 IF X NE X&STR(&IGNORE) THEN +
    IF &STR(') EQ &SUBSTR(1:1,&STR(&IGNORE)) THEN +
       SET IGNORE = &SUBSTR(2:&LENGTH(&IGNORE)-1,&IGNORE)
 SET IL = &LENGTH(&STR(&IGNORE))
 SET IL = &IL+1
 IF &STR(') NE &SUBSTR(1:1,&STR(&LEVEL)) THEN +
    SET LVL = &STR('&SYSPREF..&LEVEL')
 ELSE +
    SET LVL = &STR(&LEVEL)

 CONTROL FLUSH MSG PROMPT
 LC L(&LVL) DD(LVXOPEFW)
 CONTROL NOFLUSH NOPROMPT

/*===================================================================*/
/* READ OUTPUT FROM 'LC' COMMAND AND CONSTRUCT FILE FOR ISPF EDIT.   */
/*===================================================================*/
 OPENFILE LVXOPEFW INPUT
 OPENFILE LVXOPEFE OUTPUT
 SET LVXOPEFE = &STR(CONTROL LIST MSG PROMPT)
 PUTFILE LVXOPEFE
 ERROR DO
    IF &LASTCC = 400 THEN GOTO ENDFILE
    ERROR OFF
 OD
 LOOP: +
    CONTROL NOMSG
 ERROR DO
    IF &LASTCC = 400 THEN GOTO ENDFILE
    ERROR OFF
 OD
    GETFILE LVXOPEFW
    ERROR OFF
/*===================================================================*/
/* COPY DSNAME RETURNED FROM 'LC' COMMAND.  SET 'R' TO CONTAIN ONLY  */
/* THE RIGHT PORTION OF EACH DSNAME AFTER THE 'IGNORE' STRING ON     */
/* LEFT.                                                             */
/*===================================================================*/
    SET S   = &SUBSTR(13:&LENGTH(&LVXOPEFW),&LVXOPEFW)
    SET S   = &S
    IF X NE X&STR(&MASK) THEN +
    IF 0 EQ &SYSINDEX(&STR(&MASK),&STR(&S)) THEN GOTO LOOP
    IF 0 LT &LENGTH(&S) THEN +
       SET R   = &SUBSTR(&IL:&LENGTH(&S),&S)
    SET C   = &SUBSTR(13:13,&LVXOPEFW)
    IF X&C = X THEN SET V = &STR(/)&STR(*)
    ELSE SET V = &STR(&OP)
    SET C = &SUBSTR(8:8,&LVXOPEFW)
    IF X&C NE X THEN SET V = &STR(/)&STR(*)
    SET LVXOPEFE = &STR(&V)&STR(&APOST)&S&STR(&APOST)+
       &STR(&PREOP)
    IF X NE X&STR(&OUTINDEX) THEN +
    SET LVXOPEFE = &STR(&LVXOPEFE)+
       &STR(&APOST)&OUTINDEX&R&STR(&APOST)
    ELSE IF X NE X&STR(&IGNORE) THEN +
       SET LVXOPEFE = &STR(&LVXOPEFE)&R
    SET LVXOPEFE = &STR(&LVXOPEFE)+
       &STR(&POSTOP)
    PUTFILE LVXOPEFE
    GOTO LOOP
 ENDFILE: ERROR OFF
    CLOSFILE LVXOPEFW
    ALLOC F(LVXOPEFW) REUSE OLD DELETE DS(&F..WORK.CNTL)
    CLOSFILE LVXOPEFE
    ALLOC F(LVXOPEFE) REUSE OLD DELETE DS(&F..EDIT.CNTL)
 REEDIT: +
 ISPEXEC EDIT DATASET(&F..EDIT.CNTL)
 ISPEXEC CONTROL DISPLAY LINE START(1)
 WRITE *** WARNING:  THOSE DATASET NAMES REMAINING IN FILE
 WRITE ***           WILL BE &OP'D.
 WRITE *** REPLY:
 WRITE ***           Y    TO CONTINUE WITH &OP'TION OF DATASETS
 WRITE ***
 WRITE ***           N    TO CANCEL LVXOP
 WRITE ***
 WRITE ***           R    TO REPEAT EDIT OF DATASET NAMES FILE
 WRITENR *** ENTER REPLY:
 READ &REPLY
 WRITE THANK YOU.
 IF XY EQ X&REPLY THEN GOTO DELETOK
 IF XN EQ X&REPLY THEN DO
    CONTROL NOMSG
    DEL (&F..EDIT.CNTL &F..WORK.CNTL)
    WRITE *** LVXOP CANCELLED ***
    EXIT CODE(12)
 OD
 WRITE OK, GOING TO SPF EDIT SO YOU CAN CHECK DATASET NAMES AGAIN.
 GOTO REEDIT
 DELETOK: +
    WRITE &OP'TION PROCEEDING.
 CONTROL MSG
 EX '&SYSPREF..&F..EDIT.CNTL' PROMPT LIST
 CONTROL NOMSG
 DEL (&F..EDIT.CNTL &F..WORK.CNTL)

ü
