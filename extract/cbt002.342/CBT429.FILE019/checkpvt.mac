CHECKPVT TITLE 'CHECK SIZE OF THE PRIVATE AREA IN MVS'
***********************************************************************
*                                                                     *
*                            C H E C K P V T                          *
*                                                                     *
*        THIS PROGRAM IS DESIGNED TO RUN IMMEDIATELY AFTER IPL        *
*        TO CHECK THE SIZE OF THE PRIVATE AREA.  SOFTWARE             *
*        MAINTENANCE OR IMPLEMENTATION OF NEW PRODUCTS MAY SHIFT      *
*        THE PRIVATE AREA SIZE DOWN AN EXTRA MEG IN ESA.  THE         *
*        PARM VALUE ENTERED SPECIFIES THE SIZE OF THE PRIVATE         *
*        AREA IN 'K' UNITS THAT IS EXPECTED.  IF THE AREA IS          *
*        SMALLER THAN THIS VALUE, A NON-ROLLABLE MESSAGE WILL BE      *
*        WRITTEN TO THE SYSTEM CONSOLE                                *
*                                                                     *
*        NOTE:                                                        *
*                                                                     *
*        THIS PROGRAM MUST BE AUTHORIZED IN ORDER FOR THE             *
*        WARNING MESSAGE TO REMAIN ON THE OPERATOR CONSOLE            *
*        AFTER JOB TERMINATION.                                       *
*                                                                     *
*        THIS UTILITY PROGRAM WAS ORIGINALLY WRITTEN BY ANOTHER       *
*        PERSON IN ANOTHER INSTALLATION SO I CANNOT TAKE CREDIT       *
*        FOR IT.  I HAVE CLEANED IT UP A LITTLE, BUT BECAUSE OF       *
*        LACK OF PERMISSION, I CANNOT GIVE THE NAME OF THE            *
*        ORIGINAL AUTHOR.                                             *
*                                                                     *
*                                                                     *
*        MODIFIED BY:                                                 *
*                   JOHN V. HOOPER                                    *
*                   FOOD LION, INC.                                   *
*                   2110 EXECUTIVE DRIVE                              *
*                   SALISBURY, N.C.  28145-1330                       *
*                                                                     *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*      THE AUTHORS INCLUDING FOOD LION, ITS OFFICERS,                 *
*      DIRECTORS, AND EMPLOYEES MAKE NO REPRESENTATION OR             *
*      WARRANTY OF ANY KIND WHATSOEVER, INCLUDING, BUT NOT            *
*      LIMITED TO, REPRESENTATIONS OR WARRANTIES, EXPRESS OR          *
*      IMPLIED, OR MERCHANTABILITY, FITNESS FOR USE OR PURPOSE,       *
*      ACCURACY OR COMPLETENESS OF PROCESSES, PROCEDURES,             *
*      DESIGNS, DEFINITIONS, INSTRUCTIONS, INFORMATION, OR            *
*      FUNCTIONING OF ANY PROGRAMS, DOCUMENTS, OR RELATED             *
*      MATERIALS; THE AUTHORS FURTHER EXPRESSLY DISCLAIM ANY          *
*      KNOWLEDGE OF PURPOSE FOR WHICH THESE PROGRAMS,                 *
*      DOCUMENTS, OR RELATED MATERIAL MAY BE UTILIZED OR THEIR        *
*      APPLICABILITY FOR SUCH USE, NOR SHALL THE FACT OF MAKING       *
*      THEM AVAILABLE CONSTITUTE ANY SUCH REPRESENTATION,             *
*      WARRANTY, OR KNOWLEDGE; NOR DO THE AUTHORS ASSUME ANY          *
*      LIABILITY, RESPONSIBILITY, OR OBLIGATION ARISING FROM          *
*      THE USE OR MALFUNCTIONING OF OF THESE COMPUTER PROGRAMS,       *
*      DOCUMENTATION, OR RELATED MATERIALS.                           *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*                         SAMPLE JCL                                  *
*                                                                     *
*        //CHECKPVT PROC                                              *
*        //CHECKPVT EXEC PGM=CHECKPVT,PARM='9000'                     *
*                                                                     *
***********************************************************************
CHECKPVT CSECT
CHECKPVT AMODE 31
CHECKPVT RMODE 24
***********************************************************************
*                                                                     *
*                R E G I S T E R   E Q U A T E S                      *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
***********************************************************************
*                                                                     *
*        START OF PROGRAM                                             *
*                                                                     *
***********************************************************************
         SAVE  (14,12),,CHECKPVT-&SYSDATE-&SYSTIME
         LR    R12,R15             SET BASE REGISTER
         USING CHECKPVT,R12        ESTABLISH PROGRAM ADDRESSABILITY
         LA    R11,SAVE            LOAD ADDRESS OF NEW SAVE AREA
         ST    R13,SAVE+4          SAVE OLD SAVE ADDRESS IN NEW
         ST    R11,8(R13)          SAVE NEW SAVE ADDRESS IN OLD
         LR    R13,R11             SET NEW SAVE AREA ADDRESS
***********************************************************************
*                                                                     *
*        EXTRACT EXPECTED PRIVATE AREA SIZE FROM PARM                 *
*                                                                     *
***********************************************************************
         LTR   R1,R1               TEST ADDRESS OF PARM
         BZ    BADPARM             NONE, ERROR
         ICM   R2,B'1111',0(R1)    LOAD ADDRESS OF PARM
         BZ    BADPARM             NONE, ERROR
         LH    R3,0(R2)            GET LENGTH OF PARM
         LTR   R3,R3               TEST PARM LENGTH
         BNP   BADPARM             NOT GREATER THAN ZERO, ERROR
         CH    R3,=H'8'            CHECK MAXIMUM LENGTH OF PARM
         BH    BADPARM             EXCEEDS MAXIMUM, BRANCH
         LR    R15,R3              SET LOOP COUNTER
         LA    R14,2(R2)           POINT TO ACTUAL PARM VALUE
PARMLOOP CLI   0(R14),C'0'         SEE IF NUMERIC
         BL    BADPARM             NO, ERROR
         CLI   0(R14),C'9'         SEE IF NUMERIC
         BH    BADPARM             NO, ERROR
         LA    R14,1(R14)          ADD 1 TO PARM LENGTH
         BCT   R15,PARMLOOP        LOOP THROUGH PARM
         BCTR  R3,0                MAKE MACHINE LENGTH
         EX    R3,PACKPARM         PACK THE PARM VALUE
         CVB   R9,DWORD            CONVERT TO BINARY
         SLL   R9,10               MULTIPLY BY 1024 FOR 'K'
         B     CKSIZE              GO CHECK SIZE OF PRIVATE AREA
PACKPARM PACK  DWORD,2(0,R2)       **** EXECUTE ONLY ****
         EJECT
***********************************************************************
*                                                                     *
*        COMPARE PARM SIZE TO THE START ADDRESS OF CSA                *
*                                                                     *
***********************************************************************
CKSIZE   L     R11,X'10'           POINT TO CVT
         USING CVT,R11             SET ADDRESSABILITY TO CVT
         L     R10,CVTSHRVM        GET START ADDRESS OF CSA
         CR    R9,R10              IS PRIVATE AREA LARGE ENOUGH
         BNH   STGISOK             YES, BRANCH
         WTO   'FLPVT002W PRIVATE AREA IS TOO SMALL - NOTIFY TSD IMMEDIX
               ATELY',ROUTCDE=(1,11),DESC=(1)
         B     RETURN              GET OUT
STGISOK  WTO   'FLPVT003I PRIVATE AREA IS LARGE ENOUGH - CONTINUE PROCEX
               SSING',ROUTCDE=(2,11)
RETURN   L     R13,4(,R13)         RESTORE ADDRESS OF ORIGINAL SAVE
         RETURN (14,12),,RC=0      RETURN TO CALLER
BADPARM  WTO   'FLPVT001E BAD PARM ON EXEC STATEMENT - JOB ABORTED',   M
               ROUTCDE=(2,11)
         B     RETURN              GET OUT
***********************************************************************
*                                                                     *
*        CONSTANTS AND WORK AREA                                      *
*                                                                     *
***********************************************************************
SAVE     DS    18F                 REGISTER SAVE AREA
DWORD    DS    D                   DOUBLE WORD WORK AREA
         CVT   DSECT=YES
         END
