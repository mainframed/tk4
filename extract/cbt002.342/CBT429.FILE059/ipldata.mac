IPLDATA  CSECT
         PRINT NOGEN
         STM   R14,R12,12(R13)
         BALR  R12,0                   LOAD REGISTER 12 WITH ENTRY
         USING *,R12
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,9(,R13)
         LA    R13,SAVEAREA
*
* WORKING CODE GOES HERE
         L     R15,CVTPTR
         L     R2,196(,R15)
* DATE (JULIAN)
         LA    R3,340(,R2)
         MVC   IPLDATE,JULPATT
         ED    IPLDATE,1(R3)
* DATE (GREGORIAN)
         BAL   R8,DATECONV
* TIME
         LA    R4,336(,R2)
         L     R1,0(,R4)
         SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  IPLTIME(2),DOUBLE+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'6000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  IPLTIME+3(2),DOUBLE+6(2)
* IPLTEXT:
         L     R9,CVTPTR
         S     R9,=F'256'
         USING CVTFIX,R9
         L     R10,CVTSMCA
         USING SMCABASE,R10
* IPLTEXT: SYSID
         MVC   IPLID,SMCASID
* IPLTEXT: FMID
         MVC   IPLFMID,CVTPRODI
* IPLTEXT: PRODUCT LEVEL
         MVC   IPLLVL,CVTPRODN
* IPLTEXT: RELEASE
         MVC   IPLRLSE,CVTRELNO
* IPLTEXT: CPU MODEL
         MVC   CPUMODEL(2),CVTMDL
         L     R4,CPUMOD
         SLL   R4,4
         ST    R4,CPUMOD
         OI    CPUMOD+3,X'0F'
         UNPK  IPLCPU(4),CPUMOD+1(3)
         OI    IPLCPU+3,X'F0'
* IPLTEXT: SYSRES VOLUME
         L     R8,CVTPTR
         USING CVTMAP,R8
         L     R5,CVTSYSAD
         USING UCBOB,R5
         MVC   IPLVOL,UCBVOLI
* IPLTEXT: SYSRES NAME
         MVC   IPLCUA,UCBNAME
         L     R7,CVTEXT2
         USING CVTXTNT2,R7
* IPLTEXT: IO CONFIG MEMBER
         MVC   IPLIO,CVTIOCID
* IPLTEXT: NUCLEUS NUMBER
         MVC   IPLNUC,CVTNUCLS
* IPLTEXT: DAY OF WEEK
         MVC   IPLDAY(12),PARM3+20  DAY OF WEEK
* IPLTEXT: JULIAN
         MVC   IPLJUL,IPLDATE
* IPLTEXT: GREGORIAN
         MVC   IPLGREG,PARM3       MONTH DAY YEAR
* IPLTEXT: TIME
         MVC   IPLTM,IPLTIME
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLDATE                                       *
***********************************************************************
*
         L     R15,CVTTVT
         L     R15,TSVTVACC-TSVT(,R15)
*
         LTR   R15,R15
         BNZ   CALL441
LINK441  LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,                                           X
               IPLDTNMP,                                               X
               IPLDTNML,                                               X
               IPLDATEP,                                               X
               IPLDATEL,                                               X
               TOKEN),                                                 X
               VL=1
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLTIME                                       *
***********************************************************************
*
         L     R15,CVTTVT
         L     R15,TSVTVACC-TSVT(,R15)
*
         LTR   R15,R15
         BNZ   CALL441
         LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,                                           X
               IPLTMEMP,                                               X
               IPLTMEML,                                               X
               IPLTIMEP,                                               X
               IPLTIMEL,                                               X
               TOKEN),                                                 X
               VL=1
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLTXTE                                       *
***********************************************************************
*
         L     R15,CVTTVT
         L     R15,TSVTVACC-TSVT(,R15)
*
         LTR   R15,R15
         BNZ   CALL441
         LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,                                           X
               IPLTXTMP,                                               X
               IPLTXTML,                                               X
               IPLTXTEP,                                               X
               IPLTXTEL,                                               X
               TOKEN),                                                 X
               VL=1
         B     RET441
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLDATE                                       *
***********************************************************************
*
CALL441  CALL  (15),                                                   X
               (ECODE,                                                 X
               IPLDTNMP,                                               X
               IPLDTNML,                                               X
               IPLDATEP,                                               X
               IPLDATEL,                                               X
               TOKEN),                                                 X
               VL
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLTIME                                       *
***********************************************************************
*
         L     R15,CVTTVT
         L     R15,TSVTVACC-TSVT(,R15)
         CALL  (15),                                                   X
               (ECODE,                                                 X
               IPLTMEMP,                                               X
               IPLTMEML,                                               X
               IPLTIMEP,                                               X
               IPLTIMEL,                                               X
               TOKEN),                                                 X
               VL
*
***********************************************************************
*  CODE TO ADD VARIABLE IPLTXTE                                       *
***********************************************************************
*
         L     R15,CVTTVT
         L     R15,TSVTVACC-TSVT(,R15)
         CALL  (15),                                                   X
               (ECODE,                                                 X
               IPLTXTMP,                                               X
               IPLTXTML,                                               X
               IPLTXTEP,                                               X
               IPLTXTEL,                                               X
               TOKEN),                                                 X
               VL
RET441   LTR   R15,R15
         BNZ   ERRORRTN
*
***********************************************************************
*                                                                     *
*        CLEANUP AREAS GETMAINED BY PARS,                             *
*           SET RETURN CODE, RESTORE REGISTERS AND                    *
*           RETURN TO CALLER                                          *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
ERRORRTN DS    0H
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
*
***********************************************************************
*                                                                     *
*         DATE CONVERSION                                             *
*                                                                     *
***********************************************************************
*
DATECONV LA    R1,3
         ST    R1,PARM2
         MVC   PARM1(4),0(R3)
         LA    R1,16+128
         ST    R1,PARM4
         LA    R1,PARMPTRS
         LA    R14,PARM1
         ST    R14,0(,R1)
         LA    R14,PARM2
         ST    R14,4(,R1)
         LA    R14,PARM3
         ST    R14,8(,R1)
         LA    R14,PARM4
         ST    R14,12(,R1)
         LA    R14,PARM5
         ST    R14,16(,R1)
         L     R15,=V(JULSUB)
         BALR  R14,R15
         BR    R8
*
***********************************************************************
*                                                                     *
*         DATA AREAS                                                  *
*                                                                     *
***********************************************************************
*
         LTORG
*
JULPATT  DC    X'4020204B202020'
JULDATE  DS    CL7
*
*********************** WORKAREAS *************************************
*
******** GREGORIAN DATE **********
*
IPLDTNM  DC    CL7'IPLDATE'
IPLDTNML DC    F'7'
IPLDTNMP DC    A(IPLDTNM)
IPLDATE  DC    CL7'YY.DDD'
IPLDATEL DC    F'7'
IPLDATEP DC    A(IPLDATE)
*
******** GREGORIAN TIME **********
*
IPLTMEM  DC    CL7'IPLTIME'
IPLTMEML DC    F'7'
IPLTMEMP DC    A(IPLTMEM)
IPLTIME  DC    CL6'HH:MM'
IPLTIMEL DC    F'6'
IPLTIMEP DC    A(IPLTIME)
*
******** IPL DATA ****************
*
IPLTXTM  DC    CL6'IPLTXT'
IPLTXTML DC    F'6'
IPLTXTMP DC    A(IPLTXTM)
IPLTXTE  DS    0C
         DC    CL6'SYSID='
IPLID    DS    CL4' '
         DC    CL10' GREGDATE='
IPLGREG  DS    CL20
         DC    CL5' DAY='
IPLDAY   DC    CL12' '
         DC    CL9' JULDATE='
IPLJUL   DC    CL7' '
         DC    CL6' TIME='
IPLTM    DC    CL6' '
         DC    CL5' CUA='
IPLCUA   DC    CL3' '
         DC    CL8' VOLSER='
IPLVOL   DC    CL6' '
         DC    CL6' FMID='
IPLFMID  DC    CL8' '
         DC    CL7' LEVEL='
IPLLVL   DC    CL8' '
         DC    CL9' RELEASE='
IPLRLSE  DC    CL4' '
         DC    CL11' IO CONFIG='
IPLIO    DC    CL2' '
         DC    CL9' NUCLEUS='
IPLNUC   DC    CL1' '
         DC    CL5' CPU='
IPLCPU   DC    CL4' '
IPLTXTEL DC    A(IPLTXTEL-IPLTXTE)
IPLTXTEP DC    A(IPLTXTE)
*
******** OTHER AREAS *************
*
CPUMOD   DS    0F
         DC    X'0000'
CPUMODEL DS    CL2
TOKEN    DC    F'0'
ECODE    DC    A(TSVEUPDT)
SAVEAREA DS    18F
DOUBLE   DS    D
*
         DS    0D
PARMPTRS DS    5F
PARM1    DS    F
PARM2    DS    F
PARM4    DS    F
PARM3    DS    CL32
PARM5    DS    32D
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         IEESMCA
*
*        PRINT GEN
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         PRINT NOGEN
*
UCBDSECT DSECT
         IEFUCBOB
*
         IKJTSVT
*
         END
