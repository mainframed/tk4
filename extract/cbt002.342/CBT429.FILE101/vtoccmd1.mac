PROC 0 DEBUG
CONTROL NOLIST NOFLUSH NOMSG NOPROMPT
IF &DEBUG NE THEN CONTROL LIST NOFLUSH MSG CONLIST SYMLIST PROMPT
IF &SYSPREF EQ &STR() THEN DO
                             SET &DSPRFX EQ &SYSUID
                           END
ELSE IF &SYSPREF EQ &SYSUID THEN DO
                                   SET &DSPRFX EQ &SYSUID
                                 END
ELSE DO
       SET &DSPRFX EQ &SYSPREF
     END
ISPEXEC CONTROL ERRORS RETURN
RESTART:ISPEXEC DISPLAY PANEL(USERVTOC)
IF &LASTCC = 8 THEN EXIT
FREE A(FBA)
FREE F(VTOCOUT)
SET &LASTCC = 0
SET &MAXCC = 0
IF &C1 EQ X THEN SET &HOLD EQ &STR(HOLD)
IF &C1 NE X THEN SET &HOLD EQ &STR(NOHOLD)
IF &VOLLIST EQ &STR() THEN SET &VOLLIST EQ &STR(DLXTSO)
IF &P EQ P THEN DO
                   SET &LR EQ 133
                   SET &BLK EQ 19019
                   SET &DSNLEN EQ &STR(DSNLEN(30))
                END
           ELSE DO
                   SET &LR EQ 151
                   SET &BLK EQ 19026
                   SET &DSNLEN EQ &STR()
                END
ATTR FBA RECFM(F B A) LRECL(&LR) BLKSIZE(&BLK)
SET &NUM = &SUBSTR(8:8,&SYSTIME)
ALLOC F(VTOCOUT) DA('&DSPRFX..TEMP&NUM..VTOCLIST') CATALOG SPACE(5 2) +
TRACKS USING(FBA) NEW UNIT(SYSTSO)
ERROR DO
          DELETE '&DSPRFX..TEMP&NUM..VTOCLIST'
          EXIT
      END
ATTN  DO
          DELETE '&DSPRFX..TEMP&NUM..VTOCLIST'
      END
IF &LEVEL NE &STR( ) THEN DO
                            SET &VLEVEL EQ &STR(LEVEL(&LEVEL))
                         END
                    ELSE DO
                            SET &VLEVEL EQ &STR()
                         END
IF &CONTAIN NE &STR( ) THEN DO
                            SET &VCONTAIN EQ &STR(CONTAINING(&CONTAIN))
                         END
                    ELSE DO
                            SET &VCONTAIN EQ &STR()
                         END
IF &ENDING NE &STR( ) THEN DO
                             SET &VENDING EQ &STR(ENDING(&ENDING))
                          END
                     ELSE DO
                             SET &VENDING EQ &STR()
                          END
IF &CAT NE &STR( ) THEN DO
                         SET &DSNLEN EQ &STR(DSNLEN(35))
                         SET &VCAT EQ &STR(CAT PRINT(REP,(ACTION,CAT)))
                       END
                  ELSE DO
                         SET &VCAT EQ &STR()
                       END
IF &SRTFLDS NE &STR( ) THEN DO
                          SET &VSRTFLDS EQ &STR(SORT(&SRTFLDS))
                       END
                  ELSE DO
                          SET &VSRTFLDS EQ &STR()
                       END
IF &SRTFLDS EQ &STR(NOSORT) THEN DO
                                    SET &VSRTFLDS EQ &STR(NOSORT)
                                 END
SET &VLIMIT EQ &STR()
SET &VAND1 EQ &STR()
SET &VAND2 EQ &STR()
SET &VAND3 EQ &STR()
IF &KEYWD1 NE &STR( ) THEN +
    SET &VLIMIT EQ &STR(LIMIT(&STR(&KEYWD1) &STR(&OP1) &STR(&VALUE1)))
IF &KEYWD2 NE &STR( ) THEN +
    SET &VAND1 EQ &STR(AND1(&STR(&KEYWD2) &STR(&OP2) &STR(&VALUE2)))
IF &KEYWD3 NE &STR( ) THEN +
    SET &VAND2 EQ &STR(AND2(&STR(&KEYWD3) &STR(&OP3) &STR(&VALUE3)))
IF &KEYWD4 NE &STR( ) THEN +
    SET &VAND3 EQ &STR(AND3(&STR(&KEYWD4) &STR(&OP4) &STR(&VALUE4)))
CONTROL MSG PROMPT
VTOC &VOLLIST HEADING('1 VTOC LIST OF &VOLLIST VOLUME(S)') +
 &VLEVEL +
 &VCONTAIN +
 &VENDING +
 &VCAT +
 &VSRTFLDS +
 &VLIMIT +
 &VAND1 +
 &VAND2 +
 &VAND3 +
 &DSNLEN
CONTROL NOMSG NOPROMPT
FREE A(FBA)
FREE F(VTOCOUT)
IF &P EQ P THEN DO
                   FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
                   ALLOC F(SYSIN) DUMMY
                   ALLOC F(SYSPRINT) DUMMY
                   ALLOC F(SYSUT1) DA('&DSPRFX..TEMP&NUM..VTOCLIST') +
                                                                 SHR
                   ALLOC F(SYSUT2) SYS(&C1) &HOLD KEEP
                END
IF &P EQ P THEN DO WHILE &N1 NE 0
                   $ IEBGENER
                   SET &N1 EQ &N1-1
                END
           ELSE DO
                 ISPEXEC BROWSE DATASET('&DSPRFX..TEMP&NUM..VTOCLIST')
                END
IF &P EQ P THEN DO
                   FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
                   ALLOC F(SYSIN) DA(*)
                   ALLOC F(SYSPRINT) DA(*)
                   ALLOC F(SYSUT1) NEW DELETE UNIT(VIO) SPACE(4) CYL
                   ALLOC F(SYSUT2) NEW DELETE UNIT(VIO) SPACE(4) CYL
                END
IF &P NE K THEN DO
                  DELETE '&DSPRFX..TEMP&NUM..VTOCLIST'
                END
IF &P EQ K THEN DO
                  CONTROL MSG
                  WRITE &DSPRFX..TEMP&NUM..VTOCLIST HAS BEEN SAVED
                  CONTROL NOMSG
                END
ERROR OFF
ATTN OFF
GOTO RESTART
END
