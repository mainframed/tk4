PROC 0 DEBUG
CONTROL NOMSG NOFLUSH NOPROMPT NOLIST
IF &DEBUG NE &STR() THEN +
  CONTROL MSG PROMPT CONLIST SYMLIST LIST
SET &ERRM EQ &STR()
RESTART: +
FREE F(SYSUT1,SYSUT2)
FREE F(SYSPUNCH,SYSIN,SYSPRINT,SYSLIB,SYSTERM)
FREE F(SYSLIB2,SYSLIB3,SYSLIB4,SYSLIB5,SYSLIB6,SYSLIB7,SYSLIB8)
FREE A(ASMLIST,ASMLIST2)
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC VGET (PRJ1,LIB1,ASMT,LNKT,MEM,BCLA,FASM) PROFILE
SET &LISTOPT EQ &STR()
SET &SLIB1 EQ &STR()
SET &SLIB2 EQ &STR()
SET &SLIB3 EQ &STR()
SET &SLIB4 EQ &STR()
SET &SLIBLIST EQ &STR()
IF &STR(&BCLA) EQ &STR() THEN +
  SET &BCLA EQ A
IF &STR(&BCLA) EQ &STR( ) THEN +
  SET &BCLA EQ A
ISPEXEC DISPLAY PANEL(S1ASM1) CURSOR(MEM) &ERRM
IF &LASTCC EQ 8 THEN +
  DO
    ALLOC F(SYSIN) DA(*)
    ALLOC F(SYSPRINT) DA(*)
    ALLOC F(SYSTERM) DA(*)
    ALLOC F(SYSUT1) NEW DELETE UNIT(VIO) SPACE(4) CYL
    ALLOC F(SYSUT2) NEW DELETE UNIT(VIO) SPACE(4) CYL
    EXIT
  END
SET &ERRM EQ &STR()
WHATIF DATASET('&PRJ1..&LIB1..&ASMT') CATALOGED
IF &LASTCC NE 0 THEN +
  DO
    SET &ERRM EQ &STR(MSG(SASM000))
    GOTO RESTART
  END
IF &SLIB1 NE &STR() THEN +
  DO
    ALLOC F(SYSLIB) DA('&SLIB1') SHR
    IF &SLIB2 NE &STR() THEN +
      DO
        ALLOC F(SYSLIB2) DA('&SLIB2') SHR
        SET &SLIBLIST EQ &STR(&SLIBLIST,SYSLIB2)
      END
      IF &SLIB3 NE &STR() THEN +
        DO
          ALLOC F(SYSLIB3) DA('&SLIB3') SHR
          SET &SLIBLIST EQ &STR(&SLIBLIST,SYSLIB3)
        END
        IF &SLIB4 NE &STR() THEN +
          DO
            ALLOC F(SYSLIB4) DA('&SLIB4') SHR
            SET &SLIBLIST EQ &STR(&SLIBLIST,SYSLIB4)
          END
    ALLOC F(SYSLIB5) DA('SER1.EDX.MACLIB') SHR
    ALLOC F(SYSLIB6) DA('SER1.EDXPP.MACLIB') SHR
    ALLOC F(SYSLIB7) DA('DLX1.EDLCOPY.ASM') SHR
    ALLOC F(SYSLIB8) DA('SER1.EDX.ASMCNTL') SHR
    ALLOC F(SYSLIB9) DA('SER1.HPPF.MACLIB') SHR
    SET &SLIBLIST EQ &STR(&SLIBLIST,SYSLIB5,SYSLIB6,SYSLIB7,SYSLIB8,SYS+
LIB9)
  END
ELSE +
  DO
    ALLOC F(SYSLIB) DA('SER1.EDX.MACLIB') SHR
    ALLOC F(SYSLIB2) DA('SER1.EDXPP.MACLIB') SHR
    ALLOC F(SYSLIB3) DA('DLX1.EDLCOPY.ASM') SHR
    ALLOC F(SYSLIB4) DA('SER1.EDX.ASMCNTL') SHR
    ALLOC F(SYSLIB5) DA('SER1.HPPF.MACLIB') SHR
    SET &SLIBLIST EQ &STR(&SLIBLIST,SYSLIB2,SYSLIB3,SYSLIB4,SYSLIB5)
  END
CONCAT (SYSLIB&SLIBLIST)
ALLOC F(SYSIN) DA('&PRJ1..&LIB1..&ASMT(&MEM)') SHR
ALLOC F(SYSPUNCH) DA('&PRJ1..&LIB1..&LNKT(&MEM)') SHR
ALLOC F(SYSUT1) UNIT(VIO) SPACE(10 2) CYLINDERS
ALLOC F(SYSUT2) UNIT(VIO) SPACE(10 2) CYLINDERS
ATTR ASMLIST RECFM(F B A) LRECL(133) BLKSIZE(2394)
ATTR ASMLIST2 RECFM(F B A) LRECL(121) BLKSIZE(968)
ALLOC F(SYSPRINT) DA('&PRJ1..&MEM..S1ASM.LIST') CATALOG SPACE(20 10) +
  TRACKS UNIT(SYSTSO) USING(ASMLIST) NEW
SET &ASMOPT EQ &STR()
IF &STR(&FASM) NE &STR() THEN +
  SET &ASMOPT EQ &STR(&STR(,)&STR(&FASM))
CALL 'SER1.HPPF.LOAD(S1HASM3M)' 'NORELOC,XREF&ASMOPT'
SET &RC = &LASTCC
WRITE SERIES 1 HOST ASSEMBLER COMPLETED RETURN CODE &RC
/* ISPEXEC BROWSE DATASET('&PRJ1..&MEM..S1ASM.LIST')  */
ISPEXEC VPUT (PRJ1,LIB1,ASMT,MEM,BCLA,FASM) PROFILE
DECONCAT SYSLIB
SET &SLIBLIST EQ &SUBSTR(2:&EVAL(&LENGTH(&SLIBLIST)+1),&SLIBLIST)
FREE F(&SLIBLIST)
FREE F(SYSUT1,SYSUT2)
FREE F(SYSPUNCH,SYSIN,SYSPRINT,SYSLIB)
FREE F(WORK)
ALLOC F(WORK) UNIT(VIO) CYLINDER SPACE(10 2) NEW DELETE
ALLOC F(SYSPRINT) DUMMY
ALLOC F(IN) DA('&PRJ1..&MEM..S1ASM.LIST') SHR
ALLOC F(LIST) DA('&PRJ1..&MEM..EDX.LIST') CATALOG SPACE(10 5)  +
  TRACKS UNIT(SYSTSO) USING(ASMLIST2) NEW
CALL 'SER1.EDX.LOAD(EDXLIST)' +
     'ISA(16)/PRINTER,XREF=$REF,RLD=NO,CONTROL=***'
SET &RC = &LASTCC
WRITE EDXLIST PROCESSOR COMPLETED RETURN CODE &RC
FREE F(SYSPRINT,WORK,IN,LIST)
FREE A(ASMLIST,ASMLIST2)
ISPEXEC BROWSE DATASET('&PRJ1..&MEM..EDX.LIST')
IF &LISTOPT NE &STR() THEN +
  DO
    IF &SUBSTR(1:1,&LISTOPT) EQ &STR(P) THEN +
      DO
        ALLOC F(SYSIN) DUMMY
        ALLOC F(SYSPRINT) DUMMY
        ALLOC F(SYSUT1) DA('&PRJ1..&MEM..EDX.LIST') SHR
        ALLOC F(SYSUT2) SYSOUT(&STR(&BCLA)) NOHOLD KEEP
        $ IEBGENER
        FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
      END
  END
DELETE '&PRJ1..&MEM..S1ASM.LIST'
DELETE '&PRJ1..&MEM..EDX.LIST'
GOTO RESTART
END
