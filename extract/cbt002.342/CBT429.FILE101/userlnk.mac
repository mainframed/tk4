PROC 0 DEBUG
CONTROL NOMSG NOFLUSH NOPROMPT NOLIST
IF &DEBUG NE &STR() THEN +
  CONTROL MSG PROMPT CONLIST SYMLIST LIST
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC VGET (ZPREFIX)
ISPEXEC VGET (PRJ1,LIB1,LNKT,MEM,FLNK,FSL1,FSL2) PROFILE
SET &OTHDSN EQ &STR()
SET &VSER EQ &STR()
RESTART: +
FREE F(SYSUT1)
FREE F(SYSPRINT,SYSLIB,SYSLIN,SYSLMOD)
ISPEXEC DISPLAY PANEL(LINK3)
IF &LASTCC EQ 8 THEN +
  EXIT
SET &SLIB1 EQ &STR()
SET &SLIB2 EQ &STR()
IF &FSL1 NE &STR() THEN +
  SET &SLIB1 EQ &STR(&STR(')&STR(&FSL1)&STR('))
IF &FSL2 NE &STR() THEN +
  SET &SLIB2 EQ &STR(&STR(,)&STR(')&STR(&FSL2)&STR('))
IF &STR(&SLIB1&SLIB2) NE &STR() THEN +
  ALLOC F(SYSLIB) DA(&SLIB1&SLIB2) SHR
IF &OTHDSN NE &STR() THEN +
  DO
    SET &UVSER EQ &STR()
    SET &OTHDSNQ EQ &OTHDSN
    ISPEXEC VGET (ZPREFIX)
    IF &ZPREFIX NE &STR() THEN +
      SET &OTHDSNQ EQ &STR(&ZPREFIX..&OTHDSN)
    IF &VSER NE &STR() THEN +
      SET &UVSER EQ &STR(UNIT(SYSALLDA) VOLUME(&VSER) )
    ALLOC F(SYSLMOD) DA('&OTHDSNQ') SHR &UVSER
  END
ELSE +
  ALLOC F(SYSLMOD) DA('&PRJ1..&LIB1..LOAD(&MEM)') SHR
ALLOC F(SYSUT1) UNIT(VIO) SPACE(10 2) CYLINDERS
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SYSLIN) DA('&PRJ1..&LIB1..&LNKT(&MEM)') SHR
$ IEWL '&FLNK'
SET &RC = &LASTCC
WRITE LINKAGE EDITOR COMPLETED RETURN CODE &RC
ISPEXEC CONTROL DISPLAY REFRESH
ISPEXEC VPUT (PRJ1,LIB1,LNKT,MEM,FLNK,FSL1,FSL2) PROFILE
FREE F(SYSUT1)
FREE F(SYSPRINT,SYSLIB,SYSLIN,SYSLMOD)
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SYSUT1) NEW DELETE UNIT(VIO) SPACE(4) CYL
GOTO RESTART
END
