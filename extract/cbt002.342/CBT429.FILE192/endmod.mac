         MACRO
         ENDMOD
.*+-------------------------------------------------------------------+
.*|                     MAINTENANCE LOG                               |
.*+----------+------------------------------------+---------+---------+
.*|  DATE    |      DESCRIPTION                   | CHANGED | REFER   |
.*+----------+------------------------------------+---------+---------+
.*| 02/19/92 | USE REGISTER R1 INSTEAD OF R13     | MJM     | #01     |
.*|          |   IN STORAGE RELEASE ADDR= OPERAND |         |         |
.*|          |   (FIX#-R0001)                     |         |         |
.*| 07/14/92 | TYPE=ESTAEX/PCRETRY= SUPPORT       | MJM     | #02     |
.*|          |   (FIX#-R0007)                     |         |         |
.*+----------+------------------------------------+---------+---------+
.*|      (C)   COPYRIGHT MD-PALADIN INC. 1992                         |
.*+-------------------------------------------------------------------+
.*                                                                    |
.**********************************************************************
.*       END MODULE WITH THIS MACRO                                   *
.**********************************************************************
         GBLA  &POOL
         GBLB  &CALSA,&WORKA,&FLOAT,&RENTA
         GBLB  &RETRYA                                              #02
         GBLB  &ESTAEA                                              #02
         GBLC  &SYSSPLV,&SYSASCE
         GBLC  &DSCTLEN,&NRENTWK,&DNAME
         GBLC  &SAVEGEN,&SAVELEN,&SAVEFLT,&SPLEVI
         GBLC  &GRSVRET                                             #02
         LCLC  &AROUND
&AROUND  SETC  'AWK'.'&SYSNDX'
*---------------------------------------------------------------------*
*        COMMON EXIT                                                  *
*---------------------------------------------------------------------*
         AIF   (&FLOAT EQ 0).NOFLT
         LD    0,&SAVEFLT               .. RESTORE
         LD    2,&SAVEFLT+8             ..FLOATING
         LD    4,&SAVEFLT+16            ..POINT
         LD    6,&SAVEFLT+24            ..REGISTERS
.NOFLT   ANOP
         AIF   (&RETRYA EQ 0).NRETY                                 #02
         LM    0,1,&GRSVRET       LOAD GENERAL REGS 0 AND 1         #02
         LAM   0,1,&GRSVRET+8     LOAD ACCESS  REGS 0 AND 1         #02
         MSTA  0                  PUT ON LINKAGE STACK              #02
.NRETY   ANOP                                                       #02
         AIF   (&WORKA EQ 0).NOWORK
         AIF   (&RENTA EQ 1).YRENT
         B     &AROUND                  BRANCH AROUND WORK AREA
         CNOP  0,8                      FORCE DOUBLEWORD ALIGNMENT
&NRENTWK DS    CL(&DSCTLEN)             WORK AREA
&AROUND  DS    0H
         AGO   .NOWORK
.YRENT   ANOP
         L     0,&SAVELEN               LENGTH OF STORAGE
         LR    1,13                     A(SAVE AREA) => R2          #01
         AIF   ('&SPLEVI' GT '2').ESA1                              #01
         AIF   (&CALSA EQ 0).NOCSA
         L     13,&SAVEGEN+4            A(CALLER'S SAVE AREA)
*                                       FREE WORK AREA STORAGE
.NOCSA   ANOP
         LR    2,15
         FREEMAIN RU,                                                  X
               LV=(0),                                                 X
               SP=&POOL,                                               X
               A=(1)
         LR    15,2
         AGO   .NOWORK
.ESA1    ANOP
         LR    2,15                     RETURN CODE => R2
*                                       FREE WORK AREA STORAGE
         STORAGE RELEASE,COND=NO,                                      X
               LENGTH=(0),                                             X
               ADDR=(1),                                            #01X
               SP=&POOL
         LR    15,2                     RETURN CODE => R15
.NOWORK  ANOP
         AIF   ('&SPLEVI' LT '3').NOESA1
         AIF   (&ESTAEA EQ 1).SESTAE                                #02
         LA    0,3                      ..RESTORE REGS 0 AND 1
         ESTA  0,0                      ..FROM STACK MODIFIABLE AREA
         AGO   .ISPR                                                #02
.SESTAE  ANOP                                                       #02
         EREG  0,1                      RESTORE REGS 0 AND 1        #02
.ISPR    ANOP                                                       #02
         PR                             RESTORE REGS AND RETURN
         AGO   .MEND
.NOESA1  ANOP
         AIF   (&CALSA EQ 0).NOCSA1
         ST    15,16(,13)
         LM    14,12,12(13)             RESTORE CALLER'S REGISTERS
.NOCSA1  ANOP
         AIF   ('&SYSSPLV' EQ '1').NOXA1
         BSM   0,14                     RESTORE AMODE AND RETURN
         AGO   .MEND
.NOXA1   ANOP
         BR    14                       RETURN TO CALLER
         AGO   .MEND
*        *---------------------------------------*
*        *     LITERALS                          *
*        *---------------------------------------*
.MEND    ANOP
         LTORG
         MEND
