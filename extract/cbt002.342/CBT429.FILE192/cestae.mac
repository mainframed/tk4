*+--------------------------------------------------------------------+
*|       (C)   COPYRIGHT MD-PALADIN INC. 1992                         |
*+--------------------------------------------------------------------+
*| FUNCTION:                                                          |
*|   ILLUSTRATE HOW TO ESTABLISH AN ESTAEX RECOVERY ROUTINE           |
*| INPUTS:                                                            |
*|   NONE                                                             |
*| OUTPUTS:                                                           |
*|   R15 - RETURN CODE                                                |
*|          0 - PROGRAM HAS COMPLETED NORMALLY                        |
*|          8 - ESTAEX ROUTINE HAS BEEN ENTERED AND RECOVERED         |
*|         12 - LOAD FAILED FOR ESTAEX ROUTINE                        |
*| PROGRAM LOGIC:                                                     |
*|   1) LOAD THE ESTAE ROUTINE.                                       |
*|   2) BUILD THE PARMLIST FOR THE ESTAEX ROUTINE.                    |
*|   3) IF THE MODULE EXECUTES IN SUPERVISOR STATE, PUT THE MODULE    |
*|      NAME AND ENTRY POINT IN THE PARMLIST.  (IN SUPERVISOR STATE,  |
*|      RTM2 DOES NOT SUPPLY THEM IN THE SDWA.                        |
*|   4) ISSUE THE ESTAEX MACRO TO ESTABLISH THE RECOVERY ROUTINE.     |
*|   5) EXECUTE REMAINING INSTRUCTIONS; AN ERROR WILL CAUSE THE       |
*|      ESTAEX ROUTINE TO RECOVER TO THE RECOVERY ADDRESS             |
*| ATTRIBUTES:                                                        |
*|   REENTRANT, AMODE 31, RMODE ANY                                   |
*+--------------------------------------------------------------------+
         MODULE CESTAE,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,           X
               TEXT=' ESTABLISH AN ESTAEX ROUTINE'
*        *---------------------------------------*
*        *     LOAD THE ESTAE ROUTINE            *
*        *---------------------------------------*
         LOAD  EP=GRECOV,ERRET=EXIT0C LOAD THE ESTAEX ROUTINE
         LR    R3,R0                 ADDRESS OF ESTAEX ROUTINE TO R3
*        *---------------------------------------*
*        *     BUILD PARMLIST FOR ESTAEX ROUTINE *
*        *---------------------------------------*
         LAE   R4,ESTPARAM           ADDRESS OF PARMS FOR ESTAE RTN
         USING ESTPARM,R4            MAP ESTAE PARMLIST
         LAE   R15,RETRY             RETRY ADDRESS
         ST    R15,ESTRETRY          SAVE IN PARMS FOR ESTAE ROUTINE
*        *---------------------------------------*
*        *     UNCOMMENT THE FOLLOWING 2 LINES   *
*        *     IF THIS MODULE EXECUTES IN        *
*        *     SUPERVISOR STATE.                 *
*        *---------------------------------------*
*        ST    R12,ESTLOAD             ENTRY POINT TO ESTAE PARMLIST
*        MVC   ESTMOD(8),=CL8'CESTAE'  MODULE NAME TO ESTAE PARMLIST
*        *---------------------------------------*
*        *     ISSUE THE ESTAEX MACRO            *
*        *---------------------------------------*
         ESTAEX (R3),PARAM=(R4),MF=(E,ESTAELST)
*        *---------------------------------------*
*        *     THE FOLLOWING 4 INSTRUCTIONS      *
*        *     REPRESENTS THE REST OF THE        *
*        *     PROCESSING IN THIS MODULE.        *
*        *     AN ERROR WILL CAUSE RTM TO INVOKE *
*        *     THE ESTAEX ROUTINE.               *
*        *---------------------------------------*
         LAM   R1,R1,=A(X'00000001')        ..LOAD ACCESS REGS TO SHOW
         LAM   R2,R2,=A(X'00000002')        ..HOW ESTAEX RTN DISPLAYS
SOC1     DC    H'0'                         PROGRAM CHECK S0C1
         B     EXIT00
*        *---------------------------------------*
*        *     THE ESTAEX ROUTINE RECOVERS HERE  *
*        *---------------------------------------*
RETRY    DS    0H
*        WTO   'AT RECOVERY POINT IN CALLER'
*======> ADD EXECUTABLE CODE HERE <====================================
         B     EXIT08
*---------------------------------------------------------------------*
*        EXIT ROUTINES                                                *
*---------------------------------------------------------------------*
EXIT00   DS    0H                      NORMAL COMPLETION
         XR    R15,R15
         B     EXIT
EXIT08   DS    0H                      ESTAE ROUTINE HAS RECOVERED
         LAE   R15,8
         B     EXIT
EXIT0C   DS    0H                      LOAD FAILED FOR ESTAEX ROUTINE
         LAE   R15,X'0C'
         B     EXIT
*---------------------------------------------------------------------*
*        COMMON EXIT                                                  *
*---------------------------------------------------------------------*
EXIT     DS    0H
         LR    R2,R15                  PRESERVE RETURN CODE
         ESTAEX 0                      CANCEL THE MOST RECENT ESTAE
         LR    R15,R2                  RESTORE  RETURN CODE
         ENDMOD                        RESTORE REGISTERS AND RETURN
*---------------------------------------------------------------------*
*        WORK AREA                                                    *
*---------------------------------------------------------------------*
WCESTAE  DSECT
ESTAELST ESTAEX ,MF=L                  ESTAEX PARAMETER LIST
ESTPARAM DS    0F,CL(LESTPARM)         PARMLIST FOR ESTAEX ROUTINE
LCESTAE  EQU   *-WCESTAE
*---------------------------------------------------------------------*
*        OTHER DSECTS                                                 *
*---------------------------------------------------------------------*
         ESTPARM                       MAP PARMLIST FOR EXTAEX ROUTINE
         END
