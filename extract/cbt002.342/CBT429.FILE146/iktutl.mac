*COPY                                                 IKTUTL
         TITLE 'CWDSET/DSPACE Routines - set/show working directory'
* Set new 'working directory', i.e., DSN prefix
* Entry: SCANPTR string has option
* Exit: R15=0 if ok, R15=1 if error or help needed. ERRNUM unchanged.
CWDSET   ENTER                                                 @SC86164
         SR    5,5                                             @SC86299
         MVI   IFILE+44,C' '                                   @SC86299
         NTOKN N=CWDLEN,H=CWDERR                               @SC86299
         LA    1,0(7,6)      End of string                     @SC86299
         BCTR  1,0                                             @SC86299
         CLC   =C'()',0(1)   Prefix is PDS name?               @SC86299
         BNE   CWDTL         No                                @SC86299
         S     7,F2          Yes, remove null member name      @SC86299
         BM    CWDERR                                          @SC86299
         MVI   IFILE+44,C'.' Indicate PDS wanted               @SC86299
CWDTL    LA    7,1(7)        Token length                      @SC86299
         CH    7,LA44+2      Suitable?                         @SC86299
         BH    CWDERR        Too long                          @SC86299
         LR    5,7                                             @SC86299
         ICM   7,8,BLANK                                       @SC86299
         LA    0,IFILE                                         @SC86299
LA44     LA    1,44          Length of DSN alone               @SC86299
         MVCL  0,6           Copy to filename buffer           @SC86299
         TR    IFILE,UPCASE  And upcase it                     @SC87034
       NXTFSET IFILE,CWD,E=CWDERR                              @SC86295
CWDLEN   MVC   DEST(45),IFILE Save new prefix                  @SC86299
         STH   5,DESTL                                         @SC86299
         B     RTRN0                                           @SC86295
CWDERR   PTEXT 'Must be valid file prefix'                     @SC86299
         B     SUBERR                                          @SC86295
*
*        DSPACE Routine - display available disk space         @SC86164
*
* Show space available in 'working directory' or other area
* Entry: SCANPTR string has option (none => working directory)
* Exit: R15=0 if ok, R15=1 if error or help needed. ERRNUM unchanged
DSPACE   ENTER ALT                                             @SC86164
* * * * * * * * * * * * * * * * * * * * * *
         PTEXT 'SPACE not implemented'                         @SC86299
         B     SUBERR                                          @SC86299
* * * * * * * * * * * * * * * * * * * * * *
         B     RTRN0                                           @SC86295
         LOCALS ,                                              @SC86295
         EXIT  ,                                               @SC86295
         TITLE 'FSPEC Routine - extract filespec from scan string'
*
* Entry: R1->name field, R0=flags selecting operation (see below)
*        For parse operations, SCANPTR defines the input string.
*        For getting foreign or display filespec, R7->output buffer
* Exit: if not FFNEW, then R15=0 if ok, 1 if ?, 2 if bad.
*        For R15=1 or 2 R3,R4 give message.  ERRNUM may be leftover.
*
*                                 Flags:                  Notes:
*   Tasks:               FFRCF FFSND FFGET FFNEW
* Parse RECV               X                     set ROVR properly
* Parse SEND 1st                 X
* Parse SEND 2nd           X     X
* Parse GET 1st            X           X
* Parse GET 2nd                        X         set ROVR properly
* Parse F-packet   (FFHDR) X     X     X
* Parse for Generic(FFUTL)       X     X         FFWLD: allow partial
* Parse TAKE
*
* Get unique name                            X     R15: 0=>ok, 1=>bad
* Interactive name check               X     X     R15: 0=>ok, 1=>bad
* Get foreign name (FFENC) X                 X     R15->end of string
* Get display form (FFDSP)       X           X     R15->end of string
*
FSPEC    ENTER                                                 @SC86295
         STC   0,FSPFLG                                        @SC86295
         LR    0,1           Copy ptr to filespec              @SC86295
         TM    FSPFLG,FFNEW                                    @SC86295
         BO    FSPWRN                                          @SC86295
         LR    8,1           Save ptr to DSN field             @SC86299
         XC    0(52,8),0(8)  Clear DSN field                   @SC86299
         PTEXT 'Invalid DSN'                                   @SC86299
         MVI   ERRNUM,ERRFNE Assume bad file name              @SC86158
         TM    FSPFLG,FFHDR                                    @SC86295
         BO    FSPHD                                           @SC86295
         TM    FSPFLG,FFUTL                                    @SC86295
         BNO   FSPTR                                           @SC86295
         TM    FSPFLG,FFWLD  Utility: default to all files?    @SC86295
         BZ    FSPASC        No                                @SC86295
         MVI   0(1),C' '     Yes, allow missing DSN            @SC86299
         MVC   1(51,1),0(1)                                    @SC86299
FSPASC   TM    FL2,SRV       Server mode?                      @SC86295
         BZ    FSPCPY        No, don't need to convert         @SC86295
         ICM   15,15,LEN     Get length                        @SC86295
         BZ    FSPCPY                                          @SC86295
         BCTR  15,0          Correct for EX                    @SC86158
         L     1,ADR         Get string ptr                    @SC86295
         EX    15,TRATOE     Change to EBCDIC                  @SC86158
         EX    15,TRUPCAS    Upcase                            @SC86299
         B     FSPCPY                                          @SC86295
FSPTR    TM    FSPFLG,FFRCF                                    @SC86295
         BZ    FSPTS                                           @SC86295
         TM    FSPFLG,FFSND+FFGET                              @SC86295
         BNZ   FSPSN2        Foreign filespec for SEND or GET  @SC86295
FSPRC    NI    FL1,255-ROVR  Setup for RECEIVE                 @SC86295
         NI    FL4,255-NMOK  Collision not checked yet         @SC87012
         MVI   0(1),C'$'     Allow missing DSN                 @SC86299
         B     FSPCPY                                          @SC86295
FSPHD    MVI   0(1),1        Use default if missing DSN        @SC86299
         B     FSPCPY                                          @SC86299
FSPTS    TM    FSPFLG,FFSND                                    @SC86295
         BO    FSPASC                                          @SC86299
         TM    FSPFLG,FFGET                                    @SC86299
         BO    FSPRC                                           @SC86299
         B     FSPCPY                                          @SC86299
FSPSN2   NTOKN H=FSP2H,N=RTRN0                                 @SC86299
         LA    1,L'JFNAM                                       @SC86295
         CLM   7,3,*-2       Does it fit?                      @SC86224
         BNH   *+6           Yes                               @SC86224
         LR    7,1           Use what we can                   @SC86224
         LR    3,0                                             @SC86295
         STC   7,0(3)        Save length                       @SC86224
         LA    0,1(3)                                          @SC86295
         MVCL  0,6           Get fn, at least                  @SC86224
         B     RTRN0                                           @SC86295
*
FSPCPY   NTOKN H=FSPH,N=FSPZ                                   @SC86299
FSPCP2   MVI   TRTBL+C'.',1  Set to intercept these            @SC86299
         MVI   TRTBL+C'(',2                                    @SC86299
         LA    15,44         Length of DSN proper              @SC86299
         AR    7,6           Last char of string               @SC86299
         CLI   0(6),C''''    Full name?                        @SC86299
         BNE   FSPPRE        No, add prefix                    @SC86299
         LA    6,1(6)        Yes, skip over quote              @SC86299
         CLI   0(7),C''''    Must have close quote as well     @SC86299
         BNE   *+6                                             @SC86299
         BCTR  7,0           Back up over it                   @SC86299
         BE    *+8                                             @SC86299
         BAL   9,FSPTU       Missing: quit if user typed this  @SC86299
         B     FSPPREZ                                         @SC86299
FSPPRE   CLI   0(7),C''''    Better not be trailing quote      @SC86299
         BNE   *+10          Ok                                @SC86299
          BAL  9,FSPTU       Error                             @SC86299
          BCTR 7,0           Didn't quit, so patch it up       @SC86299
         LH    1,DESTL       Length of prefix                  @SC86299
         LTR   1,1           Any?                              @SC86299
         BZ    FSPPREZ       No                                @SC86299
         LA    14,DEST       Ptr to prefix string              @SC86299
         MVCL  0,14          Copy prefix to name field         @SC86299
         CLI   DESTP,C'.'    PDS?                              @SC86299
         BE    FSPCPF        Yes, prefix is entire DSN         @SC86299
FSPDOT   LA    14,LOCASE+C'.'                                  @SC86299
         LA    1,1                                             @SC86299
         MVCL  0,14          Append a dot                      @SC86299
FSPPREZ  BAL   2,FSPANAT     Add '#' if numeric char next      @SC86299
FSPCPA   LA    1,1(7)        End of string                     @SC86299
         LA    2,2           In case no breaks                 @SC86299
         SR    7,6                                             @SC86299
         EX    7,FSPTRT      Find break                        @SC86299
         AR    7,6           Restore ptr to last char          @SC86299
         SR    1,6           Length of token                   @SC86299
         BP    *+8                                             @SC86299
          BAL  9,FSPTU       Null token                        @SC86299
         LR    14,6          Save start of token               @SC86299
         AR    6,1           Ptr to break                      @SC86299
         CR    1,5           Max allowed for this token        @SC86299
         BNH   *+10          Ok                                @SC86299
          BAL  9,FSPTU       Too long                          @SC86299
          LR   1,5           Use max                           @SC86299
         CR    1,15          Room left in name field?          @SC86299
         BNH   FSPCPC        Ok                                @SC86299
         BAL   9,FSPTU       Overfilled                        @SC86299
         MVI   TRTBL+C'.',0  Keep going, but ignore further tok@SC86299
         LR    1,15                                            @SC86299
FSPCPC   MVCL  0,14          Copy token                        @SC86299
         BCT   2,FSPCPF      Go if reached end of name         @SC86299
         LA    6,1(6)        Skip over dot                     @SC86299
         CR    6,7           Was dot the last char?            @SC86299
         BH    FSPCPE        Yes, oops                         @SC86299
         C     15,F1         Room for another token?           @SC86299
         BH    FSPDOT        Ok, keep going                    @SC86299
         SR    5,5           No, suppress further tokens       @SC86299
         BAL   9,FSPTU       Quit if user typed it             @SC86299
         B     FSPCPA        Keep going                        @SC86299
FSPTRT   TRT   0(,6),TRTBL   Find end of token                 @SC86299
FSPCPE   BAL   9,FSPTU       Quit if user type it              @SC86299
FSPCPF   LR    14,0          Output ptr                        @SC86299
         SR    1,1                                             @SC86299
         ICM   1,8,BLANK                                       @SC86299
         MVCL  14,0          Pad with blanks                   @SC86299
         LA    15,8          Length of member name, if any     @SC86299
         CLI   DESTP,C'.'    PDS?                              @SC86299
         BE    FSPCPP        Yes, member without parentheses   @SC86299
         BCTR  6,0           Back up to last char of DSN       @SC86299
         CR    6,7                                             @SC86299
         BE    FSPCPG        No member name                    @SC86299
         LA    6,2(6)        Ptr to member name                @SC86299
         CLI   0(7),C')'     Must be matching paren            @SC86299
         BE    FSPCPG        Ok                                @SC86299
         BAL   9,FSPTU       Oops                              @SC86299
FSPCPP   LA    7,1(7)        Pretend it's there                @SC86299
FSPCPG   SR    7,6           Length of member name             @SC86299
         BZ    FSPCPM        None, forget it                   @SC86299
         LR    0,14          Copy output ptr                   @SC86299
         BAL   2,FSPANAT     '#' if numeric char next          @SC86299
         LR    14,0                                            @SC86299
FSPCPM   ICM   7,8,BLANK                                       @SC86299
         MVCL  14,6          Copy member name                  @SC86299
         CLM   7,7,F0        Did it fit?                       @SC86299
         BE    *+8                                             @SC86299
          BAL  9,FSPTU       Oops                              @SC86299
         MVC   FSPDSN,0(8)   Save raw name                     @SC86299
         TR    FSPDSN,UPCASE Upcase it                         @SC87034
         TR    0(52,8),FSPTAB Convert to valid chars, if nec.  @SC86299
         TR    44(8,8),FSPMTAB Stricter limits on member name  @SC86299
         CLC   FSPDSN,0(8)   Any conversions?                  @SC86299
         BE    *+8           No, ok                            @SC86299
          BAL  9,FSPTU       Yes, quit if user typed it        @SC86299
         OI    FL1,ROVR      Found a name                      @SC86299
         MVI   TRTBL+C'.',0  Restore table                     @SC86299
         MVI   TRTBL+C'(',0                                    @SC86299
         B     RTRN0                                           @SC87034
*
FSPZ     LA    6,=C'$.$'     In case we must use default       @SC87338
         LA    7,3-1                                           @SC87338
         CLI   0(8),1                                          @SC86299
         BE    FSPCP2        Get default DSN 'prefix.$.$'      @SC87338
         BH    RTRN0         Don't insist                      @SC86299
         PTEXT 'Missing DSN'                                   @SC86299
         B     FSPINV                                          @SC86299
FSPTU    TM    FSPFLG,FFHDR                                    @SC86299
         BOR   9             From other Kermit, accept it      @SC86299
FSPINV   MVI   TRTBL+C'.',0  Restore table                     @SC86299
         MVI   TRTBL+C'(',0                                    @SC86299
         LA    15,2                                            @SC86299
         B     FSPPTRS                                         @SC86295
*
FSPH     PTEXT 'Enter DSN'                                     @SC86299
         B     FSP0H                                           @SC86295
FSP2H    PTEXT 'Enter foreign filespec'                        @SC86295
FSP0H    LA    15,1                                            @SC86295
FSPPTRS  L     14,4(13)                                        @SC86295
         STM   3,4,32(14)    Return msg ptrs                   @SC86295
FSPRET   RET   ,                                               @SC86295
*
* Non-parsing functions . . .
*
* Get unique filespec
FSPWRN   LR    4,1           Save name ptr                     @SC86295
         TM    FSPFLG,FFENC                                    @SC86295
         BO    FSPENC        Encode name into buffer           @SC86295
         TM    FSPFLG,FFDSP                                    @SC86295
         BO    FSPDSP        Copy name into buffer for display @SC86295
         TM    FL4,NMOK      Already checked?                  @SC87012
         BO    RTRN0         Yes, ok                           @SC87012
* This routine checks to see if the old data set is a PDS.     @TS86001
* If so, it then allocates and opens the data set and does a   @TS86001
* FIND to determine if the member is present.                  @TS86001
         LA    5,10+1        Allowed retries                   @BS86001
         LA    7,C'0'        Extra character                   @BS86001
         MVC   FSPDSN,0(4)                                     @SC87015
         BAL   9,FSPTOPN                                       @SC87015
         USING FDBD,1                                          @SC87015
         CLI   FSPDSMB,C' '  Member specified?                 @SC87015
         BE    FSPNOPDS      No, be sure it isn't a PDS        @SC87015
         TM    FDBFLGS,PDSF  Yes, be sure it is                @SC87015
         BZ    RTRN1         Too bad                           @SC87015
         MVI   FSPDSMB,0     Signal DSORG=PO for allocation    @SC87015
         OPENF I,FSPDSN,FILFDB,PDSPTR,E=RTRN1                  @SC87015
         MVC   FSPDSMB,44(4) Copy requested member name        @SC87015
         LA    1,FSPDSMB+7   Last char of member               @SC87015
         TRT   FSPDSMB,TRTBL Find blank                        @SC87015
         LR    6,1           Tentative byte to modify          @SC86299
         LA    3,FSPTFND                                       @SC87015
         BALR  9,3           See if member exists              @SC87015
         B     FSPRMPT       Yes, resolve                      @SC87015
FSPTFND  L     1,PDSPTR                                        @SC87015
         FIND  (1),FSPDSMB,D Search for member name            @SC87015
         B     *+4(15)       Branch on return code             @TS86001
         B     0(9)          0  - member was found             @TS86001
         B     FSPNOKM       4  - member not found             @TS86001
         B     FSPDERR       8  - I/O error or lack of memory  @TS86001
FSPTOPN  OPENF T,FSPDSN,E=FSPNOKD No collision                 @SC87015
         BR    9                                               @SC87015
FSPNOPDS TM    FDBFLGS,PDSF  Be sure it isn't a PDS            @SC87015
         BO    RTRN1         Too bad                           @SC87015
         LA    3,FSPTOPN     Just test DSN for existence       @SC87015
         MVI   TRTBL+C'.',1                                    @SC87015
         TRT   FSPDSN(9),TRTBL Find end of 1st index           @SC87015
         LR    6,1                                             @SC87015
         LA    1,8(6)        Last possible end of 2nd          @SC87015
         TRT   2(7,6),TRTBL                                    @SC87015
         MVI   TRTBL+C'.',0  Restore TRT                       @SC87015
         LR    6,1           Byte to modify                    @SC87015
         BZ    FSPRMPT       Index level was 8 bytes           @SC87015
         CLI   FSPDSN+42,C'.' Is last level a 1-byte qualifier?@SC88020
         BNE   *+10          No, ok                            @SC88020
          BCTR 6,0           Yes, can't shift name over        @SC88020
          B    FSPRMPT                                         @SC88020
         LA    1,FSPDSN                                        @SC87015
         MVC   1(43,1),0(4)  Shift name over one               @SC87015
         SR    6,1                                             @SC87015
         EX    6,FSPMVDS     And copy beginning back           @SC87015
         AR    6,1                                             @SC87015
FSPRMPT  TM    FL1,REN       Warning mode on?                  @SC87015
         BZ    FSPNOK        No, just overwrite                @SC87015
         TM    FL2,PROTO     User typed it?                    @SC88026
         BZ    FSPRMP2       Yes                               @SC88026
FSPSTA   BALR  9,3           See if still a conflict           @SC87015
         STC   7,0(6)        Yes, modify DSN                   @SC86299
         LA    7,1(7)        Bump counter                      @BS86001
         BCT   5,FSPSTA                                        @BS86001
FSPDERR  CLOSF PDSPTR        Close the data set                @SC87015
         B     RTRN1         Failed                            @SC87015
FSPMVDS  MVC   0(,1),0(4)                                      @SC88020
FSPNOKM  MVC   44(8,4),FSPDSMB                                 @SC87015
FSPNOKD  MVC   0(44,4),FSPDSN Copy name back                   @SC87015
FSPNOK   OI    FL4,NMOK                                        @SC87015
         CLOSF PDSPTR                                          @SC87015
         B     RTRN0                                           @SC87015
FSPRMP2  LA    7,CMD                                           @SC87015
         LA    0,FFDSP                                         @SC87015
         KCALL FSPEC,(4)     Format DSN for message            @SC87015
         MVC   0(34,15),=C' exists.  Reply "OK" to overwrite:' @SC87015
         LA    3,34(15)                                        @SC87015
         SR    3,7                                             @SC87015
         RTEXT (7),PROMPT=((7),(3))                            @SC87268
         LTR   0,0           Length of reply                   @SC87015
         BNP   RTRN1         If zero give up                   @TS86001
         TR    0(2,7),UPCASE Upcase 1st 2 chars of reply       @SC87015
         CLC   =C'OK',0(2)   Was reply "ok"?                   @TS86001
         BNE   RTRN1         No, abort operation               @TS86001
         B     FSPNOK                                          @SC87015
*
* Encode name at (R1) into (R7) buffer (in ASCII), possibly with
*  substitution from JFSPEC, but disable subsequent subst.
*  Return updated ptr in R15
FSPENC   LA    1,JFSPEC      Complex string?                   @SC86224
         BAL   14,PAKFOR                                       @SC86224
         BNZ   FSPECPZ       Yes, name overridden              @SC86299
         CLI   44(4),C' '    Member?                           @SC86299
         BE    FSPENT        No, get name and type from DSN    @SC86299
         LA    3,43(4)       Yes, ptr one before it            @SC86299
         LA    1,9(3)        Utmost end                        @SC86299
         TRT   1(8,3),TRTBL  Find end of name                  @SC86299
         B     FSPECP                                          @SC86299
FSPENT   LA    1,44(4)                                         @SC86299
         TRT   0(44,4),TRTBL Find end of DSN                   @SC86299
         LA    0,2                                             @SC86299
         LR    3,1                                             @SC86299
FSPESCN  BCTR  3,0           Scan back for dots                @SC86299
         CR    3,4           Past beginning of DSN?            @SC86299
         BL    FSPECP        Yes, use all                      @SC86299
         CLI   0(3),C'.'     No, found dot?                    @SC86299
         BNE   FSPESCN       No, keep looking                  @SC86299
         BCT   0,FSPESCN     Also get 2nd dot                  @SC86299
FSPECP   LA    3,1(3)        Stuff to copy                     @SC86299
         MVC   0(17,7),0(3)  At most 2 tokens + dot            @SC86299
         TR    0(17,7),ETOA                                    @SC86299
         SR    1,3           Length of stuff copied            @SC86299
         AR    7,1           Advance ptr                       @SC86299
FSPECPZ  MVI   JFSPEC,0      Turn off string                   @SC86299
FSPENR   LR    15,7          Save ptr                          @SC86295
         B     FSPRET                                          @SC86295
*
* Copy name at (R1) into (R7) buffer in display form
*  Return updated ptr in R15
FSPDSP   LR    14,7          Copy output ptr                   @SC86299
         LA    2,DEST        Check if prefix exists            @SC86299
         LH    3,DESTL                                         @SC86299
         LTR   3,3                                             @SC86299
         BZ    FSPDCP        No prefix, skip quotes            @SC86299
         LA    1,1(3)        One extra for dot                 @SC86299
         ICM   3,8,LOCASE+C'.'                                 @SC86299
         CLCL  0,2           Does it match prefix?             @SC86299
         BE    FSPDCP        Yes, chop it off                  @SC86299
         LR    0,4           No, use quotes for whole name     @SC86299
         MVI   0(14),C''''                                     @SC86299
         LA    14,1(14)                                        @SC86299
FSPDCP   LA    1,44(4)                                         @SC86299
         TRT   0(44,4),TRTBL Find end of name                  @SC86299
         SR    1,0           Length                            @SC86299
         LR    15,1                                            @SC86299
         MVCL  14,0          Copy name to buffer               @SC86299
         CLI   44(4),C' '    Member name, too?                 @SC86299
         BE    FSPDCY        No, done                          @SC86299
         MVI   0(14),C'('    Yes, insert in parens             @SC86299
         MVC   1(8,14),44(4) Copy name to buffer               @SC86299
         LA    1,9(14)                                         @SC86299
         TRT   1(8,14),TRTBL Find end of member name           @SC86299
         MVI   0(1),C')'     Close member name                 @SC86299
         LA    14,1(1)                                         @SC86299
FSPDCY   LR    15,14         Return output ptr                 @SC86299
         CLI   0(7),C''''    Need close quote?                 @SC86299
         BNE   *+12          No, that's all                    @SC86299
         MVI   0(15),C''''   Yes, do it                        @SC86299
         LA    15,1(15)                                        @SC86299
         B     FSPRET                                          @SC86299
*
* Insert '#' if token would otherwise begin with a digit       @SC86299
FSPANAT  LA    5,8           Tentative token length            @SC86299
         CLI   0(6),C'0'     Digit?                            @SC86299
         BLR   2             No, ok                            @SC86299
         CLI   0(6),C'9'     Really?                           @SC86299
         BHR   2             No, but illegal anyway            @SC86299
         BAL   9,FSPTU       Bad form                          @SC86299
         LA    14,LOCASE+C'#'                                  @SC86299
         LA    1,1                                             @SC86299
         MVCL  0,14          Copy '#'                          @SC86299
         BCTR  5,0           Now allow only 7                  @SC86299
         BR    2                                               @SC86299
*
* Valid DSN characters                                         @SC86299
FSPTAB   DC    64C'#',C' '           space                     @SC86299
         DC    10C'#',C'.'           dot                       @SC86299
         DC    15C'#',C'$*'          dollar sign, asterisk     @SC86299
         DC    03C'#',C'-'           hyphen                    @SC86299
         DC    26C'#',C'#@'          pound sign, at sign       @SC86299
         DC    04C'#',C'ABCDEFGHI'   a-i                       @SC86299
         DC    07C'#',C'JKLMNOPQR'   j-r                       @SC86299
         DC    08C'#',C'STUVWXYZ'    s-z                       @SC86299
         DC    22C'#',C'{ABCDEFGHI'  {,A-I                     @SC86299
         DC    07C'#',C'JKLMNOPQR'   J-R                       @SC86299
         DC    08C'#',C'STUVWXYZ'    S-Z                       @SC86299
         DC    06C'#',C'0123456789'  0-9                       @SC86299
         DC    06C'#'                                          @SC86299
* Valid member name characters                                 @SC86299
FSPMTAB  DC    75AL1(*-FSPMTAB),C'#' dot                       @SC86299
         DC    16AL1(*-FSPMTAB),C'#' asterisk                  @SC86299
         DC    03AL1(*-FSPMTAB),C'#' hyphen                    @SC86299
         DC    95AL1(*-FSPMTAB),C'#' {                         @SC86299
         DC    63AL1(*-FSPMTAB)                                @SC86299
         LOCALS ,                                              @SC86295
PDSPTR   DS    A             Ticket for PDS testing            @SC87015
FSPDSN   DS    0CL52         Temp for name field               @SC86299
PDSNM    DS    CL44          Test DSN                          @SC87015
FSPDSMB  DS    CL8           Test member                       @SC87015
FSPFLG   DS    X             Filespec flags                    @SC86295
FSPEC    EXIT                                                  @SC86295
         TITLE 'KHELP routine - perform HELP command'
* Handle HELP command, rest of string given by SCANPTR.
KHELP    ENTER ,                                               @SC86355
         LA    0,4           Length if no operands             @SC86355
         NTOKN N=KHLI        See if subcommand given           @SC86355
         L     1,=A(USNCMD)  Command table                     @SC87117
         SCAN  (1),KHLF,NODISP                                 @SC86355
         WTEXT 'Not a valid subcommand'   Not found            @SC86355
         RET   ,                                               @SC86355
KHLF     CLM   7,8,F0        Just '?'                          @SC86355
         BE    RTRN          Yes, done                         @SC86355
         MVC   CMD+5(30),5(1) Copy operand                     @SC86355
         IC    7,4(1)        Get length from table             @SC86355
         LA    0,5+1(7)      Total for SCAN                    @SC86355
KHLI     LA    1,CMD         Command buffer                    @SC87007
         MVC   0(5,1),=CL5'HELP'                               @SC86355
         STM   0,1,SCANPTR   Set up for system                 @SC86355
         OI    FL4,UCMD                                        @SC86355
         KCALL SUPFNC,3      Do it                             @SC86355
         RET   ,                                               @SC86355
         LOCALS ,
KHELP    EXIT  ,                                               @SC87007
         TITLE 'SUPFNC Routine - various supervisor functions' @SC86158
SUPFNC   ENTER                                                 @SC86295
*  On entry, R1 = operation code, R0 = possible ptr            @SC86158
* Exit: R15 set (0 => ok, <0 => illegal cmd, >0 => depends)
*       ERRNUM set appropriately (R1=1,3,4) or unchanged (2,5-9)
* 1 -> Start typeout interception.  N.B.  &MAXLR >> 2048 for this
* 2 -> Clean up afterwards and stop interception
* 3 -> Execute host command with or without interception
*      If UCMD set, SCANPTR gives text, else R0->text,R6=len
* 4 -> (not used)
* 5 -> Stop interception if going
* 6 -> Retrieve original cmd parm string into CBUF (R15=1 if null)
* 7 -> Test for stacked lines, return number in R15
* 8 -> Log off (must return to TMP)
* 9 -> Wait specified time
* 10-> Return clock time in R15 (centisec)
* 11-> Setup up new prompt string at (R0)
         BCT   1,ICPFIN                                        @SC86158
* Start interception, initialize ptrs                          @SC86158
         MVI   ERRNUM,ERRNOE OK                                @SC86158
         LA    0,2048        Suitable offset                   @SC86158
         A     0,WBUF        Output buffer                     @SC86158
         L     1,TSENT       Limit                             @SC86158
         LR    15,0                                            @SC86158
         STM   15,0,TXTPTR   Save                              @SC86158
         STM   0,1,SVCOPTR                                     @SC86158
         SR    1,0           Get length                        @SC86158
         L     15,=X'15000000'                                 @SC86158
         MVCL  0,14          Fill with NL (X'15')              @SC86158
* ------------ determine if SVC screen is possible             @SC88026
* -            if so, then do it                               @SC88026
         B     ICPSTK                                          @SC88026
         MVI   ICPFL,2       Now intercepting subtask SVC's    @SC88026
         B     RTRN0                                           @SC88026
*          Can't screen SVC's, create a STACK element          @SC88026
ICPSTK   OPENF T,STKDSN,E=ICPST2 See if any previous output    @SC88026
         ERASF STKDSN        Yes, clear it                     @SC88026
ICPST2   LA    1,STKDSN      Get ptrs to DYNALC arguments      @SC88026
         LA    2,STKDD                                         @SC88026
         LA    3,FILUNT                                        @SC88026
         LA    4,FILVOL                                        @SC88026
         LA    5,=X'42'                                        @SC88026
         LA    6,FILTRKAL                                      @SC88026
         LA    7,STKDRC                                        @SC88026
         STM   1,7,STKDYN    Set up calling sequence           @SC88026
         KCALL DYNALC,STKDYN,EXT Allocate output file          @SC88026
         STACK MF=(E,IOPLAREA),PARM=STKA  Create STACK elt.    @SC88026
         MVI   ICPFL,1       Now intercepting                  @SC87020
         B     RTRN0                                           @SC86295
* Clean up after interception                                  @SC86295
ICPFIN   BCT   1,ICPHST                                        @SC86158
         L     5,SVCOPTR     End of text                       @SC86158
         ST    5,TXTPTR+4    Save                              @SC86158
         CLI   ICPFL,2       Were we intercepting SVC's?       @SC88026
         BNE   ICPFINST      No, see if STACK                  @SC88026
*---------- stop snagging SVC's                                @SC88026
         B     ICPRST1       Ok                                @SC88026
ICPFINST CLI   ICPFL,1       Were we intercepting via STACK?   @SC88026
         BNE   ICPRST1       No, fine                          @SC88026
         STACK MF=(E,IOPLAREA),PARM=STKZ Yes, remove STACK elt.@SC88026
*          Copy output to buffer                               @SC88026
         OPENF I,STKDSN,FILFDB,STKTKT,E=ICPRST1                @SC88026
         L     5,TXTPTR+4    Buffer ptr                        @SC88026
ICPSTLP  READF STKTKT,BUFFER=(5),BSIZE=255,E=ICPSTZ            @SC88026
         AR    5,0           Space over data                   @SC88026
         LA    5,1(5)        Leave one X'15'                   @SC88026
         B     ICPSTLP       And read more                     @SC88026
ICPSTZ   CLOSF STKTKT        Done                              @SC88026
         ST    5,TXTPTR+4    New end of output                 @SC88026
         B     ICPRST1       Now restore interrupts            @SC86295
* Restore SVC interrupt vector                                 @SC86158
ICPRST   BCT   1,SFCLIN                                        @SC86295
ICPRST1  MVI   ICPFL,0                                         @SC87020
         B     RTRN0
* Execute TSO command at (R0) with length (R6), unless UCMD set,
*  in which case string given by SCANPTR
ICPHST   BCT   1,ICPCP                                         @SC86158
         TM    FL4,UCMD      User command?                     @SC86295
         BO    ICPCM0        Yes, scan already set up          @SC86355
ICPCMI   ST    0,ADR         Set scan string ptrs              @SC86355
         ST    6,LEN                                           @SC86355
ICPCM0   LM    0,1,SCANPTR   Get length and adr                @SC87034
         LTR   6,0           Copy length                       @SC87034
         BNP   ICPCMIL       No good                           @SC87034
         BCTR  6,0                                             @SC87034
         EX    6,TRUPCAS                                       @SC87034
         NTOKN N=ICPCMIL     No good                           @SC86355
         MVC   SFCBUF+4(130),0(6)  Copy text to command buffer @SC86355
         LR    5,6           Start of command name             @SC86355
         LA    7,1(7)        Length of name                    @SC86355
         ICM   7,8,BLANK     Set up for padding                @SC86355
         L     2,ORGR1       Get address of kermit CPPL        @TS86001
         MVC   ATCHCPPL(16),0(2)  initialize attach CPPL       @TS86001
         LA    2,ATCHCPPL    Get address of attach CPPL        @TS86001
         USING CPPL,2        Make attach CPPL addressable      @TS86001
         LA    1,SFCBUF                                        @SC86355
         ST    1,CPPLCBUF    Put the command buffer into CPPL  @TS86001
         L     3,CPPLECT     Get the ECT address               @TS86001
         USING ECT,3         Make it addressable               @TS86001
         MVC   SAVPCMD,ECTPCMD Save it for restoring           @SC88026
         LA    14,ECTSCMD                                      @SC86355
         LA    15,L'ECTSCMD                                    @SC86355
         MVCL  14,6          Copy to subcommand field          @SC86355
         CLC   =C'HELP ',ECTSCMD                               @SC88026
         BE    *+10                                            @SC88026
          MVC  ECTPCMD,ECTSCMD This is really a command        @SC88026
         LR    4,6           Default parameter ptr             @SC86355
         LR    8,6           Default end of string             @SC86355
         NTOKN N=SFCNPRM     Find parameters, if any           @SC86355
         L     8,ADR                                           @SC86355
         A     8,LEN         True end of string                @SC86355
         LR    4,6           Start of parameters               @SC86355
SFCNPRM  SR    4,5           Get offset to parameters          @SC86355
         STH   4,SFCBUF+2    Save in command buffer            @SC86355
         SR    8,5           Get total length                  @SC86355
         LA    9,SFCBUF+4(8) Point past the buffer             @SC88022
         MVI   0(9),C' '     Tack on a blank                   @SC88022
         LA    8,4(8)        Plus prefix info                  @SC88022
         STH   8,SFCBUF      Save in command buffer            @SC86355
         ATTACH ECB=ATCHECB,EPLOC=ECTSCMD,SHSPV=78,SZERO=NO,           +
               MF=(E,(2)),SF=(E,ATCBLK)                        @SC86355
         LTR   15,15         Was attach successful?            @TS86001
         BZ    *+14          Ok                                @SC88026
          MVC  ECTPCMD,SAVPCMD Restore ECT                     @SC88026
          B    ICPCMIL       No, must not exist                @SC88026
         ST    1,ATCHTCB     Save TCB address                  @TS86001
         WAIT  ECB=ATCHECB   Wait for subtask to finish        @TS86001
         DETACH ATCHTCB      Detach the subtask                @TS86001
         MVC   ECTPCMD,SAVPCMD Restore ECT                     @SC88026
         SR    4,4                                             @SC86355
         ICM   4,7,ATCHECB+1 Get return code                   @SC86355
         DROP  3                                               @SC86355
* Issue return code msg if needed                              @SC86295
         BZ    SFCZRC        RC=0                              @SC86158
         TM    FL4,UCMD      User cmd?                         @SC86316
         BZ    SFCZRC        No, don't issue message           @SC86316
         MVC   CMD(2),=C'R(' Set up message                    @SC86209
         LA    15,CMD+2                                        @SC86209
         BAL   2,EDDEC       Edit RC into msg                  @SC86295
         MVI   0(15),C')'    Format is R(rc)                   @SC86209
         LA    0,1(15)                                         @SC86268
         LA    1,CMD         Start of edited string            @SC86209
         SR    0,1           Length                            @SC86268
         WTEXT (1),(0)                                         @SC86268
SFCZRC   LR    15,4                                            @SC86295
         MVI   ERRNUM,ERRNOE No errors                         @SC86295
         B     RTRN                                            @SC86295
* Unused, system-specific command type
ICPCP    BCT   1,ICPRST                                        @SC86158
ICPCMIL  MVI   ERRNUM,ERRSYS Illegal system command            @SC86295
         B     RTRNM1                                          @SC86295
*
SFCLIN   BCT   1,SFCSTK                                        @SC86295
* Retrieve original command line arguments, if any             @SC86295
*   Return code =0 if yes, =1 if no                            @SC86295
*   Leave string in CBUF buffer (up to 256), length in CLEN    @SC86295
         L     2,ORGR1       Original R1                       @SC86355
         L     1,CPPLCBUF    CBUF ptr                          @SC86355
         LH    15,0(1)       PARM length                       @SC86299
         S     15,F4                                           @SC86299
         LH    14,2(1)       Parm offset                       @SC86355
         SR    15,14                                           @SC86355
         BNP   RTRN1         Nothing there                     @SC86299
         LA    14,4(14,1)    Start of string                   @SC86299
         L     0,CBUF                                          @SC86299
         LA    1,256         Max length allowed                @SC86299
         CR    1,15                                            @SC86299
         BL    *+6                                             @SC86299
         LR    1,15          Shorter than max                  @SC86299
         ST    1,CLEN                                          @SC86299
         MVCL  0,14                                            @SC86299
         B     RTRN0                                           @SC86295
*
* Test for stacked commands                                    @SC86295
*   return code = number of stacked lines                      @SC86295
SFCSTK   BCT   1,SFCKIL                                        @SC86295
         LA    2,APGPB                                         @NW86330
         USING GTPB,2                                          @NW86330
         ICM   1,15,GTPBIBUF Ptr to input buffer, if any       @SC87015
         BNZ   RTRN1         Yes, line is stacked              @SC87015
         L     15,GETLINAD   Entry point for GETLINE routine   @NW86330
         GETLINE PARM=(2),TERMGET=(EDIT,NOWAIT),ENTRY=(15),            +
               MF=(E,IOPLAREA)                                 @SC87015
         C     15,F4         Check return code                 @SC87015
         BH    RTRN0         Nothing stacked                   @SC87015
         B     RTRN1         Got one now                       @SC87015
*
* Log out                                                      @SC86295
SFCKIL   BCT   1,SFCWT                                         @SC86295
         LR    3,13                                            @SC88026
         L     3,4(3)        Look back through save areas      @SC88026
         CLC   AUSNTRF,16(3) Find main loop                    @SC88026
         BNE   *-10                                            @SC88026
         L     3,8(3)        Ptr to main save area             @SC88026
         OI    KFLG-USNTRFSV(3),CMDC Set flag to quit          @SC88026
         PTEXT 'LOGOFF',AREG=0,LREG=6                          @SC88026
         NI    FL4,255-UCMD  Internal                          @SC86355
         B     ICPCMI        Do it                             @SC86355
*
* Wait specified time in R0 (sec)
SFCWT    BCT   1,SFCCLK                                        @SC86295
         MH    0,=H'100'     Convert to centisec               @SC86299
         ST    0,TMPDW                                         @SC86299
        STIMER WAIT,BINTVL=TMPDW                               @SC86299
         B     RTRN0                                           @SC86295
*
* Return time in centisec in R15
SFCCLK   BCT   1,SFCPRP                                        @SC87351
         STCK  TMPDW         Store TOD clock                   @SC86295
         LM    14,15,TMPDW                                     @SC86295
         SLDL  14,8          Take mod 204 days                 @SC86295
         SRDL  14,20         Get in microsec                   @SC86295
         D     14,=F'10000'  Get in centisec                   @SC86295
         B     RTRN                                            @SC86295
*
SFCPRP   B     RTRN0         No action for prompting           @SC87351
         TITLE 'SVC interceptor,  executed in system protect key'
         USING ICPTYP,15                                       @SC86283
ICPTYP   STM   12,14,SVCSV1  Save regs                         @SC86283
         LR    13,15         Addressability                    @SC87020
         DROP  15
         USING ICPTYP,13                                       @SC87020
ICPTGO   LM    14,15,SVCOPTR Output ptrs                       @SC86158
         SR    15,14         Length left                       @SC86158
         LA    12,255        Limit                             @SC86158
         CLR   12,0          Buffer length                     @SC87020
         BNH   *+8           Too big                           @SC86158
         LR    12,0          Ok, use it                        @SC87020
         LTR   12,12                                           @SC86158
         BNP   ICPTRET                                         @SC86283
         CR    12,15         Enough room?                      @SC86283
         BH    ICPTRET       No                                @SC86283
         BCTR  12,0          Set up for mvc                    @SC86158
         EX    12,SVCCOPY    Move to WBUF                      @SC86158
         LA    14,2(12,14)   New end                           @SC86158
         ST    14,SVCOPTR                                      @SC86158
ICPTRET  SR    15,15         Success                           @SC86283
         LM    12,14,SVCSV1  Restore regs                      @SC86283
         BR    14            Return                            @SC86283
SVCCOPY  MVC   0(,14),0(1)                                     @SC87020
*
* Storage for SVC interception                                 @SC86158
SVCSV1   DS    2F            Saved 12,13                       @SC86158
SVCSV2   DS    2F            Saved 14,15                       @SC86158
SVCOPTR  DS    2F            Buffer output and end ptrs        @SC86158
STKA     STACK MF=L,DATASET=(*,OUTDD=STKDD)                    @SC88026
STKZ     STACK MF=L,DELETE=TOP                                 @SC88026
STKDD    DC    CL8'K999999Y' DD name for STACK interception    @SC88026
         LOCALS ,                                              @SC86295
SAVPCMD  DS    CL8           Saved command name                @SC88026
ATCHCPPL DS    4F            Subtask CPPL area                 @TS86001
ATCBLK   ATTACH SF=L         ATTACH parameter list             @SC88022
ATCHECB  DS    F             Subtask ECB                       @TS86001
ATCHTCB  DS    F             Subtask TCB ptr                   @TS86001
SFCBUF   DS    F,CL130       Command buffer                    @SC86355
STKDYN   DS    7F            DYNALC calling sequence           @SC88026
STKDRC   DS    F             DYNALC return code                @SC88026
STKTKT   DS    A             Ptr to STACK file FAB             @SC88026
SUPFNC   EXIT                                                  @SC86158
         TITLE 'TERMIO Routine - Handle terminal I/O'
* R1 points to a pair of (adr,len) for read or write.  If I/O is
* successfull, R15 returns transferred byte count (else returns -1).
*               Command code is in R0:
* 1 => Open line for I/O            4 => Write packet
* 2 => Close line                   5 => Read packet
* 3 => Reset line status after    ( 6 => Write message ) not used
*      environment changes
*
TERMIO   ENTER
         SR    15,15         OK                                @SC86295
         BCT   0,TRMCLS                                        @SC86295
* Open terminal line for protocol
         STAX  BR14          Ingore attention interrupts
         MVI   RIOC,X'80'    Nothing saved                     @SC86295
         MVI   TRMFLG,X'FF'  Initialize w/r flag               @SC87275
         B     RTRN0                                           @SC86295
* Close terminal line after protocol transfer
TRMCLS   BCT   0,TRMRSET                                       @SC86295
         STAX
         B     RTRN0                                           @SC86295
* (Re)set terminal characteristics to suit environment
TRMRSET  BCT   0,TRMRW                                         @SC86295
         B     RTRN0                                           @SC86295
*
*  Perform I/O request
TRMRW    BCT   0,TRMRD                                         @SC87015
         CLI   WRRD,0        Write/read?                       @SC87275
         BNE   *+8           Yes                               @SC87275
         MVI   TRMFLG,0      Indicate no action on follow-up   @SC87275
         L     0,4(1)        Get length                        @SC87015
         L     1,0(1)        and address                       @SC87015
         ICM   1,8,=X'02'    CONTROL                           @SC87317
         CLI   TRMTP,C'F'                                      @SC87317
         BNE   *+8                                             @SC87317
         ICM   1,8,=X'03'    FULLSCR                           @SC87317
         TPUT  (1),(0),R     Flags already set                 @SC87317
         B     RTRN0                                           @SC87317
*
* Read from terminal
TRMRD    MVC   KTGETT(8),0(1) Copy adr,len                     @SC87015
         TS    TRMFLG                                          @SC87275
         BZ    RTRN0         Just a follow-up. 0-length read   @SC87275
         MVI   ECBTGET,0     Clear ECB                         @SC87015
         SR    5,5           Set flag 'no timing'              @SC87015
         TM    FL5,TIMF                                        @NW86330
         BZ    TRMPST                                          @NW86330
         ICM   5,1,TIMOUT    Any timing requested?             @SC87015
         BZ    TRMPST        No, just wait                     @SC87015
         MH    5,=H'100'                                       @SC87015
         ST    5,TMPDW                                         @SC87015
         ST    11,TRMSTRP    Set up addressibility             @SC87015
         STIMER REAL,TIMEEXIT,BINTVL=TMPDW                     @NW86330
TRMPST   POST  ECBREAD       Tell async sub to go for it       @NW86330
         WAIT  ECB=ECBTGET                                     @NW86330
         CLI   ECBTGET+3,0   Check return code                 @NW86330
         BNE   TRMTIM                                          @NW86330
         LTR   5,5           Timing enabled?                   @SC87015
         BZ    TRMRET        No, fine                          @SC87015
         TTIMER CANCEL       Yes, kill timer                   @SC87015
TRMRET   L     15,KTGETT+4   Get length read                   @SC87015
         B     RTRN                                            @SC87015
TRMTIM   DETACH TASKADD      Blow off task                     @NW86330
         MVI   ECBREAD,0     Zero out read ECB                 @NW86330
         ATTACH EP=KERMTGET,MF=(E,COMPTR)                      @NW86330
         ST    1,TASKADD     Save adr for detach               @NW86330
         L     1,APKT        Ptr to data buffer                @SC87015
         MVI   0(1),AT       Timed out                         @SC87015
         B     RTRN1         Set count to one                  @SC87015
*
* Timer exit routine                                           @SC87015
         USING *,15          Addressiblity                     @NW86330
TIMEEXIT L     11,TRMSTRP                                      @SC87015
         POST  ECBTGET,1     Post timer ECB                    @NW86330
         BR    14            Return to OS                      @NW86330
TRMSTRP  DS    A             Ptr to storage                    @SC87015
         DROP  15                                              @SC87015
         LOCALS ,                                              @SC86295
         EXIT
         TITLE 'KERMTGET Routine - Read from terminal (timed)'
*  ECB's control timing flow                                   @NW86330
KERMTGET CSECT                                                 @SC87015
         USING *,15                                            @SC87015
         SAVE  (14,12),,*                                      @SC87015
         CNOP  0,4                                             @SC87015
         BAL   12,*+76                                         @SC87015
         DROP  15                                              @SC87015
         USING *,13                                            @SC87015
         DS    18F                                             @SC87015
         ST    12,8(13)                                        @SC87015
         ST    13,4(12)                                        @SC87015
         LR    13,12                                           @SC87015
         LM    10,11,0(1)    Set up addressibility             @SC87015
         LA    5,BR14        Exit routine adr (do nothing)     @SC86299
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
KTGLP0   WAIT  ECB=ECBREAD                                     @NW86330
         MVI   ECBREAD,0     Zero ECB                          @NW86330
         L     1,KTGETT      Adr of buffer to put in           @NW86330
         L     0,KTGETT+4    Max TGET (although tcam's 4k)     @NW86330
         TGET  (1),(0),ASIS                                    @NW86330
         LTR   15,15                                           @NW86330
         BZ    KTGLEN        Ok                                @NW86330
         C     15,F12                                          @NW86330
         BE    KTGLEN        Ok                                @NW86330
         SR    1,1           Error                             @NW86330
         BCTR  1,0                                             @NW86330
KTGLEN   ST    1,KTGETT+4    Save length                       @SC87015
         POST  ECBTGET       Tell em we read it                @NW86330
         B     KTGLP0        Keep repeating                    @NW86330
         LTORG                                                 @SC87015
         DROP  13
         TITLE 'GETLIN Routine - Get a line from terminal'     @SC87015
* Entry: R1->buffer of length 256                              @SC87015
* Exit: Buffer filled, R0=length, R15=0 if ok. Else R15=1.     @SC87015
GETLIN   ENTER                                                 @SC87015
         LR    7,1           Save buffer ptr                   @SC87015
         LA    2,APGPB                                         @NW86330
         USING GTPB,2                                          @NW86330
         ICM   1,15,GTPBIBUF Already got something?            @SC87015
         BNZ   GTL1          Yes, return it                    @SC87015
         L     15,GETLINAD   Entry point for GETLINE routine   @NW86330
         GETLINE PARM=(2),TERMGET=(EDIT,WAIT),ENTRY=(15),              +
               MF=(E,IOPLAREA)                                 @SC87015
         SR    0,0                                             @SC87015
         C     15,F4         Problem?                          @SC87015
         BH    GTLA          Yes, give up with len=0           @SC87015
         L     1,GTPBIBUF                                      @NW86330
GTL1     LH    5,0(1)        Length of stuff                   @NW86330
         LR    0,5           Save it                           @SC87015
         S     5,F5          Fix for EX                        @SC87015
         BM    GTLA          Too short                         @SC87015
         C     5,F256                                          @SC87015
         BL    *+8                                             @SC87015
         LA    5,255         Too long                          @SC87015
         EX    5,GTLCMMV     Copy to buffer                    @SC87015
         FREEMAIN RC,LV=(0),A=(1),SP=1 Free input buffer       @NW86330
GTLA     MVC   GTPBIBUF,F0   Clear input indicator             @SC87015
         L     1,4(13)                                         @SC87015
         LA    0,1(5)        Buffer length                     @SC87015
         ST    0,20(1)       Return as R0                      @SC87015
         B     RTRN0                                           @SC87015
         DROP  2                                               @NW86330
GTLCMMV  MVC   0(,7),4(1)                                      @SC87015
         LOCALS ,                                              @SC87015
GETLIN   EXIT  ,                                               @SC87015
         TITLE 'SCRNIO Routine - Handle screen I/O via Series/1'
* R1 points to a pair of (adr,len) for read or write.  If I/O is
* successfull, R15 returns transferred byte count (else returns -1).
*               Command code is in R0:
* 1 => Open screen for I/O            4 => Write packet
* 2 => Close screen                   5 => Read packet
* 3 => Reset screen status after      6 => Write message
*      environment changes
*
SCRNIO   ENTER
         BCT   0,SCRCLS                                        @SC86295
* Set up for transparent I/O
         B     RTRN0                                           @SC86295
SCRCLS   BCT   0,SCRRSET                                       @SC86295
* Clean up after I/O
         B     RTRN0                                           @SC86295
* (Re)set device characteristics to suit environment
SCRRSET  BCT   0,SCRRW                                         @SC86295
         B     RTRN0
*
*  Perform I/O request
SCRRW    LA    8,SCRPLST     Get PLST ptr                      @SC88019
         MVC   5(3,8),1(1)   Copy adr                          @SC88019
         MVC   2(2,8),6(1)   Copy len                          @SC88019
         BCT   0,SCRRD       Different handling for each       @SC88019
         LR    1,8           WRITE: use new form of call       @SC88019
         MVI   4(8),X'03'    Flags: FULLSCR/NOEDIT             @SC88019
         MVI   12(8),X'81'   More flags: NOEDIT                @SC88019
         ICM   0,8,=X'80'    Set hi bit of R0                  @SC88019
         SVC   93            Issue TPUT                        @SC88019
         B     RTRN0         Assume OK                         @SC88019
SCRRD    BCT   0,SCRWM       Go if "Write message"             @SC88019
         MVI   4(8),X'81'    Flags: TGET                       @SC88019
         BAL   9,SCRNEX      Execute internal subr             @SC86295
         TM    FL1,DEBUG     Logging in effect?                @SC87286
         BZ    RTRN          No, that's all                    @SC87286
         L     2,LOGBUF      Ptr to buffer                     @SC87286
         MVI   0(2),C'A'     Set label                         @SC87286
         L     3,0(8)        Ptr to AID                        @SC87286
         MVC   2(3,2),0(3)   Copy into buffer                  @SC87286
         LR    9,15          Save data length                  @SC87286
         WRITF LOGPTR,BSIZE=5 Log it                           @SC87286
         LR    15,9          Return data length                @SC87286
         B     RTRN          Return                            @SC86299
*                                                              @SC88019
SCRWM    MVI   4(8),X'02'    Flags: CONTROL code               @SC88019
         BAL   9,SCRNEX      Execute internal subr             @SC88019
         B     RTRN0         Return OK                         @SC88019
*
SCRNEX   LM    0,1,0(8)                                        @SC88019
         SVC   93                                              @SC86299
         LR    15,1          Number of chars recv'd            @SC86299
         BR    9                                               @SC86299
         LOCALS ,                                              @SC86299
SCRPLST  DS    4F            Plist for TPUT/TGET               @SC88019
SCRNIO   EXIT  ,                                               @SC86299
         TITLE 'SETMSG Routine - controls CP breakin'
* Entry: R1 selects operation
* Exit: R15=0 if ok
* 1-> Analyze user environment, determine if suitable.
*     Save quantities needed and condition line for entering commands.
*     Perform any system-dependent initialization.
* 2-> Condition line for protocol transfers.
* 3-> Decondition line at end of transfer.
* 4-> System-dependent clean-up at exit.
* 5-> Reperform system-dependent initialization after SET LINE.
SETMSG   ENTER ,                                               @SC87015
         BCT   1,STM2                Go if R1 not 1, so no init
         L     1,ORGR1       Get original R1                   @SC86299
         TM    0(1),X'80'    Is this a command processor?      @SC86299
         BO    NOTCP         No, then refuse user              @SC86299
         USING CPPL,1                                          @SC86299
         L     2,CPPLUPT     Get ptr to UPT                    @SC86299
         USING UPT,2                                           @SC86299
         XR    3,3                                             @SC86299
         IC    3,UPTPREFL    Get length                        @SC86299
         STH   3,DESTL       Save for later                    @SC86299
         MVC   DEST(7),UPTPREFX Move prefix                    @SC86299
         MVI   DESTP,C' '    Not a PDS                         @SC86299
         LA    4,IOPLAREA    Get address of IOPL               @TS86001
         USING IOPL,4        Make it addressable               @TS86001
         MVC   IOPLUPT,CPPLUPT Copy UPT ptr                    @TS86001
         MVC   IOPLECT,CPPLECT Copy ECT ptr                    @TS86001
         LA    0,CPECB       Get address of ECB                @TS86001
         ST    0,IOPLECB     Put into IOPL                     @TS86001
         LA    4,DAPLAREA    Address of DAPL                   @SC87351
         USING DAPL,4                                          @SC87351
         MVC   DAPLUPT,CPPLUPT  Fill it in                     @SC87351
         MVC   DAPLECT,CPPLECT                                 @SC87351
         MVC   DAPLPSCB,CPPLPSCB                               @SC87351
         ST    0,DAPLECB                                       @SC87351
         LA    0,DA00        Ptr to parameter block            @SC87351
         ST    0,DAPLDAPB                                      @SC87351
         GTSIZE ,            Get terminal info                 @SC86299
         LTR   0,0           Is this a graphics device?        @SC86299
         BZ    STMSTY        No                                @SC86299
         MVI   TRMTP,C'S'    Remember going via S/1            @SC87166
         B     STMOK                                           @SC86299
STMSTY  STSIZE SIZE=130      Set up linesize                   @TS86001
STMOK    MVC   STMUCH,UPTCDEL                                  @SC86299
         STM   10,11,COMPTR  Save ptrs for KERMTGET            @SC87015
         MVI   CIROPT,2                                        @SC87015
         L     1,CIRWA       Initialize length ptrs            @SC87015
         MVC   0(2,1),CIRWAL                                   @SC87015
         MVC   2(2,1),F0                                       @SC87015
         LA    0,STKDSN      Set up DSN for STACK              @SC88026
         LH    1,DESTL                                         @SC88026
         LA    2,DEST        Get userid prefix                 @SC88026
         LA    3,LFID                                          @SC88026
         MVCL  0,2           Copy prefix                       @SC88026
         LR    1,3                                             @SC88026
         LA    2,=CL8'.KER.BUF'                                @SC88026
         LA    3,8           Copy rest of name                 @SC88026
         ICM   3,8,BLANK     Fill with blanks                  @SC88026
         MVCL  0,2                                             @SC88026
         LA    5,BR14        Exit routine adr (do nothing)     @SC86299
         STAX  (5),DEFER=NO,REPLACE=YES,MF=(E,STAXPLR)         @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         STAX  (5),DEFER=NO,REPLACE=NO,MF=(E,STAXPL)           @NW86330
         LOAD  EP=IKJGETL    Get line routine adr              @NW86330
         ST    0,GETLINAD    Store it off                      @NW86330
         LA    0,PTLLEN                                        @SC88026
         ST    0,PTPB+4      Set up PUTLINE parameter block    @SC88026
         LOAD  EP=IKJPUTL    PUTLINE routine adr               @SC88026
         ST    0,PUTLINAD                                      @SC88026
         L     5,=A(KERMTGET) Adr of TGET module               @NW86330
         PTEXT 'IDENTIFY failed.' Just in case                 @SC87015
         IDENTIFY EP=KERMTGET,ENTRY=(5)                        @NW86330
         LTR   15,15                                           @NW86330
         BNZ   SUBERR                                          @SC87015
         PTEXT 'ATTACH failed.' Just in case                   @SC87015
         ATTACH EP=KERMTGET,MF=(E,COMPTR)                      @SC87015
         LTR   15,15                                           @NW86330
         BNZ   SUBERR                                          @SC87015
         ST    1,TASKADD     Save adr for detach               @NW86330
         B     RTRN0                                           @SC86295
*
STM2     BCT   1,STM3                Go if R1 was not 2, so not off
         CLI   TRMTP,C'T'    TTY terminals can't change hndshk @SC87343
         BNE   STM2X                                           @SC87343
         CLI   S1HND,XON     User wants special one anyway?    @SC87343
         BNE   STM2X                                           @SC87343
         MVI   S1HND,0       System provides the handshake     @SC87343
STM2X    DS    0H                                              @SC87343
         TM    FL1,TSTF                                        @SC86295
         BO    RTRN0         Just testing, don't change it     @SC86295
         CLI   TRMLIN,C' '   Alternate comm line?              @SC87300
         BNE   RTRN1         Not allowed!                      @SC87300
         LA    5,STMUOFF                                       @SC86299
         B     STMD
*
STM3     BCT   1,STM4                                          @SC86316
         LA    5,STMUCH      Restore user's settings           @SC86299
STMD     L     1,ORGR1       Get original R1                   @SC86299
         L     2,CPPLUPT     Get ptr to UPT                    @SC86299
         MVC   UPTCDEL(2),0(5)                                 @SC86299
         L     4,CPPLPSCB    Get ptr to PSCB                   @SC86299
         USING PSCB,4                                          @SC86299
*        MVC   PSCBCHAR(2),0(5)                                @SC87296
         DROP  1,2,4                                           @SC86299
         B     RTRN0
*
STM4     BCT   1,STM5        Special clean-up                  @SC87351
         DETACH TASKADD      Kill sub-task                     @SC87296
         B     RTRN0         Special clean-up done             @SC87296
*
STM5     B     RTRN1         Other lines not allowed           @SC87351
*
NOTCP    PTEXT 'Kermit-TSO must be a command processor'        @SC86299
         B     SUBERR                                          @SC86299
*
STMUOFF  DC    2X'0'         No char & line delete             @SC86299
         LOCALS ,                                              @SC86295
SETMSG   EXIT
         TITLE 'DISKIO Routine - performs disk I/O functions'
* Function selected on entry by R0:
* 1=> open (in): R1->pattern FDB, R2->name.  Returns R0->FAB, R1->FDB
* 2=> open (out): (same, but no complete FDB if new file)
* 3=> test name: R2->name.  Returns R1->FDB if found (else R15=1)
* 4=> close file: R1->adr(FAB).
* 5=> set up search: R1->pattern name.
* 6=> return next file in list:  Returns R1->FDB + sets up FILNAM
* 7=> close search (if any).
* 8=> test CWD string: R1->string.  Returns R15=0 if ok, else =1.
* 9=> read: R1->FAB.  Returns R15=12 if EOF, 0 if ok; R0=# data
* 10=> write: R1->FAB.  Returns R15=13 if disk full, 0 if ok.
* 11=> test space: R1->adr(FAB), R2=est. Kbytes.  Return R15=0 if ok.
* 12=> analyze R/W error, set ERRNUM, make EMSG: R1->FAB, TMPDW=code
*      always returns R15=1
* 13=> directory info on file: R1->name.  Returns R15=0 if ok.
* 14=> delete file: R1->name.  Returns R15=0 if ok.
* 15=> rename file: R1->name, R2->new name.  Returns R15=0 if ok.
* 16=> copy file: R1->name, R2->new name.  Returns R15=0 if ok.
DISKIO   ENTER
         USING FABD,3                                          @SC86295
         SR    4,4           Signal no block assigned          @SC86295
         LA    5,DYNDSP                                        @SC86345
         LA    6,FDBTRKAL-FDBD(1) Use pattern TRKAL            @SC88026
         LA    7,DYNRC                                         @SC86345
         STM   5,7,DYNPL+16  Set up calling sequence           @SC86345
         BCT   0,DSKOPNO                                       @SC86295
*
* Open for input file whose name is at (R2), FDB at (R1)
         BAL   9,DSKALC      Get FAB                           @SC86295
         BAL   2,DSKLKP      Get DSCB                          @SC86299
         BNZ   DSKER1        Not found                         @SC86295
         BAL   14,DSKVALS                                      @SC86295
         BAL   9,DSKFABS     Set up FAB from FDB               @SC86299
         LH    0,FABLRECL                                      @SC86299
         CH    0,FDBBSIZ+2   Too big?                          @SC86299
         BNL   *+8           Yes, just read a buffer full      @SC86299
         ST    0,FDBBSIZ     Set buffer size, in case RECFM=F  @SC86299
         MVI   DYNDSP,X'88'  SHR,KEEP                          @SC86299
         CNOP  0,4                                             @SC86299
         BAL   2,DSKOPT      Open and test                     @SC86299
         OPEN  (0,INPUT),MF=L                                  @SC86299
*
* Open for output file whose name is at (R2), FDB at (R1)
DSKOPNO  BCT   0,DSKTEST                                       @SC86295
         BAL   9,DSKALC      Get FAB                           @SC86295
         MVI   DYNDSP,X'42'  NEW,CATLG                         @SC86299
         BAL   2,DSKLKP      Get DSCB                          @SC86299
         BNZ   DSKOPN        Not found, just writing new       @SC86299
         MVI   DYNDSP,X'18'  OLD,KEEP                          @SC86299
         TM    FDBFLGS,APPN                                    @SC86295
         BZ    DSKOPN                                          @SC86299
         BAL   14,DSKVALS                                      @SC86295
         BAL   9,DSKFABS     Set up FAB from FDB               @SC86299
         MVI   DYNDSP,X'28'  MOD,KEEP                          @SC86299
DSKOPN   CNOP  0,4                                             @SC86299
         BAL   2,DSKOPT      Open and test                     @SC86299
         OPEN  (0,OUTPUT),MF=L                                 @SC86299
DSKOPT   KCALL DYNALC,DYNPL,EXT                                @SC86299
         OPEN  ((3)),MF=(E,(2))                                @SC86299
         TM    FABOFLGS,X'10'                                  @SC86299
         BZ    DSKER1        Didn't work                       @SC86299
         B     RTRN0                                           @SC86295
*
* Test for existence of file whose name is at (R2)
DSKTEST  BCT   0,DSKCLOS                                       @SC86295
         LR    15,2          Copy DSN ptr                      @SC86299
         LA    3,DSKSTT                                        @SC86295
         MVC   FABDSN,0(2)   Copy DSN as well                  @SC88026
         BAL   2,DSKLKP      Get DSCB                          @SC86299
         BNZ   DSKER1        Not found                         @SC86299
         BAL   14,DSKVALS                                      @SC86299
         B     RTRN0                                           @SC86299
*
* Close file whose ticket is at (R1), release block
DSKCLOS  BCT   0,DSKNSET                                       @SC86295
         ICM   3,15,0(1)     Get FAB ptr, if any               @SC86295
         BZ    RTRN0         None, ignore                      @SC86295
         XC    0(4,1),0(1)   Yes, now clear ticket             @SC86295
         CLOSE ((3))                                           @SC86299
      FREEPOOL (3)                                             @SC86299
         LA    0,FABDWDS                                       @SC86295
         LR    1,3                                             @SC86299
       DMSFRET DWORDS=(0),LOC=(1)                              @SC86295
         B     RTRN0                                           @SC86295
*
* Read from file whose ticket is at (R1)
DSKRED   BCT   0,DSKWRT                                        @SC86295
         LTR   3,1           Get FAB ptr                       @SC86299
         BNP   RTRN1         Not defined anymore               @SC86299
         L     15,FABGET     I/O routine                       @SC86299
         BALR  14,15         Go to it                          @SC86299
         LM    4,5,FDBBUFF   Get buffer and size               @SC86299
         LH    7,FABLRECL    Actual length                     @SC86299
         AR    7,1           End of record                     @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   1,4(1)        Skip over SDW if V                @SC86299
         SR    7,1           Revised length                    @SC86299
         LR    6,1                                             @SC86299
         CR    7,5                                             @SC86299
         BNL   *+6                                             @SC86299
         LR    5,7           Buffer not filled                 @SC86299
         L     1,4(13)                                         @SC86299
         ST    5,20(1)       Return length in R0               @SC86299
         MVCL  4,6           Copy to buffer                    @SC86299
         B     RTRN0                                           @SC86299
* End of file on input. Don't close it yet.                    @SC86295
DSKEOD   LA    15,12         End return code                   @SC86295
         B     RTRN                                            @SC86295
*
* Write to file whose ticket is at (R1)
DSKWRT   BCT   0,DSKTSP                                        @SC86316
         LTR   3,1           Get FAB ptr                       @SC86299
         BNP   RTRN1         Not defined anymore               @SC86299
         LM    4,5,FDBBUFF   Get buffer and size               @SC86299
         LR    6,5           Copy for LRECL                    @SC86299
         CH    6,FDBLRC                                        @SC86299
         BNH   *+8                                             @SC86299
         LH    6,FDBLRC      Don't allow more than LRECL if V  @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   6,4(5)        + 4 if RECFM=V                    @SC86299
         STH   6,FABLRECL    Set up for output                 @SC86299
         MVI   ERRNUM,0      Clear error number                @SC86299
         L     15,FABGET     I/O routine                       @SC86299
         BALR  14,15         Do it                             @SC86299
         SR    15,15                                           @SC86299
         ICM   15,1,ERRNUM   See if deadly error               @SC86299
         BNZ   RTRN          Yes, pass return code             @SC86299
         XC    0(4,1),0(1)                                     @SC86299
         STCM  6,3,0(1)      In case V                         @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   1,4(1)        V: space over SDW                 @SC86299
         LR    6,1                                             @SC86299
         LR    7,5                                             @SC86299
         MVCL  6,4           Copy to output record             @SC86299
         B     RTRN0                                           @SC86295
*
* Analyze error: packed dec. code in TMPDW
DSKXXX   BCT   0,DSKUTL                                        @SC86316
         MVI   ERRNUM,ERRDIE Set Kermit error code             @SC87338
         L     2,EMSGP       Ptr to msg buffer                 @SC87338
         CLC   =C'  ',0(2)   Proper SYNAD message?             @SC87338
         BE    *+10          Yes, ok                           @SC87338
         XC    EMSGL,EMSGL   No, clear length                  @SC87338
         B     RTRN1                                           @SC87338
*
* Disk utility for file(s) at (R1) and (R2)
DSKUTL   LR    8,0           Save code-12                      @SC86316
         BCTR  0,0           Code-13: DIR,DEL,REN,COP          @SC86316
         SLA   0,3                                             @SC86295
         LA    5,DSKCMDS                                       @SC86295
         AR    5,0           Ptr to command name               @SC86295
         LA    7,CMD         Buffer for system command         @SC86299
         MVC   0(8,7),0(5)                                     @SC86299
         LA    7,8(7)                                          @SC86299
         BAL   3,DSKUTCP                                       @SC86295
         SRA   0,4                                             @SC86295
         BZ    *+10                                            @SC86295
         LR    1,2           2nd file                          @SC86295
         BAL   3,DSKUTCP                                       @SC86295
         LA    0,CMD                                           @SC86295
         LR    6,7                                             @SC86299
         SR    6,0                                             @SC86299
         NI    FL4,255-UCMD  Not user command: already tokens  @SC86295
         KCALL SUPFNC,3      Execute it                        @SC86295
         B     RTRN                                            @SC86295
*
DSKUTCP  LR    4,0           Save ID                           @SC86299
         LA    0,FFDSP                                         @SC86299
         KCALL FSPEC                                           @SC86299
         MVI   0(15),C' '                                      @SC86299
         LA    7,1(15)       New output ptr                    @SC86299
         LR    0,4                                             @SC86299
         BR    3                                               @SC86295
*
DSKCMDS  DC    C'LISTCAT '   Utility command names             @SC86299
         DC    C'DELETE  '                                     @SC86299
         DC    C'RENAME  '                                     @SC86299
         DC    C'COPY    '                                     @SC86299
*
DSKTV    TM    FABRECFM,FABRECU                                @SC86299
         BNM   4(2)          U                                 @SC86299
         TM    FABRECFM,FABRECF                                @SC86299
         BO    4(2)          F                                 @SC86299
         BR    2             V                                 @SC86299
* Return on error, release useless block, if any
DSKER1   LTR   1,4           Any block assigned?               @SC86295
         BZ    RTRN1         No                                @SC86295
         LA    0,FABDWDS     Yes, release it                   @SC86295
       DMSFRET DWORDS=(0),LOC=(1)                              @SC86295
         B     RTRN1         Flag error                        @SC86295
*
DSKALC   LR    5,1           Save FDB ptr                      @SC86295
         LA    6,1           Update counter                    @SC86299
         A     6,EVCTR                                         @SC86299
         ST    6,EVCTR                                         @SC86299
         LA    0,FABDWDS                                       @SC86295
       DMSFREE DWORDS=(0),ERR=DSKER1                           @SC86295
         LR    3,1           New block ptr                     @SC86295
         LR    4,1                                             @SC86295
         L     1,4(13)                                         @SC86295
         ST    3,20(1)       Return R0                         @SC86295
         XC    0(8*FABDWDS,3),0(3)                             @SC86295
         MVC   FDBD(FDBCOP),0(5) Copy user's FDB               @SC86295
         MVC   FABDSN,0(2)                                     @SC86299
         LA    15,FABDSN     Set up DSN ptr                    @SC86299
         LA    0,FABDDNAM    Get DDN ptr                       @SC86299
         LA    1,FDBUNT      Get UNIT ptr                      @SC86299
         LA    2,FDBVOL      Get VOL ptr                       @SC86299
         STM   15,2,DYNPL    Set up DYNALC                     @SC86299
         MVI   FABBUFCB+3,1  Fill out DCB                      @SC86299
         MVI   FABDSORG,X'40' =PS                              @SC86299
         CLI   FABDSN+44,0   Special case of PDS?              @SC86299
         BNE   *+12          No                                @SC86299
         MVI   FABDSORG,X'02' Yes, set DSORG=PO                @SC86299
         MVI   FABDSN+44,C' ' and blot out member              @SC86299
         MVI   FABIOBAD+3,1                                    @SC86299
         LA    0,DSKEOD                                        @SC86299
         LA    1,DSKOPEX                                       @SC86299
         STM   0,1,FABEODAD                                    @SC86299
         UNPK  FABDDNAM,EVCTR(5)                               @SC86299
         TR    FABDDNAM,TRHEX  Get unique DDNAME               @SC86299
         MVI   FABDDNAM,C'K'                                   @SC86299
         MVI   FABDDNAM+7,C'Z'                                 @SC86299
         MVC   FABOFLGS(4),=X'02,00,48,48'                     @SC86299
         MVI   FABCHECK+3,1                                    @SC86299
         LA    1,DSKSYN                                        @SC87338
         ST    1,FABSYNAD    In case of error                  @SC86299
         MVI   FABIOBA+3,1                                     @SC86299
         MVC   FABEOBAD(16),FABIOBA                            @SC87314
         MVI   FABEOB+3,1                                      @SC86299
DSKFABS  LH    1,FDBLRC      Copy Info to DCB                  @SC86299
         STH   1,FABLRECL                                      @SC86299
         MVC   FABBLKSI,FDBBLKSI                               @SC86299
         MVI   FABRECFM,FABRECU                                @SC86299
         CLI   FDBRCF,C'U'                                     @SC86299
         BER   9                                               @SC86299
         MVI   FABRECFM,FABRECF+FABRECBR                       @SC86299
         CLI   FDBRCF,C'F'                                     @SC86299
         BER   9                                               @SC86299
         MVI   FABRECFM,FABRECV+FABRECBR                       @SC86299
         LA    1,4(1)        Allow for RDW                     @SC86299
         STH   1,FABLRECL                                      @SC86299
         BR    9                                               @SC86299
*
* Call with R15->name, return to R2 with CC set (Z if ok)
DSKLKP   SR    0,0                                             @SC86299
         LA    1,CAMVOLS                                       @SC86299
         LA    14,X'44'      Name code                         @SC86299
         SLL   14,24                                           @SC86299
         STM   14,1,CAMLOC   Save dsn ptr, etc                 @SC86299
         LA    0,CAMVOLS+6                                     @SC86299
         LA    1,CAMDSCB                                       @SC86299
         LA    14,X'C1'      Search code                       @SC86299
         SLL   14,24                                           @SC86299
         STM   14,1,CAMOBT                                     @SC86299
        LOCATE CAMLOC                                          @SC86299
         LTR   6,15          Retain 1st code in R6             @SC86299
         BNZR  2             Give up                           @SC86299
        OBTAIN CAMOBT        Get DSCB                          @SC86299
         LTR   15,15         Test return code                  @SC86299
         BR    2                                               @SC86295
*
* Handle synchronous disk I/O errors
DSKSYN   SYNADAF ACSMETH=QSAM Get system to do the work        @SC87338
         L     2,EMSGP       Ptr to msg buffer                 @SC87338
         MVC   0(80,2),48(1) Copy message (inc. 2 blanks)      @SC87338
         LA    2,80                                            @SC87338
         ST    2,EMSGL       Length of string                  @SC87338
         SYNADRLS            Clean up                          @SC87338
         B     RTRN1                                           @SC87338
*
* Set up search through list of files, pattern at (R1)
DSKNSET  BCT   0,DSKNXT                                        @SC86295
         NI    DSKFL,255-WFN-NXDON                             @SC87015
         MVC   NXFN,0(1)     Copy name                         @SC87015
         LA    1,NXFN+44                                       @SC87015
         TRT   NXFN(44),TRTBL                                  @SC87015
         LR    3,1           End of name                       @SC87015
         MVI   TRTBL+C'*',1                                    @SC87015
         TRT   NXFN(44),TRTBL Check for wild card              @SC87015
         MVI   TRTBL+C'*',0                                    @SC87015
         BZ    RTRN0         Len=44, just use the one file     @SC87015
         CLI   0(1),C'*'     Did we find an asterisk           @SC87015
         BNE   RTRN0         No, just the end of the name      @SC87015
         OI    DSKFL,WFN     Mark it wild                      @SC87015
         LA    4,1(1)                                          @SC87015
         ST    4,NXSFPTR     Save ptr to suffix                @SC87015
         SR    3,4                                             @SC87015
         STH   3,DSNSFL      and length                        @SC87015
         LA    0,NXFN                                          @SC87015
         SR    1,0                                             @SC87015
         STH   1,DSNPFL      Length of prefix                  @SC87015
         L     14,CIRSRCH    Argument ptr                      @SC87015
         LA    15,44                                           @SC87015
         ICM   1,8,BLANK                                       @SC87015
         MVCL  14,0          Copy with blank fill              @SC87015
         LINK  EP=IKJEHCIR,MF=(E,CIRPARM) Call catalog routine @NW86330
         LTR   15,15                                           @SC87015
         BNZ   RTRN1         Not found                         @SC87015
         L     2,CIRWA       Adr of returned catalog buffer    @NW86330
         MVC   2(2,2),F0     Zero out length in cat buffer     @NW86330
         SH    2,=Y(45-4)    Skip count bytes, then back one   @SC87015
         ST    2,CATDSPTR    Save ptr to buffer                @NW86330
         B     RTRN0                                           @SC86295
*
* Flush previous file pattern
DSKXSET  BCT   0,DSKCWDF                                       @SC86295
         OI    DSKFL,NXDON                                     @SC87015
         B     RTRN0                                           @SC87015
*
* Check CWD string, return code in R15
DSKCWDF  BCT   0,DSKRED                                        @SC86295
         LR    15,1          Copy name ptr                     @SC87015
         LR    5,1                                             @SC87015
         BAL   2,DSKLKP      Check name                        @SC87015
         BZ    DSKCWD1       Was a full DSN, not a prefix      @SC87015
         CR    6,15                                            @SC87015
         BNE   RTRN1         ditto                             @SC87015
         CLI   44(5),C'.'    PDS requested?                    @SC87015
         BE    RTRN1         Yes, but file not found           @SC87015
         SH    15,=H'12'     RC of 12 means was a prefix       @SC87015
         B     RTRN                                            @SC87015
DSKCWD1  CLI   44(5),C'.'    PDS requested?                    @SC87015
         BNE   RTRN1         No, but file was found            @SC87015
         B     RTRN0         Yes, ok                           @SC87015
*
* Check disk space for proposed file: FAB ptr at (R1)
DSKTSP   BCT   0,DSKXXX                                        @SC86316
* - - - get size of available space in R0,R1                   @SC87015
         LA    0,1023        For now, claim 4 Tbyte            @SC87015
         SRDA  0,10          Convert to Kbytes                 @SC86316
         CLR   1,2                                             @SC87012
         BL    RTRN1         No room                           @SC86316
         B     RTRN0         Ok                                @SC86316
*
DSKNXT   BCT   0,DSKXSET                                       @SC86295
* Check against prefix and suffix criteria and return next match,
*   if any
* Also return info in a File Descriptor Block                  @SC86151
         TM    DSKFL,NXDON                                     @SC87015
         BO    RTRN1         Nothing more                      @SC87015
         TM    DSKFL,WFN     Are we scanning?                  @SC87015
         BO    NXFBEG        Yes, do it                        @SC87015
         OI    DSKFL,NXDON   No, that's the only one           @SC87015
         MVC   FILNAM,NXFN                                     @SC87015
         LA    2,FILNAM                                        @SC87015
         B     DSKTEST+4     Now return file info              @SC87015
NXFBEG   L     6,CATDSPTR    Ptr to place in catalog           @NW86330
         USING CATDSET,6                                       @NW86330
NXFDS    LA    6,45(6)       Next                              @SC87015
         CLI   TYPEBYTE,C'A'                                   @NW86330
         BNE   NXFZ          Assume end of list                @SC87015
         LH    2,DSNPFL      Get prefix length                 @SC87015
         LTR   2,2                                             @NW86330
         BNP   XL0092                                          @NW86330
         LA    14,NXFN       Compare saved prefix              @SC87015
         LA    3,CATDNAME     against this name                @SC87015
         LA    5,0(2,3)      End of possible match             @SC87015
         BCTR  2,0           Set up for CLC                    @SC87015
         EX    2,NXFCMP                                        @SC87015
         BNE   NXFDS         No match                          @SC87015
XL0092   CLC   DSNSFL,F0                                       @SC87015
         BNH   XL0002        Don't check suffix                @NW86330
         LA    1,44(3)       Limit of name field               @SC87015
         TRT   0(44,3),TRTBL Find end of name                  @SC87015
         LR    3,1                                             @SC87015
         LH    4,DSNSFL                                        @SC87015
         SR    3,4           Ptr to start of suffix            @SC87015
         CR    3,5                                             @SC87015
         BNH   NXFDS         Not longer than prefix+suffix     @SC87015
         BCTR  4,0                                             @SC87015
         L     14,NXSFPTR    Ptr to comparison suffix          @SC87015
         EX    4,NXFCMP                                        @SC87015
         BNE   NXFDS         No match                          @SC87015
XL0002   MVC   FILNAM(44),CATDNAME   Copy name                 @SC87015
         MVI   FILNAM+44,C' '                                  @SC87015
         MVC   FILNAM+45(7),FILNAM+44                          @SC87015
         ST    6,CATDSPTR    Save ptr for next time            @NW86330
         LA    2,FILNAM                                        @SC87015
         B     DSKTEST+4     Now return file info              @SC87015
*
NXFCMP   CLC   0(,3),0(14)                                     @SC87015
*
NXFZ     OI    DSKFL,NXDON                                     @SC87015
         B     RTRN1         Ran out of names                  @SC87015
*
DSKVALS  LA    0,FDBD        Ptr to FDB                        @SC86295
         L     1,4(13)                                         @SC86295
         ST    0,24(1)       Return ptr to caller              @SC86295
         NI    FDBFLGS,255-PDSF                                @SC87015
         TM    DS1DSO,2      ORG=PO?                           @SC87015
         BZ    *+8           No                                @SC87015
         OI    FDBFLGS,PDSF  Yes, it's a PDS                   @SC87015
         SR    7,7                                             @SC87296
         IC    7,DS1CRDT     Get year in binary                @SC87296
         CVD   7,TMPDW                                         @SC87296
         MVO   FDBDATE+1(2),TMPDW Copy year                    @SC87296
         ICM   7,3,DS1CRDT+1 Get day-of-year in binary         @SC87296
         MVC   DSKMNTH,=AL1(30,31,30,31,31,30,31,30,31,28,31)  @SC86299
         TM    DS1CRDT,3                                       @SC87296
         BNZ   *+8                                             @SC87296
         MVI   DSKMNTH+9,29  Leap year, change Feb.            @SC86299
         LA    6,11                                            @SC86299
         SR    0,0                                             @SC86299
DSKVMDL  IC    0,DSKMNTH-1(6)                                  @SC86299
         SR    7,0           Test if passed the right month    @SC86299
         BNP   DSKVMDM       Got it                            @SC86299
         BCT   6,DSKVMDL                                       @SC86299
         SR    0,0           Hit December                      @SC86299
DSKVMDM  AR    7,0           Get day of month                  @SC86299
         LCR   6,6                                             @SC86299
         LA    6,12(6)       Get month                         @SC86299
         MH    6,=H'100'                                       @SC86299
         AR    6,7           Combine MMDD                      @SC86299
         MH    6,=H'10'                                        @SC86299
         CVD   6,TMPDW                                         @SC86299
         MVC   FDBDATE+2(2),TMPDW+5                            @SC86299
* = = = = = get file size in bytes in R6,R7 - - -
         SR    6,6           Return 0 for now (i.e., unknown)  @SC87015
         SR    7,7                                             @SC87015
         AL    7,=F'1023'    Round up                          @SC87007
         BNO   *+8           No overflow                       @SC86239
         LA    6,1(6)                                          @SC86239
         SRDA  6,10                                            @SC86239
         ST    7,FDBSIZE                                       @SC86299
         MVI   FDBDATE,X'19' Assume 20th Cent                  @SC86295
         CLI   FDBDATE+1,X'50'                                 @SC86295
         BH    *+8           Ok                                @SC86295
         MVI   FDBDATE,X'20' Must be 21st                      @SC86295
         MVC   FDBBLKSI,DS1BLK                                 @SC86299
         MVC   FDBVOL,DS1VOL Copy volume name                  @SC86299
         LH    1,DS1BLK      Use BLKSIZE if 'U'                @SC86299
         MVI   FDBRCF,C'U'                                     @SC86299
         TM    DS1RCF,FABRECU                                  @SC86299
         BO    DSKVLR                                          @SC86299
         LH    1,DS1LRC      Use LRECL if 'F'                  @SC86299
         MVI   FDBRCF,C'F'                                     @SC86299
         TM    DS1RCF,FABRECF                                  @SC86299
         BO    DSKVLR                                          @SC86299
         MVI   FDBRCF,C'V'                                     @SC86299
         S     1,F4          Use LRECL-4 if 'V'                @SC86299
DSKVLR   STH   1,FDBLRC                                        @SC86299
         XC    DA00(20),DA00 Clear parameter block             @SC87351
         LA    6,FABLDSN     Ptr to halfword at beginning      @SC87351
         ST    6,DA00PDSN    Set up parameter block            @SC87351
         LA    1,FABDSN+44   End of DSN                        @SC88026
         TRT   FABDSN,TRTBL  Find 1st blank, if any            @SC88026
         LA    6,FABDSN      Start                             @SC88026
         SR    1,6           Get length in bytes               @SC87351
         STH   1,FABLDSN                                       @SC87351
         LR    6,14          Save return adr                   @SC87351
         LINK  EP=IKJDAIR,MF=(E,DAPLAREA)                      @SC88036
         LR    14,6                                            @SC87351
         TM    DA00+2,2      Dataset allocated?                @SC87351
         BZR   14            No                                @SC87351
         OI    FDBFLGS,FDBACTV Yes                             @SC86295
         BR    14                                              @SC86299
*
DSKOPEX  DC    0F'0',X'05',AL3(DSKOPC) OPEN EXIT               @SC86299
         DC    X'91',AL3(DSKABEND)  DCB ABEND exit             @TS86001
*
* Look for x37 abends                                          @TS86001
DSKABEND MVI   ERRNUM,ERRFUL Assume full                       @SC86355
         XC    EMSGL,EMSGL   Clear extra message               @SC87338
         CLC   =X'B370',0(1) B37 abend?                        @TS86001
         BE    DSKABX        Yes                               @SC86355
         CLC   =X'D370',0(1) D37 abend?                        @TS86001
         BE    DSKABX        Yes                               @SC86355
         CLC   =X'E370',0(1) E37 abend?                        @TS86001
         BE    DSKABX        Yes                               @SC86355
* Look for 013 abend                                           @TS86001
         MVI   ERRNUM,ERRDIE Assume I/O error                  @SC86355
         CLC   =X'0130',0(1) 013 abend?                        @TS86001
         BNE   DSKABX        No, assume worst                  @SC86355
         CLI   2(1),X'14'    Mismatch DSORG?                   @TS86001
         BNE   *+12          No                                @SC86355
         MVI   ERRNUM,ERRFNE Yes, member invalid or missing    @SC86355
         B     DSKABX                                          @SC86355
         CLI   2(1),X'18'    Unknown member name?              @TS86001
         BNE   DSKABX        No, assume worst                  @SC86355
         MVI   ERRNUM,ERRFNF Yes, say "not found"              @SC86355
DSKABX   MVI   3(1),X'04'    Ignore if possible                @SC86355
         BR    14            Return                            @TS86001
*
DSKOPC   LR    3,1                                             @SC86299
         LH    5,FABBLKSI                                      @SC86299
         LTR   5,5                                             @SC86299
         BP    *+8                                             @SC86299
         LH    5,=H'6233'                                      @SC86299
         LR    2,5                                             @SC86299
         TM    FABRECFM,FABRECU                                @SC86299
         BO    DSKOPS                                          @SC86299
         LH    2,FABLRECL                                      @SC86299
         BNZ   *+8                                             @SC86299
         OI    FABRECFM,FABRECV+FABRECBR                       @SC86299
         LTR   2,2                                             @SC86299
         BP    *+8                                             @SC86299
         LA    2,80                                            @SC86299
         TM    FABRECFM,FABRECF                                @SC86299
         BZ    DSKOPV                                          @SC86299
         SR    4,4                                             @SC86299
         DR    4,2                                             @SC86299
         MR    4,2                                             @SC86299
         B     DSKOPS                                          @SC86299
DSKOPV   LA    4,4(2)                                          @SC86299
         CR    4,5                                             @SC86299
         BNH   DSKOPS                                          @SC86299
         LR    5,4                                             @SC86299
DSKOPS   STH   2,FABLRECL                                      @SC86299
         STH   5,FABBLKSI                                      @SC86299
         BR    14                                              @SC86299
*
DSKFL    EQU   DSKSTT+FDBFLGS-FABD   Flags for operation       @SC86295
* Flags in addition to those defined for FDBFLGS:              @SC87015
NXDON    EQU   X'40'         Catalog search done               @SC87015
*
         DROP  6                                               @SC87015
*
         LOCALS ,                                              @SC86295
CAMLOC   DS    4F            Ptrs for locating dataset         @SC86299
CAMOBT   DS    4F            Ptrs for getting DSCB             @SC86299
CAMVOLS  DS    0D,XL265      Storage for volume list           @SC86299
CAMDSCB  DS    0F,XL101      Storage for DSCB                  @SC88014
         ORG   CAMDSCB+1                                       @SC88014
DS1VOL   DS    CL6,XL2       Volume serial                     @SC86299
DS1CRDT  DS    2XL3,3X,XL13  Creation date                     @SC86299
DS1RFDT  DS    XL3,XL4       Reference date                    @SC86299
DS1DSO   DS    XL2           Dataset org                       @SC86299
DS1RCF   DS    X             Record format                     @SC86299
DS1OPT   DS    X             Error option                      @SC86299
DS1BLK   DS    H             Block size                        @SC86299
DS1LRC   DS    H             Logical record length             @SC86299
         ORG   ,                                               @SC86299
DYNPL    DS    A(0,0,0,0,DYNDSP,0,DYNRC)                       @SC88026
DYNRC    DS    F                                               @SC86299
DYNDSP   DS    X                                               @SC86299
DSKMNTH  DS    XL11          Month length table                @SC86299
         EXIT
