*COPY                                                 RTEXT
         MACRO
&LABEL   RTEXT  &BUF,&PROMPT=,&E=
.* Read from the terminal, possible prompt.  Get length read in R0.
.*  &1: read buffer (len=130) (LA), &PROMPT(1)= prompt buf. if any
.*  (LA/R), &PROMPT(2)= prompt length (LA/R), &E= branch if error
         GBLC  &KSYS                                           @SC88036
&LABEL   DS    0H                                              @SC86299
         AIF   (T'&PROMPT EQ 'O').NOPR                         @SC87268
         TPUT  &PROMPT(1),&PROMPT(2),ASIS                      @SC87268
.NOPR    ANOP
         KCALL GETLIN,&BUF,E=&E                                @SC88036
         MEXIT                                                 @SC88036
.GUPI    TGET  &BUF,130                                        @SC88036
         LR    0,1           Copy length read                  @SC88036
         MEND
*COPY                                                 DMSFREE
         MACRO
&LABEL   DMSFREE &DWORDS=(0),&ERR=
.* Obtain free storage block: len=8*(R0).  Returns ptr in R1, but
.*    preserves registers 2-14
.*  &DWORDS= length in doublewords should be in R0,
.*  &ERR= branch if failure
&LABEL   LREG  0,&DWORDS                                       @SC86299
         SLA   0,3                                             @SC86299
         AIF   ('&ERR' NE '').COND                             @SC86345
         GETMAIN R,LV=(0)                                      @SC86299
         MEXIT
.COND    GETMAIN RC,LV=(0)                                     @SC86345
         LTR   15,15                                           @SC86345
         BNZ   &ERR                                            @SC86345
         MEND
*COPY                                                 DMSFRET
         MACRO
&LABEL   DMSFRET &DWORDS=(0),&LOC=(1),&ERR=
.* Return free storage block: len=8*(R0), adr=(R1).  Preserve R2-14.
.*  &DWORDS= length in doublewords should be in R0, &LOC= adr (in R1),
.*  &ERR= branch if failure
&LABEL   LREG  0,&DWORDS                                       @SC86299
         SLA   0,3                                             @SC86299
         FREEMAIN R,LV=(0),A=&LOC                              @SC86299
         MEND
*COPY                                                 WRITF
         MACRO
&LABEL   WRITF &TICK,&BUFFER=,&BSIZE=,&E=
.* Write to a disk file (ticket ptr in R1)
.*  &1: adr of file access ticket returned by OPENF (A),
.*  &BUFFER= data ptr (LA/R), &BSIZE= data length (LA/R) - if either is
.*  given, it replaces FDB value (see OPENF), &E= branch on error
&LABEL   READF &TICK,BUFFER=&BUFFER,BSIZE=&BSIZE,E=&E,CODE=10
         MEND
*COPY                                                 READF
         MACRO
&LABEL   READF &TICK,&BUFFER=,&BSIZE=,&E=,&CODE=9
.* Read from disk file (or write) (see WRITF)
         LCLC  &R                                              @SC86299
&LABEL   L     1,&TICK                                         @SC86299
         AIF   ('&BUFFER' EQ '').BZ                            @SC86299
         AIF   ('&BUFFER'(1,1) NE '(').BLA                     @SC86299
&R       SETC  '&BUFFER(1)'                                    @SC86299
         AGO   .BST                                            @SC86299
.BLA     LA    15,&BUFFER                                      @SC86299
&R       SETC  '15'                                            @SC86299
.BST     ST    &R,FDBBUFF-FABD(1)                              @SC86299
.BZ      AIF   ('&BSIZE' EQ '').SZ                             @SC86299
         AIF   ('&BSIZE'(1,1) NE '(').SLA                      @SC86299
&R       SETC  '&BSIZE(1)'                                     @SC86299
         AGO   .SST                                            @SC86299
.SLA     LA    15,&BSIZE                                       @SC86299
&R       SETC  '15'                                            @SC86299
.SST     ST    &R,FDBBSIZ-FABD(1)                              @SC86299
.SZ      LA    0,&CODE                                         @SC86299
         KCALL DISKIO,E=&E                                     @SC86299
         MEND
*COPY                                                 KSETKW
         MACRO
         KSETKW ,                                              @SC87166
.* Define system-specific SET/SHOW parameters (keywords)
         KW    'PREFIX',SHODST,MIN=3                           @SC87166
         KW    'TIMER',SHOTIM,MIN=2                            @SC87166
         MEND
*COPY                                                 KSETPRC
         MACRO
         KSETPRC
.* System-specific SET handlers (in any order).  No operands.
         PUSH  PRINT                                           @SC86355
         PRINT GEN                                             @SC86355
SETDST   KCALL CWDSET                                          @SC86164
         B     RTRN          Preserve return code              @SC86295
         POP   PRINT                                           @SC86355
         MEND
*COPY                                                 KSHOPRC
         MACRO
         KSHOPRC
.* System-specific SHOW handlers (in same order as KW).  No operands.
         PUSH  PRINT                                           @SC86355
         PRINT GEN                                             @SC86355
SHODST   LA    8,DEST                                          @SC86316
         LH    9,DESTL       Get length                        @SC86316
         BAL   14,SHOCHRN                                      @SC86295
          B    SETDST                                          @SC87166
SHOTIM   BAL   14,SHOOO                                        @SC87166
          OI   FL5,TIMF      Timer flag                        @SC87166
         POP   PRINT                                           @SC86355
         MEND
*COPY                                                 KFILKW
         MACRO
         KFILKW ,                                              @SC87166
.* Define system-specific file attribute parameters (keywords)
         KW    'BLKSIZE',SHOBSZ,MIN=2                          @SC87166
         KW    'RECFM',SHORFM                                  @SC87166
         KW    'SPACE',SHOTRK,MIN=2                            @SC87166
         KW    'UNIT',SHOUNT                                   @SC87166
         KW    'VOLUME',SHOVOL,MIN=2                           @SC87166
         MEND
*COPY                                                 KFILSET
         MACRO
         KFILSET
.* Specific SET FILE handlers (any order).  No operands.
         PUSH  PRINT                                           @SC87012
         PRINT GEN                                             @SC87012
SETRECVF MVC   RFM,0(6)      Copy RECFM                        @SC87012
         B     RTRN0                                           @SC87012
*                                                              @SC87012
SETRFM   BAL   4,SETSCN                                        @SC87012
         KW    'FIXED',SETRECVF                                @SC87012
         KW    'VARIABLE',SETRECVF                             @SC87012
         KW    'UNDEFINED',SETRECVF                            @SC86295
         KW    ,                                               @SC87012
*
SETUNT   BAL   2,SETFSTR     Get fixed-format string           @SC86316
         TR    FILUNT,UPCASE Should always be upper case       @SC88020
         MVC   LOGUNT,FILUNT                                   @SC86316
         B     RTRN0                                           @SC86316
*
SETVOL   BAL   2,SETFSTR     Get fixed-format string           @SC86295
         TR    FILVOL,UPCASE Should always be upper case       @SC88020
         MVC   LOGVOL,FILVOL                                   @SC86316
         B     RTRN0                                           @SC86295
         POP   PRINT                                           @SC87012
         MEND
*COPY                                                 KFILSHO
         MACRO
         KFILSHO
.* Specific SHOW FILE handlers (same order as KW).  No operands.
         PUSH  PRINT                                           @SC87012
         PRINT GEN                                             @SC87012
SHOBSZ   L     8,MAXBSZ      Limit                             @SC87166
         LH    4,FILBLKSI                                      @SC87320
         BAL   14,SHONUM     Print it                          @SC86295
          B    RTRN0                                           @SC86295
SHORFM   LA    8,RFM                                           @SC87012
         BAL   14,SHOCHR                                       @SC87012
          B    SETRFM                                          @SC87166
SHOTRK   L     8,MAXBSZ      Limit                             @SC87166
         L     4,FILTRKAL                                      @SC88026
         BAL   14,SHONUM     Print it                          @SC86295
          B    RTRN0                                           @SC87166
SHOUNT   LA    8,FILUNT                                        @SC86316
         LA    9,8                                             @SC86316
         BAL   14,SHOCHRN                                      @SC86316
          B    SETUNT                                          @SC87166
SHOVOL   LA    8,FILVOL                                        @SC86295
         LA    9,6                                             @SC86295
         BAL   14,SHOCHRN                                      @SC86295
          B    SETVOL                                          @SC87166
         POP   PRINT                                           @SC87012
         MEND
*COPY                                                 WTEXT
         MACRO
&LABEL   WTEXT &ARG,&LEN
.* Display some text, e.g., WTEXT 'Hi there' or WTEXT (3),(4)
.* Preserves R2-R14
.*  &1: 'text' (where text has no doubled ' or & characters)  OR
.*  &1: adr of text (LA/R), &2: length of text (LA/R)
         GBLC  &KSYS                                           @SC88036
&LABEL   PTEXT &ARG,&LEN,AREG=1,LREG=0                         @SC86295
.*
         BAL   15,WTEXT      'TPUT'                            @SC87020
         MEXIT                                                 @SC88036
.GUPI    SVC   93            'TPUT'                            @SC88036
         MEND
*COPY                                                 FDBD
         MACRO
         FDBD
.* Map of File Descriptor Block + File Access Block
FABD     DSECT ,                                               @SC86295
FABRELAD DS    17X           FAB maps DCB                      @SC86299
FABREL   DS    AL3                                             @SC86299
FABBUFNO DS    0BL1                                            @SC86299
FABBUFCB DS    A                                               @SC86299
FABBUFL  DS    H                                               @SC86299
FABDSORG DS    BL2                                             @SC86299
FABIOBAD DS    A                                               @SC86299
FABEODAD DS    A                                               @SC86299
FABRECFM DS    0BL1                                            @SC86299
FABRECU  EQU   X'C0'         Undefined-length records          @SC86299
FABRECF  EQU   X'80'         Fixed-length records              @SC86299
FABRECV  EQU   X'40'         Varying-length records            @SC86299
FABRECBR EQU   X'10'         Blocked records                   @SC86299
FABEXLST DS    A                                               @SC86299
FABDDNAM DS    CL8                                             @SC86299
FABOFLGS DS    BL1                                             @SC86299
FABIFLG  DS    BL1                                             @SC86299
FABMACR  DS    BL2                                             @SC86299
         ORG   FABDDNAM                                        @SC86299
FABTIOT  DS    BL2                                             @SC86299
FABMACRF DS    BL2                                             @SC86299
FABDEBAD DS    A                                               @SC86299
FABGET   DS    A                                               @SC86299
FABCHECK DS    A                                               @SC86299
FABSYNAD DS    A                                               @SC86299
FABCIND1 DS    BL2                                             @SC86299
FABBLKSI DS    H                                               @SC86299
FABWCPO  DS    BL4                                             @SC86299
FABIOBA  DS    A                                               @SC86299
FABEOBAD DS    A                                               @SC86299
FABRECAD DS    A                                               @SC86299
FABDIRCT DS    H                                               @SC86299
FABLRECL DS    H                                               @SC86299
FABCNTRL DS    A                                               @SC86299
         ORG   FABD+90                                         @SC86299
FABPRECL DS    AL2                                             @SC86299
FABEOB   DS    A                                               @SC86299
FDBD     DS    0F            Beginning of short descriptor     @SC86295
FDBBUFF  DS    A             Buffer ptr                        @SC86295
FDBBSIZ  DS    F             Max record length                 @SC86295
FDBRCF   DS    C             Record format                     @SC86295
FDBFLGS  DS    X             Flags                             @SC86295
FDBACTV  EQU   X'80'         File is already open              @SC86295
* APPN   EQU   X'10'         DISP=MOD                          @SC86295
WFN      EQU   X'02'         Filename contains wild chars
PDSF     EQU   X'01'         Dataset is a PDS                  @SC87015
FDBLRC   DS    H             File record length                @SC86295
FDBTRKAL DS    F             File track allocation increment   @SC88026
FDBBLKSI DS    H             File block size                   @SC86295
FDBVOL   DS    CL6           File volume label                 @SC86295
FDBUNT   DS    CL8           File unit name                    @SC86299
FDBCOP   EQU   *-FDBD        Length to copy for OPEN           @SC86295
FDBSIZE  DS    F             File size in Kbytes               @SC86299
FDBDATE  DS    F             Creation date: packed yyyymmdd    @SC86295
FABLDSN  DS    H             Name length                       @SC87351
FABDSN   DS    CL52          Dataset name                      @SC86299
FDBINFO  EQU   *-FDBD        Length of info returned           @SC86295
FABCOMM  DS    CL8           Command name                      @SC87351
FABDWDS  EQU   (*-FABD+7)/8                                    @SC86295
         MEND
*COPY                                                 FDBPAT
         MACRO
         FDBPAT &N,&RFM,&SIZ                                   @SC88120
.* Define system-dependent part of output FDB patterns
.*  &1: variable-name prefix (or null if defining init. values)
.*  &2: RECFM (1-char), &3: LRECL (skip rest of FDB if omitted)@SC88120
         LCLC  &T,&B,&V,&U,&Z                                  @SC87320
         LCLC  &R,&F,&L,&P4                                    @SC88120
         AIF   ('&N' EQ '').ALC                                @SC86316
&R       SETC  'RCF'                                           @SC88120
&F       SETC  'FLGS'                                          @SC88120
&L       SETC  'LRC'                                           @SC88120
&T       SETC  'TRKAL'                                         @SC87320
&B       SETC  'BLKSI'                                         @SC87320
&V       SETC  'VOL'                                           @SC86316
&U       SETC  'UNT'                                           @SC86316
.ALC     ANOP                                                  @SC86316
&N&R     DC    C'&RFM'       RECFM                             @SC88120
&N&F     DC    X'00'         Flags                             @SC88120
         AIF   ('&SIZ' EQ '').DONE                             @SC88120
         AIF   ('&RFM' NE 'V').OKL                             @SC88120
&P4      SETC  '+4'          Add 4 for RDW                     @SC88120
.OKL     ANOP                                                  @SC88120
&N&L     DC    Y(&SIZ&P4)    LRECL                             @SC88120
&N&T     DC    F'5'          Track allocation                  @SC88026
&Z       SETC  '6233'        Default block size                @SC87320
         AIF   ('&SIZ' NE 'LPKT').P1                           @SC88120
&Z       SETC  'LPKT+8'      Block size for log file           @SC87320
.P1      ANOP
&N&B     DC    Y(&Z)                                           @SC87320
&N&V     DC    CL6' '        No volume specified               @SC87320
&N&U     DC    CL8'SYSDA'    Default unit                      @SC87320
.DONE    ANOP                                                  @SC88120
         MEND
*COPY                                                 KSYSVAR
         MACRO
         KSYSVAR
.* Define system-dependent globally-known variables
COMPTR   DS    2A            Common/storage ptrs               @SC87015
APGPB    GETLINE MF=L        Parameter block for GETLINE       @NW86330
STAXPL   STAX  0,MF=L        Parameter block for STAX          @NW86330
STAXPLR  STAX  0,MF=L        Parameter block for STAX (replace)@NW86330
IOPLAREA DS    4F            IOPL                              @TS86001
CPECB    DS    F             GETLINE/PUTLINE/PUTGET ECB        @TS86001
ECBREAD  DS    F                                               @NW86330
TASKADD  DS    A             Async task adr                    @NW86330
ECBTGET  DS    F                                               @NW86330
KTGETT   DS    2F            Adr and length of read request    @SC87015
PUTLINAD DS    A             Adr of PUTLINE routine            @SC88026
GETLINAD DS    A             Adr of GETLINE routine            @NW86330
DAPLAREA DS    5F            DAIR plist block                  @SC87351
DA00     DS    F             Parameter block                   @SC87351
DA00PDSN DS    A,CL8,4X      DSN ptr                           @SC87351
CATDSPTR DS    A             Catalog buffer ptr                @NW86330
NXSFPTR  DS    A             Ptr to suffix comparand           @SC87015
CIRPARM  DS    0F                                              @NW86330
CIROPT   DS    X'02'         Get all matches                   @NW86330
         DS    2AL1(0)       Reserved by system                @NW86330
CIRLOCRC DS    AL1(0)        Locate return code                @NW86330
CIRSRCH  DS    A             Search arg: adr of test DSN       @NW86330
CIRCVOL  DS    F'0'          Vol adr=0 - force cat lookup      @NW86330
CIRWA    DS    A             Ptr to user work area             @NW86330
CIRSAVE  DS    A             Ptr to save area for macro        @NW86330
CIRPSWD  DS    F'0'          Adr of password                   @NW86330
DEST     DS    CL44          Default PREFIX                    @SC86299
DESTP    DS    C' '          PDS indicator ('.' if so)         @SC86299
DESTL    DS    H'0'          Length                            @SC86299
         DS    0F                                              @SC86299
DSKSTT   EQU   *-FDBD+FABD                                     @SC86299
         DS    XL(FDBINFO)   Room for FDB                      @SC86299
NXFN     DS    CL(LFID)      Pattern filespec for search       @SC87015
DSNPFL   DS    H             Prefix length for search          @NW86330
DSNSFL   DS    H             Suffix length for search          @NW86330
ICPRGS   DS    4F            Saved registers for type-out      @SC88026
ICPFL    DS    X             Flag for type-out interception    @SC87020
STMUCH   DS    XL2           Saved user profile values         @SC86299
PTLLEN   DS    0F,2H         PUTLINE data descriptor           @SC88026
PTLBUF   DS    CL133                                           @SC88026
STKDSN   DS    CL(LFID)      DSN for STACK                     @SC88026
         MEND
*COPY                                                 KSYSTF
         MACRO
         KSYSTF
.* Define system-dependent globally-known constants and init. variables
.*  symb .DS + label &P.DEFS mark start of variables/init. values
         LCLC  &P
         PUSH  PRINT
         PRINT GEN
         AIF   ('&SYSECT' EQ 'STORAG').DS
&P       SETC  'I'           For initial values
WTEXT    STM   14,1,ICPRGS   Save                              @SC88026
         CLI   ICPFL,2       Intercepting?                     @SC88026
         BE    WTXICP        Yes, do it                        @SC88026
         MVC   PTLBUF,0(1)   Copy to buffer                    @SC88026
         A     0,F4                                            @SC88026
         STH   0,PTLLEN      And save length                   @SC88026
         L     15,PUTLINAD                                     @SC88026
         PUTLINE PARM=PTPB,MF=(E,IOPLAREA),ENTRY=(15)          @SC88026
         B     WTXRET                                          @SC87020
WTXICP   KCALL ICPTYP        Call interception routine         @SC87020
WTXRET   LM    14,1,ICPRGS   Restore                           @SC88026
         BR    15                                              @SC87020
SYSATR   DC    C'."I2'       System type for A-packet          @SC86299
LOGNAM   DC    C'KER.LOG'                                      @SC86299
REPNAM   DC    C'KER.REPLY'                                    @SC86299
SYSTAKE  DC    C'''SYS1.KERMINI'''                             @SC86299
LSYST    EQU   *-SYSTAKE                                       @SC86299
USRTAKE  DC    C'KERMINI'    Init file                         @SC86299
LUSRT    EQU   *-USRTAKE                                       @SC86299
         ADCON GETLIN,ICPTYP Various routines                  @SC87020
CIRWAL   DC    H'32004,0'    Length of catalog work area       @SC87015
.DS      ANOP
&P.DEFS  DS    0D
&P.KPRPL DC    AL1(L'&P.KPRPT)                                 @SC87268
&P.KPRPT DC    C'Kermit-TSO>'                                  @SC87268
         ORG   &P.KPRPT+20                                     @SC87268
         POP   PRINT
&P.PTPB  PUTLINE MF=L,OUTPUT=(0,TERM,SINGLE,DATA)              @SC88026
         MEND
*COPY                                                 KSYSBUF
         MACRO
         KSYSBUF
.* Store buffer ptrs from R1 and increment R1 for specific buffers
         LA    0,4-1                                           @SC87015
         AR    1,0                                             @SC87015
         OR    1,0                                             @SC87015
         XR    1,0                                             @SC87015
         ST    1,CIRSAVE     Catalog scan save area            @SC87015
         LA    1,72(1)                                         @SC87015
         ST    1,CIRWA       Catalog info buffer               @SC87015
         AH    1,CIRWAL                                        @SC87015
         ST    1,CIRSRCH     Catalog search comparand          @SC87015
         LA    1,44(1)                                         @SC87015
         MEND
*COPY                                                 SSYMS
         MACRO
         SSYMS
.* Set global symbols for conditional assembly
         GBLC  &KVRSN,&KSYS,&KDATE,&RTN,&TYPCMD,&S1CMD         @SC86355
         GBLA  &MAXLR,&MAXBS                                   @SC86268
&KSYS    SETC  'TSO'         System name                       @SC86299
         MNOTE '*** Kermit-&KSYS release &KVRSN (&KDATE) ***'
&MAXLR   SETA  32756         Max lrecl                         @SC86299
&MAXBS   SETA  32760         Max blksize                       @SC86299
&S1CMD   SETC  'F1F0'        S/1 order                         @SC86299
         PUSH  PRINT
         PRINT GEN
MAXWT    EQU   1024          Max TPUT buffer                   @SC86299
MAXRT    EQU   1024          Max TGET buffer                   @SC86299
LFID     EQU   52            Max length of filespec            @SC86299
&TYPCMD  SETC  'LIST'        Host command for TYPE             @SC86299
TYPMIN   EQU   4             Min abbrv of system TYPE cmd or 2 @SC86299
KMAXE    EQU   1920-7        < 9025  Kermit extended max pkt   @SC87351
STKDWDS  EQU   511           Size of save-area stack           @SC87012
         POP   PRINT
         MEND                                                  @SC86268
*COPY                                                 SYSMACS
         MACRO
         SYSMACS
.* Include system control block definition macros and list all macros
 MNOTE '---MACROs: ATTACH, CLOSE, DETACH, FIND, FREEMAIN,'
 MNOTE '---        FREEPOOL, GETLINE, GETMAIN, GTSIZE, IKJCPPL,'
 MNOTE '---        IKJCVT, IKJECT, IKJGTPB, IKJIOPL,'
 MNOTE '---        IKJPSCB, IKJUPT, LOCATE, OBTAIN, OPEN, POST,'
 MNOTE '---        SAVE, STAX, STIMER, TPUT, TGET, TTIMER, WAIT'
         IKJCPPL ,                                             @SC86299
*        CVT ,                                                 @SC88036
         IKJDAPL ,                                             @SC87351
         IKJECT ,                                              @SC86299
         IKJGTPB ,                                             @NW86330
         IKJIOPL ,                                             @TS86001
         IKJPSCB ,                                             @SC86299
         IKJUPT ,                                              @SC86299
* DSECT for addressing catalog information work area
CATDSET  DSECT                                                 @NW86330
TYPEBYTE DS    XL1           Type byte we want only A's        @NW86330
CATDNAME DS    44CL1         Data set name                     @NW86330
         MEND                                                  @SC86268
*COPY                                                 STRTMSGS
         MACRO
&LABEL   STRTMSGS
.* Print system-dependent start-up messages
&LABEL   CLI   S1HND,XON                                       @SC87338
         BNE   STRT1Z                                          @SC87338
         CLI   TRMTP,C'T'                                      @SC87338
         BE    STRT1Z                                          @SC87338
         WTEXT 'Handshake is XON -- not needed'                @SC87338
STRT1Z   DS    0H                                              @SC87338
         MEND                                                  @SC87338
