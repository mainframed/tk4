*COPY                                                 IK0MAI
         TITLE 'Kermit-370 main code'                          @SC86268
KERMIT   ENTER MAIN                                            @SC86295
         STM   0,1,ORGR0     Save R0,R1                        @SC87253
* Initialize STORAG section
         OI    FL5,TIMF      Set timer on                      @SC86355
         MVC   DEFS(LDEFS),IDEFS Init. values                  @SC86295
         MVC   S1DATA,S1XON  Series/1 orders                   @SC86295
         LA    1,S1XON                                         @SC87274
         ST    1,S1XOPL      Set up prompt plist               @SC87274
         MVI   TRTBL+C' ',1  Set up blank finder               @SC86295
         MVI   TRMTP,C'T'    Assume going via TTY line         @SC87166
         MVC   ETOA,ETOAD    Init translators                  @SC86295
         MVC   ATOE,ATOED                                      @SC86295
         MVC   TETOA,ETOAD                                     @SC87117
         MVC   TATOE,ATOED                                     @SC87117
*        MVC   OFF80(128),LOCASE                               @SC87253
*        MVC   OFF80+128(128),LOCASE                           @SC87253
         LA    1,255                                           @SC86295
         STC   1,UPCASE(1)                                     @SC86295
         BCT   1,*-4                                           @SC86295
         MVC   UPCASE+C'a'(9),ATOE+AA                          @SC86295
         MVC   UPCASE+C'j'(9),ATOE+AJ                          @SC86295
         MVC   UPCASE+C's'(8),ATOE+AS                          @SC86295
         LA    1,CMD                                           @SC86295
         ST    1,TAKBUF                                        @SC86295
         SR    1,1                                             @SC86295
         LA    5,4095        For page boundaries...            @SC87281
         BAL   14,STPTRS     Get length of storage needed      @SC86295
         LA    0,7(1,5)      Round up                          @SC87281
         SRA   0,3                                             @SC86295
         ST    0,FREEDW      Number of dw's to get             @SC86295
       DMSFREE DWORDS=(0),ERR=KRMXITS                          @SC86295
         ST    1,FREEPTR     Save start of area                @SC87286
         BAL   9,KRMPG       Round up to next page             @SC87281
         BAL   14,STPTRS     Set up ptrs                       @SC86295
         LH    1,BUFSIZ                                        @SC86295
         ST    1,TYRDPL+4    Set up read lists                 @SC86295
         A     1,F3                                            @SC86295
         ST    1,S1RDPL+4                                      @SC86295
         L     1,LOGBUF                                        @SC86295
         MVI   1(1),C':'     Set up prefix                     @SC86190
         KCALL SETMSG,1,E=KRMXITF Get user's settings
         KCALL SPARSET                                         @SC86152
         MVC   DATL,F0                                         @SC87012
         KCALL SPAR          Set defaults for other Kermit     @SC87012
         KCALL USNTRF        Call main loop                    @SC87117
*
KRMXITF  KCALL SETMSG,4      System-dependent clean-up         @SC86316
         LM    0,1,FREEDW    Number of dw's, ptr to area       @SC87286
       DMSFRET DWORDS=(0),LOC=(1)                              @SC86295
         TM    FL1,DEBUG                                       @SC86295
         BZ    KRMXITQQ                                        @SC86295
         CLOSF LOGPTR        Close                             @SC86295
KRMXITQQ BAL   14,LDERR      Get error code                    @SC87117
         RET   MAIN                                            @SC86295
*
KRMXITS  WTEXT 'Unable to allocate buffers'
         B     KRMXITQQ
*
STPTRS   ST    1,RBUF        Addr of first buffer
         A     1,F64KP               Second buffer
         ST    1,WBUF                Away from first
         A     1,F64KP
         ST    1,TSENT               address of sent files
         AH    1,=Y(MAXNSENT*LFID) Length of table             @SC86190
         ST    1,CBUF        Ptr to CP answer buffer           @SC86295
         A     1,F256                                          @SC86295
         ST    1,EMSGP       Ptr to saved micro error msg      @SC86295
         LA    1,LEMSG(1)                                      @SC86295
         ST    1,LOGBUF      Ptr to LOG buffer                 @SC86295
         LA    1,2(1)        Data record overlays S/1 orders   @SC86190
         LA    2,S1ORDL(1)                                     @SC87281
         BAL   9,KRMPGTB     Round up if necessary             @SC87281
         ST    1,S1WRPL                                        @SC86295
         LA    1,S1ORDL(1)   Ptr to output data                @SC86190
         ST    1,TYWRPL                                        @SC86190
         AH    1,BUFSIZ      Ptr to input buffer               @SC86190
         LA    2,3(1)                                          @SC87281
         BAL   9,KRMPGTB     Round up if necessary             @SC87281
         LR    2,1                                             @SC86190
         LA    1,3(1)        Allow S/1 screen info             @SC87281
         ST    2,S1RDPL                                        @SC86295
         ST    1,APKT                                          @SC86190
         ST    1,TYRDPL      Read plist                        @SC86295
         AH    1,BUFSIZ      Ptr to output holding buffer      @SC86190
         ST    1,ASPKT                                         @SC86190
         ST    1,AASPKT                                        @SC86190
         LA    2,7(1)        Skip over extended header         @SC86190
         ST    2,ASDATA                                        @SC86190
         AH    1,BUFSIZ      Ptr to input decoding buffer      @SC86190
         ST    1,ARPKT                                         @SC86190
         AH    1,BUFSIZ      Ptr to take table                 @SC86190
         KSYSBUF ,           Other buffers                     @SC86355
         BR    14            Return with R1 -> end             @SC86295
*
* Round up R1 to next page if (R1)-(R2) would cross boundary.  @SC87281
* (R5) = page size - 1 (should be 4095), return to (R9)        @SC87281
KRMPGTB  AH    2,BUFSIZ      Tentative end of buffer           @SC87281
KRMPGT   BCTR  2,0           Last byte                         @SC87281
         XR    2,1           Same page as first?               @SC87281
         NR    2,5                                             @SC87281
         BZR   9             Same page, ok                     @SC87281
KRMPG    AR    1,5           Round up to next page             @SC87281
         OR    1,5                                             @SC87281
         XR    1,5                                             @SC87281
         BR    9
         LOCALS ,                                              @SC86295
KERMIT   EXIT
