DYNA     TITLE 'DYNAMIC FILE ALLOCATION ROUTINE'
***********************************************************************
* DYNALC - J.F. Chandler - 1986 October                               *
* TSO FORTRAN-callable routine based on version from KERMSRV          *
*  e.g., CALL DYNALC(DSN,DDN,UNIT,VOL,DISP,SPACE,RC)                  *
*    or  CALL DYNALC(DSN,DDN,UNIT,VOL,DISP,SPACE,RC,BUFF)             *
*  with  DSN   52-char string of DSN + MEMBER (blank if none)         *
*        DDN   8-char string of DDNAME or FORTRAN unit number         *
*        UNIT  8-char string of device type                           *
*        VOL   6-char string of volume name                           *
*        DISP  1-byte code giving dataset disposition:                *
*              80: SHR       08: KEEP       One bit must be set       *
*              40: NEW  +    04: DELETE     in each HEX digit.        *
*              20: MOD       02: CATLG                                *
*              10: OLD       01: UNCATLG                              *
*        SPACE fullword track allocation increment                    *
*        RC    fullword returned completion (0 if ok, 1 if not)       *
*        BUFF  (optional) 512-byte buffer for returned error message. *
*              If not given, in case of error, display the message.   *
***********************************************************************
DYNALC   CSECT
         PRINT NOGEN
         SAVE  (14,12),,*
         USING DYNALC,15
         CNOP  0,4
         BAL   12,*+76
         USING *,13
         DS    18F
         ST    12,8(13)
         ST    13,4(12)
         LR    13,12
         LM    4,11,0(1)     Get arguments                     @SC88119
         TM    0(4),X'F0'
         BNM   EXITBAD       Must be old
         LR    1,4           Dsname ptr
         LA    0,44
         LA    3,TUDSN+2
         BAL   14,GETTU
         LA    1,44(4)       Possible member name
         LA    0,8           Max length
         LA    3,TUMEM+2
         BAL   14,GETTU
         LR    1,5           Ddname ptr
         TM    0(1),X'F0'
         BNZ   DDCHAR        Must be char string
         L     0,0(1)        Numeric, get value
         CVD   0,DBLWORD
         OI    DBLWORD+7,15
         LA    1,FTXXF001
         UNPK  2(2,1),DBLWORD Convert to zoned
DDCHAR   LA    0,8           Max length
         LA    3,TUDDN+2
         BAL   14,GETTU
         SR    0,0
         IC    0,0(8)        Get stat,disp
         SRDL  0,4           Separate nybbles
         SRL   1,28
         STC   0,TUSTAT      Save values
         STC   1,TUDISP
         LR    1,6           Unit ptr
         LA    0,8           Max length
         LA    3,TUUNT+2
         BAL   14,GETTU
         LR    1,7           Volume ptr
         LA    0,6           Max length
         LA    3,TUVOL+2
         BAL   14,GETTU
         L     2,0(9)        Space value
         STCM  2,7,TUPRIME   Use for both
         STCM  2,7,TUSECOND
         LA    1,TEXTTST
         MVC   0(12,1),=A(TUUNT,TUVOL,TUMEM)
         LA    3,3
TSTSLP   L     2,0(1)
         CLI   5(2),0        Is is specified?
         BNE   *+10          Yes, keep it
         XC    0(4,1),0(1)   No, exclude it from list
         LA    1,4(1)        On to next
         BCT   3,TSTSLP
         LA    1,TEXTOLD     Determine which units to use
         TM    TUSTAT,X'04'
         BZ    DYNALLOC
         LA    1,TEXTNEW
         CLI   TUMEM+5,0     Any member given?
         BE    DYNALLOC      No, that's fine
         LA    1,TEXTNEWM    Yes, must allocate directory
DYNALLOC ST    1,DYNTXTPP
         LA    1,DYNRBPTR
         DYNALLOC ,
         LTR   15,15
         BZ    EXITRC
         NI    DFSWTCHS,X'9F'                                  @SC88119
         LTR   10,10         Is there a message buffer?        @SC88119
         BM    *+8           No                                @SC88119
         OI    DFSWTCHS,X'40' Yes, set flag for filling it     @SC88119
         STCM  11,7,DFBUFP+1 Pass pointer                      @SC88119
DYNFAIL  ST    15,S99RC
         LA    1,DFPARMS
         LINK  EP=IKJEFF18
EXITBAD  LA    15,1
EXITRC   ST    15,0(10)      Save RC
         L     13,4(13)
         RETURN (14,12)
*
* Copy string+length into text unit. R1->string, R3->length field
GETTU    LR    2,1           Save start of string
GLLP     CLI   0(2),C' '     Find end
         BE    GOTLEN
         LA    2,1(2)
         BCT   0,GLLP
GOTLEN   SR    2,1           Length of token
         STCM  2,3,2(3)      Save in text unit
         BZR   14            Empty string
         BCTR  2,0           Fix for execute
         EX    2,COPYTU
         BR    14
COPYTU   MVC   4(,3),0(1)    Move string to text unit
         EJECT
         DS    0F
DYNRBPTR DC    X'80',AL3(DYNRB)
DYNRB    DC    AL1(20,S99VRBAL)
         DC    AL2(0,0,0)
DYNTXTPP DC    AL4(*-*)
         DC    AL4(0,0)
S99RC    DC    F'0'
TEXTNEWM DC    A(TUDIR)
TEXTNEW  DC    A(TUTRK,TUPRI,TUSEC,TUREL)
TEXTTST  DC    A(TUUNT,TUVOL)
TEXTOLD  DC    A(TUMEM,TUDDN,TUDSN,TUSTA,TUDIS),X'80',AL3(TUFRE)
*
TUDDN    DC    AL2(DALDDNAM,1)   DDNAME
         DS    AL2,CL8
TUDSN    DC    AL2(DALDSNAM,1)   DSNAME
         DS    AL2,CL44
TUMEM    DC    AL2(DALMEMBR,1)   Member
         DS    AL2,CL8
TUDIR    DC    AL2(DALDIR,1)     Dir blks
         DC    AL2(3),AL3(5)
TUDIS    DC    AL2(DALNDISP,1,1) Disp
TUDISP   DC    X'00'
TUSTA    DC    AL2(DALSTATS,1,1) Status
TUSTAT   DC    X'00'
TUUNT    DC    AL2(DALUNIT,1)    Unit
         DS    AL2,CL8
TUVOL    DC    AL2(DALVLSER,1)   Volume
         DS    AL2,CL6
TUTRK    DC    AL2(DALTRK,0)     Tracks
TUPRI    DC    AL2(DALPRIME,1,3) Primary
TUPRIME  DC    AL3(*-*)
TUSEC    DC    AL2(DALSECND,1,3) Secondary
TUSECOND DC    AL3(*-*)
TUREL    DC    AL2(DALRLSE,0)    Release
TUFRE    DC    AL2(DALCLOSE,0)   FREE=CLOSE
DFPARMS  DS    0D            DAIR fail plist
DFS99RBP DC    A(DYNRB)      Adr of SVC 99 req blk
DFRCP    DC    A(S99RC)      Adr of SVC 99 ret code
DFJEFF02 DC    A(DFZEROES)   Adr of unknown writer
DFIDP    DC    A(DFSWTCHS)   Adr of DAIRFAIL options
DFCPPLP  DC    A(0)          Unknown CPPL address
DFBUFP   DC    A(0)          Do not return message
DFZEROES DC    A(0)
DFSWTCHS DC    X'80',X'33'   WTP for DYNALLOC, please
DBLWORD  DC    D'0'
FTXXF001 DC    C'FTXXF001'   Place to build FORTRAN ddname
         IEFZB4D0
         IEFZB4D2
         END
