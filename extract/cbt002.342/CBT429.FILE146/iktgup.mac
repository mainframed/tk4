*COPY                                                 GUPVAR
*          Specific variables
EVCTR    DS    F             File sequence number                   TSO
ICPRGS   DS    4F            Saved registers for type-out      @SC88026
FNAME    DS    CL130         Buffer for reading                     TSO
*COPY                                                 GUPSPC
*          Specific preliminaries
*
LFID     EQU   52            Filespec length                        TSO
STKDWDS  EQU   511           Requested stack length                 TSO
XXBAT    EQU   X'04'         Special flag for batch mode       GUP1.1
*
         IKJCPPL ,                                             GUP1.1
*COPY                                                 GUPFIN
         LR    2,15          Save return code                  GUP1.1
         CLOSE MSGFIL                                          GUP1.1
         LR    15,2          Return code                       GUP1.1
*COPY                                                 GUPNIT
*          TSO user interface                                       TSO
*
         TM    0(1),X'80'    What kind of plist?               GUP1.1
         BZ    GUPCP         Seems to be CP                    GUP1.1
         MVC   SRCNAM(3*LFID+3),BATDDNS  Copy ddnames+mark     GUP1.1
         LA    4,XXCOR+XX8+XXBAT Default flags                 GUP1.1
         L     1,0(1)        Ptr to parm string                GUP1.1
         LH    2,0(1)        Get length                        GUP1.1
OPTLP    SR    0,0           Mask: zeroes                      GUP1.1
         CH    2,EH2         Enough for a 'NO'?                GUP1.1
         BL    OPTZ          No                                GUP1.1
         CLC   =C'NO',2(1)   Is it?                            GUP1.1
         BNE   OPTYES        No, assume positive option        GUP1.1
EH2      EQU   *+2,2                                           GUP1.1
         LA    1,2(1)        Yes, it is.  Space over the NO    GUP1.1
         SH    2,EH2         Cut off the NO                    GUP1.1
         BCTR  0,0           Mask: ones                        GUP1.1
OPTYES   SH    2,EH4         See if room for option            GUP1.1
         BL    OPTZ          No, done scan                     GUP1.1
         CLC   =C'MARK=',2(1)                                  GUP1.1
         BNE   OPTCK         Check flags                       GUP1.1
         SH    2,EH4         See if mark field available       GUP1.1
         BL    OPTZ          No, done scan                     GUP1.1
         MVC   MRKD,7(1)     Copy in case NOSEQ8               GUP1.1
         LA    1,8(1)        Space over option                 GUP1.1
         B     OPTLQ                                           GUP1.1
OPTCK    LA    3,XX8         Test for SEQ8                     GUP1.1
         CLC   =C'SEQ8',2(1)                                   GUP1.1
         BE    OPTOK         Found it                          GUP1.1
         LA    3,XXCOR       Test for in-storage               GUP1.1
         CLC   =C'STOR',2(1)                                   GUP1.1
         BNE   OPTZ          None of these, give up            GUP1.1
OPTOK    OR    4,3           Turn flag on                      GUP1.1
         NR    3,0                                             GUP1.1
         XR    4,3           Turn off if "NO"                  GUP1.1
         LA    1,4(1)        Advance ptr over option           GUP1.1
OPTLQ    LTR   2,2           Any more options?                 GUP1.1
         BNP   OPTZ                                            GUP1.1
         CLI   2(1),C','     Make sure there is a separator    GUP1.1
         BNE   OPTZ          No, give up                       GUP1.1
         LA    1,1(1)                                          GUP1.1
         BCT   2,OPTLP                                         GUP1.1
OPTZ     STC   4,FLG         Save current flags                GUP1.1
         OPEN  (MSGFIL,OUTPUT)  Message data set               GUP1.1
         TM    MSGFIL+FABOFLGS-FABD,X'10'                      GUP1.1
         BZ    ERREX         Oops                              GUP1.1
         B     OPN                                             GUP1.1
*
         USING CPPL,1                                          @SC86299
GUPCP    MVI   SRCNAM,C' '                                     GUP1.1
         MVC   SRCNAM+1(3*LFID+2),SRCNAM  Blank out parm area  GUP1.1
         MVI   FLG,0                                           GUP1.1
         L     3,CPPLUPT     Fill in parse parameter list      GUP1.1
         L     4,CPPLECT                                       GUP1.1
         STM   3,4,PPLAREA                                     GUP1.1
         L     3,CPPLCBUF                                      GUP1.1
         ST    3,PPLAREA+20                                    GUP1.1
         DROP  1                                               GUP1.1
         MVI   CPECB,0                                         GUP1.1
         LINK  EP=IKJPARS,MF=(E,PPLAREA) Perform parsing serviceUP1.1
         LTR   15,15         Any good?                         GUP1.1
         BNZ   ERREX         No, exit with error               GUP1.1
*          Interpret results                                   GUP1.1
         L     8,RESULT      Address parsed data               GUP1.1
         USING IKJPARMD,8                                      GUP1.1
         LA    1,PRSSRC      -> Base dataset name info         GUP1.1
         LA    6,SRCNAM      -> Destination field              GUP1.1
         BAL   7,MOVDSN      Move dataset name                 GUP1.1
         LA    1,PRSCTL      Do update DSN                     GUP1.1
         LA    6,CTLNAM                                        GUP1.1
         BAL   7,MOVDSN                                        GUP1.1
         LA    1,PRSOUT      Do output DSN                     GUP1.1
         LA    6,OUTNAM                                        GUP1.1
         BAL   7,MOVDSN                                        GUP1.1
         CLI   PRSSEQ8+1,1   SEQ8 option set?                  GUP1.1
         BNE   *+8           No                                GUP1.1
         OI    FLG,XX8       Yes, enable flag                  GUP1.1
         CLI   PRSSTOR+1,1   STOR option set?                  GUP1.1
         BNE   *+8           No                                GUP1.1
         OI    FLG,XXCOR     Yes, enable flag                  GUP1.1
         LA    1,PRSMRKV                                       GUP1.1
         LA    6,MRKD                                          GUP1.1
         BAL   7,MOVMEM      Move mark, if any                 GUP1.1
         B     OPN           Done                              GUP1.1
*
MOVDSN   L     2,0(1)        --> dataset name                  GUP1.1
         LH    3,4(1)        Length                            GUP1.1
         BCTR  3,0                                             GUP1.1
         EX    3,CPYTXT      Move dataset name                 GUP1.1
         LA    6,44(6)       Point to member storage           GUP1.1
         LA    1,8(1)                                          GUP1.1
MOVMEM   L     2,0(1)        Member name                       GUP1.1
         LTR   2,2           Test for member                   GUP1.1
         BZR   7             None                              GUP1.1
         LH    3,4(1)        Length                            GUP1.1
         BCTR  3,0                                             GUP1.1
         EX    3,CPYTXT      Move member name                  GUP1.1
         BR    7                                               GUP1.1
CPYTXT   MVC   0(,6),0(2)                                      GUP1.1
         DROP  8                                               GUP1.1
*
WTEXT    STM   14,1,ICPRGS   Save registers                    GUP1.1
         TM    FLG,XXBAT     Batch version?                    GUP1.1
         BZ    WTXCP         No, just do a TPUT                GUP1.1
         STH   0,MSGFIL+FABLRECL-FABD Save LRECL               GUP1.1
         LR    0,1                                             GUP1.1
         PUT   MSGFIL,(0)    And write it out                  GUP1.1
         B     WTXRET                                          GUP1.1
WTXCP    SVC   93                                              GUP1.1
WTXRET   LM    14,1,ICPRGS   Restore and return                GUP1.1
         BR    15                                              GUP1.1
*
MSGFIL   DCB   DDNAME=SYSPRINT,MACRF=PM,RECFM=U,BLKSIZE=130,DSORG=PS
*
BATDDNS  DC    CL52'+SYSUT1'                                   GUP1.1
         DC    CL52'+SYSIN'                                    GUP1.1
         DC    CL52'+SYSUT2'                                   GUP1.1
         DC    C'   '        Leave sequence field blank        GUP1.1
*
PPLAREA  DC    A(0,0,CPECB,PRSPCL,RESULT,0,USERBLK)            GUP1.1
CPECB    DS    F             GETLINE/PUTLINE/PUTGET ECB        @TS86001
RESULT   DS    A             Parse PDL ptr                     GUP1.1
USERBLK  DS    D             Parse work area (not used)        GUP1.1
*
PRSPCL   IKJPARM ,                                             GUP1.1
PRSSRC   IKJPOSIT DSNAME,USID,PROMPT='SOURCE DSNAME'           GUP1.1
PRSCTL   IKJPOSIT DSNAME,USID,PROMPT='UPDATE DSNAME'           GUP1.1
PRSOUT   IKJPOSIT DSNAME,USID,PROMPT='OUTPUT DSNAME'           GUP1.1
PRSSEQ8  IKJKEYWD DEFAULT='SEQ8'                               GUP1.1
         IKJNAME 'SEQ8'                                        GUP1.1
         IKJNAME 'NOSEQ8'                                      GUP1.1
PRSSTOR  IKJKEYWD DEFAULT='STOR'                               GUP1.1
         IKJNAME 'STOR'                                        GUP1.1
         IKJNAME 'NOSTOR'                                      GUP1.1
PRSMARK  IKJKEYWD ,                                            GUP1.1
         IKJNAME 'MARK',SUBFLD=PRS2MRK                         GUP1.1
PRS2MRK  IKJSUBF ,                                             GUP1.1
PRSMRKV  IKJIDENT 'SEQUENCE MARK',FIRST=ANY,OTHER=ANY,MAXLNTH=3 UP1.1
         IKJENDP ,                                             GUP1.1
GUPI     CSECT
*                                                                   TSO
OPNERR   LA    1,L'OPNEM                                            TSO
         BAL   0,FILERR                                             TSO
OPNEM    DC    C'FILE NOT FOUND: '                                  TSO
DSKERR   LA    2,8(1)                                               TSO
         LA    1,L'DSKEM                                            TSO
         BAL   0,FILERR                                             TSO
DSKEM    DC    C'DISK ERROR ON FILE '                               TSO
*                                                                   TSO
FILERR   LA    4,FNAME       Buffer to use                          TSO
         LR    5,1                                                  TSO
         MVCL  4,0           Copy message                           TSO
         LA    3,LFID        Length of a name field                 TSO
         LR    5,3                                                  TSO
         MVCL  4,2           Copy name                              TSO
         LA    1,FNAME       Start of buffer again                  TSO
         SR    4,1                                                  TSO
         WTEXT (1),(4)                                              TSO
         B     ERREX                                                TSO
*COPY                                                 GUPSUB
         TITLE 'DISKIO Routine - performs disk I/O functions'
* Function selected on entry by R0:
* 1=> open (in): R1->pattern FDB, R2->name.  Returns R0->FAB, R1->FDB
* 2=> open (out): (same, but no complete FDB if new file)
* 4=> close file: R1->adr(FAB).
* 9=> read: R1->FAB.  Returns R15=12 if EOF, 0 if ok; R0=# data
* 10=> write: R1->FAB.  Returns R15=13 if disk full, 0 if ok.
DISKIO   ENTER
         USING FABD,3                                          @SC86295
         SR    4,4           Signal no block assigned          @SC86295
         LA    6,FDBTRKAL-FDBD(1) Use pattern TRKAL            @SC88026
         ST    6,DYNPL+20    Set up calling sequence           GUP1.1
         BCT   0,DSKOPNO                                       @SC86295
*
* Open for input file whose name is at (R2), FDB at (R1)
         MVI   DYNDSP,X'88'  SHR,KEEP                          @SC86299
         BAL   9,DSKALC      Get FAB                           @SC86295
         BAL   2,DSKLKP      Get DSCB                          @SC86299
         BNZ   DSKER1        Not found                         @SC86295
         BAL   14,DSKVALS                                      @SC86295
         BAL   9,DSKFABS     Set up FAB from FDB               @SC86299
         CNOP  0,4                                             @SC86299
         BAL   2,DSKOPT      Open and test                     @SC86299
         OPEN  (0,INPUT),MF=L                                  @SC86299
*
* Open for output file whose name is at (R2), FDB at (R1)
DSKOPNO  BCT   0,DSKTEST                                       @SC86295
         MVI   DYNDSP,X'42'  NEW,CATLG                         @SC86299
         BAL   9,DSKALC      Get FAB                           @SC86295
         BAL   2,DSKLKP      Get DSCB                          @SC86299
         BNZ   DSKOPN        Not found, just writing new       @SC86299
         MVI   DYNDSP,X'18'  OLD,KEEP                          @SC86299
         TM    DS1DSO,2      PDS?                              GUP1.1
         BZ    DSKOPN        No, we just write over it         GUP1.1
         BAL   14,DSKVALS    Yes, copy DCB info                GUP1.1
         BAL   9,DSKFABS                                       GUP1.1
DSKOPN   CNOP  0,4                                             @SC86299
         BAL   2,DSKOPT      Open and test                     @SC86299
         OPEN  (0,OUTPUT),MF=L                                 @SC86299
DSKOPT   CLI   FABDSN,C'+'   Just DDNAME?                      GUP1.1
         BE    DSKOPDZ       Yes, don't need to allocate       GUP1.1
         KCALL DYNALC,DYNPL,EXT                                @SC86299
DSKOPDZ  DS    0H                                              GUP1.1
         OPEN  ((3)),MF=(E,(2))                                @SC86299
         TM    FABOFLGS,X'10'                                  @SC86299
         BZ    DSKER1        Didn't work                       @SC86299
         B     RTRN0                                           @SC86295
*
DSKTEST  BCT   0,DSKCLOS                                       @SC86295
         B     RTRN1                                           @SC86299
*
* Close file whose ticket is at (R1), release block
DSKCLOS  BCT   0,DSKRED                                        @SC86295
         ICM   3,15,0(1)     Get FAB ptr, if any               @SC86295
         BZ    RTRN0         None, ignore                      @SC86295
         XC    0(4,1),0(1)   Yes, now clear ticket             @SC86295
         CLOSE ((3))                                           @SC86299
      FREEPOOL (3)                                             @SC86299
         LA    0,FABDWDS                                       @SC86295
         LR    1,3                                             @SC86299
       DMSFRET DWORDS=(0),LOC=(1)                              @SC86295
         B     RTRN0                                           @SC86295
*
* Read from file whose ticket is at (R1)
DSKRED   SH    0,=H'4'
         BCT   0,DSKWRT                                        @SC86295
         LTR   3,1           Get FAB ptr                       @SC86299
         BNP   RTRN1         Not defined anymore               @SC86299
         L     15,FABGET     I/O routine                       @SC86299
         BALR  14,15         Go to it                          @SC86299
         LM    4,5,FDBBUFF   Get buffer and size               @SC86299
         LH    7,FABLRECL    Actual length                     @SC86299
         AR    7,1           End of record                     @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   1,4(1)        Skip over SDW if V                @SC86299
         SR    7,1           Revised length                    @SC86299
         LR    6,1                                             @SC86299
         CR    7,5                                             @SC86299
         BNL   *+6                                             @SC86299
         LR    5,7           Buffer not filled                 @SC86299
         L     1,4(13)                                         @SC86299
         ST    5,20(1)       Return length in R0               @SC86299
         MVCL  4,6           Copy to buffer                    @SC86299
         B     RTRN0                                           @SC86299
* End of file on input. Don't close it yet.                    @SC86295
DSKEOD   LA    15,12         End return code                   @SC86295
         B     RTRN                                            @SC86295
*
* Write to file whose ticket is at (R1)
DSKWRT   DS    0H
         LTR   3,1           Get FAB ptr                       @SC86299
         BNP   RTRN1         Not defined anymore               @SC86299
         LM    4,5,FDBBUFF   Get buffer and size               @SC86299
         LR    6,5           Copy for LRECL                    @SC86299
         CH    6,FDBLRC                                        @SC86299
         BNH   *+8                                             @SC86299
         LH    6,FDBLRC      Don't allow more than LRECL if V  @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   6,4(5)        + 4 if RECFM=V                    @SC86299
         STH   6,FABLRECL    Set up for output                 @SC86299
         L     15,FABGET     I/O routine                       @SC86299
         BALR  14,15         Do it                             @SC86299
         XC    0(4,1),0(1)                                     @SC86299
         STCM  6,3,0(1)      In case V                         @SC86299
         BAL   2,DSKTV                                         @SC86299
          LA   1,4(1)        V: space over SDW                 @SC86299
         LR    6,1                                             @SC86299
         LR    7,5                                             @SC86299
         MVCL  6,4           Copy to output record             @SC86299
         B     RTRN0                                           @SC86295
*
DSKTV    TM    FABRECFM,FABRECU                                @SC86299
         BNM   4(2)          U                                 @SC86299
         TM    FABRECFM,FABRECF                                @SC86299
         BO    4(2)          F                                 @SC86299
         BR    2             V                                 @SC86299
* Return on error, release useless block, if any
DSKER1   LTR   1,4           Any block assigned?               @SC86295
         BZ    RTRN1         No                                @SC86295
         LA    0,FABDWDS     Yes, release it                   @SC86295
       DMSFRET DWORDS=(0),LOC=(1)                              @SC86295
         B     RTRN1         Flag error                        @SC86295
*
DSKALC   LR    5,1           Save FDB ptr                      @SC86295
         LA    6,1           Update counter                    @SC86299
         A     6,EVCTR                                         @SC86299
         ST    6,EVCTR                                         @SC86299
         LA    0,FABDWDS                                       @SC86295
       DMSFREE DWORDS=(0),ERR=DSKER1                           @SC86295
         LR    3,1           New block ptr                     @SC86295
         LR    4,1                                             @SC86295
         L     1,4(13)                                         @SC86295
         ST    3,20(1)       Return R0                         @SC86295
         XC    0(8*FABDWDS,3),0(3)                             @SC86295
         MVC   FDBD(FDBCOP),0(5) Copy user's FDB               @SC86295
         MVC   FABDSN,0(2)                                     @SC86299
         LR    15,2          Set up DSN ptr                    @SC86299
         LA    0,FABDDNAM    Get DDN ptr                       @SC86299
         LA    1,FDBUNT      Get UNIT ptr                      @SC86299
         LA    2,FDBVOL      Get VOL ptr                       @SC86299
         STM   15,2,DYNPL    Set up DYNALC                     @SC86299
         MVI   FABBUFCB+3,1  Fill out DCB                      @SC86299
         MVI   FABDSORG,X'40' =PS                              @SC86299
         MVI   FABIOBAD+3,1                                    @SC86299
         LA    0,DSKEOD                                        @SC86299
         LA    1,DSKOPEX                                       @SC86299
         STM   0,1,FABEODAD                                    @SC86299
         UNPK  FABDDNAM,EVCTR(5)                               @SC86299
         TR    FABDDNAM,TRHEX  Get unique DDNAME               @SC86299
         MVI   FABDDNAM,C'K'                                   @SC86299
         MVI   FABDDNAM+7,C'Z'                                 @SC86299
         MVC   FABOFLGS(4),=X'02,00,48,48'                     @SC86299
         MVI   FABCHECK+3,1                                    @SC86299
         LA    1,RTRN1                                         @SC86299
         ST    1,FABSYNAD    In case of error                  @SC86299
         MVI   FABIOBA+3,1                                     @SC86299
         MVI   FABEOBAD+3,1                                    GUP1.1
         MVI   FABRECAD+3,1                                    GUP1.1
         MVI   FABCNTRL+3,1                                    GUP1.1
         MVI   FABEOB+3,1                                      @SC86299
DSKFABS  LH    1,FDBLRC      Copy Info to DCB                  @SC86299
         CLI   FABDSN,C'+'   Just DDNAME?                      GUP1.1
         BE    DSKDDA        Yes, copy it to FAB               GUP1.1
         STH   1,FABLRECL                                      @SC86299
         MVC   FABBLKSI,FDBBLKSI                               @SC86299
         MVI   FABRECFM,FABRECU                                @SC86299
         CLI   FDBRCF,C'U'                                     @SC86299
         BER   9                                               @SC86299
         MVI   FABRECFM,FABRECF+FABRECBR                       @SC86299
         CLI   FDBRCF,C'F'                                     @SC86299
         BER   9                                               @SC86299
         MVI   FABRECFM,FABRECV+FABRECBR                       @SC86299
         LA    1,4(1)        Allow for RDW                     @SC86299
         STH   1,FABLRECL                                      @SC86299
         BR    9                                               @SC86299
DSKDDA   MVC   FABDDNAM,FABDSN+1 Copy to DDNAME                GUP1.1
         BR    9                                               GUP1.1
*
* Call with R15->name, return to R2 with CC set (Z if ok)
DSKLKP   SR    0,0                                             @SC86299
         CLI   0(15),C'+'    Just DDNAME?                      GUP1.1
         BER   2             Yes, say we found it              GUP1.1
         LA    1,CAMVOLS                                       @SC86299
         LA    14,X'44'      Name code                         @SC86299
         SLL   14,24                                           @SC86299
         STM   14,1,CAMLOC   Save dsn ptr, etc                 @SC86299
         LA    0,CAMVOLS+6                                     @SC86299
         LA    1,CAMDSCB                                       @SC86299
         LA    14,X'C1'      Search code                       @SC86299
         SLL   14,24                                           @SC86299
         STM   14,1,CAMOBT                                     @SC86299
        LOCATE CAMLOC                                          @SC86299
         LTR   6,15          Retain 1st code in R6             @SC86299
         BNZR  2             Give up                           @SC86299
        OBTAIN CAMOBT        Get DSCB                          @SC86299
         LTR   15,15         Test return code                  @SC86299
         BR    2                                               @SC86295
*
DSKVALS  LA    0,FDBD        Ptr to FDB                        @SC86295
         L     1,4(13)                                         @SC86295
         ST    0,24(1)       Return ptr to caller              @SC86295
         CLI   FABDSN,C'+'   Just DDNAME?                      GUP1.1
         BER   14            Yes, done: no DSCB                GUP1.1
         MVC   FDBBLKSI,DS1BLK                                 @SC86299
         MVC   FDBVOL,DS1VOL Copy volume name                  @SC86299
         LH    1,DS1BLK      Use BLKSIZE if 'U'                @SC86299
         MVI   FDBRCF,C'U'                                     @SC86299
         TM    DS1RCF,FABRECU                                  @SC86299
         BO    DSKVLR                                          @SC86299
         LH    1,DS1LRC      Use LRECL if 'F'                  @SC86299
         MVI   FDBRCF,C'F'                                     @SC86299
         TM    DS1RCF,FABRECF                                  @SC86299
         BO    DSKVLR                                          @SC86299
         MVI   FDBRCF,C'V'                                     @SC86299
         S     1,F4          Use LRECL-4 if 'V'                @SC86299
DSKVLR   STH   1,FDBLRC                                        @SC86299
         BR    14                                              @SC86299
*
DSKOPEX  DC    0F'0',X'85',AL3(DSKOPC) OPEN EXIT               @SC86299
*
DSKOPC   LR    3,1                                             @SC86299
         LH    5,FABBLKSI                                      @SC86299
         LTR   5,5                                             @SC86299
         BP    *+8                                             @SC86299
         LH    5,=H'6233'                                      @SC86299
         LR    6,5                                             @SC86299
         TM    FABRECFM,FABRECU                                @SC86299
         BO    DSKOPS                                          @SC86299
         LH    6,FABLRECL                                      @SC86299
         BNZ   *+8                                             @SC86299
         OI    FABRECFM,FABRECF+FABRECBR                       @SC86299
         LTR   6,6                                             @SC86299
         BP    DSKOPQ                                          @SC86299
         LA    6,80                                            @SC86299
         BAL   2,DSKTV                                         @SC88049
          LA   6,4(6)        Allow LRECL=84 for VB             @SC88049
DSKOPQ   TM    FABRECFM,FABRECF                                @SC86299
         BZ    DSKOPV                                          @SC86299
         SR    4,4                                             @SC86299
         DR    4,6                                             @SC86299
         LTR   5,5                                             @SC88104
         BP    *+8                                             @SC88104
         LA    5,1           BLKSIZE was less than LRECL!      @SC88104
         MR    4,6                                             @SC86299
         B     DSKOPS                                          @SC86299
DSKOPV   LA    4,4(6)                                          @SC86299
         CR    4,5                                             @SC86299
         BNH   DSKOPS                                          @SC86299
         LR    5,4                                             @SC86299
DSKOPS   STH   6,FABLRECL                                      @SC86299
         STH   5,FABBLKSI                                      @SC86299
         BR    14                                              @SC86299
*
CAMLOC   DS    4F            Ptrs for locating dataset         @SC86299
CAMOBT   DS    4F            Ptrs for getting DSCB             @SC86299
CAMVOLS  DS    0D,XL265      Storage for volume list           @SC86299
CAMDSCB  DS    0F,XL101      Storage for DSCB                  @SC88014
         ORG   CAMDSCB+1                                       @SC88014
DS1VOL   DS    CL6,XL2       Volume serial                     @SC86299
DS1CRDT  DS    2XL3,3X,XL13  Creation date                     @SC86299
DS1RFDT  DS    XL3,XL4       Reference date                    @SC86299
DS1DSO   DS    XL2           Dataset org                       @SC86299
DS1RCF   DS    X             Record format                     @SC86299
DS1OPT   DS    X             Error option                      @SC86299
DS1BLK   DS    H             Block size                        @SC86299
DS1LRC   DS    H             Logical record length             @SC86299
         ORG   ,                                               @SC86299
DYNPL    DC    A(0,0,0,0,DYNDSP,0),X'80',AL3(DYNRC)            GUP1.1
DYNRC    DS    F                                               @SC86299
DYNDSP   DS    X                                               @SC86299
         LOCALS ,                                              @SC86295
         EXIT
