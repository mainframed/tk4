//SYSRBWD JOB 'ACCT#','DASDROOM ASSEMBLE',REGION=4096K,
//    CLASS=S,MSGCLASS=H,NOTIFY=SYSRBW
//*    SYS.MVS.CNTL(DASDROOM)
//STEP1 EXEC ASMHCL <<<<<< ANY ASM PROC
//C.SYSIN DD *
*
*
* LIST THE VOLSERS, ONE PER LINE, TO BE INCLUDED IN
* THE REPORT. ANY VOLUMES LISTED HERE BUT NOT ONLINE WILL RESULT IN A
* RETURN CODE OF 4. ADDITIONALY, 'PARM =MISSMSG' WILL CAUSE A MESSAGE
* TO BE WRITTEN FOR MISSING VOLUMES.
*
* FOR SYSIN DD STATEMENT, SPECIFY VOLUME SERIAL NUMBERS STARTING IN
* CARD COLUMN 1.
*
* ALSO, '+' CAN BE USED IN YOUR CONTROL CARD COLUMN '1' FOR COMMENTS
* IN YOUR CONTROL CARDS
*
* --------------------------------------------------------------------
*
LSPACE   CSECT
         BASER R12              .REG EQUS, SAVE, AND USING
         LA    R11,SAVEAREA     .LOADUP SAVEAREA ADDRESS
         ST    R13,4(,R11)      .CHAIN TCB SAVEAREA
         ST    R11,8(,R13)      .....WITH LSPACE SAVEAREA
         LR    R13,R11          .PRIME R13 WITH LSPACE SAVEAREA
         L     R1,0(,R1)        LOAD PARM PTR
         CLC   =H'0',0(R1)      DID WE GET A PARM?
         BE    NOPARM           NO - BRANCH
         CLC   =C'MISSMSG',2(R1)  MESSAGE FOR MISSING VOLUMES?
         BNE   NOPARM           DON'T KNOW THIS PARM
         MVI   MISSMSG,C'Y'     INDICATE MESSAGE FOR MISSING VOLS
*
NOPARM   OPEN  (SYSPRINT,(OUTPUT),SYSIN,(INPUT))
         TIME
         ST    R1,PACKAREA+4         STORE THE '0CYYDDDF' FROM TIME
         UNPK  DATE(5),PACKAREA+5(3) UNPACK THE DATE FOR PRINTING
         LR    R1,R0                 CAPTURE THE TIME PART
         SRL   R1,16                 SHIFT OUT THE 'SSTH' PART
         SLL   R1,4                  SHIFT BACK FOR THE SIGN CHARACTER
         LA    R1,15(R1)             GET READY FOR UNPACKING
         ST    R1,PACKAREA+4         STORE THE 'HHMMF' FROM TIME
         UNPK  PACKAREA(4),PACKAREA+5(3)
         MVC   HOURS(2),PACKAREA     MOVE IN HOUR OF DAY
         MVC   MINUTES(2),PACKAREA+2 MOVE IN MINUTES
         PUT   SYSPRINT,TITLE0
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,TITLE1
         PUT   SYSPRINT,TITLE2
         PUT   SYSPRINT,TITLE3
         PUT   SYSPRINT,LINE
         PUT   SYSPRINT,BLANKS
*
         XR    R11,R11            CLEAR GRAND TOTAL
*
*
NEXTVOL  GET   SYSIN              READ SYSIN RECORD
         CLI   0(R1),C'*'         COMMENT CARD?
         BE    NEXTVOL            YES - IGNORE
         CLI   0(R1),C'+'         MESSAGE CARD?
         BNE   ISVOL              NO - MUST BE A VOLUME CARD
         MVC   MSG(79),1(R1)      MOVE MESSAGE
         PUT   SYSPRINT,MSG       WRITE MESSAGE
         B     NEXTVOL            GET NEXT CARD
*
ISVOL    MVC   VOLID(6),0(R1)     MOVE VOLID TO OUTPUT LINE
         XC    USWORK,USWORK CLEAR UCBSCAN WORK AREA
UCBSCAN  EQU   *
         L     R2,CVTPTR          CVT ADDRESS
         USING CVTMAP,R2
         LA    R1,USPLIST         POINT TO PARMLIST
         L     R15,CVTUCBSC       LOAD UCB SCAN ROUTINE ADDRESS
         DROP  R2
         BALR  R14,R15            SCAN
         LTR   R15,R15            FOUND ONE?
         BNZ   VOLNFND            NO - END OF UCBS - VOLUME NOT FOUND
         L     R2,USUCBPTR        LOAD PTR TO RETURNED UCB
         USING UCBOB,R2
         TM    UCBSTAT,UCBONLI    IS DEVICE ONLINE?
         BNO   UCBSCAN            NO - TRY THE NEXT ONE
         CLC   VOLID,UCBVOLI      OUR VOLUME?
         BNE   UCBSCAN            NO - TRY THE NEXT ONE
         MVC   ADDRESS(3),UCBNAME    MOVE ADDRESS TO OUTPUT LINE
         DROP  R2
         B     GOTUCB             FOUND IT - GO PROCESS
*
VOLNFND  EQU   *
         MVC   RETCODE,=H'4'      SET RETURN CODE
         CLI   MISSMSG,C'Y'       MSG FOR MISSING VOLUMES?
         BNE   NEXTVOL            NO - SKIP MSG
         MVC   VOLBAD(6),VOLID    MOVE VOLSER TO MESSAGE
         PUT   SYSPRINT,VOLBAD    WRITE ERROR MESSAGE
         PUT   SYSPRINT,BLANKS
         B     NEXTVOL            TRY NEXT VOLUME
*
GOTUCB   DS    0H
         LR    R0,R2            .PUT UCB ADDR IN R0
         LA    R1,LEN30         .PUT MSG AREA IN R1
         SVC   78               .CALL UP LSPACE SVC
         LSPACE UCB=(R2),MSG=LEN30,F4DSCB=F4DSCB
         LTR   R15,R15          .DID IT GO OK ?
         BZ    FORMAT
         PUT   SYSPRINT,SVCBAD
         B     RETURN
FORMAT   DS    0H
*        LA    R8,LEN30
*        USING SPACED,R8
         MVC   CYLSFREE(4),FREECYL   MOVE IN NUMBER OF FREE CYLS
         MVC   TRKSFREE(4),FREETRK   MOVE IN NUMBER OF FREE TRKS
         MVC   FREEXTNT(4),FREEEXT   MOVE IN NUMBER OF FREE EXTENTS
         MVC   MAXCYLS(4),EXTCYL     MOVE IN LARGEST FREE CYL
         MVC   MAXTRKS(4),EXTTRK     MOVE IN LARGEST FREE TRACK
         SR    R10,R10               CLEAR REG FOR CALCULATIONS
         XC    PACKAREA(8),PACKAREA  CLEAR AREA TO 0
         PACK  PACKAREA+4(4),FREECYL(4) PACK FOR % ALLOC CALCULATION
         CVB   R7,PACKAREA           GET BINARY VALUE
*
         LA    R1,F4DSCB             POINT TO FORMAT 4 DSCB
         USING IECSDSL4,R1           ADDRESS DSECT
         MH    R7,DS4DEVSZ+2         CYLS TIMES TRACKS PER CYL
*
         LR    R10,R7                TRACK COUNT TO R10
         PACK  PACKAREA+4(4),FREETRK(4) PACK FOR % ALLOC CALCULATION
         CVB   R7,PACKAREA           GET BINARY VALUE
         AR    R10,R7                ADD TRACK COUNT TO TOTAL
*
         LH    R7,DS4DEVSZ           LOAD DEVICE CAP IN CYLS
         MH    R7,DS4DEVSZ+2         TIME DEVICE TRACKS PER CYL
         DROP  R1
         L     R1,ALLDASD            GET RUNNING CAPACITY COUNTER
         AR    R1,R7                 UPDATE WITH CAP THIS DEVICE
         ST    R1,ALLDASD            STORE IT BACK
         LR    R15,R7                PREPARE FOR DIVIDE
         XR    R14,R14
         D     R14,=F'100'           DIV CAP BY 100 FOR PCT CALCULATION
*
         SR    R7,R10                FIND NUMBER OF TRACKS ALLOCATED
         AR    R11,R7                GET GRAND TOTAL OF ALLOC TRACKS
         XR    R6,R6                 ZERO FOR DIVIDE
         DR    R6,R15                CALCULATE % ALLOCATED
         CVD   R7,PACKAREA           CONVERT TO DECIMAL FOR PRINTING
         OI    PACKAREA+7,X'0F'      CHANGE SIGN FOR PRINTING
         UNPK  PCTALLOC(3),PACKAREA+6(2)
         CLI   PCTALLOC,X'F0'        IS LEADING DIGIT A ZERO
         BNE   NOTZERO               BRANCH IF IT'S NOT A ZERO
         NI    PCTALLOC,X'40'        CHANGE IT TO A BLANK
NOTZERO  PUT   SYSPRINT,STATS
         PUT   SYSPRINT,BLANKS
         B     NEXTVOL               GET NEXT VOLSER
*
DOTOTAL  PUT   SYSPRINT,BLANKS
         CVD   R11,PACKAREA          CONVERT GRAND TOTAL FOR PRINTING
         OI    PACKAREA+7,X'0F'      CHANGE SIGN FOR PRINTING
         UNPK  TOTALLOC(6),PACKAREA+4(4)
         L     R1,ALLDASD            LOAD TOTAL CAPACITY
         CVD   R1,PACKAREA           CONVERT TOTAL CAP FOR PRINTING
         OI    PACKAREA+7,X'0F'      CHANGE SIGN FOR PRINTING
         UNPK  TOTALCAP(6),PACKAREA+4(4)
*
         L     R15,ALLDASD           GET TOTAL CAPACITY
         LTR   R15,R15               DID WE FIND ANY VOLUMES?
         BZ    RETURN                NO - EXIT
         XR    R14,R14               PREPARE FOR DIVIDE
         D     R14,=F'100'           DIVIDE BY 100 FOR PCT CALC
         SR    R10,R10               CLEAR REG FOR DIVIDE
         DR    R10,R15               DIVDE TO GET PERCENT ALLOCATED
*
         CVD   R11,PACKAREA          CONVERT PERCENT ALLOC FOR PRINTING
         OI    PACKAREA+7,X'0F'      CHANGE SIGN OFR PRINTING
         UNPK  PERCENT(2),PACKAREA+6(2)
         PUT   SYSPRINT,TOTALINE
         PUT   SYSPRINT,LASTLINE
RETURN   CLOSE SYSPRINT
         LH    R15,RETCODE
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
*
*
MOVVOL   MVC   0(*-*,R5),0(R3) .GENERIC MOVER
PACKAREA DS    D'0'
SAVEAREA DS    18F
*   PARM LIST AND ARGUMENTS FOR IOSVUCB
USPLIST  DC    A(USWORK),A(USDEVT),X'80',AL3(USUCBPTR)
USWORK   DC    XL100'00'          WORK AREA FOR IOSSVUCB
USDEVT   DC    AL1(UCB3DACC)      DASD DEVICES ONLY
USUCBPTR DS    F
*
ALLDASD  DC    F'0'                RUNNING COUNT OF TRACK CAP /100
RETCODE  DC    H'0'               RETURN CODE
MISSMSG  DC    C'N' PRINT MSG FOR MISSING VOLS - Y OR N
F4DSCB   DS    CL96                RETURN AREA FOR F4 DSCB
VOLBAD   DC    CL133'XXXXXX   *** VOLUME NOT FOUND'
SVCBAD   DC    CL133'*** ERROR RETURN FROM SVC 78'
BLANKS   DC    CL133' '
MSG      DC    CL133' '
LINE     DC    133C'-'
TITLE0   DC    C'DASD UTILIZATION REPORT: '
DATE     EQU   *
         DC    CL8' '
HOURS    EQU   *
         DC    CL3'  :'
MINUTES  EQU   *
         DC    CL(133-(*-TITLE0))' '
TITLE1   DC    C'VOLUME   ADDRESS   TOTAL FREE   LARGEST FREE'
         DC    C'   NUMBER    PERCENT'
         DC    CL(133-(*-TITLE1))' '
TITLE2   DC    C'                     SPACE         EXTENT   '
         DC    C'   OF FREE   ALLOCATED'
         DC    CL(150-(*-TITLE2))' '
TITLE3   DC    C'                   CYLS  TRKS    CYLS + TRKS'
         DC    C'   EXTENTS            '
         DC    CL(133-(*-TITLE3))' '
STATS    DC    CL62' '
         DC    CL1'%'
         DC    CL87' '
TOTALINE DC    C'** TOTAL TRACKS ON LISTED PACKS : '
TOTALCAP DC    CL6' '
         DC    CL(133-(*-TOTALINE))'        *****'
LASTLINE DC    C'******** TOTAL TRACKS ALLOCATED : '
TOTALLOC DC    CL6' '                GRAND TOTAL GOES HERE
         DC    C'  ('
PERCENT  DC    CL2' '                PERCENT ALLOCATED GOES HERE
         DC    C'%)'
         DC    CL(133-(*-LASTLINE))' *****'
VOLID    EQU   STATS,6
ADDRESS  EQU   STATS+11,3
CYLSFREE EQU   STATS+19
TRKSFREE EQU   STATS+25
MAXCYLS  EQU   STATS+33
MAXTRKS  EQU   STATS+40
FREEXTNT EQU   STATS+48
PCTALLOC EQU   STATS+59
LEN30    DS    0CL30           .LSPACE WILL PUT GOODIES HERE
LITERAL  DS    CL6             .TEST FOR 'SPACE='
FREECYL  DS    CL4             .TOTAL NUMBER OF FREE CYLINDERS
         DS    CL1             .COMMA
FREETRK  DS    CL4             .TOTAL NUMBER OF FREE TRACKS
         DS    CL1             .COMMA
FREEEXT  DS    CL4             .TOTAL NUMBER OF FREE EXTENTS
         DS    CL1             .COMMA
EXTCYL   DS    CL4             .NUMBER OF CYLS IN LARGEST FREE EXTENT
         DS    CL1             .COMMA
EXTTRK   DS    CL4             .NUMBER OF TRKS IN LARGEST FREE EXTENT
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=F,LRECL=133,MACRF=PM,DSORG=PS
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,RECFM=FB,LRECL=80,EODAD=DOTOTAL,  X
               MACRF=GL
         LTORG
         CVT   DSECT=YES
         DSECT
         IEFUCBOB
         IECSDSL1 4
         END
/*
//L.SYSLMOD DD DISP=SHR,DSN=SYS3.TEMP.LINKLIB
//L.SYSIN DD *
  SETCODE AC(1)
  NAME DASDROOM(R)
