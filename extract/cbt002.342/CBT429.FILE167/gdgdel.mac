PROC 5 GDGPREF GDGENTS GDGNAME SUF ACTION TRACE
/*
/*--------------------------------------------------------------------*
/*                                                                    *
/* FUNCTION - GENERATION DATA-GROUP UTILITY                           *
/*                                                                    *
/* PURPOSE  - DELETE GDG ENTRY (INVOKED BY GDGUTL)                    *
/*                                                                    *
/* AUTHOR   - ____________________________                            *
/*                                                                    *
/* DATE     - ___________ 1990                                        *
/*                                                                    *
/*--------------------------------------------------------------------*
/*
/*--------------------------------------------------------------------*
/* ENABLE DEBUG MODE IF REQUESTED BY USER OR FUNCTION AS NORMAL       *
/*--------------------------------------------------------------------*
/*
IF &TRACE = THEN -
   CONTROL NOFLUSH NOMSG
ELSE -
   CONTROL NOFLUSH MSG CONLIST LIST SYMLIST
/*
/*--------------------------------------------------------------------*
/* SET COMMAND LIST INITIAL VALUES                                    *
/*--------------------------------------------------------------------*
/*
/*
/*--------------------------------------------------------------------*
/* DISABLE ISPF DIALOG DIAGNOSTICS AND HANDLE ALL ERRORS WITH COMMAND *
/* LIST ..                                                            *
/*--------------------------------------------------------------------*
/*
ISPEXEC CONTROL ERRORS RETURN
/*
/*--------------------------------------------------------------------*
/* INVOKE LISTCAT COMMAND TO GATHER INFORMATION ON GDG                *
/*--------------------------------------------------------------------*
/*
LISTC ENT(&GDGPREF) &ALL OUTFILE(D&SUF.1)
SET &CCND = &LASTCC
/*
/*--------------------------------------------------------------------*
/* IF GDG DOES NOT EXIST, DISPLAY ERROR MESSAGE                       *
/*--------------------------------------------------------------------*
/*
IF &CCND > 0 THEN -
   DO
     SET &ZEDSMSG = &STR(GDG NOT FOUND)
     SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS NOT FOUND RC=&CCND)
     ISPEXEC SETMSG MSG(ISRZ000)
   END
/*
/*--------------------------------------------------------------------*
/* OTHERWISE, PROCEED WITH MODIFY                                     *
/*--------------------------------------------------------------------*
/*
ELSE -
   DO
     SET BLANKS = +
         &STR(                                                  )
     SET GDGWRK = +
         &STR(&SUBSTR(1:45,&STR(&GDGNAME.&BLANKS))&STR(&GDGENTS))
     SET HCHARCNL = &ZCHARCNL
     SET ZCHARCNL = NOCC
     ISPEXEC VPUT (ZCHARCNL)
     FREE DD(D&SUF.1) DELETE
     ATTR $G$R$O             +
     LRECL(133)              +
     BLKSIZE(9044)           +
     RECFM(F B M)
     ATTR $G$R$I             +
     LRECL(80)               +
     BLKSIZE(80)             +
     RECFM(F B)
     ALLOC DD(GDGOUT)                  +
           DS('&SYSUID..GDGPRT1.LIST') +
           REUSE US($G$R$O)            +
           MOD                         +
           CAT                         +
           SPACE(2,1)                  +
           TRACK
     ALLOC DD(GDGIN)                   +
           DS('&SYSUID..GDGIN.DATA')   +
           REUSE                       +
           US($G$R$I)                  +
           NEW                         +
           DEL                         +
           SPACE(1)                    +
           TRACK
     ALLOC DD(SYSPRINT)                +
           DS('&SYSUID..GDGPRT2.LIST') +
           REUSE                       +
           US($L$C$P)                  +
           MOD                         +
           CAT                         +
           SPACE(2,1)                  +
           TRACK
     OPENFILE  GDGIN OUTPUT
     SET GDGIN = &STR(&GDGWRK)
     PUTFILE GDGIN
     CLOSFILE GDGIN
     CALL 'DEV.GLR.LOADLIB(GDGRESET)'
     ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT1.LIST')
     ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT2.LIST')
     SET ZCHARCNL = &HCHARCNL
     ISPEXEC VPUT (ZCHARCNL)
     FREE DD(D&SUF.3) DELETE
     SET &ZEDSMSG = &STR(GDG WAS &ACTION)
     SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS &ACTION RC=&CCND)
     ISPEXEC SETMSG MSG(ISRZ000)
   END
FREE DD(GDGIN) DELETE
IF &K = D THEN -
   FREE DD(GDGOUT SYSPRINT) DELETE
ELSE -
   FREE DD(GDGOUT SYSPRINT)
ALLOC DD(SYSPRINT) DS(*) REUSE
EXIT
