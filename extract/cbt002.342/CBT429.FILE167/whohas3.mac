*/*
*/*****************************************************************/*
*/** WHOHAS3:                                                    **/*
*/** FUNCTION: 3RD   MODULE CALLED BY 'WHOHAS'                   **/*
*/**           WILL ISSUE GQSCAN AND FORMAT THE INFORMATION      **/*
*/**           FOR SUBSEQUENT TPUT THEN RETURN TO WHOHAS2,&1     **/*
*/*****************************************************************/*
*/*
WHOHAS3  CSECT
         BASER R12                    .SET BASE REG + GET EQUATES
         LA    R11,SAVEHAS3           .R11 POINTS TO SAVEAREA
         ST    R13,4(,R11)            .NOW CHAIN
         ST    R11,8(,R13)            .......... THE SAVEAREAS
         LR    R13,R11                .R13 POINTS TO OUR SAVEAREA
         B     BEGIN                  .AND BRANCH TO CODE
*/*
SAVEHAS3 DS    18F                    .OUR SAVEAREA
BEGIN    EQU   *
BALREG   EQU   2                      .ASSIGN R2 AS BALREG
         LR    R6,R1                  .SECURE PARMLIST ADDR
         MVC   WHO3LIST(52),0(R6)     .ACTUALLY MOVE THE DATA 4NOW
         LA    R1,X'10'               .ADDR OF CVT
         L     R1,X'22C'(,R1)         .ADDR OF ASVT
         LA    R1,X'210'(,R1)         .START OF ASVTENTY IN ASVT
         ST    R1,ENTYADDR            .KEEP IT FOR LATER
         MVC   QNAME,=XL8'40'         .CLEAR QNAME
         CLC   0(2,R6),=C'DA'         .DA FUNCTION     ?
         BE    DAINQ
         CLC   0(3,R6),=C'CAT'        .CAT FUNCTION    ?
         BE    CATINQ
         CLC   0(2,R6),=C'LL'         .LL FUNCTION     ?
         BE    LLINQ
         CLC   0(3,R6),=C'VOL'        .VOL FUNCTION    ?
         BE    VOLINQ
         CLC   0(2,R6),=C'VT'         .VT FUNCTION     ?
         BE    VTINQ
*/*
DAINQ    EQU   *
         MVC   QNAME,=C'SYSDSN  '
         MVC   QNLEN,=X'06'
         BAL   BALREG,GQSCAN
         B     FORMAT
*/*
LLINQ    EQU   *
         MVC   QNAME,=C'SYSIEWLP'
         MVC   QNLEN,=X'08'
         BAL   BALREG,GQSCAN
         B     FORMAT
*/*
VTINQ    EQU   *
         MVC   QNAME,=C'SYSVTOC '
         MVC   QNLEN,=X'07'
         BAL   BALREG,GQSCAN
         B     FORMAT
*/*
VOLINQ   EQU   *
         MVC   QNAME,=C'SYSZVOLS'
         MVC   QNLEN,=X'08'
         BAL   BALREG,GQSCAN
         B     FORMAT
*/*
CATINQ   EQU   *
         MVC   QNAME,=C'SYSIGGV2'
         MVC   QNLEN,=X'08'
         BAL   BALREG,GQSCAN
         B     FORMAT
*/*
TPUT     EQU   *
         TPUT  (1),(0)
         BR    BALREG
*/*
FREEMAIN FREEMAIN R,LV=FOR096,A=(R10)
RETURN   L     R13,4(,R13)
         RETURN (14,12),,RC=00
*/*
*/*
GQSCAN   EQU   *
         GETMAIN R,LV=FOR096
         LTR   R15,R15
         BZ    GOSCAN
         LA    R1,BADGET
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     RETURN
GOSCAN   LR    R10,R1
         L     R11,FOR096
         LA    R13,GQSAVE
         LA    R7,QNAME
         LA    R8,RNAME
         L     R9,INLEN
*/*
         GQSCAN AREA=((R10),(R11)),                                    +
               REQLIM=20,                                              +
               SCOPE=SYSTEM,                                           +
               RESNAME=((R7),(R8),(R9),SPECIFIC),                      +
               REQCNT=1
*/*
         C     R15,=F'0'
         BE    GQRC00
         C     R15,=F'4'
         BE    GQRC04
         BH    GQGT04
*/*
GQRC00   EQU   *
         STH   R0,RIBELEN
         SRL   R0,16
         STH   R0,RIBLEN
         ST    R1,RIBNUM
         LA    R13,SAVEHAS3
         BR    BALREG
*/*
GQRC04   EQU   *
         LA    R13,SAVEHAS3
         LA    R1,GQZIPP
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     FREEMAIN
*/*
GQGT04   EQU   *
         LA    R13,SAVEHAS3
         LA    R1,GQFAIL
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     FREEMAIN
*/*
FORMAT   EQU   *
         GETMAIN R,LV=L1760
         LTR   R15,R15
         BZ    GO4MAT
         LA    R1,BADGET
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     FREEMAIN
GO4MAT   LR    R9,R10
         LR    R8,R1
         ST    R8,TPADDR
         USING RIB,R9
         MVI   RESOURCE+0,X'40'
         MVC   RESOURCE+1(43),RESOURCE+0
         MVC   RESOURCE(44),RNAME
         L     R3,RIBNTO            .#TASKS OWNING THE RESOURCE
         CVD   R3,DWORK
         UNPK  CWORK+0(3),DWORK+6(2)
         OI    CWORK+2,X'F0'
         MVC   TOWN(2),CWORK+1
         L     R3,RIBNTWE           .#TASKS WAITING EXCLUSIVE
         CVD   R3,DWORK
         UNPK  CWORK+0(3),DWORK+6(2)
         OI    CWORK+2,X'F0'
         MVC   TEXC(2),CWORK+1
         L     R3,RIBNTWS           .#TASKS WAITING EXCLUSIVE
         CVD   R3,DWORK
         UNPK  CWORK+0(3),DWORK+6(2)
         OI    CWORK+2,X'F0'
         MVC   TSHR(2),CWORK+1
         MVC   0(80,R8),TITLE1
         LA    R8,80(,R8)
         MVC   0(80,R8),TITLE2
*/*
         LA    R11,X'28'(,R10)      .R11 = ADDR FOR END OF RIB
         AH    R11,RIBVLEN          .ADD RIB VARIABLE PORTION
         USING RIBE,R11             .BASE RIBE WITH R11
         LA    R8,80(,R8)           .BEGINING OF OUTPUT TABLE
         L     R3,RIBNRIBE          .RIBES RETURNED TO US
         C     R3,=F'20'            .? LESS THAN 20 RIBES
         BL    USER3VAL             .IF < 20 USE R3 CONTENTS
         LA    R7,20                ....ELSE DO MAX 20 OUTPUTS
         B     MORRIBE              .......USING R7 FOR BCT
USER3VAL LR    R7,R3                .PRIME R7 WITH R3 CONTENTS
MORRIBE  MVI   0(R8),X'40'          .CLEAR STORAGE 80 BYTES
         MVC   1(79,R8),0(R8)       ......FOR OUTPUT
         LH    R4,RIBEASID          .ASID ASSOC WITH THIS RIBE
         MVC   0(8,R8),RIBEJBNM     .GET THE JOBNAME
         TM    RIBERFLG,RIBETYPE    .? SHR OR EXC
         BNO   SHARED
         MVC   10(3,R8),=C'EXC'
         B     MUST
SHARED   MVC   10(3,R8),=C'SHR'
MUST     TM    RIBERFLG,RIBEMC      .? MC OR NOMC
         BNO   NOMC
         MVC   15(4,R8),=C'  MC'
         B     RESRV
NOMC     MVC   15(4,R8),=C'NOMC'
RESRV    TM    RIBERFLG,RIBERESV    .? RESERVE OR NORESERVE
         BNO   NORES
         MVC   25(6,R8),=C'  RESV'
         B     OWN
NORES    MVC   25(6,R8),=C'NORESV'
OWN      TM    RIBESFLG,RIBESTAT    .? OWNER OR WAITER
         BNO   NOOWN
         MVC   35(6,R8),=C' OWNER'
         B     MATDONE
NOOWN    MVC   35(6,R8),=C'WAITER'
*/*
MATDONE  LA    R8,80(,R8)           .NEXT OUTPUT ENTRY
         LA    R11,36(,R11)         .NEXT RIBE
         BCT   R7,MORRIBE           .PROCESS NEXT RIBE
         L     R1,TPADDR            .POINT TO STORAGE
         LR    R0,R8                .POINT TO END OF USED STORAGE
         SR    R0,R1                .R0 = LEN USED
         BAL   BALREG,TPUT
         L     R8,TPADDR
         FREEMAIN R,LV=L1760,A=(R8)
         B     FREEMAIN
*/*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*++          PROGRAM STORAGE / ETC.        COUNTRY                ++*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*/*
*/*
EIGHTY   DC    F'80'
FOR096   DC    F'4096'
L1760    DC    F'1760'
DWORK    DS    D
TPADDR   DS    F
ENTYADDR DS    F
RIBNUM   DS    F
RIBLEN   DS    H
RIBELEN  DS    H
CWORK    DS    CL3
*/*
GQSAVE   DS    18F
RRNAME   DS    CL44
QNAME    DS    CL8
QNLEN    DS    XL1
*/*
*/*
BADGET   DC    CL80'GETMAIN FAILURE, NOTIFY SYSTEM PROGRAMMER'
GQZIPP   DC    CL80'GQSCAN COMPLETE, NO DATA FOR THIS REQUEST'
GQFAIL   DC    CL80'GQSCAN RC > 04, NOTIFY SYSTEM PROGRAMMER'
*/*
         DS    0F
TITLE1   DC    80C' '
         ORG   TITLE1+10
         DC    C'RESOURCE IS '
         ORG   TITLE1+24
RESOURCE DS    CL44
         ORG   ,
TITLE2   DC    80C' '
         ORG   TITLE2+16
         DC    C'# TASKS OWNING '
TOWN     DS    CL2
         ORG   TITLE2+34
         DC    C',WAITING EXC '
TEXC     DS    CL2
         ORG   TITLE2+50
         DC    C',WAITING SHR '
TSHR     DS    CL2
         ORG   ,
*/*
WHO3LIST DS    0F
FUNCTION DS    CL3
         DS    CL1
INLEN    DS    F
RNAME    DS    CL44
*/*
         LTORG
*/*
         ISGRIB
*/*
         END
