*/*
*/*****************************************************************/*
*/** WHOHAS2:                                                    **/*
*/** FUNCTION: 2ND   MODULE CALLED BY 'WHOHAS'                   **/*
*/**           WILL VALIDATE THE INPUT RECEIVED FROM WHOHAS1     **/*
*/**           AND PASS THE DATA ON TO WHOHAS3                   **/*
*/*****************************************************************/*
*/*
WHOHAS2  CSECT
         BASER R12                    .SET BASE REG + GET EQUATES
         LA    R11,SAVEHAS2           .R11 POINTS TO SAVEAREA
         ST    R13,4(,R11)            .NOW CHAIN
         ST    R11,8(,R13)            .......... THE SAVEAREAS
         LR    R13,R11                .R13 POINTS TO OUR SAVEAREA
         B     BEGIN                  .AND BRANCH TO CODE
*/*
SAVEHAS2 DS    18F                    .OUR SAVEAREA
BEGIN    EQU   *
BALREG   EQU   2                      .ASSIGN R2 AS BALREG
         LR    R10,R1                 .SECURE PARMLIST ADDR
         USING WHO3LIST,R10           .ESTABLISH ADDRESSABILITY
         MVC   ADSN(44),RNAME         .MOVE RNAME FOR CAMLST
         CLC   0(2,R10),=C'DA'        .DA FUNCTION     ?
         BE    DAINQ
         CLC   0(3,R10),=C'CAT'       .CAT FUNCTION    ?
         BE    CATINQ
         CLC   0(2,R10),=C'LL'        .LL FUNCTION     ?
         BE    LLINQ
         CLC   0(3,R10),=C'VOL'       .VOL FUNCTION    ?
         BE    VTINQ
         CLC   0(2,R10),=C'VT'        .VT FUNCTION     ?
         BE    VTINQ
*/*
DAINQ    EQU   *
         BAL   BALREG,TEST44
         BAL   BALREG,LOCATE
         B     PASSTO3
*/*
LLINQ    EQU   *
         BAL   BALREG,TEST44
         BAL   BALREG,LOCATE
         B     PASSTO3
*/*
VTINQ    EQU   *
         BAL   BALREG,TEST6
         BAL   BALREG,TESTVOL
         B     PASSTO3
*/*
CATINQ   EQU   *
         BAL   BALREG,TEST44
         BAL   BALREG,LOCATE
         B     PASSTO3
*/*
TPUT     EQU   *
         TPUT  (1),(0)
         BR    BALREG
*/*
PASSTO3  EQU   *
         LR    R1,R10                 .PARM LIST ADDR IN WHOHAS1
         L     R15,=V(WHOHAS3)
         BALR  R14,R15
*/*
RETURN   L     R13,4(,R13)
         RETURN (14,12),,RC=00
*/*
TEST44   EQU   *
         LA    R3,44
         L     R1,INLEN
         CR    R1,R3
         BH    ERROR44
         LA    R3,3                   .I.E.,  T.P FOR DSNAME
         CR    R1,R3
         BL    ERROR44
         BR    BALREG
*/*
TEST6    EQU   *
         LA    R3,6
         L     R1,INLEN
         CR    R1,R3
         BNE   ERROR6                 .MUST BE (6) CHARACTERS
         BR    BALREG
*/*
ERROR44  EQU   *
         LA    R1,MSG44
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     RETURN
*/*
ERROR6   EQU   *
         LA    R1,MSG6
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     RETURN
*/*
TESTVOL  EQU   *
         LA    R3,RNAME
         LA    R4,VOLTAB
         L     R5,VTLEN
MORVOL   CLC   0(6,R3),0(R4)
         BE    ENDVOL
         LA    R4,6(,R4)
         BCT   R5,MORVOL
         LA    R1,BADVOL
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     RETURN
ENDVOL   BR    BALREG
*/*
LOCATE   EQU   *
         LOCATE CAMLOC8
         LTR   R15,R15
         BZ    ENDLOC8
         LA    R1,BADLOC
         L     R0,EIGHTY
         BAL   BALREG,TPUT
         B     RETURN
ENDLOC8  BR    BALREG
*/*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*++          PROGRAM STORAGE / ETC.        COUNTRY                ++*
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*/*
CAMLOC8  CAMLST NAME,ADSN,,CAMWORK
*/*
ADSN     DS    CL44
*/*
EIGHTY   DC    F'80'
*/*
MSG6     DC    CL80'NO INPUT OR INPUT GREATER THAN (6) CHARACTERS'
MSG44    DC    CL80'NO INPUT OR INPUT GREATER THAN (44) CHARACTERS'
BADVOL   DC    CL80'UNIDENTIFIED VOLUME SERIAL NUMBER, REINITIATE'
BADLOC   DC    CL80'DATASET IS NOT CATALOGED, REINITIATE'
*/*
VOLTAB   DS    0F
         DC    C'PROD01PROD02PROD03PROD04PROD05PROD05'
         DC    C'PROD06PROD07PROD08TEST01TEST02TEST03'
         DC    C'TEST04TEST05TSO001TSO002SPOOL1PLPA01'
         DC    C'DLIB01LOCAL1LOCAL2LOCAL3XARESAXARESB'
         DC    C'XASMPESYS001SYS002'
VTLEN    DC    A((*-VOLTAB)/6) .NUMBER OF (6) BYTE ENTRIES
*/*
         LTORG
*/*
CAMWORK  DS    0D,265C
*/*
WHO3LIST DSECT
FUNCTION DS    CL3
         DS    CL1
INLEN    DS    F
RNAME    DS    CL44
*/*
         END
/*
