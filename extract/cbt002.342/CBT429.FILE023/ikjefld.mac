         PRINT GEN
*                                                                     *
*  ALTJES          SCOTT C. DIMOND  1985                              *
*                  SOFTWARE SUPPORT ANALYST                           *
*                  NOVA, AN ALBERTA CORPORATION                       *
*                  CALGARY, ALBERTA, CANADA                           *
*                                                                     *
*  DISCLAIMER:     THE INFORMATION OR MATERIAL BEING PROVIDED,        *
*                  WHETHER IN HARD COPY OR MACHINE READABLE FORM,     *
*                  CARRIES NO WARRANTIES EXPRESSED OR IMPLIED.        *
*                  THIS MATERAIL IS SUPPLIED 'AS IS'.                 *
*                                                                     *
IKJEFLD  CSECT
         USING IKJEFLD,R12       ADDRESS
*
*USREFLD CSECT                    USRELFD IS USED BY ACF2 IKJEFLD
*        USING USREFLD,R12        ADDRESS
*
         SAVE  (14,12),,*        SAVE CALLER'S REGS
         LR    R12,R15           SET BASE
         B     SKIPID
*
         DC    C'ALTJES - S. DIMOND 1985'
         DC    C' ALL RIGHTS RESERVED '
*
SKIPID   LR    R2,R1             SAVE PARM ADDRESS
         GETMAIN RU,LV=SWKLEN    GET SOME STORAGE
         ST    R13,4(R1)         BACKWARD POINTER
         ST    R1,8(R13)         FORWARD POINTER
         LR    R13,R1            ADDRESS SAVE AREA
         USING SAVEAREA,R13      ADDRESS GETMAINED AREA
*
*********************************************************************
*  THIS SECTION OF CODE CHECKS THE LOGON BUFFER FOR THE ALTJES KEYWORD
*  IN THE FORM 'ALTJES(XXXX)'
*  EX.  LOGON USERID ALTJES(JESA)
*
         L     R5,4(,R2)         ADDRESS OF INPUT BUFFER ADDRESS
         L     R6,0(,R5)         ADDRESS INPUT BUFFER
         LH    R5,6(,R5)         ADDRESS INPUT BUFFER LENGTH
         S     R5,=F'5'          FORGET LAST 5 BYTES
         LA    R5,0(R6,R5)       ADDRESS ENDING BYTE
CHECK    CLC   0(6,R6),=C'ALTJES' ALTJES LOGON KEYWORD ?
         BE    ALTJESGO
         LA    R6,1(,R6)         ADDRESS NEXT BYTE
         CR    R6,R5             END OF INPUT ?
         BH    RETURN            YES...ALTJES NOT ENTERED
         B     CHECK
*
ALTJESGO A     R5,=F'5'          ADD FIVE BACK
         LA    R6,6(,R6)         ADDRESS NEXT BYTES
         CR    R6,R5             END OF INPUT ?
         BNL   RETURN            NOT SPECIFIED
         CLI   0(R6),C'('        CORRECT ?
         BNE   RETURN            NO...
         CLI   5(R6),C')'        CORRECT ?
         BNE   RETURN            NO...
         MVC   NEWSSNM,1(R6)     MOVE NAME IN
*
*
         L     R8,CVTPTR         FIND CVT
         USING CVTMAP,R8         ADDRESS CVT
         L     R9,CVTTCBP        TCB POINTER
         L     R9,4(,R9)         OUR TCB
         USING TCB,R9            ADDRESS TCB
         L     R7,TCBJSCB        FIND JOB STEP CONTROL BLOCK
         USING IEZJSCB,R7        JSCB CHAIN
*
         OC    NEWSSNM,=X'40404040' UPPER CASE
         CLC   NEWSSNM,=C'JES2'  PRIMARY ?
         BNE   SETSSNAM          NO...THEN GO SET ALT JES NAME
         MVC   NEWSSNM,=F'0'     YES...THEN ZERO ALT JES NAME
*
SETSSNAM EQU   *
         MODESET  KEY=ZERO
*
*  WARNING JSCRSV01 IS A RESERVED FIELD *
*
         MVC   JSCRSV01,NEWSSNM  NEW SUBSYSTEM NAME
*
         MODESET  KEY=NZERO
*
RETURN   LR    R3,R13            SAVEWORK AREA
         L     R13,4(R13)        CALLER'S SAVE AREA
         FREEMAIN RU,LV=SWKLEN,A=(R3) FREEMAIN
         SR    R15,R15           ZERO RETURN CODE
         RETURN (14,12),RC=(15)  RETURN WITH RC IN R15
*********************************
SAVEWORK DSECT
SAVEAREA DS    18F
NEWSSNM  DS    CL4
SWKLEN   EQU   *-SAVEAREA
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         PRINT OFF,NOGEN
         CVT   DSECT=YES
         IKJTCB
         IEZJSCB
         IKJGTPB
         IKJCPPL
         END
         PRINT GEN
