         MACRO
&NAME    BLINK &BASE=12
         LCLA  &NBASE,&PBASE
         ACTR  4000
&NBASE   SETA  N'&BASE
&PBASE   SETA  N'&BASE
         AIF   (N'&BASE GT 3).ERR1
.LOOP1   ANOP
         AIF   (T'&BASE(&NBASE) NE 'N').ERR2
         AIF   ((&BASE(&NBASE) LT 2)OR(&BASE(&NBASE) GT 12)).ERR3
&NBASE   SETA  &NBASE-1
         AIF   (&NBASE NE 0).LOOP1
&NAME    STM   14,12,12(13)             SAVE REG.14-12.
         LR    &BASE(1),15              BEGINING LOCATION
         AIF   (&PBASE EQ 3).BASE3
         AIF   (&PBASE EQ 2).BASE2
         USING &NAME,&BASE
         AGO   .BASE4
.BASE2   ANOP
         USING &NAME,&BASE(1),&BASE(2)
         LH    &BASE(2),A&SYSNDX
         AR    &BASE(2),&BASE(1)
         B     *+6
A&SYSNDX DC    H'4096'
         AGO   .BASE4
.BASE3   ANOP
         USING &NAME,&BASE(1),&BASE(2),&BASE(3)
         LH    &BASE(2),B&SYSNDX
         LR    &BASE(3),&BASE(2)
         AR    &BASE(2),&BASE(1)
         AR    &BASE(3),&BASE(2)
         B     *+6
B&SYSNDX DC    H'4096'
.BASE4   GETMAIN  R,LV=72,SP=0
         ST    1,8(,13)
         ST    13,4(,1)
         L     0,24(,13)
         LR    13,1
         LR    1,0
         MEXIT
.ERR1    MNOTE 8,'ONLY THREE BASE REG MAY BE USED'
         MEXIT
.ERR2    MNOTE 8,'BASE REG MUST HAVE A CODE VALUE FROM 2-12'
         MEXIT
.ERR3    MNOTE 8,'ONLY REG 2-12 CAN BE USED AS BASE REG'
.COMP    MEND
