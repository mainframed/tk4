***********************************************************************
*DISCLAIMER                                                           *
*  THIS MODULE HAS BEEN USED SUCCESSFULLY AND IS OPERATIONAL AS       *
*  OF 02/87; HOWEVER, NO WARRANTY IS MADE TO THE ACCURACY OF THE      *
*  PROGRAM  OR RELATED MATERIAL AND NO RESPONSIBILITY IS ASSUMED      *
*  FOR ANY MODIFICATION DIRECTLY OR INDIRECTLY CAUSED BY THE USE      *
*  OF THIS PROGRAM. IT IS THE USERS RESPONSIBILITY TO EVALUATE        *
*  THE USEFULLNESS OF THE MATERIAL SUPPLIED.                          *
*                                                                     *
*  WE DO NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP-TO-DATE       *
*  NOR DO WE GUARANTEE TO PROVIDE AND CORRECTIONS OR EXTENSIONS       *
*  DESCRIBED BY THE USER OR CORRECTIONS OR EXTENSIONS MADE IN         *
*  THE FUTURE.                                                        *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*        PROGRAM       - SURVEY                                       *
*        DESCRIPTION   - PERFORMANCE SATISFACTION SURVEYS             *
*        AUTHOR        - O/S UNIT                                     *
*                                                                     *
*        FUNCTION      -                                              *
*                                                                     *
*        MODIFICATIONS -                                              *
***********************************************************************
        EJECT
        SPACE
SURVEY   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT NOGEN
BEGIN    BLINK
*
*        INITIALIZATION LOGIC
*
         ST    R1,PARM
         GETMAIN R,LV=4096
         LR    R8,R1
         USING IREC,R8
         GETMAIN R,LV=200
         LR    R11,R1
         USING SMFR,R11
         LA    R2,SMFJOB
         EXTRACT (2),FIELDS=TIOT
         L     R1,0(R2)
*        CLC   0(2,R1),=C'A2'     SELECT USERID PREFIX
*        BE    MAIN
*        CLC   0(2,R1),=C'O9'
*        BNE   EXIT0
         EJECT
***********************************************************************
*                                                                     *
*        MAIN LOGIC                                                   *
*                                                                     *
***********************************************************************
MAIN     BAL   R9,BEGMSG     OBTAIN SELECTION PARAMETERS
         LTR   R15,R15
         BNZ   EXIT0
         BAL   R9,UPDATE     UPDATE MASTER FILE
         BAL   R9,ENDMSG     UPDATE MASTER FILE
EXIT0    LR    R1,R8
         FREEMAIN R,LV=4096,A=(1)
         LR    R1,R11
         FREEMAIN R,LV=200,A=(1)
         L     R1,PARM
         CALL  IKJEFL00
         RLINK RC=0
         EJECT
         SPACE
***********************************************************************
*                                                                     *
*        MSG1 DISPLAY                                                 *
*                                                                     *
***********************************************************************
BEGMSG   MVC   P,BLANK
MN1      BAL   R14,TPUT0
         CLI   AID,X'F3'   IF PF3 EXIT
         BE    MN1XT4
         CLI   AID,X'7D'   IF NOT ENTER
         BNE   BEGMSG
         OC    ITS,BLANK
         OC    IBS,BLANK
         OC    ITXT,BLANK
         CLI   ITS,C'0'
         BL    MN1ER
         CLI   ITS,C'9'
         BH    MN1ER
         CLI   IBS,C' '
         BE    MN1DOK
         CLI   IBS,C'0'
         BL    MN1ER
         CLI   IBS,C'9'
         BH    MN1ER
MN1DOK   SR    R15,R15
         BR    R9
MN1ER    MVC   P(30),=CL30'INVALID NUMBERIC RESPONSE'
         B     MN1
MN1XT4   LA    R15,4
         BR    R9
***********************************************************************
*                                                                     *
*        UPDATE                                                       *
*                                                                     *
***********************************************************************
UPDATE   DS    0H
         XC    SMFR(110),SMFR
         TIME  BIN
         STCM  R0,15,SMFTME
         STCM  R1,15,SMFDTE
         MVI   SMFR+1,90
         MVI   SMFT,160
         MVI   SMF,1     SUB TYPE CODE
         L     R1,16
         L     R1,X'C4'(R1)
EXT      MVC   SMFSID,16(R1)
         LA    R2,SMFJOB
         EXTRACT (2),FIELDS=TIOT
         L     R1,0(R2)
         MVC   SMFJOB(8),0(R1)
         MVC   SMFTS,ITS
         MVC   SMFBS,IBS
         MVC   SMFTX,ITXT
WSMF     LA    R1,SMFR
         SVC   222
         BR    R9
***********************************************************************
*                                                                     *
*        END MESSAGE                                                  *
*                                                                     *
***********************************************************************
ENDMSG   DS    0H
  MVC   P(60),=CL60'SURVEY COMPLETE.... THANK YOU'
         BAL   R14,TPUT1
         STIMER WAIT,BINTVL=HSEC
         SR    R15,R15
         BR    R9
         EJECT
***********************************************************************
*                                                                     *
*        3270 ROUTINES                                                *
*                                                                     *
***********************************************************************
         SPACE
TPUT0    ST    R14,SAVE14A
         MVC   MLINE2,P
         TPUT  MENU0,M0LEN,FULLSCR
         LA    R1,IN
         TGET  (1),1930,ASIS
         LA    R1,IN
         MVC   AID(1),0(R1)       SAVE 3270 AID
         MVC   MLINE2,BLANK
         L     R14,SAVE14A
         BR    R14
         SPACE
TPUT1    ST    R14,SAVE14A
         MVC   LINE2,P
         TPUT  SCREEN0,160,FULLSCR
         MVC   LINE2,BLANK
         L     R14,SAVE14A
         BR    R14
         SPACE
         EJECT
         EJECT
***********************************************************************
*                                                                     *
*       CONSTANTS                                                     *
*                                                                     *
***********************************************************************
         SPACE
SAVE14A  DS    F
PARM     DS    F
INLEN    DC    F'0'
HSEC     DC    F'500'
AID      DC    C' '
Q        DS    D
X        DS    D
Y        DS    D
Z        DS    D
L        DS    XL16
P        DC    CL133' '
BLANK    DC    CL160' '
         LTORG
*             *
         EJECT
***********************************************************************
*                                                                     *
*       OUTPUT SCREEN AREA                                            *
*                                                                     *
***********************************************************************
MENU0    DS  0H
         DC  X'27F5401140401D68'
MHDR     DC  79C' '
         ORG MHDR
         DC  C'COMPUTER SYSTEM PERFORMANCE SATISFACTION SURVEY'
         ORG
         DC  X'1D60'
MLINE2   DC  CL79' '
         DC  CL80' '
         DC  X'11C2601D60'
         DC  CL40'TSO SATISFACTION LEVEL (0 - 9) =====>'
         DC  X'1D5913'
OTS      DC  CL1' '
         DC  X'1D70'
         DC  X'11C3F01D60'
         DC  CL40'SUBMIT SATISFACTION LEVEL (0 - 9) ==>'
         DC  X'1D59'
OBS      DC  CL1' '
         DC  X'1D70'
         DC  X'11C5401D60'
         DC  CL13'COMMENTS ==> '
         DC  X'1D49'
OTX      DC  CL60' '
         DC  X'1D70'
         DC  X'115A501D68'
 DC  CL79' PLEASE INDICATE YOUR RELATIVE SATISFACTION LEVEL '
 DC  CL80'                                            '
 DC  CL80' (0)- VERY DISASTIFIED  (5) - AVERAGE  (9) - VERY SATISFIED'
M0LEN    EQU *-MENU0
         SPACE 3
SCREEN0  DS  0H
         DC  X'27F5401140401D60'
HDR      DC  79C' '
         ORG HDR
         DC  C'COMPUTER SYSTEM PERFORMANCE SATISFACTION SURVEY'
         ORG
         DC  X'1D68'
LINE2    DC  CL79' '
LINE3    DC  CL80' '
S0LEN    EQU *-SCREEN0
S1LINES  EQU 20
         EJECT
***********************************************************************
*                                                                     *
*       DSECTS                                                        *
*                                                                     *
***********************************************************************
SMFR     DSECT
SMFRDW   DS    F
SMF      DS    0CL106
SMFSTP   DS    BL1     SUBTYPE
SMFT     DS    BL1     TYPE
SMFTME   DS    BL4
SMFDTE   DS    PL4
SMFSID   DS    CL4     SYSID
SMFJOB   DS    CL8     JOBNAME
SMFTS    DS    CL1
SMFBS    DS    CL1
SMFTX    DS    CL60
IREC     DSECT
IN       DS   0CL2048     DEFINES IN
IAID     DS    CL3      AID
ISF      DS    CL3      SF
ITS      DS    CL1      REASON CODE
         DS    CL3      SF
IBS      DS    CL1      HARDWARE CODE
         DS    CL3      SF
ITXT     DS    CL60     TEXT
         ORG
         END
