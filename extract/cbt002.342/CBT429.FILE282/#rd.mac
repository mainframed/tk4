***********************************************************************
*                                                                     *
*              .RD (READ TERMINAL) --- POST A TERMINAL READ           *
*                                                                     *
***********************************************************************
         SPACE
RD      $ENTER ,
        $IFON  TTYPE,TONLINE,RD01  BRANCH IF ONLINE
        $CALL  SP                  OFFLINE PROCESS LIKE .SP
         B     RD05                GO TO EXIT
         SPACE
RD01     DS    0H
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET COUNT FROM ARGUMENT
         BC    BNEG+BDNO,RDE005    ERROR IF NEGATIVE OR INVALID
         LTR   COUNT,COUNT         TEST RESULT
         BNP   RD05                BRANCH IF NOT POSITIVE
         SPACE
         ST    COUNT,CONSMAX       STORE COUNT IN PARM BLOCK
         XC    CONSFLAG,CONSFLAG   CLEAR CONSOLE FLAGS
        $ON    CONSFLAG,IO$JIGG    SPECIFY 'JIGGLE' TYPE BALL
        $ON    CONSFLAG,IO$UEOF    IGNORE ALL EOFS
        $ON    CONSFLAG,IO$IM      AND IMBED THIS FILE
         MVC   CONSGOTO,BLANKS     CLEAR GOTO TARGET
         SPACE
         LA    R1,CONSBLOK         'IMBED' THE CONSOLE FILE
        $CALL  RDFILE              CALL INPUT SUPERVISOR
         SPACE
RD02     DS    0H
         LH    TEMP,PLCT           ACCOUNT FOR RD SPACES
         BCTR  TEMP,0              ..
         CH    TEMP,RMTRIG         SHOULD REMOTE BE TRIGGERED .Q
         BNE   RD03                IF NOT, SKIP
         LH    R1,RMTRIG           REMOTE TO IMBED
        $CALL  RMIMBED             YES, GO IMBED THE REMOTE SEQUENCE
         B     RD02                BACK TO ACCOUNT FOR SPACE
RD03     DS    0H
         LH    R1,BMMRG            CURRENT BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    TEMP,R1             BELOW BOTTOM MARGIN
         BNL   RD04                IF NOT, SKIP AHEAD
        $CALL  PAGE                CAUSE PAGE EJECT
         B     RD02                GO DECREMENT LINE CTR
         SPACE
RD04     DS    0H
         STH   TEMP,PLCT           STORE UPDATED LINE COUNT
        $CALL  TERMREAD            READ ONE LINE
         CLI   1(R1),X'00'         IS IT A NULL RECORD .Q
         BNE   RD02                IF NOT, TAKE LINE INTO ACCOUNT
RD05     DS    0H
        $EXIT  RC=0                BACK TO MAIN CONTROL
         SPACE
RDE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     RD05                GO TO EXIT
