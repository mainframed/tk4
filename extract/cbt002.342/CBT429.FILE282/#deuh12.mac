*                             ********
* *            SOURCE BOOK *** DEUH12 ***  ALPHA SCAN ROUTINE
*                             ********
         SPACE 3
*
*        ***** INITIAL ALPHA SCAN  RESOLVE 'Y' & ISOLATE WORDS WITH
*        *****  LESS THAN 4 CHARS OR LASS THAN 2 VOWELS
*
ASCAN    OI    HYFSW,X'08'         SET EOW SWITCH ON TO INITIALIZE
         LA    REGWK3,0
         LH    REG4,MRGCNT         ESTABLISH CHARACTER COUNT
         LA    REG3,ASTATUS
FRSTSIG  LA    REG1,0(REGWK3,REG3) START AS BEGINNING OF ASTATUS FIELD
         EX    REG4,IASCN          LOOK FOR 1ST SIGNIF CHARACTER
         BC    8,NOHYP             NO VOWELS IN WORD
         STC   REG2,SIGCHR         SAVE INDICATOR FOR CHARACTER TYPE
         TM    SIGCHR,X'02'             TEST CHAR FOR Y
         BZ    SCEND                    IF NOT BYPASS 1ST Y PROCESSING
         LA    REG2,ASTATUS             SEE IF CHAR.IS 1ST IN WORD
         CR    REG2,REG1
         BE    FRSTY                    IF NOT 1ST CHAR IN WORD
         NI    0(REG1),X'FD'            SET STATUS TO VOWEL
         B     SCEND                    & EXIT
FRSTY    TM    1(REG1),X'01'            IF 1ST CHAR IN WORD TEST FOLLOW
         BZ    SETFYVL                  CHAR & BRANCH IF IT IS CONSON.
         NI    0(REG1),X'FE'            ELSE SET STATUS OF Y TO CONSON
         B     SCEND                    & EXEI
SETFYVL  NI    0(REG1),X'FD'            SET STATUS OF Y TO VOWEL
SCEND    LA    REGWK3,1(REG1)
         SR    REGWK3,REG3         SUBT START ADDR FOR COUNT
         SR    REG4,REGWK3         AND DECREMENT NUMBER OF CHARS
         BC    12,EOW              IF ZERO OR MINUS - END OF WORD
         LA    REG3,1(REG1)
         LR    REG1,REG3
         EX    REG4,IASCN          TRT FOR OTHER VOWELS OR 'Y'
         BC    8,EOW               AT END OF WORD BRANCH
         STC   REG2,SIGCHR         SAVE SIGNIF CHAR
         TM    SIGCHR,X'02'        TEST FOR ' Y' CHARACTER
         BO    YCHAR                BRANCH YES
EOWOFF   NI    HYFSW,X'F7'         TURN OFF E-O-W SWITCH
         B     SCEND
YCHAR    LR    REGWK1,REG1         GET ADDR OF ' Y ' FOR WORK
         BCT   REGWK1,*+4               BACK UP TO PRECEEDING CHAR.
         TM    0(REGWK1),X'01'      & TEST FOR VOWEL
         BZ    SETYVL              IF NOT VOWEL THEN SET Y TO VOWEL
         NI    0(REG1),X'FE'        ELSE SET Y TO CONSONANT
         B     SCEND               AND RETURN FOR CONTINUATUIN OF SERCH
SETYVL   NI    0(REG1),X'FD'       SET Y TO VOWEL
         B     EOWOFF
EOW      TM    HYFSW,X'08'         TEST FOR MORE THAN ONE VOWEL IN
         BO    NOHYP                WORD & IF NOT GO OT NO-HYPHEN EXIT
         LH    REGWK1,NUMCHR       COMPARE NUMBER OF CHARACTERS IN
         CH    REGWK1,=H'4'         WORD & IF LESS THAN 4 CHARS
         BL    NOHYP                    REJECT WORD SEGMENT
