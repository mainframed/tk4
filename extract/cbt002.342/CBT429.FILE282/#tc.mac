***********************************************************************
*                                                                     *
*              .TC (TABLE OF CONTENTS) --- CONTROL                    *
*                                                                     *
***********************************************************************
         SPACE
TC      $ENTER ,
        $CALL  PRINTNB             CLEAR ANY PRINT DATA
        $CALL  GETYESNO            GET AND TEST FIRST OPERAND
         BC    BPOS,TC07           BRANCH IF POSITIVE
         BC    BNEG,TC10           BRANCH IF NEGATIVE
         CLI   YNRESULT,YNDEL      WAS OPERAND 'DELETE' .Q
         BE    TC13                BRANCH IF YES
         CLI   YNRESULT,YNADD      WAS OPERAND 'ADD' .Q
         BE    TC14                BRANCH IF YES
         CLI   YNRESULT,YNASTK     WAS OPERAND '*' .Q
         BE    TC01                BRANCH IF YES
         SPACE
         ST    COUNT,ARGSTART      RESCAN FOR NUMERIC
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    BDNO,TCE005         ERROR IF INVALID
         BC    15-BOMIT,TC02       BRANCH IF NOT OMITTED
TC01     DS    0H
         LA    COUNT,1             ONE PAGE BY DEFAULT
TC02     DS    0H
         L     X1,DHNUMTC          TABLE OF CONTENTS NUMBER
         SLL   X1,2                TIMES FOUR
         L     X1,DHANCH(X1)       CURRENT HEADER DESCRIPTOR
         LTR   X1,X1               PRESENT .Q
         BZ    TCE012              HOW DID WE EVER GET HERE .Q
         LA    X1,DHDLEN(,X1)      ITEM NUMBER ONE
         USING DHDSECT,X1          HEAD DESCRIPTOR LIKE THIS
        $IFON  SWITCHS,FEMPTY,TC03 BRANCH IF PAGE EMPTY
        $IFOFF DHFLAGS,DHPA,TC03   BRANCH IF LEVEL ONE DOESN'T EJECT
        $CALL  PAGEREAL            START AT TOP OF PAGE
TC03     DS    0H
         A     COUNT,PACNTI        ADD CURRENT PAGE NUMBER
         BNP   TCE014              BRANCH IF TOO SMALL
         CLI   YNRESULT,YNASTK     WAS FIRST OPERAND '*' .Q
         BNE   TC04                BRANCH IF NOT
         SR    COUNT,COUNT         ZERO PAGE NUMBER (NORMALLY INVALID)
TC04     DS    0H
         ST    COUNT,TCPAGE        THAT IS WHERE WE WILL BE
         SPACE
         L     R1,BUFF3AD          WORKING BUFFER
         LA    R1,4(,R1)           BUMP OVER RDW
         MVC   0(L'TCON,R1),TCON   THIS WILL GET US GOING
         MVC   0(1,R1),PERIND      MAKE IT A CONTROL WORD
         LA    TEMP,L'TCON-1       LENGTH-1
        $CALL  HIDBADD             STACK SAME LIFO
         SPACE
        $CALL  USARG               ISOLATE OPERAND LINE
         BC    BOMIT,TC05          BRANCH IF NONE
         LH    COUNT,BUFF2LGZ      LENGTH OF OPERAND
         LA    TEMP,BUFF2          POINT TO SAME
         B     TC06                CONTINUE
TC05     DS    0H
         LA    COUNT,L'TCCONTS     DEFAULT LENGTH
         LA    TEMP,TCCONTS        DEFAULT STRING
TC06     DS    0H
         L     R1,BUFF3AD          WORKING BUFFER
         LA    R1,4(,R1)           BUMP OVER RDW
         BCTR  COUNT,0             LENGTH-1 OF TITLE
         EX    COUNT,TCCMVC1       MOVE TITLE
         MVC   BUFF2,BLANKS        CLEAR INPUT BUFFER
        $MVC   BUFF2,TCHL1         DUMMY HEAD LEVEL
         MVC   BUFF2(1),PERIND     CONTROL WORD INDICATOR
         EX    COUNT,TCCMVC2       MOVE TITLE BACK
         LA    COUNT,L'TCHL1+1(,COUNT)  RESULT LENGTH
         STH   COUNT,BUFF2LGZ      SET LENGTH IN INPUT BUFFER
        $CALL  ARGINIT             INITIALIZE FOR SCAN
         SPACE
         MVC   TCDHSAVE,DHDSECT    SAVE ALL
        $OFF   DHFLAGS,(DHTO,DHTC,DHPA) I'LL DO IT MY WAY
        $CALL  HL                  PRINT TITLE
        $MVC   DHDSECT,TCDHSAVE    RESTORE HEADER DESCRIPTOR
         DROP  X1
         B     TC17                RETURN TO PROCESS HEAD LEVEL
         SPACE
TC07     DS    0H
        $IFON  SWITCHS,TCSWS,TCE012     TEST INVALID PLACEMENT
         L     R1,BUFF3AD          WORKING BUFFER
         LA    R1,4(,R1)           SKIP THE RDW
         MVC   0(L'TCOFF,R1),TCOFF THIS TERMINATES .TC
         MVC   0(1,R1),PERIND      CONTROL WORD INDICITOR
         LA    TEMP,L'TCOFF-1      LENGTH-1 OF DATA
         LNR   R1,R1               ENTER 'FIFO'
         SPACE
         L     X3,DHNUMTC          TABLE OF CONTENTS NUMBER
         SLL   X3,2                TIMES FOUR
         LA    X2,TCANCH(X3)       .TC LINE ANCHOR
         L     X1,HIDBANCH         RETAIN HIDDEN BUFFERS
         MVC   HIDBANCH,0(X2)      ENTER CONTENTS LINES INSTEAD
         XC    0(L'TCANCH,X2),0(X2)     CLEAR CONTENTS ANCHOR
        $CALL  HIDBADD             ENTER CONTENTS TERMINATOR
         SPACE
         LA    X3,HIDBANCH+HIDBCHN-HIDB START OF HIDDEN CHAIN
         USING HIDB,X3             AND TELL THE ASSEMBLER
TC08     DS    0H
         OC    HIDBCHN,HIDBCHN     END OF CONTENTS CHAIN .Q
         BZ    TC09                BRANCH IF YES
         L     X3,HIDBCHN          NEXT ENTRY
         B     TC08                GO AND TRY AGAIN
TC09     DS    0H
         ST    X1,HIDBCHN          ADD FORMER HIDDEN BUFFER LINES
         DROP  X3
         SPACE
        $CALL  SA                  SAVE FORMATTING ENVIRONMENT
        $OFF   SWITCHS,NFSWS       CONCATENATE ON
         MVI   JUVAL,JUVNEG        AND NO JUSTIFY
         L     R1,BUFF3AD          WORKING BUFFER
         LA    R1,4(,R1)           SKIP THE RDW
         MVC   0(L'TCTBSET,R1),TCTBSET  DEFINE CONTENTS TAB
         MVC   0(1,R1),PERIND      CONTROL WORD INDICATOR
         MVC   4(1,R1),PERIND      AND AGAIN
         LA    TEMP,L'TCTBSET-1    LENGTH OF THE STRING
        $CALL  HIDBADD             ADD LIFO ON CHAIN
        $ON    SWITCHS,TCSWS       NOW IN TABLE OF CONTENTS MODE
         B     TC17                AND GO TO RETURN
         SPACE
TC10     DS    0H
        $IFOFF SWITCHS,TCSWS,TCE012     TEST INVALID PLACEMENT
        $OFF   SWITCHS,TCSWS       NO LONGER IN TABLES OF CONTENTS
        $IFON  SWITCHS,FEMPTY,TC11 BRANCH IF PAGE EMPTY
        $IFOFF TCDHSAVE+DHFLAGS-DHDSECT,DHPA,TC11 IF .DH 1 NOPAGE
        $CALL  PAGE                TERMINATE PAGE OR COLUMN
TC11     DS    0H
        $CALL  RE                  RESTORE FORMATTING ENVIRONMENT
         L     COUNT,TCPAGE        POTENTIAL NEXT PAGE NUMBER
         LTR   COUNT,COUNT         IS IT VALID .Q
         BNP   TC12                BRANCH IF NOT
         ST    COUNT,PACNTI        SET NEXT PAGE NUMBER
TC12     DS    0H
         B     TC17                GO TO RETURN
         SPACE
TC13     DS    0H
         L     X3,DHNUMTC          TABLE OF CONTENTS NUMBER
         SLL   X3,2                MULT BY FOUR
         LA    R1,TCANCH(X3)       POINT TO DATA ANCHOR
        $CALL  HIDBPUR             AND DELETE IT ALL
         B     TC17                GO TO RETURN
         SPACE
TC14     DS    0H
         L     X3,DHNUMTC          TABLE OF CONTENTS NUMBER
         SLL   X3,2                MULT BY FOUR
         LA    X1,TCANCH-HIDBCHN+HIDB(X3)  POINT TO DATA ANCHOR
         SPACE
         USING HIDB,X1             TELL THE ASSEMBLER
TC15     DS    0H
         OC    HIDBCHN,HIDBCHN     END OF THE LINE .Q
         BZ    TC16                BRANCH IF YES
         L     X1,HIDBCHN          NEXT ENTRY
         B     TC15                TRY AGAIN
TC16     DS    0H
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    BNEG,TCE014         BRANCH IF TOO SMALL
         BC    BOMIT,TC17          ALL DONE IF NOTHING
         LA    R0,DHMAXTC          MAXIMUM ALLOWED
         CR    COUNT,R0            TOO BIG .Q
         BNL   TCE013              ERROR IF TOO HIGH
         C     COUNT,DHNUMTC       ADDING THE CURRENT .Q
         BE    TC16                YES, FORGET IT
         SLL   COUNT,2             MULT BY FOUR
         LA    COUNT,TCANCH(COUNT) POINT TO NEW
         MVC   HIDBCHN,0(COUNT)    NEW TO OLD
         XC    0(L'HIDBCHN,COUNT),0(COUNT)  CLEAR OLD CHAIN
         B     TC15                GET NEW END AND CONTINUE
         SPACE
TC17     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
TCE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     TC17                GO TO RETURN
TCE012   DS    0H
         MVI   EFLAG012,ERRSET     INVALID PLACEMENT
         B     TC17                GO TO RETURN
TCE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     TC17                GO TO RETURN
TCE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     TC17                GO TO RETURN
         SPACE
TCCMVC1  MVC   0(*-*,R1),0(TEMP)   SAVE LINE OPERAND
TCCMVC2  MVC   BUFF2+L'TCHL1(*-*),0(R1) RESTORE LINE OPERAND
TCCONTS  DC    C'CONTENTS'         DEFAULT CONTENTS TITLE
TCHL1    DC    C'.HL 1 '           DUMMY HEAD LEVEL ONE
TCON     DC    C'.TC ON'           TO INITIATE TABLE OF CONTENTS
TCOFF    DC    C'.TC OFF'          TO TERMINATE TABLE OF CONTENTS
TCTBSET  DC    C'.UR .TB ''&&SYSTCTB''&&SYSLL.R'  RIGHT CONTENTS TAB
