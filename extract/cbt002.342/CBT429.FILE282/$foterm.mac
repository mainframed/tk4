         MACRO
        $FOTERM &HORIZ=,&VERT=,&SUP=,&SUB=,                            *
               &TAB=,&VMI=,&HMI=,&NLF=
         LCLA      &I
FOT&SYSNDX DC  (TDEVLEN/4)F'0'      INITIAL SETTING TO ZERO
         AIF   ('&HORIZ' EQ '').SKIP01
         ORG   FOT&SYSNDX+TDEVRESH-TDEVBEG
         DC    A(&HORIZ)           HORIZONTAL RESOLUTION
.SKIP01  AIF   ('&VERT' EQ '').SKIP02
         ORG   FOT&SYSNDX+TDEVRESV-TDEVBEG
         DC    A(&VERT)            VERTICAL RESOLUTION
.SKIP02  AIF   ('&SUP' EQ '').SKIP03
         ORG   FOT&SYSNDX+TDEVSUP-TDEVBEG
         DC    AL1(FOT&SYSNDX.A-*-2)
         DC    AL1&SUP             SUPERSCRIPT
FOT&SYSNDX.A EQU *
.SKIP03  AIF   ('&SUB' EQ '').SKIP04
         ORG   FOT&SYSNDX+TDEVSUB-TDEVBEG
         DC    AL1(FOT&SYSNDX.B-*-2)
         DC    AL1&SUB             SUBSCRIPT
FOT&SYSNDX.B EQU *
.SKIP04  AIF   ('&TAB' EQ '').SKIP05
         ORG   FOT&SYSNDX+TDEVTAB-TDEVBEG
         DC    AL1(FOT&SYSNDX.C-*-2)
         DC    AL1&TAB             TAB TO COLUMN
FOT&SYSNDX.C EQU *
.SKIP05  AIF   ('&VMI' EQ '').SKIP06
         ORG   FOT&SYSNDX+TDEVVMI-TDEVBEG
         DC    AL1(FOT&SYSNDX.D-*-2)
         DC    AL1&VMI             VERTICAL MOVEMENT
FOT&SYSNDX.D EQU *
.SKIP06  AIF   ('&HMI' EQ '').SKIP07
         ORG   FOT&SYSNDX+TDEVHMI-TDEVBEG
         DC    AL1(FOT&SYSNDX.E-*-2)
         DC    AL1&HMI             HORIZONTAL MOVEMENT
FOT&SYSNDX.E EQU *
.SKIP07  AIF   ('&NLF' EQ '').SKIP08
         ORG   FOT&SYSNDX+TDEVNLF-TDEVBEG
         DC    AL1(FOT&SYSNDX.F-*-2)
         DC    AL1&NLF             NEGATIVE LINE FEED
FOT&SYSNDX.F EQU *
.SKIP08  ANOP
&I       SETA  1
.SKIP09  AIF   (&I GT N'&SYSLIST).SKIP11
         AIF   ('&SYSLIST(&I)' EQ '').SKIP10
         ORG   FOT&SYSNDX+TDEVAESC-TDEVBEG+&SYSLIST(&I,1)
         DC    AL1(&SYSLIST(&I,2)-1)    LENGTH-1 OF ESCAPE STRING
.SKIP10  ANOP
&I       SETA  &I+1
         AGO   .SKIP09
.SKIP11  ANOP
         ORG   ,
         MEND
