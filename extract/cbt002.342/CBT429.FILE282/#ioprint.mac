***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'IOPRINT' --- OUTPUT LINE            *
*                                                                     *
***********************************************************************
         SPACE
IOPRINT $ENTER ,
IOPRI01  DS    0H
        $IFOFF SWITCHS,FEMPTY,IOPRI05   BRANCH IF PAGE NOT EMPTY
         SPACE
        $SAVE  ,                   PRINT HEADER LINE AND MARGIN FIRST
         LH    X1,PLCT             WE KNOW THAT WE'RE REALLY AT THE
         MVC   PLCT,PLZ            SET LINES LEFT ON EMPTY PAGE
        $CALL  PAGETOP             PRINT TOP TITLES AND FLOATING KEEPS
        $CALL  PRSKSPC             PROCESS ANY EXTRA SKIP/SPACE REQUEST
        $CALL  RMNEXT              SETUP FOR NEXT REMOTE
         CLC   PLCT,RMTRIG         REMOTE SET FOR PAGE TOP .Q
         BNE   IOPRI02             IF NOT, SKIP
         LH    R1,RMTRIG           REMOTE TO IMBED
        $CALL  RMIMBED             YES, TRIGGER IT
IOPRI02  DS    0H
         LH    TEMP,PLZ            DO THE SUBTRACTION THAT OUR CALLER
         SR    TEMP,X1             DID FOR THE PAGE LENGTH COUNTER
         SH    TEMP,PLCT           ..
         LPR   TEMP,TEMP           ..
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    TEMP,R1             IS PAGE EXACTLY FULL .Q
         BNL   IOPRI03             BRANCH IF NOT
        $CALL  PAGE                FINISH OFF THIS PAGE
         STH   X1,PLCT             RETAIN CALLER'S ORIGINAL VALUE
         B     IOPRI04             AND GO START THE NEXT
IOPRI03  DS    0H
         STH   TEMP,PLCT           SET NEW LINES LEFT ON THIS PAGE
        $CALL  RMNEXT              SETUP FOR NEXT REMOTE (IF ANY)
IOPRI04  DS    0H
        $RESTOR ,                  RESTORE REGISTERS
         B     IOPRI01             TRY AGAIN TO PRINT THE LINE
         SPACE
IOPRI05  DS    0H
        $IFON  SWITCHS,OOSWS,IOPRI07    BRANCH IF DISABLED
         L     R1,OOANCH           OUTPUT OVERLAY ANCHOR
         LTR   R1,R1               ANYTHING PRESENT .Q
         BZ    IOPRI06             BRANCH IF NOT
        $CALL  OOPRT               PROCESS OUTPUT OVERLAY
IOPRI06  DS    0H
         L     R1,BXPTR            BOX BUILDING POINTER
         LTR   R1,R1               ANYTHING PRESENT .Q
         BZ    IOPRI07             BRANCH IF NOT
        $CALL  BXVERT              PRINT SIDES OF BOX
         SPACE
IOPRI07  DS    0H
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   IOPRI08             BRANCH IF NOT
        $CALL  COLSAVE             SAVE THIS COLUMN OF DATA
         B     IOPRI12             AND GO TO RETURN
         SPACE
IOPRI08  DS    0H
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS THE LAST PASS .Q
         BNE   IOPRI12             GO TO RETURN NOW IF NOT
        $IFON  SWITCHS,NPSWS,IOPRI12    RETURN IF NO PRINT MODE
         SPACE
         L     R1,DEFLNUM+TAB3DEF  GET LINE NUMBER COLUMN OFFSET
         LTR   R1,R1               IS IT POSITIVE .Q
         BNP   IOPRI09             BRANCH AROUND IF NOT
        $CALL  PRLIN               ENTER LINE NUMBER INTO OUTPUT
         SPACE
IOPRI09  DS    0H
        $IFON  TTYPE,TONLINE,IOPRI10    PRINT DIRECT IF ONLINE
         LR    R1,R0               RECORD ADDRESS
         CLI   4(R1),SUPRSPAC      A PRINTER OVERLAY LINE .Q
         BE    IOPRI10             NO CHANNEL SKIPS IF YES
         LH    X1,PLZ              PAGE LENGTH
         SH    X1,PLCT             LESS LINES LEFT ON PAGE
         C     X1,DEFFFTO+TAB3DEF  TOP OF FORM LINE .Q
         BNE   IOPRI10             BRANCH IF NOT
         MVC   4(1,R1),DEFFFCH+TAB3DEF+3     SKIP TO CHANNEL
         OI    4(R1),C'0'          CHARACTER VALUE
IOPRI10  DS    0H
         LH    X1,ADJCEVN          EVEN PAGE ADJUST
         AH    X1,ADJCODD          PLUS ODD PAGE ADJUST
         BZ    IOPRI11             BRANCH IF NOTHING
        $CALL  IOADJ               ADJUST THIS LINE FURTHER
         SPACE
IOPRI11  DS    0H
        $CALL  SCRIPUT             GO AND PRINT THE LINE
         SPACE
IOPRI12  DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN
