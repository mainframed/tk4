***********************************************************************
*                                                                     *
*              .CE (CENTER) --- CONTROL                               *
*                                                                     *
***********************************************************************
         SPACE
CE      $ENTER ,
        $ON    SWITCHS,CESWS       SET CENTERING SWITCH
        $CALL  CELARA              SCAN AND SET ARGUMENT
        $EXIT  RC=(R15)            AND RETURN TO CALLER
         SPACE
CELARA  $ENTER ,
        $CALL  PRINTNB             CLEAR OUT PRINT LINE
         L     X1,ARGSTART         RETAIN ARG LINE POINTER
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,CE01          BRANCH IF NULL, ASSUME ONE
         BC    BPOS,CE02           AFFIRMATIVE IS FOREVER
         BC    BNEG,CE03           NEGATIVE FINISHES IT
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              IT IS PROBABLY NUMERIC
         BC    BPOS,CE04           POSSIBLY VALID IF POSITIVE OR ZERO
         ST    X1,ARGSTART         ELSE MUST BE A 'LINE' ARGUMENT
         B     CE04                JOIN 'LINE' TEST
         SPACE
CE01     DS    0H
         LA    COUNT,1             ONE LINE BY DEFAULT
         B     CE04                ENTER VALUE FOR CE/LA/RA
CE02     DS    0H
         LH    COUNT,H32767        LARGE POSITIVE NUMBER
         B     CE04                ENTER VALUE FOR CE/LA/RA
CE03     DS    0H
         SR    COUNT,COUNT         ZERO VALUE
CE04     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
         LR    R1,COUNT            RETAIN LINE COUNT
        $CALL  GETARG              ANY FURTHER TEXT .Q
         LA    R15,0               ASSUME ONLY SINGLE VALID ARG
         BC    BOMIT,CE05          NONE, SINGLE VALID ARG ONLY
         ST    X1,ARGSTART         ORIGINAL ARG STARTS HERE
        $CALL  USARG               MOVE ARG LINE INTO BUFFER
         LA    R15,WRETNR          PROCESS RESULTING LINE
         LA    R1,1                COUNT OF LINE TO PROCESS
CE05     DS    0H
         STH   R1,CECNT            ENTER CE/LA/RA VALUE
        $EXIT  RC=(R15)            AND GET NEXT LINE
