***********************************************************************
*                                                                     *
*                             LKINIT                                  *
*                                                                     *
*        ROUTINE TO INITIALIZE THE LINK ELEMENT FREE STORAGE LIST.    *
*                                                                     *
***********************************************************************
         SPACE
*        ON EXIT  --  CC = BPOS    IF SUCCESSFUL
*                     CC = BNEG    IF ERROR
         SPACE
LKINIT  $ENTER ,
LKINIT01 DS    0H
         L     PCHAR,LKANCH        LINK CHARACTER ANCHOR
         LTR   PCHAR,PCHAR         FIRST TIME .Q
         BNZ   LKINIT02            BRANCH IF NOT
        $CALL  LKADD               ADD ONE BLOCK
         BC    BPOS,LKINIT01       IF SUCCESSFUL, TRY AGAIN
         MVI   EFLAG020,ERRTERM    SET ERROR MESSAGE
        $CC    NEG                 SET ERROR CONDITION CODE
         B     LKINIT05            AND GO TO RETURN
         SPACE
LKINIT02 DS    0H
         XC    LKANCH,LKANCH       CLEAR CHARACTER BLOCK ANCHOR
         XC    LKASIZE,LKASIZE     AND CHARACTER COUNT
         XC    LKSTAR,LKSTAR       CLEAR START OF FREE LIST
         XC    LKSIZE,LKSIZE       CLEAR COUNT OF FREE LIST
LKINIT03 DS    0H
         LTR   PCHAR,PCHAR         AT THE END .Q
         BZ    LKINIT04            BRANCH IF YES
         USING LARAELEM,PCHAR      CHARACTER HEADER BLOCK
        $CALL  LKCHAIN             CHAIN ELEMENTS OF THIS BLOCK
         L     PCHAR,LARAFOR       NEXT CHARACTER BLOCK HEADER
         B     LKINIT03            TRY AGAIN
         DROP  PCHAR
LKINIT04 DS    0H
        $CC    POS                 INDICATE SUCCESS
         SPACE
LKINIT05 DS    0H
        $EXIT  RC=0                RETURN TO CALLER
