***********************************************************************
*                                                                     *
*              OVERLAY CHARACTER ON INPUT                             *
*                                                                     *
***********************************************************************
         SPACE
MAINOC  $ENTER ,
         LH    R15,OCCNT           GET OVERLAY TEXT LINE COUNT
         S     R15,F1              LESS ONE
         BM    MAINOC08            BRANCH IF NONE LEFT
         STH   R15,OCCNT           UPDATE WITH REMAINDER
         CLI   OCCHAR,C' '         ANY OVERLAY CHARACTER .Q
         BE    MAINOC08            BRANCH IF NONE
         LH    TEMP,BUFF2LGZ       LENGTH OF INPUT TEXT LINE
         LTR   TEMP,TEMP           ANY DATA PRESENT TO CHECK .Q
         BNP   MAINOC08            BRANCH IF NONE
         LA    R0,B2LG             MAX OUTPUT LENGTH ALLOWED
         SPACE
MAINOC01 DS    0H
         LA    R1,BUFF2-1(TEMP)    LAST CHAR OF LINE
         CLI   0(R1),C' '          IS IT A BLANK .Q
         BE    MAINOC06            YES, NO OVERLAY CHARACTER
         CLI   0(R1),X'05'         IS IT A TAB .Q
         BE    MAINOC06            YES, NO OVERLAY CHARACTER
         CLI   0(R1),X'16'         IS IT A BACKSPACE .Q
         BE    MAINOC06            YES, NO OVERLAY CHARACTER
         CLC   0(1,R1),OCCHAR      IS IT OVERLAY CHARACTER .Q
         BE    MAINOC06            YES, NO OVERLAY CHARACTER
         SR    COUNT,COUNT         ZERO WORK REG
         IC    COUNT,OCEXCEP       GET EXCEPTION COUNT
         CLI   OCEXCEP,X'FF'       IS EXCEPTION COUNT FOR .UL .Q
         BNE   MAINOC02            BRANCH IF NOT
         CLI   0(R1),C'A'-X'40'    LESS THAN LOWER 'A' .Q
         BL    MAINOC06            YES, NO OVERLAY
         CLI   0(R1),C'9'          MORE THAN '9' .Q
         BH    MAINOC06            YES, NO OVERLAY
         B     MAINOC03            ELSE OVERLAY IT
MAINOC02 DS    0H
         S     COUNT,F1            DECREMENT COUNT BY ONE
         BM    MAINOC03            BRANCH OUT IF DONE
         LA    R15,OCEXCEP+1(COUNT)  EXCEPTION CHAR TO TEST
         CLC   0(1,R1),0(R15)      AN EXCEPTION CHAR .Q
         BE    MAINOC06            YES, NO OVERLAY
         B     MAINOC02            AND TRY AGAIN
MAINOC03 DS    0H
         LH    COUNT,BUFF2LGZ      LENGTH OF CURRENT BUFFER
         LA    COUNT,2(,COUNT)     BUMP BY TWO
         CR    COUNT,R0            RESULT TOO BIG .Q
         BH    MAINOC07            BRANCH OUT IF YES
         STH   COUNT,BUFF2LGZ      UPDATE WITH NEW LENGTH
         LA    COUNT,BUFF2-3(COUNT)  LAST CHARACTER OF BUFFER
         SR    COUNT,R1            REMAINDER LENGTH-1
         L     R15,BUFF3AD         WORK BUFFER ADDRESS
         EX    COUNT,MAINOCSV      SAVE REMAINDER
         MVI   1(R1),X'16'         BACKSPACE
         CLI   OCCHAR,X'00'        CHARACTER ITSELF REQUIRED .Q
         BE    MAINOC04            BRANCH IF YES
         MVC   2(1,R1),OCCHAR      OVERLAY CHARACTER
         B     MAINOC05            GO TO RESTORE REMAINDER
MAINOC04 DS    0H
         MVC   2(1,R1),0(R1)       THE CHARACTER ITSELF
MAINOC05 DS    0H
         EX    COUNT,MAINOCRE      RESTORE REMAINDER
MAINOC06 DS    0H
         BCT   TEMP,MAINOC01       CHECK ALL CHARACTERS
         B     MAINOC08            GO TO RETURN
MAINOC07 DS    0H
         MVI   EFLAG044,ERRWARN    SOMETHING HAS BEEN LOST
MAINOC08 DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
MAINOCSV MVC   4(*-*,R15),1(R1)    SAVE BUFFER REMAINDER
MAINOCRE MVC   3(*-*,R1),4(R15)    RESTORE BUFFER REMAINDER
