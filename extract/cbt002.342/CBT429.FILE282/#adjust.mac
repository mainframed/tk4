***********************************************************************
*                                                                     *
*                             ADJUST                                  *
*                                                                     *
*              ADJUST THE LINE TO THE RIGHT MARGIN.                   *
*                                                                     *
*        REGISTER USAGE:                                              *
*                                                                     *
*        M     MULTIPLIER                                             *
*        P     CHARACTER BLOCK POINTER                                *
*        W     1/2 SPACE = NUMBER OF INTER-WORD SPACES                *
*        R     FRACTIONAL LEFTOVER                                    *
*        C     STANDARD NUMBER OF ADDITIONAL SPACES NEEDED            *
*        Q     (ACCUMULATED FRACT)-(1/2 SPACE)                        *
*        A     ADJUSTMENT AMOUNT REMAINING                            *
*                                                                     *
*        METHOD:                                                      *
*                                                                     *
*              STANDARD ADJUSTMENT = (FOLL-FONOCHAR)/FOIWSPC          *
*              FRACTION = REM(FOLL-FONOCHAR,FPIWSPC)                  *
*                                                                     *
*        WHEN FRACTIONS ACCUMULATE TO MORE THAN 1/2 SPACE, AN EXTRA   *
*        BLANK IS ADDED, AND A WHOLE SPACE SUBTRACTED FROM THE        *
*        ACCUMULATED FRACTION.                                        *
*                                                                     *
***********************************************************************
         SPACE
ADJUST  $ENTER ,
         USING FOPMLIST,R1
         USING LINKELEM,P
         CLC   FOPCOUNT,F0         IS LINE NULL .Q
         BE    ADJRET              YES, DO NOTHING
         L     A,FOLL              CURRENT LINE LENGTH
         AH    A,FOINDR            PLUS RIGHT INDENT VALUE
         S     A,FONOCHAR          CALCULATE ADJUSTMENT VALUE
         BNP   ADJRET              IF ADJUSTMENT = 0, DO NOTHING
         SR    R15,R15             ZERO WORK REGISTER
         IC    R15,FOJUVAL         GET JUSTIFY OPTION VALUE
         B     *+4(R15)            INDEXED BRANCH
         SPACE
         B     ADJRET              NO JUSTIFY
         B     ADJHALF             HALF JUSTIFY RIGHT AND LEFT
         B     ADJPOS              JUSTIFY RIGHT AND LEFT
         B     ADJLEFT             LEFT JUSTIFY
         B     ADJRGHT             RIGHT JUSTIFY
         B     ADJCNTR             CENTRE JUSTIFY
         B     ADJISDE             INSIDE JUSTIFY
         B     ADJOSDE             OUTSIDE JUSTIFY
         SPACE
ADJHALF  DS    0H
         LA    A,1(,A)             BUMP NUMBER OF BLANKS BY ONE
         SRL   A,1                 AND DIVIDE BY TWO FOR HALF
         SPACE
ADJPOS   DS    0H
         L     W,FOIWSPC           GET INTER-WORD SPACES (FROM MERGE)
         LTR   W,W                 IS IT NULL .Q
         BNP   ADJRET              YES, DO NOTHING
        $IFOFF FOSWTCH,NFSWS,ADJ01 BRANCH IF CONCAT MODE
         LA    W,1(,W)             BUMP BY DUMMY ONE
ADJ01    DS    0H
         L     R,VDEVICEA          TERMINAL DESCRIPTOR BLOCK
         LTR   R,R                 DOES IT EXIST .Q
         BZ    ADJ02               BRANCH IF NOT
         USING TDEVICEA,R          AREA LOOKS LIKE SO
         CLI   TDEVHMI,X'00'       HORIZONTAL MOTION DEFINED .Q
         DROP  R
         BE    ADJ02               BRANCH IF NOT
         LA    C,0(W,W)            TWICE INTERWORD SPACE COUNT
         CR    C,A                 COMPARE WITH DESIRED ADJUSTMENT
         BL    ADJ02               BRANCH IF WOULD NOT BE WORTHWHILE
         B     ADJ20               SETUP FOR TERMINAL TYPE
         SPACE
ADJ02    DS    0H
        $CALL  PUNCADJ             ADJUST ANY PUNCTUATION FIRST
         BC    BOMIT,ADJRET        BRANCH IF NOTHING LEFT
         LR    R,A                 TOTAL ADJUSTMENT TO MAKE
         SRDA  R,32                CALCULATE STANDARD ADJUSTMENT
         DR    R,W                 AND FRACTIONAL LEFTOVER
         LR    C,Q                 GET STANDARD ADJUSTMENT
         SLA   R,1                 MULTIPLY FRACTION * 2
         LCR   Q,W                 -1/2 SPACE
         L     P,FOPLAST           GET POINTER TO LAST LINK BLOCK
         SR    M,M                 ZERO MULTIPLIER REGISTER HANDLER
         B     ADJ04               SKIP FORWARD
         SPACE
ADJ03    DS    0H
         L     P,LINKBACK          FIND NEXT NON-BLANK
ADJ04    DS    0H
         LTR   P,P                 AT THE BEGINNING .Q
         BZ    ADJRET              BRANCH IF YES
         CLI   LINKCHAR,C' '       ..
         BE    ADJ03               ..
         SPACE
ADJ05    DS    0H
         L     P,LINKBACK          FIND NEXT BLANK
         LTR   P,P                 AT THE BEGINNING .Q
         BZ    ADJRET              BRANCH IF YES
         CLI   LINKCHAR,C' '       ..
         BNE   ADJ05               ..
         IC    M,LINKMULT          GET MULTIPLIER
         AR    M,C                 ADD STANDARD ADJUSTMENT AMOUNT
         SR    A,C                 ..
         AR    Q,R                 DO WE NEED EXTRA BLANK
         BL    ADJ06               NO
         SR    Q,W                 SUBTRACT WHOLE BLANK
         SR    Q,W                 ..
         LA    M,1(,M)             ADD EXTRA BLANK TO MULTIPLIER
         BCTR  A,0                 ..
ADJ06    DS    0H
         STC   M,LINKMULT          STORE THE MULTIPLIER
         LTR   A,A                 HAVE ALL ADJUSTMENTS BEEN MADE .Q
         BP    ADJ03               NO, KEEP FILLING BLANKS
         B     ADJRET              AND GO TO RETURN
         SPACE
ADJ20    DS    0H
         MH    A,TDEVWID           TOTAL ADJUSTMENT (IN RESOLUTION)
         LR    R,A                 TOTAL ADJUSTMENT TO MAKE
         SRDA  R,32                CALCULATE STANDARD ADJUSTMENT
         DR    R,W                 AND FRACTIONAL LEFTOVER
         LR    C,Q                 GET STANDARD ADJUSTMENT
         SLA   R,1                 MULTIPLY FRACTION * 2
         LCR   Q,W                 -1/2 SPACE
         L     P,FOPLAST           GET POINTER TO LAST LINK BLOCK
         B     ADJ22               SKIP FORWARD
         SPACE
ADJ21    DS    0H
         L     P,LINKBACK          FIND NEXT NON-BLANK
ADJ22    DS    0H
         LTR   P,P                 AT THE BEGINNING .Q
         BZ    ADJ25               BRANCH IF YES
         CLI   LINKCHAR,C' '       IS IT A BLANK .Q
         BE    ADJ21               BRANCH BACK IF YES
ADJ23    DS    0H
         L     P,LINKBACK          FIND NEXT BLANK
         LTR   P,P                 AT THE BEGINNING .Q
         BZ    ADJ25               BRANCH IF YES
         CLI   LINKCHAR,C' '       IS THIS A BLANK .Q
         BNE   ADJ23               BRANCH IF NOT
         LH    M,LINKWID           GET CHARACTER WIDTH
         AR    M,C                 ADD STANDARD ADJUSTMENT AMOUNT
         SR    A,C                 DECREMENT AMOUNT LEFT TO DO
         AR    Q,R                 DO WE NEED AN EXTRA PART .Q
         BL    ADJ24               BRANCH IF NOT
         SR    Q,W                 SUBTRACT A HALF
         SR    Q,W                 AND AGAIN FOR A WHOLE BLANK
         LA    M,1(,M)             ADD EXTRA PART TO WIDTH
         BCTR  A,0                 DECREMENT AMOUNT LEFT
ADJ24    DS    0H
         STH   M,LINKWID           STORE CHARACTER WIDTH
         LTR   A,A                 HAVE ALL ADJUSTMENTS BEEN MADE .Q
         BP    ADJ21               NO, KEEP FILLING BLANKS
ADJ25    DS    0H
         B     ADJRET              AND GO TO RETURN
         SPACE
ADJLEFT  DS    0H
         B     ADJRET              AND GO TO RETURN
         SPACE
ADJRGHT  DS    0H
        $CALL  RIGHTADJ            RIGHT ADJUST THIS LINE
         B     ADJRET              AND GO TO RETURN
         SPACE
ADJCNTR  DS    0H
        $CALL  CENTER              CENTRE THIS LINE
         B     ADJRET              AND GO TO RETURN
         SPACE
ADJISDE  DS    0H
        $CALL  ODDEVN              ODD OR EVEN PAGE .Q
         BC    BNEG,ADJLEFT        BRANCH IF ODD PAGE
         B     ADJRGHT             ELSE EVEN PAGE
         SPACE
ADJOSDE  DS    0H
        $CALL  ODDEVN              ODD OR EVEN PAGE .Q
         BC    BPOS,ADJLEFT        BRANCH IF EVEN
         B     ADJRGHT             ELSE ODD
         SPACE
ADJRET   DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
         DROP  P,R1
