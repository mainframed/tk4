         MACRO
&SECT   $VERSION &BASE=R15,&NAME=
         LCLC  &LNAME
         GBLC  &SCRSECT,&TRPSECT
&TRPSECT SETC  'TR'                TRAP ROUTINE CSECT
         AIF   ('&SECT' EQ '').MNOTE01
&LNAME   SETC  '&NAME'
         AIF   ('&LNAME' NE '').SKIP01
&LNAME   SETC  '&SECT'
.SKIP01  ANOP
&SECT    CSECT ,
         USING NSCDAT,R13          DATA AREA BASE
         USING SAVESCR,BASESAVE    REGISTER STACK AREA BASE
&SCRSECT SETC  '&SECT'(7,2)
         AIF   ('&SCRSECT' EQ '&TRPSECT').SKIP20
         USING &SECT,&BASE         ENTRY ADDRESS
         B     VERSEND-*(,&BASE)   BRANCH AROUND IDENT
         DC    AL1(L'VERSN)        LENGTH OF IDENT
VERSN    DC    C'UOW &LNAME - VERSION(3.2) &SYSDATE.'
VERSEND  DS    0H
        $HEAD  '&LNAME'
         AIF   ('&SECT' NE '&LNAME').SKIP10
         AIF   ('&SCRSECT' EQ '').MNOTE02
         STM   14,12,12(13)        SAVE CALLER'S REGISTERS
         LA    1,VL&SCRSECT.SCRI   ADCON LENGTH
         LA    2,V&SCRSECT.SCRIP-SCRICVT     TARGET OFFSET
         A     2,VSCRICVT          PLUS CVT BASE
         LA    3,A&SCRSECT.SCRIP   FROM ADDRESS
VERSEND1 DS    0H
         S     1,F256              DECREMENT LENGTH
         BM    VERSEND2            BRANCH IF LAST
         MVC   0(256,2),0(3)       MOVE A CHUNK
         BZ    VERSEND3            BRANCH IF DONE
         LA    2,256(,2)           INCREMENT TARGET
         LA    3,256(,3)           INCREMENT FROM
         B     VERSEND1            TRY AGAIN
VERSEND2 DS    0H
         A     1,F255              RESTORE LENGTH -1
         EX    1,VERSENDM          MOVE THE REMAINDER
VERSEND3 DS    0H
         LM    14,12,12(13)        RESTORE CALLER'S REGS
         BR    14                  RETURN TO CALLER
VERSENDM MVC   0(*-*,2),0(3)       MOVE REMAINDER OF DATA
A&SCRSECT.SCRIP DC (VL&SCRSECT.SCRI/4)A(*-*) ROUTINE ADDRESS CONSTANTS
         ORG   A&SCRSECT.SCRIP+V&SCRSECT.SCRIP-V&SCRSECT.SCRIP
         DC    A(&SECT)            MAIN ROUTINE ENTRY ADDR
         ORG   ,                   END OF AREA
.SKIP10  ANOP  ,
         DROP  &BASE
         MEXIT
.SKIP20  ANOP
        $HEAD  '&LNAME'
         MEXIT
.MNOTE01 MNOTE 8,'$VERSION -- MISSING SECTION LABEL.'
         MEXIT
.MNOTE02 MNOTE 8,'$VERSION -- SECTION LABEL ''&SECT'' TOO SHORT.'
         MEND
