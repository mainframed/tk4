***********************************************************************
*                                                                     *
*              .UD (UNDERSCORE DEFINITION) --- CONTROL                *
*                                                                     *
***********************************************************************
         SPACE
UD      $ENTER ,
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,UD01          BRANCH IF OMITTED TO RESET
         BC    BPOS,UD02           BRANCH IF TURN ON
         BC    BNEG,UD03           BRANCH IF TURN OFF
         LA    X1,UDINCCHR         TARGET FOR UNDERSCORE INCLUDE
         LR    X2,X1               SECOND TARGET THE SAME
         CLI   YNRESULT,YNINC      WAS OPERAND 'INCLUDE' .Q
         BE    UD05                BRANCH IF YES
         LA    X1,UDIGNCHR         TARGET FOR UNDERSCORE IGNORE
         LR    X2,X1               SECOND TARGET THE SAME
         CLI   YNRESULT,YNIGN      WAS OPERAND 'IGNORE' .Q
         BE    UD05                BRANCH IF YES
         LA    X1,UDINCCHR         TARGET FOR UNDERSCORE SET
         LA    X2,UDIGNCHR         SECOND TARGET DIFFERENT
         CLI   YNRESULT,YNSET      WAS OPERAND 'SET' .Q
         BE    UD05                BRANCH IF YES
         B     UDE005              ELSE, MUST BE INVALID
         SPACE
UD01     DS    0H
        $CALL  UDRESET             RESET UNDERSCORE TABLE
         B     UD08                GO TO RETURN
         SPACE
UD02     DS    0H
         LA    X1,UDON             TURN CHAR UNDERSCORE ON
         B     UD04                CONTINUE
UD03     DS    0H
         LA    X1,UDOFF            TURN CHAR UNDERSCORE OFF
UD04     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET AN ARGUMENT
         BC    BOMIT,UD08          NONE LEFT, GO TO RETURN
        $CALL  TRHEX               GET HEX EQUIVALENT
         BC    BDNO,UDE005         ERROR IF DOES NOT CONVERT
         A     R1,UDTABAD          POINT TO RESULT SLOT
         STC   X1,0(,R1)           TURN CHAR ON OR OFF
         B     UD04                DO ALL CHARS IN LIST
         SPACE
UD05     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET CHARACTER ARGUMENT
         BC    BOMIT,UD06          BRANCH TO RESET IF NONE
        $CALL  TRHEX               GET HEX EQUIVALENT
         BC    BDNO,UDE005         ERROR IF DOES NOT CONVERT
         B     UD07                GO ENTER RESULT
UD06     DS    0H
         LA    R1,C' '             THIS DEFAULT KILLS FACILITY
UD07     DS    0H
         STC   R1,0(,X1)           SET UNDERSCORE ESCAPE CHAR
         STC   R1,0(,X2)           AND SET IT AGAIN
         SPACE
UD08     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
UDE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     UD08                GO TO RETURN
         SPACE
UDRESET $ENTER ,
         L     R1,UDTABAD          UNDERSCORE TABLE ADDRESS
         MVI   0(R1),UDON          ASSUME THIS CHAR UNDERSCORED
         MVC   1(L'UDTABLE-1,R1),0(R1)  THEN TURN THEM ALL ON
         SPACE
         MVI   X'05'(R1),UDOFF     NOT THE TAB
         MVI   X'16'(R1),UDOFF     NOT THE BACKSPACE
         MVI   X'40'(R1),UDOFF     NOT THE BLANK
         MVI   X'4B'(R1),UDOFF     NOT THE PERIOD
         MVI   X'4D'(R1),UDOFF     NOT THE LEFT PAREN
         MVI   X'5A'(R1),UDOFF     NOT THE EXCLAMATION MARK
         MVI   X'5D'(R1),UDOFF     NOT THE RIGHT PAREN
         MVI   X'5E'(R1),UDOFF     NOT THE SEMI-COLON
         MVI   X'6B'(R1),UDOFF     NOT THE COMMA
         MVI   X'6D'(R1),UDOFF     NOT THE UNDERSCORE ITSELF
         MVI   X'6F'(R1),UDOFF     NOT THE QUESTION MARK
         MVI   X'7A'(R1),UDOFF     NOT THE COLON
         MVI   X'7F'(R1),UDOFF     NOT THE DOUBLE QUOTE
         MVI   X'8B'(R1),UDOFF     NOT THE LEFT BRACE
         MVI   X'9B'(R1),UDOFF     NOT THE RIGHT BRACE
         MVI   X'AD'(R1),UDOFF     NOT LEFT SQUARE BRACKET
         MVI   X'BD'(R1),UDOFF     NOT RIGHT SQUARE BRACKET
         SPACE
         MVI   UDINCCHR,C' '       CLEAR UNDERSCORE INCLUDE CHAR
         MVI   UDIGNCHR,C' '       CLEAR UNDERSCORE IGNORE CHAR
         SPACE
        $EXIT  RC=0                AND RETURN TO CALLER
