*                             ********
* *            SOURCE BOOK *** DEUH18 *** EXCEPTION WORD ANALYSIS
*                             ********
         SPACE
EXCEPT  $ENTER ,
*        WAS   DEUHYD01
         SPACE
         MVC   HYWKWORD,HYFCOM     ENTER STRING TO PROCESS
        $CALL  DCOMPOZ             GO TO DCOMPOSE ROUTINE
         CLI   HYDCWORD+1,X'1A'    IF DCOMWD IS ALL 1A'S -
         BE    XNOHIT                GET OUT OF EXCEPT
         SPACE
         LR    REGWK1,REG1         INTERNAL FIRST CHARACTER
         SLL   REGWK1,2            TIMES FOUR
         L     REGWK1,HYANCH(REGWK1)    LETTER ANCHOR
         USING HYDSECT,REGWK1      AND TELL THE ASSEMBLER
EXCEP01  DS    0H
         LTR   REGWK1,REGWK1       AT THE END OF LETTER CHAIN .Q
         BZ    XNOHIT              YES, WORD NOT FOUND
         CLC   HYWORD,HYWKWORD     COULD IT BE THIS WORD .Q
         BE    HITXIT              BRANCH IF YES
         L     REGWK1,HYCHN        NEXT WORD POINTER
         B     EXCEP01             AND CONTINUE SEARCHING
         SPACE
HITXIT   XC    HYFCOM,HYFCOM        CLEAR AREA
         LH    REG1,NUMCHR         GET THE WORD SIZE
         EX    REG1,CLRASTAT       RESET ASTATUS BYTES TO NO
         UNPK  HYFCOM(9),HYBRK(5)  GET ZWORD INDICATOR BYTES
         TR    HYFCOM(8),BINHEX-240   TRANSLATE TO BINARY VALUES
         LA    REGWK3,HYFCOM       INITIALIZE INDICATOR BYTE POINTER
         LA    REGIX1,0            SET INDEX TO ZERO
         LA    REG1,ASTATUS-1      INITIALIZE POINTER TO ASTATUS.
NXTIND   CLI   0(REGWK3),X'F0'     IS THIS THE LAST INDICATOR BYTE
         BE    FNDXIT              IF YES-PREPARE TO EXIT
         IC    REGIX1,0(REGWK3)    GET NEXT INDICATOR BYTE
         AR    REG1,REGIX1            AND ADD IT TO ASTATUS POINTER.
         OI    0(REG1),YESXCPT     SET POINT IN ASTATUS TO YES.
         LA    REGWK3,1(REGWK3)    BUMP POINTER TO NEXT INDICATOR
         B     NXTIND              LOOP BACK TO COMPARE NEXT BYTE
FNDXIT  $CC    POS                 SET SUCCESSFUL SEARCH
         B     EXCEPRET            GO TO FINAL RETURNE
         SPACE
CLRASTAT XC    ASTATUS-1(0),ASTATUS-1
         SPACE
XNOHIT   DS    0H
        $CC    NEG                 NO HIT CODE
         SPACE
EXCEPRET DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
         DROP  REGWK1
YESXCPT  EQU   X'70'               BREAK YES FLAG
