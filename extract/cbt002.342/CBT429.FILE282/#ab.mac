***********************************************************************
*                                                                     *
*              .AB (ABEND) --- CONTROL                                *
*                                                                     *
***********************************************************************
         SPACE
AB      $ENTER ,
        $IFOFF SWITCHS,DBSWS,AB06  IGNORE IF 'DEBUG' NOT SPECIFIED
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT+BPOS,AB04     BRANCH IF NULL OR AFFIRMATIVE
         BC    BNEG,AB06           IGNORE IF NEGATIVE
         CLI   YNRESULT,YNTRON     A 'TRACEON' CONDITION .Q
         BNE   AB01                BRANCH IF NOT
         TM    TRACEPTR,X'80'      IS SUPRTRAC OUT THERE .Q
         BO    AB06                BRANCH IF NOT
         L     R15,TRACEPTR        GET TRACE ENTRY ADDRESS
         LA    R14,AB06            RETURN ADDRESS
         BR    R15                 GO TO TRACE
AB01     DS    0H
         CLI   YNRESULT,YNTROFF    A 'TRACEOFF' CONDITION .Q
         BNE   AB02                BRANCH IF NOT
         TM    0,6                 TRACEOFF
         B     AB06                AND GO TO EXIT
AB02     DS    0H
         ST    COUNT,ARGSTART      RESET POINTER FOR RESCAN
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    BDNO+BNEG,AB03      ERROR IF INVALID OR NEGATIVE
         LR    R1,COUNT            TRANSFER THE RESULT
         B     AB05                GO TO ABEND
AB03     DS    0H
         MVI   EFLAG005,ERRSET     SET THE ERROR
         B     AB06                AND GO TO RETURN
AB04     DS    0H
         LA    R1,1                ABEND 1 BY DEFAULT
AB05     DS    0H
        $CALL  SYABEND             CALL ABEND ROUTINE
         B     AB06                IN CASE HE SHOULD RETURN
         SPACE
         DC    A(AB06)             TRACE (R14) IN LINE
TRACEPTR DC    X'80',AL3(0)        FILLED IN BY TRACE
         ENTRY TRACEPTR            FOR SUPRTRAC'S INFORMATION
AB06     DS    0H
        $EXIT  RC=0                CONTINUE PROCESSING
