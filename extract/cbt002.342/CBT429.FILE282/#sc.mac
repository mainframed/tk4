***********************************************************************
*                                                                     *
*              .SC (SINGLE COLUMN) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
SC      $ENTER ,
         CLI   ICORFLG,ICORNONE    'NO KEEP' IN PROGRESS
         BNE   SCE043              ERROR IF NOT
         L     R1,SCCOUNT          SINGLE COLUMN NEST COUNT
         A     R1,F1               PLUS ONE
         ST    R1,SCCOUNT          UPDATE SINGLE COLUMN COUNT
        $CALL  PRINTNB             CLEAR PRINT BUFFER
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   SC02                BRANCH IF NOT
         OC    FNQCNT,FNQCNT       ANY FOOTNOTES QUEUED .Q
         BZ    SC01                BRANCH IF NONE
         LA    R1,1                CONSTANT ONE
         C     R1,CDCURR           CURRENTLY IN COLUMN ONE
         BE    SC01                YES, LET IT RIDE
        $CALL  COLNEW              FINISH OFF THIS COLUMN
         SPACE
SC01     DS    0H
        $CALL  COLPRT              PRINT COLUMNS TO THIS POINT
         MVI   ICOLFLG,ICOLSC      IN SINGLE COLUMN MODE
SC02     DS    0H
         L     R1,LLZ              GET LINE LENGTH
         ST    R1,CLLZ             AND SET TO COLUMN LENGTH
         SPACE
SC03     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
SCE043   DS    0H
         MVI   EFLAG043,ERRSET     INVALID CONTROL WORD WITHIN KEEP
         B     SC03                GO TO RETURN
