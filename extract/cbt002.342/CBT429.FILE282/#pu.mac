***********************************************************************
*                                                                     *
*              .PU (PUT WORK FILE) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
PU      $ENTER ,
         L     X3,ARGSTART         LINE MAY FOLLOW IMMEDIATELY
        $CALL  GETNUM              GET FILE NUMBER
         LA    R1,1                FILE ONE BY DEFAULT
         BC    BOMIT+BDNO,PU01     BRANCH IF OMITTED OR INVALID
         LTR   R1,COUNT            TEST RESULT TOO SMALL .Q
         BNP   PUE014              ERROR IF FILE NUMBER TOO SMALL
         LA    R0,PUMAX            MAX FILE SUPPORTED
         CR    R1,R0               FILE NUMBER TOO BIG .Q
         BH    PUE013              ERROR IF TOO BIG
         L     X3,ARGSTART         LINE MAY NOW FOLLOW IMMEDIATELY
PU01     DS    0H
         LA    X3,1(,X3)           BEGINNING OF OPERAND LINE
         L     COUNT,ARGSTOP       END OF ARG LINE
         SR    COUNT,X3            LENGTH-1 OF LINE
         LA    R0,0                ASSUME NO DATA
         BM    PU02                BRANCH IF NO ARG LINE
         L     X1,BUFF3AD          WORKING BUFFER ADDRESS
         XC    0(4,X1),0(X1)       CLEAR RDW
         MVC   4(B2LG,X1),BLANKS   CLEAR DATA IN CASE FIXED
         EX    COUNT,PUMVC         ENTER DATA
         LA    COUNT,5(,COUNT)     LENGTH INCL RDW
         STH   COUNT,0(,X1)        SET RECORD LENGTH
         LR    R0,X1               POINT TO DATA
PU02     DS    0H
        $CALL  PUWORK              DO YOUR WORST
         SPACE
PU03     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
PUE013   DS    0H
         MVI   EFLAG013,ERRSET     FILE OPERAND TOO BIG
         B     PU03                GO TO RETURN
PUE014   DS    0H
         MVI   EFLAG014,ERRSET     FILE OPERAND TOO SMALL
         B     PU03                GO TO RETURN
         SPACE
PUMVC    MVC   4(*-*,X1),0(X3)     ENTER LINE OPERAND
