***********************************************************************
*                                                                     *
*              MAIN INPUT FILE DRIVER                                 *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1)  >  0  ADDR OF DSECT LIKE 'IOBLOCK'
*                      (R1)  =  0  PURGE ALL LEVELS OF NESTED FILES
*                      (R1)  = -1  PURGE CURRENT NESTED FILE
         SPACE
RDFILE  $ENTER ,
         L     BASE,VSCRICVT       COMMUNICATION VECTOR TABLE
         L     BASE,VRDSCRIP-SCRICVT(,BASE)  LOCAL DATA BASE
         SPACE
*  IF THIS IS A PURGE REQUEST, CLOSE ALL INPUT FILES                  *
         LTR   RPARMPTR,R1         IS PARM POINTER ZERO .Q
         BZ    RFPURGE             IF SO, PURGE ALL INPUT FILES
         BM    RFPURON             IF MINUS, PURGE ONE LEVEL ONLY
         SPACE
         USING IOBLOK,RPARMPTR     TELL ASSEMBLER OF PARM LIST
         SPACE
*  FIND OR CONSTRUCT DDBLOCK FOR THE NEW FILE..............           *
        $CALL  RDFINDF             CALL SUBROUTINE
         SPACE
*  ESTABLISH THE CURRENT FILE ENVIRONMENT                             *
         LM    RFILE,RDD,CURFILE   POINT TO FILBLOCK, DDBLOCK
         SPACE
*  REMOVE CURRENT FILE FROM STACK, IF 'APPEND' IS INDICATED           *
        $IFOFF IOFLAG,IO$AP,RF01   BRANCH IF NO APPEND INDICATED
        $CALL  RDPOP               REMOVE CURRENT FILE
         SPACE
*  ADD THE NEW FILE ONTO THE INPUT STACK                              *
RF01     DS    0H
        $CALL  RDPUSH              PUSH THE STACK DOWN
         B     RF02                AND GO TO EXIT
         SPACE
*  PURGE THE INPUT STACK AND CLOSE ALL INPUT FILES
RFPURGE  DS    0H
        $CALL  RDPURGE
         B     RF02                AND GO TO EXIT
         SPACE
*  PURGE ONE LEVEL OF INPUT STACK
RFPURON  DS    0H
        $CALL  RDPURON             ONE LEVEL
         SPACE
*  RETURN TO THE CALLER                                               *
RF02     DS    0H
        $RESTOR KEEP=(R15)         RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
         DROP  RPARMPTR
