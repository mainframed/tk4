***********************************************************************
*                                                                     *
*              INTERNAL ROUTINE 'TBGETBLK' -- GET A TAB BUFFER        *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY -  TEMP   LENGTH OF FILL CHARACTER
*                    X3     CURRENT TAB COLUMN
         SPACE
*        ON EXIT  -  X1     POINTER TO TAB BUFFER
*                 -  IF FIRST CALL TBANCHR FILLED IN
*                    OTHERWISE TBCHN VALUE SET
         SPACE
*        CC=BPOS     TAB BUFFER OBTAINED AND INITIALIZED
*        CC=BNEG     UNABLE TO OBTAIN TAB BUFFER
         SPACE
TBGETBLK $SAVE
         USING TBAREA,X1           CALLER PROVIDED
         LA    X2,TBCHN            POINT TO CHAIN FIELD IN BUFFER
         LTR   X3,X3               IS THIS 1ST OPERAND OF .TB .Q
         BNZ   TBG01               BR IF NO
         LA    R1,TBANCHR          PTR TO CURRENT USER TAB ANCHOR
        $CALL  TBFREBLK            FREE ALL TAB BUFFERS
         LA    X2,TBANCHR          POINT TO BUFFER ANCHOR
TBG01    DS    0H
         LA    R1,TBLKSZE(TEMP)    GET LENGTH OF TAB BUFFER
        $CALL  GETCELL             GET CORE FOR BUFFER
         BC    BNEG,TBG03          ERROR IF NOT AVAILABLE
         LR    X1,TEMP             X1 NOW POINTS TO TAB BUFFER
         XC    TBAREA(TBLKSZE),TBAREA   ZERO OUT TAB BUFFER PREFIX
TBG02    DS    0H
         ST    TEMP,0(X2)          LINK CURRENT BUFFER TO CHAIN
        $CC    POS                 SET POSITIVE CONDITION CODE
TBG03    DS    0H
        $RESTOR KEEP=(X1)          RESTORE REGISTERS EXCEPT X1
         BR    RETURN              RETURN TO CALLER
         SPACE
         DROP  X1
