***********************************************************************
*                                                                     *
*              .OC (OVERLAY CHARACTER) --- CONTROL                    *
*                                                                     *
***********************************************************************
         SPACE
OC      $ENTER ,
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              GET FIRST ARGUMENT
         BC    BOMIT,OC07          BRANCH IF NOTHING TO OFF
         MVI   OCEXCEP,X'00'       CLEAR EXCEPTION COUNT
         MVC   OCCHAR(1),0(COUNT)  SET OVERLAY CHARACTER
         XC    OCCNT,OCCNT         AND ZERO TEXT LINES COUNT
         C     TEMP,F1             ONE IN LENGTH .Q
         BL    OC04                BRANCH IF YES
         CLI   1(COUNT),C'/'       REALLY TWO ARGS .Q
         BE    OC01                BRANCH IF YES
         MVI   OCCHAR,X'00'        OVERLAY WITH ITSELF
         B     OC02                RESCAN FOR HOW LONG
OC01     DS    0H
         LA    COUNT,2(,COUNT)     BUMP OVER 'CHAR/'
         C     TEMP,F2             MORE THAN TWO IN LENGTH .Q
         BL    OC03                ERROR IF NOT
OC02     DS    0H
         ST    COUNT,ARGSTART      SET RESCAN ADDRESS
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BNEG,OC07           IF OFF, GO AND CLEAR
         BC    BPOS,OC04           IF ON, SET COUNT AND FIND EXCEPT
         ST    COUNT,ARGSTART      RESCAN ARGUMENT FOR NUMERIC
        $CALL  GETNUM              GET AND TEST ARGUMENT
         BC    BPOS,OC05           IF VALID, GO AND ENTER
OC03     DS    0H
         MVC   OCCHAR(1),OCCHAR+1  RESET DEFAULT FOR THIS LINE
         B     OCE005              GO TO ERROR EXIT
OC04     DS    0H
         LH    COUNT,H32767        MAXIMUM TEXT LINE COUNT
OC05     DS    0H
         STH   COUNT,OCCNT         SET TEXT LINE COUNT FOR OVERLAY
         SPACE
OC06     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET EXCEPTION CHAR
         BC    BOMIT,OC08          BRANCH IF DONE
         SR    R1,R1               CLEAR WORK REG
         IC    R1,OCEXCEP          GET CURRENT EXCEPTION COUNT
         LA    R1,1(,R1)           BUMP BY ONE
         LA    R0,L'OCEXCEP-1      MAX ALLOWED
         CR    R1,R0               COMPARE
         BH    OCE005              ERROR IF TOO MANY
         STC   R1,OCEXCEP          UPDATE EXCEPTION COUNT
         LA    R1,OCEXCEP(R1)      TARGET LOCATION
         MVC   0(1,R1),0(COUNT)    ENTER EXCEPTION CHAR
         B     OC06                AND LOOK AGAIN
         SPACE
OC07     DS    0H
         MVI   OCEXCEP,X'00'       ZERO EXCEPTION COUNT
         MVC   OCCHAR(1),OCCHAR+1  RESET DEFAULT VALUE
         XC    OCCNT,OCCNT         AND ZERO TEXT LINES COUNT
         SPACE
OC08     DS    0H
        $EXIT  RC=0                AND CONTINUE
         SPACE
OCE005   DS    0H
         MVI   EFLAG005,ERRSET     SET ERROR FOR INVALID
         B     OC08                AND GO TO TERMINATE
