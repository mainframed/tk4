***********************************************************************
*                                                                     *
*              SEARCH AVAILABLE FILES                                 *
*                                                                     *
***********************************************************************
         SPACE
*                                                                     *
*  THIS SUBROUTINE  MAKES SURE THAT THERE IS AN OPEN DCB              *
*  FOR THE FILE BEING ADDED TO THE INPUT STACK.  IT EXITS             *
*  WITH THE ADDRESS OF A DDBLOCK (OR ZERO, IF THE FILE WAS            *
*  UNDEFINED) IN 'NEWDD'.                                             *
*                                                                     *
         SPACE
RDFINDF $ENTER ,
         SPACE
         USING IOBLOK,RPARMPTR
         SPACE
         MVC   BLDLNAME,IOMEMB     GET SPECIFIED MEMBER NAME
         CLI   BLDLNAME,C' '       IS IT REALLY THERE
         BNE   FIND01              IF NOT, TAKE FILE NAME
         MVC   BLDLNAME,IONAME
         SPACE
*  TRY USING THE FILE NAME AS A DDNAME                                *
         SPACE
FIND01   DS    0H
         MVC   NEWDDNAM,IONAME     USE FILE NAME AS DDNAME
        $CALL  RDSCANF             TRY TO OPEN THE FILE
         LTR   RDD,RDD             WAS THE OPEN SUCCESSFUL
         BNZ   FIND08              IF SO, INPUT FILE HAS BEEN FOUND
         SPACE
*  IF EXPLICIT DDNAME/MEMBERNAME GIVEN, DON'T SEARCH THROUGH          *
*  THE CURRENT FILE STACK                                             *
         SPACE
         CLI   IOMEMB,C' '         IS THE MEMBER NAME BLANK
         BNE   FIND04              IF NOT, TRY TO FIND IN SYSLIB
         SPACE
         L     RFILE,CURFILE       ESTABLISH CURRENT FILE
         SPACE
*  SEARCH THROUGH THE LIST OF ACTIVE FILES FOR FILES                  *
*  WHICH HAVE BEEN OPENED AS PARTITIONED DATA SETS.  CHECK            *
*  ANY SUCH FOR A MEMBER OF THE SPECIFIED NAME.                       *
         SPACE
FIND02   DS    0H
         LTR   RFILE,RFILE         ANY MORE FILES ON THE STACK .Q
         BZ    FIND04              IF NOT, MUST SEARCH ELSEWHERE
         L     RDD,FILDDADR        POINT TO DDBLOCK
         SPACE
         TM    DDDCB+DCBDSORG,DSORG$PO IS IT PARTITIONED .Q
         BZ    FIND03              IF NOT, NO LUCK
         SPACE
         BLDL  DDDCB,BLDLLIST      ISSUE THE BLDL MACRO
         LTR   R15,R15             WAS THE MEMBER THERE .Q
         BNZ   FIND03              BRANCH IF ANSWER IS NO
         LH    RA,DDUSECT          CURRENT DDBLOCK USE COUNT
         LA    RA,1(,RA)           BUMP BY ONE
         STH   RA,DDUSECT          UPDATE USE COUNT
         B     FIND08              MEMBER FOUND
         SPACE
FIND03   DS    0H
         L     RFILE,FILPREV       LINK TO PREVIOUS FILE
         B     FIND02
         SPACE
*  TRY SEARCHING SYSLIB FOR A MEMBER OF THE GIVEN NAME                *
         SPACE
FIND04   DS    0H
        $IFON  IOFLAG,IO$RM,FIND06 BRANCH IF A REMOTE
         CLI   IONAME,C' '         WAS DDNAME ORIGINALLY BLANK .Q
         BE    FIND05              YES, OK TO ASSUME AND TRY AGAIN
         CLI   IOMEMB,C' '         WAS MEMBER ORIGINALLY BLANK .Q
         BNE   FIND06              BOTH SPECIFIED, NO ASSUMPTIONS
FIND05   DS    0H
         MVC   NEWDDNAM,=CL8'SYSLIB  '
        $CALL  RDSCANF             TRY TO OPEN THIS MEMBER
         LTR   RDD,RDD             WAS THE MEMBER FOUND
         BNZ   FIND07              BRANCH IF YES
         SPACE
*  PUT OUT AN ERROR MESSAGE.....FILE NOT FOUND                        *
         SPACE
FIND06   DS    0H
         MVI   EFLAG051,ERRSET     SET ERROR FLAG
        $CALL  RDERROR             CALL ERROR SUBROUTINE
         SR    RDD,RDD             INDICATE NO FILE FOUND
         SR    RRMQ,RRMQ           IN CASE IT WAS A REMOTE
         B     FIND08
         SPACE
FIND07   DS    0H
         OI    DDFLAG,DD$NOCLO     DON'T CLOSE SYSLIB
FIND08   DS    0H
         ST    RDD,NEWDD           SAVE ADDR OF DDBLOCK
         ST    RRMQ,NEWRMQ         AND ADDR OF REMOTE QUEUE START
        $RESTOR ,                  RESTORE CALLER'S REGS
         BR    RETURN              RETURN TO THE CALLER
         SPACE
         DROP  RPARMPTR
