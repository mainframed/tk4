***********************************************************************
*                                                                     *
*              .TE (READ TERMINAL) --- READ INPUT FROM CONSOLE        *
*                                                                     *
***********************************************************************
         SPACE
TE      $ENTER ,
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,TE01          BRANCH IF NULL, ASSUME ONE
         BC    BPOS,TE02           AFFIRMATIVE IS FOREVER
         BC    BNEG,TE03           NEGATIVE FINISHES IT
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              IT IS PROBABLY NUMERIC
         BC    BNEG,TEE014         TREAT NEGATIVE AS ERROR
         BC    BDNO,TEE005         BRANCH IF INVALID
         B     TE04                ENTER VALUE FOR TERMINAL INPUT
         SPACE
TE01     DS    0H
         LA    COUNT,1             ONE LINE BY DEFAULT
         B     TE04                ENTER VALUE FOR READ
TE02     DS    0H
         LH    COUNT,H32767        LARGE POSITIVE NUMBER
         B     TE04                ENTER VALUE FOR READS
TE03     DS    0H
         SR    COUNT,COUNT         ZERO VALUE
TE04     DS    0H
         ST    COUNT,CONSMAX       STORE IN PARAMETER BLOCK
         MVC   CONSGOTO,BLANKS     CLEAR GOTO TARGET
         XC    CONSFLAG,CONSFLAG   INITIALIZE CONSOLE FLAGS
        $ON    CONSFLAG,IO$JIGG    SPECIFY 'JIGGLE' TYPE BALL
        $ON    CONSFLAG,IO$IM      AND IMBED THIS TERMINAL FILE
         SPACE
        $IFOFF TTYPE,TONLINE,TE05  SUPPRESS PRINT NOT ONLINE
         CLC   PASSNO,DEFPASOF+TAB3DEF  IS THIS THE LAST PASS .Q
         BNE   TE05                NO, DON'T SUPPRESS PRINT
        $IFON  TTYPE,TFILE,TE05    BRANCH IF NO TERMINAL CONFLICT
         SPACE
        $ON    CONSFLAG,IO$SUPR    SUPPRESS PRINTING
         SPACE
TE05     DS    0H
         LA    R1,CONSBLOK         TO 'IMBED' THE CONSOLE FILE
        $CALL  RDFILE              CALL THE INPUT SUPERVISOR
         SPACE
TE06     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
TEE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     TE06                GO TO RETURN
TEE005   DS    0H
         MVI   EFLAG005,ERRSET     OPERAND INVALID
         B     TE06                GO TO RETURN
