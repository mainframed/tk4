***********************************************************************
*                                                                     *
*              .OO (OVERLAY OUTPUT) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
OO      $ENTER ,
         LA    R1,OOANCH-OOQFCHN+OODSECT     POINT TO ANCHOR
        $CALL  OVPROC              PROCESS THE OPERAND
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE 2
OVPROC  $ENTER ,
         SPACE
*        ON ENTRY --  (R1) -> OVERLAY ANCHOR ELEMENT
         SPACE
         LR    X1,R1               REMEMBER ANCHOR ELEMENT
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,OVP02         BRANCH IF NULL, ASSUME DEFAULT
         BC    BPOS,OVP03          AFFIRMATIVE IS FOREVER
         BC    BNEG,OVP04          NEGATIVE TERMINATES CURRENT
         CLI   YNRESULT,YNDEL      IS THIS 'DELETE' .Q
         BNE   OVP01               BRANCH IF NOT
         LR    R1,X1               INPUT ANCHOR ELEMENT
        $CALL  OVCLR               DELETE ALL ENTRIES
         B     OVP10               AND GO TO RETURN
         SPACE
OVP01    DS    0H
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              ARGUMENT BETTER BE NUMERIC
         BC    BDNO,OVE005         ERROR IF INVALID
         BC    BNEG,OVE014         ERROR IF NEGATIVE
         B     OVP05               ENTER COUNT FOR OVERLAY
         SPACE
OVP02    DS    0H
         LA    COUNT,1             ONE LINE BY DEFAULT
         B     OVP05               GO ENTER VALUE
OVP03    DS    0H
         LH    COUNT,H32767        LARGE POSITIVE VALUE
         B     OVP05               GO ENTER VALUE
OVP04    DS    0H
         LR    R1,X1               OVERLAY ANCHOR ELEMENT
        $CALL  OVREMOV             REMOVE FIRST BUFFER FROM LIST
         BC    BNEG,OVE012         ERROR IF NONE OUTSTANDING
         B     OVP10               GO TO RETURN
         SPACE
OVP05    DS    0H
         LR    X2,COUNT            REMEMBER OVERLAY COUNT
         L     COUNT,ARGSTART      BLANK DELIMITER ADDRESS
         LA    COUNT,1(,COUNT)     POTENTIAL STRING START ADDR
         C     COUNT,ARGSTOP       ANY STRING PRESENT .Q
         BH    OVP06               NO, READ NEXT RECORD
         LR    X3,COUNT            REMEMBER DATA ADDRESS
         S     COUNT,ARGSTOP       MINUS LENGTH-1 OR ARGUMENT
         LPR   COUNT,COUNT         LENGTH-1 OF ARGUMENT
         LA    COUNT,1(,COUNT)     GET ACTUAL LENGTH OF DATA
         B     OVP07               BRANCH TO ENTER
OVP06    DS    0H
        $CALL  READ                GET NEXT INPUT LINE
         LH    COUNT,0(,R1)        LINE LENGTH
         LA    X3,4(,R1)           POINT TO LINE DATA
         SPACE
OVP07    DS    0H
         LA    R1,OOQLEN(,COUNT)   AREA REQUIRED TO SAVE
        $CALL  GETCELL             GET THE AREA
         BC    BNEG,OVE024         ERROR IF FAILED
         USING OODSECT,TEMP        TELL THE ASSEMBLER
         XC    OODSECT(OOQLEN),OODSECT  CLEAR PREFIX AREA
         ST    X2,OOQCNT           SAVE OVERLAY COUNT
         STH   COUNT,OOQRDW        SAVE LENGTH OF DATA
         S     COUNT,F1            LENGTH-1 FOR MVC
         BM    OVP08               BRANCH IF EMPTY
         EX    COUNT,OVPMVC        MOVE THE DATA
         B     OVP08               AND CONTINUE
OVPMVC   MVC   OOQDATA(*-*),0(X3)  EXECUTED MOVE DATA
OVP08    DS    0H
         DROP  TEMP
         USING OODSECT,X1          SEARCH THE OLD
         LR    R1,X1               INITIALIZE POINTER
OVP09    DS    0H
         LR    X1,R1               FORMER POINTER
         L     R1,OOQFCHN          NEXT ON LIST
         LTR   R1,R1               AT THE END .Q
         BNZ   OVP09               NO, KEEP LOOKING
         ST    TEMP,OOQFCHN        CHAIN TO THE END
         DROP  X1
         B     OVP10               BRANCH TO RETURN
         SPACE
OVE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     OVP10               AND GO TO RETURN
OVE012   DS    0H
         MVI   EFLAG012,ERRSET     ILLEGAL CONTROL WORD
         B     OVP10               AND GO TO RETURN
OVE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     OVP10               AND GO TO RETURN
OVE024   DS    0H
         MVI   EFLAG024,ERRSET     NO CORE TO SAVE DATA
         B     OVP10               AND GO TO RETURN
         SPACE
OVP10    DS    0H
        $EXIT  RC=0                RETURN TO CALLER
