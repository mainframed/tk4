***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'IOADJ' --- EVEN/ODD PAGE ADJUST     *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER  ---   LA    R0,RDW OF OUTPUT LINE
*                       $CALL  IOADJ
*
*        ON EXIT   ---  (R0) -> OLD/NEW RDW OF OUTPUT LINE
         SPACE
IOADJ   $ENTER ,
         LH    X3,ADJCEVN          ASSUME EVEN PAGE
        $CALL  ODDEVN              GO AND CHECK
         BC    BPOS,IOADJ01        BRANCH IF EVEN PAGE
         LH    X3,ADJCODD          GET ODD PAGE ADJUST
IOADJ01  DS    0H
         LTR   X3,X3               ANY VALUE PRESENT
         BNP   IOADJ05             BRANCH IF NOTHING
         SPACE
         L     X1,ADJBUFFA         EVEN/ODD ADJUST WORK BUFFER
         LTR   X1,X1               DOES IT EXIST .Q
         BNZ   IOADJ02             BRANCH IF YES
         LA    R1,B2LG+4           LENGTH OF A WORK BUFFER
        $CALL  GETCELL             GET SOME SPACE
         BC    BNEG,IOADJ06        BRANCH IF FAILED
         LR    X1,TEMP             ADDR OF AREA GOT
         ST    X1,ADJBUFFA         RETAIN WORK BUFFER ADDR
         SPACE
IOADJ02  DS    0H
         LR    TEMP,R0             POINT TO RDW AND DATA
         LH    COUNT,0(,TEMP)      LENGTH OF DATA AND RDW
         XC    0(4,X1),0(X1)       CLEAR THE NEW RDW
        $IFOFF TTYPE,TONLINE,IOADJ03    BRANCH IF NOT ONLINE
         EX    X3,IOADJBMV         BLANK ADJUST AMOUNT
         LA    X2,4(X3,X1)         OLD DATA TARGET ADDRESS
         S     COUNT,F5            LENGTH-1 OF DATA
         EX    COUNT,IOADJDMV      MOVE ONLINE DATA
         LA    COUNT,5(X3,COUNT)   NEW LENGTH
         STH   COUNT,0(,X1)        SAVE NEW LENGTH
         LR    R0,X1               POINT TO RESULT
         B     IOADJ05             GO TO RETURN
         SPACE
IOADJ03  DS    0H
         LA    X2,BLNKRDWL         LENGTH OF A BLANK LINE
         CR    X2,COUNT            A CANDIDATE .Q
         BNE   IOADJ04             BRANCH IF NOT
         CLI   5(TEMP),C' '        BLANK DATA .Q
         BNE   IOADJ04             BRANCH IF NOT
         CLI   4(TEMP),SINGLSP     SINGLE SPACE BLANK .Q
         BE    IOADJ05             QUIT IF YES
         CLI   4(TEMP),DOUBLSP     DOUBLE SPACE BLANK .Q
         BE    IOADJ05             QUIT IF YES
         CLI   4(TEMP),TRIPLSP     TRIPLE SPACE BLANK .Q
         BE    IOADJ05             QUIT IF YES
IOADJ04  DS    0H
         EX    X3,IOADJBMV         BLANK ADJUST
         MVC   4(1,X1),4(TEMP)     RETAIN CARRIAGE CONTROL
         LA    X2,5(X3,X1)         DATA TARGET ADDRESS
         S     COUNT,F6            LENGTH-1 OF DATA
         EX    COUNT,IOADJEMV      MOVE OFFLINE DATA
         LA    COUNT,6(X3,COUNT)   NEW LENGTH
         STH   COUNT,0(,X1)        SAVE NEW LENGTH
         LR    R0,X1               POINT TO RESULT
         B     IOADJ05             GO TO RETURN
         SPACE
IOADJ05  DS    0H
        $RESTOR KEEP=(R0)          RESTORE CALLER'S REGS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
IOADJ06  DS    0H
         MVI   EFLAG024,ERRSET     NO WORK SPACE AVAILABLE
         B     IOADJ05             AND GO TO RETURN
         SPACE
IOADJBMV MVC   4(*-*,X1),BLANKS    LEADING BLANK MOVE
IOADJDMV MVC   0(*-*,X2),4(TEMP)   ONLINE DATA MOVE
IOADJEMV MVC   0(*-*,X2),5(TEMP)   OFFLINE DATA MOVE
