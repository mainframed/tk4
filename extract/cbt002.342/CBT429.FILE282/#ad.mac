***********************************************************************
*                                                                     *
*              .AD (ADJUST) --- CONTROL                               *
*                                                                     *
***********************************************************************
         SPACE
AD      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETYESNO            GET AND TEST FIRST ARG
         LA    X1,ADJCNT           NORMAL TARGET ADDRESS
         BC    BOMIT,AD01          BRANCH IF OMITTED
         LA    X1,ADJCEVN          EVEN TARGET
         CLI   YNRESULT,YNEVN      IS ARG 'EVEN' .Q
         BE    AD00                BRANCH IF YES
         LA    X1,ADJCODD          ODD TARGET
         CLI   YNRESULT,YNODD      IS ARG 'ODD' .Q
         BE    AD00                BRANCH IF YES
         ST    COUNT,ARGSTART      RESCAN FOR A NUMERIC
         LA    X1,ADJCNT           RESET NORMAL TARGET
         SPACE
AD00     DS    0H
        $CALL  GETNUM              GET ARGUMENT
         BC    BDNO,AD03           ERROR IF INVALID
         BC    BOMIT,AD01          BRANCH IF OMITTED
         CLI   NUMSIGN,C' '        WAS OPERAND SIGNED .Q
         BE    AD02                BRANCH IF YES
         AH    COUNT,0(X1)         MAKE RELATIVE CHANGE
         B     AD02                GO CHECK AND ENTER
AD01     DS    0H
         L     COUNT,DEFADJ+TAB3DEF  GET DEFAULT ADJUST VALUE
AD02     DS    0H
         C     COUNT,DEFADJ+TAB3MIN  TOO SMALL .Q
         BL    AD05                BRANCH IF YES
         C     COUNT,DEFADJ+TAB3MAX  TOO BIG .Q
         BH    AD04                BRANCH IF YES
         STH   COUNT,0(X1)         SET NEW ADJUST VALUE
         B     AD06                AND GO TO TERMINATE
         SPACE
AD03     DS    0H
         MVI   EFLAG005,ERRSET     SET INVALID OPERAND
         B     AD06                AND GO TO TERMINATE
AD04     DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO LARGE
         B     AD06                AND GO TO TERMINATE
AD05     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         SPACE
AD06     DS    0H
        $EXIT  RC=0                RETURN TO CONTROLLER
