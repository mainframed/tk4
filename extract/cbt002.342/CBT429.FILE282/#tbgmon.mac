***********************************************************************
*                                                                     *
* INTERNAL ROUTINE 'TBGETMON' -- GET A NUMBER (T1 FIELD)              *
*                        --   VERIFY SEQUENCE INCREASES MONOTONICALLY *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY -  X1     BASE REGISTER FOR TAB BUFFER
         SPACE
*        ON EXIT  -  X3     NEW TAB VALUE (CURRENT T1 FIELD
         SPACE
*        CC=BPOS     SUCCESSFUL COMPLETION
*        CC=BNEG     TAB VALUE TOO BIG OR SMALL
*                    (ERROR FLAG SET IN THIS ROUTINE)
*        CC=BOMIT    ARGUMENT OMITTED
*        CC=BDNO     ARGUMENT NOT NUMERIC
*                    (COUNT) = START ADDR OF ERROR
         SPACE
TBGETMON $SAVE
         USING TBAREA,X1           INFORM ASSEMBLER
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    BOMIT,TBM2          ERROR IF OMITTED
         BC    BDNO,TBM2           ERROR IF NOT NUMERIC
         LR    X2,X3               GET PREVIOUS TAB VALUE
         CLI   NUMSIGN,C' '        WAS A SIGN SPECIFIED .Q
         BNE   TBM1                BRANCH IF YES
         SR    X2,X2               CLEAR BASE TAB VALUE
TBM1     DS    0H                  RELATIVE OR ABSOLUTE TABS
*                                  ARE PROCESSED THE SAME
         AR    COUNT,X2            ADD BASE TAB VALUE
         C     COUNT,DEFLL+TAB3MAX IS T1 GREATER THAN MAX .LL .Q
         BH    TBME01              ERROR IF YES
         CR    COUNT,X3            IS T1 > PREVIOUS T1 FIELD .Q
         BNH   TBME02              ERROR IF NOT
        $CC    POS                 SET POSITIVE COND CODE
         LR    X3,COUNT            CURRENT T1 VALUE
TBM2     DS    0H                  RETURN TO CALLER
        $RESTOR KEEP=(COUNT,X3)    SAVE T1 VALUE AND PTR TO NUMBER
         BR    RETURN              AND RETURN
         SPACE
         DROP  X1
         SPACE
TBME01   DS    0H
         MVI   EFLAG013,ERRSET     NUMERIC ARGUMENT TOO BIG
         B     TBME99              SET CONDITION CODE
TBME02   DS    0H
         MVI   EFLAG014,ERRSET     NUMERIC ARGUMENT TOO SMALL
         B     TBME99              SET CONDITION CODE
TBME99   DS    0H
        $CC    NEG                 ERROR RETURN
         B     TBM2                AND GO TO RETURN
