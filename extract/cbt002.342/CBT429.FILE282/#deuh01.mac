*                             ********
* *            SOURCE BOOK *** DEUH01 *** USER ENTRY TO H/360
*                             ********
         SPACE 3
*        ON ENTRY  --  (R1) -> ONE BYTE LAST FIT LENGTH FOLLOWED
*                              BY UP TO 64 EBCDIC CHARACTERS.
*        ON EXIT   --  (R15) < 0  IF ERROR
*                            > 0  PTR TO GSTATUS TABLE
         SPACE
HYPHEN  $ENTER ,                   MAIN AUTO HYPHENATION ENTRY POINT
*        WAS   DEUHYA01
         SPACE
* USING REGISTER 1 AS PARAMETER LIST POINTER  ESTABLISH ADDRESSES
* OF USERS WORD AND NO-HYPHEN REJECT  ROUTINE
         SPACE
         L     BASE,VSCRICVT       COMMUNICATION VECTOR TABLE
         L     BASE,VHYSCRIP-SCRICVT(,BASE)  LOCAL DATA BASE
         SPACE
         ST    REG1,HFLDAD         HFLDAD  USER WORD (L-T-F BYTE)
         L     REG3,HYPRATAD       INTERNAL CHARACTER TRANS TABLE
         TR    1(64,REG1),0(REG3)  TRANSLATE INPUT TO INTERNAL
         LA    REG3,1(,REG1)       REG3  ADDRESS OF 1ST CHAR IN WORD
         LA    REG1,GSTATUS         GET ADDRESS OF HYPHEN INFORMATION
         SPACE
* EXECUTE SEARCH FOR TERMINAL CHARACTER , REJECT CHAR. & COUNT OF HYPFL
         TRT   0(64,REG3),ITESTBL  ITESTBL EQUATED TO GSTATUS
         BC    8,HREJ
         TM    0(REG1),X'21'      TEST FOR VALID EOW TERMINATION
         BO    HREJ                BRANCH IF REJECT CHARACTER
         CLI   0(REG1),X'20'       TEST FOR VALID EOW CHARACTER
         BNE   HREJ                 & BRANCH IF NOT
* DEVELOP COUNT OF CHARACTERS IN HYPFIELD AT EOW
HYPINP2  LR    REG5,REG1           ADDRESS OF EOW
         SR    REG5,REG3            MINUS START OF HYPFIELD
         STH   REG5,HYFLDCCT       SAVE CHARACTER COUNT
         XC    ASTATUS,ASTATUS     CLEAR ASTATUS
         XC    GSTATUS,GSTATUS     AND CLEAR GSTATUS
         EX    REG5,SGSTS          THEN MOVE VALID CHARS.TO GSTATUS
         EX    REG5,TGSTS           FOR TRANSLATE TO VOWEL,CONSON.ETC
         B     NASCN               GO TO NON ALPHA SCAN
* AT THIS STAGE GSTATUS SHOULD REFLECT THE CHARACTERISTICS OF HYPFIELD
* RELATING TO 1-NON-ALPHA,2-VOWEL,3-CONSONANT,4-'Y'
