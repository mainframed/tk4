***********************************************************************
*                                                                     *
*              .BM (BOTTOM MARGIN) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
BM      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    15-BDNO,BM01        BRANCH IF OK
         MVI   EFLAG005,ERRSET     SET ERROR FLAG
         B     BM09                AND GO TO RETURN
BM01     DS    0H
         BC    BOMIT,BM02          SET DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        IS ARGUMENT SIGNED .Q
         BE    BM03                BRANCH IF NOT
         AH    COUNT,BMMRG         ALTER CURRENT BOTTOM MARGIN
         B     BM03                GO CHECK AND ENTER
BM02     DS    0H
         L     COUNT,DEFBM+TAB3DEF SET DEFAULT BOTTOM MARGIN
BM03     DS    0H
         LTR   COUNT,COUNT         CHECK VALUE FOR NEGATIVE
         BNM   BM04                BRANCH IF NOT NEGATIVE
         MVI   EFLAG014,ERRSET     SET THE ERROR
         B     BM09                AND GO TO RETURN
BM04     DS    0H
         LH    TEMP,FTMRG          FOOTING MARGIN
         AH    TEMP,FSCNT          FOOTING SPACING
         CR    COUNT,TEMP          COMPARE VALUES
         BNL   BM05                BRANCH IF NOT LOW
         MVI   EFLAG042,ERRSET     SET THE ERROR
         B     BM09                AND GO TO RETURN
BM05     DS    0H
         LH    TEMP,PLZ            GET CURRENT PAGE LENGTH
         SH    TEMP,TOPMRG         LESS TOP MARGIN
         CR    COUNT,TEMP          TOO BIG .Q
         BL    BM06                BRANCH IF NOT TOO BIG
         MVI   EFLAG045,ERRSET     SET THE ERROR
         B     BM09                AND GO TO RETURN
BM06     DS    0H
         CH    COUNT,PLCT          STILL ENOUGH LINES LEFT ON PAGE .Q
         BNH   BM07                BRANCH IF YES
        $CALL  PAGE                TERMINATE THIS PAGE
         B     BM06                AND TRY AGAIN
         SPACE
BM07     DS    0H
         OC    FNSEP,FNSEP         ANY FOOTNOTE LINES FOR THIS PAGE .Q
         BZ    BM08                BRANCH IF NONE
         SPACE
         LH    TEMP,FNSEP          FOOTNOTE LINES QUEUED
         AR    TEMP,COUNT          PLUS NEW BOTTOM MARGIN
         CH    TEMP,PLCT           STILL ENOUGH LINES ON PAGE .Q
         BNH   BM08                BRANCH IF YES
        $CALL  PAGE                TERMINATE THIS PAGE
         B     BM06                AND TRY AGAIN
BM08     DS    0H
         STH   COUNT,BMMRG         SET NEW BOTTOM MARGIN
         SPACE
BM09     DS    0H
        $EXIT  RC=0                BACK TO MAIN LINE
