***********************************************************************
*                                                                     *
*              .UL (UNDERLINE ALPHANUMERICS) --- CONTROL              *
*                                                                     *
***********************************************************************
         SPACE
UL      $ENTER ,
         L     X1,ARGSTART         REMEMBER BEGINNING OF ARGUMENT
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,UL01          BRANCH TO DEFAULT IF OMITTED
         BC    BPOS,UL02           BRANCH IF BEGIN UNDERLINE
         BC    BNEG,UL03           BRANCH IF END UNDERLINE
         SPACE
         ST    X1,ARGSTART         RESCAN FOR NUMERIC ARGUMENT
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    BPOS,UL04           BRANCH IF ZERO OR POSITIVE ARG
         ST    X1,ARGSTART         ALL FAILED, ONLY UNDERLINE OPERAND
         SPACE
UL01     DS    0H
         LA    COUNT,1             ONE UNDERLINED LINE BY DEFAULT
         B     UL04                MAKE THE ENTRY
UL02     DS    0H
         L     COUNT,F32767        DO IT FOREVER IF BEGIN
         B     UL04                MAKE THE ENTRY
UL03     DS    0H
         XC    OCCNT,OCCNT         CLEAR OVERLAY CHAR LINE COUNT
         MVC   OCCHAR(1),OCCHAR+1  RESET DEFAULT OVERLAY CHAR
         MVI   OCEXCEP,X'00'       AND RESET EXCEPTION CHAR COUNT
         B     UL05                AND GO TEST FURTHER TEXT
UL04     DS    0H
         MVI   OCCHAR,C'_'         SET UNDERSCORE FOR OVERLAY CHAR
         MVI   OCEXCEP,X'FF'       SET MAX EXCEPTION COUNT
         STH   COUNT,OCCNT         AND SET COUNT OF TEXT LINES
         SPACE
UL05     DS    0H
        $CALL  USARG               PROCESS ARGUMENT LINE
         BC    BOMIT,UL06          CONTINUE WITH NEXT LINE IF EMPTY
         LA    R15,WRETNR          RETURN CODE TO PROCESS THE RESULT
         B     UL07                AND GO TO FINAL RETURN
         SPACE
UL06     DS    0H
         SR    R15,R15             SET RETURN CODE TO IGNORE
         SPACE
UL07     DS    0H
        $EXIT  RC=(R15)            AND RETURN TO CALLER
         SPACE
ULE014   DS    0H
         MVI   EFLAG014,ERRSET     ARGUMENT TOO SMALL
         B     UL06                AND GO TO RETURN
