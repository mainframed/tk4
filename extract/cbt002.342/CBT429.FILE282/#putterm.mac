***********************************************************************
*                                                                     *
*              'PUTTERM' --- I/O ROUTINE TO ERROR FILE                *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0)=0 AND IOCBTERM=0 TO OPEN FILE
*                      (R0)>0 ADDRESS OF DATA TO PUT DATA
*                      (R0)<0 TO CLOSE FILE
*
*        ON EXIT   --  CC = BPOS  IF OK
*                         = BNEG  IF ERROR
         SPACE
PUTTERM $ENTER ,
         L     X1,IOCBTERM         SYSTERM IOCB ADDRESS
         LTR   R0,R0               TEST CALL TYPE
         BP    PTRM04              BRANCH IF PUT CALL
         BM    PTRM02              BRANCH IF CLOSE CALL
         LTR   X1,X1               FILE TRIED BEFORE .Q
         BNZ   PTRM06              YES, DON'T DO IT AGAIN
         SPACE
         MVI   IOCBTERM,X'80'      MARK ADDR TRIED
         LA    TEMP,SAVED0         POINT TO SHORT WORK AREA
         USING IOCIODEF,TEMP       IODEF LOOKS LIKE THIS
         MVC   IOCIODEF(PTIODEFL),PTIODEF  FRESH IODEF
         LA    R1,IOCDDNAM         POINTER TO FILENAME
        $CALL  RDDDREP             REPLACEMENT FILENAME .Q
         MVC   IOCDDNAM,0(R1)      ENTER REQUIRED NAME
         SPACE
         LA    R1,IOCDDNAM         POINT AGAIN
        $CALL  SYTEST              CAN OPEN SUCCEED .Q
         BC    BNEG,PTRM01         BRANCH IF NOT
         LA    R1,IOCIODEF         POINT TO IODEF
        $CALL  SYPUTOP             OPEN OUTPUT FILE
         ST    R1,IOCBTERM         SAVE IOCB ADDRESS
         BC    BPOS,PTRM01         CONTINUE IF SUCCESSFUL
         MVI   IOCBTERM,X'80'      TRIED AND FAILED
PTRM01   DS    0H
         DROP  TEMP
         TM    IOCBTERM,X'80'      TRIED AND SUCCEEDED .Q
         BZ    PTRM05              BRANCH IF YES
         B     PTRM06              ELSE SET FOR FAILURE
         SPACE
         USING IOCB,X1             FILE CONTROL BLOCK
PTRM02   DS    0H
         LA    X1,0(,X1)           CLEAR HIGH BYTE
         LTR   X1,X1               DOES FILE EXIST .Q
         BZ    PTRM03              BRANCH IF NOT
         LA    R1,IOCB             IOCB ADDRESS
        $CALL  SYPUTCL             CLOSE OUTPUT FILE
PTRM03   DS    0H
         XC    IOCBTERM,IOCBTERM   CLEAR IOCB POINTER
         B     PTRM05              GO TO SUCCESSFUL EXIT
         SPACE
PTRM04   DS    0H
         LA    X1,0(,X1)           CLEAR HIGH BYTE
         LTR   X1,X1               DOES FILE EXIST .Q
         BZ    PTRM05              BRANCH IF NOT
         LA    R1,IOCB             IO CONTROL BLOCK
        $CALL  SYPUT               OUTPUT THE RECORD
         DROP  X1
         SPACE
PTRM05   DS    0H
        $CC    POS                 ALL IS SUCCESSFUL
         B     PTRM07              GO TO RETURN
         SPACE
PTRM06   DS    0H
        $CC    NEG                 UNSUCCESSFUL
         SPACE
PTRM07   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
PTIODEF  SYSIODEF FILETYP=IOC$QSAM,DDNAME=SYSTERM,                     *
               RECFM=IOCRECV+IOCRECCA,                                 *
               RECMERG=IOCRECF+IOCRECV+IOCRECBR+IOCRECCA,              *
               LRECL=137,MAXLREC=240
PTIODEFL EQU   *-PTIODEF           LENGTH OF IODEF
