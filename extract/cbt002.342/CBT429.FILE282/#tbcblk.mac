***********************************************************************
*                                                                     *
*              INTERNAL ROUTINE 'TBCOPY'  -- COPY A TAB BUFFER        *
*                                                                     *
***********************************************************************
         SPACE
*        COPIES TAB STRING BLOCK POINTED TO BY TBANCHR FIELD
         SPACE
*        ON EXIT  -  R1     ADDRESS OF NEW TBANCHR FIELD
*                 -  IF ORIGINAL VALUE OF TBANCHR WAS
*                    ZERO THEN R1 IS SET TO 0
         SPACE
*        CC=BPOS     TAB BUFFER COPIED SUCCESSFULLY
*        CC=BNEG     UNABLE TO COPY TAB BUFFER
         SPACE
TBCOPY   $ENTER
         SR    X3,X3               INITIALIZE RETURNED NEW ANCHOR
         L     X1,TBANCHR          POINT TO BUFFER ANCHOR
         LTR   X1,X1               HAS ANCHOR BEEN USED .Q
         BZ    TBC04               BRANCH IF NO
         USING TBAREA,X1           INFORM ASSEMBLER
TBC01    DS    0H
         LA    R1,TBLKSZE          LENGTH OF TAB BUFFER
         AH    R1,TBLEN            PLUS LENGTH OF FILL STRING
        $CALL  GETCELL             PASS LENGTH OF BLOCK REQ'D
         BC    BNEG,TBC05          ERROR IF NO CORE
         BCTR  R1,0                LENGTH-1 OF DATA
         EX    R1,TBCMVC           MOVE TAB ELEMENT
         XC    TBCHN-TBAREA(L'TBCHN,TEMP),TBCHN-TBAREA(TEMP)  CLEAR PTR
         LTR   X3,X3               FIRST ELEMENT .Q
         BNZ   TBC02               BRANCH IF NOT
         LA    X3,TBCHN-TBAREA(,TEMP)  REMEMBER FIRST POINTER
         B     TBC03               AND CONTINUE
TBC02    DS    0H
         ST    TEMP,TBCHN-TBAREA(,X2)   SET NEXT AREA POINTER
TBC03    DS    0H
         LA    X2,TBCHN-TBAREA(,TEMP)   KEEP LAG POINTER
         L     X1,TBCHN            CHAIN TO NEXT BUFFER
         LTR   X1,X1               ARE WE AT END OF CHAIN .Q
         BNZ   TBC01               BR IF NO
TBC04    DS    0H
        $CC    POS                 INDICATE SUCCESS
TBC05    DS    0H
         LR    R1,X3               RETURN VALUE OF ANCHOR OR ZERO
        $EXIT  KEEP=(R1),RC=0      RETURN WITH PTR TO ANCHOR FIELD
         SPACE
TBCMVC   MVC   0(*-*,TEMP),TBAREA  DUPLICATE TAB DATA
         DROP  X1
