***********************************************************************
*                                                                     *
*  THIS ENTRY POINT TO SCRIPTRD IS USED TO REWIND THE CURRENT         *
*  INPUT FILE, BE IT DASD OR REMOTE.                                  *
*                                                                     *
*  UPON ENTRY, REGISTER ONE POINTS TO AN AREA OF STORAGE THAT LOOKS   *
*  LIKE 'IOBLOK'.                                                     *
*                                                                     *
*  UPON EXIT, R15 HOLDS A RETURN CODE WITH THE FOLLOWING MEANING --   *
*        0     REWIND WAS SUCCESSFUL                                  *
*        4     REWIND WAS UNSUCCESSFUL                                *
*                                                                     *
***********************************************************************
         SPACE
RDREWIND $ENTER ,
         L     BASE,VSCRICVT       COMMUNICATION VECTOR TABLE
         L     BASE,VRDSCRIP-SCRICVT(,BASE)  LOCAL DATA BASE
         SPACE
         LR    RPARMPTR,R1         SAVE PARM POINTER
         USING IOBLOK,RPARMPTR     AND TELL THE ASSEMBLER
         LM    RFILE,RDD,CURFILE   ESTABLISH CURRENT FILE ENVIRONMENT
         LTR   RFILE,RFILE         IS ANY FILE ON STACK .Q
         BZ    RDREW03             ERROR IF NOT
         LTR   RDD,RDD             DID ALLOC OF BOTTOM FILE FAIL .Q
         BZ    RDREW03             ERROR IF NOT
         CLC   IONAME,DDLDDNAM     SAME FILE NAME .Q
         BNE   RDREW03             ERROR IF NOT
         CLC   IOMEMB,FILMEMNM     SAME MEMBER .Q
         BNE   RDREW03             ERROR IF NOT
         TM    DDDCB+DCBDEVT,X'40' UNIT RECORD INPUT FILE .Q
         BO    RDREW03             YES, FORGET REREADING SAME
        $IFNONE FILFLAG,(FIL$AP,FIL$IM),RDREW01   BRANCH IF A REMOTE
         LR    R1,RPARMPTR         POINT TO CALLER'S PARM
        $CALL  RDFILE              CALL INPUT COORDINATOR TO APPEND
         B     RDREW02             GO TO SUCCESSFUL EXIT
         SPACE
RDREW01  DS    0H
         L     RRMQ,FILRMQAD       REMOTE DESCRIPTOR BLOCKS
         LTR   RRMQ,RRMQ           DID ALLOC SUCCEED .Q
         BZ    RDREW03             BRANCH IF NOT
         USING RMQD,RRMQ           AND TELL THE ASSEMBLER
         MVC   FILBUFAD,RMQAD      POINT TO FIRST RECORD AGAIN
         XC    FILITEM,FILITEM     STARTING POINT
         MVC   FILMIN,IOMIN        FIRST RECORD NUMBER
         DROP  RRMQ
         SPACE
RDREW02  DS    0H
         SR    R15,R15             SET RETURN CODE
         B     RDREW04             GO TO RETURN
         SPACE
RDREW03  DS    0H
         LA    R15,4               SET UNSUCCESSFUL RETURN CODE
         SPACE
RDREW04  DS    0H
        $EXIT  RC=(R15)            AND RETURN TO CALLER
