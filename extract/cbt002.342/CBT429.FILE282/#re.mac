***********************************************************************
*                                                                     *
*              .RE (RESTORE STATUS) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
RE      $ENTER ,
         L     X1,STAKANCH         SAVE ELEMENT ANCHOR
         LTR   X1,X1               FIRST ELEMENT PRESENT .Q
         BZ    REE011              ERROR IF NOT
         SPACE
         USING STAKSAVE,X1         AREA LOOKS LIKE THIS
         CLC   CDCOUNT,STAKTLD2+CDCOUNT-FMTOLD2   HAS .CD CHANGED .Q
         BE    RE01                BRANCH IF NOT
        $CALL  PRINT               CAUSE A BREAK
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   RE01                BRANCH IF NOT
        $CALL  COLPRT              DUMP DATA
         SPACE
RE01     DS    0H
         MVC   STAKANCH,STAKSAVE   UNCHAIN THIS FIRST ELEMENT
         MVC   SWCTL(SWCTLZ),STAKTLD1   RESTORE LOCAL SWITCHS
         MVC   FMTOLD2(LFMTOLD2),STAKTLD2  RESTORE PAGE DESCRIPTORS
         MVC   ICOLFLG,STAKICOL    RESTORE TYPE OF COLUMN FORMAT
        $MVC   OCCNT,STAKOC        RESTORE OVERLAY CHARACTER STATUS
         L     R1,TRANADO          OUTPUT TRANSLATE TABLE ADDRESS
         MVC   0(256,R1),STAKTRNO  RESTORE OUTPUT TRANSLATE TABLE
         L     R1,TRANADI          INPUT TRANSLATE TABLE ADDRESS
         MVC   0(256,R1),STAKTRNI  RESTORE INPUT TRANSLATE TABLE
         L     TEMP,STAKESTA       ESCAPE CHAR/ TABLE ADDRESS
         ST    TEMP,ESTABAD        RESTORE SAME
         LA    TEMP,0(,TEMP)       CLEAR CHARACTER
         LTR   TEMP,TEMP           ADDRESS PRESENT .Q
         BZ    RE02                BRANCH IF NOT
         XC    0(256,TEMP),0(TEMP) ZERO THE TABLE
         SR    R1,R1               ZERO WORKING REGISTER
         IC    R1,STAKESTA         GET ESCAPE CHARACTER
         STC   R1,0(R1,TEMP)       ENTER CHARACTER IN THE TABLE
RE02     DS    0H
         LA    R1,TBANCHR          PTR TO CURRENT USER TAB ANCHOR
        $CALL  TBFREBLK            FREE CURRENT USER'S TABS
         MVC   TBIND,STAKTABI      RESTORE USER TAB CHARACTER
         MVC   TBANCHR,STAKTABA    RESTORE FORMER TAB CHAIN
         LA    R0,STAKSAVE         WHERE TO FREE
         LA    R1,STAKSIZE         HOW MUCH TO FREE
        $CALL  FREECELL            RELEASE STORAGE
         DROP  X1
         L     R1,STAKCTR          COUNT OF NESTED AREAS
         S     R1,F1               LESS ONE
         BM    REE011              CAN'T BE ZERO (OR CAN IT) .Q
         ST    R1,STAKCTR          SET NEW NEST LEVEL
RE03     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
REE011   DS    0H
         MVI   EFLAG011,ERRSET     STACK IS EMPTY
         B     RE03                GO TO EXIT
         SPACE 2
STAKFREE $ENTER ,
         SR    R0,R0               CLEAR WORK REGISTER
         L     X1,STAKANCH         SAVE/RESTORE ANCHOR POINTER
         ST    R0,STAKANCH         CLEAR ANCHOR POINTER
         ST    R0,STAKCTR          CLEAR NEST COUNTER
STKF01   DS    0H
         LTR   X1,X1               AT THE END .Q
         BZ    STKF02              BRANCH IF YES
         USING STAKSAVE,X1         AREA LOOKS LIKE THIS
         LA    R1,STAKTABA         PTR TO CURRENT USER TAB ANCHOR
        $CALL  TBFREBLK            FREE CURRENT USER'S TABS
         LA    R0,STAKSAVE         FREE FROM HERE
         LA    R1,STAKSIZE         FREE THIS MUCH
         L     X1,STAKCHN          NEXT AREA POINTER
        $CALL  FREECELL            FREE ONE AREA
         B     STKF01              GO CHECK FOR DONE
         DROP  X1
STKF02   DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
