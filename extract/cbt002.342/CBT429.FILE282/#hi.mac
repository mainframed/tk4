***********************************************************************
*                                                                     *
*              .HI (HANGING INDENT) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
HI      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BNEG+BOMIT,HI02     BRANCH IF NEGATIVE OR OMITTED
         BC    BPOS,HI01           BRANCH IF AFFIRMATIVE
         ST    COUNT,ARGSTART      RESCAN FOR NUMERIC
        $CALL  GETNUM              GET OPERAND
         BC    BDNO,HIE005         ERROR IF INVALID
         BC    BOMIT,HI02          BRANCH IF OMITTED TO DEFAULT
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    HI03                BRANCH IF YES
         AH    COUNT,HICNT         MAKE RELATIVE CHANGE
         B     HI03                GO CHECK AND ENTER
         SPACE
HI01     DS    0H
         LA    COUNT,5             A NICE AFFIRMATIVE VALUE
         B     HI03                AND GO ENTER
HI02     DS    0H
         SR    COUNT,COUNT         DEFAULT HANGING INDENT
HI03     DS    0H
         LTR   COUNT,COUNT         TEST NOT TOO SMALL
         BM    HIE014              ERROR IF TOO SMALL
         C     COUNT,LLZ           TEST NOT TOO BIG
         BNL   HIE013              ERROR IF TOO BIG
         STH   COUNT,HICNT         SET HANGING INDENT
         SR    COUNT,COUNT         THIS IS LIKE OFFSET ZERO
         LH    TEMP,INDL           CURRENT WORKING LEFT INDENT
         SH    TEMP,OFFL           LESS OFFSET
         STH   TEMP,INDL           ADJUST INDENTATION
         SH    TEMP,UNDL           ..
         BM    HIE013              ERROR IF TOO LARGE
         STH   TEMP,RMARGIN        ..
         STH   COUNT,OFFL          CLEAR OFFSET VALUE
         STH   COUNT,OFFLI         CLEAR NEXT OFFSET VALUE
         STH   COUNT,OFUNCNT       CLEAR MARGIN VALUE FOR FORMAT
         SPACE
HI04     DS    0H
        $EXIT  RC=0                RETURN TO CONTROLLER
         SPACE
HIE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     HI04                GO TO RETURN
HIE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     HI04                GO TO RETURN
HIE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     HI04                GO TO RETURN
