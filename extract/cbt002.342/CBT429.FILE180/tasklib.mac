TLIB     TITLE 'QCTL01 -- TASKLIB EFFECTOR FOR TESTING THE IPS'
***   COPYRIGHT 1976 - REGENTS OF THE UNIVERSITY OF CALIFORNIA,     ***
***   CAMPUS COMPUTING NETWORK   (SHARE CODE 'UR')  -  1/15/76      ***
***   ALTHOUGH WE ARE RUNNING THIS MODULE IN OUR PRODUCTION SYSTEM, ***
***   NO WARRANTY, EXPRESSED OR IMPLIED, IS MADE BY CCN AS TO THE   ***
***   FUNCTIONING OR CORRECTNESS OF THE FOLLOWING CODE.             ***
*F* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*F
*F
*F       ID:   QCTL01 ...
*F             H. C. LUDLAM, FEBRUARY, 1973.
*  Updates:                                                           *
*    10Apr88  U006 LDW - Don't CLOSE if didn't OPEN!  (This bug       *
*                           didn't show up until MVS/XA 2.2)          *
*                      - Fix id on SAVE to make ASMH V2 happy         *
*    10/20/82 U005 LDW - SEARCH LPA FOR MODULE TO BE ATTACHED         *
*    11/18/81 U004 LDW - CHANGE SYSTEMS PROGRAMMER TEST TO PSCBACCT   *
*    ??/??/79 U003 LDW - ADD ALIASES 'ACP' AND 'ATASKLIB' TO ATTACH   *
*                           A COMMAND WITH APF AUTHORIZATION          *
*                           (REQUIRES THIS COMMAND TO BE IN IKJEFTE2) *
*    ??/??/79 U002 LDW - ADD ALIAS OF 'CP' TO ASSUME DDNAME=SYSCP     *
*    ??/??/79 U001 LDW - FIX FOR MVS (ATTACH LIST IS LONGER)          *
*                                                                     *
*F       FUNCTION:
*F             THIS IS A TSO COMMAND PROCESSOR WHICH ALLOWS THE
*F             INVOCATION OF ANOTHER COMMAND PROCESSOR OR PROCEDURE
*F             WITH THE SPECIFICATION OF A TASK LIBRARY.
*F             THIS IS SOMEWHAT LIKE BEING ABLE TO 'CALL' A
*F             COMMAND PROCESSOR.  (NOTE THAT THE LIBRARY IS NOT
*F             IN EFFECT DURING THE EXECUTION OF A COMMAND
*F             PROCEDURE, BUT ONLY DURING THE BUILDING OF IT.)
*F             THE MODULE IS NORMALLY STORED UNDER AN ALIAS OF
*F             'TASKLIB.'
*F       ENTRY:
*F             THE MODULE CAN BE INVOKED VIA ANY MEANS THAT WILL
*F             PASS A CPPL VIA R1.  THIS INCLUDES DIRECT INVOCATION
*F             BY COMMAND 'TASKLIB' OR  INDIRECT INVOCATION THROUGH
*F             'TEST.'  IT DOES NOT INCLUDE 'CALL.'
*F             THE INPUT COMMAND BUFFER CONTAINS:
*F
*F                  TASKLIB   FILE  COMMAND   OPERANDS -OR-
*F                  CP              COMMAND   OPERANDS             U002
*F                  ACP             COMMAND   OPERANDS             U003
*F
*F             WHERE:  'TASKLIB' IS THE INVOKING COMMAND NAME;
*F                  'FILE' IS A FILENAME TO BE OPENED AS A TASK
*F                  LIBRARY (IT SHOULD ALREADY BE COMPLETELY
*F                  ALLOCATED -- WE USE A FILE INSTEAD OF A DATA
*F                  SET NAME TO GIVE US A CONCATENATED ABILITY);
*F                  IF THIS OPERAND IS SPECIFIED AS '*,' NO TASK
*F                  LIBRARY WILL BE USED.
*F                  IF THIS COMMAND IS EXECUTED UNDER A NAME OF    U002
*F                  'CP', A DDNAME OF 'SYSCP' WILL BE USED.        U002
*F                  IF THIS COMMAND IS EXECUTED UNDER A NAME OF    U003
*F                  'ACP', NO TASK LIBRARY WILL BE USED.           U003
*F                  (THE SYSTEM PROGRAMMER PRIVILEGE IS REQUIRED.) U003
*F                  'COMMAND' IS THE INVOKING COMMAND OF THE CP TO
*F                   BE EXECUTED UNDER THE TASK LIBRARY;
*F                  'OPERANDS' IS WHATEVER THE 'COMMAND' PROCESSOR
*F                  WANTS.
*F             WHEN THE CP IS ATTACHED, IT WILL RECEIVE THE SAME
*F             INPUT CPPL AS THIS ROUTINE, BUT ALL NON-BLANK
*F             CHARACTERS AHEAD OF 'COMMAND' WILL HAVE BEEN
*F             REPLACED BY BLANKS.
*F       NORMAL EXIT:
*F             WHEN THE CP'S ATTACH ECB IS POSTED, THE TASK LIBRARY
*F             DCB IS CLOSED AND THIS ROUTINE RETURNS NORMALLY.
*F       ABNORMAL EXIT:
*F             IF ANY ERROR IS DISCOVERED, THIS ROUTINE WILL ISSUE A
*F             MESSAGE AND RETURN WITH A CODE OF 8, AS OPPOSED TO THE
*F             USUAL ZERO.  THE ERROR MESSAGE IS ONE OF THESE
*F             VARIANTS:
*F        'QCTL001I TASKLIB FAILED -- INVALID COMMAND SYNTAX.'
*F                                 -- CANNOT OPEN LIBRARY FILE.'
*F                                 -- BLDL ERROR.'
*F             THE NAME 'TASKLIB' IS NOT BUILT IN, BUT WILL APPEAR
*F             AS WHATEVER ALIAS WAS USED TO INVOKE THIS ROUTINE.
*F       EXTERNAL ROUTINES:
*F             IKJSCAN VIA LOAD/CALL/DELETE, IKJPUTL VIA LINK,
*F             IKJDAIR VIA LINK.
*F             THE NAMED COMMAND PROCCESSOR, VIA ATTACH.
*F             OS SUPERVISOR SERVICES:
*F                  GETMAIN, FREEMAIN, ABEND, OPEN, CLOSE, BLDL, LINK,
*F                  ATTACH, DETACH, WAIT, IDENTIFY, LOAD, DELETE.
*F       TABLES / WORKAREAS:
*F             THE CPPL, CSPL, CSOA, DCB, ECT, ATTACH PARAMETER LIST,
*F             THE DAIR PARAMETER LIST, AND DAIR BLOCK '2C.'
*F       ATTRIBUTES:
*F             REENTRANT; READ-ONLY;  COMPLETELY NON-PRIVILEGED.
*F       NOTES / DEPENDENCIES
*F             THE ROUTINE IS TOTALLY DEPENDENT ON THE TSO ENVIRONMENT.
*F             IT CAN BE USED UNDER 'TEST,' BUT THE MODULE REALLY
*F             BEING TESTED WILL NOT BE IN CORE UNTIL AFTER A 'GO,'
*F             WHICH WILL AFFECT CONSOLE TECHNIQUES.
*F
*F
*F* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         LCLC  &GEN
&GEN     SETC  'GEN'
         PRINT &GEN
         SPACE 2
QCTL01   CSECT
         SPACE
R0       EQU   0                        THESE ARE REGISTERS WHICH ARE
R1       EQU   1                        ASSIGNED GLOBAL MEANINS
R13      EQU   13                       FOR THIS OPERATING SYSTEM.
R14      EQU   14                       THEY DON'T DESERVE  REAL
R15      EQU   15                       NAMES.
CPPLR    EQU    2                       --> INPUT PARAMETER LIST.
WK       EQU    3                       GENERAL WORK REGISTER.
SCRET    EQU    4                       RETURN FROM THE SCAN ROUTINE.
CA       EQU    5                       ADDRESS OF SCANNED COMMAND.
CLNG     EQU    6                       LENGTH OF SCANNED COMMAND.
ACB      EQU    7                       --> COMMAND BUFFER.
RC       EQU    8                       HOLDS RETURN CODE.
SCREG    EQU    9                       HOLDS 'SCAN' ADDRESS.
BASE     EQU    10                      PROGRAM BASE REGISTER.
DCBR     EQU    11                      INDICATES PRESENCE OF DCB.
         EJECT
TASKLIB  SAVE  (14,12),,TASKLIB_&SYSDATE._&SYSTIME NORMAL, BORING  U006
         LR    CPPLR,R1                 ENTRY-POINT HOUSEKEEPING.  SAVE
         USING CPPL,CPPLR               REGS, SET UP ADDRESSABILITIES,
         LR    BASE,R15
         USING TASKLIB,BASE
         L     R0,COREPARM              GET THE DYNAMIC CORE AREA,
         GETMAIN R,LV=(0)
         ST    R0,0(,R1)                CHAIN IT INTO THE SAVE
         ST    R1,8(,R13)               STACK, AND
         ST    R13,4(,R1)
         LR    R13,R1                   PUSH DOWN THE STACK.
         USING DSA,R13
         SPACE 2
         MVC   DINIT(256),SINIT         MOVE THE STATIC INTITAL VALUES
         MVC   DINIT+256(DLINIT-256),SINIT+256                     U001
         MVC   CSPLUPT,CPPLUPT          INTO THE DYNAMIC AREA.  FILL IN
         MVC   CSPLECT,CPPLECT          THE ADDRESSES PASSED FROM TSO.
         MVC   CSPLCBUF,CPPLCBUF
         MVC   SAVEPSCB,CPPLPSCB
         LA    WK,DFLAG                 ADJUST THE RELOCATABLE PARTS
         ST    WK,CSPLFLG               OF THE DYNAMIC AREA.
         LA    WK,DECB
         ST    WK,CSPLECB
         MVI   DECB,0
         SPACE
         LA    WK,CSOA
         ST    WK,CSPLLOA
         SPACE
         LOAD  EPLOC=SCNAME             LOAD AND SAVE THE
         LR    SCREG,R0                 'SCAN' ROUTINE.
         SPACE
         BAL   SCRET,SCAN               LOCATE AND CLEAR THE ORIGINAL
         EX    CLNG,SETNM               COMMAND.  SAVE IT FIRST,
         EX    CLNG,CLRCOM              MAKING A TENTATIVE MESSAGE
         LA    CLNG,5(,CLNG)            SEGMENT.
         STH   CLNG,MSGNAME
         SPACE
         SLR   DCBR,DCBR
         MVI   DFLAG,X'80'
         SPACE 1
         CLI   MSGNAME+4,C'A'           AUTHORIZED VERSION?        U003
         BNE   NOT$A                    NO - SKIP CHECK            U003
         L     R15,SAVEPSCB             -> PSCB                    U003
*U004    TM    PSCBATU1-PSCB(R15),PSCBSYSP  SYSTEMS PROGRAMMER?    U003
         TM    PSCBATR1-PSCB(R15),PSCBACCT  SYSTEMS PROGRAMMER?    U004
         BNO   ERR4                     NO - BLOW HIM OUT          U003
         CLC   =C'CP ',MSGNAME+5        IS THIS 'ACP'?             U003
         BNE   ATASKLIB                 NO - GET THE FILE NAME     U003
         MVI   DFLAG,X'00'                                         U003
         B     NOFILE2                  YES - NO FILE NAME TO USE  U003
         SPACE 2
NOT$A    CLC   =C'CP ',MSGNAME+4        BY MY ALIAS NAME?          U002
         BE    CP$CP01                  YES - DDNAME IS 'SYSCP'    U002
         SPACE 1
ATASKLIB BAL   SCRET,SCAN               BREAKOUT THE FILE NAME.  IF IT
         MVI   DFLAG,X'00'
         CLI   0(CA),C'*'               IS '*' THERE IS NO FILE.
         BE    NOFILE                   OTHERWISE, SET DCB'S IN THE
         SPACE 1
CP$CP01  LA    DCBR,DDCB                ATTACH LIST AND A REGISTER.
         ATTACH SF=(E,DATCH),DCB=(DCBR),TASKLIB=(DCBR)
         ORG   *-2
         SPACE
         CLC   =C'CP ',MSGNAME+4        BY MY ALIAS NAME?          U002
         BE    CP$CP02                  YES - DDNAME IS 'SYSCP'    U002
         MVC   DDNAME(8),=CL8' '        BLANK THE RECEIVING FIELD  U002
         EX    CLNG,SETFILE             SET THE INDICATED DDNAME AND
CP$CP02  OPEN  DDCB,MF=(E,DOPEN)        TRY TO OPEN THE NAMED FILE.
         TM    DOFLG,OPENED             IF IT WON'T COOPERATE,
         BZ    ERR3                     NEITHER NEED WE.
         SPACE
NOFILE   CLC   =C'CP ',MSGNAME+4        BY MY ALIAS NAME?          U002
         BE    *+8                      YES - NO DDNAME TO CLEAR   U002
         EX    CLNG,CLRCOM              CLEAR THE FILE NAME OR *.
NOFILE2  BAL   SCRET,SCAN               BREAKOUT THE COMMAND NAME, AND
         EX    CLNG,SETCP               MOVE IT TO THE ATTACH LIST.
         SPACE 2
*---  FIRST, SEE IF THE MODULE IS IN LPA.                          U005
*  **WARNING** REGISTER 3 IS CALLED 'WK'!!!                        U005
EXECLOOP L     3,CVTPTR                 GET CVT ADDRESS            U005
         LM    R0,R1,DEPN               GET MODNAME FOR ROUTINE    U005
         L     R15,CVTLPDSR-CVTMAP(,3)  GET ADDRESS OF ROUTINE     U005
         BALR  R14,R15                  CALL ROUTINE               U005
         B     BYNAME                   +0 RETURN = FOUND IN LPA   U005
*                                       +4 RETURN = NOT   IN LPA   U005
         IDENTIFY EPLOC=DEPN,ENTRY=0    BEFORE YOU BLDL, WHICH'LL
         CH    R15,H12                  PRECLUDE USE OF AN IN-CORE
*U005    LA    R15,DATCH                MODULE, DECIDE WHETHER SUCH A
         BE    NOTLDLD                  MODULE EXISTS BY TRYING TO
*U005    ATTACH  SF=(E,(15)),EPLOC=DEPN  REUSE THE NAME.  SIMULATE
BYNAME   ATTACH  SF=(E,DATCH),EPLOC=DEPN REUSE THE NAME.  SIMULATE U005
         ORG   *-2                      A 'B' FORM MACRO INSTRUCTION,
         B     BLDLD                    SETTING UP FOR EITHER FAST
NOTLDLD  ATTACH  SF=(E,DATCH),DE=DEPN   DISK FETCH OR CORE SEARCH. U005
         ORG   *-2                      IF IDENTIFY WORKED, NOT LOADED.
         SPACE
         BLDL  (DCBR),BLDLIST           ATTEMP TO LOCATE THE COMMAND
         CH    R15,H4                   PROCESSOR IN THE NEW LIBRARY.
         BL    BLDLD                    PASS BACK ANY ERROR.
         BH    ERR1                     IF NOT THERE,
         BLDL  0,BLDLIST                TRY THE COMMAND LIBRARY.
         CH    R15,H4                   IF IT IS FOUND, GO FINISH THE
         BL    BLDLD                    TB.  PASS BACK ANY ERROR.
         BH    ERR1                     IF NOT IN COMMAND LIB,
         L     WK,CPPLCBUF              TRY AGAIN IN
         CLC   2(2,WK),ZERO             THE PROCEDURE LIBRARIES.
         BE    ERR1                     ONE TIME ONLY, ZERO OUT THE
         MVC   2(2,WK),ZERO             'SCAN' OFFSET POINTER, AND
         MVC   DEPN,EXECNAME            TRY TO CALL 'EXEC'.
         B     EXECLOOP
         SPACE
BLDLD    L     WK,CPPLECT               ADDRESS THE ECT.
         USING ECT,WK                   SAVE THE OLD COMMAND NAME
         MVC   OLDNAME,ECTPCMD          AND REPLACE IT WITH THE
         MVC   ECTPCMD,DEPN             NEW ONE.
         MVC   ECTSCMD,BLANKS
         MVI   DECB,0
         TM    CSOAFLG,CSOAVNP          SINCE WE HAVE CHANGED THE
         BZ    *+8                      COMMAND BUFFER, RESET THE
         OI    ECTSWS,ECTNOPD           'NO OPERANDS' BIT.
         ATTACH SF=(E,DATCH),           ATTACH THE NAMED PROCESSOR     *
               MF=(E,(CPPLR)),          USING THE PARAMETERS THAT      *
               ECB=DECB                 HAVE BEEN SET UP.
         SPACE
         ST    R1,DAPTCB                HANG ONTO THE TASK ADDRESS, AND
         WAIT  1,ECB=DECB               WAIT FOR WHATEVER.
         SPACE
         MVI   DECB,0                   TRANSFORM SCAN PARAMETER LIST
         LA    WK,DAPB                  INTO A DAIR LIST.
         ST    WK,TMDAPB
         MVC   TMDPSCB,SAVEPSCB         CALL DAIR TO MARK FILES
         LA    R1,TMDAIR                'NOT IN USE.'
         LINK  EP=IKJDAIR
         SPACE
         DETACH DAPTCB                  IT'S ALL OVER --
         SPACE
         LA    RC,0                     RETURN WITH AN OK
         B     CLOSE                    STATUS CODE.
         SPACE 2
ERR1     LA    WK,SEG3A                 FOR EACH ERROR CONDITION,
         B     ERRJOIN                  SELECT THE APPROPRIATE
ERR2     LA    WK,SEG3B                 ERROR MESSAGE.
         B     ERRJOIN
ERR3     LA    WK,SEG3C
         B     ERRJOIN                                             U003
ERR4     LA    WK,SEG3D                                            U003
ERRJOIN  ST    WK,ASEG3                 COMPLETE THE PUTLINE
         LA    WK,MSGNAME               PARAMETERS AND WRITE
         ST    WK,ASEG2                 THE MESSAGE.
         PUTLINE MF=(E,CSPL),PARM=PLPB,OUTPUT=OLD1
         LA    RC,8                     RETURN A BAD CODE.
         SPACE 3
CLOSE    CLC   =C'ACP',MSGNAME+4        is this 'ACP'?             U006
         BE    DELETE                   Yes - nothing to CLOSE     U006
         SPACE 1
         CLOSE MF=(E,DOPEN)             WASH UP AND FORGET IT.
         SPACE 2
DELETE   DELETE  EPLOC=SCNAME           DELETE 'SCAN.OC=SCNAME
         SPACE
         LA    R1,0(,R13)               ADDRESS THE DSA AND FREE IT.
         L     R0,0(,R1)
         L     R13,4(,R13)
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,RC                   RESTORE ALL REGS BUT THE
         L     R14,12(0,R13)            RETURN CODE, AND RETURN.
         RETURN (0,12),T,RC=(15)
         EJECT
SCAN     L     ACB,CPPLCBUF             RTN TO LOCATE THE FIRST THING
         USING CBUF,ACB                 STILL IN THE CBUFFER.  ALWAYS
         MVC   CBOFS,ZERO               START FROM THE BEGINNING.
         SPACE
         LA    R1,CSPL                  CALL THE SCAN ROUTINE.
         LR    R15,SCREG
         BALR  R14,R15
         LTR   R15,R15                  IF SCAN DID NOT COMPLETE, OR
         BNZ   ERR1                     RETURNED A LESS-THAN-NICE
         TM    CSOAFLG,CSOAVWP+CSOAVNP  OPINION, FORGET IT.
         BZ    ERR1
         SPACE
         L     CA,CSOACNM               DON'T ALLOW STRINGS LONGER
         LH    CLNG,CSOALNM             THAN EIGHT CHARACTERS.
         CH    CLNG,H8                  IF, THROUGHOUT ALL THESE PERILS
         BH    ERR1                     YOU REMAIN STEADFAST, YOU MAY
         BCTR  CLNG,SCRET               RETURN SAFELY TO YOUR
         BR    SCRET                    WHATEVER ...
         SPACE 2
SETFILE  MVC   DDNAME(0),0(CA)          VARIOUS INSTRUCTIONS THAT MAY
CLRCOM   MVC   0(0,CA),BLANKS           BE EXECUTED WITH THE RESULTS
SETCP    MVC   DEPN(0),0(CA)            OF THE ABOVE ROUTINE.
SETNM    MVC   MSGNAME+4(0),0(CA)
         EJECT
*        STATIC DATA AREA:
         SPACE
SCNAME   DC    CL8'IKJSCAN'             'LOAD' NAME FOR 'SCAN' RTN.
EXECNAME DC    CL8'EXEC'                CP NAME FOR 'EXEC' COMMAND.
COREPARM DC    0F'0',AL1(0),AL3(LDSA)   GETMAIN PARAMETER.
H4       DC    H'4'                     USEFUL CONSTANTS...
H8       DC    H'8'
H12      DC    H'12'
         SPACE
SEG1     DC    Y(ES1-*,0)               SKELETON OF ERROR MESSAGE.
MS1A     DC    C'QCTL001I '
MS1B     DC    C' FAILED -- '
MS1C     DC    C'.'
ES1      EQU   *
         SPACE
SEG3A    DC    Y(ES3A-*,MS1C-MS1A),C'INVALID COMMAND SYNTAX'
ES3A     EQU   *
         SPACE
SEG3B    DC    Y(ES3B-*,MS1C-MS1A),C'BLDL ERROR'
ES3B     EQU   *
         SPACE
SEG3C    DC    Y(ES3C-*,MS1C-MS1A),C'CANNOT OPEN LIBRARY FILE'
ES3C     EQU   *
         SPACE
SEG3D    DC    Y(ES3D-*,MS1C-MS1A),C'COMMAND NOT AUTHORIZED FOR USERID'
ES3D     EQU   *                                                   U003
         EJECT
DSA      DSECT DESCRIBING THE DYNAMIC STORAGE AREA:
         DS    18F                      STANDARD BORING SAVE AREA.
         SPACE
DINIT    DS    0F  THIS IS THE BEGINNING OF THE INITIALIZATION AREA.
         SPACE
DATCH    ATTACH SF=L,                   HERE GOES THE SKELETON         *
               SHSPV=78,                ATTACH PARAMETER LIST.         *
               SZERO=NO
         SPACE
DECB     DC    F'0'                     TERMINATION ECB.
         SPACE
         PRINT NOGEN
DDCB     DCB   DSORG=PO,                SKELETON DATA CONTROL BLOCK.   *
               MACRF=E,                 THIS IS BOTH TASKLIB, AND THE  *
               DDNAME=SYSCP             ATTACH DCB ITSELF.         U002
DOPEN    OPEN  0,MF=L                   SKELETON OPEN PARAMETERS.
         PRINT &GEN
         SPACE
CSPL     DS    0F                       THE COMMAND SCAN PARAM LIST:
CSPLUPT  DC    A(0)                     --> USER PROFILE TABLE.
CSPLECT  DC    A(0)                     --> ENVIRONMENT CONTROL TABLE.
CSPLECB  DC    A(0)                     --> INTERRUPT ECB (NOT USED).
CSPLFLG  DC    A(0)                     --> INPUT OPTION FLAGS.
CSPLLOA  DC    A(0)                     --> SCAN OUTPUT AREA.
CSPLCBUF DC    A(0)                     --> COMMAND BUFFER.
         SPACE
         ORG   CSPL                     OVERLAY WITH
TMDAIR   DS    0F                       DAIR PARAMETER LIST:
TMDUPT   DS    A                          --> UPT.
TMDECT   DS    A                          --> ECT.
TMDECB   DS    A                          --> ECB.
TMDPSCB  DS    A                          --> PSCB.
TMDAPB   DS    A                          --> PARAMETERS.
         SPACE
         ORG
DAPB     DS    0F                       DAIR PARAMETERS:
DAPID    DC    X'002C0000'                CODE FOR 'MARK NOT IN USE.'
DAPTCB   DS    A                          --> TCB.
DAECB    DS    A                          DAIR ECB.
         SPACE
DFLAG    DC    BL4'0'                   OPTIONS -- REQ SYNTAX CHECK.
         SPACE
OLD1     DC    A(3)                     SKELETON FOR AN OUTPUT
ASEG1    DC    A(0)                     LINE DESCRIPTOR WITH
ASEG2    DC    A(0)                     THREE SEGMENTS.
ASEG3    DC    A(0)
         SPACE
MSGNAME  DC    2H'0',CL8' '             PLACE FOR CP NAME.
         SPACE
PLPB     DC    3F'0'                    PUTLINE PARAMETER BLOCK.
         SPACE
BLDLIST  DC    F'0'
DEPN     DC    0CL8' ',CL58' '          ATTACH PROGRAM NAME
         SPACE
DLINIT   EQU   *-DINIT  LENGTH OF THE AREA TO INITIALIZE.
         SPACE
CSOA     DS    0F                       COMMAND SCAN OUTPUT AREA:
CSOACNM  DC    A(0)                     --> COMMAND NAME.
CSOALNM  DC    H'0'                     TRUE LENGTH OF NAME.
CSOAFLG  DC    BL2'0'                   SCAN STATUS FLAGS:
CSOAVWP  EQU   X'80'                    VALID WITH PARAMETERS.
CSOAVNP  EQU   X'40'                    VALID WITH NO PARAMETERS.
CSOAQM   EQU   X'20'                    QUESTION MARK.
CSOANOC  EQU   X'10'                    NO COMMAND AT ALL.
CSOABAD  EQU   X'08'                    INVALID COMMAND NAME.
         SPACE
SAVEPSCB DS    A                        --> PSCB.
         SPACE
OLDNAME  DS    CL16                     STORAGE FOR SWAPPING ECT NAME.
         EJECT
QCTL01   CSECT RESUME TO BUILD DSA'S INITIALIZATION AREA:
         SPACE
SINIT    DS    0F  THIS IS THE BEGINNING OF THE INITIALIZING DATA.
         SPACE
         ATTACH SF=L,                   HERE GOES THE SKELETON         *
               SHSPV=78,                ATTACH PARAMETER LIST.         *
               SZERO=NO
         SPACE
ZERO     DC    F'0'                     TERMINATION ECB.
         SPACE 2
         PRINT NOGEN
         DCB   DSORG=PO,                SKELETON DATA CONTROL BLOCK.   *
               MACRF=E,                                                *
               DDNAME=SYSCP                                        U002
         OPEN  0,MF=L                   SKELETON OPEN PARAMETERS.
         PRINT &GEN
         SPACE 2
         DC    6F'0'                    THE COMMAND SCAN PARAM LIST
         DC    0F'0',X'002C0000',2A(0)  DAIR PARAMETERS.
         DC    F'0'                     AND FLAGWORD.
         SPACE
         DC    A(3,SEG1,0,0)            SKELETON FOR BLOCKS FOR
SEG2     DC    Y(4,MS1B-MS1A)           'PUTLINE'.
MS2      DC    CL8' '
         PUTLINE MF=L
         SPACE
         DC    0F'0',Y(1,58)            BLDL LIST, WITH
BLANKS   DC    0CL8' ',CL58' '          ATTACH PROGRAM NAME
         SPACE 2
SLINIT   EQU   *-SINIT  LENGTH OF THE INITIALIZING DATA.
         SPACE
         DS    (SLINIT-DLINIT)X         THESE TWO LENGTHS HAD BETTER
         DS    (DLINIT-SLINIT)X         BE THE SAME.
         EJECT
ECT      DSECT DESCRIBING THE ENVIRONMENT CONTROL TABLE:
         IKJECT ,
         SPACE 2
PARAMS   DSECT DESCRIBING THE COMMAND PROCESSOR PARAMETER LIST:
         IKJCPPL  ,
         SPACE 2
         IKJPSCB  ,                                                U003
         SPACE 1
*U004    GISPSCB  ,                     GIBRALTAR BIT DEFINITIONS  U003
         SPACE 2
CBUF     DSECT DESCRIBING THE COMMAND BUFFER:
CBLNG    DS    H                        TOTAL BUFFER LENGTH.
CBOFS    DS    H                        SCAN HISTORY INDEX.
CBTXT    DS    C                        ACTUAL TEXT...
         SPACE 2
         PRINT NOGEN                    NOBODY WANTS TO SEE:
         CVT   DSECT=YES                                           U005
         DCBD  DSORG=XE,DEVD=DA
         PRINT &GEN
DOFLG    EQU   DCBOFLGS-IHADCB+DDCB
OPENED   EQU   X'10'
DDNAME   EQU   DCBDDNAM-IHADCB+DDCB
         SPACE 2
DSA      DSECT TO BE FINALIZED:
         ORG
         DS    0D
LDSA     EQU   *-DSA
         SPACE
QCTL01   CSECT TO BE FINALIZED:
         LTORG
         SPACE
         END   QCTL01
