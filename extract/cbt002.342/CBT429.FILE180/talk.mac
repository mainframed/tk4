***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1990 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
TALK     TITLE '-----  Simple two-way TSO communication  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   Program name:  TALK                                               *
*                                                                     *
*   Purpose:       Provide a simple alternative to repeated SENDs     *
*                                                                     *
*   Author:        Leonard D. Woren                                   *
*                                                                     *
*   Date:          March 21, 1987                                     *
*                                                                     *
*   Attributes:    None                                               *
*                                                                     *
*   Installation:  University of Southern California                  *
*                                                                     *
*   Updates:                                                          *
*      13Apr87  U001 LDW - Indent prompt and change TGET length       *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
TALK     OSENTER  EXIT=LEAVE,RC=0,PARMREG=R2,ADDRREG=R14,LENREG=R15,   $
               ENV=(CALL,SIM)
         LTR   R15,R15                  Any parm?
         BNZ   HAVEPARM                 Yes - continue
         TPUTX 'TALK - target userid operand required'
         B     LEAVE
         SPACE 2
MVC$PARM OC    HISID(*-*),0(R14)        << Executed >>
HAVEPARM CH    R15,=H'8'                Too long?
         BL    PARM$OK                  No - continue
         TPUTX 'TALK - target userid invalid (> 7 characters)'
         B     LEAVE
         SPACE 2
PARM$OK  BCTR  R15,0                    -1 for EX
         EX    R15,MVC$PARM             OC  hisid,0(R14)
         L     R1,16                    -> CVT
         L     R1,0(,R1)                -> Dispatch
         L     R1,4(,R1)                -> TCB
         L     R1,180(,R1)              -> JSCB
         L     R1,264(,R1)              -> PSCB
*        MVC   PROMPT(7),PSCBUSER-PSCB(R1)  Move in my userid
         MVC   PROMPT(7),0(R1)          Move in my userid
         MVC   PROMPT+27(7),0(R1)       Move in my userid
         MVC   BYEMSG(7),0(R1)          Move in my userid
         MVC   MYID(7),0(R1)            Move in my userid
         SR    R15,R15                  Clear for IC
         IC    R15,7(R1)                Get userid length
         LA    R15,MYID(R15)            -> past end of userid
         MVI   0(R15),C':'              Put in separator
         TPUT  PROMPT,PROMPTL,USERIDL=HISID   Prompt the other guy
         BAL   R11,TPUT_CHK             Check the TPUT return code
         TPUTX 'Enter null (not blank) line to wait, "end" to exit'
         SPACE 2
*LOOP    TPUTX '>',ASIS
LOOP     TPUTX '        >',ASIS                                    U001
*        ???
         LA    R1,BUFFER
         LA    R0,L'BUFFER
         TGET  (1),(0)
*        ???
         LTR   R1,R1                    Any input?
         BNP   LOOP                     No - just ask again
         MVC   WORK(3),BUFFER           Get first part of input
         OC    WORK(3),=CL3' '          Upper case it
         CLC   =C'END',WORK             Want out?
         BE    BYE                      Yes
         LA    R0,L'MYID(,R1)           Get length of input
         TPUT  MYID,(0),USERIDL=HISID     So do it
         BAL   R11,TPUT_CHK             Check the TPUT return code
         B     LOOP
         SPACE 2
BYE      TPUT  BYEMSG,BYEMSGL,USERIDL=HISID
         B     LEAVE
         SPACE 2
TPUT_CHK B     *+4(R15)                 decode TPUT return code
         BR    R11                      00 - ok
         NOPR  0                        filler
         B     TPUT_ERR                 04 - no buffers
         B     TPUT_ERR                 08 - attn
         B     TPUTRC12                 12 - nointercom
         B     TPUT_ERR                 16 - bad parmlist
         B     TPUTRC20                 20 - disc or not logged on
         SPACE 1
TPUT_ERR MVC   ERRMSG+ERRMSGL-8(8),HISID
         TPUTX ERRMSG,ERRMSGL           'Error in TPUT to xxxxxxxx'
         B     BYE
         SPACE 1
TPUTRC12 MVC   NOINT(8),HISID
         TPUTX NOINT,NOINTL             'xxxxxxxx is not accepting msgs
         B     BYE
         SPACE 2
TPUTRC20 MVC   DISCMSG(8),HISID
         TPUTX DISCMSG,DISCMSGL         'Error in TPUT to xxxxxxxx'
         B     BYE
         SPACE 2
         LTORG ,
         SPACE 2
HISID    DC    CL8' '                   Target hisid
PROMPT   DC    C'xxxxxxx TALKing, use "TALK xxxxxxx" to respond.'
PROMPTL  EQU   *-PROMPT
BYEMSG   DC    C'xxxxxxx done TALKing (enter "END" to exit).'
BYEMSGL  EQU   *-BYEMSG
ERRMSG   DC    C'Error in TPUT to xxxxxxxx.'
ERRMSGL  EQU   *-ERRMSG
NOINT    DC    C'xxxxxxxx is not accepting messages.'
NOINTL   EQU   *-NOINT
DISCMSG  DC    C'xxxxxxxx is disconnected or not logged on.'
DISCMSGL EQU   *-DISCMSG
MYID     DC    C'xxxxxxx  '             My id, with ':' after
*BUFFER  DC    CL71' '
BUFFER   DC    CL69' '                                             U001
WORK     DC    CL8' '
         END
