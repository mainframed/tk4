WHICH    TITLE '-----  Find which library a module is in -----'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*   Program name:  WHICH                                              *
*                                                                     *
*   Purpose:       Do a BLDL and then interpret the results and       *
*                  display the library name.  Note that this program  *
*                  runs only under MVS/XA because it needs to use     *
*                  the table of linklist dsnames.                     *
*                                                                     *
*   Author:        Leonard D. Woren                                   *
*                                                                     *
*   Date:          May 15, 1988                                       *
*                                                                     *
*   Attributes:    AMODE=31                                           *
*                                                                     *
*   Installation:  University of Southern California                  *
*                                                                     *
*   Updates:                                                          *
*      26May91  U001 LDW - Check for module in LPA                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
WHICH    OSENTER  EXIT=LEAVE,RC=(R15),AMODE=31,RMODE=24,               $
               GETMAIN=(WORKLEN,WORKD),PL=(IO),                        $
               PARMREG=R2,ENV=(CP,SIM),LENREG=R14,ADDRREG=R15
         MVC   BUFFOLD(4),=F'1'         NUMBER OF OUTPUT SEGMENTS
         LA    R1,BUFFLEN               -> PUTLINE BUFFER
         ST    R1,BUFFOLD+4
         XC    BUFFOFF(2),BUFFOFF       OFFSET
         MVI   BUFFMID,C' '             INIT MSGID
         LTR   R14,R14                  any parm?
         BP    HAVEPARM                 yes - continue
         MVC   BUFFER(24),=C'WHICH - operand required'
         MVC   BUFFLEN,=H'29'           set length
         BAL   R11,PUTLINE
         LA    R15,12                   set rc
         B     LEAVE
         SPACE 2
HAVEPARM CH    R14,=H'8'                parm length ok?
         BNH   PARMOK                   yes
         MVC   BUFFER(24),=C'WHICH - operand too long'
         MVC   BUFFLEN,=H'29'           set length
         BAL   R11,PUTLINE
         LA    R15,12                   set rc
         B     LEAVE
         SPACE 2
PARMMVC  OC    MEMBER(*-*),0(R15)       << executed >>
PARMOK   BCTR  R14,0                    -1 for ex
         MVC   MEMBER,=CL8' '           blank the target
         EX    R14,PARMMVC              save member name, in upper case
         MVC   BUFFER(8),MEMBER
         LM    R0,R1,MEMBER             get parms for routine
         L     R3,CVTPTR                -> CVT
         L     R15,CVTLPDSR-CVTMAP(,R3) -> LPA search routine
         BALR  R14,R15                  call it
         B     FOUNDLPA              +0 module is in LPA
*                                    +4 module not in LPA
         MVC   BLDL(4),=H'1,14'         number of entries, length
*
         BLDL  0,BLDL                   try to find the module
*
         LTR   R15,R15                  find it?
         BNZ   NOT$LOAD                 no - try for a clist in SYSPROC
         CLI   Z,0                      private library?
         BNE   *+8                      no - ok
         EX    0,*                      yes - shouldn't happen
         CLI   Z,1                      link library?
         BE    LINKLIST                 yes
         MVC   BUFFER(8),MEMBER
         MVC   BUFFER+09(11),=C' K=xx, Z=xx'
         HEX   BUFFER+12,K,LEN=1,BYTE=C','
         HEX   BUFFER+18,Z,LEN=1
         MVC   BUFFLEN,=H'25'           set length
*%       BAL   R11,PUTLINE              %debug
         NOP   PUTLINE                  %debug
         L     R15,PSATOLD-PSA          -> current TCB
         SR    R0,R0                    clear for ic
         IC    R0,Z                     get library indicator
         SH    R0,=H'2'                 adjust
         BNM   *+8                      continue if ok
         EX    0,*                      logic error
         BZ    GET$DCB                  skip loop if current task's jlb
         L     R15,TCBOTC-TCB(,R15)     -> parent
         BCT   R0,*-8                   go up the TCB tree
         SPACE 1
GET$DCB  L     R4,TCBJLB-TCB(,R15)      -> joblib DCB
         BAL   R11,FIND$DSN             get the dsn
         MVC   BUFFER,BLANKS
         MVC   BUFFER(8),MEMBER
         MVC   BUFFER+9(31),=C'found in tasklib-nnn (xxxxxxxx)'
         SR    R0,R0                    clear for ic
         IC    R0,Z                     get library indicator
         SH    R0,=H'2'                 adjust
         CVD   R0,DWD
         OI    DWD+7,X'0F'              remove sign
         UNPK  BUFFER+26(3),DWD         put concatenation number in msg
         MVC   BUFFER+31(8),DDNAME
         MVC   BUFFER+41(44),0(R1)      fill in dsn
         MVC   BUFFLEN,=H'90'
         CLI   Z,2                      tasklib for this task?
         BNE   *+4+6+6
         MVC   BUFFER+25(56),BUFFER+29  shift out the -nnn
         MVC   BUFFLEN,=H'86'           and adjust length
         BAL   R11,PUTLINE
         SR    R15,R15                  set rc
         B     LEAVE
         SPACE 2
LINKLIST MVC   BUFFER(8),MEMBER
         MVC   BUFFER+8(24),=C' is in linklist library '
         MVC   BUFFER+32(44),BLANKS
         HEX   BUFFER+32,K,LEN=1
         SR    R15,R15                  clear for ic
         IC    R15,K                    get concatenation number
         L     R1,CVTPTR                -> cvt
         L     R2,CVTLLTA-CVT(,R1)      -> link list table
         CLC   =C'LLT ',0(R2)           id there?
         BE    *+8                      yes - continue
         EX    0,*
         C     R15,4(,R2)               K too high?
         BNH   *+8                      no - ok
         EX    0,*                      yes - die
*
         MH    R15,=H'45'               multiply by entry length
         LA    R15,8(R15,R2)            -> dsname
         MVC   BUFFER+32(44),1(R15)
         MVC   BUFFLEN,=H'81'
         BAL   R11,PUTLINE
         SR    R15,R15                  set rc
         B     LEAVE
         SPACE 3
FOUNDLPA MVC   BUFFER,BLANKS
         MVC   BUFFER(8),MEMBER
         MVC   BUFFER+9(12),=C'found in LPA'
         MVC   BUFFLEN,=H'26'           and adjust length
         BAL   R11,PUTLINE
         SR    R15,R15                  set rc
         B     LEAVE
         SPACE 3
*--- try to find a clist with this name
NOT$LOAD OPEN  SYSPROC
         TM    SYSPROC+48,X'10'         did it open?
         BZ    NOTFOUND                 no - so the module is not found
*
         BLDL  SYSPROC,BLDL
*
         LTR   R15,R15                  did it work?
         BNZ   NOTFOUND                 nope - module not found
         CLI   Z,0                      private library?
         BE    *+8                      yes - ok
         EX    0,*                      no - logic error
         LA    R4,SYSPROC               -> DCB
         BAL   R11,FIND$DSN
         MVC   BUFFER(8),MEMBER
         MVC   BUFFER+8(31),=C' is a clist in SYSPROC library '
         MVC   BUFFER+39(44),0(R1)      dsname
         MVC   BUFFLEN,=H'88'
         BAL   R11,PUTLINE
         LA    R15,4                    set rc
         B     LEAVE
         SPACE 2
NOTFOUND MVC   BUFFER(25),=C'module xxxxxxxx not found'
         MVC   BUFFER+7(8),MEMBER
         MVC   BUFFLEN,=H'30'
         BAL   R11,PUTLINE
         LA    R15,8                    set rc
         B     LEAVE
         SPACE 3
         USNGX IHADCB,R4
FIND$DSN SR    R0,R0                    clear for icm
         ICM   R0,B'0001',K             get concatenation number
*  locate the TIOT DD entry for this concatenation
         L     R15,PSATOLD-PSA          -> current TCB
         L     R15,TCBTIO-TCB(,R15)     -> TIOT
         AH    R15,DCBTIOT              -> entry for this DCB
         USNGX TIOENTRY,R15
         MVC   DDNAME,TIOEDDNM          save ddname for later
         LTR   R0,R0                    first concat #?
         BNP   CONCATOK                 yes - skip loop
         SR    R14,R14                  clear for icm
CONCATLP ICM   R14,B'0001',TIOELNGH     get length of this entry
         AR    R15,R14                  -> next entry
         BCT   R0,CONCATLP
         SPACE 2
*  r15 -> TIOT DD entry for desired dataset
CONCATOK SR    R1,R1                    clear for icm
         ICM   R1,B'0111',TIOEJFCB      -> JFCB prefix
         LA    R1,16(,R1)               -> JFCB
         BR    R11                      return with dsn ptr in R1
         SPACE 1
         DROPX R4,R15                   IHADCB, TIOENTRY
         SPACE 3
PUTLINE  PUTLINE  OUTPUT=BUFFOLD,PARM=PUTLINEC,MF=(E,IOPL)
         SPACE 1
         BR    R11
         SPACE 2
         LTORG ,
         SPACE 2
PUTLINEC PUTLINE  MF=L
         SPACE 2
HEXTAB   HEXTAB  DUAL=NO
         SPACE 2
BLANKS   DC    CL131' '
         SPACE 3
SYSPROC  DCB   DDNAME=SYSPROC,MACRF=E,DEVD=DA
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D                        CVD workarea
DDNAME   DS    CL8                      filled in by FIND$DSN
BLDL     DS    2H                       entry number, length
MEMBER   DS    CL8
TTR      DS    XL3
K        DS    X
Z        DS    X
         SPACE 2
BUFFOLD  DS    2F                       OUTPUT LINE DESCRIPTOR
BUFFLEN  DS    H                        PRINTLINE LENGTH
BUFFOFF  DS    H                        OFFSET
BUFFMID  DS    C                        delim after msgid
BUFFER   DS    CL131                    OUTPUT BUFFER
WORKLEN  EQU   *-WORKD                  LENGTH TO GETMAIN
         SPACE 2
         PRINT NOGEN
         SPACE 2
         IHAPSA  ,
         SPACE 2
         CVT   DSECT=YES
         SPACE 2
         IEFTIOT1  ,
         SPACE 2
         IKJTCB  ,
         SPACE 2
         DCBD  ,
         SPACE 2
         END
