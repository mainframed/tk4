WHOHAS   TITLE '-----  Show who is enqueued on a dataset  -----'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*   Program name:  WHOHAS                                             *
*                                                                     *
*   Purpose:       Give a TSO user a simple way to find out who       *
*                  (batch jobs or TSO users) is enqueued on a         *
*                  dataset.                                           *
*                                                                     *
*   Attributes:    Rent/Reus/Refr                                     *
*                                                                     *
*   Updates:                                                          *
*      04Mar88  U003 LDW - FREEMAIN some storage to avoid S40D abends *
*                        - Change to OSENTER                          *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
**********************************************************************
* MODIFICATION: 06/27/83: WHEN WE WENT TO MVS/SP1.3 WHOHAS NO LONGER *
*               WORKED. THIS WAS BECAUSE OF THE GLOBAL RESOURCE      *
*               SERIALIZATION. WHAT MVS/SP1.3 DID WAS REMOVE THE     *
*               POINTER TO THE QCB FROM THE CVT. SINCE GRS IS IN ITS *
*               OWN ADDRESS SPACE IBM GAVE US A NEW MACRO "GQSCAN".  *
*               THIS NEW MACRO BUILDS TWO NEW CONTROL BLOCKS (RIB &  *
*               RIBE) WHICH GIVES US THE SAME INFORMATION WE GOT     *
*               FROM THE QCB AND QEL.                                *
*               WHOHAS WAS ALSO CHANGED TO PREFIX ANY NON FULLY      *
*               QUALIFIED DATA SET WITH USER PREFIX FROM THE USER    *
*               PROFILE TABLE (UPT).                                 *
**********************************************************************
         SPACE 3
**********************************************************************
* MODIFICATION: 10/21/83: A NEW ALIAS WAS ASSIGNED TO WHOHAS (WH)    *
*               REPLACING THE WHO ALIAS. WHOHAS HAS HARD CODED INTO  *
*               IT A CHECH FOR THE MINIMUM LENGTH OF THE COMMAND AND *
*               THE MINUMUM OFFSET INTO THE CPPL BUFFER FOR THE      *
*               COMMAND OPERAND. SINCE THE LENGTH OF THE COMMAND     *
*               NAME WAS SHORTED FROM A MINIMUM OF THREE BYTES TO A  *
*               MINIMUM OF 2 BYTES THE MINIMUN COMMAND LENGTH AND    *
*               MINIMUN DISPLACEMENT NEEDED TO BE CHANGED.           *
**********************************************************************
         EJECT
WHOHAS   OSENTER  PARMREG=R11,USING=(CPPL,R11),EXIT=RETURN,RC=0,   U003$
               GETMAIN=(LWRKSPCE,WRKSPACE)                         U003
         MVI   DSNAME,C' '
         MVC   DSNAME+1(43),DSNAME
         MVC   MSG(5),SETMSG
         MVI   MSG+5,C' '
         MVC   MSG+6(64),MSG+5
         MVI   PSW,0
         L     R2,CPPLCBUF             LOAD BUFFER ADDRESS
         CLI   1(R2),X'08'             BUFFER LENGTH MUST BE   10/21/83
         BL    ERROR                   AT LEAST 9 BYTES AND
         CLI   3(R2),X'03'             THE OFFSET 4 BYTES      10/21/83
         BL    ERROR
         LH    R3,2(R2)                LOAD OFFSET
         LA    R3,4(R3)
         LH    R5,0(R2)                LOAD BUFFER LENGTH
         SR    R5,R3                   FIND OPERAND LENGTH
         AR    R3,R2                   POINT TO FIRST CHARACTER
*                                      AFTER COMMAND DSN
         SPACE 2
         LR    R4,R3                   FIND LAST CHARACTER
DSN1     CLI   0(R4),X'40'             OF DSNAME
         BE    DSN2
         LA    R4,1(R4)
         BCT   R5,DSN1
         SPACE 2
DSN2     BCTR  R4,R0
         CLI   0(R3),C' '
         BE    ERROR
         CLI   0(R3),C''''             IF FIRST CHARACTER IS ',
         BNE   USERID                  DON'T ADD USERID TO DSNAME
         CLI   0(R4),C''''
         BNE   ERROR
         LA    R3,1(R3)
         SR    R4,R3                   FIND LENGTH OF DSNAME - 1
         LTR   R4,R4                   AND CHECK FOR ERRORS
         BNP   ERROR
         LR    R8,R4
         LA    R5,44
         CR    R8,R5
         BH    ERROR
         ST    R4,DSNLNGTH             SAVE DSNAME LENGTH      06/27/83
         BCTR  R4,R0
         EX    R4,MOVEFQ
         B     SEARCH
USERID   L     R7,CPPLUPT              LOAD ADDRESS OF UPT     06/27/83
         USING UPT,R7                                          06/27/83
         SR    R8,R8
         IC    R8,UPTPREFL             LENGTH OF PREFIX        06/27/83
         BCTR  R8,R0
         EX    R8,MOVEU                MOVE USERID
         LA    R7,DSNAME
         AR    R7,R8
         LA    R7,1(R7)
         MVI   0(R7),C'.'
         SR    R4,R3                   DSNAME LENGTH - 1
         AR    R8,R4
         LA    R8,3(R8)
         LA    R5,44
         CR    R8,R5
         BH    ERROR
         ST    R8,DSNLNGTH             SAVE DSNAME LENGTH      06/27/83
         EX    R4,MOVEDSN
         SPACE 4
SEARCH   LR    R7,R8                   CONVERT DSNAME TO
         LA    R2,DSNAME               UPPER CASE
CONV     OI    0(R2),X'40'
         LA    R2,1(R2)
         BCT   R7,CONV
         SPACE 2
*        L     R2,16                   ADDRESS OF CVT          06/27/83
*        USING CVT,R2                                          06/27/83
*        L     R2,CVTFQCB              ADDRESS OF FIRST MAJOR  06/27/83
*CBNEXT  LTR   R2,R2                                           06/27/83
*        BZ    NOFIND                                          06/27/83
*        USING QCB,R2                                          06/27/83
*        CLC   MAJNAME(8),SYSDSN                               06/27/83
*        BE    FINDDSN                                         06/27/83
*        L     R2,MAJNMAJ              ADDRESS OF NEXT MAJOR   06/27/83
*        B     QCBNEXT                                         06/27/83
*        SPACE 2                                               06/27/83
*INDDSN  L     R3,MAJFMIN              ADDRESS OF FIRST MINOR Q06/27/83
*        SR    R4,R4                                           06/27/83
*EXTMIN  LTR   R3,R3                                           06/27/83
*        BZ    NOFIND                                          06/27/83
*        DROP  R2                                              06/27/83
*        USING MINNMIN,R3                                      06/27/83
*        IC    R4,MINNAMEL             LENGTH OF QCB NAME      06/27/83
*        CR    R4,R8                                           06/27/83
*        BNE   INCR                                            06/27/83
*        BCTR  R4,R0                                           06/27/83
*        EX    R4,COMPDSN                                      06/27/83
*        BE    GETQEL                                          06/27/83
*NCR     L     R3,MINNMIN              ADDRESS OF NEXT MINOR QC06/27/83
*        B     NEXTMIN                                         06/27/83
*        SPACE 2                                               06/27/83
*ETQEL   LA    R4,58                                           06/27/83
*        LA    R2,MSG                                          06/27/83
*        L     R3,MINFQEL     A(1ST QEL)                       06/27/83
*        USING QEL,R3         ADDRESSABILITY                   06/27/83
*        LA    R5,MSG+6                                        06/27/83
*        B     TESTQEL                                         06/27/83
*EXTQEL1 LA    R5,MSG+6                                        06/27/83
*EXTQEL  L     R3,QELNQEL              LOAD ADDRESS OF QEL     06/27/83
*ESTQEL  LTR   R3,R3                                           06/27/83
*        BZ    OUT                                             06/27/83
*        MVI   PSW,X'00'               RESET PRINT SWITCH      06/27/83
*        TM    QELQFLGS,QELSHARE QEL SHARED?                   06/27/83
*        BO    SHR                                             06/27/83
*        LA    R1,MSG+6       ANYTHING IN THE MESSAGE          06/27/83
*        CLR   R1,R5           BUFFER YET?                     06/27/83
*        BE    OLDER          NO => STARTING WITH OLD          06/27/83
*        SR    R5,R2          PUT OUT                          06/27/83
*        BCTR  R5,R0          DROP THE COMMA                   06/27/83
*        TPUT  (R2),(R5)       WHAT'S THERE                    06/27/83
         MVC   GETMAINL(GETMLEN),GETMDC BUY AN AREA FOR GQSCAN 06/27/83
GETAREA  GETMAIN VC,LA=MEMLOW,A=AREAA,MF=(E,GETMAINL),SP=1,            X
               RELATED=(FREEAREA)                              06/27/83
         LTR   R15,R15                                         06/27/83
         BZ    GETRIB                                          06/27/83
         TPUT  NOMEM,35                                        06/27/83
         B     RETURN                                          06/27/83
GETRIB   L     R1,AREAA                 -> GOTTEN STORAGE          U003
         L     R0,AREAA+4               GET LENGTH OF GOTTEN STG   U003
         AR    R1,R0                    -> PAST END                U003
         S     R1,=A(32*1024)           -> PART TO FREE            U003
         S     R0,=A(32*1024)           ADJUST LENGTH TO KEEP      U003
         ST    R0,AREAA+4               STORE UPDATED LENGTH       U003
*  CAN'T BE NON-POSITIVE, SINCE THE MINIMUM WE'LL ACCEPT WAS       U003
*  CHANGED FROM 32K TO 64K                                         U003
         FREEMAIN  RU,A=(1),LV=32*1024,SP=1  GIVE BACK 32K TO SYS  U003
         LA    R5,DSNAME      TELL GQSCAN WHAT WE WANT         06/27/83
         LA    R4,SYSDSN        TO FIND                        06/27/83
         L     R6,DSNLNGTH    DSNAME LENGTH                    06/27/83
         MVC   GQLIST(ALLQL),ALLQ INIT PARM BLOCK              06/27/83
         XC    TOKEN(4),TOKEN ZERO TOKEN                       06/27/83
         LM    R2,R3,AREAA    GET AREA AND LENGTH              06/27/83
SCAN     GQSCAN AREA=((R2),(R3)),TOKEN=TOKEN,RESNAME=((R4),(R5),(R6)), X
               MF=(E,GQLIST)                                   06/27/83
         CH    R15,=H'4'      EMPTY                            06/27/83
         BE    NOFIND         IF SO RETURN AFTER MSG           06/27/83
         CH    R15,=H'12'     ERROR                            06/27/83
         BL    SCAN1                                           06/27/83
         CH    R15,=H'16'     IVALID SYSNAME                   06/27/83
         BNE   GQABEND        NO, SCAN ENDED ABNORMALLY        06/27/83
         TPUT  INVALID,34                                      06/27/83
         B     RETURN                                          06/27/83
GQABEND  TPUT  QSABEND,46                                      06/27/83
         B     RETURN                                          06/27/83
SCAN1    ST    R0,RIBL        SAVE LENGTH OF RIB AND RIBE      06/27/83
         ST    R1,NRIBS       SAVE # RIBS THIS AREA            06/27/83
         SR    R15,R15        CLEAR RETURN REGISTER            06/27/83
         L     R3,AREAA       POINT TO RIB                     06/27/83
         USING RIB,R3
         MVC   NRIBES(4),RIBTRIBE TOTAL NUMBER OF RIBES FOR    06/27/83
*                                   THIS RIB                   06/27/83
         MVC   NCRIBES(4),RIBNRIBE SAVE # RIBES THIS AREA      06/27/83
         LA    R4,58
         LA    R2,MSG
         AH    R3,RIBVLEN     ADD IN LENGTH OF VARIABLE PORTION06/27/83
         AH    R3,RIBL        ADD IN RIBLENGTH RETURN BY SCAN  06/27/83
         DROP  R3
         USING RIBE,R3        RIBE ADDRESSABILITY              06/27/83
         LA    R5,MSG+6
         B     RIBELOOP                                        06/27/83
NXTRIBE1 LA    R5,MSG+6                                        06/27/83
NXTRIBE  AH    R3,RIBEL       POINT TO NEXTRIBE                06/27/83
         L     R1,NRIBES      GET # UNPROCESSED RIBES          06/27/83
         BCT   R1,CHK4LAST    DECREMENT                        06/27/83
         B     OUT                                             06/27/83
*                                                              06/27/83
*        THERE  IS AT LEAST ONE UNPROCESSED RIBE IN THIS AREA  06/27/83
*                                                              06/27/83
CHK4LAST ST    R1,NRIBES      STORE NEW #                      06/27/83
         L     R1,NCRIBES     GET # UNPROCESSED RIBS THIS AREA 06/27/83
         BCT   R1,WHOHAS1                                      06/27/83
         CLI   PSW,X'00'      ANYTHING TO PRINT?               06/27/83
         BNE   WHOHAS2                                         06/27/83
         BCTR  R5,R0          REMOVE COMMA                     06/27/83
         SR    R5,R2                                           06/27/83
         TPUT  (R2),(R5)                                       06/27/83
WHOHAS2  TPUT  MOREM,9        TELL THEM THERE IS MORE          06/27/83
         B     RETURN                                          06/27/83
WHOHAS1  ST    R1,NCRIBES     STORE NEW #                      06/27/83
RIBELOOP MVI   PSW,X'00'      RESET PRINT SWITCH               06/27/83
         TM    RIBERFLG,RIBETYPE SHARED?                       06/27/83
         BO    SHR                                             06/27/83
         LA    R1,MSG+6       ANYTHING IN THE MESSAGE          06/27/83
         CLR   R1,R5            BUFFER YET                     06/27/83
         BE    OLDER          NO => STARTING WITH OLD          06/27/83
         SR    R5,R2          PUT OUT                          06/27/83
         BCTR  R5,R0          REMOVE COMMA                     06/27/83
         TPUT  (R2),(R5)      WHATS THERE                      06/27/83
OLDER    MVC   MSG(3),=C'OLD'
         LA    R5,MSG+6
         BAL   R6,FINDJN
         SR    R5,R2          SET LENGTHS
         TPUT  (R2),(R5)       AND POINTERS
         MVC   MSG(3),=C'SHR' RESET THE MESSAGE
         MVI   PSW,X'FF'      EMPTY BUFFER
         B     NXTRIBE1       CHECK FOR SHR ENQ
PRINT    SR    R5,R2
         TPUT  (R2),(R5)
         B     RETURN
         SPACE 2
SHR      LR    R6,R5
         SR    R6,R2
         CR    R6,R4
         BL    SHR1
         BCTR  R6,R0
         TPUT  (R2),(R6)
         MVI   MSG+6,C' '
         MVC   MSG+7(63),MSG+6
         LA    R5,MSG+6
         MVI   PSW,X'FF'
SHR1     BAL   R6,FINDJN
         MVI   0(R5),C','
         LA    R5,1(R5)
         B     NXTRIBE
OUT      CLI   PSW,X'00'
         BNE   RETURN
         BCTR  R5,R0
         B     PRINT
ERROR    TPUT  ERMSG1,15
         B     RETURN
         SPACE 4
NOFIND   TPUT  ERMSG2,22
         B     RETURN                                              U003
         EJECT
*        FINDJN    -    FINDS THE JOB NAME AND MOVES IT
*                       TO A SPECIFIED LOCATION
*
*        UPON ENTRY     REG. 3  -  ADDRESS OF RIBE             06/27/83
*                       REG. 5  -  LOC. TO REC. JOB NAME OR USERID
*                       REG. 6  -  RETURN REG.
*
*        UPON EXIT      REG. 5  -  CONTAINS ADDRESS FOLLOWING
*                                  THE JOB NAME OR USERID
*
*                       REG. 7  -  WORK REG.
*                       REG. 8  -  WORK REG.
*                       REG. 9  -  WORK REG.
*                       REG. 10 -  WORK REG.
         SPACE 4
FINDJN   L     R7,16          A(CVT)
         USING CVT,R7         ADDRESSABILITY
         L     R7,CVTASVT     A(ASVT)
         USING ASVT,R7        ADDRESSABILITY
         LH    R8,RIBEASID    JOB'S ASID                       06/27/83
         BCTR  R8,R0          INDEXING INTO ASVT
         SLL   R8,R2          BY USING (ASID - 1) * 4
         LA    R7,ASVTENTY(R8) A(A(JOB'S ASCB))
         L     R7,0(R7)       A(JOB'S ASCB)
         USING ASCB,R7        ADDRESSABILITY
         L     R8,ASCBJBNI    A(JOB NAME) IF INITIATED JOB
         MVI   0(R5),C'J'    ASSUME IT'S A JOB
         LTR   R8,R8          INITIATED JOB?
         BNZ   GOTIT          YES => GET THE JOB NAME
         MVI   0(R5),C'U'     ASSUME IT'S A USER
         L     R8,ASCBJBNS    A(START/MOUNT/LOGON JOB NAME)
GOTIT    MVI   1(R5),C'='     SEPARATOR
         MVC   2(8,R5),RIBEJBNM INSERT THE JOB NAME            06/27/83
         LA    R7,8           MAXIMUM JOB NAME LENGTH
         LA    R5,2(R5)       A(1ST CHARACTER OF JOB NAME)
LOOP     CLI   0(R5),C' '     END OF JOB NAME?
         BER   R6             YES => RETURN TO OUR CALLER
         LA    R5,1(R5)       A(NEXT CHARACTER IN JOB NAME)
         BCT   R7,LOOP        JOB NAME 8 CHARACTERS LONG?
         BR    R6             YES => RETURN TO OUR CALLER
         EJECT
MOVEFQ   MVC   DSNAME(0),0(R3)
MOVEU    MVC   DSNAME(0),UPTPREFX-UPT(R7)                      06/27/83
MOVEDSN  MVC   1(0,R7),0(R3)
SETMSG   DC    C'SHR -'
SYSDSN   DC    CL8'SYSDSN'
ERMSG1   DC    CL15'ERROR IN DSNAME'
ERMSG2   DC    CL22'DATA SET NOT ALLOCATED'
GETMDC   GETMAIN VC,SP=1,MF=L
GETMLEN  EQU   *-GETMDC
         DS    0F
*MEMLOW  DC    X'00008000'  MEM LIMITS FOR GQSCAN
MEMLOW   DC    X'00010000'  MEM LIMITS FOR GQSCAN                  U003
MEMHIGH  DC    X'7FFFFFFF'
NOMEM    DC    C'NOT ENOUGH CORE TO PROCESS REQUEST'
ALLQ     GQSCAN REQLIM=MAX,SCOPE=ALL,                                  X
               REQCNT=1,MF=L
ALLQL    EQU   *-ALLQ
INVALID  DC    C'INVALID SYSNAME; CALL TECH SUPPORT'
QSABEND  DC    C'QUEUE SCAN ENDED ABNORMALLY; CALL TECH SUPPORT'
MOREM    DC    C' (MORE)'
         EJECT
WRKSPACE DSECT                          resume
DSNAME   DS    44C
MSG      DS    CL70
PSW      DS    X
GETMAINL GETMAIN VC,MF=L
AREAA    DS    F       ADDRESS OF Q AREA
AREAL    DS    F       LENGTH OF Q AREA
SYSDSNAD DS    F       QNAME ADDRESS
DSNAMEAD DS    F       DATA SET NAME ADDRESS
TOKEN    DS    F       TOKEN FOR GQSCAN
RIBL     DS    H       LENGTH OF RIB
RIBEL    DS    H       LENGTH OF RIBE
NRIBS    DS    F       NUMBER OF RIBS RETURNED
NRIBES   DS    F       NUMBER OF RIBES THIS RIB
NCRIBES  DS    F       NUMBER OF RIBES THIS AREA
DSNLNGTH DS    F       LENGTH OF DATA SET NAME
GQLIST   GQSCAN MF=L
LWRKSPCE EQU   *-WRKSPACE
         CVT   DSECT=YES
         IKJUPT                                                06/27/83
         IHAASCB
         IHAASVT
         IKJCPPL
         ISGRIB                                                06/27/83
         END
