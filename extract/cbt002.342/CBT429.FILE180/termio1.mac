***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
TERMIO1  TITLE 'TERMIO1 -- 3270 ACCESS METHOD SELECTOR ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        T E R M I O 1                                                *
*                                                                     *
*        AN OPEN-ENDED CSECT WHOSE ONLY FUNCTION IS TO                *
*        DETECT WHICH SERVICE I/O ROUTINE TO USE -                    *
*        BY DETERMINING WHETHER OR NOT THIS JOBSTEP                   *
*        IS   T S O    OR   D C M S  "START"                          *
*                                                                     *
*        THE ASCB TSB FIELD IS CHECKED FOR NONZERO.                   *
*            A NONZERO TSB ADDRESS IMPLIES                            *
*            THAT THIS IS A TSO ADDRESS SPACE:                        *
*                                                                     *
*  Updates:                                                           *
*    24Apr88   LDW2 - Eliminate use of LC macro                       *
*              LDW2 - Change id to keep ASMH V2 happy                 *
*    11-12-81  SDM1 - HIDE VAM MACROS BEHIND CONDITIONAL ASSEMBLY     *
*    09-18-81  LDW1 - CHANGE LOWER CASE TO USE 'LC' MACRO             *
*                   - ADD VAM SUPPORT (PUKE)                          *
*                   - MISC MINOR CLEANUP                              *
*    08-07-80  LDW  - CHANGE SOME MESSAGES TO LOWER CASE FOR          *
*                     ESTHETICS                                       *
*    06-25-80  LDW  - CHANGE "INITIAL TSO DELAY" FROM 1 TO 2 SECONDS  *
*                   - CHANGE CSECT NAMES:   TERMIO#  -> TERMIO1       *
*                                           TERMIO$  -> TERMIO2       *
*                                           TERMIO@  -> TERMIO3       *
*                                           TERMIOWT -> TERMIO4       *
*                                           TERMIOHC -> TERMIO5       *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         LCLB  &VAM                     INDICATE VAM SYSTEM        SDM1
&VAM     SETB  0                        THIS IS NOT A VAM SYSTEM   SDM1
         SPACE 1
*  SEQUENCE SYMBOLS IN USE:
*   .NVAM**  01 02 03
         SPACE 3
TERMIO1  CSECT                                                     LDW1
         SPACE 2
         DCMSREGS
         SAVE  (14,12),,TERMIO1_&SYSDATE._&SYSTIME                 LDW2
         LR    R11,R15
         USING TERMIO1,R11
         LR    R10,R1                       SAVE IOCB ADDRESS
         USING IOCB,R10
         EJECT
*
*
*       REGISTERS:
*                   R2  = FSA
*                   R3  = IOCBTIME
*                   R5  = TDCB
*                   R6  = ASCB
*                   R7  = NEW/OLD
*                   R8  = CVT
*                   R10 = IOCB
*                   R11 = BASE
*                   R12 = TCB
*
*
         SPACE 2
         MVI   IOCBAID,AIDNULL          R E S E T  CURRENT AID
         SR    R5,R5                        CLEAR TDCB
         L     R8,16                        POINT AT THE CVT
         L     R7,0(,R8)                    NEW/OLD
         L     R12,4(,R7)                   OUR TCB
         L     R2,112(,R12)                     AND ITS FSA
         L     R6,12(,R7)                   OUR ASCB
*  SINCE THE DORK WHO DESIGNED DCMS DIDN'T HAVE ANY OF THE PROCESSORS
*  INITIALIZE THE IOCB TO ANYTHING USEFUL (LIKE ZERO), AND UNDER
*  REGULAR TSO, ETC, IT WILL LIKELY BE ZERO, BUT UNDER VAM IT OFTEN
*  WON'T BE ZERO BECAUSE VAM SHARES STORAGE ALL OVER THE PLACE, WE NOW
*  MAKE A FEEBLE ATTEMPT (IT'S THE BEST I CAN DO) TO DETERMINE IF THE
*  IOCB HAS REALLY BE INITIALIZED.  IF IT HASN'T, THE TYPE DETECTION
*  MAY BRANCH WHEN IT SHOULDN'T.                                   LDW1
         CLI   IOCBTIME,C'0'            DOES IT LOOK ANY GOOD?     LDW1
         BNE   NEWIOCB                  NO - DO TYPE CHECKING      LDW1
         CLI   IOCBTIME+3,X'7A'         COLON THERE?               LDW1
         BNE   NEWIOCB                  NO - DO TYPE CHECKING      LDW1
         CLI   IOCBTIME+6,X'7A'         HOW ABOUT HERE?            LDW1
         BNE   NEWIOCB                  NO - DO TYPE CHECKING      LDW1
         CLI   IOCBTIME+9,X'4B'         DOT HERE?                  LDW1
         BNE   NEWIOCB                  NO - DO TYPE CHECKING      LDW1
*  AT THIS POINT, I ASSUME IT'S AN INITIALIZED IOCB.  GOD HELP US IF
*  IT'S NOT, ESPECIALLY IF THIS IS VAM.                            LDW1
         TM    IOCBFLGT,IOCBTTSO+IOCBTCMD+IOCBTBSC
         BNZ   OLDIOCBX                 SKIP DETECTION IF KNOWN TYPE
         EJECT
*
*
*        T E R M I N A L    T Y P E    S E L E C T I O N    C O D E
*
*
*        NEW IOCB - WE MUST SCAN FOR TYPE
*
*   AT THIS POINT WE HAVE LOCATED THE FIRST SAVE AREA PROVIDED TO THIS
*   TASK WHEN IT WAS ATTACHED. NOBODY CARES ABOUT THE REGISTERS THAT
*   IT CONTAINS, SO TERMIO USES IT AS THE ANCHOR FOR ITS TABLE.
*   USAGE IS AS FOLLOWS:
*
*        24    ORIGINAL PARM SAVED BY INITIAL ROUTINE (DCB PTR)
*        28    '#???' IF TERMIO HAS EVER BEEN INVOKED
*        32    POINTER TO PREVIOUS FORMAT TABLE USED.
*        36    POINTER TO CURRENT TERMIO WORK AREA
*
NEWIOCB  EQU   *
         MVI   IOCBFLGT,0               INITIALIZE FLAGS           LDW1
         L     R1,60(,R6)               THE TSB,
         LA    R1,0(,R1)                IF IT
         LTR   R1,R1                    EXISTS -
         BZ    NOTSO                    IMPLIES
         OI    IOCBFLGT,IOCBTTSO        THIS IS
         B     CONTINU                  T S O
         SPACE 2
NOTSO    EQU   *
         AIF   (NOT &VAM).NVAM01                                   SDM1
*  USE THE IOCBTIME WORK AREA FOR A BLDL LIST TO DETERMINE IF THE VAM
*  INTERFACE MODULE IS PRESENT.  IF IT ISN'T, WE ASSUME THAT THIS IS
*  NOT A VAM USER.                                                 LDW1
         MVC   IOCBTIME(4),=H'1,12'     #, LENGTH OF BLDL ENTRIES  LDW1
         MVC   IOCBTIME+4(8),=C'VAMINF00'  FILL IN MODULE NAME     LDW1
         SPACE 1
         BLDL  0,IOCBTIME               SEE IF WE CAN FIND IT      LDW1
         SPACE 1
         LTR   R15,R15                  IS VAM INTERFACE HERE?     LDW1
         BNZ   NOTVAM                   NO - SEE IF BSC            LDW1
         SPACE 1
         VAMUSER  NO=NOTVAM,LINK=YES    SEE IF THIS IS A VAM USER  LDW1
         SPACE 1                                                   LDW1
         OI    IOCBFLGT,IOCBTTSO+IOCBTVAM  FLAG THIS AS VAM + TSO  LDW1
         B     CONTINU                  YES                        LDW1
         SPACE 2
NOTVAM   EQU   *                                                   LDW1
.NVAM01  L     R5,24(,R2)               RECOVER
         L     R5,0(,R5)                    TDCB
         LA    R5,0(,R5)                        FROM
         ST    R5,IOCBTDCB                          FSA+24
*
         CLC   TDCBTYPE-TDCB(R5),=CL4'#CMD'
         BNE   NOTCMD
         OI    IOCBFLGT,IOCBTCMD
         B     CONTINU
         SPACE 2
NOTCMD   EQU   *
         CLC   28(4,R2),=C'#BSC'        IS THIS OUR FIRST TIME THRU
*        BNE   NOTBSC
         OI    IOCBFLGT,IOCBTBSC
         B     CONTINU
         SPACE 2
NOTBSC   EQU   *
         EX    0,*                      ILLEGAL TYPE !
         SPACE 2
CONTINU  EQU   *
         ST    R5,IOCBTDCB              THE TDCB
*
*         END OF TYPE SELECTION CODE
*
         EJECT
OLDIOCBX EQU   *
         L     R5,IOCBTDCB              ** INIT IOCB WAIT VALUE **
         L     R3,IOCBWAIT              CHECK CURRENT VALUE
         LTR   R3,R3                    IS IT ZERO?
         BNZ   SETTIME                  ...NO, DONT RESET
*
         LTR   R5,R5                    COPY
         BZ    TSOTIME                       TIME
         L     R4,TDCBDUPT-TDCB(,R5)             FROM
         L     R3,DUPTINTV-DUPT(,R4)                 DUPT
         B     SETTIME                                   UNLESS TSO
         SPACE 2
TSOTIME  EQU   *
         LA    R3,200                   DFLT INITIAL TSO DELAY = 2 SEC
         SPACE 1
SETTIME  EQU   *
         ST    R3,IOCBWAIT              INIT DELAY TIME IN .01 UNITS
         EJECT
*
*
*        S C R E E N    A U T O    T I M E S T A M P    C O D E
*
*
         MVC   IOCBTIME(L'IOCBTIME),SPACEZ
         TIME  DEC
         ST    R0,IOCBFWDW              TIME
         MVI   IOCBPSGN,X'0C'               STAMP
         MVC   IOCBTIME(12),=X'F021207A20207A20204B2020' THE
         ED    IOCBTIME(12),IOCBFWDW                IOCB
         ST    R1,IOCBFWDW                              FOR
         MVC   IOCBTIME+13(9),=X'40202020214B202020'        SCREEN
         ED    IOCBTIME+13(9),IOCBFWDW                          SERVICE
         MVC   IOCBTIME+12(4),SPACER             USAGE
         MVC   IOCBCTRL(5),=X'115C6F1DE8'             LATER
         TM    IOCBFLGC,IOCBCINT
         BO    NOINTV
         L     R14,IOCBWAIT
         CVD   R14,IOCBISWK
         MVC   IOCBSSTH-1(6),=XL6'2120204B2120'
         ED    IOCBSSTH-1(6),IOCBISWK+5
         MVC   IOCBITVL(12),INTERVAL
         AIF   (NOT &VAM).NVAM02                                   SDM1
         TM    IOCBFLGT,IOCBTVAM        IS THIS VAM?               LDW1
         BNO   *+10                     NO - SKIP                  LDW1
         MVC   IOCBITVL(10),=C'<< VAM >> '  YES - NO INTERVAL      LDW1
.NVAM02  ANOP                                                      SDM1
NOINTV   EQU   *
         MVC   IOCBSHOW(L'IOCBSHOW),SPACEZ    CLEAR INTERVAL AREA
         MVC   IOCBEND(L'IOCBEND),SPACEZ      CLEAR INTERVAL AREA
*  THE FOLLOWING TEST IS STILL WRONG, BUT IT ISN'T AS WRONG AS IT WAS.
         CLI   IOCBNAME,C'A'                                       LDW1
         BL    SPCNAM
         CLI   IOCBNAME,C'Z'                                       LDW1
         BH    SPCNAM
         B     ROUTINE
         EJECT
SPCNAM   EQU   *
         MVC   IOCBNAME(8),SPACEZ
         SPACE 1
ROUTINE  EQU   *
         MVC   IOCBSHOW,NAM#A
         TM    IOCBFLGS,IOCBFNRD+IOCBFNWR
         BZ    NN#W                     READ + WRITE = WAITING
         BO    NN#H                     NOREAD + NOWRITE = HALTED
         TM    IOCBFLGS,IOCBFNRD
         BNO   NN#A                     WRITE + NOREAD = RUNNING
         SPACE 1
         AIF   (NOT &VAM).NVAM03                                   SDM1
         TM    IOCBFLGT,IOCBTVAM        IS THIS VAM?               LDW1
         BNO   NN#R                     NO - "RUNNING" IS OK       LDW1
         MVI   IOCBAID,AIDPA1           SIMULATE STOP REQUEST      LDW1
         LM    R14,R12,12(R13)          RESTORE CALLER'S REGISTERS LDW1
         BR    R14                      AND JUST RETURN TO CALLER  LDW1
.NVAM03  ANOP                                                      SDM1
         SPACE 3
*---  DISPLAY AN ACTION MODIFIER ON STATUS LINE
         SPACE 1
NN#R     MVC   IOCBSHOW,NAM#R           'RUNNING'
         B     NN#END
         SPACE 1
NN#W     MVC   IOCBSHOW,NAM#W           'WAITING'
         B     NN#END
         SPACE 1
NN#H     MVC   IOCBSHOW,NAM#H           'HALTED'
         B     NN#END
         SPACE 1
NN#A     MVC   IOCBSHOW,NAM#A           'ACTIVE'
         B     NN#END
         SPACE 2
NN#END   DS    0H
         EJECT
*
*
*        T E R M I N A L   R O U T I N E  S E L E C T I O N
*
*
         LR    R1,R10
         TM    IOCBFLGT,IOCBTCMD
         BO    CMDGOT
         TM    IOCBFLGT,IOCBTTSO
         BO    TSOGOT
         TM    IOCBFLGT,IOCBTBSC
         BO    BTAMGOT
         EX    0,*       INVALID TERMINAL TYPE
TSOGOT   EQU   *
         L     R15,=V(TERMIO2)
         BR    R15                      GO TO TSO IO SERVICE
BTAMGOT  EQU   *
         ST    R10,TDCBIOCB-TDCB(,R5)
         L     R15,=V(TERMIO3)
         BR    R15                      GO TO BTAM IO SERVICE
CMDGOT   EQU   *
         L     R15,=V(TERMIO4)
         BR    R15                      GO TO TSO IO SERVICE
         SPACE 3
INTERVAL DC    C'Interval ='                                       LDW2
         DC    X'1D40'                                             LDW1
         SPACE 1
SPACER   DC    CL5' on  '                                          LDW2
         SPACE 2
NAM#A    DC    CL11'  Active   '                                   LDW2
NAM#R    DC    CL11'  Running  '                                   LDW2
*nam#b   dc    cl11'  Working  '                                   LDW2
NAM#H    DC    CL11'  Halted   '                                   LDW2
NAM#W    DC    CL11'  Waiting  '                                   LDW2
         SPACE 2
SPACEZ   DC    CL80' '
PPATCH   DC    20CL8'-TERMIO1'
         SPACE 2
         LTORG
         SPACE 5
IOCB     DSECT
         IOCB
         EJECT
TDCB     TDCB
         EJECT
         DCMSUPT
         SPACE 2
         END
