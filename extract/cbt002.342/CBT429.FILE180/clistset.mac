***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1985 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
CLISTSET TITLE 'C L I S T S E T  --  TSO/E COMPATIBILITY VERSION'
***********************************************************************
*                                                                     *
*                           C L I S T S E T                           *
*                                                                     *
*                     BUILDS PARM LIST FOR IKJUPDT                    *
*                                                                     *
*    THIS SUBROUTINE IS A REPLACEMENT FOR A ROUTINE OF THE SAME NAME  *
*    WHICH WOULD SET A SYMBOLIC VARIABLE TO A USER-SPECIFIED VALUE    *
*    WITHIN A CLIST.  WHAT THIS ROUTINE DOES IS TAKE THE PARM LIST    *
*    BUILT ORIGINALLY FOR THE ORIGINAL CLISTSET ROUTINE AND           *
*    BUILD A PARM LIST COMPATIBLE WITH IKJCT441.                      *
*                                                                     *
*    ON ENTRY: R1 CONTAINS THE ADDRESS OF A THREE FULLWORD            *
*              PARMAMETER LIST.                                       *
*                                                                     *
*    R1  --->  EXTADDR  DS   A         ADDR OF ECT (NO LONGER USED)   *
*              VALADDR  DC   A(VALUE)  ADDR OF NEW VALUE              *
*              NAMADDR  DC   A(NAME)   ADDR OF SYMBOLIC NAME          *
*                                                                     *
*              VALUE    DC   H'8',CL8'ABCDEFGH'                       *
*              NAME     DC   H'7',CL7'SYSDVAL'                        *
*                                                                     *
*              NOTE THAT THE VARIABLE NAME IS *NOT* PRECEEDED BY      *
*              AN AMPERSAND ('&').                                    *
*                                                                     *
*                                                                     *
*              ON ENTRY TO IKJCT441 R1 CONTAINS THE ADDRESS OF A 6    *
*              WORD PARAMETER LIST.                                   *
*                                                                     *
*       R1  --->  ECADDR    DS   A(F'ENTRY_CODE')                     *
*                 VARADDR   DS   A(A(VARNAME))                        *
*                 VARLEN    DS   A(F'VARLEN')                         *
*                 VALADDR   DS   A(A(VALNAME))                        *
*                 VALLEN    DS   A(F'VALLEN')                         *
*                 TOKNADDR  DS   A(F'TOKEN')                          *
*                                                                     *
*      RETURN CODES:                                                  *
*                 R15 = 0    NORMAL RETURN; VALUE WAS SET             *
*                 R15 = 4    NO CLIST RUNNING                         *
*                 R15 = 8    VARIABLE NAME NOT FOUND                  *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
CLISTSET OSENTER GETMAIN=(WKAREAL,WORKAREA),EXIT=RETURN,RC=(R15),      X
               PARMREG=R11
         EJECT
*  SET PTRS TO VAR NAME, LEN, VALUE NAME, LEN
         L     R15,8(,R11)              -> VARIABLE NAME ELEMENT
         LA    R14,2(,R15)              SKIP OVER LENGTH FIELD
         ST    R14,NAMEPTR
         SPACE 1
         LH    R0,0(,R15)               GET LENGTH OF VARIABLE NAME
         ST    R0,NAMELEN               SET FOR IKJCT441
         SPACE 1
         L     R15,4(,R11)              -> VALUE ELEMENT
         LA    R14,2(,R15)              SKIP OVER LENGTH FIELD
         ST    R14,DATAPTR
         SPACE 1
         LH    R0,0(,R15)               GET LENGTH OF VALUE
         ST    R0,DATALEN               SET FOR IKJCT441
         SPACE 1
         XC    TOKEN,TOKEN              CLEAR IT JUST TO BE SURE
         SPACE 1
         LA    R14,=A(TSVEUPDT)         -> ENTRY CODE VALUE FOR 'SET'
         LA    R15,NAMEPTR              -> -> VAR NAME
         LA    R0,NAMELEN               -> VAR LENGTH
         LA    R1,DATAPTR               -> -> VALUE
         LA    R2,DATALEN               -> VALUE LENGTH
         LA    R3,TOKEN                 -> TOKEN (NOT USED)
         STM   R14,R3,PARMLIST          SET PARMS FOR IKJCT441
         OI    PARMLIST+5*4,X'80'       SET VL=1
         L     R15,CVTPTR               -> CVT
         L     R15,CVTTVT(,R15)         -> TSVT
         L     R15,TSVTVACC-TSVT(,R15)  -> IKJCT441
         LA    R1,PARMLIST              A(PARMLIST)
         BALR  R14,R15                  DO IT
         LTR   R15,R15                  CHECK RETURN CODE
         BZ    EXIT
         CH    R15,=H'40'               INVALID CLIST ENVIRONMENT?
         BE    NOTCLIST                 YES - SET RC=4
         LA    R15,8                    NAME NOT DEFINED
         B     EXIT
NOTCLIST LA    R15,4                    NOT A CLIST
EXIT     B     RETURN
         EJECT
WORKAREA DSECT
PARMLIST DS    6A
NAMEPTR  DS    A
DATAPTR  DS    A
NAMELEN  DS    F
DATALEN  DS    F
TOKEN    DS    F
WKAREAL  EQU   *-WORKAREA
         SPACE 5
CVTPTR   EQU   16
CVTTVT   EQU   X'9C'
         SPACE 2
         IKJTSVT
         END
