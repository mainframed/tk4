LOCK     TITLE '-----  SECURE TERMINAL WITHOUT LOGGING OFF  -----'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  LOCK                                               *
*                                                                     *
*   PURPOSE:       SECURE A TSO TERMINAL FROM UNAUTHORIZED USE,       *
*                  WITHOUT HAVING TO LOG OFF.  THIS COMMAND WILL NOT  *
*                  EXIT UNTIL THE CORRECT ACF2 PASSWORD FOR THE       *
*                  ACTIVE USER IS ENTERED.  ALSO, THIS COMMAND WILL   *
*                  WAKE UP EVERY 10 MINUTES FOR UP TO 90 MINUTES,     *
*                  TO KEEP THE SESSION FROM ABENDING S522.            *
*                                                                     *
*   Author:        Leonard D. Woren                                   *
*                                                                     *
*   Date:          January 17, 1984                                   *
*                                                                     *
*   Attributes:    APF authorized                                     *
*                                                                     *
*   Installation:  University of Southern California                  *
*                                                                     *
*   Written at:    Hughes Aircraft Company                            *
*                                                                     *
*   Updates:                                                          *
*      27May88  LDW  Update acf2 check for USC                        *
*      25Apr88  LDW  Insert @SWITCH calls for PIE/Sessions            *
*      30Aug87  LDW  Put conditional assembly around ACF2 code        *
*      06May87  LDW  Minor change to message text (say "logon" pswd)  *
*                    Eliminate use of LC macro                        *
*      01/16/86 LDW  ALLOW USE BY USERS, BUT DON'T DO 522 BYPASS      *
*                    REMOVE CALLS TO APFAUTH SVC                      *
*      07/11/85 LDW  DISPLAY TERMID ON SCREEN                         *
*      07/03/85 LDW  DISPLAY SMFID ON SCREEN                          *
*                    UPDATE FOR NEW F3270 MACRO FORMAT                *
*                    SAY 'ENTER UADS PASSWORD' IF ACF2 NOT UP         *
*      06/18/85 LDW  REMOVE CHECK FOR OPERATOR & ACCOUNT              *
*      05/01/85 LDW  USE STTMPMD TO MAKE SESSION MANAGER HAPPY        *
*      01/17/85 LDW  FIX FOR 3278-5                                   *
*      11/15/84 LDW  CHANGE ROUTCDE ON TIMER WTO'S FROM 11 TO 15, SO  *
*                       THAT THEY WILL APPEAR ONLY IN SYSLOG          *
*      08/10/84 LDW  ADD LONG=YES TO SUBTASK'S "WAIT"                 *
*      07/26/84 LDW  DON'T ALLOW ATTN                                 *
*      07/05/84 LDW  FINISH AND INSTALL                               *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         COPY  @GLOBALS
         EJECT
LOCK     OSENTER  EXIT=LEAVE,RC=0,PARMREG=R2,ENV=(CP,SIM),             $
               GETMAIN=(WORKLEN,WORKD)
         MVI   FLAGS,0                  INIT ALL FLAGS
*--------------------------------------*
*  SEE IF THIS USER IS A PRIVILEGED    *
*  USER.  IF SO, WE WILL WAKE UP       *
*  EVERY 10 MINUTES FOR AN HOUR TO     *
*  DELAY AN ABEND S522.                *
*--------------------------------------*
          AIF   (&@@ACF2 EQ 0).NOACF2A
         ACFINCVT  R4,NONE=NO$UID       GET ACF2 CVT PTR
         SPACE 1
         USNGX ACCVT,R4
         OI    FLAGS,FACF2              ACF2 IS AVAILABLE
         SPACE 1
         ACFGUCB  R5,NONE=ACF2DOWN,INLINE=YES,SYS=AOS2
         SPACE 1
         USNGX ACUCB,R5
         SPACE 1
         ICM   R1,B'1111',ACULRECP      -> LIDREC
         BZ    NO$UID
         CLC   =C'UCS',LIDDEPT-LIDREC(R1)  GOOD GUY?
         BNE   NO$UID                   NO
         OI    FLAGS,FSTAFF             YES - REMEMBER
          AGO   .ACF2A
.NOACF2A  ANOP
         L     R1,PSATOLD-PSA           -> TCB
         USNGX TCB,R1
         TM    TCBFBYT3,TCBEXP          TCB EXPANSION THERE ?
         BNO   GETUID2                  TRY ASXBSENV IF NO TCBSENV
         ICM   R4,B'1111',TCBSENV       TASK ACEE PRESENT?
         BNZ   GETUID3                  ACEE FOUND IF PRESENT
GETUID2  L     R3,PSAAOLD-PSA           -> ASCB (STILL VALID?)
         L     R4,ASCBASXB-ASCB(,R3)    -> ASXB
         ICM   R4,B'1111',ASXBSENV-ASXB(R4)
         BZ    NO$UID
GETUID3  CLC   =C'SYS',ACEEGRPN-ACEE(R4)  GOOD GUY?
         BE    SYS$UID                  YES
         CLC   =C'STAFF',ACEEGRPN-ACEE(R4)  GOOD GUY?
         BNE   NO$UID                   NO
SYS$UID  OI    FLAGS,FSTAFF             YES - REMEMBER
.ACF2A    ANOP
         SPACE 3
*--------------------------------------*
*  FIND REAL PSCB, SEE IF USER IS      *
*  ALLOWED TO USE THIS PROCESSOR.      *
*--------------------------------------*
NO$UID   L     R10,CPPLPSCB             -> PSCB
         USNGX PSCB,R10
***      TM    PSCBATR1,PSCBCTRL+PSCBACCT  OPER OR ACCT AUTH?
***      BNZ   USER$OK                  YES - CONTINUE
***      WTO   'LOCK - USE NOT AUTHORIZED',ROUTCDE=11
***      B     LEAVE
         SPACE 3
*--------------------------------------*
*  SEE IF WE CAN RUN ON THIS TERMINAL. *
*--------------------------------------*
USER$OK  GTSIZE  ,                      DETERMINE SCREEN ATTRIBUTES
         LTR   R0,R0                    DISPLAY TERMINAL?
         BP    TERM$OK                  YES - CONTINUE
         TPUTX '"LOCK" CAN ONLY BE USED ON 3270 TYPE DEVICES, SORRY'
         B     LEAVE
         SPACE 3
*--------------------------------------*
*  ESTABLISH SUBTASK TO PREVENT S522.  *
*--------------------------------------*
TERM$OK  TM    FLAGS,FSTAFF             SYSTEMS STAFF?
         BZ    NO$ATTCH                 NO - SKIP ATTACH
         SPACE 1
         IDENTIFY  ENTRY=LOCKTIME,EP=LOCKTIME  SET UP ANOTHER ENTRY PT
         SPACE 1
         ATTACH  EP=LOCKTIME
         SPACE 1
         ST    R1,TCBADDR               SAVE TCB ADDRESS
         SPACE 3
*--------------------------------------*
*  MISCELLANEOUS OTHER INITIALIZATION  *
*--------------------------------------*
NO$ATTCH MVC   USERID,PSCBUSER          MOVE USERID TO SCREEN
         MVC   USERIDL,PSCBUSRL         save userid length
         MVC   USERIDA,PSCBUSER         save userid
         L     R1,CVTPTR                -> CVT
         L     R1,CVTSMCA-CVT(,R1)      -> SMF CONTROL BLOCK
         MVC   SMFID,SMCASID-SMCABASE(R1)  DISPLAY SMF SYSTEM ID
         L     R1,PSAANEW-PSA           -> ASCB
         L     R2,ASCBTSB-ASCB(,R1)     -> TSB (FETCH PROTECTED)
         LTR   R2,R2                    ANY?
         BZ    SKIPTERM                 NO - NO TERMINAL NAME TO DISP
         SPACE 1
***      LA    R0,1
***      SVC   243                      GET APF AUTHORIZED
         SPACE 1
         MODESET  KEY=ZERO              SET PROTECT KEY = 0
         SPACE 1
         UNPK  LINE-1(5),TSBLINE-TSB(3,R2)  GET THE LINE ADDRESS
         MVC   TERMID,TSBTRMID-TSB(R2)  AND THE TERMINAL NAME
         SPACE 1
         MODESET  KEY=NZERO             RESTORE PROBLEM KEY
         SPACE 1
***      LA    R0,0
***      SVC   243                      GET APF UNAUTHORIZED
         SPACE 1
         TR    LINE,HEXTAB              MAKE IT DISPLAYABLE HEX
         CLC   =C'000',LINE             ANY LINE ADDRESS
         BNE   SKIPTERM                 YES - LEAVE IT
         MVC   LINE,BLANKS              NO - BLANK IT OUT
         MVC   ON,BLANKS                AND BLANK OUT THE ' ON LINE '
         SPACE 1
***      STAX  STAXEXIT                 TRAP ATTENTIONS
SKIPTERM STAX  DEFER=YES                IGNORE ATTENTIONS
         STFSMODE  ON,INITIAL=YES
         STTMPMD   ON,KEYS=ALL
         @SWITCH  CONTROL=STOP          disallow PIE/Sessions switching
         TIME  DEC                      GET CURRENT TIME (HHMMSSTH)
         ST    R0,DWD                   STORE FOR EDIT
         MVC   TIMELOCK,TIMEMASK        MOVE EDIT MASK TO SCREEN
         ED    TIMELOCK,DWD
         SR    R3,R3                    CLEAR LOOP COUNTER
***      TM    FLAGS,FACF2              IS ACF2 ACTIVE?
***      BO    *+10                     YES - KEEP MESSAGE TEXT ASIS
***      MVC   PSWDTYPE,=C'UADS'        NO - ALTER MESSAGE TEXT
         SPACE 3
*--------------------------------------*
*  MAIN LOOP                           *
*--------------------------------------*
NO$INPUT MVC   ACF2MSG,BLANKS           CLEAR MSG AREA
         SPACE 1
LOOP     CVD   R3,DWD
         MVC   NUM,=X'40202020'
         ED    NUM,DWD+6
         TIME  DEC                      GET CURRENT TIME (HHMMSSTH)
         ST    R0,DWD                   STORE FOR EDIT
         MVC   TIME,TIMEMASK            MOVE EDIT MASK TO SCREEN
         ED    TIME,DWD
         TPUT  SCREEN,SCREENL,FULLSCR
         CH    R15,=H'20'               RC TOO BIG?
         BH    TPUT$ERR                 YES - ABEND
         B     *+4(R15)                 DECODE RC
         B     TPUT$OK                  00 - CONTINUE
         B     TPUT$ERR                 04 - NO BUFFERS FOR NOWAIT
         B     ATTN$HIT                 08 - ATTN HIT
         B     TPUT$ERR                 0C - INPUT BUFFER TOO SMALL
         B     TPUT$ERR                 10 - INVALID PARAMETERS
         B     DETACH                   14 - TERMINAL IS DISCONNECTED
         SPACE 1
TPUT$ERR ABEND 93,,STEP                 GET OUT FOR SURE
         SPACE 3
TPUT$OK  MVC   ATTNMSG,BLANKS           CLEAR MSG
         OC    WAITTIME,WAITTIME        ANY WAITTIME?
         BZ    NO$WAIT                  NO - GO DO TGET NOW
         CLC   INBUF,BLANKS             WAS THERE ANY INPUT?
         BE    NO$WAIT                  NO - DON'T DO THE WAIT
         STIMER  WAIT,BINTVL=WAITTIME   YES - MAKE HIM WAIT A WHILE
         SPACE 1
NO$WAIT  TGET  INBUF,80                 READ SOMETHING
         XC    POPCOUNT,POPCOUNT        RESET COUNTER FOR ANOTHER HOUR
         CH    R15,=H'20'               RC TOO BIG?
         BH    TGET$ERR                 YES - ABEND
         B     *+4(R15)                 DECODE RC
         B     TGET$OK                  00 - CONTINUE
         B     TGET$ERR                 04 - NO BUFFERS FOR NOWAIT
         B     ATTN$HIT                 08 - ATTN HIT
         B     TGET$OK                  0C - INPUT BUFFER TOO SMALL
         B     TGET$ERR                 10 - INVALID PARAMETERS
         B     DETACH                   14 - TERMINAL IS DISCONNECTED
         SPACE 1
TGET$ERR ABEND 93,,STEP                 GET OUT FOR SURE
         SPACE 3
*--------------------------------------*
*  SEE IF CORRECT PASSWORD SUPPLIED    *
*--------------------------------------*
TGET$OK  TCLEARQ  INPUT
         OC    INBUF,BLANKS             UPPER CASE THE INPUT
         CLC   INBUF,BLANKS             ANY INPUT?
         BE    NO$INPUT                 NO - CLEAR MSG & RESHOW SCREEN
         CLI   INBUF,C'>'               RESHOW???
         BNE   NOT$RS                   NO - CONTINUE
         CLC   INBUF+1(L'INBUF-1),BLANKS  ANY MORE INPUT?
         BE    LOOP                     NO - JUST RESHOW SCREEN
NOT$RS   LA    R3,1(,R3)                INCR ATTEMPTS COUNTER
         MVC   ATTEMPT,ATTEMPTS         MOVE IN TEXT STRING 'ATTEMPTS'
         CH    R3,=H'1'                 IS THIS THE FIRST ATTEMPT?
         BNE   *+8                      NO - SKIP
         MVI   ATTEMPT+L'ATTEMPT-1,C' ' YES - MAKE IT SAY 'ATTEMPT '
         CLC   =C'XYZZY',INBUF      %%% PASSWORD CORRECT?
         NOP   RELEASE              %%% YES - CLEAN UP EVERYTHING
         SPACE 2
          AIF   (&@@ACF2 EQ 0).NOACF2B
         TM    FLAGS,FACF2              IS ACF2 AVAILABLE?
         BZ    ACF2DOWN                 NO - CHECK UADS PASSWORD
         SPACE 1
         XC    ACVALD(ACVLEN),ACVALD    CLEAR WHOLE PARM LIST
         MVI   ACVFCN,1                 SET FUNCTION CODE
         MVI   ACVSFCN,ACVSRVLD         REVALIDATE PASSWORD
**??     MVI   ACVCNTL,ACVCIPSD         BUMP BAD PW COUNT IF BAD
**??     MVI   ACVCNTL,ACVCNACT         DO NOT INCR SYSTEM ACCESS COUNT
         ST    R5,ACVUCB                ADDR OF ACUCB
         LA    R0,MSGBUFF               -> 128 BYTE MESSAGE BUFFER
         ST    R0,ACVMSG                ...
         MVC   ACVLID,USERID            LID TO BE REVALIDATED
         MVC   ACVPSWD,INBUF            PASSWORD TO BE VALIDATED
         XC    INBUF,INBUF              CLEAR SENDING FIELD
         MVC   ACVSRCE,BLANKS           SHOW WHERE FROM
         MVC   ACVJOBV,USERID           USE LID FOR JOBNAME
         MVC   ACVSLID,=CL8'LOGON  '    INDICATE IT'S ME
         MVC   ACVPATH,BLANKS           NO REMOTE PATH
         MVC   ACVAPROG,=CL8'LOCK'      NO PROGRAM NAME
         SPACE 1
***      LA    R0,1
***      SVC   243                      GET APF AUTHORIZED
         SPACE 1
         ACFSVC  ACVALD,CVT=HAVE        VALIDATE PASSWORD
         SPACE 1
***      LR    R2,R15                   SAVE RETURN CODE
***      LA    R0,0
***      SVC   243                      GET APF UNAUTHORIZED
         SPACE 1
***      LTR   R15,R2                   CORRECT PASSWORD?
         LTR   R15,R15                  CORRECT PASSWORD?
         BZ    RELEASE                  YES - BAIL OUT
         TM    ACVRFLG,ACVRMSG          MESSAGE PRESENT?
         BNO   LOOP                     NO - JUST LOOP
*        LH    R0,MSGBUFF               GET MSG LENGTH
*        LA    R1,MSGBUFF+4             -> MSG TEXT
*        TPUT  (1),(0),R                PUT IT OUT
         MVC   ACF2MSG,MSGBUFF+4        COPY MSG TEXT
          AGO   .ACF2B
.NOACF2B  ANOP
         MVC   PASSWORD,INBUF           set password
         SPACE 1
         RACINIT  ENVIR=CREATE,USERID=USERIDL,PASSWRD=PSWDL,           $
               ACEE=ADDRACEE,SUBPOOL=2
         SPACE 1
         CH    R15,=Y(X'34')            RC too big?
         BH    RACF$BAD                 yes - just loop
         B     *+4(R15)                 no - decode it
         B     RACF$00                  00 - ok
         B     RACF$04                  04 - user not defined to RACF
         B     RACF$08                  08 - password not authorized
         B     RACF$0C                  0C - the password is expired
         B     RACF$10                  10 - the new pswd is invalid
         B     RACF$14                  14 - user not defined to group
         B     RACF$18                  18 - RACINIT failed by inst ext
         B     RACF$1C                  1C - user access revoked
         B     ACF2DOWN                 20 - RACF is not active
         B     RACF$24                  24 - user acc to group revoked
         B     RACF$28                  28 - oid reqd not supplied
         B     RACF$2C                  2C - oid card invalid for user
         B     RACF$30                  30 - user not auth to terminal
         B     RACF$34                  34 - user not auth to appl
         SPACE 2
RACF$00  RACINIT  ENVIR=DELETE,                                        $
               ACEE=ADDRACEE,SUBPOOL=2
         SPACE 1
         B     RELEASE                  All done - leave
         SPACE 2
RACF$BAD MVC   ACF2MSG(27),=C'RACINIT return code > X''34'''
         B     LOOP
         SPACE 2
RACF$04  MVC   ACF2MSG(24),=C'User not defined to RACF'
         B     LOOP
         SPACE 2
RACF$08  MVC   ACF2MSG(18),=C'Incorrect password'
         B     LOOP
         SPACE 2
RACF$0C  MVC   ACF2MSG(19),=C'Password is expired'
         B     LOOP
         SPACE 2
RACF$10  MVC   ACF2MSG(23),=C'New password is invalid'
         B     LOOP
         SPACE 2
RACF$14  MVC   ACF2MSG(25),=C'user not defined to group'
         B     LOOP
         SPACE 2
RACF$18  MVC   ACF2MSG(27),=C'RACINIT failed by inst exit'
         B     LOOP
         SPACE 2
RACF$1C  MVC   ACF2MSG(19),=C'user access revoked'
         B     LOOP
         SPACE 2
RACF$24  MVC   ACF2MSG(28),=C'user access to group revoked'
         B     LOOP
         SPACE 2
RACF$28  MVC   ACF2MSG(26),=C'OID required, not supplied'
         B     LOOP
         SPACE 2
RACF$2C  MVC   ACF2MSG(20),=C'OID invalid for user'
         B     LOOP
         SPACE 2
RACF$30  MVC   ACF2MSG(31),=C'user not authorized to terminal'
         B     LOOP
         SPACE 2
RACF$34  MVC   ACF2MSG(34),=C'user not authorized to application'
         B     LOOP
         SPACE 2
.ACF2B    ANOP
         SPACE 3
*--------------------------------------*
*  IF INCORRECT PASSWORD ENTERED,      *
*  INCREASE DELAY TIME BEFORE USER IS  *
*  GIVEN ANOTHER TRY.                  *
*--------------------------------------*
BAD$PSWD L     R1,WAITTIME              GET CURRENT WAIT TIME
         LA    R1,100(,R1)              ADD 1 SECOND
         ST    R1,WAITTIME              SAVE NEW WAIT TIME
         B     LOOP                     AND TRY AGAIN
         SPACE 3
*--------------------------------------*
*  ACF2 IS NOT UP.  GET PSWD FROM TSB. *
*--------------------------------------*
*CF2DOWN LA    R0,1
***      SVC   243                      GET APF AUTHORIZED
         SPACE 1
ACF2DOWN MODESET  KEY=ZERO              TSB IS FETCH PROTECTED
         L     R1,PSAANEW-PSA           -> MY ASCB
         L     R1,ASCBTSB-ASCB(,R1)     -> TSB
         LTR   R1,R1                    ANY TSB?
         BZ    RELEASE                  NO - WHAT'S GOING ON HERE?
         MVC   DWD(8),TSBPSWD-TSB(R1)   SAVE PASSWORD TEMPORARILY
         MODESET  KEY=NZERO             GO BACK TO PROBLEM KEY
         XC    INBUF(8),DWD             DOES PASSWORD MATCH?
         BE    RELEASE                  YES - LEAVE
         MVC   ACF2MSG(30),=C'PASSWORD INCORRECT -- RE-ENTER'
         B     BAD$PSWD                 CONTINUE
         SPACE 3
*--------------------------------------*
*  CORRECT PSWD ENTERED.  CLEAN UP.    *
*--------------------------------------*
RELEASE  STFSMODE  OFF                  TURN OFF FULLSCREEN MODE
         STTMPMD   OFF,KEYS=NO
         @SWITCH  CONTROL=START         allow PIE/Sessions switching
         LTR   R15,R15                  VTAM?
         BZ    DETACH                   YES - SKIP
         TPUT  CLEAR,CLEARL,FULLSCR
         SPACE 2
DETACH   TM    FLAGS,FSTAFF             SYSTEMS STAFF?
         BZ    NO$DETCH                 NO - SUBTASK WASN'T ATTACHED
         DETACH  TCBADDR                TERMINATE SUBTASK
         SPACE 2
NO$DETCH STAX  DEFER=NO                 ALLOW ATTENTIONS
         B     LEAVE                    AND LEAVE
         SPACE 3
*--------------------------------------*
*  ATTN PRESSED.  PRETEND LIKE WE      *
*  IGNORED IT.                         *
*--------------------------------------*
ATTN$HIT MVC   ATTNMSG,ATTNMSGC
         MVI   STAR,C'*'                PERMANENT INDICATOR
         LA    R3,1(,R3)                INCR ATTEMPTS COUNTER
         MVC   ATTEMPT,ATTEMPTS         MOVE IN TEXT STRING 'ATTEMPTS'
         CH    R3,=H'1'                 IS THIS THE FIRST ATTEMPT?
         BNE   *+8                      NO - SKIP
         MVI   ATTEMPT+L'ATTEMPT-1,C' ' YES - MAKE IT SAY 'ATTEMPT '
         L     R1,WAITTIME              PICK UP WAITTIME
         SRL   R1,2                     DIVIDE BY 4
         ST    R1,WAITTIME              STORE REDUCED WAITTIME
         STFSMODE  ON                   ATTN TURNS OFF FULLSCR
         B     LOOP
         SPACE 1
         DROPX ,
         SPACE 3
STAXEXIT BR    R14                      JUST RETURN, TGET RC=8
         SPACE 3
*--------------------------------------*
*  SUBTASK TO PREVENT S522.  ONE HOUR  *
*  LIMIT.                              *
*--------------------------------------*
LOCKTIME LR    R12,R15                  SET BASE
         USNGX LOCKTIME,R12
TIMELOOP MVI   TIMEECB,0
         STIMER  REAL,TIMEEXIT,BINTVL==A(10*60*100)  WAIT 10 MINUTES
         WAIT  ECB=TIMEECB,LONG=YES
         TTIMER  CANCEL
         XC    WAITTIME,WAITTIME        RESET TGET WAIT TIME TO 0
         L     R1,POPCOUNT              GET COUNT OF TIMER POPS
         LA    R1,1(,R1)                INCREMENT
         ST    R1,POPCOUNT              SAVE UPDATED COUNT
         CH    R1,=H'6'                 6 * 10 MIN = 1 HOUR
         BNL   TIMEOUT                  NO MORE EXTENSIONS
         WTO   'LOCK - TIMER POP',ROUTCDE=15
         B     TIMELOOP
         SPACE 1
TIMEOUT  WTO   'LOCK - EXTENSION LIMIT REACHED',ROUTCDE=15
         MVI   TIMEECB,0                RESET ECB
         WAIT  ECB=TIMEECB,LONG=YES     IT'LL NEVER WAKE UP AGAIN
         EX    0,*                      CAN'T GET HERE.
         DROPX R12                      LOCKTIME
         SPACE 2
         USNGX *,R15
TIMEEXIT POST  TIMEECB
         BR    R14
         DROP  R15
         SPACE 3
*--------------------------------------*
*  MISCELLANEOUS CONSTANTS             *
*--------------------------------------*
TIMEECB  DC    F'0'                     ECB FOR STIMER
POPCOUNT DC    F'0'                     NUMBER OF TIMER POPS
WAITTIME DC    F'0'                     WAITTIME BEFORE TGET, IN .01SEC
ADDRACEE DC    A(0)                     Addr of dummy ACEE
USERIDL  DC    AL1(7)                   Length of userid
USERIDA  DC    CL7' '                   userid
PSWDL    DC    AL1(8)                   Length of password
PASSWORD DC    CL8' '                   password
ATTEMPTS DC    C'Attempts'
ATTNMSGC DC    C'Nice try, but that would be too easy.'
TIMEMASK DC    C' ',X'2120',C':',X'2020',C':',X'2020'
BLANKS   DC    CL80' '
         HEXTAB  DUAL=NO
         SPACE 3
*--------------------------------------*
*  3270 SCREEN BUFFER                  *
*--------------------------------------*
SCREEN   DC    X'27F5'                  ESC, ERASE WRITE STD
         DC    X'C1'                    WCC:
         F3270 ROW=24,COL=80,MODEL=2    SPF START
         F3270 ROW=1,COL=1              ...
         F3270 ROW=1,COL=1,RA=00        CLEAR WHOLE SCREEN
         F3270 ROW=1,COL=1,ATTR=(SF,PROT,BRIGHT)
USERID   DC    CL7' ',C' '              PADDING FOR MOVE FROM THIS FLD
         F3270 ROW=1,COL=32
SMFID    DC    CL4' ',C' / '
TERMID   DC    CL8' '
ON       DC    C' on line '
LINE     DC    CL3' ',C' '
         F3270 ROW=1,COL=70
TIME     DC    C' HH:MM:SS'
         F3270 ROW=5,COL=1,ATTR=(SF,PROT,NORM)
*        DC    '/this terminal locked.  /to unlock, enter '
         DC    C'This terminal locked at'
TIMELOCK DC    C' HH:MM:SS',C'.'
         F3270 ROW=7,COL=2
*        DC    'To unlock, enter '
*SWDTYPE DC    C'ACF2'
*        DC    C' password.'
         DC    C'To unlock, enter logon password.'
         F3270 ATTR=(SF,PROT,BRIGHT),INITIAL='===>'
         F3270 ATTR=(SF,NDISP,UNPROT),LENGTH=8
         F3270 ATTR=(SF,PROT,BRIGHT)
         F3270 ROW=9,COL=2
ACF2MSG  DC    CL79' '
         F3270 ROW=22,COL=1,ATTR=(SF,PROT,BRIGHT)
NUM      DC    CL4' '
         F3270 ATTR=(SF,PROT,NORM)
ATTEMPT  DC    CL8' '                   'ATTEMPTS'
         F3270 ROW=22,COL=80
STAR     DC    C' '                     ATTN HIT INDICATOR
         F3270 ROW=24,COL=5
ATTNMSG  DC    CL(L'ATTNMSGC)' '
*        F3270 ROW=5,COL=62
         F3270 ROW=7,COL=40
         DC    X'13'                    IC
SCREENL  EQU   *-SCREEN
         SPACE 2
CLEAR    DC    X'40'                    WCC:  NOTHING SPECIAL
         F3270 ROW=24,COL=79            LAST SPF FULLSCREEN
         F3270 ROW=1,COL=1              ...
         F3270 ROW=1,COL=1,RA=00        CLEAR ENTIRE SCREEN
CLEARL   EQU   *-CLEAR
         SPACE 3
*--------------------------------------*
*  WORK AREA                           *
*--------------------------------------*
WORKD    DSECT                          RESUME
DWD      DS    D                        CVD WORK AREA
TCBADDR  DS    F                        ADDR OF SUBTASK'S TCB
FLAGS    DS    B
FPRIV     EQU   X'80'                   PRIVILEGED USER
FACF2     EQU   X'40'                   ACF2 IS ACTIVE
FSTAFF    EQU   X'20'                   USER IS SYSTEMS STAFF
         DS    XL3                      WASTED FOR ALIGNMENT
INBUF    DS    CL80                     TGET INPUT BUFFER
MSGBUFF  DS    CL128                    ACF2 ERROR MESSAGE BUFFER
         SPACE 2
          AIF   (&@@ACF2 EQ 0).NOACF2C
         ACVALD  DSECT=NO
.NOACF2C  ANOP
         SPACE 2
WORKLEN  EQU   *-WORKD                  LENGTH TO GETMAIN
         SPACE 2
*--------------------------------------*
*  CONTROL BLOCK DSECTS                *
*--------------------------------------*
         PRINT NOGEN
         CVT   DSECT=YES
         IEESMCA  ,
         IKJPSCB  ,
         IHAPSA  ,
         IHAASCB  ,
         IHAASXB ,
         IHAACEE ,
         IKJTCB ,
         IKJTSB  ,
          AIF   (&@@ACF2 EQ 0).NOACF2D
         ACCVT ,
         ACFASVT  ,
         ACUCB ,
         LIDREC  ,
.NOACF2D  ANOP
         SPACE 2
         END
