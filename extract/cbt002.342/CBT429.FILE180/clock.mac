***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1982 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
CLOCK  TITLE '-----  DISPLAY CURRENT TIME AND WASTE SOME  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  CLOCK                                              *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          MARCH 10, 1982                                     *
*                                                                     *
*   INSTALLATION:  TITLE INSURANCE                                    *
*                                                                     *
*   PURPOSE:       NOT MUCH                                           *
*                                                                     *
*   UPDATES:                                                          *
*      04/15/82 EMS   - FIX FOR 3278-5                                *
*      03/15/82 LDW   - CHANGE ATTN PROCESSING:  BOUNCE IF BITLESS    *
*                          USER, DISPLAY TIMESTAMP IF SYSPGMR         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
CLOCK    OSENTER  EXIT=LEAVE,RC=0,GETMAIN=(WORKLEN,WORKD)
         GTSIZE  ,                      GET THE TERMINAL LINE SIZE
         SPACE 1
         CH    R1,=H'80'                80 COLUMNS WIDE?
         BL    BADTERM                  NO - GET OUT
         CH    R0,=H'24'                24 OR MORE LINES?
         BL    BADTERM                  NO - GET OUT
         MVC   IOCBNAME(8),=CL8'CLOCK'
         MVC   IOCBWAIT,=F'099'         .99 SECOND UPDATE INTERVAL
         MVI   FLAG,FAUTO               DEFAULT START AUTO UPDATING
         SPACE 1
         LOAD  EPLOC==CL8'TERMIO'       LOAD DCMS TERMINAL I/O ROUTINE
         ST    R0,IOCBATIO              SAVE ADDRESS
         SPACE 1
         STFSMODE  ON,INITIAL=YES
         SPACE 2
LOOP     LA    R14,REPLY                -> START
         LH    R15,SCR1                 LENGTH OF SCREEN BUFFER
         L     R1,=X'40000000'          GET PAD CHAR & LEN=0
         MVCL  R14,R0                   CLEAR THE SCREEN BUFFER
         SPACE 2
         TIME  DEC                      GET TIME IN R1: HHMMSSTH
         SPACE 1
         LR    R15,R0                   GET HHMMSSTH INTO BETTER REG
         SPACE 1
         LA    R2,LINEONE               WHERE TO START FORMATTING
         BAL   R10,FMTCHAR              FORMAT FIRST H
         SPACE 1
         LA    R2,12(,R2)               -> NEXT SCREEN POSITION
         BAL   R10,FMTCHAR              SECOND H
         SPACE 1
         LA    R2,12(,R2)               -> NEXT SCREEN POSITION
         MVC   02*79(2,R2),=C'**'       PUT
         MVC   03*79(2,R2),=C'**'          IN
         MVC   08*79(2,R2),=C'**'            THE
         MVC   09*79(2,R2),=C'**'               COLON
         SPACE 1
         LA    R2,4(,R2)                -> NEXT SCREEN POSITION
         BAL   R10,FMTCHAR              FORMAT FIRST M
         SPACE 1
         LA    R2,12(,R2)               -> NEXT SCREEN POSITION
         BAL   R10,FMTCHAR              SECOND M
         SPACE 1
         LA    R2,12(,R2)               -> NEXT SCREEN POSITION
         MVC   02*79(2,R2),=C'**'       PUT
         MVC   03*79(2,R2),=C'**'          IN
         MVC   08*79(2,R2),=C'**'            THE
         MVC   09*79(2,R2),=C'**'               COLON
         SPACE 1
         LA    R2,4(,R2)                -> NEXT SCREEN POSITION
         BAL   R10,FMTCHAR              FORMAT FIRST S
         SPACE 1
         LA    R2,12(,R2)               -> NEXT SCREEN POSITION
         BAL   R10,FMTCHAR              SECOND S
         EJECT
WRITE    OI    IOCBFLGC,IOCBCTIM        DO NOT TIMESTAMP SCREENS
         TM    FLAG,FAUTO               AUTO UPDATE?
         BO    WRT$AUTO                 YES - GO DO IT
         NI    IOCBFLGC,255-IOCBCTIM    DON'T TIMESTAMP WHEN NOT AUTO
         MVI   IOCBFLGD,0               CLEAR FLAGS
         SPACE 1
         TRMIO IOCB,FORMAT=SCR1,IMAGE=REPLY
         SPACE 1
         OI    IOCBFLGC,IOCBCCLR        FORMAT WHOLE SCREEN NEXT TIME
         SPACE 2
PFKEY    PFKEY ATTN=ATTN,K2=UPDATE,K3=QUIT,K12=QUIT
         SPACE 2
         B     LOOP
         SPACE 3
ATTN     TM    FLAG,FAUTO               IN AUTO UPDATE MODE?
         BNO   QUIT                     NO - GET OUT OF HERE
         L     R1,16                    -> CVT
         L     R1,0(,R1)                -> DISPATCH
         L     R1,4(,R1)                -> TCB
         L     R1,180(,R1)              -> JSCB
         L     R1,264(,R1)              -> PSCB
*        TM    PSCBATR1,PSCBCTRL        ACCOUNT?
         TM    16(R1),X'40'             ACCOUNT?
         BNO   QUIT                     NO - GET OUT OF HERE
         SPACE 2
UPDATE   XI    FLAG,FAUTO               FLIP FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
WRT$AUTO OI    IOCBFLGD,IOCBDYON+IOCBDFRC
         SPACE 1
         TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=REPLY
         SPACE 1
         NI    IOCBFLGC,255-IOCBCCLR    STOP FORMATTING WHOLE SCREEN
         SPACE 2
         PFKEY ATTN=ATTN,DEF=LOOP
         SPACE 3
QUIT     STFSMODE  OFF
         SPACE 1
         B     LEAVE
         EJECT
FMTCHAR  SR    R14,R14                  CLEAR FOR SHIFT
         SLDL  R14,4                    GET A DIGIT
         MH    R14,=Y(BLOCKLEN)         GET OFFSET INTO "BLOCK"
         LA    R14,BLOCK(R14)           -> CHARACTER
         MVC   00*79(10,R2),00*10(R14)  FIRST LINE
         MVC   01*79(10,R2),01*10(R14)
         MVC   02*79(10,R2),02*10(R14)
         MVC   03*79(10,R2),03*10(R14)
         MVC   04*79(10,R2),04*10(R14)
         MVC   05*79(10,R2),05*10(R14)
         MVC   06*79(10,R2),06*10(R14)
         MVC   07*79(10,R2),07*10(R14)
         MVC   08*79(10,R2),08*10(R14)
         MVC   09*79(10,R2),09*10(R14)
         MVC   10*79(10,R2),10*10(R14)
         MVC   11*79(10,R2),11*10(R14)  LAST LINE
         BR    R10                      RETURN TO MAINLINE
         SPACE 3
BADTERM  TPUTX 'ONLY 3270 TERMINALS ARE SUPPORTED'
         SPACE 1
         B     LEAVE
         SPACE 3
         LTORG
         EJECT
*  COLON
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4' ** '
         DC    CL4' ** '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4'    '
         DC    CL4' ** '
         DC    CL4' ** '
         DC    CL4'    '
         DC    CL4'    '
*  ZERO
BLOCK    DC    CL10'  ******  '
BLOCK1   EQU   *-BLOCK                  LENGTH OF 1 LINE
         DC    CL10' ******** '
         DC    CL10'**      **'
         DC    CL10'**     ***'
         DC    CL10'**    * **'
         DC    CL10'**   *  **'
         DC    CL10'**  *   **'
         DC    CL10'** *    **'
         DC    CL10'***     **'
         DC    CL10'**      **'
         DC    CL10' ******** '
         DC    CL10'  ******  '
BLOCKLEN EQU   *-BLOCK                  LENGTH OF WHOLE CHARACTER
*  ONE
         DC    CL10'    **    '
         DC    CL10'   ***    '
         DC    CL10'  ****    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10'    **    '
         DC    CL10' ******** '
         DC    CL10' ******** '
*  TWO
         DC    CL10' ******** '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'        **'
         DC    CL10'        **'
         DC    CL10'       ** '
         DC    CL10'     **   '
         DC    CL10'   **     '
         DC    CL10' **       '
         DC    CL10'**        '
         DC    CL10'**********'
         DC    CL10'**********'
*  THREE
         DC    CL10' ******** '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'        **'
         DC    CL10'        **'
         DC    CL10'     **** '
         DC    CL10'     **** '
         DC    CL10'        **'
         DC    CL10'        **'
         DC    CL10'**      **'
         DC    CL10'**********'
         DC    CL10' ******** '
*  FOUR
         DC    CL10'      *** '
         DC    CL10'     **** '
         DC    CL10'    ** ** '
         DC    CL10'   **  ** '
         DC    CL10'  **   ** '
         DC    CL10' *********'
         DC    CL10'**********'
         DC    CL10'       ** '
         DC    CL10'       ** '
         DC    CL10'       ** '
         DC    CL10'       ** '
         DC    CL10'       ** '
*  FIVE
         DC    CL10'**********'
         DC    CL10'**********'
         DC    CL10'**        '
         DC    CL10'**        '
         DC    CL10'**        '
         DC    CL10'*******   '
         DC    CL10'********  '
         DC    CL10'       ** '
         DC    CL10'        **'
         DC    CL10'        **'
         DC    CL10'**********'
         DC    CL10'********* '
*  SIX
         DC    CL10' ******** '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'**        '
         DC    CL10'**        '
         DC    CL10'********* '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10'**********'
         DC    CL10' ******** '
*  SEVEN
         DC    CL10'**********'
         DC    CL10'********* '
         DC    CL10'**    **  '
         DC    CL10'     **   '
         DC    CL10'    **    '
         DC    CL10'   **     '
         DC    CL10'   **     '
         DC    CL10'   **     '
         DC    CL10'   **     '
         DC    CL10'   **     '
         DC    CL10'   **     '
         DC    CL10'   **     '
*  EIGHT
         DC    CL10' ******** '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10' **    ** '
         DC    CL10'  ******  '
         DC    CL10'  ******  '
         DC    CL10' **    ** '
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10'**********'
         DC    CL10' ******** '
*  NINE
         DC    CL10' ******** '
         DC    CL10'**********'
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10'**      **'
         DC    CL10'**********'
         DC    CL10'**********'
         DC    CL10'        **'
         DC    CL10'        **'
         DC    CL10'**      **'
         DC    CL10'**********'
         DC    CL10' ******** '
         EJECT
         PRINT NOGEN
         SPACE 1
SCR1     DCMSFMT
REPLY    DCMSFLD  10,ALPHA,INTEN=HI,CURSOR=YES
         DCMSFLD  68,INTEN=HI
LINEONE  DCMSFLD  79,INTEN=LO,ROW=7     1
         DCMSFLD  79,INTEN=LO           2
         DCMSFLD  79,INTEN=LO           3
         DCMSFLD  79,INTEN=LO           4
         DCMSFLD  79,INTEN=LO           5
         DCMSFLD  79,INTEN=LO           6
         DCMSFLD  79,INTEN=LO           7
         DCMSFLD  79,INTEN=LO           8
         DCMSFLD  79,INTEN=LO           9
         DCMSFLD  79,INTEN=LO           10
         DCMSFLD  79,INTEN=LO           11
         DCMSFLD  79,INTEN=LO           12
         DCMSFEND
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D
FLAG     DS    X
FAUTO    EQU   X'80'
         SPACE 2
IOCB     IOCB
         EJECT
         DSGEN SCR1                     SCREEN BUFFER
         SPACE 2
WORKLEN  EQU   *-WORKD
         SPACE 2
         END
