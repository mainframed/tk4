***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
LAU      TITLE '-----  LIST ACF2 USERS (LOGGED ON AND BATCH)   -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  LAU                                                *
*                                                                     *
*   PURPOSE:       SEARCH THE ACF2 ASVT AND LIST INFORMATION ON THE   *
*                  ACF2 LOGONID FOR EACH CURRENTLY EXECUTING JOB AND  *
*                  TSO SESSION.                                       *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          DECEMBER 31, 1981                                  *
*                                                                     *
*   UPDATES:                                                          *
*      21Dec88  LDW  Use PUTLINE instead of TPUT                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         MACRO
&NFS     SHOW  &FIELD
         LCLA  &A
         LCLC  &LQ
&A       SETA  K'&FIELD
&LQ      SETC  'L'''                    STUPID ASSEMBLER
&NFS     MVC   BUFFER,BLANKS
         MVC   BUFFER+5(&A+1),=C'&FIELD='
         MVC   BUFFER+5+&A+1(&LQ&FIELD),&FIELD
.*       BAL   LINK,TPUTX
         BAL   LINK,PUTLINE
         MEND
         MACRO
&NFS     TPUTLIT  &TEXT,&LENGTH,&OPTS
         LCLA  &L
         AIF   ('&OPTS' EQ '').OPTSOK
         MNOTE 8,'Options not supported by TPUTLIT'
         MEXIT
.OPTSOK  ANOP
.LIT     ANOP
&L       SETA  K'&TEXT-2
&NFS     MVC   BUFFER(&L),=C&TEXT
         LA    R0,&L&LENGTH
         BAL   LINK,PUTLINE
         SPACE 1
         MEND
         EJECT
LAU      OSENTER  EXIT=LEAVE,RC=0,PARMREG=R2,ADDRREG=R3,LENREG=R4,     $
               GETMAIN=(WORKLEN,WORKD),ENV=(CP,SIM),PL=(IO)
LINK     EQU   R11
         SPACE 2
         MVC   BUFFOLD(4),=F'1'         NUMBER OF OUTPUT SEGMENTS
         LA    R1,BUFFLEN               -> PUTLINE BUFFER
         ST    R1,BUFFOLD+4
         MVC   BUFFLEN(4),=H'80,0'      length, offset (BUFFOFF)
         MVI   BUFFMID,C' '             INIT MSGID
         MVC   BUFFER,BLANKS            CLEAR DISPLAY BUFFER
*        LTR   R4,R4                    ANY PARM?
*        BNP   NOPARM                   NO - ERROR
*        CH    R4,=H'8'                 PARM TOO LONG?
*        BNL   PARMLONG                 YES - ERROR
*        BCTR  R4,0                     -1 FOR EX
*        EX    R4,PACK                  << PACK DWD,0(*-*,R3) >>
*        CVB   R4,DWD                   GET ASID IN BINARY
         SR    R4,R4                    DO ALL ASID'S
         ACFINCVT  R9,NONE=NOACFCVT     GET THE ACF2 CVT ADDRESS
         USING ACCVT,R9
         MVC   BUFFER(15),=C'ACF2 CVT ADDR ='
         ST    R9,DWD
         HEX   BUFFER+16,DWD+1,LEN=3
*        BAL   LINK,TPUTX
         BAL   LINK,PUTLINE
         L     R8,ACCASVT               OBTAIN ASVT ADDR
         MVC   BUFFER(16),=C'ACF2 ASVT ADDR ='
         ST    R8,DWD
         HEX   BUFFER+17,DWD+1,LEN=3
*        BAL   LINK,TPUTX
         BAL   LINK,PUTLINE
         L     R1,CVTPTR                -> CVT
         L     R1,CVTASVT-CVT(,R1)      -> ASVT
         L     R3,ASVTMAXU-ASVT(,R1)    MAXIMUM NUMBER OF ENTRIES
         LA    R6,ASVTENTY-4-ASVT(,R1)  -> FIRST ENTRY - 4
*        TPUTX ' '
         TPUTLIT  ' '
*        TPUTX HEADER1,80
         MVC   BUFFER(80),HEADER1
         BAL   LINK,PUTLINE
*        TPUTX HEADER1,80
         MVC   BUFFER(80),HEADER2
         BAL   LINK,PUTLINE
         SPACE 3
LOOP     LA    R4,1(,R4)                NEXT ASID
         ST    R4,DWD
         HEX   ASID,DWD+2,LEN=2
         LA    R6,4(,R6)                -> NEXT ASCB ADDRESS
         ICM   R15,B'1111',0(R6)        GET THE ASCB ADDRESS
         BM    NEXT                     SKIP IF NOT IN USE
         USING ASCB,R15
         ICM   R14,B'1111',ASCBJBNI     AN INITIATED JOB?
         BNZ   *+8                      YES - OK
         ICM   R14,B'1111',ASCBJBNS     NO - TRY S/M/L
         BZ    *+10                     NO JOBNAME POINTER
         MVC   JOBNAME,0(R14)
         DROP  R15
         LR    R7,R4  **TEMP KLUDGE**   OBTAIN USERS ASID
         SLL   R7,ASVINDEX              OFFSET IN ASVT
         ALR   R7,R8                    NOW HAVE ENTRY PTR
         USING ACFASVT,R7
         CLI   ASVLID,0                 ANYTHING HERE?
         BE    SKIPACF2                 NO - DON'T MOVE LOGONID OR SRC
         MVC   SOURCE,ASVSRC
         MVC   LOGONID,ASVLID
SKIPACF2 MVC   JOBID,ASVJID
         MVC   ADRACUCB,=C'  ..  '
         TM    ASVFLAG1,ASVF1UCB        ACUCB VALID?
         BNO   NO$ACUCB                 NO
         HEX   ADRACUCB,ASVUCB+1,LEN=3
*O$ACUCB BAL   LINK,TPUTX
NO$ACUCB BAL   LINK,PUTLINE
***  ACUCB IS IN PRIVATE AREA - MAY ONLY BE ACCESSED FOR CURRENT USER.
*        L     R3,ASVUCB                GET THE ACUCB ADDRESS
*        USING ACUCB,R3
*        TPUTX (R3),ACULEN              **TEMP KLUDGE**
NEXT     BCT   R3,LOOP
         B     LEAVE
         SPACE 2
*TPUTX   TPUTX BUFFER,80
*        MVC   BUFFER,BLANKS
*        BR    LINK
         SPACE 2
*NOPARM  TPUTX '*** PARM MISSING ***'
NOPARM   TPUTLIT  '*** PARM MISSING ***'
         SPACE 1
         B     LEAVE
         SPACE 2
*ARMLONG TPUTX '*** PARM TOO LONG ***'
PARMLONG TPUTLIT  '*** PARM TOO LONG ***'
         SPACE 1
         B     LEAVE
         SPACE 2
*OACFCVT TPUTX '*** ACF2 IS NOT UP ***'
NOACFCVT TPUTLIT '*** ACF2 IS NOT UP ***'
         SPACE 1
         B     LEAVE
         SPACE 3
PUTLINE  PUTLINE  OUTPUT=BUFFOLD,PARM=PUTLINEC,MF=(E,IOPL)
         SPACE 1
         MVC   BUFFER,BLANKS
         BR    LINK
         SPACE 3
PACK     PACK  DWD,0(*-*,R3)            << EXECUTED >>
         EJECT
         LTORG ,
         SPACE 2
PUTLINEC PUTLINE  MF=L
         SPACE 2
BLANKS   DC    CL80' '
HEADER1  DC    CL80'ASID  JOB ID    JOBNAME   LOGONID   SOURCE    @ACUC$
               B'
HEADER2  DC    CL80'----  --------  --------  --------  --------  -----$
               -'
HEXTAB   HEXTAB  DUAL=NO
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D
         SPACE 2
BUFFOLD  DS    2F                       OUTPUT LINE DESCRIPTOR
BUFFLEN  DS    H                        PRINTLINE LENGTH
BUFFOFF  DS    H                        OFFSET
BUFFMID  DS    C                        delim after msgid
BUFFER   DS    CL80
         ORG   BUFFER
ASID     DS    CL4,CL2
JOBID    DS    CL8,CL2
JOBNAME  DS    CL8,CL2
LOGONID  DS    CL8,CL2
SOURCE   DS    CL8,CL2
ADRACUCB DS    CL6
         ORG   ,
WORKLEN  EQU   *-WORKD                  DEFINE LENGTH
         EJECT
         ACCVT ,                        ACF2 CVT
         EJECT
         ACFASVT  ,                     ACF2 ASVT ENTRY
         EJECT
         ACUCB ,                        ACF2 USER CONTROL BLOCK
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAASVT  ,
         EJECT
         IHAASCB  ,
         SPACE 3
         END
