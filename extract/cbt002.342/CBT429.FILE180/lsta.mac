***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1980 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
LSTA     TITLE 'LIST ALLOCATED -- SHORT VERSION '
***********************************************************************
* UPDATES:                                                            *
*    10/27/80 LDW2 - CHANGE NAME DISPLAYED FOR VTOC                   *
*    02/15/80 LDW1 - CHECK FOR VTOC DSNAME, REMOVE MODID MACRO        *
*    11/23/79 SDM2 - STOP SKIPPING SYSTEM GENERATED DATASET NAMES     *
*    06/26/79 SDM1 - 1. CATCH "NO DATASETS ALLOCATED" CONDITION       *
*                    2. ELIMINATE THOSE PUTRID OVERLAPPING MVC'S      *
*                    3. IDENTIFY MODULE AND ASSMBLY DATE WITH 'MODID' *
***********************************************************************
         SPACE 2
LSTA     CSECT
         SAVE  (14,12),,LSTA_&SYSDATE_&SYSTIME                     LDW1
         LR    R12,R15
         USING LSTA,R12
         LA    R15,REGSAVE
         ST    R15,8(,R13)
         ST    R13,4(,R15)
         LR    R13,R15
         SPACE 3
NEXT     CLI   LE+6,X'80'               LAST ENTRY?
         BE    END                      ...YES, TERMINATE
         MVC   BUFF,SPACES                                         SDM1
         LA    R1,PARMLIST
         MVC   DD+4(10),XDD
         MVC   DA+4(46),XDA
         MVC   MEM+4(10),XMEM
         LH    R2,RR+6
         LA    R2,1(,R2)
         STH   R2,RR+6                  UP ENTRY COUNT
         SVC   99                       DO IT.
         LTR   R11,R15                  SAVE IT FOR THE TURKEYS
         BNZ   SVC99ERR                 POOR FOOL FOUND NOTHING    SDM1
*                                       (OR SOME OTHER ERROR)      SDM1
         SPACE 1
* IF SYSTEM ASSIGNED NAME, SKIP COMPLETELY
*SDM2    CLI   DA+5,X'18'
*SDM2    BL    SETDD                    TOO SHORT FOR SYSTEM ASSIGNED
*SDM2    CLC   DA+6(3),=C'SYS'
*SDM2    BNE   SETDD
*SDM2    CLC   DA+14(2),=C'.T'
*SDM2    BNE   SETDD
*SDM2    CLC   DA+22(3),=C'.RA'
*SDM2    BE    NEXT                     GOOD ENUFF FOR ME
         SPACE 1
SETDD    MVC   DDNAME,DD+6
         CLI   DS+6,X'80'               DUMMY ALLOCATION
         BNE   CK2
         MVC   DSNAME(8),=CL8'DUMMY'
         B     PUT
         SPACE 1
CK2      CLI   DS+6,X'40'               TERMINAL ALLOCATION
         BNE   SETDSN
         MVC   DSNAME(8),=CL8'TERMINAL'
         B     PUT
         SPACE 1
SETDSN   MVC   DSNAME,DA+6
         CLC   DSNAME,=44X'04'          CUTE?                      LDW1
         BNE   NOTVTOC                  NO - SKIP                  LDW1
         MVC   DSNAME,=CL44'*** VTOC ***'  YES - BE CUTER HERE     LDW2
         LA    R2,DSNAME+12             POINT TO NEXT AREA         LDW1
         B     SETDISP                  SKIP MEMBERNM PROCESSING   LDW1
         SPACE 2
NOTVTOC  LH    R2,DA+4                  DSN LENGTH
         LA    R2,DSNAME(R2)            WHERE TO START MEMBER FIELD
         SPACE 1
         CLI   MEM+5,X'00'              IS THERE MEMBER NAME?
         BE    SETDISP                  NO - SKIP PROCESSING
         MVI   0(R2),C'('
         MVC   1(8,R2),MEM+6            MOVE MEMBER NAME
         SR    R3,R3
         IC    R3,MEM+5                 GET LENGTH OF MEMBER NAME
         LA    R2,1(R2,R3)              END OF MEMBER NAME
         MVI   0(R2),C')'
         LA    R2,1(,R2)
         SPACE 2
SETDISP  MVC   3(3,R2),=C'SHR'
         CLI   D1+6,X'08'
         BE    PUT                      FOR SHR, DON'T SET  DISP
         MVC   3(3,R2),=C'NEW'
         CLI   D1+6,X'04'
         BE    CK3
         MVC   3(3,R2),=C'MOD'
         CLI   D1+6,X'02'
         BE    CK3
         MVC   3(3,R2),=C'OLD'
         SPACE 1
CK3      LA    R1,D2+6
         LA    R2,6(,R2)
         BAL   R14,DODISP
         LA    R1,D3+6
         BAL   R14,DODISP
         SPACE 1
PUT      TPUT  BUFF,74
         B     NEXT                     GO GET THE NEXT ONE
         SPACE 1
END      SR    R15,R15                                             SDM1
GETOUT   L     R13,4(,R13)              TIME TO LEAVE              SDM1
         RETURN (14,12),T,RC=(15)                                  SDM1
SVC99ERR CH    R15,=H'4'                WUZZIT "NO DATASETS"       SDM1
*                                       (OR WHATEVER RC=4 REALLY   SDM1
*                                       MEANS)                     SDM1
         BNE   SVC99UGH                 NO, IT REALLY IS A PROBLEM SDM1
         TPUT  NDSMSG,L'NDSMSG          TELL GREG "NO DATASETS"    SDM1
         LR    R15,R11                  RESTORE SAVED SVC 99 RC    SDM1
         B     GETOUT                   EXIT WITH SPECIAL RC       SDM1
SVC99UGH TPUT  UGHMSG,L'UGHMSG          SAY I'VE GOT A PROBLEM     SDM1
         LR    R15,R11                  RESTORE SAVED SVC 99 RC    SDM1
         B     GETOUT                   EXIT WITH SPECIAL RC       SDM1
         EJECT
DODISP   SR    R3,R3
         SR    R4,R4
         IC    R3,0(,R1)                DISP INDICATOR
         SPACE 1
         LTR   R3,R3                    IS IT ZERO?
         BZR   R14                      IF SO, SKIP COMPLETELY
         SPACE 1
LOOP     LA    R4,1(,R4)                COUNT LOOP EXECUTIONS
         SRA   R3,1                     SHIFT,
         BNZ   LOOP                     UNTIL VALUE IS ZERO
         SPACE 1
         SLA   R4,3                     MULTIPLY BY 8
         LA    R4,DISPTAB-8(R4)         OFFSET OF NAME
         MVC   1(7,R2),1(R4)            MOVE DISP
         MVI   0(R2),C','
         SR    R3,R3
         IC    R3,0(,R4)                LENGTH OF OF DISP NAME
         LA    R2,1(R2,R3)              WHERE TO START NEXT FIELD
         BR    R14
         EJECT
         LTORG
         EJECT
REGSAVE  DC    9D'0'
         SPACE 1                                                   SDM1
NDSMSG   DC    C'NO DATASETS ALLOCATED!!!!'                        SDM1
UGHMSG   DC    C'SVC 99 DIDN''T LIKE SOMETHING'                    SDM1
         SPACE 1
DISPTAB  DC    X'07',C'UNCATLG',X'05',C'CATLG  '
         DC    X'06',C'DELETE ',X'04',C'KEEP   '
         DC    X'04',C'PASS   '
         SPACE 1
SPACES   DC    CL75' '                                             SDM1
BUFF     DS    0CL75
DDNAME   DS    CL8
         DS    2C
DSNAME   DS    CL44
         DS    21C
         SPACE 1
PARMLIST DS    0F
         DC    X'80',AL3(RB)
RB       DC    X'14071000',F'0',A(TP),2F'0'
TP       DC    A(DD),A(DA),A(MEM),A(D1),A(D2),A(D3),A(LE),A(DS)
         DC    X'80',AL3(RR)
DD       DC    H'4',H'1',H'8',8C' '
DA       DC    H'5',H'1',H'44',44C' '
MEM      DC    H'6',H'1',H'8',8C' '
D1       DC    H'7',H'1',H'1',X'00'
D2       DC    H'8',H'1',H'1',X'00'
D3       DC    H'9',H'1',H'1',X'00'
LE       DC    H'13',H'1',H'1',X'00'
DS       DC    H'14',H'1',H'1',X'00'
RR       DC    H'15',H'1',H'2',H'0'
         SPACE 1
XDD      DC    H'8',8C' '
XDA      DC    H'44',44C' '
XMEM     DC    H'8',8C' '
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         END
