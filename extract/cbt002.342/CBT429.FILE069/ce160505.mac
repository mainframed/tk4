*
* CODING EXAMPLE 16.5.5
*
*
	 MACRO
&LABEL	 MOVE  &TO,&FROM,&LEN
	 LCLC  &X,&C
         AIF   (T'&LEN EQ 'O').MISOPRD
         AIF   (T'&FROM EQ 'O').MISOPRD
         AIF   (T'&TO EQ 'O').MISOPRD
&LABEL	 DS    0H
         AIF   ('&LEN'(1,1) EQ '(').LENREG
         L     1,=F'&LEN'
.B2      AIF   ('&TO'(1,1) EQ '(').TOREG
	 LA    14,&TO
.B3      AIF   ('&FROM'(1,2) EQ 'X''').LITX
         AIF   ('&FROM'(1,2) EQ 'C''').LITC
         AIF   ('&FROM'(1,1) EQ '(').FROMREG
	 LA    15,&FROM
	 AGO   .B1
.LITX	 ANOP
&X       SETC  '&FROM'(2,4)
	 LA    15,=256XL1&X
	 AGO   .B1
.LITC	 ANOP
&C       SETC  '&FROM'(2,3)
	 LA    15,=256CL1&C
.B1      C     1,=F'256'
         AIF   ('&FROM'(1,2) EQ 'X''').NOINCR1
         AIF   ('&FROM'(1,2) EQ 'C''').NOINCR1
	 BL    *+26
	 AGO   .B4
.NOINCR1 BL    *+22
.B4	 ANOP
	 MVC   0(256,14),0(15)
	 LA    14,256(0,14)
         AIF   ('&FROM'(1,2) EQ 'X''').NOINCR2
         AIF   ('&FROM'(1,2) EQ 'C''').NOINCR2
	 LA    15,256(0,15)
.NOINCR2 S     1,=F'256'
	 B     *-26
         C     1,=F'0'
	 BE    *+22
         S     1,=F'1'
	 EX    1,*+8
	 B     *+10
	 MVC   0(0,14),0(15)
	 AGO   .END
.LENREG  AIF   ('&LEN(1)' EQ '1').B2
	 LR    1,&LEN(1)
	 AGO   .B2
.TOREG   AIF   ('&TO(1)' EQ '14').B3
	 LR    14,&TO(1)
	 AGO   .B3
.FROMREG AIF   ('&FROM(1)' EQ '15').B1
	 LR    15,&FROM(1)
	 AGO   .B1
.MISOPRD MNOTE 'ONE OR MORE REQUIRED OPERANDS ARE MISSING---MACRO NOT GC
               ENERATED'
.END	 MEND
