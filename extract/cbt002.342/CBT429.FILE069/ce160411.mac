*
* CODING EXAMPLE 16.4.11
*
*
//-------- JOB	 ----
//STEP     EXEC  ASMACL,PARM.L='AC(1)'
//C.SYSLIB DD	 DSN=SYS1.MACLIB,DISP=SHR
//	   DD	 DSN=USER.MACLIB,DISP=SHR
//C.SYSIN  DD *
*
*
MKCOMDSP CSECT
*
*
**********************************************************************
* INITIALIZATION
**********************************************************************
*
MKCOMDSP AMODE 31	       \ INDICATE 31-BIT ADDRESSING
MKCOMDSP RMODE ANY	       /
	 INITL 3,4,5,EQU=R     SET UP BASE REGS AND SAVEARE
*
*
**********************************************************************
* MAINSTREAM OF PROGRAM
**********************************************************************
*
	 BAL   R6,SETMODE      SET SUPV STATE, PSW KEY ZERO
	 BAL   R6,MKUNSWAP     MAKE ADDR SPACE UNSWAPPABLE
	 BAL   R6,MAKEDS       CREATE DATA SPACE
	 BAL   R6,SETDSADR     SET UP ADDRESSABLITY TO DATASPACE
	 BAL   R6,PUTINLPA     PUT ALET INTO LMOD IN LPA
	 BAL   R6,DOWAIT       GO INTO WAIT STATE
	 B     RETURN	       RETURN TO MVS OR CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE PUTS THE PROGRAM INTO SUPERVISOR STATE, PSW KEY 0. THE
* PROGRAM MUST BE APF-AUTHORIZED IN ORDER TO ISSUE THIS MACRO.
**********************************************************************
*
SETMODE  MODESET MODE=SUP,KEY=ZERO
	 BR    R6	       RETURN TO CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE MAKES THE HOME ADDRESS SPACE IN WHICH THIS PROGRAMS
* EXECUTES NONSWAPPABLE.
**********************************************************************
*
MKUNSWAP SYSEVENT DONTSWAP
	 BR    R6	       RETURN TO CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE CREATES THE SCOPE=COMMON DATA SPACE.
**********************************************************************
*
MAKEDS	 DSPSERV CREATE,TYPE=BASIC,STOKEN=DS99TOK,SCOPE=COMMON,        -
	       BLOCKS=MAXBLKS,NUMBLKS=CURRBLKS, 		       -
	       KEY=PROTKEY,FPROT=NO,				       -
	       NAME=DS99NAME,GENNAME=YES,OUTNAME=DS99GNAM,	       -
	       ORIGIN=DS99ORGN
	 LTR   R15,R15		  CHECK IF DSPSERV OK
	 BZR   R6		  IF OK, RETURN TO CALLING ROUTINE
	 LA    R10,901		  IF NOT, SET USERCODE
	 B     ABEND		  ABEND PROGRAM
*
*
**********************************************************************
* THIS ROUTINE PUTS THE DATA SPACE ENTRY ONTO THE PASN-AL OF THIS
* ADDRESS SPACE AND NOTIFIES MVS TO PLACE THE ENTRY ONTO THE PASN-AL
* OF ALL EXISTING ADDRESS SPACES.
**********************************************************************
*
SETDSADR SYSSTATE ASCENV=AR	  INDICATE AR-MODE TO FOLLOWING MACROS
	 ALESERV ADD,STOKEN=DS99TOK,ALET=DS99ALET,AL=PASN
	 LA    R10,902		  PRIME USERCODE IN REG 10
	 LTR   R15,R15		  CHECK IF ALESERV OK
	 BNZ   ABEND		  IF NOT, ABEND PROGRAM
	 BR    R6		  RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE LINKS TO PROGRAM PUTINLPA AND PASSES THE FOLLOWING
* DATA TO IT IN THE FOLLOWING GENERAL PURPOSE REGISTERS:
*   GPR  8: ALET
*   GPR  9: BEG ADDR OF DATA SPACE
*   GRP 10: FIRST 4 BYTES OF STOKEN
*   GRP 11: LAST 4 BYTES OF STOKEN.
**********************************************************************
*
PUTINLPA L     R8,DS99ALET     \
	 L     R9,DS99ORGN	\  LOAD DATA TO BE PASSED TO CALLED
	 L     R10,DS99TOK	/  PROGRAM INTO GPRS
	 L     R11,DS99TOK+4   /
	 LINKX EP=PUTINLPA
	 BR    R6	       RETURN TO CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE PUTS THE PROGRAM INTO A SHORT WAIT STATE. THIS IS FOR
* DEMO PURPOSES ONLY. IF THIS PROGRAM HAD OTHER PROCESSING TO DO,
* IT WOULD BE DONE AT THIS TIME. IF THE ONLY FUNCTION OF THIS
* PROGRAM WERE TO CREATE A SCOPE=COMMON DATA SPACE, THEN THE PROGRAM
* WOULD GO INTO A LONG WAIT.
**********************************************************************
*
DOWAIT	 STIMER WAIT,DINTVL=WAITTIME
	 BR    R6	       RETURN TO CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE ABENDS THE PROGRAM USING USERCODE IN REGISTER 10.
**********************************************************************
*
ABEND	 ABEND (R10),DUMP
*
*
**********************************************************************
* THIS ROUTINE RESTORES THE REGISTERS AND RETURNS TO MVS OR THE
* CALLING PROGRAM.
**********************************************************************
*
RETURN	 RCNTL RC=0
*
*
**********************************************************************
* WORKING STORAGE AND CONSTANTS
**********************************************************************
*
PROTKEY  DC    X'80'
DS99TOK  DS    D
DS99ALET DS    F
DS99NAME DC    C'D01$$$$$'
DS99GNAM DS    CL8
MAXBLKS  DC    A(524288)
CURRBLKS DS    CL4
DS99ORGN DS    F
WAITTIME DC    C'00100000'    TEN MINUTES
*
*
**********************************************************************
* END OF PROGRAM
**********************************************************************
*
	 END
//*
//*
//L.SYSLMOD DD DSN=SYS1.AUTHLIB(MKCOMDSP),DISP=SHR
//
