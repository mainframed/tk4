ISREDIT MACRO (ARG) NOPROCESS
 /********************* ADDAFTR  ISPF EDIT MACRO *********************/
 /*                                                                  */
 /* ADDAFTR IS A SPECIAL PURPOSE EDIT MACRO THAT WILL INSERT A LINE  */
 /* OF DATA AFTER EACH LINE WITHIN AND IDENTIFIED RANGE OF LINES.    */
 /* A RANGE IS DEFINED BY USING A RANGE COMMAND IDENTIFIED BY A 'Z'. */
 /* 'Z', 'ZZ', AND 'ZNNN' CAN BE USED AS RANGE IDENTIFIERS.          */
 /*                                                                  */
 /* DATA CAN BE PASSED ON THE COMMAND LINE. IF DATA IS NOT PASSED ON */
 /* THE COMMAND LINE, THE CURSOR MUST BE PLACE ON THE DATA LINE TO   */
 /* BE USED AS DATA.                                                 */
 /*                                                                  */
 /* SYNTAX: ADDAFTR {OPTIONAL DATA TO BE USED AS INSERT}             */
 /*                                                                  */
 /********************************************************************/

 IF &ARG = THEN DO
 ISREDIT (ROW,COL) = CURSOR
 ISREDIT (ARG1) = LINE &ROW
 SET &ARG = &NRSTR(&ARG1)
 END
 ISPEXEC CONTROL ERRORS RETURN
ISREDIT PROCESS RANGE Z
SET RC = &LASTCC
IF &RC = 4 THEN +
  DO
     SET ZEDSMSG = &STR(RANGE NOT SPECIFIED)
     SET ZEDLMSG = &STR(SPECIFY RANGE FOR THE CUT AND RE-EXECUTE +
                        THE MACRO)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(4)
   END
ELSE +
    IF &RC > 0 THEN +
      DO
     SET ZEDSMSG = &STR(SEVERE ERROR)
     SET ZEDLMSG = &STR(SEVERE ERROR OCCURED, RETURN CODE WAS +
                        &RC)
     ISPEXEC SETMSG MSG(ISRZ001)
     EXIT CODE(&RC)
   END
 ISREDIT (CMD) = RANGE_CMD
 ISREDIT (FIRST) = LINENUM .ZFRANGE
 ISREDIT (LAST) = LINENUM .ZLRANGE
 DOIT: +
 ISREDIT LINE_AFTER &FIRST =  '&NRSTR(&ARG)'
  SET &FIRST = &FIRST + 2
  SET &LAST  = &LAST  + 1
  IF &FIRST > &LAST THEN GOTO MD
  GOTO DOIT
MD: +
EXIT
