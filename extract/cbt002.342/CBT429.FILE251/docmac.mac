 /********************* DOCMAC   ISPF EDIT MACRO *********************/
 /* DOCMAC ISPF EDIT MACRO IS USED  TO PLACE A BOX IN WHICH COMMENTS */
 /* OR  DOCUMENTATION CAN  BE  PLACED  WITHIN  A EDIT MACRO.  DOCMAC */
 /* ACCEPTS A VARIABLE TO INDICATE  THE  NUMBER OF BLANK LINES TO BE */
 /* PLACED WITHIN THE COMMENT  BOX.   THE  DEFAULT  IS 5 BLANK BLANK */
 /* LINES.  IF PRESENT.  A  BEFORE  OR  AFTER  RANGE COMMAND WILL BE */
 /* USED TO DETERMINE THE  PLACEMENT  OF  THE  BOX.   IF NO RANGE IS */
 /* FOUND THE BOX WILL BE PLACED  AT  THE TOP OF THE CURRENT DATASET */
 /* BEING EDITIED.                                                   */
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
 /* SYNTAX: DOCMAC                                                   */
 /*                                                                  */
 /********************************************************************/


ISREDIT MACRO (LINES) NOPROCESS
    IF &LINES = HELP  THEN DO
     ISREDIT MACPEEK DOCMAC
    EXIT
    END
    IF &LINES = ?  THEN DO
     ISREDIT MACPEEK DOCMAC
    EXIT
    END
    /* SET DEFAULT NUMBER OF LINES */
    IF &LINES =   THEN SET &LINES = 5

  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  ISREDIT PROCESS DEST
    SET &DESTCC = &LASTCC
      IF &DESTCC = 0 THEN DO
       ISREDIT (WHERE) = RANGE_CMD
       ISREDIT LOCATE .ZDEST
       ISREDIT (PTR,COL) = CURSOR
      END
  IF &DESTCC = 8 THEN SET &PTR  = 0
  IF &WHERE > B  THEN SET &PTR  = &PTR   - 1
  ISREDIT (MEM) = MEMBER
  IF &LENGTH(&MEM) NE 8 THEN DO
  SET &MEM = &STR(&MEM&STR(        ))
  SET &MEM = &SUBSTR(1:8,&STR(&MEM))
  END
  SET &TOP = +
   &STR(/*************  )&STR(&MEM)+
  &STR( ISPF EDIT MACRO  *************/)
  SET &BOTTOM = +
   &STR(/******************************************************/)
  SET &FILL   = +
   &STR(/*                                                    */)
  ISREDIT LINE_AFTER &PTR = '&STR(          &TOP)'
  SET &PTR = &PTR + 1
  DO WHILE &LINES > 0
  ISREDIT LINE_AFTER &PTR = '&STR(          &FILL)'
  SET &PTR = &PTR + 1
  SET  &LINES = &LINES - 1
  END
  ISREDIT LINE_AFTER &PTR = '&STR(          &BOTTOM)'
  SET &PTR = &PTR + 1
  ISREDIT LINE_AFTER &PTR = '&STR(    )'
