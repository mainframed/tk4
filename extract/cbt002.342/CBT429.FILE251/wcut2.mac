 /********************* WCUT2    ISPF EDIT MACRO *********************/
 /* WCUT2 IS A SPECIAL PURPOSE EDIT  MACRO CALLED BE THE WCUT MACRO. */
 /* WCUT SETS PF12  TO  THE  VALUE  WCUT2,  WHIC  THEN  COMES IN AND */
 /* FINISHES THE WORK.  IT  RESETS  PF12  TO  ITS ORIGINAL VALUE AND */
 /* ISSUES A MESSAGE GIVING THE SIZE OF THE CUT AREA.                */
 /*                                                                  */
 /* SYNTAX: WCUT2     {CAN BE USED RATHER THAN PF12}                 */
 /*                                                                  */
 /********************************************************************/

  ISREDIT MACRO (HELP)
       IF &STR(&HELP) = HELP THEN +
           DO
           ISPEXEC DISPLAY PANEL(WCUT)
           EXIT
           END

       IF &STR(&HELP) = ? THEN +
           DO
           ISPEXEC DISPLAY PANEL(WCUT)
           EXIT
           END

  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  SET &WSVAR = WSVAR
  SET LCC = &LASTCC
  ISREDIT (ROW,COL) = CURSOR
  SET &WCROWE = &ROW
  SET &WCCOLE = &COL
  ISPEXEC VGET (WCCOLS WCROWS) PROFILE
  SET &WSLEN = &COL - &WCCOLS
  ISPEXEC VPUT (WCCOLE WCROWE) PROFILE
  ISREDIT LABEL &WCROWS = .A
  ISREDIT LABEL &WCROWE = .B
  ISREDIT C X'50'  '@'  .ZFIRST .ZLAST ALL
  SET &WS = &EVAL(&WCROWS)
  SET &WE = &EVAL(&WCROWE)
  SET &WT = &WE - &WS
  SET &WT = &WT +1
  SET &WL = &WCCOLE - &WCCOLS
  SET &WL = &WL +1
  ISPEXEC VPUT (WSLEN) PROFILE
CUTLOOP: +
   ISREDIT (CURLINE) = LINE &WS
    SET WSVAR&WS = &SUBSTR(&WCCOLS:&WCCOLE,&NRSTR(&CURLINE))
    ISPEXEC VPUT WSVAR&WS PROFILE
    SET &WS = &WS + 1
    IF &WS > &WE THEN GOTO FINISH:
    GOTO CUTLOOP
FINISH: +
  ISREDIT C '@'  '50'X  .ZFIRST .ZLAST ALL
ISPEXEC VGET WCZPF12 PROFILE
SET &ZPF12 = &WCZPF12
ISPEXEC VPUT ZPF12 PROFILE
   SET ZEDSMSG = &STR(WINDOW &WL COLS X &WT ROWS)
   SET ZEDLMSG = &STR(WINDOW &WL COLUMNS WIDE X &WT ROWS DEEP CUT FOR +
   PASTING WITH WPASTE MACRO)
   ISPEXEC SETMSG MSG(ISRZ001)
   ISREDIT CURSOR = &ROW &COL
   ISREDIT RESET
EXIT
