 /********************* MACDOC   ISPF EDIT MACRO *********************/
 /* MACDOC IS AN ISPF EDIT MACRO THAT PLACES A  COMMENT BOX IN THE   */
 /* CURRENT CLIST OR EDIT MACRO PROVIDING SPACE TO  DOCUMENT THE     */
 /* CLIST OR MACRO. THE MACRO NAME IS PLACED IN THE TOP LINE TO      */
 /* IDENTIFY THE MACRO, AND AGAIN IN THE FIRST LINE OF TEXT IN ORDER */
 /* TO START THE COMMENTARY. AT THE NEXT TO LAST LINE OF THE BOX A   */
 /* STARTER SYNTAX STATEMENT IS ALSO INCLUDED. MACDOC HAS A DEFAULT  */
 /* OF NINE LINES NOT INCLUDING TOP AND BOTTOM. MORE LINES CAN BE    */
 /* CREATED BY INCLUDING AN ARGUMENT WHEN ISSUING THE MACRO. THIS    */
 /* BOX WAS CREATED USING MACDOC. AN EDIT BEFORE OR AFTER INDICATOR  */
 /* WILL BE HONORED OTHERWISE THE BOX WILL WILL BE PLACED AT THE TOP */
 /* OF THE DATASET.                                                  */
 /*                                                                  */
 /* SYNTAX MACDOC  {NUMBER-LINES} OPTIONAL                           */
 /*                                                                  */
 /********************************************************************/

ISREDIT MACRO (LINES) NOPROCESS
       IF &STR(&LINES) = ? THEN +
           DO
   ISREDIT MACPEEK MACDOC
           EXIT
           END
    /* SET DEFAULT NUMBER OF LINES */
    IF &LINES =   THEN SET &LINES = 9
    SET &LINES = &LINES - 1
  SET &BLNKS = &STR(        )

  SET &STARS = &STR(*******)

  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  ISREDIT PROCESS DEST
    SET &DESTCC = &LASTCC
      IF &DESTCC = 0 THEN DO
       ISREDIT (WHERE) = RANGE_CMD
       ISREDIT LOCATE .ZDEST
       ISREDIT (PTR,COL) = CURSOR
      END
  IF &DESTCC = 8 THEN SET &PTR  = 0
  IF &WHERE > B  THEN SET &PTR  = &PTR   - 1
  ISREDIT (MEM) = MEMBER
  IF &LENGTH(&MEM) NE 8 THEN DO
  SET &MEM = &STR(&MEM&STR(        ))
  SET &MEM = &SUBSTR(1:8,&STR(&MEM))
  END
  SET &SYN = &LINES - 2
  SET &SYN1 =  &LINES - &SYN
  SET &TOP = +
   &STR(/&STARS&STARS&STARS )&STR(&MEM)+
  &STR( ISPF EDIT MACRO &STARS&STARS&STARS/)
  SET &BOTTOM = +
   &STR(/&STARS&STARS&STARS&STARS&STARS&STARS&STARS&STARS&STARS*****/)
  SET &LINE1  = +
   &STR(/* )&STR(&MEM)&STR(                            )+
  &STR(                             */)
  SET &FILL   = +
   &STR(/*                                   )+
  &STR(                               */)
  SET &SYNTAX = +
   &STR(/* SYNTAX: )&STR(&MEM)&STR(                  )+
  &STR(                               */)
  ISREDIT LINE_AFTER &PTR = '&STR( &TOP)'
  SET &PTR = &PTR + 1
  ISREDIT LINE_AFTER &PTR = '&STR( &LINE1)'
  SET &PTR = &PTR + 1
  DO WHILE &LINES > 0
  ISREDIT LINE_AFTER &PTR = '&STR( &FILL)'
  SET &PTR = &PTR + 1
  SET  &LINES = &LINES - 1
  IF &SYN1 = &LINES THEN DO
  ISREDIT LINE_AFTER &PTR = '&STR( &SYNTAX)'
  SET &PTR = &PTR + 1
  SET  &LINES = &LINES - 1
  END
  END
  ISREDIT LINE_AFTER &PTR = '&STR( &BOTTOM)'
  SET &PTR = &PTR + 1
  ISREDIT LINE_AFTER &PTR = '&STR(    )'
