//TECHBHW  JOB  (450,FMAIN),'BRIAN WESTERMAN',MSGCLASS=H,CLASS=A,
//   NOTIFY=TECHBHW,REGION=0M,TIME=1440
//*-----------------------------------------------*
//*   DOC: ASSEMBLE AND LINK USING ASMBHWL
//*-----------------------------------------------*
//ASMBHWL  EXEC ASMBHWL
//C.SYSLIB  DD  DSN=SYS1.MACLIB,DISP=SHR,DCB=BLKSIZE=32000
//          DD  DSN=SYS1.AMODGEN,DISP=SHR
//          DD  DISP=SHR,DSN=SYSX.CAI.R10.CAIMAC
//SYSIN    DD  *
         MACRO
&NFS     TODAY &LOC,&MONTHS=MONTHS,&DAYS=DAYS,&WORK=DWD,               º
               &PUNCT=YES,&WEEKDAY=,&TIME=,                            º
               &F15==F'15',&F6==F'6',&H2==H'2',&H12==H'12',            º
               &H10==H'10',&H13==H'13',&F5==F'5',&H34==H'34',          º
               &F7==F'7'
.*.
.*ºMACRO=TODAY      DATE=06/23/78       SOURCE=LDW
.*
.*  THIS MACRO WILL FORMAT THE DATE AND TIME, INCLUDING COMPUTING THE
.*       DAY OF THE WEEK.
.*.
         LCLC  &LQ,&IHBNX
&LQ      SETC  'L'''
&LQ      SETC  '&LQ&DAYS'
&IHBNX   SETC  'IHB&SYSNDX'
&NFS     L     R1,16                    -> CVT
         UNPK  &LOC+16(5),57(3,R1)      UNPK THE DATE
         OI    &LOC+20,C'0'             FIX THE SIGN
         MVC   &LOC+15(2),&LOC+16       MOVE THE YEAR OVER
         MVI   &LOC+17,C'.'             PUT IN THE DOT
         AIF   ('&PUNCT' NE 'YES').NOP1
         MVI   &LOC+14,C'('
         MVI   &LOC+21,C')'
.NOP1    TM    57(R1),X'01'             LEAP YEAR?
         BO    &IHBNX.N                 NO (ODD YEAR) - NOT LEAP
         TM    57(R1),X'12'             DIVISIBLE BY 4?
         BM    &IHBNX.N                 NO - NOT LEAP YEAR
         MVI   &MONTHS+7,29             FEB HAS 29 DAYS THIS YEAR
         SPACE 1
&IHBNX.N MVC   &WORK+6(2),58(R1)        COPY DDDF
         XC    &WORK.(6),&WORK          CLEAR BEGINNING
         CVB   R0,&WORK                 GET DAY OF YEAR IN BINARY
         LA    R15,&MONTHS              POINT TO TABLE
         SPACE 1
&IHBNX.L SH    R0,0(,R15)               SUB NUMBER OF DAYS THIS MONTH
         BNP   &IHBNX.H                 HAVE MONTH
         LA    R15,6(,R15)              NEXT TABLE ENTRY
         B     &IHBNX.L                 KEEP LOOKING
         SPACE 1
&IHBNX.H MVC   &LOC.(3),2(R15)          MOVE NAME OF MONTH
         AH    R0,0(,R15)               GET BACK CORRECT RESIDUAL
         CVD   R0,&WORK
         UNPK  &LOC+4(2),&WORK+6(2)     DAY OF MONTH
         OI    &LOC+5,C'0'              FIX SIGN
         MVC   &LOC+10(2),&LOC+15       MOVE IN YEAR
         AIF   ('&PUNCT' NE 'YES').NOP2
         MVC   &LOC+6(4),=C', 19'
.NOP2    AIF   ('&WEEKDAY' EQ '').NODAY
         ST    R0,&WORK                 SAVE DAY OF MONTH
.*
.* MONTH=MOD(MONTH+9,12)+1
.* IF (MONTH=11 × MONTH=12) YEAR=YEAR-1  ==> IF MONTH > 10 YEAR=YEAR-1
.* A=(13*MONTH-1)/5
.* B=YEAR/4
.* C=CENTURY/4
.* D=A+B+C+DAY+YEAR-2*CENTURY
.* Z=MOD(D,7)+1
.* WHERE Z=DAY OF WEEK, 1=SUNDAY,... 7=SATURDAY
.*
.* THIS MACRO ASSUMES 1900-1999... IE, CENTURY=19
         SPACE 2
         LA    R14,&MONTHS-6
         SR    R15,R14                  GET MONTH # * 6
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F6                  GET MONTH #
* MONTH=MOD(MONTH+9,12)+1
         SH    R15,&H2
         BP    *+8
         AH    R15,&H12
.***     XR    R14,R14                  CLEAR FOR DIVIDE
.***     LA    R15,9(,R15)              MONTH+9
.***     D     R14,=F'12'               DIVIDE BY 12
.***     LA    R14,1(,R14)
         SPACE 1
         IC    R0,57(,R1)               GET YEAR
         SRL   R0,4
         N     R0,&F15
         MH    R0,&H10
         IC    R1,57(,R1)
         N     R1,&F15
         AR    R1,R0                    = YEAR IN BINARY
         SPACE 1
         CH    R15,&H10                 MONTH = 11 OR 12 ?
         BNH   *+6                      NO - SKIP
         BCTR  R1,0                     -1 FROM YEAR
* A=(13*MONTH-1)/5
         MH    R15,&H13                 MONTH * 13
         BCTR  R15,0                    -1
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F5                  ALL OVER 5 (R5=A)
* B=YEAR/4
         LR    R0,R1                    COPY YEAR
         SRL   R0,2                     YEAR/4
         AR    R15,R0                   D=A+B SO FAR
* C=CENTURY/4 = 19/4 = 4                CENTURY*2 = 19*2 = 38
* +C-2*CENTURY = -34
         SH    R15,&H34                 D=A+B+C-2*CENT NOW
* D=A+B+C+DAY+YEAR-2*CENTURY
         A     R15,&WORK                ADD DAY
         AR    R15,R1                   ADD YEAR
* Z=MOD(D,7)+1
         XR    R14,R14                  CLEAR FOR DIVIDE
         D     R14,&F7                  GET MOD(D,7) IN R4=Z
         MH    R14,=Y(&LQ)              OFFSET INTO TABLE
         LA    R14,&DAYS.(R14)          POINT TO DAY IN TABLE
         MVC   &WEEKDAY.(&LQ),0(R14)    DAY TO OUTPUT LOCATION
*  SEEMS LIKE A LOT, BUT BETTER THAN AN SVC CALL, AND TRANSPORTABLE!!
.NODAY   AIF   ('&TIME' EQ '').NOTIME
         SPACE 2
*        TIME  DEC                      GET TIME
         TIME  DEC                      GET TIME
         ST    R0,&WORK                 PUT TIME INTO WORK AREA
         OI    &WORK+3,X'0F'            FIX THE SIGN
         MVC   &TIME.(9),=X'4021207A20207A2020'      ' DD:DD:DD'
         ED    &TIME.(9),&WORK
.NOTIME  SPACE 2
         MEND
         MACRO
&NFS     MONTHS  &DUMMY,&LC=YES
.*.
.*ºMACRO=MONTHS     DATE=04/19/77       SOURCE=LDW
.*
.*  THIS MACRO GENERATES A TABLE OF THE NUMBER OF DAYS IN EACH MONTH,
.*       (ASSUMING NOT LEAP YEAR), AND THE 3 CHAR MONTH NAMES.
.*.
.*  Updates:
.*    29Nov86 LDW - Support LC=YES and make it the default
.*
         AIF   ('&LC' EQ 'YES').LC
         AIF   ('&LC' EQ 'NO' ).UC
         MNOTE 4,'LC=&LC invalid, LC=NO assumed'
.UC      ANOP
&NFS     DC    H'31',C'JAN '            THIRTY DAYS
         DC    H'28',C'FEB '            HATH SEPTEMBER,
         DC    H'31',C'MAR '            APRIL, JUNE,
         DC    H'30',C'APR '            AND NOVEMBER.
         DC    H'31',C'MAY '            ALL THE REST
         DC    H'30',C'JUN '            HAVE THIRTY ONE,
         DC    H'31',C'JUL '            EXCEPT FEBRUARY,
         DC    H'31',C'AUG '            WHICH HAS ONLY 28.
         DC    H'30',C'SEP '
         DC    H'31',C'OCT '
         DC    H'30',C'NOV '
         DC    H'31',C'DEC '
         SPACE 2
         MEXIT
.LC      ANOP
&NFS     DC    H'31',C'Jan '            Thirty days
         DC    H'28',C'Feb '            Hath September,
         DC    H'31',C'Mar '            April, June,
         DC    H'30',C'Apr '            and November.
         DC    H'31',C'May '            All the rest
         DC    H'30',C'Jun '            Have thirty one,
         DC    H'31',C'Jul '            except February,
         DC    H'31',C'Aug '            Which has only 28.
         DC    H'30',C'Sep '
         DC    H'31',C'Oct '
         DC    H'30',C'Nov '
         DC    H'31',C'Dec '
         SPACE 2
         MEND
SYZYGYVR CSECT
*  THE PARM PASSED TO THIS PROGRAM MUST BE IN THE FORMAT OF
*  AAAAAA:BBBBBB,C  WHERE
*  AAAAAA = BEGINNING VOLSER
*  BBBBBB = ENDING VOLSER
*  C      = 'S' FOR TAPES INSIDE SILO
*         = 'E' FOR TAPES EXTERNAL TO SILO
*         = 'B' FOR ALL TAPES (WHETHER INSIDE OR OUTSIDE SILO)
ENTRY    TMMLINK ENTRY,BASE=(R11),SYSMOD=SYZYGYVR
         L     R2,0(,R1)    OBTAIN PARM LIST
         LA    R2,2(,R2)    GET THE FIRST ADDRESS
         MVC   PARMIN+0(15),0(R2) MOVE THE INPUT PARM
         MVC   PARMLINE+34(15),PARMIN
OPENTMC  TMMTMOPN TMC,NOTOPEN,INPUT
         DS    0H
         STM   14,1,12(13)        SAVE REGISTERS.
         OPEN  (SYSPRINT,(OUTPUT))
         B     DATESTUF
*        TM    SYSPRINT+48,DCBOFOPN    WAS THE OPEN SUCCESSFUL?
*        BO    DATESTUF                YES, CONTINUE
*        WTO   'SYZ0001E OPEN FOR SYSPRINT DATASET FAILED',ROUTCDE=11
*        B     ABEND1
NOTOPEN  DS    0H
         WTO   'SYZ0002E OPEN FOR TMC HAS FAILED',ROUTCDE=11
         B     ABEND1
DATESTUF TODAY INFOLINE+15,TIME=INFOLINE+46
         PUT   SYSPRINT,STARLINE
         PUT   SYSPRINT,PARMLINE
         PUT   SYSPRINT,STARLINE
         PUT   SYSPRINT,INFOLINE
         PUT   SYSPRINT,STARLINE
         PUT   SYSPRINT,STARLINE
POINTMC  TMMTMPNT TMC,(2)         POINT TO FIRST VOLSER OF SEQUENCE
GETTMC   TMMTMGET TMC,TMRECORD,EOD=RETURN,EODTMC=RETURN
         CLC   BEGINVOL,TMVOLSER
         BH    GETTMC             LOWER THAN BEGIN, GET NEXT RECORD
         CLC   ENDVOL,TMVOLSER
         BL    RETURN             HIGHER THAN END, THEN EXIT
         MVC   OUTVOL,TMVOLSER    GET VOLSER
         MVC DISPOSIT+35(25),=CL25'                         '
         CLI   TYPE2PRT,C'E'      EXTERNAL REPORT?
         BNE   CHECKB
         MVI   REPORT,C'E'        SAY SO
CHECKB   CLI   TYPE2PRT,C'B'      BOTH REPROTS?
         BNE   DONECHK
         MVI   REPORT,C'B'        SAY SO
DONECHK  DS    0H
         TM    TMFLAG1,TMDELET    IS TAPE IN DELETE STATUS ?
         BO    GETTMC             BYE - DON'T USE IT
         TM    TMFLAG1,TMSCRTCH   IS IT A SCRATCH TAPE?
         BNO   GETTMC             BYE - DON'T USE IT EITHER
         CLI   TMROBTY,X'02'      IN SILO?
         BNE   OUTSILO            NOPE, MUST BE OUTSIDE
         MVC   DISPOSIT,=CL11'IS IN SILO '
         B     DOREST
OUTSILO  DS    0H
         MVC   DISPOSIT,=CL11'IS EXTERNAL'
DOREST   CLC   TMOUTAR,=CL4'    ' OUT OF AREA ?
         BE    ONSITE
         MVC   DISPOSIT+35(20),=CL20'BUT IS OFFSITE CODE='
         MVC   DISPOSIT+55(4),TMOUTAR
ONSITE   CLI   REPORT,C'B'        PRINT EVERYTHING?
         BNE   CHKE               NO, TRY 'E'
         PUT   SYSPRINT,TMOUT     JUST WRITE WHAT WE WANT!!!!
         B     GETTMC             GET NEXT RECORD
CHKE     CLI   REPORT,C'E'        PRINT EXTERNAL ONLY?
         BNE   CHKS               NO, TRY 'S'
         CLI   TMROBTY,X'02'      IN SILO?
         BE    GETTMC             YES, JUST GET NEXT RECORD
PUTE     PUT   SYSPRINT,TMOUT     JUST WRITE WHAT WE WANT!!!!
         B     GETTMC             GET NEXT RECORD
CHKS     CLI   REPORT,C'S'        PRINT SILO ONLY?
         BNE   GETTMC             NO, TRY 'S'
         CLI   TMROBTY,X'02'      IN SILO?
         BNE   GETTMC             NO, JUST GET NEXT RECORD
PUTS     PUT   SYSPRINT,TMOUT     JUST WRITE WHAT WE WANT!!!!
         B     GETTMC             GET NEXT RECORD
RETURN   DS    0H
         TMMTMCLO TMC             CLOSE TMC
         SLR   R15,R15
         TMMLINK EXIT,RC=(15)
         LTORG  ,
ABEND1   DS    0H
         ABEND 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FPR0     EQU   0
FPR2     EQU   2
FPR4     EQU   4
FPR6     EQU   6
DWD      DS    D
PARMIN   DS    0CL15
BEGINVOL DC    CL6'XXXXXX'            START VOLUME
COLON    DC    CL1' '                 WILL BE A ":"
ENDVOL   DC    CL6'XXXXXX'            ENDING VOLUME
COMMA    DC    CL1' '                 WILL BE A ","
TYPE2PRT DC    CL1' '                 S=SILO, E=EXTERNAL, B=BOTH
         DC    CL20' '                FILLER
TMOUT    DS    0CL80
         DC    CL5' '
OUTVOL   DC    CL6'XXXXXX'            VOLSER
         DC    CL4'    '
DISPOSIT DC    CL11'XXXXXXXXXXX'      IN OR OUT OF SILO
         DC    CL4'    '
         DC    CL19'IS A SCRATCH VOLUME'
         DC    CL51' '                FILLER
PARMLINE DS    0CL80
         DC    CL5' '
 DC    CL55'********** INPUT PARAMETER = XXXXXXXXXXXXXXX **********'
         DC    CL20' '
STARLINE DS    0CL80
         DC    CL5' '
 DC    CL55'*******************************************************'
         DC    CL20' '
INFOLINE DS    0CL80
         DC    CL5'     '
 DC    CL55'*   DATE=                          TIME=              *'
         DC    CL20' '
REPORT   DC    CL1'S'    S,E,B
MONTHS   MONTHS
ALLIGN   DS    0H
TMC      TMMTMDCB DDNAME=TMC,LRECL=TMC,EODAD=RETURN,OUTPUT=NO
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FB,LRECL=80,    X
               BLKSIZE=80
         TMMLINK PATCH
         TMMTMREC DSECT=ALL
         END
//L.SYSLMOD DD  DSN=SYS2.TEST.LINKLIB(SYZYGYVR),DISP=SHR
