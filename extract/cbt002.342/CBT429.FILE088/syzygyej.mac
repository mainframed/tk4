*---------------------------------------------------------------------*
*                      S Y Z Y G Y E J                                *
*                                                                     *
*                                                                     *
*        THIS PROGRAM EDITS THE OUTPUT LISTING FROM TMSGRW AND MAKES  *
*        EJECT RECORDS FOR ALUADMIN TO REMOVE TAPES FROM THE SILO.    *
*        THE INPUT FROM TMSGRW MUST CONTAIN THE VOLSER AND THE ROBTY  *
*        FIELDS, ANY OTHER FIELDS ARE IGNORED                         *
*                                                                     *
*        BRIAN WESTERMAN,  SYZYGY INCORPORATED                        *
*                                                                     *
*        //EJECTTP  EXEC PGM=SYZYGYEJ,PARM=NNN  (NUMBER TO EJECT)     *
*        //STEPLIB  DD - PRIVATE LOADLIB -                            *
*        //TMSGRW   DD - INPUT TMS VOLSER REPORT -                    *
*        //EJECTCD  DD - FLAT FILE OF SELECTED TMS TAPES -            *
*        //SYSPRINT DD - OPTIONAL PRINTED REPORT OF FLAT FILE -       *
*                                                                     *
*        NOTES: INPUT FILE COMES IN SORTED BY VOLSER.                 *
*               IF OUTPUT FILE IS EMPTY, RETURN WITH RC=12            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         MACRO
&NAME    MODE   &AMODE=31
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*  SET ADDRESSING MODE.  EITHER 'MODE AMODE=31' OR 'MODE AMODE=24'  *
.*                                                                   *
.* NOTES: EXAMPLE - '    MODE AMODE=31  SET ADDREESSING MODE TO 31   *
.*                  ' -MORE CODE -                                   *
.*                  '    MODE AMODE=24  SET ADDREESSING MODE TO 24   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         LCLC   &I
         GBLA   &B
&I       SETC   '&SYSNDX'
         AIF    (&AMODE EQ 31).MOD31
&NAME    LA     15,RMOD&I
         BSM    0,15
RMOD&I   DS     0H
         AGO    .MEND
.MOD31   ANOP
&NAME    L      15,AMOD&I
         BSM    0,15
AMOD&I   DC     A(CON&I+X'80000000')
CON&I    DS     0H
.MEND    MEND
         SPACE
SYZYGYEJ AMODE 31
SYZYGYEJ RMODE 24
SYZYGYEJ CSECT
         SAVE (14,12),,STKETMS_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING STKETMS,R12
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(R13)
         LR    R13,R9
         SPACE
*---------------------------------------------------------------------*
*        HAVE TMS REPORT NOW, SO READ IN LISTING EDIT OUT HEADINGS,   *
*        ETC, ETC
*---------------------------------------------------------------------*
         SPACE
LETSGO   EQU   *
         OPEN  (TMSGRW,INPUT,EJECTCD,(OUTPUT),SYSPRINT,(OUTPUT))
         LA    R9,0                KEEP A TALLY OF STK RECORDS
         MODE  AMODE=24
         PUT   EJECTCD,EJREC       PUT INITIAL RECORD
         MODE  AMODE=31
         MVC   EJCMD,EJBLANK       CLEAR COMMAND PART OF RECORD
LOOP2    EQU   *
         MODE  AMODE=24
         GET   TMSGRW,TMSREC
         MODE  AMODE=31
         LA    R5,5                CHECK 5 NUMBERS
         LA    R6,TMSVOL           GET VOLSER
         CLI   0(R6),C'H'          IS FIRST DIGIT AN 'H'?
         BNE   LOOP2               NO, GET NEXT RECORD
         LA    R6,1(,R6)           BUMP POINTER
LOOP3    EQU   *
         TM    0(R6),X'F0'         IS IT NUMBERIC
         BNO   LOOP2
         LA    R6,1(,R6)           BUMP POINTER
         BCT   R5,LOOP3            CONTINUE CHECKING
         SPACE
         CLC   TMSROBTY(2),=C'02'  MAKE SURE IT'S IN THE SILO
         BE    CONT3A
         B     LOOP2               NOT IN SILO, GET NEXT RECORD
         SPACE
CONT3A   EQU   *
         MVC   VOLSER(6),TMSVOL    MOVE IN VOLUME SERIAL
         MVC   EJVOL(6),TMSVOL    MOVE IN VOLUME SERIAL
         MVC   CDATE(5),TMSCDATE   MOVE IN CDATE
         MVC   EXPDT(5),TMSEXPDT   MOVE IN EXPDT
         MVC   CJOBN(8),TMSCJOBN   MOVE IN CREATING JOBNAME
         MVC   DSN(44),TMSDSN      MOVE IN DSNAME
         MODE  AMODE=24
         PUT   SYSPRINT,TMSOREC    MIRROR THE RECORD
         PUT   EJECTCD,EJREC       WRITE THE EJECT RECORD
CONT3    EQU   *
         MODE  AMODE=31
         LA    R9,1(,R9)           KEEP TALLY
         B     LOOP2
EOJ2     MVC   EJVOL,=CL6'XXXXXX'  PUT DUMMY VOLSER IN
         MVC   EJCOMMA,EJCLOSE     PUT RIGHT PARENTH IN
         MVC   EJCONT,EJBANK       BLANK OUT REST OF THE COMMAND
         MODE  AMODE=24
         PUT   EJECTCD,EJREC       WRITE THE EJECT RECORD
         MODE  AMODE=31
EOJ      CLOSE (TMSGRW,,EJECTCD,,SYSPRINT)
         LTR   R9,R9               DID WE GET ANY RECORDS ??
         BNZ   ENDIT
         LA    R15,12              GET A 12
         ST    R15,RETCODE         STORE IT IN RETURN CODE VARIABLE
ENDIT    EQU   *
         L     R15,RETCODE
         L     R13,SAVE+4
         ST    R15,16(,R13)        PUT IN RETURN CODE
         LM    R14,R12,12(R13)     RESTORE THE REGS
         BR    R14
         SPACE
TMSGRW   DCB   DSORG=PS,MACRF=GM,DDNAME=TMSGRW,EODAD=EOJ2
         SPACE
EJECTCD  DCB   DSORG=PS,MACRF=PM,DDNAME=EJECTCD,RECFM=FBA,LRECL=133,   X
               BLKSIZE=3990
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,  X
               BLKSIZE=133
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA FOLLOWS.                                           *
*---------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
SAVE     DC    18F'0'
RETCODE  DC    F'0'                RETURN CODE
PRTSW    DC    C'N'
BLANKS   DC    CL6' '
         SPACE
TMSREC   DS    0CL133
         DC    CL03' '
TMSVOL   DC    CL06'XXXXXX'        04 - VOLSER
         DC    CL03' '
TMSROBTY DC    CL02'XX'            13 - ROBOT FIELD
         DC    CL06' '
TMSCDATE DC    CL08'XXXX/XXX'      21 - JULIAN CREATION DATE
         DC    CL05' '
TMSEXPDT DC    CL08'XXXX/XXX'      34 - JULIAN EXPDT
         DC    CL05' '
TMSCJOBN DC    CL08'XXXXXXXX'      47 - CREATING JOBNAME
         DC    CL03' '
TMSDSN   DC    CL44'     '         57 - JULIAN CREATION DATE
         DC    CL03' '
TMSSILO  DC    CL08'XXXXXXXX'      56 - IN SILO?
         DC    CL55' '             FILL OUT RECORD
         SPACE
TMSOREC  DS    0CL133
         DC    CL03' '
VOLSER   DC    CL06'XXXXXX'        04 - VOLSER
         DC    CL03' '
CDATE    DC    CL05'XXXXX'         13 - JULIAN CREATION DATE
         DC    CL03' '
EXPDT    DC    CL05'XXXXX'         20 - JULIAN EXPDT
         DC    CL03' '
CJOBN    DC    CL08'XXXXX'         29 - CREATING JOBNAME
         DC    CL03' '
DSN      DC    CL44'     '         56 - DSN
         DC    CL16' WILL BE EJECTED'
         DC    CL55' '             FILL OUT RECORD
         SPACE
EJREC    DS    0CL80
EJCMD    DC    CL14' EJECT VOLSER('
EJVOL    DC    CL06'XXXXXX'
EJCOMMA  DC    CL01','
EJCONT   DC    CL59'       +'
         SPACE
EJBLANK  DC    CL14'              '
EJCLOSE  DC    CL01')'
         END
