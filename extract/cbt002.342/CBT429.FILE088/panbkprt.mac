*          DATA SET PV1001104A AT LEVEL 003 AS OF 02/01/80
         TITLE 'PANVALET PROTECTION FILE DIRECTORY PRINTER'
PRTBKUP  CSECT
         SPACE
R0       EQU   0                   GENERAL
R1       EQU   1
R2       EQU   2                    PURPOSE
R3       EQU   3
R4       EQU   4                     REGISTER
R5       EQU   5
R6       EQU   6                      EQUATES
R7       EQU   7
LINECNT  EQU   8
SEARCHER EQU   9
R10      EQU   10
RA       EQU   10
R11      EQU   11
RB       EQU   11
R12      EQU   12
RC       EQU   12
R13      EQU   13
RD       EQU   13
R14      EQU   14
RE       EQU   14
R15      EQU   15
RF       EQU   15
         SPACE
         SAVE  (14,12)
         LR    RC,RF
         USING PRTBKUP,RC
         ST    RD,REGSAVE+4
         LR    R2,RD
         LA    RD,REGSAVE
         ST    RD,8(R2)
         SPACE
         WTO   'ABOUT TO OPEN DATASETS',ROUTCDE=11
         OPEN  (PRINT,OUTPUT)   OPEN PRINT FILE FOR DIRECTORY
         OPEN  (SYSPRINT,OUTPUT)   OPEN PRINT FILE FOR DIRECTORY
         LA    R2,SYSPRINT
         USING IHADCB,R2
         TM    DCBOFLGS,X'10'
         BZ    NOPRINT
         WTO   'OPENED SUCESSFULLY',ROUTCDE=11
         DROP  R2
         EJECT
         WTO   'BEGIN PUTS',ROUTCDE=11
         PUT   SYSPRINT,HELLO
         PUT   SYSPRINT,DDNAME
         PUT   SYSPRINT,BACKUP
         PUT   PRINT,HELLO
         PUT   PRINT,DDNAME
         PUT   PRINT,BACKUP
         WTO   'END PUTS',ROUTCDE=11
         XC    ACTION,ACTION       CLEAR PAM RETURN CODE
         CALL  POPEN,(ACTION,DDNAME,BACKUP),VL OPEN PANVALET PROT FILE
         CLC   ACTION,=F'0'
         BNE   OPENFAIL
         SPACE
         L     R1,16               R1 => CVT
         L     R1,0(,R1)           R1 => TCB ADDR LIST
         L     R1,4(,R1)           R1 => CURRENT TCB
         L     R3,8(,R1)           R3 => FIRST DEB IN CHAIN
CHKDEB   LA    R3,0(,R3)           CLEAR HIGH ORDER BYTE
         LTR   R3,R3               END OF DEB CHAIN?
         BZ    DDCLOSED             IF SO, DDNAME NOT OPEN
         L     R2,24(,R3)           ELSE R2 => DCB
         L     R4,12(,R1)          R4 => TIOT
         AH    R4,40(,R2)          R4 => DD ENTRY IN TIOT
         CLC   4(8,R4),DDNAME      IS THIS THE PANVALET PROT FILE DD?
         BE    *+4+4+4 >==========  IF SO, READ THE JFCB
         L     R3,4(R3)          V  ELSE R3 => NEXT DEB IN CHAIN
         B     CHKDEB            V  AND WE GO BACK TO CHECK IT
         L     R3,36(,R2) <=======
         LA    R4,JFCBEXIT
         ST    R4,36(,R2)
         RDJFCB ((2))
         ST    R3,36(,R2)
         SPACE
         LA    SEARCHER,BUILDPRT
         ZAP   PAGECNT,=P'0'
         BAL   R10,TOPHEAD
         EJECT
SRCHPAN  EQU   *
         SPACE
         XC    ACTION,ACTION       CLEAR ACTION WORD
         MVC   SUBSET,=CL27'SUBSET'
         MVC   COMMENT,=CL52'COMMENT'
         CALL  PSRCH,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL
         CLC   ACTION,=F'0'
         BNE   SRCHFAIL
         CLC   DIRENTRY(2),=C'$*'
         BE    CLOSEPAN
         BR    SEARCHER
         SPACE
BUILDPRT EQU   *
         SPACE
         MVC   PNAME,DNAME
         MVC   PFORMAT,DFORMAT
         CLC   DDATEM,DDATEA
         BE    *+4+6 >============
         MVC   PDATEM,DDATEM     V
         MVC   PDATEA,DDATEA <====
         BAL   R11,PACKUP
         BAL   R11,EDITPACK
         SPACE
         LA    R3,LANGTBL
         LA    R4,LLANGTBL
LANGLOOP CLC   DTYPE,0(R3)
         BE    MOVELANG
         LA    R3,14(R3)
         BCT   R4,LANGLOOP
         LA    R3,QUESTION-5
MOVELANG MVC   PTYPE,5(R3)
         EJECT
         LA    R3,ACTTBL
         LA    R4,LACTTBL
ACTLOOP  CLC   DLASTACT+1(3),0(R3)
         BE    MOVEACT
         LA    R3,12(R3)
         BCT   R4,ACTLOOP
         CLI   DLASTACT+1,C' '
         BE    STATCHK
         LA    R3,QUESTION-3
MOVEACT  MVC   PLASTACT,3(R3)
         MVC   PPRODACT,DLASTACT
         SPACE
STATCHK  LA    R3,STATTBL
         LA    R4,LSTATTBL
         LA    R5,PSTATUS
         LA    R6,3
         LA    R7,DSTATUS
STATLOOP CLC   0(1,R7),0(R3)
         BE    MOVESTAT
         LA    R3,5(R3)
         BCT   R4,STATLOOP
MOVESTAT MVC   0(5,R5),0(R3)
         LA    R3,5(R3)
         LA    R5,5(R5)
         LA    R7,1(R7)
         BCT   R6,STATLOOP
         SPACE
         BAL   R11,LINEOUT
         SPACE
         CLI   COMMENT,C'*'
         BNE   *+4+6+4 >==========
         MVC   PCOMMENT,COMMENT  V
         SPACE 1                 V
         BAL   R11,LINEOUT       V
         SPACE 1                 V
         CLI   SUBENTRY,C'.' <====
         BE    PRTSUB
         SPACE
         B     SRCHPAN
         EJECT
CLOSEPAN EQU   *
         SPACE
         XC    ACTION,ACTION       CLEAR ACTION WORD
         CALL  PCLOSE,(ACTION),VL
         CLC   ACTION,=F'0'
         BNE   CLOSFAIL
         SPACE
CLOSEOS  EQU   *
         SPACE
         CLOSE (SYSPRINT)
         CLOSE (PRINT)
         SPACE
RETURN   EQU   *
         SPACE
NORMEND  DS    0H                       NORMAL END ROUTINE
         L     R15,ACTION          LOAD RETURN CODE
         L     RD,4(RD)                 LOAD RECURSIVE SAVE REG
         RETURN (14,12)
         SPACE
NOPRINT  ABEND 301                 SYSPRINT DD STMT MISSING
         SPACE
*** THIS SHOULD NEVER HAPPEN ***
DDCLOSED ABEND 302,DUMP            PAMS DID NOT OPEN PROT FILE
         EJECT
PACKUP   EQU   *
         SPACE
         LH    R3,DVERSION
         CVD   R3,DBLEWORD
         PACK  DLEVEL,DLEVEL
         PACK  DUSER,DUSER
         PACK  DSECURE,DSECURE
         PACK  DBLOCKS,DBLOCKS
         PACK  DSTMTS,DSTMTS
         PACK  DBYTES,DBYTES
         PACK  DSUBSET,DSUBSET
         BR    R11
         SPACE
EDITPACK EQU   *
         SPACE
         MVC   PVERSION,=X'402020202020'
         ED    PVERSION,DBLEWORD+5
         MVC   PLEVEL,=X'40202020'
         ED    PLEVEL,DLEVEL+1
         MVC   PUSER,=X'402020202020'
         ED    PUSER,DUSER+1
         MVC   PSECURE,=X'4020'
         ED    PSECURE,DSECURE
         MVC   PBLOCKS,=X'402020202020'
         ED    PBLOCKS,DBLOCKS+2
         MVC   PSTMTS,=X'40202020202020202020'
         ED    PSTMTS,DSTMTS+3
         MVC   PBYTES,=X'40202020'
         ED    PBYTES,DBYTES
         MVC   PSUBSET,=X'402020202020'
         ED    PSUBSET,DSUBSET+1
         BR    R11
         SPACE
PRTSUB   EQU   *
         SPACE
         LA    SEARCHER,PRTSUB+4+6+4
         ZAP   DBLEWORD,DSUBSET
         CVB   R3,DBLEWORD
         MVC   PSUBNAME,SUBNAME
         MVC   PSUBDATE,SUBDATE
         PACK  SUBSTMT,SUBSTMT
         MVC   PSUBSTMT,=X'402020202020'
         ED    PSUBSTMT,SUBSTMT+2
         BAL   R11,LINEOUT
         BCT   R3,SRCHPAN
         LA    SEARCHER,BUILDPRT
         B     SRCHPAN
         EJECT
TOPHEAD  EQU   *
         SPACE
         ST    R11,SAVER
         AP    PAGECNT,=P'1'
         MVI   CARRCNTL,C'1'
         MVC   PRTAREA(L'HA),HA
         MVC   PRTAREA+112(6),=X'402020202020'
         ED    PRTAREA+112(6),PAGECNT
         MVC   PRTAREA+109(4),=C'PAGE'
         BAL   R11,LINEOUT
         MVC   PRTAREA(5),=C'DSN ='
         MVC   PRTAREA+6(44),DSNAME
         MVC   PRTAREA+51(8),=C'VOLSER ='
         MVC   PRTAREA+60(6),VOLSER
         BAL   R11,LINEOUT
         MVI   CARRCNTL,C'0'
         MVC   PRTAREA+120-L'H2(L'H2),H2
         BAL   R11,LINEOUT
         MVC   PRTAREA(120-L'H2),H1
         MVC   PRTAREA+120-L'H3(L'H3),H3
         BAL   R11,LINEOUT
         MVI   CARRCNTL,C'0'
         LA    LINECNT,LINES-9
         L     R11,SAVER
         BR    R10
         SPACE
BOTHEAD  EQU   *
         SPACE
         ST    R11,SAVER
         MVI   CARRCNTL,C'0'
         MVC   PRTAREA,H1
         BAL   R11,LINEOUT
         MVC   PRTAREA+120-L'H3(L'H3),H3
         BAL   R11,LINEOUT
         L     R11,SAVER
         BR    R10
         SPACE
LINEOUT  EQU   *
         SPACE
         PUT   SYSPRINT,PRINTOUT
         MVC   PRINTOUT,BLANK
         BCTR  LINECNT,R11
         BAL   R10,BOTHEAD
         BAL   R10,TOPHEAD
         BR    R11
         EJECT
OPENFAIL EQU   *
         SPACE
         MVC   HERR+39(6),=CL6'OPEN.'
         MVI   CARRCNTL,C'1'
         BAL   R10,PRTFAIL
         B     CLOSEOS
         SPACE
SRCHFAIL EQU   *
         SPACE
         MVC   HERR+39(6),=CL6'SRCH.'
         MVI   CARRCNTL,C'1'
         BAL   R10,PRTFAIL
         B     CLOSEPAN
         SPACE
CLOSFAIL EQU   *
         SPACE
         MVC   HERR+39(6),=C'CLOSE.'
         BAL   R10,PRTFAIL
         B     CLOSEOS
         SPACE
PRTFAIL  EQU   *
         SPACE
         LA    LINECNT,3(LINECNT)
         MVI   PRTAREA,C'*'
         MVC   PRTAREA+1(46),PRTAREA
         BAL   R11,LINEOUT
         MVC   PRTAREA(47),HERR
         L     R3,ACTION
         CVD   R3,DBLEWORD
         UNPK  PRTAREA+19(3),DBLEWORD+6(2)
         OI    PRTAREA+21,X'F0'
         BAL   R11,LINEOUT
         MVI   PRTAREA,C'*'
         MVC   PRTAREA+1(46),PRTAREA
         BAL   R11,LINEOUT
         BR    R10
         EJECT
* PANVALET ACCESS METHOD PARAMETERS
         SPACE
ACTION   DC    F'0'  RETURN CODE FRON PAN, SHOULD BE SET TO ZERO
REGSAVE  DS    18F
         SPACE
* POPEN PARAMETERS
         SPACE
DDNAME   DC    CL8'PANDD3'         DDNAME OF FILE TO BE OPENED
BACKUP   DC    CL8'BACKUP'  MUST BE CL8'BACKUP' IF PROTECTION FILE
HELLO    DC    CL50'HELLO THERE PRINT DATASET'
         SPACE
* PSRCH PARAMETERS
         SPACE
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    ZL3                 LEVEL NUMBER
DUSER    DS    ZL4                 USER CODE
DSECURE  DS    ZL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    ZL5                 NO. OF BLOCKS
DSTMTS   DS    ZL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD.
DBYTES   DS    ZL2                 NO. BYTES PER STATMENT
DSUBSET  DS    ZL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
DFORMAT  DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
NAME1    DC    CL22'NO-ENTRY'      SEARCH NAME1
NAME2    DC    CL11'NO-ENTRY'      SEARCH NAME2
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
SUBSET   DS    CL27                NO SUBSETS WANTED, ELSE='SUBSET'
         ORG   SUBSET
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    ZL5                 # OF STMTS IN SUBSET
         SPACE
         EJECT
LINES    EQU   60                  SET VALUE FOR LINES PER PAGE
         SPACE
BLANK    DC    C' '
PRINTOUT DS    0CL121
CARRCNTL DC    C' '
PRTAREA  DC    CL120' '
         ORG   PRTAREA
PNAME    DS    CL10
         DS    CL1
PLEVEL   DS    CL4
PVERSION DS    CL6
PUSER    DS    CL6
         DS    CL1
PSECURE  DS    CL2
         DS    CL1
PTYPE    DS    CL9
         DS    CL1
PFORMAT  DS    CL1
         DS    CL1
PSTATUS  DS    3CL5
         DS    CL1
PDATEM   DS    CL8
         DS    CL1
PDATEA   DS    CL8
         DS    CL1
PBLOCKS  DS    CL6
         DS    CL2
PSTMTS   DS    CL10
         DS    CL1
PPRODACT DS    CL1
PLASTACT DS    CL9
         ORG   *-1
PBYTES   DS    CL4
         DS    CL2
PSUBSET  DS    CL6
         ORG   PRTAREA
PCOMMENT DS    CL52
         ORG   PRTAREA
PSUBNAME DS    CL11
         DS    CL1
PSUBDATE DS    CL8
         DS    CL1
PSUBSTMT DS    CL6
         ORG
         SPACE
HA       DC    C'PANVALET PROTECTION FILE DIRECTORY REPORT'
H1       DC    CL12'NAME',CL23'LVL VERSN  USER',CL13'TYPE',CL11'STATUS'
H2       DC    CL61'  DATE     DATE   NO OF     NO OF      LAST   AVG  X
                 NO OF'
H3       DC    CL61'MAINTNED ACCESSED BLOCKS  STATEMENTS  ACTION BYTES X
                SUBSETS'
HERR     DC    CL47'* PAM ACTION CODE ''   '' RETURNED FROM P       *'
         EJECT
LANGTBL  DC    C'AUTOC',C'AUTOCODER'
         DC    C'ASMB ',C'ASSEMBLER'
         DC    C'COBOL',C'COBOL    '
         DC    C'ANSCB',C'ANSCOBOL '
         DC    C'COB72',C'COBOL-72 '
         DC    C'FORT ',C'FORTRAN  '
         DC    C'PL/1 ',C'PL/1     '
         DC    C'RPG  ',C'RPG      '
         DC    C'OBJCT',C'OBJECT   '
         DC    C'JCL  ',C'JCL      '
         DC    C'DATA ',C'DATA     '
         DC    C'OTHER',C'OTHER    '
         DC    C'UNSPC',C'UNSPECIFD'
         DC    C'USER1',C'USER180  '
         DC    C'USER2',C'USER780  '
LLANGTBL EQU   (*-LANGTBL)/14
         SPACE
ACTTBL   DC    C'ADD',C'ADDED    '
         DC    C'COP',C'COPIED   '
         DC    C'UPD',C'UPDATED  '
         DC    C'REN',C'RENAMED  '
         DC    C'STA',C'STATUS   '
         DC    C'LEV',C'LEVEL    '
         DC    C'USE',C'USER     '
         DC    C'FOR',C'FORMAT   '
         DC    C'FMT',C'FORMAT   '
         DC    C'REP',C'REPLACED '
         DC    C'RES',C'RESTORED '
         DC    C'ALL',C'ALLOCATED'
         DC    C'ATT',C'ATTACHED '
         DC    C'DET',C'DETACHED '
         DC    C'COM',C'COMMENT  '
         DC    C'SEQ',C'RESEQ    '
LACTTBL  EQU   (*-ACTTBL)/12
         SPACE
STATTBL  DC    C'TEST PROD '
         DC    C'ACTV INAC '
         DC    C'ENABLDSABL'
LSTATTBL EQU   (*-STATTBL)/5
         EJECT
PAGECNT  DS    PL3
QUESTION DC    9C'?'
DBLEWORD DS    D
SAVER    DS    F
DD3JFCB  DS    CL176
         ORG   DD3JFCB+0
DSNAME   DS    CL44
         ORG   DD3JFCB+118
VOLSER   DS    CL6
         ORG
         SPACE
         LTORG
         SPACE
         PRINT NOGEN
         SPACE
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=121,BLKSIZE=1210
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=121,BLKSIZE=1210
         SPACE
         DS    0F
JFCBEXIT DC    XL1'87'
         DC    AL3(DD3JFCB)
         SPACE
         DCBD  DSORG=(PS)
         END
