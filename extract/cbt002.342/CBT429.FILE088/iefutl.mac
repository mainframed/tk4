IEFUTL   TITLE 'ALCATEL - USER TIME LIMIT EXIT'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  USER TIME LIMIT EXIT                *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW JOBS TO BE CANCELLLED IF JOB (STEP) TIME       *
*                   LIMIT EXCEEDED.                                   *
*                ALLOW TSO SESSIONS TO HAVE ONE MORE MINUTE OF CPU    *
*                   TIME IF JOB (STEP) TIME LIMIT EXCEEDED.           *
*                ALLOW JOBS TO HAVE 15 MORE MINUTES OF WAIT TIME      *
*                   IF WAIT TIME LIMIT EXCEEDED.                      *
*                ALLOW TSO SESSIONS TO BE EXTENDED ONE TEN MINUTE     *
*                   PERIOD.                                           *
*                                                                     *
*                OPERATION =                                          *
*                   ANALYZE SMF COMMON EXIT PARAMETER AREA.           *
*                   IF WAIT TIME LIMIT EXCEEDED SEE IF JOB.  NO,      *
*                      EXTEND THE TSO SESSION.  IF JOB NOTIFY         *
*                      OPERATOR THAT ONE 15 MINUTE EXTENSION WILL     *
*                      BE GRANTED.  RETURN TO CALLER WITH A RETURN    *
*                      CODE OF 8 IN REGISTER 15 AND THE NUMBER        *
*                      OF SECONDS (900) OF WAIT TIME EXTENSION IN     *
*                      REGISTER 1.  INCREMENT THE USER COMMUNICA-     *
*                      TION FIELD TO INDICATE A WAIT TIME EXTENSION.  *
*                   IF JOB OR STEP TIME LIMIT IS EXCEEDED SEE IF      *
*                      TSO SESSION.  NO, CANCEL THE JOB (AS ABOVE).   *
*                      YES, SEE IF TIME HAS ALREADY BEEN INCREMENTED  *
*                      ONCE.  YES, EXTEND THE TSO SESSION (AS ABOVE). *
*                      NO, INFORM THE USER THAT THE CPU TIME LIMIT    *
*                      HAS BEEN EXCEEDED AND A 1 MINUTE EXTENSION     *
*                      IS BEING GRANTED.  RETURN TO CALLER WITH A     *
*                      RETURN CODE OF 8 IN REGISTER 15 AND THE        *
*                      NUMBER OF SECONDS (60) OF CPU TIME EXTENSION   *
*                      IN REGISTER 1.  INCREMENT THE USER COMMUNICA-  *
*                      TION FIELD TO INDICATE A CPU TIME EXTENSION.   *
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE  *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 2  = WORK REGISTERS                *
*                   REGISTERS 3 TO 9  = UNUSED                        *
*                   REGISTER  10      = ADDRESS OF GETMAINED AREA     *
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA           *
*                   REGISTER  12      = ADDRESSABILITY TO IEFUTL      *
*                                       CSECT                         *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                  'ZAP*'S)                                           *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = ABOUT 600 BYTES                        *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                USER COMMUNICATION FIELD - EXTENSION COUNT           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, TPUT                                     *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THE MESSAGES ISSUED BY THE PROBLEM ARE FOUND AT      *
*                   THE LABELS MESSAGEC, MESSAGEW, AND WTOC.          *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
*        CHANGED TO CANCEL CLASS=X                                 WCH1
***********************************************************************
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL.IPO.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
*
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,D0(R1)              LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R0,FULL4                SEE IF JOB/STEP TIME EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
*                                      TIME EXCEEDED
*
***********************************************************************
*                                                                     *
*        CPU TIME EXCEEDED - ONLY ALLOW TSO SESSION A 5  MINUTE       *
*           EXTENSION                                                 *
*                                                                     *
***********************************************************************
*
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   CHKJOB                  NO.. GO SEE IF TEST JOB
*
         CLI   SMFUSERC,ONE            SEE IF CPU TIME HAS NOT YET BEEN
*                                      EXTENDED ONCE
         BE    CANCEL                  YES...GO CANCEL TSO SESSION -
*                                      ALREADY EXTENDED ONCE
         TPUT  MESSAGEC,L'MESSAGEC     PUT OUT MESSAGE TO TERMINAL
         MVI   SMFUSERC,ONE            INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,FULL300              LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
CHKJOB   DS    0H
         CLI   SMFJOBN,C'X'        USERID FORMAT?                 MHL1
         BE    CANCEL              YES, ASSUME TEST JOB           MHL1
         CLI   SMFJOBN+6,C'T'
         BE    CANCEL
         CLI   SMFJCLAS,C'A'
         BE    CANCEL
         CLI   SMFJCLAS,C'B'
         BE    CANCEL
         CLI   SMFJCLAS,C'X'                                       WCH1
         BE    CANCEL                                              WCH1
         CLI   SMFJCLAS,C'N'                                       WCH1
         BE    CANCEL                                              WCH1
         CLI   SMFUSERC,ONE
         BE    NOWTOC
         GETMAIN R,LV=JOBMSGL,SP=241
         LR    R10,R1                  SAVE ADDRESS OF GETMAINED AREA
         MVC   D0(JOBMSGL,R10),JOBMSG
         MVC   44(L'SMFJOBN,R10),SMFJOBN
         SVC   WTOSVC                  ISSUE WTO SVC
         MVC   D0(JOBMSGL,R10),JOBMSG2
         LR    R1,R10
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=JOBMSGL,A=(R10),SP=241
*                                      FREEMAIN AREA FOR WTO DATA
NOWTOC   MVI   SMFUSERC,ONE            INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,FULL600              LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*   WAIT TIME EXCEEDED                                                *
*        - ALLOW TSO A 10 MINUTE EXTENSION                            *
*        - MODIFIED TO 5 MINUTE EXTENSION 1/17/84    CXV              *
*        - ALLOW JOB A 10 MINUTE EXTENSION AND NOTIFY OPERATOR        *
*                                                                     *
***********************************************************************
*
WAITTIME DS    0H
         CLI   SMFUSERW,ONE            SEE IF WAIT TIME HAS NOT YET
*                                      BEEN EXTENDED ONCE
         BE    CANCEL                  YES...GO CANCEL JOB - WAIT TIME
*                                      ALREADY EXTENDED ONCE
*
***********************************************************************
*                                                                     *
*        ROUTCDE=2 WAS USED SO THAT ENQ RET=TEST WAS UNNECESSARY      *
*        ENQ RET=TEST IS NECESSARY FOR ROUTCDE=11 (WTP) WHETHER       *
*           OR NOT THAT ROUTING OCCURS BY DEFAULT                     *
*        WTP CAUSES AN ENQ OF THE FOLLOWING FORM:                     *
*           MAJORNAME: SYSSJWTP                                       *
*           MINORNAME: RPL + ASID (LENGTH OF 6)                       *
*              RPL IS THE 4 BYTE REQUEST PARAMETER LIST POINTER       *
*                 LOCATED AT OFFSET JSCBSMLR (X'128') IN THE JSCB     *
*              ASID IS THE 2 BYTE ADDRESS SPACE IDENTIFIER            *
*                 LOCATED AT OFFSET ASCBASID (X'24') IN THE ASCB      *
*                                                                     *
***********************************************************************
*
*                                      EXTEND CPU TIME IN SECONDS  CXV
*                                      INTO REGISTER 1             CXV
         TM    SMFOPT,SMFTSO           IS IT A TSO SESSION ?
         BO    WAITTSO                   YES, DON'T ISSUE WTO     MHL1
         GETMAIN R,LV=WTOL,SP=241      GETMAIN AREA FOR WTO DATA
         LR    R10,R1                  SAVE ADDRESS OF GETMAINED AREA
         MVC   D0(WTOL,R1),WTOC        MOVE WTOC CONSTANT INTO WORKAREA
         MVC   WTOL-D12(L'SMFJOBN,R1),SMFJOBN  MOVE JOBNAME INTO WTO
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=WTOL,A=(R10),SP=241
*                                      EXTEND CPU TIME IN SECONDS  CXV
*                                      INTO REGISTER 1             CXV
         L     R1,WAIT600              LOAD TIME EXTENS IN SECONDS CXV
*                                      FREEMAIN AREA FOR WTO DATA
NOWTO    MVI   SMFUSERW,ONE            INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*        L     R1,WAIT600              LOAD TIME EXTENS IN SECONDS CXV
         TM    SMFOPT,SMFTSO
         BO    RETURN
         MVI   SMFUSERW,D0
         B     RETURN                  GO RETURN TO CALLER
         SPACE 1                                                  MHL1
* TSO USERS GET ONE 5 MINUTE EXTENSION (AT SOME FUTURE DATE)      MHL1
WAITTSO  DS    0H                                                 MHL1
*        L     R1,WAIT300              LOAD TIME EXTENS IN SECONDS CXV
         L     R1,WAIT600              LOAD TIME EXTENS IN SECONDS CXV
*        B     NOWTO
         CLC   =C'XOP',SMFJOBN         OPERATIONS?                MHL1
         BE    WAITSP                  YES, CHECK TIME OF DAY     MHL1
         CLC   =C'XSPR',SMFJOBN        SYSTEMS?                   MHL1
         BNE   NOWTO                   NO, WAIT 1 TIME            MHL1
* IF SYSTEMS, DON'T LOGOFF FROM 07:30 TO 17:30                    MHL1
WAITSP   DS    0H                                                 MHL1
         TIME  DEC                     GET TIME                   MHL1
*        L     R1,WAIT300              LOAD TIME EXTENS IN SECONDS CXV
         L     R1,WAIT600              LOAD TIME EXTENS IN SECONDS CXV
*        C     R0,TIME0730             SEE IF BEFORE 07:30        MHL1
*        BL    NOWTO                   EARLY, TIMEOUT             MHL1
*        C     R0,TIME1730             SEE IF AFTER 17:30         MHL1
*        BH    NOWTO                   LATE, TIMEOUT              MHL1
         L     R2,FULL8                RETCODE = 8, EXTEND TIME   MHL1
         B     RETURN                  RETURN TO MVS              MHL1
         EJECT
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         SLR   R2,R2                   INDICATE RETURN CODE OF ZERO -
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         L     R14,D12(,R13)           LOAD ADDRESS FOR RETURN
         LR    R15,R2                  LOAD RETURN CODE FROM REGISTER 2
         LM    R2,R12,D28(R13)         RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
         SPACE 5
**************************** CONSTANTS ********************************
FULL4    DC    F'4'
FULL8    DC    F'8'
FULL300  DC    F'300'
FULL600  DC    F'600'
WAIT600  DC    F'600'
WAIT300  DC    F'300'
TIME0730 DC    X'07300000'             07:30 IN HHMMSSTH          MHL1
TIME1730 DC    X'17300000'             17:30 IN HHMMSSTH          MHL1
         LTORG ,                                                  MHL1
         EJECT
****************************** MESSAGES *******************************
         SPACE 2
MESSAGEC DC    C'CPU TIME LIMIT HAS BEEN EXCEEDED. A SINGLE FIVE MINUTEX
                EXTENSION WILL BE GRANTED. PLEASE LOGOFF SHORTLY'
*
JOBMSG   WTO   'CPU TIME LIMIT HAS BEEN EXCEEDED BY JOB XXXXXXXX. PLEASX
               E CORRECT THE TIME PARAMETER IN THE JCL.',              X
               ROUTCDE=(2),DESC=(6),MF=L
JOBMSGL  EQU   *-JOBMSG
JOBMSG2  WTO   'THIS IS ONLY A WARNING NOW. CHECK THE TIME PARAMETER INX
                BOTH THE JOB AND THE EXEC CARDS.       ',              X
               ROUTCDE=(2),DESC=(6),MF=L
*
WTOC     WTO   'WAIT TIME LIMIT HAS BEEN EXCEEDED. A SINGLE 10 MINUTE EX
               XTENSION WILL BE GRANTED FOR JOB 12345678',             X
               ROUTCDE=(2),DESC=(6),MF=L
WTOL     EQU   *-WTOC
*
         SPACE 2
***************************** PATCH AREA ******************************
         DS    0F
PATCH    DC    8CL4'ZAP*'
         EJECT
************************ DSECTS (MAPPING MACROS) **********************
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFJCLAS DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 5
********************************* EQUATES *****************************
D0       EQU   0
D12      EQU   12
D28      EQU   28
ONE      EQU   1
WTOSVC   EQU   35
         SPACE 5
***************************** REGISTER EQUATES ************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   IEFUTL
