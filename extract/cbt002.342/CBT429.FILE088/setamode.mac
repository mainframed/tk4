         MACRO
&LABEL   SETAMODE  &AMOD,&R=15,&SAVE=,&TESTCVT=NO
         GBLC  &SYSSPLV
         LCLC  &TARGET,&REG
         AIF   ('&AMOD' EQ '31' OR '&AMOD' EQ '24').AMODOK
         AIF   ('&AMOD' EQ 'ANY' OR '&AMOD' EQ 'TEST').AMODOK
         MNOTE 12,'** INVALID AMODE, &AMOD'
         MEXIT
.AMODOK  AIF   ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         SPLEVEL TEST
         AIF   ('&SYSSPLV' NE '1').YESXA1
         MEXIT
.YESXA1  ANOP
&TARGET  SETC  'AMOD&SYSNDX'
         AIF   ('&TESTCVT' EQ 'NO' OR '&TESTCVT' EQ 'N').TSTDONE
         AIF   ('&TESTCVT' EQ 'YES' OR '&TESTCVT' EQ 'Y').TSTCVT
         MNOTE 8,'** INVALID VALUE, TESTCVT=&TESTCVT, NO ASSUMED'
         AGO   .TSTDONE
.TSTCVT  ANOP
         L     &R,16
         TM    CVTMVSE-CVT(&R),CVTMVSE   XA MODE?
         BZ    &TARGET                   BRANCH IF NO.
.TSTDONE ANOP
         AIF   ('&SAVE' EQ '').NOSAVE
         AIF   ('&SAVE'(1,1) EQ '(').REGSAVE
         LA    &R,*+6
         BASSM &R,&R         GET CURRENT AMODE
         LTR   &R,&R
         BNM   *+12          BRANCH IF NOT AMODE=31
         OI    &SAVE,X'80'   ELSE SET AMODE BIT
         B     *+8
         NI    &SAVE,X'7F'   RESET AMODE BIT
         AGO   .NOSAVE
.REGSAVE ANOP
         AIF   (K'&SAVE LT 3).SAVEBAD
         AIF   ('&SAVE'(K'&SAVE,1) NE ')').SAVEBAD
&REG     SETC  '&SAVE'(2,K'&SAVE-2)
         LA    &R,*+6
         BASSM &REG,&R       GET CURRENT AMODE
         N     &REG,=A(X'80000000')  SAVE ONLY THE AMODE BIT
         AGO   .NOSAVE
.SAVEBAD MNOTE 8,'** INVALID AMODE SAVE REGISTER, &SAVE, IGNORED **'
         MEXIT
.NOSAVE  ANOP
         AIF   ('&AMOD' EQ 'ANY' OR '&AMOD' EQ 'TEST').EXIT
         LA    &R,&TARGET
         AIF   ('&AMOD' EQ '31').ORIT
         N     &R,=A(X'7FFFFFFF')
         AGO   .REJOIN
.ORIT    ANOP
         O     &R,=A(X'80000000')
.REJOIN  ANOP
         BSM   0,&R
.EXIT    ANOP
&TARGET  DS    0H
         MEND
