EX10     TITLE '1.3.4 JES2 EXIT 10 - SEND JOB END MSG TO WYLBUR'
***********************************************************************
*                                                                     *
*        JES2 EXIT 10. $WTO MESSAGE PROCESSOR                         *
*                                                                     *
***********************************************************************
         SPACE 3
         PRINT OFF,NOGEN          TURN OFF FOR COPY $HASPGBLS
         COPY  $HASPGBL
         PRINT ON,NOGEN
EXIT010  $MODULE SYSP=(NOGEN,NOGEN,NODATA,NOGEN,NOGEN),                *
               $HASPEQU,$KIT,$MIT,$MITETBL,$MODMAP,                    *
               $TAB,$SJB,$PCE,$BUFFER,$JQE,$JOE,$JOT,$QSE,             *
               $JCT,$PDDB,$IOT,$CAT,$SCAT,$HCT,$LRC,$CMB,              *
               $RDRWORK,$EXITPL,$ERA,$DCT
EXIT010  CSECT                    RESUME CSECT
         SPACE 1
         PRINT ON,GEN
         TITLE '1.3.4 JES2 EXIT 10 - SEND JOB END MSG TO WYLBUR'
***********************************************************************
*                                                                     *
*                                                                     *
*        REGISTERS ON ENTRY ARE:                                      *
*                                                                     *
*                                                                     *
*        REG. 1:    --> ADDRESS OF THE CMB                            *
*                                                                     *
*        REG. 2-9-----NOT SPECIFIED                                   *
*                                                                     *
*        REG. 10----==> JCT                                           *
*                                                                     *
*        REG. 11----==> HCT                                           *
*                                                                     *
*        REG. 12------NOT SPECIFIED                                   *
*                                                                     *
*        REG. 13----==> HASPRDR PCE                                   *
*                                                                     *
*        REG. 14------CONTAINS RETURN ADDRESS                         *
*                                                                     *
*        REG. 15------CONTAINS ENTRY POINT ADDRESS                    *
*                                                                     *
***********************************************************************
         SPACE 2
EXIT10   $ENTRY CSECT=NO,BASE=R12
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER USAGE IN THIS EXIT:                                 *
*                                                                     *
*        REG. 1 CMB REGISTER                                          *
*                                                                     *
*                                                                     *
*        REG. 10----==> JCT                                           *
*                                                                     *
*        REG. 11----==> HCT                                           *
*                                                                     *
*        REG. 12------BASE FOR CODE                                   *
*                                                                     *
*        REG. 13----==> HASPRDR PCE                                   *
*                                                                     *
*        ALL OTHER REGISTERS ARE USED AS LOCAL WORK REGISTERS,        *
*        LINKAGE REGISTERS, MACRO PARAMETER REGISTERS, ETC.           *
*                                                                     *
***********************************************************************
         SPACE 2
EX010    $SAVE NAME=PRCX10
         SPACE 1
         LR    R12,R15            ********** SET BASE FOR CODE
         USING CMBDSECT,R1         NOTE CMB ADDRESSABILITY
         USING JCTDSECT,R10
         USING HCTDSECT,R11
         USING PCEDSECT,R13
         SPACE 2
         CLC   =C'$HASP165 ',CMBMID       IS THIS A HASP165 MSG ?
         BNE   FASTRETN                   IF NOT, GET OUT FAST
         CLC   CMBJOBN,IEF097I            IS THIS EXTRA MSG
         BE    NOPE                       YES,THROW IT AWAY
         CLI   CMBUSER,C'$'            IS THIS A MSG FOR WYLBUR TO GET
         BNE   FASTRETN                   IF NOT, GET OUT FAST
         MVC   USERID,CMBUSER+1           STRIP '$' OFF USERID
         MVC   JOBNUM,CMBJOBID            MOVE JOB NUMBER
         MVC   JOBNAME,CMBJOBN            MOVE JOB NAME
         MVC   ENDTIME,CMBTIME            MOVE TIME ENDED
         MVC   TEXT(L'CMBTEXT),CMBTEXT    PUT MESSAGE TEXT IN
SENDIT   LA    R3,MESSAGE                 LOAD MESSAGE INT R3
         LA    R4,85                      PUT LENGTH IN R4
WTOWYL   $WTO  (R3),(R4),JOB=NO,TYPE=SVC34,CMB=NO      TO WYLBUR
NOPE     LA    R15,8               TELL NOT TO ISSUE ORIIGINAL NOTIFY
         B     RETRN               EXIT
         SPACE 1
FASTRETN DS    0H                         FAST RETURN TO CALLER
         SLR   R15,R15                    SET ZERO RETURN CODE
RETRN    $RETURN RC=(R15)                 RETURN
         SPACE 3
MESSAGE  DS    0CL85
         DC    CL2' '              BLANKS
FWYLBUR  DC    CL12'F WYLBUR,TO '  PREFIX OF MESSAGE
USERID   DC    CL6'      '
         DC    CL1' '
ENDTIME  DC    CL8'        '
         DC    CL1' '
JOBNUM   DC    CL8'        '
         DC    CL1' '
JOBNAME  DC    CL8'        '
         DC    CL1' '
TEXT     DC    CL37' '
BUFFER   DC    CL150' '
IEF097I  DC    CL8'IEF097I '        TRASH
         SPACE 2
         LTORG ,                   DEFINE LITERAL ORIGIN
         TITLE 'MODIFY HASP165 MESSAGE -- EPILOG ($MODEND)'
        $MODEND ,                  COMPLETE MODULE INFO TABLE
         SPACE 2
         END   ,                   END OF ASSEMBLY
