GETINFO  TITLE 'GET TSO USER STUFF FROM JCT INTO CLIST VARIABLES-V1M4'
         PRINT OFF    MACROS BE HERE
         MACRO
&NAME   $FINDCB  &ACOEF=,&ACTIM=,&ASCB=,&ASID=,&ASVT=,&ASXB=,          C
               &CVT=,&FIX=,&JCT=,&JSCB=,&LCT=,&OSACT=,&OSJCT=,&OSSCT=, C
               &RB=,&SJB=,&SSIB=,&TCB=,&TCBINT=,&TCBRCT=,&TCBSTC=,     C
               &TCT=,&TCTI=,&TIOT=,&TQE=,&TUCCVT=,&DOC=NO
.*       REFER TO THE DESCRIPTION AFTER THE MEND STATEMENT FOR THE
.*       MEANING OF THE DIFFERENT OPERANDS.
.*       THIS MACRO MAY BE USED TO OBTAIN THE ADDRESS OF ANY OR ALL OF
.*       THE CONTROL BLOCKS DEFINED IN THE CALLING PROTOTYPE STATMENT.
.*       BY GIVING A REGISTER (EITHER ABSOLUTE OR SYMBOLIC) FOR ANY OF
.*       THE SPECIFIED KEYWORD PARAMETERS, THE MACRO INSTRUCTION WILL
.*       WILL RETURN THE REQUESTED CONTROL BLOCK IN THAT REGISTER. NO
.*       REGISTERS OTHER THAN THOSE SPECIFIED ARE DESTROYED.
.*       THE FETCH CHAIN CAN BE STARTED AT A GIVEN BLOCK IN THE CHAIN
.*       BY SPECIFYING THE REGISTER POINTING TO THE CONTROL BLOCK
.*       ENCLOSED IN BRACKETS [].
.*
.*       IE. A TIOT REQUEST EXPANDS TO THE FOLLOWING
.*       $FINDCB  TIOT=X1         CALL
.*       L     X1,X'21C'          LOAD CURRENT TCB
.*       L     X1,X'0C'(,X1)      LOAD TIOT
.*       $FINDCB  TCB=[X3],TIOT=X1     CALL
.*       L     X1,X'0C'(,X3)      LOAD TIOT
.*
.*         THIS MACRO WAS CREATED 20 FEBRUARY, 1979
.*         THE LAST UPDATE FOR THIS MACRO WAS 07 JUNE, 1979
.*         A PROGRAM DESCRIPTION IS AVAILABLE AFTER THE MEND STATEMENT
.*
         GBLB  &FINDDN       GLOBAL TO SET EQUATES
         LCLC  &R1,&R2,&R3,&R4,&R5,&R6,&R7,&R8,&R9 REGISTER SYMBOLS FOR
         LCLC  &R10,&R11,&R12,&R13,&R14,&R15,&R16  CHAIN 1
         LCLC  &R17,&R18,&R19,&R20,&R21,&R22,&R23
         LCLC  &R24,&R25,&R26,&R27,&R28
         LCLC  &S1,&S2,&S3,&S4,&S5,&S6,&S7,&S8,&S9 REGISTER
         LCLC  &S10,&S11,&S12,&S13,&S14,&S15,&S16  SYMBOLS
         LCLC  &S17,&S18,&S19,&S20,&S21            FOR CHAIN 2
         LCLC  &T1,&T2,&T3,&T4,&T5,&T6,&T7,&T8     REGISTER SYMBOLS FOR
.*                                                 CHAIN 3
.*
.*       INCLUDE OFFSET EQUATES FIRST TIME ONLY
         AIF   (&FINDDN).EQUDONE  SKIP IT EQUATES ARE DONE
&FINDDN  SETB  1             INDICATE THAT IT IS DONE
$$FASID  EQU   X'24'         'ASCBASID' - ADDRESS OF ASID
$$FASXB  EQU   X'6C'         'ASCBASXB' - ADDRESS OF ASXB
$$FFTCB  EQU   X'04'         'ASXBFTCB' - ADDRESS OF FIRST TCB
$$FASVT  EQU   X'22C'        'CVTASVT'  - ADDRESS OF ASVT
$$FVSCVT EQU   X'CC'         'CVTUSER'  - ADDRESS OF TUC VS CVT
$$VPSIB  EQU   X'324'        'CVTVPSIB' - ADDRESS OF PAGE SERVICES
$$FCVT   EQU   X'10'         'FLCCVT'   - ADDRESS OF CVT
$$FOSACT EQU   X'27'         'JCTACTAD' - ADDRESS OF OS ACT (3 BYTE)
$$FJSCA  EQU   X'15C'        'JSCBACT'  - ADDRESS OF ACTIVE JSCB
$$FQMPI  EQU   X'F4'         'JSCBQMPI' - ADDRESS OF QUE MAN IN LCT
$$FSSIB  EQU   X'13C'        'JSCBSSIB' - ADDRESS OF SSIB
$$FOSJCT EQU   X'10'         'LCTJCTAD' - ADDRESS OF OS JCT
$$FOSSCT EQU   X'14'         'LCTSCTAD' - ADDRESS OF OS SCT
$$FASCBO EQU   X'224'        'PSAAOLD'  - ADDRESS OF CURRENT ASCB
$$FTCBO  EQU   X'21C'        'PSATOLD'  - ADDRESS OF CURRENT TCB
$$FLCTQ  EQU   X'F8'         'QMGR1'    - OFFSET OF QUE MAMAGER AREA
$$FJCT   EQU   X'60'         'SJBJCT'   - ADDRESS OF JES2 JCT
$$FSJB   EQU   X'20'         'SSIBSUSE' - ADDRESS OF JES SJB (SUBSYS)
$$FLCTR4 EQU   X'40'         'TCBGRS4'  - REGISTER 4 POINTS TO LCT
$$FJSCB  EQU   X'B4'         'TCBJSCB'  - ADDRESS OF CURRENT JSCB
$$FLTC   EQU   X'88'         'TCBLTC'   - ADDRESS OF LAST TCB
$$FRB    EQU   X'00'         'TCBRBP'   - ADDRESS OF LAST RB
$$FTCT   EQU   X'A4'         'TCBTCT'   - ADDRESS OF MAIN TCT
$$FTIOT  EQU   X'0C'         'TCBTIO'   - ADDRESS OF TIOT
$$FTQE   EQU   X'78'         'TCBTME'   - ADDRESS OF TASK TQE
$$FTCTI  EQU   X'0C'         'TCTIOTBL' - ADDRESS OF IO PART OF TCT
$$FACOEF EQU   X'40'         'TUCACOEF' - ADDRESS OF ACCOUNT TABLE
$$FACTIM EQU   X'3C'         'TUCACTIM' - ADDRESS OF CONVERSION
.EQUDONE ANOP  ,             CONTINUE
.*       ALL EQUATES ARE ESTABLISHED
.*
.*       PROCESS CHAIN 1, BACK TO FRONT
         AIF   (T'&TCBRCT EQ 'O' AND T'&TCBSTC EQ 'O' AND T'&TCBINT EQ '
               'O').NOTCBFT  JUMP IF NOT PART OF TCB CHAIN
         AIF   (T'&OSACT EQ 'O').RROSACT USE OSACT
         AIF   ('&OSACT'(1,1) EQ '[').RROSACT IGNORE OSACT
&R1      SETC  '&OSACT'      SET FOR OS ACT
.RROSACT AIF   (T'&OSJCT NE 'O').ROSJCT USE OS JCT
&R2      SETC  '&R1'         CARRY DOWN
         AGO   .NROSJCT      GO DO REST
.ROSJCT  AIF   ('&OSJCT'(1,1) NE '[').UROSJCT START WITH
&R2      SETC  '&OSJCT'(2,K'&OSJCT-2)  MOVE REGISTER
         AGO   .RROSJCT      JUMP TO NEXT CHECK
.UROSJCT ANOP  ,
&R2      SETC  '&OSJCT'      SET TO PUT VALUE HERE
.NROSJCT ANOP  ,
&R3      SETC  '&R2'         CARRY DOWN
.RROSJCT AIF   (T'&OSSCT EQ 'O').RROSSCT
         AIF   ('&OSSCT' EQ '[').RROSSCT IGNORE
&R5      SETC  '&OSSCT'      SET REG FOR OS SCT
.RROSSCT AIF   (T'&LCT NE 'O').RLCT
         AIF   ('&R3' EQ '').RRS3 USE OS SCT INSTEAD
&R4      SETC  '&R3'         REGISTER FROM SCT CHAIN
         AGO   .NRLCT        TO NEXT CHECK
.RRS3    ANOP  ,
&R4      SETC  '&R5'         REGISTER FROM JCT CHAIN
         AGO   .NRLCT        TO NEXT CHECK
.RLCT    AIF   ('&LCT'(1,1) NE '[').URLCT USE LCT
&R4      SETC  '&LCT'(2,K'&LCT-2)      MOVE IN SYMBOL
         AGO   .RRLCT        GO TO NEXT CHECK
.URLCT   ANOP  ,
&R4      SETC  '&LCT'        SET FOR STORAGE SYMBOL
.NRLCT   ANOP  ,
&R6      SETC  '&R4'         CARRY SYMBOL DOWN
.RRLCT   AIF   (T'&JCT EQ 'O').RRJCT
         AIF   ('&JCT'(1,1) EQ '[').RRJCT IGNORE START
&R8      SETC  '&JCT'        SET LOAD SYMBOL
.RRJCT   AIF   (T'&SJB NE 'O').RSJB
&R9      SETC  '&R8'         CARRY REGISTER DOWN
         AGO   .NRSJB        GO TO NEXT CHECK
.RSJB    AIF   ('&SJB'(1,1) NE '[').URSJB  USE SJB
&R9      SETC  '&SJB'(2,K'&SJB-2)      MOVE REGISTER
         AGO   .RRSJB        GO TO NEXT CHECK
.URSJB   ANOP  ,
&R9      SETC  '&SJB'        PUT OUT REGISTER
.NRSJB   ANOP  ,
&R10     SETC  '&R9'         CARRY DOWN
.RRSJB   AIF   (T'&SSIB NE 'O').RSSIB
&R11     SETC  '&R10'        CARRY REGISTER DOWN
         AGO   .NRSSIB       GO TO NEXT CHECK
.RSSIB   AIF   ('&SSIB'(1,1) NE '[').URSSIB USE SSIB
&R11     SETC  '&SSIB'(2,K'&SSIB-2)    MOVE REGISTER
         AGO   .RRSSIB       GO TO NEXT CHECK
.URSSIB  ANOP  ,
&R11     SETC  '&SSIB'       PUT OUT REGISTER
.NRSSIB  ANOP  ,
&R12     SETC  '&R11'        CARRY DOWN
.RRSSIB  AIF   (T'&JSCB NE 'O').RJSCB
&R13     SETC  '&R12'        CARRY REGISTER DOWN
         AGO   .NRJSCB       TO NEXT CHECK
.RJSCB   AIF   ('&JSCB'(1,1) NE '[').URJSCB USE JSCB
&R13     SETC  '&JSCB'(2,K'&JSCB-2)    MOVE IN SYMBOL
         AGO   .RRJSCB       GO TO NEXT CHECK
.URJSCB  ANOP  ,
&R13     SETC  '&JSCB'       SET FOR STORAGE SYMBOL
.NRJSCB  ANOP  ,
&R14     SETC  '&R13'        CARRY DOWN
.RRJSCB  AIF   (T'&TCTI EQ 'O').RRTCTI
         AIF   ('&TCTI'(1,1) EQ '[').RRTCTI IGNORE START
&R15     SETC  '&TCTI'       SET LOAD SYMBOL
.RRTCTI  AIF   (T'&TCT NE 'O').RTCT
&R16     SETC  '&R15'        CARRY REGISTER DOWN
         AGO   .NRTCT        GO TO NEXT CHECK
.RTCT    AIF   ('&TCT'(1,1) NE '[').URTCT  USE TCT
&R16     SETC  '&TCT'(2,K'&TCT-2)      MOVE REGISTER
         AGO   .RRTCT        GO TO NEXT CHECK
.URTCT   ANOP  ,
&R16     SETC  '&TCT'        PUT OUT REGISTER
.NRTCT   ANOP  ,
&R17     SETC  '&R16'        CARRY DOWN
.RRTCT   AIF   (T'&TIOT EQ 'O').RRTIOT
         AIF   ('&TIOT'(1,1) EQ '[').RRTIOT IGNORE START
&R18     SETC  '&TIOT'       SET LOAD SYMBOL
.RRTIOT  AIF   (T'&TCBINT NE 'O').TCBI
         AIF   ('&R6' EQ '').RRS6 JUMP IF NO R6
&R7      SETC  '&R6'         SET SAME AS LAST
         AGO   .NRTIOT       GO TO END
.RRS6    AIF   ('&R14' EQ '').RRS14 JUMP IF NO R14
&R7      SETC  '&R14'        SET SAME AS LAST
         AGO   .NRTIOT       GO TO END
.RRS14   AIF   ('&R17' EQ '').RRS17    NEXT LONGEST
&R7      SETC  '&R17'        SET NEXT LONGEST
         AGO   .NRTIOT       TO END OF CHAIN
.RRS17   ANOP  ,             LAST ENTRY IN NODE
&R7      SETC  '&R18'        SET SAME AS LAST
         AGO   .NRTIOT       GO TO END
.TCBI    AIF   ('&TCBINT'(1,1) NE '[').USTCBI USE TCB
&R7      SETC  '&TCBINT'(2,K'&TCBINT-2) MOVE IN SYMBOL
         AGO   .NOTCBI       QUIT
.USTCBI  ANOP  ,
&R7      SETC  '&TCBINT'     SET FOR TCB SYMBOL
.NRTIOT  ANOP  ,             ENTRANCE FOR NULL
&R19     SETC  '&R7'         CARRY SYMBOL DOWN
.NOTCBI  AIF   (T'&TCBSTC NE 'O').TCBSTC
&R20     SETC  '&R19'        CARRY REGISTER DOWN
         AGO   .NLTBSTC      GO TO NEXT CHECK
.TCBSTC  AIF   ('&TCBSTC'(1,1) NE '[').USTBSTC USE TCBSTC
&R20     SETC  '&TCBSTC'(2,K'&TCBSTC-2)  MOVE REGISTER
         AGO   .NOTBSTC      GO TO NEXT CHECK
.USTBSTC ANOP  ,
&R20     SETC  '&TCBSTC'     PUT OUT REGISTER
.NLTBSTC ANOP  ,
&R21     SETC  '&R20'        CARRY DOWN
.NOTBSTC AIF   (T'&TCBRCT NE 'O').TCBRCT
&R22     SETC  '&R21'        CARRY REGISTER DOWN
         AGO   .NLTBRCT      GO TO NEXT CHECK
.TCBRCT  AIF   ('&TCBRCT'(1,1) NE '[').USTBRCT USE TCBRCT
&R22     SETC  '&TCBRCT'(2,K'&TCBRCT-2)  MOVE REGISTER
         AGO   .NOTBRCT      GO TO NEXT CHECK
.USTBRCT ANOP  ,
&R22     SETC  '&TCBRCT'     PUT OUT REGISTER
.NLTBRCT ANOP  ,
&R23     SETC  '&R22'        CARRY DOWN
.NOTCBFT ANOP  ,             ENTRY FOR NOT TCB CHAIN
.NOTBRCT AIF   (T'&ASXB NE 'O').ASXB
&R24     SETC  '&R23'        CARRY REGISTER DOWN
         AGO   .NLASXB       GO TO NEXT CHECK
.ASXB    AIF   ('&ASXB'(1,1) NE '[').USASXB USE ASXB
&R24     SETC  '&ASXB'(2,K'&ASXB-2)    MOVE REGISTER
         AGO   .NOASXB       GO TO NEXT CHECK
.USASXB  ANOP  ,
&R24     SETC  '&ASXB'       PUT OUT REGISTER
.NLASXB  ANOP  ,
&R25     SETC  '&R24'        CARRY DOWN
.NOASXB  AIF   (T'&ASID EQ 'O').NOASID
         AIF   ('&ASID'(1,1) EQ '[').NOASID IGNORE START
&R27     SETC  '&ASID'       SET STORE REGISTER
.NOASID  AIF   (T'&ASCB NE 'O').ASCB
         AIF   ('&R25' EQ '').NOR25    USE ASID INSTEAD
&R26     SETC  '&R25'        CARRY DOWN
         AGO   .NLASCB       TO NEXT CHECK
.NOR25   ANOP  ,
&R26     SETC  '&R27'        CARRY LONGER CHAIN DOWN
         AGO   .NLASCB       TO NEXT CHECK
.ASCB    AIF   ('&ASCB'(1,1) NE '[').USASCB USE ASCB
&R26     SETC  '&ASCB'(2,K'&ASCB-2)    MOVE IN SYMBOL
         AGO   .NOASCB       GO TO NEXT CHECK
.USASCB  ANOP  ,
&R26     SETC  '&ASCB'       SET FOR STORAGE SYMBOL
.NLASCB  ANOP  ,
&R28     SETC  '&R26'        CARRY SYMBOL DOWN
.NOASCB  ANOP  ,
.*       ALL REGISTERS FOR CHAIN 1 ESTABLISHED
.*
.*       PROCESS CHAIN 2, BACK TO FRONT
         AIF   (T'&TCBRCT NE 'O' OR T'&TCBSTC NE 'O' OR T'&TCBINT NE 'O'
               ').TCBFET     JUMP IF TCB CHAIN DONE FOR LCT
         AIF   (T'&OSACT EQ 'O').NOOSACT USE OSACT
         AIF   ('&OSACT'(1,1) EQ '[').NOOSACT IGNORE OSACT
&S1      SETC  '&OSACT'      SET FOR OS ACT
.NOOSACT AIF   (T'&OSJCT NE 'O').OSJCT USE OS JCT
&S2      SETC  '&S1'         CARRY DOWN
         AGO   .NLOSJCT      GO DO REST
.OSJCT   AIF   ('&OSJCT'(1,1) NE '[').USOSJCT START WITH
&S2      SETC  '&OSJCT'(2,K'&OSJCT-2)  MOVE REGISTER
         AGO   .NOOSJCT      JUMP TO NEXT CHECK
.USOSJCT ANOP  ,
&S2      SETC  '&OSJCT'      SET TO PUT VALUE HERE
.NLOSJCT ANOP  ,
&S3      SETC  '&S2'         CARRY DOWN
.NOOSJCT AIF   (T'&OSSCT EQ 'O').NOOSSCT
         AIF   ('&OSSCT' EQ '[').NOOSSCT IGNORE
&S5      SETC  '&OSSCT'      SET REG FOR OS SCT
.NOOSSCT AIF   (T'&LCT NE 'O').LCT
         AIF   ('&S3' EQ '').NOS3 USE OS SCT INSTEAD
&S4      SETC  '&S3'         REGISTER FROM SCT CHAIN
         AGO   .NLLCT        TO NEXT CHECK
.NOS3    ANOP  ,
&S4      SETC  '&S5'         REGISTER FROM JCT CHAIN
         AGO   .NLLCT        TO NEXT CHECK
.LCT     AIF   ('&LCT'(1,1) NE '[').USLCT USE LCT
&S4      SETC  '&LCT'(2,K'&LCT-2)      MOVE IN SYMBOL
         AGO   .NOLCT        GO TO NEXT CHECK
.USLCT   ANOP  ,
&S4      SETC  '&LCT'        SET FOR STORAGE SYMBOL
.NLLCT   ANOP  ,
&S6      SETC  '&S4'         CARRY SYMBOL DOWN
.NOLCT   AIF   (T'&JCT EQ 'O').NOJCT
         AIF   ('&JCT'(1,1) EQ '[').NOJCT IGNORE START
&S8      SETC  '&JCT'        SET LOAD SYMBOL
.****************************** PATTERN FOR STRAIGHT THROUGH CARRY .**
.NOJCT   AIF   (T'&SJB NE 'O').SJB                                 .**
&S9      SETC  '&S8'         CARRY REGISTER DOWN                   .**
         AGO   .NLSJB        GO TO NEXT CHECK                      .**
.SJB     AIF   ('&SJB'(1,1) NE '[').USSJB  USE SJB                 .**
&S9      SETC  '&SJB'(2,K'&SJB-2)      MOVE REGISTER               .**
         AGO   .NOSJB        GO TO NEXT CHECK                      .**
.USSJB   ANOP  ,                                                   .**
&S9      SETC  '&SJB'        PUT OUT REGISTER                      .**
.NLSJB   ANOP  ,                                                   .**
&S10     SETC  '&S9'         CARRY DOWN                            .**
.****************************** PATTERN FOR STRAIGHT THROUGH CARRY .**
.NOSJB   AIF   (T'&SSIB NE 'O').SSIB
&S11     SETC  '&S10'        CARRY REGISTER DOWN
         AGO   .NLSSIB       GO TO NEXT CHECK
.SSIB    AIF   ('&SSIB'(1,1) NE '[').USSSIB USE SSIB
&S11     SETC  '&SSIB'(2,K'&SSIB-2)    MOVE REGISTER
         AGO   .NOSSIB       GO TO NEXT CHECK
.USSSIB  ANOP  ,
&S11     SETC  '&SSIB'       PUT OUT REGISTER
.NLSSIB  ANOP  ,
&S12     SETC  '&S11'        CARRY DOWN
.NOSSIB  AIF   (T'&JSCB NE 'O').JSCB
         AIF   ('&S6' EQ '').NOS6 USE ASID INSTEAD
&S7      SETC  '&S6'         CARRY DOWN
         AGO   .NLJSCB       TO NEXT CHECK
.NOS6    ANOP  ,
&S7      SETC  '&S12'        CARRY LONGER CHAIN DOWN
         AGO   .NLJSCB       TO NEXT CHECK
.JSCB    AIF   ('&JSCB'(1,1) NE '[').USJSCB USE JSCB
&S7      SETC  '&JSCB'(2,K'&JSCB-2)    MOVE IN SYMBOL
         AGO   .NOJSCB       GO TO NEXT CHECK
.USJSCB  ANOP  ,
&S7       SETC  '&JSCB'       SET FOR STORAGE SYMBOL
.NLJSCB  ANOP  ,
&S13     SETC  '&S7'         CARRY SYMBOL DOWN
.NOJSCB  AIF   (T'&TCTI EQ 'O').NOTCTI
         AIF   ('&TCTI'(1,1) EQ '[').NOTCTI IGNORE START
&S15     SETC  '&TCTI'       SET LOAD SYMBOL
.NOTCTI  AIF   (T'&TCT NE 'O').TCT
&S16     SETC  '&S15'        CARRY REGISTER DOWN
         AGO   .NLTCT        GO TO NEXT CHECK
.TCT     AIF   ('&TCT'(1,1) NE '[').USTCT  USE TCT
&S16     SETC  '&TCT'(2,K'&TCT-2)      MOVE REGISTER
         AGO   .NOTCT        GO TO NEXT CHECK
.USTCT   ANOP  ,
&S16     SETC  '&TCT'        PUT OUT REGISTER
.NLTCT   ANOP  ,
&S17     SETC  '&S16'        CARRY DOWN
.NOTCT   AIF   (T'&TIOT EQ 'O').NOTIOT
         AIF   ('&TIOT'(1,1) EQ '[').NOTIOT IGNORE START
&S18     SETC  '&TIOT'       SET LOAD SYMBOL
.TCBFET  ANOP  ,             ENTRY FOR TCB CHAIN DONE IN OTHER
.NOTIOT  AIF   (T'&TQE EQ 'O').NOTQE
         AIF   ('&TQE'(1,1) EQ '[').NOTQE IGNORE START
&S19     SETC  '&TQE'        SET LOAD SYMBOL
.NOTQE   AIF   (T'&RB EQ 'O').NORB
         AIF   ('&RB'(1,1) EQ '[').NORB IGNORE START
&S20     SETC  '&RB'         SET LOAD SYMBOL
.NORB    AIF   (T'&TCB NE 'O').TCB     REGISTER
         AIF   ('&S13' EQ '').NOS13    FIND LONGER CHAIN
&S14     SETC  '&S13'        SET LONGEST
         AGO   .NLTCB        TO END OF CHAIN
.NOS13   AIF   ('&S17' EQ '').NOS17    NEXT LONGEST
&S14     SETC  '&S17'        SET NEXT LONGEST
         AGO   .NLTCB        TO END OF CHAIN
.NOS17   AIF   ('&S18' EQ '').NOS18
&S14     SETC  '&S18'        SET TO CHAIN
         AGO   .NLTCB        TO END OF CHAIN
.NOS18   AIF   ('&S19' EQ '').NOS19
&S14     SETC  '&S19'        SET TO CHAIN
         AGO   .NLTCB        TO END OF CHAIN
.NOS19   ANOP  ,
&S14     SETC  '&S20'        SET FOR LAST CHAIN
         AGO   .NLTCB        TO END OF CHAIN
.TCB     AIF   ('&TCB'(1,1) NE '[').USTCB USE TCB
&S14     SETC  '&TCB'(2,K'&TCB-2) MOVE IN SYMBOL
         AGO   .NOTCB        QUIT
.USTCB   ANOP  ,
&S14     SETC  '&TCB'        SET FOR TCB SYMBOL
.NLTCB   ANOP  ,
&S21     SETC  '&S14'        SET TO BOTH
.NOTCB   ANOP  ,             END OF CHECK
.*       END OF CHAIN 2 PROCESSING
.*
.*       PROCESS CHAIN 3
         AIF   (T'&ACOEF EQ 'O').NOACOEF
         AIF   ('&ACOEF'(1,1) EQ '[').NOACOEF IGNORE START
&T1      SETC  '&ACOEF'      SET LOAD SYMBOL
.NOACOEF AIF   (T'&ACTIM EQ 'O').NOACTIM
         AIF   ('&ACTIM'(1,1) EQ '[').NOACTIM IGNORE START
&T3      SETC  '&ACTIM'      SET LOAD SYMBOL
.NOACTIM AIF   (T'&TUCCVT NE 'O').TUCCVT
         AIF   ('&T1' EQ '').NOT1 USE ASID INSTEAD
&T2      SETC  '&T1'         CARRY DOWN
         AGO   .NLTUCVT      TO NEXT CHECK
.NOT1    ANOP  ,
&T2      SETC  '&T3'         CARRY LONGER CHAIN DOWN
         AGO   .NLTUCVT      TO NEXT CHECK
.TUCCVT  AIF   ('&TUCCVT'(1,1) NE '[').USTUCVT USE TUCCVT
&T2      SETC  '&TUCCVT'(2,K'&TUCCVT-2) MOVE IN SYMBOL
         AGO   .NOTUCVT      GO TO NEXT CHECK
.USTUCVT ANOP  ,
&T2      SETC  '&TUCCVT'     SET FOR STORAGE SYMBOL
.NLTUCVT ANOP  ,
&T4      SETC  '&T2'         CARRY SYMBOL DOWN
.NOTUCVT AIF   (T'&ASVT EQ 'O').NOASVT
         AIF   ('&ASVT'(1,1) EQ '[').NOASVT  IGNORE START
&T6      SETC  '&ASVT'       SET LOAD SYMBOL
.NOASVT  AIF   (T'&FIX EQ 'O').NOFIX
         AIF   ('&FIX'(1,1) EQ '[').NOFIX  IGNORE START
&T7      SETC  '&FIX'        SET LOAD SYMBOL
.********************************* PATTERN FOR A NODE .**.**.**.**.**
.NOFIX   AIF   (T'&CVT NE 'O').CVT                                .**
         AIF   ('&T4' EQ '').NOT4 FIND LONGEST                    .**
&T5      SETC  '&T4'         CARRY REGISTER DOWN                  .**
         AGO   .NLCVT        GO TO NEXT CHECK                     .**
.NOT4    AIF   ('&T6' EQ '').NOT6 FIND LONGEST                    .**
&T5      SETC  '&T6'         CARRY REGISTER DOWN                  .**
         AGO   .NLCVT        GO TO NEXT CHECK                     .**
.NOT6    ANOP  ,             SET LAST POSSIBLE                    .**
&T5      SETC  '&T7'         SET CHAIN ALONG                      .**
         AGO   .NLCVT        GO TO END                            .**
.CVT     AIF   ('&CVT'(1,1) NE '[').USCVT USE CVT                 .**
&T5      SETC  '&CVT'(2,K'&CVT-2)      MOVE REGISTER              .**
         AGO   .NOCVT        GO TO NEXT CHECK                     .**
.USCVT   ANOP  ,                                                  .**
&T5      SETC  '&CVT'        PUT OUT REGISTER                     .**
.NLCVT   ANOP  ,                                                  .**
&T8      SETC  '&T5'         CARRY DOWN                           .**
.********************************* PATTERN FOR A NODE .**.**.**.**.**
.NOCVT   ANOP  ,             END OF CHAIN 3
.*       CHAIN 3 PROCESSING FINISHED
.*
&NAME    DS    0H
.*       DO LOAD STATEMENTS FOR CHAIN 1
         AIF   ('&R28' EQ '').SKPR28   SKIP THIS LOAD
         L     &R28,$$FASCBO      --> ASCB
.SKPR28  AIF   ('&R27' EQ '').SKPR27   SKIP THIS LOAD
         LH    &R27,$$FASID(,&R26) IS THE ASID
.SKPR27  AIF   ('&R25' EQ '').SKPR25   SKIP THIS LOAD
         L     &R25,$$FASXB(,&R26) --> ASXB
.SKPR25  AIF   ('&R23' EQ '').SKPR23   SKIP THIS LOAD
         L     &R23,$$FFTCB(,&R24) --> FIRST TCB (RCT)
.SKPR23  AIF   ('&R21' EQ '').SKPR21   SKIP THIS LOAD
         L     &R21,$$FLTC(,&R22) --> STC INIT TCB (STC)
.SKPR21  AIF   ('&R19' EQ '').SKPR19   SKIP THIS LOAD
         L     &R19,$$FLTC(,&R20) --> ORIGINATOR TCB (INT)
.SKPR19  AIF   ('&R18' EQ '').SKPR18   SKIP THIS LOAD
         L     &R18,$$FTIOT(,&R7) --> TIOT
.SKPR18  AIF   ('&R17' EQ '').SKPR17   SKIP THIS LOAD
         L     &R17,$$FTCT(,&R7)  --> TCT
.SKPR17  AIF   ('&R15' EQ '').SKPR15   SKIP THIS LOAD
         L     &R15,$$FTCTI(,&R16) --> TCTIOTBL
.SKPR15  AIF   ('&R14' EQ '').SKPR14   SKIP THIS LOAD
         L     &R14,$$FJSCB(,&R7) --> JSCB (CURRENT)
         L     &R14,$$FJSCA(,&R14) --> JSCB (ACTIVE)
.SKPR14  AIF   ('&R12' EQ '').SKPR12   SKIP THIS LOAD
         L     &R12,$$FSSIB(,&R13) --> SSIB
.SKPR12  AIF   ('&R10' EQ '').SKPR10   SKIP THIS LOAD
         L     &R10,$$FSJB(,&R11) --> SJB
.SKPR10  AIF   ('&R8' EQ '').SKPR8     SKIP THIS LOAD
         L     &R8,$$FJCT(,&R9)   --> JES2 JCT
.SKPR8   AIF   ('&R6' EQ '').SKPR6     SKIP THIS LOAD
         L     &R6,$$FLCTR4(,&R7) --> LCT
.SKPR6   AIF   ('&R5' EQ '').SKPR5     SKIP THIS LOAD
         L     &R5,$$FOSSCT(,&R4) --> OS SCT
.SKPR5   AIF   ('&R3' EQ '').SKPR3     SKIP THIS LOAD
         L     &R3,$$FOSJCT(,&R4) --> OS JCT
.SKPR3   AIF   ('&R1' EQ '').SKPR1     SKIP THIS LOAD
         L     &R1,$$FOSACT(,&R2) --> OS ACT (-10)
         LA    &R1,16(,&R1)       --> OS ACT
.SKPR1   ANOP  ,             END OF CHAIN 2
.*       LOAD STATMENTS FOR CHAIN 1 FINISHED
.*
.*       DO LOAD STATEMENTS FOR CHAIN 2
         AIF   ('&S21' EQ '').SKPS21   SKIP THIS LOAD
         L     &S21,$$FTCBO       --> CURRENT TCB
.SKPS21  AIF   ('&S20' EQ '').SKPS20   SKIP THIS LOAD
         L     &S20,$$FRB(,&S14)  --> RB
.SKPS20  AIF   ('&S19' EQ '').SKPS19   SKIP THIS LOAD
         L     &S19,$$FTQE(,&S14) --> TQE
.SKPS19  AIF   ('&S18' EQ '').SKPS18   SKIP THIS LOAD
         L     &S18,$$FTIOT(,&S14) --> TIOT
.SKPS18  AIF   ('&S17' EQ '').SKPS17   SKIP THIS LOAD
         L     &S17,$$FTCT(,&S14) --> TCT
.SKPS17  AIF   ('&S15' EQ '').SKPS15   SKIP THIS LOAD
         L     &S15,$$FTCTI(,&S16) --> TCTIOTBL
.SKPS15  AIF   ('&S13' EQ '').SKPS13   SKIP THIS LOAD
         L     &S13,$$FJSCB(,&S14) --> JSCB (CURRENT)
         L     &S13,$$FJSCA(,&S13) --> JSCB (ACTIVE)
.SKPS13  AIF   ('&S12' EQ '').SKPS12   SKIP THIS LOAD
         L     &S12,$$FSSIB(,&S7) --> SSIB
.SKPS12  AIF   ('&S10' EQ '').SKPS10   SKIP THIS LOAD
         L     &S10,$$FSJB(,&S11) --> SJB
.SKPS10  AIF   ('&S8' EQ '').SKPS8     SKIP THIS LOAD
         L     &S8,$$FJCT(,&S9)   --> JES2 JCT
.SKPS8   AIF   ('&S6' EQ '').SKPS6     SKIP THIS LOAD
         L     &S6,$$FQMPI(,&S7)  --> LCT
         B     FND2&SYSNDX        BRANCH AROUND OFFSET
FND1&SYSNDX DC A($$FLCTQ)         LOAD OFFSET TO QMANG AREA
FND2&SYSNDX S  &S6,FND1&SYSNDX    REDUCE TO START OF LCT
.SKPS6   AIF   ('&S5' EQ '').SKPS5     SKIP THIS LOAD
         L     &S5,$$FOSSCT(,&S4) --> OS SCT
.SKPS5   AIF   ('&S3' EQ '').SKPS3     SKIP THIS LOAD
         L     &S3,$$FOSJCT(,&S4) --> OS JCT
.SKPS3   AIF   ('&S1' EQ '').SKPS1     SKIP THIS LOAD
         L     &S1,$$FOSACT(,&S2) --> OS ACT (-10)
         LA    &S1,16(,&S1)       --> OS ACT
.SKPS1   ANOP  ,             END OF CHAIN 2
.*       END OF CHAIN 2 PROCESSING
.*
.*       BEGIN CHAIN 3 PROCESSING
         AIF   ('&T8' EQ '').SKPT8     SKIP THIS LOAD
         L     &T8,$$FCVT         --> CVT
.SKPT8   AIF   ('&T7' EQ '').SKPT7     SKIP THIS LOAD
         L     &T7,$$VPSIB(,&T5)  --> CVTVPSIB (PAGE SERVICES)
.SKPT7   AIF   ('&T6' EQ '').SKPT6     SKIP THIS LOAD
         L     &T6,$$FASVT(,&T5)  --> ASVT
.SKPT6   AIF   ('&T4' EQ '').SKPT4     SKIP THIS LOAD
         L     &T4,$$FVSCVT(,&T5) --> TUCVSCVT
.SKPT4   AIF   ('&T3' EQ '').SKPT3     SKIP THIS LOAD
         L     &T3,$$FACTIM(,&T2) --> TUCACTIM
.SKPT3   AIF   ('&T1' EQ '').SKPT1     SKIP THIS LOAD
         L     &T1,$$FACOEF(,&T2) --> TUCACOEF
.SKPT1   ANOP  ,             END OF CHAIN 3
.*       PROCESSING FINISHED WITH CHAIN 3
.*
         MEXIT
         AIF    ('&DOC' EQ 'NO').NODOC
         SPACE  2
*
*        ACOEF  = ACCOUNTING COEFFICIENTS POINTED TO BY TUCVSCVT
*        ACTIM  = I/O TIME, JOB CPU TIME, AND PROGRAMMER NAME TABLE
*        ASCB   = ASCB, POINTED TO FROM X'224'
*        ASID   = ASID AS FOUND IN THE ASCB
*        ASVT   = ASVT POINTED TO BY THE CVT
*        ASXB   = ASXB POINTED TO BY ASCB
*        CVT    = CVT, ADDRESS OBTAINED FROM X'10'
*        FIX    = ADDRESS OF PAGE SERVICES (FROM CVT)
*        JCT    = JES2 JCT CHAINED FROM THE ACTIVE JSCB, SSIB, SJB.
*                 EITHER TCB OR TCBINT MAY BE USED TO LOCATE THE JSCB.
*        JSCB   = ACTIVE JSCB FROM EITHER THE TCB OR TCBINIT.
*        LCT    = INITIATOR LCT, OBTAINED EITHER FROM THE TCBINT R4
*                 OR THE JSCB QMANAGER POINTER (MINUS AN OFFSET)
*        OSACT  = OS ACT, POINTED TO BY THE OS JCT
*        OSJCT  = OS JCT, POINTED TO BY THE LCT
*        OSSCT  = OS SCT, POINTED TO BY THE LCT
*        RB     = RB, POINTED TO BY THE TCB
*        SJB    = SJB, POINTED TO BY THE SSIB (SEE JSCB FOR CHAIN)
*        SSIB   = SSIB, POINTED TO BY THE JSCB (SEE JSCB FOR CHAIN)
*        TCB    = THE TCB POINTED TO BY LOCATION X'21C'
*        TCBINT = THE TCB CHAINED FROM THE ASCB, ASXB. WHEN THIS TCB
*                 IS REQUESTED, THEN THE JSCB AND LCT CHAINS (IF
*                 REQUESTED) ARE STARTED FROM THIS REFERENCE.
*                 IF THIS ENTRY IS NOT INCLUDED, THEN THE JSCB,
*                 LCT CHAINS WILL START FROM THE TCB FROM X'21C'.
*        TCBRCT = THE TCB FOR THE REGION CONTROL, POINTED TO BY ASXB
*        TCBSTC = THE TCB FOR THE STC, CHAINED FROM THE RCT TCB.
*        TCT    = SMF TIMING CONTROL TABLE, POINTED TO BY EITHER
*                 THE INITIATOR TCB OR THE STEP TCB.
*        TCTI   = THE IO PORTION OF THE TCT.
*        TIOT   = TIOT FOR THE TCB, EITHER STEP OR TCB INIT.
*        TQE    = THE TQE, POINTED TO FROM THE TCB
*        TUCCVT = TUC VS CVT, POINTED TO BY THE CVT
*
*        THE FOLLOWING CHAINS ARE USED IN THIS MACRO;
*    CHAIN 1                 OFF ASCB OLD
*        X'224'
*        28×
*         ASCB----/
*        27×26  25×26
*         ASID   ASXB
*               23×24
*               TCBRCT
*               21×22
*               TCBSTC
*               19×20
*               TCBINT---/------/------/
*               18× 7  17× 7  14× 7  6 × 7
*                TIOT   TCT    JSCB   LCT-----/
*                      15×16  12×13  5 × 4  3 × 4
*                       TCTI   SSIB  OSSCT  OSJCT
*                             10×11         1 × 2
*                              SJB          OSACT
*                             8 × 9
*                              JCT
*
*    CHAIN 2                 OFF TCB OLD
*        X'21C'
*        21×
*         TCB-----/------/------/------/
*        20×14  19×14  18×14  17×14  13×14
*         RB     TQE    TIOT   TCT    JSCB----/
*                             15×16  12× 7  6 × 7
*                              TCTI   SSIB   LCT-----/
*                                    10×11  5 × 4  3 × 4
*                                     SJB   OSSCT  OSJCT
*                                    8 × 9         1 × 2
*                                     JCT          OSACT
*
*    CHAIN 3                 OFF CVT
*         X'10'
*        8 ×
*         CVT-----/------/
*        7 × 5  6 × 5  4 × 5
*         FIX    ASVT  TUCCVT---/
*                      3 × 2  1 × 2
*                      ACTIM  ACOEF
*
*  THE SYMBOLIC REGISTER ON THE LEFT HAND SIDE OF THE '×' IS USED
*   TO INDICATE WHETHER OR NOT THE LOAD INSTRUCTION IS DESIRED.
*  THE CHAIN IS ESTABLISHED FROM LAST TO FIRST USING THE CONTINUING
*   (LONGER) CHAINS TO PROVIDE THE NODES FOR THE BRANCHES.
*  TO ADD A NEW ENTRY, OR A CHAIN OF ENTRIES:
*   A. LOCATE THE POSITION WHERE THE NEW ENTRY BELONGS ON THE
*      CHAINING DIAGRAM.
*   B. IF THE ADDITION IS AN EXTENSION OF AN EXISTING CHAIN THEN
*      ESTABLISH NEW SYMBOLIC REGISTERS FOR THE NEW INSTRUCTIONS
*      BEGINNING WITH THE END OF THE CHAIN AND WORKING FORWARD.
*   C. IF THE ADDITION IS A PARALLEL CHAIN, THEN USE THE RIGHT HAND
*      SYMBOLIC REGISTER NUMBER AS THE RIGHT HAND NUMBER AT THE
*      BEGINNING OF THE NEW CHAIN. FINISH NUMBERING THE REST OF THE
*      CHAIN FROM LAST TO FIRST. FOLLOW THE DIAGRAM FOR THE SSIB TO
*      SJB TO JCT AS AN EXAMPLE. (&S7 IS THE COMMON REGISTER OF THE
*      JSCB BLOCK. THE VALUE OF &S13 WILL EQUAL THE VALUE OF &S7 IF
*      THE CHAIN IS LOADED FROM THE TCB. &S13 WILL BE NULL AND &S7
*      WILL BE THE REGISTER OF THE JSCB IF THE JSCB IS PROVIDED AS
*      A STARTING ADDRESS.)
*   D. CODE THE PROPER SETC AND AIF STATEMENTS TO SET THE NEW SYMBOLIC
*      REGISTERS. USE THE EXISTING STATEMENTS AS A GUIDE. ALSO BUILD
*      UP THE CONDITIONAL LOAD STATEMENTS. THE NEW CODE SHOULD BE
*      PLACED IN THE MACRO IN THE LOGICAL RELATIONSHIP TO THE REST
*      OF THE CHAINS. IE., PUT THE CODE BETWEEN THE INSTRUCTIONS THAT
*      THE CHAINING DIAGRAM INDICATE.
*
*  THE FOLLOWING TABS WERE USED IN THE MAIN PART OF THE MACRO:
*   TAB ON(10,16,30,35,40)
*
.NODOC  ANOP
        MEND
*          DATA SET MODESET    AT LEVEL 002 AS OF 01/15/82
         MACRO
&LABEL   FMODESET &KEY=,&MF=,&RET=R14,&MODE=     CHANGE NAME TO MODESET
.*
.* CHANGE MACRO NAME TO FMODESET TO ALLOW USE OF REAL MODESET
.*
         LCLC    &PRINT         ******TO ALLOW USE OF MAGIC SVC  KT
&PRINT   SETC    'OFF'
         AIF     ('&MF(1)' EQ 'E').CODE
         AIF     ('&KEY' EQ 'ZERO').GOSUP
         AIF     ('&MODE' EQ 'SUP').GOSUP
         AIF     ('&KEY' EQ 'NZERO').GOPROB
         AIF     ('&MODE' EQ 'PROB').GOPROB
 MNOTE 8,'MODESET (NTL) OF &KEY NOT RECOGNIZED'
         MEXIT
.GOSUP   ANOP
*
&LABEL   SR    R0,R0                    GET CODE
         LA    R0,1             INDICATE KEY=0
         SVC   232              DOIT PLEASE
         AIF   ('&MF' NE 'L').FIN1                                 NTL*
         BR    &RET             RETURN TO MAINLINE
*
.FIN1    ANOP  ,                                                   NTL*
         MEXIT
.GOPROB  ANOP
*
&LABEL   SR    R0,R0                    GET CODE
         SVC   232                IF YOU PLEASE.....
         AIF   ('&MF' NE 'L').FIN2                                 NTL*
         BR    &RET             RETURN TO CALLER
*
.FIN2    ANOP  ,                                                   NTL*
         MEXIT
.CODE    ANOP
         AIF   ('&MF(1)' NE 'E').OOPS
&LABEL   BAL   &RET,&MF(2)      INVOKE ROUTINE
*
         MEXIT
.OOPS    ANOP
 MNOTE 12,'NTL MODESET MF NOT=L OR (E,ADDR)   MF=&MF'
         MEND
         MACRO
         REGEQU
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         MEND
         MACRO
&LAB     LEAVER &WR=R13,&WL=WORKLEN
&LAB     LR    R1,&WR             WORKAREA ADDR FOR FREEMAIN
         LA    R0,&WL             WORKAREA LEN   "    "
         L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         LR    R11,R15            SAVE RETURN CODE
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R11            RESTORE RETURN CODE
         RETURN (14,12),RC=(15)
         MEND
         MACRO
&LAB     LEAVENR
&LAB     L     R13,4(R13)         GET CALLERS SAVEAREA ADDR
         RETURN (14,12),RC=(15)
         MEND
         MACRO
&LAB     KKPARSE &CPPL=R11,&ANS=ANS,&ECB=ECB,&PCL=PCL,&PPLA=PPLA
*                  DEFAULT OPERANDS :
*        KKPARSE &CPPL=R11,&ANS=ANS,&ECB=ECB,&PCL=PCL,&PPLA=PPLA
*                            CPPL IS THE REGISTER CONTAINING THE
*                                 POINTER TO THE COMMAND PROCESSOR
*                                 PARAMETER LIST.
*                            ANS  IS A FULLWORD WHERE PARSE RETURNS
*                                 THE POINTER TO HIS ANSWER (IKJPARMD).
*                            ECB  IS A FULLWORD.
*                            PCL  IS THE NAME OF THE PARSE CONTROL
*                                 LIST CSECT (IKJPARM).
*                            PPLA IS A 7 FULLWORD AREA USED FOR
*                                 THE PARSE PARAMETER LIST.
*
*        KKPARSE             USES REGS R14,R15,R1,&CPPL REG
*
         USING CPPL,&CPPL         POINTS TO CPPL (PARM LIST)
*
&LAB     LA    R1,&PPLA           R1->PPL AREA
         USING PPL,R1             TELL ASSEMBLER POINTS TO PPL AREA
*
*                         CONSTRUCT PARSE PARAMETER LIST
*                                           FOR IKJPARS :
*
         MVC   PPLUPT,CPPLUPT     UPT
         MVC   PPLECT,CPPLECT     ECT
         LA    R15,&ECB           ECB
         ST    R15,PPLECB
         L     R15,=V(&PCL.)      PCL
         ST    R15,PPLPCL
         LA    R15,&ANS           ANS
         ST    R15,PPLANS
         MVC   PPLCBUF,CPPLCBUF   CBUF
         XC    PPLUWA,PPLUWA
*
         DROP  R1
         EJECT
*        CALLTSSR EP=IKJPARS      CALL PARSE:
         CALLTSSR EP=IKJPARS      CALL PARSE:
         EJECT
*
*                        DSECTS NEEDED BY PARSE:
*
*CPPL    IKJCPPL
CPPL     IKJCPPL
*PPL     IKJPPL
PPL      IKJPPL
*
CVTMAP   DSECT
         ORG   CVTMAP+524
CVTPARS  DS    F
CVTPTR   EQU   16
*
*
&SYSECT  CSECT
         MEND
         MACRO
&LAB1    ENTERR &SA=SAVEAREA,&WA=WORKAREA,&WL=WORKLEN
&LAB1    START
         SAVE  (14,12),,*         HO-HUM
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         LR    R10,R1             SAVE PARM PTR R10->PARM PTR
         LA    R0,&WL             R0=GET LENGTH
         GETMAIN R,LV=(0)         R1->WORKAREA
         LR    R11,R13            R11->CALLERS SAVEAREA
         LR    R13,R1             R13->WORKAREA
         USING &WA.,R13
         ST    R11,&SA.+4         SAVE HIS SAVEAREA PTR
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         LR    R11,R1             R11->WORKAREA IN CASE NOT SAME AS R13
*                       WORKAREA ADDR IS STILL R13 FOR
*                       THE ASSEMBLER - IF DIFFERENT FROM R13
*                       THEN USE: DROP R13 AND USING &WA.,R11
         LR    R1,R10             RESTORE PARM PTR PTR
         REGEQU
         MEND
         MACRO
&LAB1    ENTERNR &SA=SAVEAREA
&LAB1    START
         SAVE  (14,12),,*         HO-HUM
         LR    R12,R15            HOPE HE KNOWS WHAT HE'S DOING
         USING &LAB1,R12
         ST    R13,&SA.+4         SAVE HIS SAVEAREA PTR
         LR    R11,R13            SAVE THE SAVE POINTER
         LA    R13,&SA            R13->SAVEAREA (MINE)
         ST    R13,8(,R11)        MINE IN HIS
         B     ENTEX
&SA      DS    18F
         REGEQU
ENTEX    DS    0H
         MEND
         MACRO
&LAB1    COMMENT &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K
         MEND
         MACRO  ,
&LABEL   STUFFIT &FIELD,&KEYWD,&PDE
         LCLC   &S
&S       SETC   '&SYSNDX'
         SPACE  1
         AIF   (T'&KEYWD EQ 'O').NOCHK
&LABEL   TM    &KEYWD+1,X'FF'  WAS FIELD REQUESTED?               NTL1
         BZ    SKPA&S          NO..SPLIT
         AGO   .FINCHK
.NOCHK   ANOP  ,
&LABEL   L     R7,&PDE         (R7)= ADDR OF PARSE DESCRIPTOR ENTRY
         AGO   .SKP1
.FINCHK  ANOP  ,
         L     R7,&PDE         (R7)= ADDR OF PARSE DESCRIPTOR ENTRY
.SKP1    ANOP  ,
         LH    R6,&PDE+4       (R6)= LENGTH OF ELEMENT
         BAL   R14,LOCVAR      GO FIND THE CLIST VARIABLE
         LTR   R1,R1           ANY LUCK?
         BZ    SKPA&S          NO.. SPLIT
         SPACE 1
         LA    R15,&FIELD      GET ADDR OF FIELD TO STUFF
         LH    R0,=AL2(L'&FIELD)   GET LENGTH OF FIELD TO STUFF   INTEL
         BAL   R14,SETVAR      GO STUFF IT AWAY INTO CLIST VAR
SKPA&S   DS    0H
         SPACE 1
         MEND  ,
         MACRO ,                                                    NTL
&LABEL   STUFFITN &NUMB,&KEYWD,&PDE                                 NTL
         SPACE 1                                                    NTL
         LCLA  &L                                                   NTL
&L       SETA  L'&NUMB         GET LENGTH OF NUMBER                 NTL
         SR    R1,R1           CLEAR NUMBER REGISTER                NTL
         AIF   (&L NE 1).NOT1                                       NTL
&LABEL   IC    R1,&NUMB        GET NUMBER (1 BYTE)                  NTL
         AGO   .DOIT                                                NTL
.NOT1    ANOP  ,                                                    NTL
         AIF   (&L NE 2).NOT2                                       NTL
&LABEL   LH    R1,&NUMB        GET NUMBER (HALFWORD)                NTL
         AGO   .DOIT                                                NTL
.NOT2    ANOP  ,                                                    NTL
         AIF   (&L NE 4).NOT4                                       NTL
&LABEL   L     R1,&NUMB        GET NUMBER (FULLWORD)                NTL
         AGO   .DOIT                                                NTL
.NOT4    ANOP  ,                                                    NTL
 MNOTE 8,'STUFFITN - LENGTH OF &NUMB IS &L - MUST BE 1,2 OR 4'      NTL
         MEXIT ,                                                    NTL
.DOIT    ANOP  ,                                                    NTL
         BAL   R14,FITINUM                                          NTL
 MNOTE '        STUFFIT NUMBER,&KEYWD,&PDE   INVOKE STUFFIT'        NTL
         STUFFIT NUMBER,&KEYWD,&PDE                                 NTL
         MEND  ,                                                    NTL
         PRINT ON   END OF INSTREAM MACROS
*
* TSO COMMAND   G E T I N F O
*
*     PURPOSE: STUFF JUNK FROM THE OS JCT/ACF2 JCT INTO CLIST
*              VARIABLES OF YOUR CHOICE.
*
*   SYNTAX - (ALL OPERANDS ARE OPTIONAL)
*                                 LENGTH  DESCRIPTION
*      GETINFO  PGMRNAME(VAR1) /*  20     PROGRAMMER NAME            */
*               BIN(VAR2)      /*   4     BIN NUMBER (SECOND ACNT FLD*/
*               PHONE(VAR3)    /*  12     USER PHONE NUMBER- ACF2    */
*               DEST(VAR4)     /*   8     DEFAULT SYSOUT DEST        */
*               JOBCLASS(VAR5) /*   1     ACF2 DFT-SUBC              */
*               MSGCLASS(VAR6) /*   1     ACF2 DFT-SUBM              */
*               SYSOUT(VAR7)   /*   1     ACF2 DFT-SOUT              */
*               PROJECT(VAR8)  /*   4     FIRST 4 CHARS OF ACNT FIELD*/
*               SRNUM(VAR9)    /*   4     LAST  4 CHARS OF ACNT FIELD*/
*               JOBNUM(VAR10)  /*   8     JES2 JOB NUMBER            */
*               LINE(VAR11)    /*   8     LOGON TERMAINAL NAME       */
*               DAY(VAR12)     /*   9     DAY OF THE WEEK FOR TODAY  */
*               JDATE(VAR13)   /*   6     JULIAN DATE IN YY.DDD FORM */
*               DATE(VAR14)    /*  13     DATE IN 'MON. DD, 19YY'    */
*               ROWS(VAR15)    /*   8     CUR # LINES/SCREEN         */
*               COLS(VAR16)    /*   8     CUR # CHARS/LINE           */
*               PROWS(VAR17)   /*   8     PRIMARY # LINES/SCREEN     */
*               PCOLS(VAR18)   /*   8     PRIMARY # CHARS/LINE       */
*               AROWS(VAR19)   /*   8     ALTERNATE # LINES/SCREEN   */
*               ACOLS(VAR20)   /*   8     ALTERNATE # CHARS/LINE     */
*               ATTR(VAR21)    /*   4    ='YES'-PROGRAM SYMB OK ELSE */
*                              /*        ='NO' -NO PROGRAM SYMB FEAT.*/
*               ENVIRON(VAR22) /*   8    ='BATCH', 'TSO' OR 'TONE4'  */
*               NOCOMPR        /*  KEYWORD- NO TRIM OF TRAILING BLNKS*/
*
         GBLB  &TONE4          TONE4 MVS DEPENDENT CODE             NTL
         GBLB  &SP3            SP1.3 VERSION OF JES2              INTEL
         GBLB  &ACF314         ACF2 3.1.4 VERSION INSTALLED       INTEL
&TONE4   SETB  0               1=YES, 0=NO                          NTL
&SP3     SETB  0               1=YES, 0=NO                        INTEL
&ACF314  SETB  1               1=YES, 0=NO                        INTEL
*
         AIF   (&ACF314).$SK1001                                  INTEL
         GBLC  &ACF2JCT    ACF 3.1.3                              INTEL
.$SK1001 ANOP  ,                                                  INTEL
          EJECT  ,                                                  NTL
*
*
*   VAR1-VAR22 ARE CLIST VARIABLE NAMES (WITHOUT THE & IN FRONT)    NTL
*   ALL VARIABLES MUST HAVE BEEN INITIALIZED BY A 'SET' STATMENT BEFORE
*   GET INFO IS INVOKED.
*
*   EXAMPLE:   RETURN PROGRAMMER NAME AND PHONE NUMBER:
*
*        SET &PHON = &STR(            )
*        SET &PRGN = &STR(                    )
*        GETINFO PGMR(PRGN) PHONE(PHON)
*
*
* RETURN-CODES : GETINFO RETURNS CODES IN CLIST VAR &LASTCC AS FOLLOWS;
*
*                       0 - NORMAL COMPLETION
*              X'20' = 32 - VARIABLE TRUNCATED - TOO SHORT FOR FIELD
*              X'40' = 64 - VARIABLE NOT FOUND
*              X'80' =128 - NOT RUNNING UNDER CONTROL OF CLIST
*                     256 - PARSE ERROR
*              NOTE: VALUES ARE ADDITIVE:
*                 A RETURN CODE OF 96 =X'60' INDICATES AT LEAST ONE
*                 VARIABLE WAS TRUNCATED, AND ANOTHER WAS NOT FOUND.
*
*  HISTORY:
*  V1.0    K TRUE     25 JUN 81  - INTEL CORP.
*      BASED ON 'KOMM' FROM KERMIT KISER OF WASHINTON STATE DP
*            SERVICE CENTER (WDPSC).  STOLEN FROM COMM WERE MACROS
*            AND MANIPULATION OF CLIST VARIABLES ROUTINES.
*                                                                   NTL
*  V1.1   K  TRUE     12 APR 82  - INTEL CORP                       NTL
*      ADDED KEYWORDS:                                              NTL
*            ROWS,COLS,PROWS,PCOLS,AROWS,ACOLS,ATTR,ENVIRON         NTL
*      ADDED TONE4 SUPPORT VIA &TONE4 GBLB VARIABLE.. CODE FOR      NTL
*            EXTRACTION OF TONE4 LINE IS BY BILL WILLIAMS (FROM     NTL
*            TSO043(WHODIS) COMMAND)                                NTL
*      INCLUDED MODESET LOCAL MACRO AS INSTREAM SO MAGIC SVC COULD  NTL
*            BE USED, OR REAL MODESET (AS YOU WISH)                 NTL
*  V1.2  K  TRUE     13 MAR 83   - INTEL CORP                       NTL
*      ADDED &SP3 FLAG FOR SP1.3 JES2...NEW ACF2 CONTROL BLOCKS..   NTL
*  V1.3  B WILLIAMS  08 OCT 84   - INTEL CORP                     INTEL
*      ADDED &ACF314 FLAG FOR ACF2  3.1.5 SUPPORT                 INTEL
*  V1.4  B WILLIAMS  30 OCT 85   - INTEL CORP                     INTEL
*      WITH &ACF314 FLAG SUPPORT JES2 1.3.4 INSTALL               NTL14
*  V1.5  B WILLIAMS  11 DEC 85   - INTEL CORP                     NTL15
*      MODIFY LOCVAR ROUTINE TO SUPPORT TSOE EXTENSION TABLE.     NTL15
*  V1.6  B WILLIAMS  27 FEB 86   - INTEL CORP                     NTL16
*      ADD SPLEVEL SET=1 FOR 370 MACRO EXPANSION (STAX)           NTL16
*                                                                   NTL
* ORIGINAL SOURCE WAS:                                              NTL
*          DATA SET CBT934     AT LEVEL 001 AS OF 02/25/79
*    FROM THE CBT MODS TAPE.                                        NTL
*                                                                   NTL
*                                                                   NTL
         EJECT
*.....................................................................*
*        STANDARD COMMAND PROCESSOR LINKAGE                           *
*.....................................................................*
         SPACE
GETINFO  ENTERR                   R13 -> WORKAREA
*                                 R12 -> GETINFO ADDRESSABILITY
         XC    RETCODE,RETCODE
         EJECT
*.....................................................................*
*        PARSE THE INPUT BUFFER                                       *
*.....................................................................*
         SPACE
         LR    R11,R1             SAVE THE PARM POINTER
         SPLEVEL SET=1                                            NTL16
*                                 R11 -> CPPL
DOPARSE  KKPARSE
*
*
         DROP  R11
ANALR    LTR   R15,R15            RC = 0? PARSE WENT OK?
         BNZ   PARSERR            NO. -> GO TO PARSE ERR
         L     R10,ANS            R10 -> IKJPARMD
         USING IKJPARMD,R10
         EJECT
         AIF   (&ACF314).$SKIP02                                  NTL14
*.....................................................................*
*        FIND JCT                                                     *
*.....................................................................*
         SPACE 1
JCT      EQU   R11           NEEDED BY $JCT MACRO FOR
         AIF   (&SP3).$SKIP01                                     NTL14
BUFDSECT EQU   *                 MAPPING OF THE
BUFSTART EQU   *+X'58'                GLORIOUS J C T
         AGO   .$SKP01E                                           INTEL
.$SKIP01 ANOP  ,                SP 1.3 JES2                       INTEL
         #ACFJES2    ,          ESTABLISH OFFSETS TO USE          INTEL
.$SKP01E ANOP  ,                                                  INTEL
         SPACE 1
FINDJCT  $FINDCB ASCB=R2,JCT=R11   FIND THE JCT AND ASCB ADDRESS
         EJECT ,                                                  INTEL
         ST    R2,ADDRASCB         SAVE FOR POSSIBLE LATER USE
         USING JCTSTART,JCT        ESTABLIST ADDRESSABILITY TO JCT
         SPACE 1                                                  INTEL
         AIF   (NOT &SP3).$SKIP02                                 NTL14
         ICM   R9,15,&ACF2JCT      TEST, GET ACF2 AREA OFFSET     INTEL
         LA    R9,0(JCT,R9)        CALCULATE ADDRESS              INTEL
         USING #ACFJCTX,R9         ADDRESSING                     INTEL
.$SKIP02 ANOP  ,                                                  NTL14
         AIF   (NOT &ACF314).$SK1003                              NTL14
         $FINDCB ASCB=R2                                          NTL15
         ST    R2,ADDRASCB                                        NTL15
         EJECT ,                                                  NTL15
         USING ACCVT,R3                ADDRESS ACF CVT            INTEL
         ACFGACVT R3,NONE=GETOUT      GET CB ADDRESSABILITY       INTEL
         USING ACUCB,R4               ADDRESS ACF UCB             INTEL
         ACFGUCB R4,NONE=GETOUT,INLINE=YES,SYS=AOS2 GET ACUCB ADDRESS
         LR    R11,R15                                            NTL14
         USING ACFASVT,R11                                        NTL14
         USING LIDREC,R9               ADDRESS ACF LID            INTEL
         ICM   R9,15,ACULRECP          GET ACF2 USER LID ADDRESS  INTEL
         BZ    GETOUT                  PUNT IF NONE               INTEL
         DROP  R3                     DROP ADDRESSABILITY         INTEL
         DROP  R4                     DROP ADDRESSABILITY         INTEL
         AGO   .$SK2002                                           NTL14
.$SK1003 ANOP  ,                                                  INTEL
         CLC   JCTID,=CL4'JCT'     IS THIS THE REAL JCT?            NTL
         BE    BEGIN               YES.....GO FOR IT                NTL
         OI    RETCODE+2,X'02'     SET RC=512                       NTL
         B     GETOUT              AND SPLIT                        NTL
         EJECT
.$SK2002 ANOP  ,                                                  NTL14
*.....................................................................*
*        PROCESS THE PARMS                                            *
*.....................................................................*
         SPACE
BEGIN    STUFFIT  LIDNAME,PGMRNAME,PGMRDD
         AIF   (NOT &ACF314).$SK2003                              NTL14
         STUFFIT  NTLROOMN,BINNUMB,BINDD                          NTL14
         AGO   .$SK2004                                           NTL14
.$SK2003 ANOP  ,                                                  NTL14
         STUFFIT  JCTROOMN,BINNUMB,BINDD
.$SK2004 ANOP  ,                                                  NTL14
         STUFFIT  LIDPHONE,PHONE,PHONDD
         EJECT   ,
         STUFFIT  LIDTDRMT,DEST,DESTDD
         STUFFIT  LIDTSUBC,JOBCLASS,JOBCDD
         STUFFIT  LIDTSUBM,MSGCLASS,MSGCDD
         EJECT   ,
         STUFFIT  LIDTSOUT,SYSOUT,SYSODD
         STUFFIT  NTLPROJ,PROJECT,PROJDD
         STUFFIT  NTLSRNUM,SRNUM,SRNUDD
         EJECT   ,
         AIF   (NOT &ACF314).$SK2005                              NTL14
         STUFFIT  ASVJID,JOBNUMB,JOBNDD                           NTL14
         AGO   .$SK2006                                           NTL14
.$SK2005 ANOP  ,                                                  NTL14
         STUFFIT  JCTJOBID,JOBNUMB,JOBNDD
.$SK2006 ANOP  ,                                                  NTL14
         BAL      R14,GETLINE#
         STUFFIT  LINEAREA,LINE,LINEDD
         STUFFIT  VLOGMODE,LOGMODE,LMODDD                           NTL
         EJECT   ,
         STUFFITN TTYPE,TERMTYPE,TTYPDD                             NTL
NOLINE   DS       0H
         TM       DAY+1,X'FF'      WAS DAY OF WEEK REQUESTED
         BZ       NODAY
         BAL      R14,DATERTN
         STUFFIT  LEFTDAY,,DAYXDD
NODAY    DS       0H
         EJECT
         TM       JDATEV+1,X'FF'   WAS JULIAN DATE REQUESTED?
         BZ       NOJDATE
         BAL      R14,DATERTN
         STUFFIT  JDATE,,JDATDD
NOJDATE  DS       0H
         SPACE
         TM       GDATEV+1,X'FF'  WAS GREGORIAL DATE REQUESTED?
         BZ       NOGDATE
         BAL      R14,DATERTN
         STUFFIT  CDATE,,DATEDD
NOGDATE  DS       0H
*
         EJECT
*.....................................................................*
*        TERMINAL CHARACTERISTICS                                     *
*.....................................................................*
         SPACE
         MVC      WHERAMI,=CL8'TSO'   INDICATE UNDER TSO            NTL
         L        R1,ADDRASCB         GET ASCB ADDRESS WE SAVED OFF NTL
         CLI      ASCBTSB-ASCB(R1),X'C0' IS THIS TONE4 LAND?        NTL
         BNE      NOTTONE4                                          NTL
         MVC      WHERAMI,=CL8'TONE4' THIS IS TONE                  NTL
         B        STUFFAMI                                          NTL
NOTTONE4 CLC      ASCBTSB-ASCB(,R1),=XL4'00' NO POINTER?            NTL
         BNE      STUFFAMI            SOME POINTER..MUST BE TSO     NTL
         MVC      WHERAMI,=CL8'BATCH' MUST BE BATCH....             NTL
STUFFAMI STUFFIT  WHERAMI,ENVIRON,ENVDD  PUT ENVIRONMENT AWAY       NTL
         XC       PRMSZE(CLRITL),PRMSZE                             NTL
         GTSIZE   ,  GET CURRENT ROWS/COLS FOR TERMINAL             NTL
         STC      R0,CURROWS     SAVE CURRENT # LINES PER SCREEN    NTL
         STC      R1,CURCOLS     SAVE # CHARS/LINE                  NTL
         LA       R15,PRMSZE                                        NTL
         ST       R15,GTTERM                                        NTL
         LA       R15,ALTSZE                                        NTL
         ST       R15,GTTERM+4                                      NTL
         LA       R15,ATTRIB                                        NTL
         ST       R15,GTTERM+8                                      NTL
         MVI      GTTERM+8,X'80'                                    NTL
         GTTERM   MF=(E,GTTERM)                                     NTL
         STC      R15,GTTERMRC   SAVE OFF RETURN CODE               NTL
         EJECT    ,                                                 NTL
         STUFFITN CURROWS,ROWS,ROWSDD                               NTL
         STUFFITN CURCOLS,COLS,COLSDD                               NTL
         EJECT    ,                                                 NTL
*        OC       GTTERMRC,GTTERMRC   GTTERM WORK OK?               NTL
*        BNZ      GETOUT              NO...CANCEL                   NTL
         STUFFITN PRMROWS,PROWS,PROWSDD                             NTL
         STUFFITN PRMCOLS,PCOLS,PCOLSDD                             NTL
         EJECT    ,                                                 NTL
         STUFFITN ALTROWS,AROWS,AROWSDD                             NTL
         STUFFITN ALTCOLS,ACOLS,ACOLSDD                             NTL
         EJECT    ,                                                 NTL
         MVC      ATTRYORN,=CL4'NO'                                 NTL
         OC       ATTRIB+3(1),ATTRIB+3 CHECK OUT EXTENDED SYMBOLS   NTL
         BZ       STUFFATT                                          NTL
         MVC      ATTRYORN,=CL4'YES'  SET UP EXTENDED ATTRIBUTES    NTL
STUFFATT STUFFIT  ATTRYORN,ATTR,ATTRDD                              NTL
         SPACE   2
*.....................................................................*
*        EXIT                                                         *
*.....................................................................*
         SPACE
GETOUT   L     R15,RETCODE
         LEAVER                   EXIT REENTRANT ROUTINE
         SPACE 2
*
*       GETINFO SUBROUTINES HERE
*
PARSERR  EQU   *
         OI    RETCODE+2,X'01'    RC=256
         B     GETOUT
*
         EJECT
*.....................................................................*
*  SUBROUTINE LOCVAR:                                                 *
*  PURPOSE: LOCATE ADDRESS OF VARABLE IN CLIST TO POKE STUFF INTO.    *
*                                                                     *
*  ENTER WITH :                                                       *
*                                                                     *
*       (R7) = ADDR PARSE VARIABLE ENTRY (LABEL ON IKJIDENT FOR       *
*              SUBFIELD TO BE EXAMINED).                              *
*       (R6) = LENGTH OF VARIABLE TO EXAMINE                          *
*                                                                     *
*       BAL R14,LOCVAR                                                *
*                                                                     *
* ON RETURN:  (R1) = 0   NOT FOUND                                    *
*             (R1) ^=0   (R1) = ADDR OF CLIST VARIABLE ELEMENT        *
*                                                                     *
*   REGISTERS USED:  R3,R4,R5,R6,R7,R8 (RESTORED)                     *
*                    R1                (NOT RESTORED)                 *
*.....................................................................*
         SPACE
LOCVAR   STM   R3,R8,SAVEREGS
         L     R5,ECTADDR         R5->ECT
         L     R5,4(,R5)          R5->IOSRL
         L     R4,0(,R5)          R4->TOP OF STACK
CKCLIST  TM    0(R4),X'08'        CLIST ELEMENT?
         BO    GOTCL              YES->WE HAVE A CLIST
         S     R4,=F'4'           R4=R4-4 (NEXT ELEMENT DOWN ON STACK)
         C     R4,4(,R5)          R4<BOTTOM OF STACK PTR?
         BNL   CKCLIST            NO ->STILL IN STACK; GO CHECK IT
         OI    RETCODE+3,X'80'    YES->SET RC=128
         LA    R1,0               NO POINTER FOR THE WICKED
         LM    R3,R8,SAVEREGS     RESTORE REGISTERS
         BR    R14                EXIT LOCVAR WITH ERROR
         SPACE 1
GOTCL    DS    0H                 R7->VARIABLE NAME
*                                 R6=VAR LENGTH FOR LENGTH COMPARE
         L     R5,0(,R4)          R5->LSD
         L     R5,12(,R5)         R5->EXECDATA
         L     R5,0(,R5)          R5->SNTAB
         L     R4,8(,R5)          R4==USED TABLE LENGTH
         LA    R4,0(R4,R5)        R4->END OF TABLE USED
         LA    R3,12(,R5)         R3->FIRST SNTAB ENTRY
         LR    R1,R6              R1=R6  VARIABLE NAME LENGTH
         BCTR  R1,R0              R1=R1-1 FOR EXECUTED COMPARE
         EJECT ,
RLOOP    LH    R2,6(,R3)          R2=NAME LENTH
         CR    R6,R2              SAME LENGTH?
         BNE   LOOPCHK            NO -> CHECK DONE
         EX    R1,EXCLC           COMPARE NAMES
         BE    GOTIT
LOOPCHK  LA    R3,8(R2,R3)        POINT TO NEXT ENTRY
         CR    R3,R4              DONE?
         BL    RLOOP              NO->CONTINUE
         SPACE 1
         ICM   R5,15,0(R5)        R5-> EXTENSION TABLE ADDRESS    NTL15
         BZ    RERR               NO-> GO SET ERROR               NTL15
         L     R4,8(,R5)          R4-> USED TABLE LENGTH          NTL15
         LA    R4,0(R4,R5)        R4-> END OF TABLE USED          NTL15
         LA    R3,12(,R5)         R3-> FIRST SNTAB ENTRY          NTL15
         B     RLOOP              CONTINUE WITH EXTENSION TBL     NTL15
         SPACE 1                                                  NTL15
RERR     LA    R1,0               SET NO FOUND AND SPLIT
         OI    RETCODE+3,X'40'    YES->SET RC=64
         LM    R3,R8,SAVEREGS     RESTORE REGISTERS
         BR    R14                EXIT LOCVAR WITH ERROR
         SPACE 1
GOTIT    EQU   *
         L     R1,0(,R3)          GET VALUE ADDR
         LM    R3,R8,SAVEREGS     RESTORE REGISTERS
         BR    R14                RETURN
         SPACE 1
EXCLC    CLC   0(0,R7),8(R3)      EXECUTED
         EJECT
*.....................................................................*
* SUBROUTINE SETVAR:                                                  *
*                                                                     *
* PURPOSE:  MOVE STUFF FROM DESIRED FIELD INTO CLIST VARIABLE FIELD   *
*                                                                     *
* ON ENTRY (R1) = ADDR CLIST VAR POINTER                              *
*          (R15)= ADDRESS OF INPUT FIELD (THE MOVEE...)               *
*          (R0) = LENGTH OF INPUT FIELD (L'MOVEE......)               *
*                                                                     *
*   BAL  R14,SETVAR                                                   *
*                                                                     *
*   ON RETURN:                                                        *
*                                                                     *
*.....................................................................*
         SPACE
*
SETVAR   DS    0H                 R1->VALUE TABLE ENTRY
         STM   R2,R4,SAVEREGS
         ST    R14,SETVR14        SAVE RETURN ADDRESS             INTEL
         LR    R14,R0             LOAD LENGTH INTO WORK REGISTER  INTEL
ROOMCHK  CH    R14,2(,R1)         ROOM FOR INPUT?                 INTEL
         BNH   SM                 YES->MOVE IT.
         OI    RETCODE+3,X'20'    SET RC=32
         LH    R14,2(,R1)         NO ->GET HIS LENGTH
SM       BCTR  R14,0              R14=R14-1 = MACHINE LENGTH
         SPACE 1
         TM    NOCOMPR+1,X'FF'    WAS NO COMPRESS WANTED?
         BNZ   SKIPCPRS           ..DONT DO COMPRESS
DEBLANK  LA    R2,0(R15,R14)      POINT TO LAST CHARACTER TO MOVE
         CLI   0(R2),C' '         IS THIS ONE A BLANK
         BNE   SKIPCPRS           NO..FINISHED WITH THIS ONE
         BCT   R14,DEBLANK        LOOP UNTIL DONE
SKIPCPRS DS    0H
         SPACE 1
         CLI   0(R15),C' '        IS FIRST CHAR A BLANK?
         BE    SETNULL            YEP...SKIP MOVE AND SET LENG=0
         EX    R14,SMOVE          MOVE IT
         LA    R14,1(,R14)        BUMP REAL COUNT
UPDTCNT  STH   R14,0(,R1)         AND SAVE COUNT AWAY
         LM    R2,R4,SAVEREGS
         LR    R0,R14             PASS BACK LENGTH USED           INTEL
         L     R14,SETVR14        RESTORE RETURN ADDRESS          INTEL
         BR    R14
         SPACE 1
SETNULL  SR    R14,R14
         B     UPDTCNT
         SPACE 1
SMOVE    MVC   4(0,R1),0(R15)     EXECUTED
         EJECT
*.....................................................................*
* SUBROUTINE: GETLINE#                                                *
*                                                                     *
* PURPOSE: GET TCAM UCB ADDRESS OR VTAM TERMINAL ID FOR THIS ADDRESS  *
*          SPACE                                                      *
*                                                                     *
* ENTER WITH (ASCBADDR) CONTAINING THE ASCB ADDRESS                   *
*                                                                     *
*      BAL   R14,GETLINE#                                             *
*                                                                     *
* RETURNS WITH 'LINEAREA' FILLED IN                                   *
*                                                                     *
*.....................................................................*
         SPACE
GETLINE# STM   R3,R8,SAVEREGS
         ST    R14,GETLR14          SAVE RETURN ADDRESS           INTEL
         MVC   VLOGMODE,BLANKS                                      NTL
         XC    TTYPE,TTYPE                                          NTL
         L     R7,ADDRASCB        RETRIEVE ASCB ADDRESS
         USING ASCB,R7
         AIF   (NOT &TONE4).$SKP10                                TONE4
*                                                                 TONE4
*     THE FOLLOWING WILL CHECK ASCBTSB IF MARKED BY TONE 4.       TONE4
*     IF ITS A TONE ADDRESS SPACE THEN IT WILL GET THE USER PROC  TONE4
*     FROM THE TIOT AND DROP ADDRESS TO ASCB CSCB, LOCATE THE     TONE4
*     THE TONE CREATED ASCB CSCB FOR THIS USER AND ENSURE         TONE4
*     ADDRESSABILITY. W WILLIAMS - APRIL 82 - INTEL CORP          TONE4
*                                                                 TONE4
         CLI   ASCBTSB,X'C0'      IS IT A TONE ADDRESS SPACE      TONE4
         BNE   NOTONE             NO                              TONE4
         $FINDCB TCB=R5           GET CURRENT TCB                 TONE4
         CLI   TCBUSER(R5),C'T'   MARKED BY ATTACH                TONE4
         BNE   NOTONE             NO....                          TONE4
         L     R5,TCBJSTCB(,R5)   GET JOBSTEP TCB                 TONE4
         CLI   TCBUSER(R5),C'T'   MARKED BY ATTACH                TONE4
         BNE   NOTONE             NO....                          TONE4
         ICM   R5,7,TCBUSER+1(R5) GET TCBUSER                     TONE4
         BNZ   ITSTONE            WE GOT SOMETHING....            TONE4
         B     NOTONE             NO ONE HOME...                  TONE4
         SPACE 2                                                  TONE4
         USING ISCCHAIN,R5        ADDRESS CHAINING BLOCK          TONE4
ITSTONE  CLC   ISCTSKID,=CL8'TONE'      IS THIS TPCDATA           TONE4
         BNE   NOTONE                   NO....                    TONE4
         CLC   ISCBLKID,=CL8'TPCDATA'   IS THIS BLOCK TPCDATA     TONE4
         BNE   NOTONE                   NO....                    TONE4
         ICM   R5,7,ISCBLOCK      GET TPCDATA ADDRESS             TONE4
         BZ    NOTONE             NO ONE HOME....                 TONE4
         SPACE 1                                                    NTL
         USING TPCDATA,R5         ADDRESS TPCDATA                 TONE4
         L     R8,TPCASCB         GET TONES ASCB FOR OUR TONE SESSTONE4
         MVC   LINEAREA(8),TPCLTERM  OK... GET LINE FROM TONE     TONE4
         B     LINEFIN                                            TONE4
         SPACE 1                                                  TONE4
TCBUSER  EQU   X'0A8',4          TCBUSER FIELD FROM TCB           TONE4
TCBJSTCB EQU   X'07C',4          TCBJSTCB FIELD FROM TCB          TONE4
         SPACE 1                                                  TONE4
NOTONE   DS    0H                                                 TONE4
.$SKP10  ANOP  ,                                                  TONE4
         L     R7,ASCBTSB         GET ADDRESS OF TSB FOR LINE ADDR
         USING TSB,R7             USE THE ADDRESS
*                                                                   NTL
* JUST FOR CHUCKLES....SEE IT THIS IS BATCH                         NTL
*                                                                   NTL
         LTR   R7,R7              ANY TSB ADDRESS THERE?            NTL
         BNZ   DOSTAX             YES..CONTINUE                     NTL
         MVC   LINEAREA(8),BLANKS NO LINE HERE                      NTL
         B     LINEFIN                                              NTL
         SPACE 1                                                    NTL
DOSTAX   STAX  MF=(E,DEFER)       HOLD ALL CALLS PLEASE....
         LA    R0,1
         SVC   232
         MODESET MF=(E,SUPER)
         ST    1,OLDKEY
*
         TM    TSBFLG5,TSBVTAM    SEE IF THIS IS A TSO/VTAM TSB
         BO    ITSVTAM            YES....GO GET TSB EXTENSION
ITSTCAM  UNPK  TCAMUCB(5),TSBLINE(3) UNPACK THE TSO LINE ADDRESS
         NC    TCAMUCB,HEXMASK
         TR    TCAMUCB,HEXTAB
         MVC   LINEAREA(3),=C'UCB'
         MVI   TCAMUCB,C'='       FIX IT UP PRETTY
         MVI   TCAMUCB+4,C' '
         B     FINSUPR
         SPACE 1
ITSVTAM  L     R8,TSBEXTNT         GET ADDRESS OF TSB EXTENSION
         USING TSBX,R8             ESTABLISH ADDRESSABILITY TO TSBX
         TM    TSBXFLG1,TSBXWREC   SEE IF TERMINAL AWAITING RECON
         BNO   GETSYM              NO.....GO GET SYMBOLIC NAME
         MVC   LINEAREA(8),DISCON  MOVE IN DISCON'D
         B     FINSUPR             GO MODESET AND RESTORE OLD KEY
GETSYM   MVC   LINEAREA(8),TSBTRMID MOVE IN TERMINAL SYMBOLIC NAME
         MVC   VLOGMODE,TSBXLMOD   SAVE LOGMODE NAME                NTL
         MVC   TTYPE,TSBXTMTP      SAVE TERMINAL TYPE               NTL
*
         DROP  R8
FINSUPR  DS    0H
         SR    R0,R0
         MODESET MF=(E,PROBLEM)
         STAX  MF=(E,RESUME)      OK TO HAVE ATTENTIONS NOW...
*
         DROP  R7
LINEFIN  LM    R3,R8,SAVEREGS
         L     R14,GETLR14          RESTORE RETURN ADDRESS        INTEL
         BR    R14
         EJECT ,
***********************************************************************
*
* FORMAT DATE TIME  HEADING FROM SMF30TME/SMF30DTE
*   R0=TIME IN 100THS OF SECOND SINCE MIDNIGHT
*   R1=DATE IN 00YYDDDS FORMAT
*
* RETURNS WITH CDAYN(LEN) = DDMONYY (DDD) DAY HH:MM:SS.TH
*                E.G.       07JUL80 (189) MON 13:45:24.56
*                           123456789.123456789.123456789
*
***********************************************************************
DATERTN  CLI   JDATE,C'0'   HAVE WE BEEN HERE BEFORE
         BHR   R14           ALREADY DONE THIS STUFF...KISS OFF
         STM   R14,R3,SAVEREGS
         MVC   MONMSK(MVCLEN),XMONMSK  INIT DATE/TIME WORK AREA
         TIME  BIN ,
         ST    R1,DATE                 SAVE DATE FOR LATER
         CVD   R0,WRKD                GET TIME IN DEC 100TH/SEC
         DP    WRKD(8),DEC6000(3) /6000 : REM IS SS.TH
         DP    WRKD(5),DEC60(2)       /60   : REM IS MM
         MVC   EDITWORK(L'HHMASK),HHMASK   SETUP MULT EDIT MASK
         ED    EDITWORK(L'HHMASK),WRKD EDIT HH,MM,SS
FINTIME  MVC   CHH(2),EDITWORK+4      MOVE IN HOURS
         MVC   CMM(2),EDITWORK+8      MOVE IN MM
         MVC   CSS(5),EDITWORK+12     MOVE IN SS.TH
*
***FORMAT THE DATE INTO THE OUTPUT MSG
*
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.
         XC    WRKD,WRKD
         MVC   WRKD+4(4),WORKWORD      CNVT YEAR TO BIN
         CVB   R0,WRKD
         ST    R0,BINYEAR              AND SAVE
         TM    BINYEAR+3,X'03'         LEAP YEAR?
         BNZ   MON1                    NO.
         MVI   MONMSK+10,29            YES, CORRECT
MON1     XC    WRKD,WRKD               CNVT DAYS TO BIN
         MVC   WRKD+6(2),DATE+2
         CVB   R3,WRKD
         ST    R3,BINDAYS              SAVE FOR LATER
         XR    R1,R1
         LA    R15,11
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY
         SR    R3,R1
         BNP   MONOVR
         BCT   R15,MON2
         B     *+6
         EJECT ,
MONOVR   AR    R3,R1                   CORRECT OVERDRAW
         CVD   R3,WRKD                 FORMAT DAY
         UNPK  CDAYN(2),WRKD+6(2)
         OI    CDAYN+1,X'F0'           FIX UP SIGN
         MH    R15,=H'3'               GET TABLE OFFSET
         LA    R15,MONTAB(R15)         AND POINT AT MONTH
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG
***DAY OF WEEK
         L     R15,BINYEAR
         SH    R15,=H'69'
         BNP   OUTT                    MUST BE GREATER OR BAD
         XR    R14,R14                 CLEAR DAY REG
         D     R14,=F'4'
         MH    R15,=H'5'
         AR    R14,R15
         A     R14,BINDAYS
         SRDA  R14,32
         D     R14,=F'7'               MODULO 7 FOR WEEK
         MH    R14,=H'09'
         LR    R0,R14                  SAVE THIS FOR LATER
         LA    R14,DAYTAB(R14)         POINT AT TODAY
         MVC   CDAY(09),0(R14)         INSERT IN MSG
         LR    R14,R0
         LA    R14,DAYTAB1(R14)        POINT AT TODAY-LEFT JUSTIFIED
         MVC   LEFTDAY,0(R14)          INSERT IN MSG
***
MOVEJD   MVC   JDATE-1(7),=X'4020204B202020'
         ED    JDATE-1(7),DATE+1
         MVI   JDATE-1,C'('          RESTORE NICE FORMAT....
         MVC   CJDAY(3),JDATE+3         MOVE DAY TO MESSAGE
*
*
DATEFIND LM    R14,R3,SAVEREGS
         BR    R14                     RETURN TO MAINLINE
*
OUTT     MVC   CDAY(9),=CL9'(UNKNOWN)'
         B     MOVEJD
         EJECT ,
*...................................................................NTL
*                                                                   NTL
* FITINUM SUBROUTINE - CONVERT BIN NUMBER TO NICE FORMAT            NTL
*                                                                   NTL
* ENTER WITH <R1>  = NUMBER IN BINARY                               NTL
*            BAL   R14,FITINUM                                      NTL
*                                                                   NTL
*                                                                   NTL
*       USES R1,R14,R15                                             NTL
*.................................................................. NTL
FITINUM  CVD   R1,WRKD        CONVERT TO PACKED DECIMAL             NTL
FITINUM1 MVC   EDITWORK(L'NORMAL),NORMAL INITIALIZE THE EDIT FORMAT NTL
         MVC   NUMBER,BLANKS  CLEAR OUT NUMBER                      NTL
         LA    R1,EDITWORK+SIGNORM IN CASE OF ZEROES                NTL
         EDMK  EDITWORK(L'NORMAL),WRKD+2 CONVERT TO EBCDIC          NTL
         ST    R14,FITNR14     SAVE RETURN ADDRESS                INTEL
         LA    R14,EDITWORK+L'NORMAL-1 A(END OF CONVERTED NUMBER)   NTL
         SLR   R14,R1         LENGTH OF THE CONVERTED NUMBER - 1    NTL
         EX    R14,MVNUMBER   PUT THE NUMBER IN THE MSG             NTL
         LA    R15,1(R14,R15) A(NEXT SPOT IN MSG)                   NTL
         L     R14,FITNR14    RESTORE RETURN ADDRESS              INTEL
         BR    R14            RETURN TO OUR CALLER                  NTL
         SPACE 3                                                    NTL
MVNUMBER MVC   NUMBER(0),0(R1) MOVE NUMBER TO 'NUMBER' AREA         NTL
         SPACE 2                                                    NTL
NORMAL   DC    X'402020202020202020202020' EDIT MASK                NTL
SIGNORM  EQU   11             OFFSET TO LAST DIGIT                  NTL
         EJECT ,
***********************************************************************
*
*       CONSTANTS FOR DATE ROUTINE USAGE
*
***********************************************************************
         SPACE ,
HHMASK   DC    X'40202021202022212020222120204B2020'
DEC60    DC    X'060C'
DEC6000  DC    X'06000C'
DEC50    DC    X'050C'
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES
*
XMONMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )
         DC    CL1' '                                          )
*                                                              )
*                 YY.DDD XXXXXXXXX, MON. DD, 19NN    L  T      )
*                0123456789.123456789.123456789.12   E  O      )
         DC    C' YY.DDD XXXXXXXXX, MON. DD, 19NN)'  A  G      )
         DC    CL3' ',C'  '                          E  E      )
         DC    CL2' ',C':'                           V  T      )
         DC    CL2' ',C':'                           E  H      )
         DC    CL5'SS.TH',C'  '                         E      )
*                                                       R      )
MVCLEN   EQU   *-XMONMSK
*
***********************************************************************
DAYTAB   DC    CL09'  TUESDAY'
         DC    CL09'WEDNESDAY'
         DC    CL09' THURSDAY'
         DC    CL09'   FRIDAY'
         DC    CL09' SATURDAY'
         DC    CL09'   SUNDAY'
         DC    CL09'   MONDAY'
DAYTAB1  DC    CL09'TUESDAY'
         DC    CL09'WEDNESDAY'
         DC    CL09'THURSDAY'
         DC    CL09'FRIDAY'
         DC    CL09'SATURDAY'
         DC    CL09'SUNDAY'
         DC    CL09'MONDAY'
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
         SPACE
* OTHER  CONSTANTS                                                    *
         SPACE
HEXMASK  DC    4X'0F'
HEXTAB   DC    C'0123456789ABCDEF'
STARTING DC    CL8'STARTING'
DISCON   DC    CL8'DISCON''D'
BLANKS   DC    CL24' '       A BLANK LINE......                     NTL
         PRINT OFF
         DS    0F
CODE     DC    X'ABCFEDAD'
         PRINT ON
         EJECT
*.....................................................................*
*        LIST FORMS OF MACROS                                         *
*.....................................................................*
         SPACE 2
         DS    0F
DEFER    STAX  DEFER=YES,MF=L            HOLD DEM ATTENTIONS...
         SPACE 2
RESUME   STAX  DEFER=NO,MF=L             RESUME ATTENTION HANDLING..
         SPACE 2
GTTERME  GTTERM ATTRIB=*-*,PRMSZE=*-*,ALTSZE=*-*,MF=L               NTL
         SPACE 2                                                    NTL
SUPER    MODESET KEY=ZERO,MODE=SUP,MF=L                             NTL
         SPACE 2                                                    NTL
PROBLEM  MODESET KEY=NZERO,MODE=PROB,MF=L                           NTL
         EJECT
*.....................................................................*
*        LITERAL POOL                                                 *
*.....................................................................*
         SPACE
         LTORG
         EJECT
*.....................................................................*
*        DYNAMIC STORAGE AREA -- GETMAINED                            *
*.....................................................................*
         SPACE
WORKAREA DSECT
SAVEAREA DS    18F
SAVEREGS DS    16F
RETCODE  DS    F
SETVR14  DS    F        RETURN REGISTER FOR SETVAR                INTEL
GETLR14  DS    F        RETURN REGISTER FOR GETLINE#              INTEL
FITNR14  DS    F        RETURN REGISTER FOR FITINUM               INTEL
*
ANS      DC    F'0'
ECB      DC    F'0'
PPLA     DS    7F
ECTADDR  EQU   PPLA+4,4
*
VLOGMODE DS    CL8                                                  NTL
TTYPE    DS    XL2                                                  NTL
LINEAREA DS    CL8,CL2 PLUS PADDING....
TCAMUCB  EQU   LINEAREA+3,8
ADDRASCB DS    F
*
OLDKEY   DC    F'0'
LEFTDAY  DC    CL9' '
PRMSZE   DS    0H                                                   NTL
PRMROWS  DS    AL1                                                  NTL
PRMCOLS  DS    AL1                                                  NTL
ALTSZE   DS    0H                                                   NTL
ALTROWS  DS    AL1                                                  NTL
ALTCOLS  DS    AL1                                                  NTL
GTTERMRC DS    X                                                    NTL
         DS    X                                                    NTL
CURROWS  DS    AL1                                                  NTL
CURCOLS  DS    AL1                                                  NTL
ATTRIB   DS    F                                                    NTL
CLRITL   EQU   *-PRMSZE   FOR XC CLEAR                              NTL
ATTRYORN DS    CL4                                                  NTL
WHERAMI  DS    CL8                                                  NTL
GTTERM   GTTERM ATTRIB=*-*,PRMSZE=*-*,ALTSZE=*-*,MF=L               NTL
GTTERML  EQU   *-GTTERM                                             NTL
WRKD     DC    D'0'
NUMBER   DS    CL8            FINAL PRODUCT OF CONVERT              NTL
         DS    CL8            FOR EXTRA STUFF                       NTL
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
DATE     DC    F'0'
WORKWORD DC    2F'0'
EDITWORK DC    12H'0'
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES
*
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )
         DC    CL1' '                                          )
*                                                              )
*                 YY.DDD XXXXXXXXX, MON. DD, 19NN    L         )
*                0123456789.123456789.123456789.12   E         )
CDATMSG  DC    C' YY.DDD XXXXXXXXX, MON. DD, 19NN)'  A         )
JDATE    EQU   CDATMSG+01,6                          V         )
CDAY     EQU   CDATMSG+08,9                          E         )
CMON     EQU   CDATMSG+19,3                                    )
CDAYN    EQU   CDATMSG+24,2                          T         )
CYR      EQU   CDATMSG+30,2                          O         )
CDATE    EQU   CDATMSG+19,13
*                                                    G         )
CJDAY    DC    CL3' ',C') '                          E         )
CHH      DC    CL2' ',C':'                           T         )
CMM      DC    CL2' ',C':'                           H         )
CSS      DC    CL5'SS.TH',C' )'                      E         )
*                                                    R         )
***********************************************************************
LEN      EQU   *-CDATMSG
WORKLEN  EQU   *-WORKAREA
         SPACE
GETINFO  CSECT
         EJECT
*.....................................................................*
*        PARSE PARAMETER LIST - KEYWORDS                              *
*.....................................................................*
         SPACE
*
         PRINT     NOGEN
PCL      IKJPARM
PGMRNAME IKJKEYWD
         IKJNAME   'PGMRNAME',SUBFLD=PGMFLD
BINNUMB  IKJKEYWD
         IKJNAME   'BIN',SUBFLD=BINFLD
PHONE    IKJKEYWD
         IKJNAME   'PHONE',SUBFLD=PHONFLD
DEST     IKJKEYWD
         IKJNAME   'DEST',SUBFLD=DESTFLD
JOBCLASS IKJKEYWD
         IKJNAME   'JOBCLASS',SUBFLD=JOBCFLD
MSGCLASS IKJKEYWD
         IKJNAME   'MSGCLASS',SUBFLD=MSGCFLD
SYSOUT   IKJKEYWD
         IKJNAME   'SYSOUT',SUBFLD=SYSOFLD
PROJECT  IKJKEYWD
         IKJNAME   'PROJECT',SUBFLD=PROJFLD
SRNUM    IKJKEYWD
         IKJNAME   'SRNUM',SUBFLD=SRNUFLD
JOBNUMB  IKJKEYWD
         IKJNAME   'JOBNUMB',SUBFLD=JOBNFLD
LINE     IKJKEYWD
         IKJNAME   'LINE',SUBFLD=LINEFLD
DAY      IKJKEYWD
         IKJNAME   'DAY',SUBFLD=DAYXFLD
JDATEV   IKJKEYWD
         IKJNAME   'JDATE',SUBFLD=JDATFLD
GDATEV   IKJKEYWD
         IKJNAME   'DATE',SUBFLD=DATEFLD
NOCOMPR  IKJKEYWD  ,
         IKJNAME   'NOCOMPR'
         EJECT
ROWS     IKJKEYWD
         IKJNAME   'ROWS',SUBFLD=ROWSFLD
COLS     IKJKEYWD
         IKJNAME   'COLS',SUBFLD=COLSFLD
PROWS    IKJKEYWD
         IKJNAME   'PROWS',SUBFLD=PROWSFLD
PCOLS    IKJKEYWD
         IKJNAME   'PCOLS',SUBFLD=PCOLSFLD
AROWS    IKJKEYWD
         IKJNAME   'AROWS',SUBFLD=AROWSFLD
ACOLS    IKJKEYWD
         IKJNAME   'ACOLS',SUBFLD=ACOLSFLD
ATTR     IKJKEYWD
         IKJNAME   'ATTR',SUBFLD=ATTRFLD
LOGMODE  IKJKEYWD
         IKJNAME   'LOGMODE',SUBFLD=LMODFLD
TERMTYPE IKJKEYWD
         IKJNAME   'TERMTYPE',SUBFLD=TTYPFLD
ENVIRON  IKJKEYWD
         IKJNAME   'ENVIRON',SUBFLD=ENVFLD
         EJECT
*.....................................................................*
*        PARSE PARAMETER LIST - SUBFIELDS                             *
*.....................................................................*
         SPACE
PGMFLD   IKJSUBF
PGMRDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
BINFLD   IKJSUBF
BINDD    IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
PHONFLD  IKJSUBF
PHONDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
DESTFLD  IKJSUBF
DESTDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
JOBCFLD  IKJSUBF
JOBCDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
MSGCFLD  IKJSUBF
MSGCDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
SYSOFLD  IKJSUBF
SYSODD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
PROJFLD  IKJSUBF
PROJDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
SRNUFLD  IKJSUBF
SRNUDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
JOBNFLD  IKJSUBF
JOBNDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
LINEFLD  IKJSUBF
LINEDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
DAYXFLD  IKJSUBF
DAYXDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
JDATFLD  IKJSUBF
JDATDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
DATEFLD  IKJSUBF
DATEDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
          EJECT   ,                                                 NTL
ROWSFLD  IKJSUBF
ROWSDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
COLSFLD  IKJSUBF
COLSDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
PROWSFLD IKJSUBF
PROWSDD  IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
PCOLSFLD IKJSUBF
PCOLSDD  IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
AROWSFLD IKJSUBF
AROWSDD  IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
ACOLSFLD IKJSUBF
ACOLSDD  IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
ATTRFLD  IKJSUBF
ATTRDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
LMODFLD  IKJSUBF
LMODDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
TTYPFLD  IKJSUBF
TTYPDD   IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
ENVFLD   IKJSUBF
ENVDD    IKJIDENT 'CLIST VARIABLE',PROMPT='CLIST VARIABLE',CHAR
         IKJENDP
         PRINT     GEN
         EJECT
         AIF   (&ACF314).$$SKP9B                                  NTL14
*.....................................................................*
*        JCT MAPPING MACRO                                            *
*.....................................................................*
         SPACE
         SPACE 2                                                  INTEL
         AIF   (NOT &SP3).$$SKP9                                  NTL14
TSO051   CSECT   ,                                                INTEL
         $BUFFER   ,                                              INTEL
         #ACFJCTX  ,       ACF2 3.1.3                             INTEL
TSO051   CSECT   ,                                                INTEL
         AGO   .$$SKP9A                                           INTEL
.$$SKP9  ANOP  ,                                                  INTEL
.$$SKP9A ANOP  ,                                                  INTEL
         $JCT  DOC=YES
         SPACE 2
         EJECT
.$$SKP9B ANOP  ,                                                  NTL14
*.....................................................................*
*       MAPPING MACROS                                                *
*.....................................................................*
         SPACE
         PRINT NOGEN                                              INTEL
         IHAASCB
         IKJTSB   LIST=YES,EXT=YES
         AIF   (NOT &TONE4).$SKP99                                TONE4
         TPCDATA  ,                                               TONE4
.$SKP99  ANOP  ,                                                  TONE4
         AIF   (NOT &ACF314).$SK1004                              INTEL
         PRINT NOGEN                                              INTEL
         IHAPSA                                                   INTEL
         ACFASVT                                                  INTEL
         ACCVT                                                    INTEL
         ACUCB                                                    INTEL
         PRINT     GEN                                            INTEL
         LIDREC                                                   INTEL
.$SK1004 ANOP  ,                                                  TONE4
*.....................................................................*
*        OVERLAY DEFINITIONS FOR JOB ACCOUNT NUMBER                   *
*.....................................................................*
         SPACE
NTLPROJ  EQU   LIDACCT,4
NTLSRNUM EQU   LIDACCT+4,4
NTLROOMN EQU   LIDACCT+9,4                                        NTL14
         END ,
