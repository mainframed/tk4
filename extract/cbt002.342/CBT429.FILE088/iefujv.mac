         MACRO                                                    MHL1
&LABEL   SNAPPER ,                                                MHL1
.* MACRO USED TO CONTROL USE OF SNAP DURING TESTING               MHL1
         GBLB  &SNAP                                              MHL1
         AIF   (NOT &SNAP).NOSNAP                                 MHL1
         BAL   R9,SNAPPER                                         MHL1
.NOSNAP  MEND                                                     MHL1
         TITLE 'ITT NORTH - PLANT 10 SMF EXIT IEFUJV'
IEFUJV   CSECT
         SPACE 1
***********************************************************************
*                                                                     *
*   PROGRAM :  IEFUJV                                                 *
*                                                                     *
*   FUNCTION:  TO SCAN JCL AND CHECK FOR FOLLOWING -                  *
*               A. JOB CARDS                                          *
*                  1. MUST HAVE TIME AND CLASS SPECIFIED.             *
*                  2. CLASS CAN ONLY BE A,B,C,D,E,F,G,H,I,J,K,L,S,Z,  *
*                              M,N,R,X.                               *
*                  3. TIME FOR CLASS A MUST BE <= 1 MIN.              *
*                              CLASS B MUST BE <= 7 MIN.              *
*                              CLASS L MUST BE <= 10 MIN.             *
*                              CLASS X MUST BE <=  5 SEC.             *
*                              CLASS N MUST BE <= 30 SEC.             *
*                  4. ACCOUNTING FIELD MUST CONTAIN :                 *
*                        (AA,DDDD,PPPPPPP,...)                        *
*                      WHERE AA = APPLICATION CODE                    *
*                            DDDD = DEPT CODE                         *
*                            PPPPPPP = PROJRCT NUMBER(TEST JOBS ONLY) *
*                  5. JOBNAME MUST BE OF FORM AAXXXXFL                *
*                      WHERE AA = APPLICATION CODE                    *
*                            F = FREQUENCY CODE(A,B,D,M,Q,R,S,T,W)    *
*                            L = LOCATION CODE(B,C,D,H,J,P,S)         *
*                  6. PERFORM KEYWORD IS NOT ALLOWED.                 *
*                                                                     *
*               B. EXEC CARDS                                         *
*                  1. PERFORM KEYWORD IS NOT ALLOWED.                 *
*                  2. DPRTY KEYWORD IS NOT ALLOWED.                   *
*                                                                     *
*               C. DD CARDS                                           *
*                  1. FOR CLASSES A AND B TAPE ALLOCATIONS ARE        *
*                     NOT ALLOWED.                                    *
*                                                                     *
*   INPUT   :  CEPA PASSED TO ROUTINE                                 *
*              JCL STATEMENT                                          *
*                                                                     *
*   OUTPUT  :  IF ANY, ERROR MESSAGES TO CONSOLE AND JOBLOG           *
*                                                                     *
*   RETURN  :  0 IF JOB PASSED ALL CHECKING                           *
*              4 IF FOUND AN ERROR AND JOB IS TO BE CANCELLED         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        MODIFIED 13 FEB 84 TO FIX A LATENT BUG IN KEYWORD        MHL1*
*               SEARCH ROUTINE. THE KEYWORD SEARCH WOULD          MHL1*
*               FIND A KEYWORD THAT WAS AFTER THE DELIMITING      MHL1*
*               BLANK ON A JOBCARD AND SHOULD BE TREATED AS       MHL1*
*               A COMMENT. HOWEVER, THE EXIT WOULD TREAT IT       MHL1*
*               AS A VALID KEYWORD. THIS WOULD ALLOW A USER       MHL1*
*               TO CODE A ' TIME=NNN' VALUE AS A COMMENT AND      MHL1*
*               ON A LATER JOBCARD CONTINUATION SPECIFY A         MHL1*
*               TIME GREATER THAN THE ROUTINE WOULD NORMALLY      MHL1*
*               ALLOW.                                            MHL1*
*                                                                 MHL1*
*               ACCOUNTING FIELD VALUE CHECK ROUTINE CHANGED      MHL1*
*               TO NOT MOVE FIELDS TO WORKAREA BUT CHECK THEM     MHL1*
*               FROM THE JOBCARD. POSSIBILITY EXISTED FOR ONE     MHL1*
*               OR MORE ACCOUNTING FIELDS TO BE ENTERED AS        MHL1*
*               A LONG VALUE AND GO BEYOND END OF WORKAREA.       MHL1*
*                                                                 MHL1*
*               TIME ROUTINE CHANGED TO ALLOW SECONDS TO BE       MHL1*
*               SPECIFIED ON A CONTINUATION CARD.                 MHL1*
*                                                                 MHL1*
*               THE COMMON EXIT PARAMETER AREA WAS MAPPED WITH    MHL1*
*               A DSECT CREATED FROM THE 'IEFJMR' MACRO FROM      MHL1*
*               'SYS1.AMODGEN'.                                   MHL1*
*                                                                 MHL1*
*                                                                     *
*        MODIFIED 02 SEP 83 TO ADD SUPPORT FOR LOCATION HARRISBURGCXV2*
*               DOCUMENTATION CHG ONLY TO SAY HARRISBURG          CXV2*
*               INSTEAD OF DES PLAINS                             CXV2*
*        MODIFIED 05 MAY 83 TO ADD SUPPORT FOR LOCATION RALEIGH   CXV1*
*                                                                     *
*        MODIFIED 21 MAY 82 TO ADD SUPPORT FOR CLASS=X            WCH1*
*                                                                     *
*        MODIFIED    11/16/81                                         *
*            PROBLEM LOG ENTRY: LOST INITIATORS AND JES2 0C4 ABENDS   *
*            RESOLUTION: IEFUJV WAS NOT CHECKING ACCOUNTING FIELD     *
*                        PARAMETERS ON JOB CARD FOR NULL ENTRIES.     *
*                        SPECIFICALLY, IN THE CASE WHERE A COMMA      *
*                        WAS PRESENT TO INDICATE ANOTHER PARAMETER    *
*                        WOULD BE ENTERED BUT WAS FOLLOWED BY ) TO    *
*                        CLOSE THE ACCOUNTING PARAMETER LIST.         *
*                        I.E.//JJJJJJJJ   JOB  (AA,DDDD,)             *
*                          THIS CODE WAS CHANGED TO CHECK FOR A       *
*                        ZERO LENGTH ACCOUNTING FIELD AND IF FOUND    *
*                        TO SKIP THE MOVING OF THAT PARAMETER TO THE  *
*                        WORK AREA.  PRIOR TO THE CHANGE, THIS CODE   *
*                        SUBTRACTED 1 FROM 0, AND MOVED 255 BYTES     *
*                        OF INVALID PARAMETER INTO IT'S 102 BYTE      *
*                        WORKAREA.....OVERLAYING JES2 PITABLE.        *
*            CHANGE ID: UJV1                                          *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT                                                    MHL1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *   MHL1
*                                                                 MHL1
*        THIS CONDITIONAL ASSEMBLY CODE INTEROGATES               MHL1
*        SYSPARM FOR ONE OR MORE OPTIONS FOR TESTING              MHL1
*        THE SUBMIT EXIT.  THE EXIT MAY BE TESTED                 MHL1
*        THROUGH THE USE OF THE TSO TEST DRIVER OR                MHL1
*        THROUGH NORMAL TSO SUBMIT PROCESSING.                    MHL1
*                                                                 MHL1
*        THE OPTIONS THAT MAY BE ENTERED ARE                      MHL1
*                                                                 MHL1
*              TEST .... THE EXIT IS RUN UNDER 'TESTEXIT'         MHL1
*              SNAP .... SNAPS ARE TO BE TAKEN                    MHL1
*                        (INCLUDES DRIVER)                        MHL1
*                                                                 MHL1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *   MHL1
         GBLA  &KPARM,&I1,&I2,&I3                                 MHL1
         GBLB  &TEST,&SNAP                                        MHL1
         GBLC  &CPARM                                             MHL1
&KPARM   SETA  K'&SYSPARM         NUMBER OF VALUES                MHL1
&I1      SETA  1                                                  MHL1
&I2      SETA  1                                                  MHL1
.PARMLUP AIF   (&I2 GT &KPARM).PARMEND                            MHL1
         AIF   (&I2 EQ &KPARM).PARMLST                            MHL1
         AIF   ('&SYSPARM'(&I2,1) EQ ',').PARMCOM                 MHL1
&I2      SETA  &I2+1                                              MHL1
         AGO   .PARMLUP                                           MHL1
.PARMLST ANOP                                                     MHL1
&I3      SETA  &I2-&I1+1          GET LENGTH OF VALUE             MHL1
         AGO   .PARMSET                                           MHL1
.PARMCOM ANOP                                                     MHL1
&I3      SETA  &I2-&I1            GET LENGTH OF PARM              MHL1
.PARMSET ANOP                                                     MHL1
&CPARM   SETC  '&SYSPARM'(&I1,&I3) ISOLATE VALUE                  MHL1
         AIF   ('&CPARM' EQ 'TEST').PARMDRV                       MHL1
         AIF   ('&CPARM' EQ 'SNAP').PARMSNP                       MHL1
         MNOTE 4,'SYSPARM VALUE ''&CPARM'' NOT RECOGNIZED'        MHL1
         AGO   .PARMNXT                                           MHL1
.PARMSNP ANOP                                                     MHL1
&SNAP    SETB  1                                                  MHL1
.PARMDRV ANOP                                                     MHL1
&TEST    SETB  1                                                  MHL1
         AGO   .PARMNXT                                           MHL1
.PARMNXT ANOP                                                     MHL1
&I2      SETA  &I2+1                                              MHL1
&I1      SETA  &I2                                                MHL1
         AGO   .PARMLUP                                           MHL1
.PARMEND ANOP                                                     MHL1
         EJECT
***********************************************************************
*     EQUATES                                                         *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2                  POINTER TO COMMON EXIT PARM     MHL1
*                                 AREA (CEPA)                     MHL1
R3       EQU   3                  POINTER TO JCL STATEMENT        MHL1
R4       EQU   4                  POINTER TO STATEMENT TYPE       MHL1
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8                  POINTER TO 'ITACCTBL'
R9       EQU   9                  BAL REGISTER
R10      EQU   10
R11      EQU   11                 BASE REGISTER                   MHL1
R12      EQU   12                 BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
*   FOLLOWING BITS MAY BE SET IN CEPA AT OFFSET 32 (CEPUCOM)      MHL1
         SPACE 1
$CLASS   EQU   X'80'           CLASS IS SPECIFIED                 MHL1
$BCLASS  EQU   X'40'           BAD CLASS IS SPECIFIED             MHL1
$TIME    EQU   X'20'           TIME IS SPECIFIED                  MHL1
$BTIME   EQU   X'10'           BAD TIME IS SPECIFIED              MHL1
$BDPRTY  EQU   X'08'           DPRTY IS SPECIFIED                 MHL1
$BPERF   EQU   X'04'           PERFORM IS SPECIFIED               MHL1
$T1439   EQU   X'02'           TIME CHANGED FROM 1440 TO 1439     MHL1
$TIMEP   EQU   X'01'              TIME IN PARENTHESIS             MHL1
         SPACE 2
*   FOLLOWING BITS MAY BE SET IN CEPA AT OFFSET 33 (CEPUCOM+1)    MHL1
         SPACE 1
$NOACCT  EQU   X'80'           APPLICATION CODE NOT SPECIFIED     MHL1
$BAPPL   EQU   X'40'           BAD APPLICATION CODE               MHL1
$BJNAME  EQU   X'20'           INVALID JOB NAME                   MHL1
$BDEPT   EQU   X'10'           BAD DEPT SPECIFIED IN ACCT FLDS    MHL1
$BPROJ   EQU   X'08'           BAD PROJECT SPECIFIED              MHL1
$BLOC    EQU   X'04'           BAD LOCATION CODE IN JOBNAME       MHL1
$BFREQ   EQU   X'02'           BAD FREQUENCY CODE IN JOBNAME      MHL1
$JCLERR  EQU   X'01'           BAD JOBCARD FORMAT                 MHL1
         SPACE 2
*   FOLLOWING BITS MAY BE SET IN CEPA AT OFFSET 34 (CEPUCOM+2)    MHL1
         SPACE 1
$PROGNAM EQU   X'80'           PROGRAMMER NAME ON CONT. CARD      MHL1
$SECONDS EQU   X'40'           SECONDS ON CONTINUATION CARD       MHL1
$TESTJOB EQU   X'20'           THIS IS A TEST JOB                 MHL1
$BADJOBN EQU   X'10'           JOBNAME NOT 8 CHARACTERS           MHL1
         SPACE 1                                                  MHL1
* MISCELLANEOUS EQUATES                                           MHL1
         SPACE 1                                                  MHL1
$FF      EQU   X'FF'                                              MHL1
         AIF   (&TEST).SETTEST                                    MHL1
$SUBPOOL EQU   241                SMF EXIT USE POOL 241           MHL1
         AGO   .SETEND                                            MHL1
.SETTEST ANOP                                                     MHL1
$SUBPOOL EQU   0                  USE POOL 0 FOR TESTING          MHL1
.SETEND  ANOP                                                     MHL1
         EJECT
***********************************************************************
*     START OF IEFUJV - PICK UP PARMS AND DETERMINE PROCESSING        *
***********************************************************************
         SPACE 1
         SAVE  (14,12),,IEFUJV.DATE(&SYSDATE).TIME(&SYSTIME)      MHL1
         LR    R12,R15                                            MHL1
         USING IEFUJV,R12,R11       SET UP BASE OF 12             MHL1
         LA    R11,2048(,R12)                                     MHL1
         LA    R11,2048(,R11)                                     MHL1
         LM    R2,R4,0(R1)        PICK UP FOLLOWING ADDRESSES :
*                                   R2 - COMMON EXIT PARAMETER AREA
*                                   R3 - JCL STATEMENT
*                                   R4 - TYPE OF JCL STATEMENT
         USING CEPA,R2                                            MHL1
         TM    CEPINDC,X'01'      T.S.O. FORGROUND JOB ?          MHL1
         BO    NOCHK                YES, GET OUT OF CODE
         SPACE 1                                                  MHL1
         CLI   0(R4),X'01'        JOB  CARD ?
         BE    JOBSRCH              YES, GO DO JOB CARD PROCESSING
         CLI   0(R4),X'02'        EXEC CARD ?
         BE    EXECSRCH             YES, GO DO EXEC PROCESSING
*        CLI   0(R4),X'08'        PROC CARD ?                     MHL1
*        BE    EXECSRCH             YES, GO DO EXEC PROCESSING    MHL1
         CLI   0(R4),X'10'        HAVE WE GOTTEN ALL CARDS ?
         BE    ANYERRS              YES, GO CHECK FOR ERRORS
         SPACE 1
NOCHK    LM    R14,R12,12(R13)    RESTORE CALLERS REGS
         XR    R15,R15            SET RETURN - CONTINUE PROCESSING
         BR    R14                RETURN TO CALLER
         EJECT
***********************************************************************
*     JOB CARD PROCESSING - CHECK LOCATION AND FRQUENCY CODES         *
***********************************************************************
         SPACE 1
JOBSRCH  DS    0H
         CLI   2(R3),C' '         IS THIS FIRST JOB CARD ?        MHL1
         BE    JOBCONT              NO, FIND KEYWORD              MHL1
* CLEAR USER COM FIELD TO ZEROS FOR FLAGS                         MHL1
         XC    CEPUCOM,CEPUCOM    CLEAR USER COM FIELD            MHL1
* JOBNAME OF TSO/CMS USERID + 1 CHARACTER IS NOW ALLOWED          MHL1
         SPACE 1                                                  MHL1
         CLI   CEPJOB,C'X'         IS THIS USERID FORMAT?         MHL1
         BE    JOBFORMX            YES, EXTRACT INFO FROM USERID  MHL1
         CLC   CEPJOB(6),=CL6'PEPPER' SPECIAL OCCURRANCE?
         BE    NOCHK               YES, CHECK NO MORE
         CLC   CEPJOB(5),=CL5'XSPRB'  SPECIAL OCCURRANCE?
         BE    NOCHK               YES, CHECK NO MORE
* STANDARD FORMAT                                                 MHL1
**    AAIIIIFL                                                    MHL1
**    AA....... APPLICATION CODE                                  MHL1
**      IIII... INITIALS (NOT VERIFIED)                           MHL1
**          F.. FREQUENCY CODE                                    MHL1
**           L. LOCATION CODE                                     MHL1
         MVC   CEPUAPCD,CEPJOB     APPLICATION CODE               MHL1
         MVC   CEPUFREQ,CEPJOB+6   FREQUENCY CODE                 MHL1
         MVC   CEPULOC,CEPJOB+7    LOCATION CODE                  MHL1
         B     CHKLOC              GO CHECK LOCATION CODE         MHL1
* USERID FORMAT                                                   MHL1
**    XAALIII                                                     MHL1
**    X........ 'X' IDENTIFIES TSO/CMS USERID                     MHL1
**     AA...... APPLICATION CODE                                  MHL1
**       L..... LOCATION CODE                                     MHL1
**        III.. INITIALS (NOT VERIFIED)                           MHL1
**              FREQUENCY CODE OF 'T' IS ASSUMED                  MHL1
JOBFORMX DS    0H                                                 MHL1
         CLI   CEPJOB+7,C' '      JOBNAME 8 CHARACTERS?           MHL1
         BNE   JOBFRMX8           YES, OK                         MHL1
         OI    CEPUCOM+2,$BADJOBN NOT 8, BAD JOBNAME              MHL1
         SNAPPER                                                  MHL1
JOBFRMX8 DS    0H                                                 MHL1
         MVC   CEPUAPCD,CEPJOB+1   APPLICATION CODE               MHL1
         MVC   CEPULOC,CEPJOB+3    LOCATION CODE                  MHL1
         MVI   CEPUFREQ,C'T'       FREQUENCY CODE                 MHL1
* CHECK JOBNAME FOR VALID LOCATION CODE                           MHL1
CHKLOC   DS    0H                                                 MHL1
         CLI   CEPULOC,C'C'       IS LOCATION CAPE CANAVERAL ?    MHL1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'D'       IS LOCATION DELAWARE ?          MHL1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'H'       IS LOCATION CORPORATE ?         MHL1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'J'       IS LOCATION JOHNSON CITY ?      MHL1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'M'       IS LOCATION CORINTH  ?          MHL1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE    MHL1
         CLI   CEPULOC,C'P'       IS LOCATION HARRISBURG  ?        CXV2
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'R'       IS LOCATION RALEIGH?             CXV1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE     CXV1
         CLI   CEPULOC,C'S'       IS LOCATION COMPANY WIDE ?
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE
         CLI   CEPULOC,C'U'       IS LOCATION BTM ?                WRH1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE     WRH1
         CLI   CEPULOC,C'A'       IS LOCATION ATC ?                WRH1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE     WRH1
         CLI   CEPULOC,C'V'       IS LOCATION VANN INDUSTRY ?      WRH1
         BE    CHKFREQ              YES, GO CHK FREQUENCY CODE     WRH1
         OI    CEPUCOM+1,$BLOC    INDICATE BAD LOCATION CODE      MHL1
         SNAPPER                                                  MHL1
         SPACE 1
* CHECK FOR VALID FREQUENCY CODE                                  MHL1
CHKFREQ  DS    0H                                                 MHL1
         CLI   CEPUFREQ,C'D'      IS FREQUENCY CODE DAILY ?       MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'T'      IS FREQUENCY CODE TEST ?        MHL1
         BE    CHKFREQT             YES, SET TEST JOB             MHL1
         CLI   CEPUFREQ,C'R'      IS FREQUENCY CODE ON REQUEST ?  MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'W'      IS FREQUENCY CODE WEEKLY ?      MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'B'      IS FREQUENCY CODE BIWEEKLY ?    MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'M'      IS FREQUENCY CODE MONTHLY ?     MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'Q'      IS FREQUENCY CODE QUARTERLY ?   MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'S'      IS FREQUENCY CODE SEMIIANNUAL ? MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         CLI   CEPUFREQ,C'A'      IS FREQUENCY CODE ANNUAL ?      MHL1
         BE    JOBACCT              YES, GO CHK JOB ACCOUNTING
         OI    CEPUCOM+1,$BFREQ   INDICATE BAD FREQUENCY CODE     MHL1
         SNAPPER                                                  MHL1
         B     JOBACCT            CHECK JOB PARMS                 MHL1
         SPACE 1                                                  MHL1
* SET TEST JOB FLAG                                               MHL1
CHKFREQT DS    0H                                                 MHL1
         OI    CEPUCOM+2,$TESTJOB  SHOW TEST JOB                  MHL1
         EJECT
***********************************************************************
*     JOB CARD PROCESSING - FIND ACCOUNTING FLDS AND LOAD TABLES      *
***********************************************************************
         SPACE 1
JOBACCT  DS    0H
         LA    R5,3(R3)           GET ADDR OF JCL + 3
         LA    R14,1                                              MHL1
         LA    R15,70(,R3)        COULMN 71                       MHL1
JOBACT01 CLC   0(5,R5),KJOB       IS IT EQ TO ' JOB '             MHL1
* SCAN JOBCARD TO FIND OPERATOR ' JOB '                           MHL1
         BE    JOBACT02             YES, GO FIND '('              MHL1
         BXLE  R5,R14,JOBACT01    BUMP TO NXT COLUMN              MHL1
         OI    CEPUCOM+1,$NOACCT+$JCLERR NO ACCOUNT FIELDS        MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB             END OF CARD                     MHL1
         SPACE 1
JOBACT02 DS    0H                                                 MHL1
* SCAN TO LEFT PAREN THAT STARTS ACCOUNTING FIELDS                MHL1
         LA    R5,L'KJOB(,R5)     BUMP PAST ' JOB '               MHL1
JOBACT03 CLI   0(R5),C'('         IS IT EQ TO '('                 MHL1
         BE    JOBACT05             YES, GO SAVE ACCNT FLDS       MHL1
         CLI   0(R5),C' '         IS THIS A BLANK ?               MHL1
         BNE   JOBACT04             NO, SET BAD ACCOUNTING        MHL1
         BXLE  R5,R14,JOBACT03    BUMP TO NXT COLUMN              MHL1
JOBACT04 OI    CEPUCOM+1,$NOACCT  END OF SRCH AREA, SET BAD ACCT  MHL1
         SNAPPER                                                  MHL1
         B     JOBPGMNM           SEE IF PROGRAMMER NAME          MHL1
         SPACE 1
JOBACT05 DS    0H                                                 MHL1
         GETMAIN R,LV=72,SP=$SUBPOOL GET A WORK AREA              MHL1
         ST    R13,4(,R1)         SAVE CALLERS SAVE AREA ADDR     MHL1
         ST    R1,8(,R13)         STORE IN CALLERS                MHL1
         LR    R13,R1             PICK UP OUR SAVE AREA           MHL1
         LOAD  EP=ITACCTBL        LOAD TABLES                     MHL1
         LR    R8,R0              POINT TO ACCOUNT CODES          MHL1
         SPACE 2                                                  MHL1
***********************************************************************
*     JOB CARD PROCESSING - CHECK APPL CODE IN JOBNAME AND ACCNT FLD  *
***********************************************************************
         SPACE 1
         L     R7,0(R8)           GET ADDR OF ACCNT TABLE         MHL1
JOBACT06 CLC   0(2,R7),KSTARS     END OF TABLE ?                  MHL1
         BE    JOBACT07             YES, GO SET BAD JOBNAME       MHL1
         CLC   0(2,R7),CEPUAPCD   IS THIS OUR ACCT CODE ?         MHL1
         BE    JOBACT10             YES, GO CHK ACCOUNTING FIELDS MHL1
         LA    R7,2(R7)           BUMP TO NXT ENTRY IN TABLE      MHL1
         B     JOBACT06           GO CHK THIS ENTRY               MHL1
         SPACE 1
JOBACT07 OI    CEPUCOM+1,$BJNAME  SET BAD JOB NAME                MHL1
         SNAPPER                                                  MHL1
         SPACE 2
***********************************************************************
*     CHECK ACCOUNTING FIELDS - FIRST THE APPLICATION CODE            *
***********************************************************************
         SPACE 1
JOBACT10 DS    0H                                                 MHL1
         LA    R14,1                                              MHL1
         LA    R15,70(,R3)        POINT TO COLUMN 71              MHL1
         BXH   R5,R14,JOBACT14    BUMP PAST '('                   MHL1
         LR    R6,R5              SAVE ADDR OF FRST FLD           MHL1
JOBACT11 DS    0H                 FIND APPICATION CODE            MHL1
* FIND END OF APPLICATION CODE FIELD                              MHL1
         CLI   0(R6),C','         END OF FIELD?                   MHL1
         BE    JOBACT12           YES, CHECK VALUE                MHL1
         CLI   0(R6),C')'         END OF FIELD?                   MHL1
         BE    JOBACT12           YES, CHECK VALUE                MHL1
         CLI   0(R6),C' '         END OF FIELD?                   MHL1
         BE    JOBACT12           YES, CHECK VALUE                MHL1
         BXLE  R6,R14,JOBACT11    END OF CARD                     MHL1
JOBACT12 DS    0H                                                 MHL1
         SR    R6,R5              GET LENGTH OF FIELD             MHL1
         C     R6,=F'2'           LENGTH OF 2?                    MHL1
         BNE   JOBACT15           NO, BAD APPLICATION CODE        MHL1
* SEE IF APPLICATION CODE IS IN TABLE                             MHL1
         L     R7,0(R8)           GET ADDR OF TABLE               MHL1
JOBACT13 CLC   0(2,R7),KSTARS     END OF TABLE ?                  MHL1
         BE    JOBACT15             YES, GO SET BAD APPL CODE     MHL1
         CLC   0(2,R7),0(R5)      IS THIS OUR ACCT CODE ?         MHL1
         BE    JOBACT21             YES, GO CHK DEPT CODE         MHL1
         LA    R7,2(R7)           BUMP TO NXT ENTRY IN TABLE      MHL1
         B     JOBACT13           GO CHK THIS ENTRY               MHL1
         SPACE 1                                                  MHL1
JOBACT14 DS    0H                                                 MHL1
* END OF CARD FOUND BEFORE APPLICATION CODE                       MHL1
         OI    CEPUCOM+1,$BAPPL+$JCLERR                           MHL1
         B     JOBACT98           NO MORE THIS CARD               MHL1
JOBACT15 DS    0H                                                 MHL1
         OI    CEPUCOM+1,$BAPPL   SET BAD APPL. CODE              MHL1
         SNAPPER                                                  MHL1
         SPACE 2
***********************************************************************
*     CHECK ACCOUNTING FIELDS - NEXT THE DEPARTMENT NUMBER            *
***********************************************************************
         SPACE 1
JOBACT21 DS    0H                                                 MHL1
         AR    R5,R6              POINT TO ENDING CHARACTER       MHL1
         CR    R5,R15             END OF CARD                     MHL1
         BH    JOBACT23           YES, BAD DEPT                   MHL1
         CLI   0(R5),C','         STOP ON COMMA?                  MHL1
         BNE   JOBACT23           NO, FORMAT ERROR                MHL1
         BXH   R5,R14,JOBACT24    BUMP PAST COMMA                 MHL1
         LR    R6,R5              COPY ADDRESS                    MHL1
JOBACT22 DS    0H                 FIND DEPARTMENT NUMBER          MHL1
         CLI   0(R6),C','         END OF FIELD?                   MHL1
         BE    JOBACT24           YES, CHECK VALUE                MHL1
         CLI   0(R6),C')'         END OF FIELD?                   MHL1
         BE    JOBACT24           YES, CHECK VALUE                MHL1
         CLI   0(R6),C' '         END OF FIELD?                   MHL1
         BE    JOBACT24           YES, CHECK VALUE                MHL1
         BXLE  R6,R14,JOBACT22    CHECK AGAIN                     MHL1
JOBACT23 DS    0H                                                 MHL1
         OI    CEPUCOM+1,$BDEPT+$JCLERR END OF CARD               MHL1
         B     JOBACT98           NOTHING ELSE TO DO              MHL1
JOBACT24 DS    0H                                                 MHL1
         SR    R6,R5              GET LENGTH OF FIELD             MHL1
         C     R6,=F'4'           LEGTH OF 4?                     MHL1
         BNE   JOBACT26           NO, BAD APPLICATION CODE        MHL1
         L     R7,4(R8)           GET ADDR OF DEPT TABLE
JOBACT25 CLC   0(4,R7),KSTARS     END OF TABLE ?                  MHL1
         BE    JOBACT26             YES, GO SET BAD DEPT CODE     MHL1
         CLC   0(4,R7),0(R5)      IS THIS OUR DEPT CODE ?         MHL1
         BE    JOBACT31             YES, GO CHK PROJ CODE         MHL1
         LA    R7,4(R7)           BUMP TO NXT ENTRY IN TABLE
         B     JOBACT25           GO CHK THIS ENTRY               MHL1
         SPACE 1
JOBACT26 DS    0H                                                 MHL1
         OI    CEPUCOM+1,$BDEPT   SET BAD DEPT. CODE              MHL1
         SNAPPER                                                  MHL1
         SPACE 2
***********************************************************************
*     CHECK ACCOUNTING FIELDS - NEXT THE PROJECT CODE                 *
***********************************************************************
         SPACE 1
JOBACT31 DS    0H                                                 MHL1
         AR    R5,R6              POINT TO ENDING CHARACTER       MHL1
         CR    R5,R15             AT END OF CARD?                 MHL1
         BH    JOBACT33           YES, ERROR                      MHL1
         CLI   0(R5),C')'         STOP ON PAREN?                  MHL1
         BE    JOBACT34           YES, OK IF PRODUCTION           MHL1
         CLI   0(R5),C','         STOP ON COMMA?                  MHL1
         BNE   JOBACT33                                           MHL1
         BXH   R5,R14,ENDJOB      BUMP PAST COMMA                 MHL1
         LR    R6,R5              COPY ADDRESS                    MHL1
JOBACT32 DS    0H                 FIND PROJECT CODE               MHL1
         CLI   0(R6),C','         END OF FIELD?                   MHL1
         BE    JOBACT35           YES, CHECK VALUE                MHL1
         CLI   0(R6),C')'         END OF FIELD?                   MHL1
         BE    JOBACT35           YES, CHECK VALUE                MHL1
         CLI   0(R6),C' '         END OF FIELD?                   MHL1
         BE    JOBACT35           YES, CHECK VALUE                MHL1
         BXLE  R6,R14,JOBACT32    CHECK AGAIN                     MHL1
JOBACT33 DS    0H                                                 MHL1
         OI    CEPUCOM+1,$JCLERR                                  MHL1
         SNAPPER                                                  MHL1
JOBACT34 DS    0H                 PROJECT NUMBER MISSING          MHL1
         TM    CEPUCOM+2,$TESTJOB TEST JOB?                       MHL1
         BNO   JOBACT98           NO, NOT BAD PROJECT             MHL1
         OI    CEPUCOM+1,$BPROJ   END OF CARD                     MHL1
         B     JOBACT98           NOTHING ELSE TO DO              MHL1
JOBACT35 DS    0H                                                 MHL1
         SR    R6,R5              GET LENGTH OF FIELD             MHL1
         C     R6,=F'7'           LEGTH OF 7?                     MHL1
         BE    JOBACT36           YES, GOOD PROJECT NUMBER        MHL1
         TM    CEPUCOM+2,$TESTJOB TEST JOB?                       MHL1
         BNO   JOBACT36           NO, NOT BAD PROJECT             MHL1
         OI    CEPUCOM+1,$BPROJ   BAD PROJECT                     MHL1
         SNAPPER                                                  MHL1
JOBACT36 DS    0H                                                 MHL1
         AR    R5,R6              POINT TO STOP CHARACTER         MHL1
JOBACT37 DS    0H                                                 MHL1
         CLI   0(R5),C')'         END OF FIELD                    MHL1
         BE    JOBACT98                                           MHL1
         BXLE  R5,R14,JOBACT37    FIND END OF ACCOUNT CODES       MHL1
         OI    CEPUCOM+1,$JCLERR  BAD JOBCARD                     MHL1
JOBACT98 DS    0H                                                 MHL1
         SNAPPER                                                  MHL1
         DELETE EP=ITACCTBL       DELETE TABLES
         SPACE 1
JOBACT99 DS    0H                                                 MHL1
         LR    R1,R13             R1 = ADDRESS OF WORKAREA        MHL1
         L     R13,4(,R13)        PICK UP CALLERS SAVE AREA       MHL1
         FREEMAIN R,LV=72,A=(1),SP=$SUBPOOL FREE AREA             MHL1
         SPACE 1                                                  MHL1
         MVI   CEPUSEID,C' '       CLEAR CEPUSEID                 MHL1
         MVC   CEPUSEID+1(L'CEPUSEID-1),CEPUSEID                  MHL1
         TM    CEPUCOM+1,$JCLERR  FORMAT ERROR?                   MHL1
         BO    ENDJOB             YES, CAN'T DO MORE THIS CARD    MHL1
* FIND AND SKIP PAST PROGRAMMER NAME FIELD                        MHL1
         LA    R15,70(,R3)        COLUMN 71                       MHL1
         LA    R14,1                                              MHL1
         BXH   R5,R14,ENDJOB      BUMP PAST ')'                   MHL1
         CLI   0(R5),C','         COMMA AFTER ACCOUNT FIELDS?     MHL1
         BE    JOBPGMNM           YES, SCAN PROGRAMMER NAME FIELD MHL1
         CLI   0(R5),C' '         BLANK AFTER ACCOUNT FIELDS?     MHL1
         BE    ENDJOB             YES, END OF JOBACRD             MHL1
         OI    CEPUCOM+1,$JCLERR  FORMAT ERROR                    MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB             CAN'T DO MORE THIS CARD         MHL1
JOBPGMNM DS    0H                                                 MHL1
         BXH   R5,R14,JOBPGM10    BUMP PAST ','                   MHL1
         CLI   0(R5),C' '         CONTINUED NEXT CARD?            MHL1
         BNE   JOBPGM11           NO, PROGRAMMER NAME THIS CARD   MHL1
JOBPGM10 DS    0H                                                 MHL1
         OI    CEPUCOM+2,$PROGNAM SHOW WAITING ON PROGRAMMER NAME MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB                                             MHL1
JOBPGM11 DS    0H                                                 MHL1
* COME HERE IF PROGRAMMER NAME ON CONTINUATION CARD               MHL1
         NI    CEPUCOM+2,$FF-$PROGNAM  TURN OFF PGM NAME CONT.    MHL1
         LA    R14,1                                              MHL1
         LA    R15,70(,R3)        COLUMN 71                       MHL1
         CLI   0(R5),C''''        NAME IN QUOTES?                 MHL1
         BE    JOBPGM21           YES, SCAN TO LAST QUOTE         MHL1
* MAY BE PROGRAMMER NAME OR A KEYWORD IF PROGRAMMER NAME OMITTED  MHL1
         LR    R6,R5              COPY ADDRESS                    MHL1
JOBPGM12 DS    0H                                                 MHL1
         CLI   0(R6),C'='         END OF KEYWORD?                 MHL1
         BE    JOBKEYWD           YES, DON'T HAVE PROGRAMMER NAME MHL1
         CLI   0(R6),C','         END OF NAME?                    MHL1
         BE    JOBPGM13           YES, NOW CAN SCAN FOR KEYWORDS  MHL1
         CLI   0(R6),C' '         END OF NAME?                    MHL1
         BE    ENDJOB             YES, END OF JOBCARD             MHL1
         BXLE  R6,R14,JOBPGM12    LOOK AT NEXT COLUMN             MHL1
         B     ENDJOB             NAME ENDS THIS CARD             MHL1
JOBPGM13 DS    0H                                                 MHL1
         LR    R5,R6              GET STOPPER ADDRESS             MHL1
         B     JOBSCAN            FIND KEYWORDS                   MHL1
JOBPGM21 DS    0H                                                 MHL1
* PROGRAMMER NAME IN QUOTES                                       MHL1
JOBPGM22 DS    0H                                                 MHL1
         BXH   R5,R14,JOBPGM24                                    MHL1
         CLI   0(R5),C''''        ANOTHER QUOTE?                  MHL1
         BNE   JOBPGM22           NO, TRY AGAIN                   MHL1
JOBPGM23 DS    0H                                                 MHL1
* CHECK FOR DOUBLE QUOTES, WHICH ACTS AS 1 CHARACTER OF NAME      MHL1
         BXH   R5,R14,ENDJOB      BUMP PAST QUOTE                 MHL1
         CLI   0(R5),C''''        DOUBLE QUOTE                    MHL1
         BE    JOBPGM22           YES, CONTINUE PROGRAMMER NAME   MHL1
         CLI   0(R5),C','         COMMA?                          MHL1
         BE    JOBSCAN            YES, CHECK FOR KEYWORDS         MHL1
         CLI   0(R5),C' '         BLANK?                          MHL1
         BE    ENDJOB             YES, END OF JOBCARD             MHL1
JOBPGM24 DS    0H                                                 MHL1
* FORMAT ERROR IN PROGRAMMER NAME                                 MHL1
         OI    CEPUCOM+1,$JCLERR                                  MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB                                             MHL1
         SPACE 1
         EJECT
***********************************************************************
*     JOB CARD PROCESSING - SEARCH FOR KEYWORDS                       *
***********************************************************************
         SPACE 1
* JOB CARD CONTINUED, SEARCH FOR FIRST OPERAND                    MHL1
JOBCONT  DS    0H                                                 MHL1
         LA    R5,3(,R3)          POINT PAST '// '                MHL1
         LA    R14,1                                              MHL1
         LA    R15,15(,R3)        END OF SEARCH - COLUMN 16       MHL1
JOBCONT1 DS    0H                                                 MHL1
         CLI   0(R5),C' '         STILL CONTINUE?                 MHL1
         BNE   JOBCONT2           NO, SEARCH FOR KEYWORD          MHL1
         BXLE  R5,R14,JOBCONT1    LOOK FOR END OF BLANKS          MHL1
         OI    CEPUCOM+1,$JCLERR  CONTINUATION NOT FOUND          MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB             GET NEXT CARD                   MHL1
JOBCONT2 DS    0H                                                 MHL1
* SEE IF TIME WAS CONTINUED TO NEXT CARD                          MHL1
         TM    CEPUCOM+2,$SECONDS TIME CONTINUED?                 MHL1
         BO    JOBTIM11           YES, SEE IF SECONDS             MHL1
* SEE IF PROGRAMMER NAME CONTINUED ON NEXT CARD?                  MHL1
         TM    CEPUCOM+2,$PROGNAM NAME NEEDED?                    MHL1
         BO    JOBPGM11           YES, SEE IF NAME OR KEYWORD     MHL1
         B     JOBKEYWD           LOOK FOR KEYWORD                MHL1
* SCAN JOB CARD FOR KEYWORDS                                      MHL1
JOBSCAN  DS    0H                                                 MHL1
* LOOK TO SEE IF AT A COMMA, IF NOT SCAN TO COMMA                 MHL1
         LA    R14,1                                              MHL1
         LA    R15,70(,R3)        POINT TO COLUMN 71              MHL1
JOBSCAN1 DS    0H                                                 MHL1
         CLI   0(R5),C','         POINTING TO COMMA?              MHL1
         BE    JOBSCAN2           YES, BUMP PAST COMMA            MHL1
         CLI   0(R5),C' '         END OF CARD?                    MHL1
         BE    ENDJOB             YES, GET NEXT CARD              MHL1
         BXLE  R5,R14,JOBSCAN1    FIND A COMMA                    MHL1
         B     ENDJOB             END OF CARD (AFTER COLUMN 71)   MHL1
JOBSCAN2 DS    0H                                                 MHL1
         BXH   R5,R14,ENDJOB      BUMP PAST COMMA                 MHL1
JOBKEYWD DS    0H                                                 MHL1
* SHOULD BE POINTING TO START OF A KEYWORD OR A BLANK OR          MHL1
* TO THE MIDDLE OF A VALUE FOLLOWING A KEYWORD                    MHL1
         CLI   0(R5),C' '         BLANK AFTER A COMMA?            MHL1
         BE    ENDJOB             YES, CONTINUED NEXT CARD        MHL1
* LOOK FOR KEYWORDS WE ARE INTERESTED IN                          MHL1
         LA    R0,71-L'KPERFORM(,R3) LAST PLACE FOR PERFORM       MHL1
         CR    R0,R5              SEE IF ENOUGH ROOM FOR PERFORM  MHL1
         BL    JOBKEYW1           NO, LOOK FOR NEXT FIELD         MHL1
         CLC   KPERFORM,0(R5)     IS IT PERFORM?                  MHL1
         BE    JOBPERF            YES, CHECK IT OUT               MHL1
         SPACE 1                                                  MHL1
JOBKEYW1 DS    0H                                                 MHL1
         LA    R0,71-L'KCLASS(,R3) LAST PLACE FOR 'CLASS='        MHL1
         CR    R0,R5              SEE IF ENOUGH ROOM              MHL1
         BL    JOBKEYW2           NO, LOOK FOR NEXT FIELD         MHL1
         CLC   KCLASS,0(R5)       IS IT CLASS?                    MHL1
         BE    JOBCLASS           YES, CHECK IT OUT               MHL1
         CLC   KDPRTY,0(R5)       IS IT DPRTY?                    MHL1
         BE    JOBDPRTY           YES, CHECK IT OUT               MHL1
         SPACE 1                                                  MHL1
JOBKEYW2 DS    0H                                                 MHL1
         LA    R0,71-L'KTIME(,R3) LAST PLACE FOR TIME             MHL1
         CR    R0,R5              SEE IF ENOUGH ROOM              MHL1
         BL    JOBNEXT1           NO, LOOK FOR NEXT FIELD         MHL1
         CLC   KTIME,0(R5)        IS IT TIME?                     MHL1
         BE    JOBTIME            YES, CHECK IT OUT               MHL1
         SPACE 1                                                  MHL1
JOBNEXT  DS    0H                                                 MHL1
         LA    R14,1                                              MHL1
         LA    R15,70(,R3)        POINT TO COLUMN 71              MHL1
JOBNEXT1 DS    0H                                                 MHL1
         BXH   R5,R14,ENDJOB      POINT PAST WHATEVER             MHL1
         B     JOBSCAN1                                           MHL1
         SPACE 2                                                  MHL1
******************************************************************MHL1
*        PROCESS 'CLASS=' KEYWORD                                 MHL1
******************************************************************MHL1
         SPACE 1                                                  MHL1
JOBCLASS DS    0H                                                 MHL1
         LA    R5,6(R5)           BUMP PAST 'CLASS='              MHL1
         CLI   0(R4),X'01'        JOB CARD? ?                     MHL1
         BNE   JOBNEXT              NO, IGNORE 'CLASS='           MHL1
         OI    CEPUCOM,$CLASS     INDICATE GOT CLASS              MHL1
         CLI   0(R5),C'A'         IS IT CLASS A ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'B'         IS IT CLASS B ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'C'         IS IT CLASS C ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'D'         IS IT CLASS D ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'E'         IS IT CLASS E ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'F'         IS IT CLASS F ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'G'         IS IT CLASS G ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'H'         IS IT CLASS H ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'I'         IS IT CLASS I ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'J'         IS IT CLASS J ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'K'         IS IT CLASS K ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'L'         IS IT CLASS L ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'M'         IS IT CLASS M ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'N'         IS IT CLASS N ?                 WRH1
         BE    JOBNEXT              YES, GO CHK TIME              WRH1
         CLI   0(R5),C'R'         IS IT CLASS R ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'S'         IS IT CLASS S ?
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'X'         IS IT CLASS X ?                  WCH1
         BE    JOBNEXT              YES, GO CHK TIME              MHL1
         CLI   0(R5),C'Z'         IS IT CLASS Z ?
         BNE   SETBCLAS              NO, GO SET BAD CLASS BIT
         CLC   KDC,CEPJOB         IS THIS DATA CONTROL ?          MHL1
         BE    JOBNEXT               YES, THEY CAN HAVE CLASS Z   MHL1
SETBCLAS OI    CEPUCOM,$BCLASS    INDICATE BAD CLASS              MHL1
         SNAPPER                                                  MHL1
         B     JOBNEXT            GO CHK FOR TIME                 MHL1
         SPACE 1
KJOB     DC    C' JOB '
KSTARS   DC    C'****'            END OF TABLE                    MHL1
KTIME    DC    C'TIME='
KDPRTY   DC    C'DPRTY'                                           MHL1
KPERFORM DC    C'PERFORM'                                         MHL1
KCLASS   DC    C'CLASS='
KDC      DC    C'DC'
         EJECT
***********************************************************************
*     JOB CARD PROCESSING - FOUND TIME=                           MHL1*
***********************************************************************
         SPACE 1
JOBTIME  DS    0H
         LA    R5,L'KTIME(,R5)    BUMP PAST TIME=                 MHL1
         CLI   0(R4),X'01'        JOB CARD ?                      MHL1
         BNE   JOBNEXT            NO, IGNORE TIME                 MHL1
         LA    R15,70(,R3)        POINT TO COLUMN 71              MHL1
         OI    CEPUCOM,$TIME      INDICATE GOT TIME               MHL1
         CLI   0(R5),C'('         IS TIME IN PARENTHESES ?
         BNE   JOBTIM01             NO, GO PROCESS AS SUCH        MHL1
         OI    CEPUCOM,$TIMEP     SHOW TIME IN PARENS             MHL1
         BXH   R5,R14,JOBTIM97    BUMP PAST '('
JOBTIM01 DS    0H                                                 MHL1
         LA    R0,71-L'K1440(,R3) LAST COLUMN FOR '1440'          MHL1
         CR    R5,R0              ROOM FOR TIME=1440?             MHL1
         BH    JOBTIM02           NOT ENOUGH ROOM                 MHL1
         CLC   K1440,0(R5)        IS IT 'TIME=1440' ?
         BNE   JOBTIM02             NO, GO CHK THE CLASS          MHL1
         OI    CEPUCOM,$T1439     INDICATE CHANGED TO 1439        MHL1
         SNAPPER                                                  MHL1
         MVC   2(2,R5),K39        CHANGE 1440 TO 1439
JOBTIM02 DS    0H                                                 MHL1
         CLI   CEPCLASS,C'A'      IS IT CLASS A ?                 MHL1
         BE    JOBTIM03             YES, CONTINUE CHECK           MHL1
         CLI   CEPCLASS,C'B'      IS IT CLASS B ?                 MHL1
         BE    JOBTIM03             YES, CONTINUE CHECK           MHL1
         CLI   CEPCLASS,C'X'      IS THIS CLASS X?                MHL1
         BE    JOBTIM03           YES, MINUTES CAN BE ZEROS       MHL1
         CLI   CEPCLASS,C'N'      IS THIS CLASS N?                MHL1
         BE    JOBTIM03           YES, MINUTES CAN BE ZEROS       MHL1
         CLI   CEPCLASS,C'L'      IS IT CLASS L ?                 MHL1
         BNE   JOBTIM99             NO, GO GET NEXT KEYWORD
JOBTIM03 DS    0H                                                 MHL1
         SR    R7,R7              CLEAR FOR ACCUM MINUTES         MHL1
         LA    R15,70(,R3)        POINT TO COLUMN 71              MHL1
         LR    R6,R5              POINT TO START OF MINUTES       MHL1
         LA    R0,5(,R5)          POINT TO LAST PLACE FOR STOPPER MHL1
         CR    R15,R0             COMPARE TO END OF CARD          MHL1
         BNH   JOBTIM04                                           MHL1
         LR    R15,R0             USE LESSER VALUE                MHL1
JOBTIM04 CLI   0(R6),C'0'         IS THIS CHAR NUMERIC ?          MHL1
         BL    JOBTIM05             NO, END OF TIME               MHL1
         IC    R0,0(,R6)          GET NUMBER                      MHL1
         N     R0,X0F             CLEAR SIGN                      MHL1
         MH    R7,H10             JUMP ACCUM                      MHL1
         AR    R7,R0              ACCUM MINUTES                   MHL1
         BXLE  R6,R14,JOBTIM04    BUMP TO NEXT CHAR               MHL1
         SR    R6,R5              GET LENGTH OF TIME              MHL1
         C     R6,=F'4'           LENGTH GREATER THAN 4?          MHL1
         BH    JOBTIM97           ONLY 4 DIGITS ALLOWED           MHL1
         TM    CEPUCOM,$TIMEP     TIME IN PARENS?                 MHL1
         BO    JOBTIM97           END OF CARD ERROR IF PARENS     MHL1
         AR    R6,R5              POINT TO STOP CHARACTER         MHL1
         SPACE 1                                                  MHL1
JOBTIM05 DS    0H                                                 MHL1
         LR    R5,R6              POINT TO STOP CHARACTER         MHL1
         CLI   CEPCLASS,C'A'      CLASS A?                        MHL1
         BNE   JOBTIM06           NO, SEE IF B                    MHL1
         CH    R7,H1              COMPARE TO 1 MINUTE             MHL1
         B     JOBTIM09           CHECK RESULTS                   MHL1
JOBTIM06 DS    0H                                                 MHL1
         CLI   CEPCLASS,C'B'      CLASS B?                        MHL1
         BNE   JOBTIM07           NO, SEE IF L                    MHL1
         CH    R7,H7              COMPARE TO 7 MINUTE             MHL1
         B     JOBTIM09           CHECK RESULTS                   MHL1
JOBTIM07 DS    0H                                                 MHL1
         CLI   CEPCLASS,C'L'      CLASS L?                        MHL1
         BNE   JOBTIM08           NO, SEE IF X                    MHL1
         CH    R7,H10             COMPARE TO 10 MINUTE            MHL1
         B     JOBTIM09           CHECK RESULTS                   MHL1
JOBTIM08 DS    0H                 MUST BE CLASS=X OR N.           MHL1
         LTR   R7,R7              MINUTES=0?                      MHL1
JOBTIM09 DS    0H                                                 MHL1
         BH    JOBTIM98           MINUTES TOO HIGH                MHL1
         BL    JOBTIM99           LOW, MINUTES OK                 MHL1
         TM    CEPUCOM,$TIMEP     TIME IN PARENS?                 MHL1
*IF CLASS=X OR N AND NOT SECONDS, TIME WILL BE ZERO FOR JCL ERROR MHL1
         BNO   JOBTIM99           NO, TIME STILL OK               MHL1
         OI    CEPUCOM+2,$SECONDS SECONDS MUST BE ZERO            MHL1
*                                 OR 5 SECONDS FOR CLASS=X        MHL1
* CHECK FOR SECONDS                                               MHL1
JOBTIM10 DS    0H                                                 MHL1
         LA    R15,70(,R3)        COLUMN 71                       MHL1
         LA    R14,1                                              MHL1
         BXH   R5,R14,ENDJOB      POINT PAST COMMA                MHL1
         CLI   0(R5),C' '         CONTINUED?                      MHL1
         BE    ENDJOB             GET NEXT CARD                   MHL1
* FALL THROUGH WITH R5 POINTING TO SECONDS                        MHL1
JOBTIM11 DS    0H                                                 MHL1
* COME HERE IF SECONDS CONTINUED NEXT CARD                        MHL1
* ONLY NEED TO CHECK SECONDS IF CLASS=X,                          MHL1
* OR CLASSES A, B, L AND MINUTES MATCH TIME LIMIT                 MHL1
* R5 POINTS TO START OF SECONDS                                   MHL1
         LA    R15,70(,R3)        COLUMN 71                       MHL1
         LA    R14,1                                              MHL1
         SR    R7,R7              ACCUM SECONDS                   MHL1
         LA    R0,3(,R5)          POINT TO LAST POSITION FOR STOP MHL1
         CR    R15,R0             COMPARE TO COLUMN 71            MHL1
         BNH   JOBTIM12           USE COLUMN 71                   MHL1
         LR    R15,R0             MAX LENGTH OF MINUTES           MHL1
JOBTIM12 DS    0H                                                 MHL1
         CLI   0(R5),C'0'         NUMERIC                         MHL1
         BL    JOBTIM13           NO, MUST BE END OF SECONDS      MHL1
         IC    R0,0(,R5)          GET NUMBER                      MHL1
         N     R0,X0F             CLEAR SIGN                      MHL1
         MH    R7,H10             ACCUM                           MHL1
         AR    R7,R0                SECONDS                       MHL1
         BXLE  R5,R14,JOBTIM12                                    MHL1
         B     JOBTIM97           FORMAT ERROR                    MHL1
JOBTIM13 DS    0H                                                 MHL1
         SNAPPER                                                  MHL1
         CLI   CEPCLASS,C'X'      CLASS=X?                        MHL1
         BNE   JOBTIM15           NO, SECONDS MUST BE ZERO        MHL1
         CH    R7,H5              TIME=(,5)?                      MHL1
         BH    JOBTIM98           NO, TIME TOO HIGH               MHL1
         B     JOBTIM99           GET NEXT KEYWORD                MHL1
JOBTIM15 DS    0H                                                 MHL1
         SNAPPER                                                  MHL1
         CLI   CEPCLASS,C'N'      CLASS=X?                        MHL1
         BNE   JOBTIM14           NO, SECONDS MUST BE ZERO        MHL1
         CH    R7,H30             TIME=(,5)?                      MHL1
         BH    JOBTIM98           NO, TIME TOO HIGH               MHL1
         B     JOBTIM99           GET NEXT KEYWORD                MHL1
JOBTIM14 DS    0H                                                 MHL1
         LTR   R7,R7              SECONDS=0?                      MHL1
         BZ    JOBTIM99           YES, TIME OK                    MHL1
JOBTIM98 DS    0H                                                 MHL1
* TIME FIELD IN ERROR                                             MHL1
         OI    CEPUCOM,$BTIME     SET BAD TIME INDICATOR          MHL1
         SNAPPER                                                  MHL1
JOBTIM99 DS    0H                                                 MHL1
         NI    CEPUCOM+2,$FF-$SECONDS  TURN OFF SECONDS FLAG      MHL1
         B     JOBSCAN            POINTING AT ',' OR ')'          MHL1
JOBTIM97 DS    0H                                                 MHL1
* FORMAT ERROR IN TIME FIELD                                      MHL1
         OI    CEPUCOM+1,$JCLERR  BAD JOBCARD                     MHL1
         NI    CEPUCOM+2,$FF-$SECONDS  TURN OFF SECONDS FLAG      MHL1
         SNAPPER                                                  MHL1
         B     ENDJOB                                             MHL1
         SPACE 1
F1440    DC    F'1440'                                            MHL1
X0F      DC    AL4(X'0F')                                         MHL1
H1       DC    H'1'               1 MINUTE FOR CLASS=A            MHL1
H7       DC    H'7'               7 MINUTES FOR CLASS=B           MHL1
H10      DC    H'10'              10 MINUTES FOR CLASS=L          MHL1
H5       DC    H'5'               5 SECONDS FOR CLASS=X           MHL1
H15      DC    H'15'                                              MHL1
H30      DC    H'30'                                              MHL1
K1440    DC    C'1440'
K39      DC    C'39'              CHANGE '40' TO '39'             MHL1
         SPACE 3
         EJECT
***********************************************************************
*     JOB AND EXEC CARD PROCESSING - FOUND 'DPRTY'                    *
***********************************************************************
         SPACE 1
JOBDPRTY DS    0H                                                 MHL1
         LA    R5,L'KDPRTY+1(,R5)  BUMP PAST DPRTY                MHL1
         OI    CEPUCOM,$BDPRTY    INDICATE BAD DPRTY              MHL1
         SNAPPER                                                  MHL1
         B     JOBNEXT            GO CHK FOR PERFORM              MHL1
         EJECT
***********************************************************************
*     JOB AND EXEC CARD PROCESSING - FOUND 'PERFORM'                  *
***********************************************************************
         SPACE 1
JOBPERF  DS    0H
         OI    CEPUCOM,$BPERF     INDICATE BAD PERFORM            MHL1
         SNAPPER                                                  MHL1
         LA    R5,L'KPERFORM(,R5)  POINT PAST 'PERFORM'           MHL1
         B     JOBNEXT            GET NEXT KEYWORD                MHL1
         SPACE 2                                                  MHL1
******************************************************************MHL1
*        START OF EXEC CARD PROCESSING                            MHL1
******************************************************************MHL1
         SPACE 1                                                  MHL1
EXECSRCH DS    0H                                                 MHL1
         LA    R14,1                                              MHL1
         LA    R15,71-L'KEXEC-1(R3)  LAST PLACE FOR 'EXEC'        MHL1
         LA    R5,2(,R3)          POINT PAST '//'                 MHL1
EXECSRC1 DS    0H                                                 MHL1
* SCAN ACROSS STEPNAME TO THE FIRST BLANK AFTER STEPNAME          MHL1
         CLI   0(R5),C' '         FOUND BLANK?                    MHL1
         BE    EXECSRC2           YES, SCAN TO NON BLANK          MHL1
         BXLE  R5,R14,EXECSRC1    LOOK FOR BLANK                  MHL1
         B     ENDJOB             SHOULD NEVER HAPPEN             MHL1
EXECSRC2 DS    0H                                                 MHL1
* SCAN ACROSS BLANKS TO OPERATOR OR OPERAND                       MHL1
         BXH   R5,R14,ENDJOB      BUMP ACROSS BLANK               MHL1
         CLI   0(R5),C' '         STILL ON BLANKS?                MHL1
         BE    EXECSRC2           YES, CONTINUE SCAN              MHL1
EXECSRC3 DS    0H                                                 MHL1
* SEE IF OPERATOR ( EXEC ) OR KEYWORD                             MHL1
         SNAPPER                                                  MHL1
         CLC   KEXEC,0(R5)        LOOK FOR ' EXEC '               MHL1
         BNE   EXECSRC6           NO, MUST BE CONTINUATION        MHL1
EXECSRC4 DS    0H                                                 MHL1
         LA    R5,L'KEXEC-1(,R5)  POINT PAST 'EXEC'               MHL1
         LA    R15,70(,R3)        COLUMN 71                       MHL1
         BXH   R5,R14,ENDJOB      COMPARE TO END OF CARD          MHL1
EXECSRC5 DS    0H                                                 MHL1
         CLI   0(R5),C' '         STILL BLANK                     MHL1
         BNE   JOBKEYWD           NO, LOOK FOR KEYWORDS           MHL1
         BXLE  R5,R14,EXECSRC5                                    MHL1
         B     ENDJOB             SHOULD NEVER HAPPEN             MHL1
EXECSRC6 DS    0H                                                 MHL1
* DID NOT FIND OPERAND ' EXEC ', MUST BE CONTINUATION             MHL1
         LR    R0,R5              COPY ADDRESS                    MHL1
         SR    R0,R3              GET OFFSET                      MHL1
         CH    R0,H15             START BY COLUMN 16?             MHL1
         BNH   JOBKEYWD           YES, GOOD CONTINUE              MHL1
         OI    CEPUCOM+1,$JCLERR  BAD CONTINUATION                MHL1
         B     ENDJOB                                             MHL1
         SPACE 1                                                  MHL1
KEXEC    DC    C'EXEC '                                           MHL1
         EJECT
***********************************************************************
*     END OF JCL PROCESSING                                           *
***********************************************************************
         SPACE 1
ENDJOB   DS    0H
         SNAPPER                                                  MHL1
         RETURN (14,12),RC=0                                      MHL1
         EJECT
***********************************************************************
*     SEARCH FOR ANY ERRORS                                           *
***********************************************************************
         SPACE 1
ANYERRS  DS    0H
         GETMAIN R,LV=72,SP=$SUBPOOL GET WORK AREA
         ST    R1,8(,R13)         CHAIN                           MHL1
         ST    R13,4(,R1)           SAVEAREAS                     MHL1
         LR    R13,R1             ADDRESS OF SAVEAREA             MHL1
         XR    R10,R10            CLEAR RETURN CODE REG
         TM    CEPUCOM+1,$NOACCT  ANY ACCT FLD SPECIFIED ?        MHL1
         BZ    CHK002               YES, GO CHK FOR BAD ACCT
         BAL   R9,WTONACCT        PUT OUT WTO SAYING NO ACCT
         SPACE 1                                                  MHL1
* APPLICATION CODE IN ACCOUNT FIELD OK?                           MHL1
CHK002   TM    CEPUCOM+1,$BAPPL   IS ACCT FLD OK ?                MHL1
         BZ    CHK003               YES, GO CHK FOR DEPT NUMBER
         BAL   R9,WTOBACCT        PUT OUT WTO SAYING BAD ACCT
         SPACE 1                                                  MHL1
* DEPARTMENT NUMBER IN ACCOUNT FIELD OK?                           MHL1
CHK003   TM    CEPUCOM+1,$BDEPT   IS DEPT FLD OK ?                MHL1
         BZ    CHK004               YES, GO CHK FOR PROJECT ID
         BAL   R9,WTOBDEPT        PUT OUT WTO SAYING BAD ACCT
         SPACE 1                                                  MHL1
* CLASS SPECIFIED?                                                MHL1
CHK004   TM    CEPUCOM,$CLASS     IS CLASS SPECIFIED ?            MHL1
         BO    CHK005               YES, GO CHK FOR BAD CLASS
         BAL   R9,WTONCLSS        PUT OUT WTO SAYING NO CLASS
         SPACE 1                                                  MHL1
* CLASS OK?                                                       MHL1
CHK005   TM    CEPUCOM,$BCLASS    IS A BAD CLASS SPECIFIED ?      MHL1
         BZ    CHK006               NO, GO CHK FOR TIME
         BAL   R9,WTOBCLSS        PUT OUT WTO SAYING BAD CLASS
         SPACE 1                                                  MHL1
* TIME SPECIFIED?                                                 MHL1
CHK006   TM    CEPUCOM,$TIME      IS TIME SPECIFIED ?             MHL1
         BO    CHK007               YES, GO SEE IF BAD TIME
         BAL   R9,WTONTIME        PUT OUT WTO SAYING NO TIME
         SPACE 1                                                  MHL1
* TIME OK?                                                        MHL1
CHK007   TM    CEPUCOM,$BTIME     IS A BAD TIME SPECIFIED ?       MHL1
         BZ    CHK008               NO, GO SEE IF TIME 1440
         BAL   R9,WTOBTIME        PUT OUT WTO SAYING BAD TIME
         SPACE 1                                                  MHL1
* TIME=1440?                                                      MHL1
CHK008   TM    CEPUCOM,$T1439     WAS TIME 1440 SPECIFIED ?       MHL1
         BZ    CHK009               NO, GO SEE IF DPRTY
         BAL   R9,WTOT1439        PUT OUT WTO SAYING CHANGED TIME
         SPACE 1                                                  MHL1
* DPRTY SPECIFIED?                                                MHL1
CHK009   TM    CEPUCOM,$BDPRTY    WAS DPRTY SPECIFIED ?           MHL1
         BZ    CHK010               NO, GO SEE IF PERFORM
         BAL   R9,WTODPRTY        PUT OUT WTO SAYING BAD DPRTY
         SPACE 1                                                  MHL1
* PERFORM SPECIFIED?                                              MHL1
CHK010   TM    CEPUCOM,$BPERF     WAS PERFORM SPECIFIED ?         MHL1
         BZ    CHK011               NO, GO CHK JOBNAME
         BAL   R9,WTOPERF         PUT OUT WTO SAYING BAD PERFORM
         SPACE 1                                                  MHL1
* JOBNAME BEGIN WITH VALID APPLICATION CODE?                      MHL1
CHK011   TM    CEPUCOM+1,$BJNAME  IS JOB NAME OK ?                MHL1
         BZ    CHK012               YES, GO CHK LOCATION CODE
         BAL   R9,WTOBJNME        PUT OUT WTO SAYING BAD JOBNAME
         SPACE 1                                                  MHL1
* JOBNAME HAVE VALID LOCATION CODE?                               MHL1
CHK012   TM    CEPUCOM+1,$BLOC    IS LOCATION CODE OK ?           MHL1
         BNO   CHK013               YES, GO CHK FREQ CODE
         BAL   R9,WTOBLOC         PUT OUT WTO SAYING BAD LOCATION MHL1
         SPACE 1                                                  MHL1
* JOBNAME HAVE VALID FREQUENCY CODE?                              MHL1
CHK013   TM    CEPUCOM+1,$BFREQ   IS FREQUENCY CODE OK ?          MHL1
         BNO   CHK014               YES, GO CHK PROJECT CODE
         BAL   R9,WTOBFREQ                                        MHL1
         SPACE 1                                                  MHL1
* JOBNAME HAVE PROJECT CODE IF TEST JOB?                          MHL1
CHK014   TM    CEPUCOM+1,$BPROJ   IS PROJ FLD OK ?                MHL1
         BZ    CHK017               YES, CHK JOBNAME 8 CHARACTERS MHL1
         BAL   R9,WTOBPROJ        PUT OUT WTO SAYING BAD PROJECT ID
         SPACE 1                                                  MHL1
* JOBNAME HAVE PROJECT CODE IF TEST JOB?                          MHL1
CHK017   TM    CEPUCOM+2,$BADJOBN IS JOBNAME 8 CHARACTERS?        MHL1
         BZ    GETOUT               YES, END OF EXIT              MHL1
         BAL   R9,WTOBJNM8        PUT OUT WTO SAYING BAD JOBANME  MHL1
         EJECT 1
GETOUT   DS    0H                                                 MHL1
         AIF   (NOT &TEST).NOTESTX                                MHL1
         TM    CEPUCOM+1,$JCLERR  FOUND JCL ERROR?                MHL1
         BNO   GETOUTX            NO, EXIT                        MHL1
         WTO   'JOB FAILED - JCL ERROR',ROUTCDE=(2,11)            MHL1
GETOUTX  DS    0H                                                 MHL1
.NOTESTX ANOP                                                     MHL1
         LR    R1,R13             POINTER TO WORKAREA             MHL1
         L     R13,4(,R1)         OLD SAVEAREA                    MHL1
         SNAPPER                                                  MHL1
         FREEMAIN R,LV=72,A=(1),SP=$SUBPOOL FREE WORK AREA
         LR    R15,R10            GET RETURN CODE                 MHL1
         RETURN (14,12),RC=(15)                                   MHL1
         SPACE 1
         EJECT
***********************************************************************
WTONACCT DS    0H
         WTO   'ACCT900E- JOB FAILED BY ACCOUNTING SYSTEM BECAUSE THEREX
                ARE NO ACCOUNTING FIELDS',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBACCT DS    0H
         WTO   'ACCT901E- JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INX
               VALID APPLICATION CODE',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBDEPT DS    0H
         WTO   'ACCT902E- JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INX
               VALID DEPARTMENT NUMBER',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBPROJ DS    0H
         WTO   'ACCT912E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE TEST JX
               OB DOES NOT HAVE VALID PROJECT ID',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTONCLSS DS    0H
         WTO   'ACCT903E- JOB FAILED BY ACCOUNTING SYSTEM BECAUSE CLASSX
                NOT SPECIFIED',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBCLSS DS    0H
         WTO   'ACCT904E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INVX
               ALID CLASS PARAMETER CODED ON JOB CARD',ROUTCDE=(2,11)
         WTO   'ACCT904A-JOB CLASS CODED IS NOT AVAILABLE TO CUSTOMER  X
               USE',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTONTIME DS    0H
         WTO   'ACCT905E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE TIME FX
               IELD WAS NOT SPECIFIED',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBTIME DS    0H
         WTO   'ACCT906E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INVX
               ALID TIME SPECIFICATION FOR JOB-CLASS',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOT1439 DS    0H
         WTO   'ACCT906I-JOB HAD TIME=1440 - ACCOUNTING SYSTEM HAS CHANX
               GED THIS TO 1439 - PLEASE CHANGE JCL',ROUTCDE=(2,11)
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTODPRTY DS    0H
         WTO   'ACCT907E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INVX
               ALID USE OF DPRTY JCL SPECIFICATION',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOPERF  DS    0H
         WTO   'ACCT908E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE OF INVX
               ALID USE OF PERFORM JCL SPECIFICATION',ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBJNME DS    0H
         WTO   'ACCT909E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE JOBNAMX
               E DOES NOT START WITH VALID APPLICATION CODE',          X
               ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
WTOBLOC  DS    0H                                                 MHL1
         WTO   'ACCT910E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE LOCATIX
               ON CODE IS INVALID',ROUTCDE=(2,11)                 MHL1
         LA    R10,4              SET RETURN CODE                 MHL1
         BR    R9                 RETURN TO CALLER                MHL1
         SPACE 3
WTOBFREQ DS    0H                                                 MHL1
         WTO   'ACCT911E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE FREQUEX
               NCY CODE IS INVALID',ROUTCDE=(2,11)                MHL1
         LA    R10,4              SET RETURN CODE                 MHL1
         BR    R9                 RETURN TO CALLER                MHL1
         SPACE 3
WTOBJNM8 DS    0H
         WTO   'ACCT917E-JOB FAILED BY ACCOUNTING SYSTEM BECAUSE JOBNAMX
               E IS NOT 8 CHARACTERS IN LENGTH',                       X
               ROUTCDE=(2,11)
         LA    R10,4              SET RETURN CODE
         BR    R9                 RETURN TO CALLER
         SPACE 3
         LTORG
         AIF   (NOT &SNAP).NOSNAP0                                MHL1
         EJECT                                                    MHL1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *   MHL1
*        THIS ROUTINE ISSUES A SNAP WHEN RUNNING UNDER            MHL1
*        'TESTEXIT'. IT IS CALLED USING R9 AS THE RETURN          MHL1
*        REGISTER.                                                MHL1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *   MHL1
SNAPPER  DS    0H                                                 MHL1
         STM   R0,R15,SNAPREGS    SAVE WORK REGS                  MHL1
         DEVTYPE SNAP+40,SNAPDEV  CHECK FOR DD CARD               MHL1
         LTR   R15,R15            CHECK FOR ZERO RETURN CODE      MHL1
         BNZ   SNAPPER0           NO DD CARD, EXIT                MHL1
         LA    R15,SNAPSAVE                                       MHL1
         ST    R15,8(,R13)        CHAIN                           MHL1
         ST    R13,4(,R15)          SAVEAREA                      MHL1
         LR    R13,R15                                            MHL1
         OPEN  (SNAP,(OUTPUT))                                    MHL1
SNAPPER1 DS    0H                                                 MHL1
         ST    R2,SNAPLIST        START OF PARMS                  MHL1
         LA    R0,CEPLNGTH-1(,R2) END OF PARMS                    MHL1
         ST    R0,SNAPLIST+4                                      MHL1
         ST    R3,SNAPLIST+8      ADDRESS OF JOBCARD              MHL1
         LA    R0,79(,R3)         END OF JOBCARD                  MHL1
         ST    R0,SNAPLIST+12                                     MHL1
         ST    R4,SNAPLIST+16     POINTER TO JCL TYPE             MHL1
         LA    R0,1(,R4)          END OF POINTER                  MHL1
         ST    R0,SNAPLIST+20                                     MHL1
         L     R15,SNAPCNT                                        MHL1
         SNAP  DCB=SNAP,ID=(R15),SDATA=CB,                        MHL1 -
               LIST=SNAPLIST                                      MHL1
SNAPPER9 DS    0H                                                 MHL1
         LA    R0,1               ADD                             MHL1
         A     R0,SNAPCNT          1 TO                           MHL1
         ST    R0,SNAPCNT           SNAPCNT                       MHL1
         CLOSE SNAP                                               MHL1
         L     R13,4(,R13)        GET OLD SAVEAREA                MHL1
SNAPPER0 DS    0H                                                 MHL1
         LM    R0,R15,SNAPREGS    RESTORE CALLING REGS            MHL1
         BR    R9                 EXIT                            MHL1
* SNAP LIST FOR SMALL SNAP                                        MHL1
SNAPLIST DC    2F'0'              POINTER TO CEPA                 MHL1
         DC    2F'0'              POINTER TO JCL CARD             MHL1
         DC    2F'0'              POINTER TO JCL TYPE             MHL1
         DC    A(SNAPREGS)        POINTER TO SNAP REGS            MHL1
         DC    AL1(128),AL3(SNAPREGS+63)                          MHL1
* REGISTER SAVEAREA FOR REGS AT ENTRY TO SNAP                     MHL1
SNAPREGS DS    16F                                                MHL1
* SAVEAREA FOR USE BY SNAP ROUTINE                                MHL1
SNAPSAVE DS    18F                                                MHL1
* COUNTER FOR SNAP MACRO                                          MHL1
SNAPCNT  DC    F'0'               COUNTER                         MHL1
* WORKAREA FOR DEVTYPE MACRO                                      MHL1
SNAPDEV  DS    2F                                                 MHL1
* DCB FOR SNAP IF NEEDED                                          MHL1
SNAP     DCB   DSORG=PS,RECFM=VBA,MACRF=W,                        MHL1 -
               BLKSIZE=1632,LRECL=125,                            MHL1 -
               DDNAME=SNAP                                        MHL1
.NOSNAP0 ANOP                                                     MHL1
         EJECT
***********************************************************************
*     REGISTER SAVE AREA                                              *
***********************************************************************
         SPACE 1
UJVAREA  DSECT
SAVE     DS    18F
* COMMON EXIT PARAMETER AREA DSECT                                MHL1
* THE FOLLOWING WAS COPIED FROM THE MACRO 'IEFJMR'                MHL1
CEPA     DSECT                                                    MHL1
CEPJOB   DS    CL8       JOB NAME                                 MHL1
CEPENTRY DS    F         ENTRY TIME IN 1/100'S SEC                MHL1
CEPEDATE DS    F         ENTRY DATE 00YYDDDF                      MHL1
CEPCPUID DS    CL4       CPU - SID AND MDL FROM SMCA              MHL1
CEPUSEID DS    CL8       USER ID                                  MHL1
* CEPUSEID IS USED TO SAVE JOBNAME INFO DURING JOBNAME            MHL1
* VALIDATION SINCE 2 JOBNAME FORMATS ARE ALLOWED                  MHL1
* INITIALIZED TO BLANKS BY JES AT READER TIME                     MHL1
CEPUAPCD EQU   CEPUSEID,2       APPLIACTION CODE                  MHL1
CEPULOC  EQU   CEPUSEID+2,1     LOCATION CODE                     MHL1
CEPUFREQ EQU   CEPUSEID+3,1     FREQUENCY CODE                    MHL1
CEPSTEP  DS    C         STEP NUMBER                              MHL1
CEPINDC  DS    C                       INDICATOR SWITCHES         MHL1
CEPJOBSW EQU   X'80'  JOB FUNCTIONS REQUESTED                     MHL1
CEPSTPSW EQU   X'40'  STEP FUNCTIONS REQUESTED                    MHL1
CEPEXITS EQU   X'20'    USER EXITS REQUESTED                      MHL1
CEPXONLY EQU   X'10'     EXITS ONLY SPECIFIED                     MHL1
CEPFIND  EQU   X'01'                   FOREGROUND INDICATED       MHL1
CEPFLG   DS    C       JOB STATUS INDICATOR                       MHL1
CEPSTRS  EQU   X'80'   STEP RESTART                               MHL1
CEPCHRS  EQU   X'40'   CHECKPOINT RESTART                         MHL1
CEPCNRS  EQU   X'20'   CONTINUE RESTART                           MHL1
CEPWARM  EQU   X'08'   WARMSTART JOB                              MHL1
CEPCLASS DS    1C      JOB CLASS                                  MHL1
CEPUCOM  DS    F       USER COMMUNICATION FIELD -                 MHL1
*                        INITIALIZED TO ZEROS BY JES AT START     MHL1
*                        OF JOB EXECUTION                         MHL1
CEPLNGTH EQU   *-CEPJOB                                           MHL1
         END   IEFUJV
