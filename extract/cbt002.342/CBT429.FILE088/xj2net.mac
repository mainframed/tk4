XJ2NET   TITLE 'BII USER EXIT CODE FOR HASPNET'
*****************************************************************
*                                                               *
* MODULE NAME = XJ2NET      BII EXIT 13                         *
*                                                               *
* DESCRIPTIVE NAME = USER EXIT MODULE TO RETURN RC OF 8         *
*                    TO NOTIFY THE TSO USER WITH HASP549 MSG.   *
*                                                               *
* FUNCTION = PROVIDE SUPPORT FOR TSO/E TO NOTIFY THE TSOUSER    *
*            WHEN A FILE IS WAITING ON THE RECEIVE QUEUE.       *
*                                                               *
*                                                               *
*                                                               *
* NOTES = SEE BELOW                                             *
*                                                               *
*    DEPENDENCIES =                                             *
*                                                               *
*    RESTRICTIONS =                                             *
*                                                               *
*    REGISTER CONVENTIONS = SEE ENTRY POINT DOCUMENTATION.      *
*                                                               *
*                                                               *
*    PATCH LABEL = NONE                                         *
*                                                               *
* MODULE TYPE = PROCEDURE ( CSECT TYPE )                        *
*                                                               *
*    ATTRIBUTES = READ ONLY, REENTRANT (BY JES2 DEFINITION).    *
*                                                               *
* ENTRY POINT = BII013                                          *
*                                                               *
*    PURPOSE = SEE FUNCTION (ABOVE).                            *
*                                                               *
*    LINKAGE = SEE ENTRY POINT DOCUMENTATION.                   *
*                                                               *
* INPUT =  SEE ENTRY POINT DOCUMENTATION.                       *
*                                                               *
* OUTPUT = NONE                                                 *
*                                                               *
* EXIT-NORMAL = RETURN TO CALLER USING RETURN MACRO             *
*                                                               *
* EXIT-ERROR = NONE                                             *
*                                                               *
* EXTERNAL REFERENCES = SEE BELOW                               *
*                                                               *
*    ROUTINES = NONE                                            *
*                                                               *
*    DATA AREAS = SEE BELOW                                     *
*                                                               *
* TABLES = SEE BELOW                                            *
*                                                               *
* MACROS =                                                      *
*                                                               *
* CHANGE ACTIVITY                                               *
*                                                               *
*     RELEASE = SP2.1.5                                         *
*                                                               *
*                                                               *
*****************************************************************
         COPY  $HASPGBL           COPY HASP GLOBALS               BHWW
XJ2NET   $MODULE NOTICE=NONE,                                     BHWW +
               TITLE='BII TSOE NOTIFY',                           BHWW +
               ENVIRON=JES2,                                      BHWW +
               $HCT,              GENERATE HASP HCT DSECT         BHWW +
               $BUFFER,           GENERATE HASP BUFFER DSECT      BHWW +
               RPL,               GENERATE HASP RPL    DSECT      BHWW +
               $JCT,              GENERAGE HASP JCT DSECT         BHWW +
               $JQE,              GENERATE HASP JQE DSECT         BHWW +
               $JOE,              GENERATE HASP JOE DSECT         BHWW +
               $DCT,              GENERATE HASP DCT DSECT         BHWW +
               $PCE,              GENERATE HASP PCE DSECT         BHWW +
               $QSE,              GENERATE HASP QSE DSECT         BHWW +
               $CAT,              GENERATE HASP CAT DSECT         BHWW +
               $MIT,              GENERATE HASP MIT DSECT         BHWW +
               $PDDB,             GENERATE HASP PDDB DSECT        BHWW +
               $NHD,              GENERATE HASP NHD DSECT         BHWW +
               $HASPEQU           GENERATE HASP EQUATES           BHWW
*****************************************************************
* ENTRY POINT DOCUMENTATION:                                    *
*                                                               *
*     REGISTERS AT ENTRY;                                       *
*                                                               *
*           R0  =       = N/A                                   *
*           R1  =       = NETWORK JOB HEADER                    *
*           R2  =       = N/A                                   *
*           R3  =       = N/A                                   *
*           R4  =       = N/A                                   *
*           R5  =       = N/A                                   *
*           R6  =       = N/A                                   *
*           R7  =       = N/A                                   *
*           R8  =       = N/A                                   *
*           R9  =       = N/A                                   *
*           R10 =       = ADDRESS OF JCT                        *
*           R11 =       = ADDRESS OF HCT                        *
*           R12 =       = OUR BASE                              *
*           R13 =       = ADDRESS OF PCE                        *
*           R14 =       = RETURN ADDRESS                        *
*           R15 =       = ENTRY ADDRESS                         *
*                                                               *
*****************************************************************
         SPACE 3
BII013   $ENTRY BASE=R12   PROVIDE PROCESSOR ENTRY POINT
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             ESTABLISH BASE
         L     R2,0(,R1)           GET NJH ADDR
         L     R3,8(,R1)           GET PDDB ADDR
         MVC   NODEJ(8),NJHGORGN-NJH(R2)        MOVE ORIGIN NODE
         MVC   SENDRJ(7),NJHGJNAM-NJH(R2)        MOVE ORIGIN SENDER
         CLC   =C'TSO',SENDRJ       BEGIN WITH TSO
         BNE   DOUSID                  NO
         MVC   SENDRJ(7),$BLANKS       CLEAR
         MVC   SENDRJ(5),NJHGJNAM-NJH+3(R2) MOVE TO SENDER
DOUSID   DS    0H
         CLC   =C'TSO',NJHGUSID-NJH(R2)
         BNE   DOREST                  NO
         MVC   NJHGUSID-NJH(5,R2),NJHGUSID-NJH+3(R2)
         MVC   NJHGUSID-NJH+5(3,R2),$BLANKS
DOREST   DS    0H
         MVC   USERJ(7),$BLANKS       CLEAR
         MVC   USERJ(7),PDBWTRID-PDB(R3) MOVE USERID OF RECR
         ICM   R7,15,PDBRECCT-PDB(R3) GET RECORD COUNT
         CVD   R7,DWORK                MAKE DEC
         MVC   DDWORK,=X'4020202020202120' MASK IT
         ED    DDWORK(8),DWORK+4       MAKE PRINTABLE
         MVC   SDRCNT,DDWORK+1         MOVE TO MESSAGE
         OC    SDRCNT,$BLANKS          MAKE UPPER CASE
         TIME DEC
         ST    R0,DWORK                SAVE TIME
         ST    R1,DWORK+4              SAVE DATE
         OI    DWORK+3,X'0F'           PUT SIGN ON
         MVC   DDATE-1(7),=X'4021204B202020'    MAKE DATE READABLE
         ED    DDATE-1(7),DWORK+5       SAME
         MVC   CTME1-1(11),=X'4021204B20204B20204B20'
         ED    CTME1-1(11),DWORK        SAME
         MVC   DATEJ(6),DDATE          MOVE DATE
         MVC   TIME1(8),CTME1          MOVE TIME
         LA    R4,USERJ                POINT TO USER
         LA    R5,7                    SET COUNT
         MVC   USERJ1,$BLANKS
ADUSR    DS    0H
         CLI   0(R4),C' '              END OF STRING
         BE    ADUSR1                  YES
         LA    R4,1(,R4)               ADJUST PTR
         BCT   R5,ADUSR
ADUSR1   DS    0H
         MVC   0(L'RST,R4),RST         MOVE THE REST
         LA    R1,SENDJ1
         SR    R4,R1
         LA    R4,L'RST(,R4)
         STH   R4,SENDJ1
         LA    R0,0
         LA    R1,SENDJ1
         SVC   34
EX2RET08 LA    R15,8               SET RETURN CODE 8 NOTIFY USER
         $RETURN RC=(R15)          RETURN TO CALLER
         SPACE 3
EX2RET00 LA    R15,0               SET ZERO RETURN CODE.
         $RETURN RC=(R15)          RETURN TO CALLER
         SPACE 3
         TITLE 'BII EXIT 013 CONSTANTS AND DSECTS      '
*****************************************************************
DWORK    DC    D'0'
         DC    C' '
DDATE    DC    CL6' '
         DC    C' '
CTME1    DC    CL10' '
DDWORK   DC    CL8' '
         DS    0F
SENDJ1   DC    AL2(SENDE-SENDJ1)
         DC    AL2(0)
         DC    C'SE ',X'7D'
DATEJ    DC    C'YY.DDD'
         DC    C' '
TIME1    DC    C'HH.MM.SS'
         DC    C' '
         DC    C'$HASP549 MAIL FROM '
NODEJ    DC    CL8' ',C' - '
SENDRJ   DC    CL7' ',C' '
         DC    C'RECORDS - '
SDRCNT   DC    CL7' ',X'7D'
         DC    C',USER=('
USERJ    DC    CL7' '
USERJ1   DC    CL15' '
SENDE    EQU   *
RST      DC    C'),LOGON '
         SPACE 1
         LTORG
*****************************************************************
         DROP  ,
         TITLE 'BII TSOE EPILOG'
         $MODEND ,
         END   ,
