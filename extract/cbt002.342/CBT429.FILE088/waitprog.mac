PROGRAM  TITLE 'SKELETON PROGRAM FOR TEST PURPOSES'
         SPACE
PROGR    CSECT
*                                       REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RA       EQU   10
R11      EQU   11
RB       EQU   11
R12      EQU   12
RC       EQU   12
R13      EQU   13
RD       EQU   13
R14      EQU   14
RE       EQU   14
R15      EQU   15
RF       EQU   15
*                                       END OF EQUATES
         EJECT
         SAVE  (14,12)                  SAVE REGS ON ENTRY
         LR    RC,RF                    ESTABLISH ADDRESSABILITY
         USING PROGR,RC                 "         "
         ST    RD,REGSAVE+4             ESTABLISH LINKAGE CONVENTION
         LR    R2,RD                    "
         LA    RD,REGSAVE               "
         ST    RD,8(R2)                 "
         SPACE
* * * *  * * *  * * * * * * * * * * * *  * * * * * * * * * * * * *
*              PROGRAM MEAT
* * * *  * * *  * * * * * * * * * * * *  * * * * * * * * * * * * *
         L     R2,0(0,R1)
         MVC   TIMEREQ(4),2(R2)
         MVC   REQUEST+36(4),TIMEREQ
         EXTRACT TIOTADDR,'S',FIELDS=(TIOT)    GET TIOT ADDRESS
         L     R4,TIOTADDR
         MVC   REQUEST+4(8),0(R4)
         WTO   MF=(E,REQUEST)
         LR    R9,R1                    SAVE MESSAGE NUMBER
         PACK  DBLPACK,TIMEREQ
         CVB   R5,DBLPACK
         MH    R5,=H'100'
         ST    R5,BINTVL
         STIMER WAIT,BINTVL=BINTVL
NORMEND  DS    0H                       NORMAL END ROUTINE
         DOM   MSG=(9)                  DELETE OLD MESSAGE
         WTO   MF=(E,DONEIT)
         L     RD,4(RD)                 LOAD RECURSIVE SAVE REG
         RETURN (14,12),RC=0
         EJECT
         TITLE 'USER DATA AREAS'
REGSAVE  DS    18F                      SAVE AREA FOR REGISTERS
DBLPACK  DS    D
BINTVL   DS    F
         DS    0D
REQUEST  WTO   'XXXXXXXX WAIT TIME REQUESTED IS XXXX SECONDS',        XX
               ROUTCDE=11,DESC=2,MF=L
DONEIT   WTO   'WAIT TIME HAS EXPIRED',ROUTCDE=11,MF=L
TIMEREQ  DC    CL4' '
TIOTADDR DC    A(0)                     TIOT ADDRESS.
         END
