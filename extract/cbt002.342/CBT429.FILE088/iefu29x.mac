IEFU29   TITLE 'SMF SWITCH EXIT ROUTINE                        '
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFU29                                    *
*                                                                     *
*                SMF EXIT ROUTINE TO START DUMP OF SMF WHEN SMF       *
*                SWITCH COMMAND IS ISSUED.                            *
*                                                                     *
*                                                                     *
*             FUNCTION =                                              *
*                ISSUES START COMMAND FOR DUMPXY PROCEDURE.           *
*                                                                     *
*                OPERATION =                                          *
*                   ISSUE COMMAND 'START DUMPXY,DSNAME=SYS1.MANN'     *
*                   WHERE N IS REPLACED BY THE NUMBER OF THE SMF      *
*                   DATA SET TO BE DUMPED.                            *
*                   ISSUE WTO THAT COMMAND HAS BEEN ISSUED.           *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = NONE                                 *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 1  = WORK REGISTERS               *
*                    REGISTERS 2 TO 11 = UNUSED                       *
*                    REGISTER  12      = ADDRESSABILITY TO IEFU29     *
*                                        CSECT                        *
*                    REGISTER  13      = ADDRESSIBILITY TO DATA DSECT *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   BINARY ZEROES)                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASSEMBLER H VERSION 2                    *
*                                                                     *
*                MODULE SIZE = 1200 BYTES                             *
*                                                                     *
*                ATTRIBUTES = KEY 0, REENTRANT,                       *
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          *
*                                                                     *
*             ENTRY POINTS = IEFU29 (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                                                                     *
*                                                                     *
*             INPUT = REG1 POINTS TO FULLWORD ADDRESS OF DATA SET     *
*                     NAME (SYS1.MANX/SYS1.MANY) TO BE DUMPED .       *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BSM 0,14             *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = FOUR TO PREVENT SMF FROM ISSUING       *
*                              MESSAGE IEE362A OR IEE362I             *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = FOUR                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, WTO, RETURN, GETMAIN, FREEMAIN           *
*                                                                     *
*             CHANGE ACTIVITY = THIS MODULE HAS BEEN UPDATED          *
*                FOR MVS/XA. THIS MODULE RUNS IN 31-BIT MODE          *
*                ABOVE THE LINE. AS SVC 34 REQUIRES PARAMETERS        *
*                BELOW THE LINE, STORAGE IS ACQUIRED WITH THE         *
*                LOC=BELOW OPTION.                                    *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*             MESSAGES =                                              *
*                INFORM OPERATOR THAT COMMAND HAS BEEN ISSUED.        *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
IEFU29   CSECT ,                       CSECT NAME DECLARED
IEFU29   AMODE 31                                             IPOXA
IEFU29   RMODE ANY                                            IPOXA
*
*  CONSTANT EQUATES
*
CMDSVC   EQU   34                      SVC TO ISSUE COMMAND
DSNLEN   EQU   9                       LENGTH OF DSNAME FIELD
*
K0       EQU   0                       CONSTANT 0
K1       EQU   1                       CONSTANT 1
K2       EQU   2                       CONSTANT 2
K4       EQU   4                       CONSTANT 4
K8       EQU   8                       CONSTANT 8
K12      EQU   12                      CONSTANT 12
K16      EQU   16                      CONSTANT 16
*
* REGISTER EQUATES
*
R00      EQU   00                      REGISTER 0
R01      EQU   01                      REGISTER 1
R02      EQU   02                      REGISTER 2
R03      EQU   03                      REGISTER 3
R04      EQU   04                      REGISTER 4
R05      EQU   05                      REGISTER 5
R06      EQU   06                      REGISTER 6
R07      EQU   07                      REGISTER 7
R08      EQU   08                      REGISTER 8
R09      EQU   09                      REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
R12      EQU   12                      REGISTER 12
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
         EJECT
         SAVE  (14,12),,IEFU29_&SYSDATE SAVE REGISTERS
         USING IEFU29,R12              SET UP BASE ADDRESSABILITY
         USING DATA,R13                SET UP DATA AREA ADDRESSABILITY
         LR    R12,R15                 LOAD BASE REG WITH ENTRY POINT
         L     R08,K0(R01)             SAVE INPUT PARM(DSNAME)
         GETMAIN RU,LV=DATALEN,SP=0,LOC=BELOW GET STORAGE        IPOXA
         ST    R13,K4(R01)             SAVE CALLER'S SAVE AREA ADDR
         ST    R01,K8(R13)             SAVE MY SAVE AREA ADDRESS
         LR    R13,R01                 LOAD SAVE AREA ADDRESS
         MVC   ENQLIST(LENQLIST),ENQLSTX LOAD IN MODEL PARM LIST
         ENQ   MF=(E,ENQLIST)          TEST IF RESOURCE IN USE?
         LTR   R15,R15                 WAS THE RESOURCE AVAILABLE?
         BNZ   SKIPDUMP                NO, DO NOT START DUMP
         MVC   WTOAREA(WTOLEN),WTOL    MOVE IN WTO MESSAGE
         MVC   WTOAREA+DSNOFF(DSNLEN),K0(R08) MOVE DSNAME IN MSG
         MVC   CMDAREA(CMDLEN),CMDL    MOVE IN START COMMAND
         MVC   CMDAREA+DSNCOFF(DSNLEN),K0(R08) MOVE DSN IN START CMD
         SLR   R00,R00                 CLEAR REG ZERO FOR SVC 34
         LA    R01,CMDAREA             POINT TO START COMMAND
         SVC   CMDSVC                  ISSUE START COMMAND
         WTO   MF=(E,WTOAREA)          ISSUE MSG
SKIPDUMP LR    R01,R13                 LOAD GETMAINED AREA
         L     R13,K4(R13)             POINT TO CALLER'S SAVE AREA
         FREEMAIN RU,LV=DATALEN,A=(1)  FREE GETMAINED STORAGE  IPOXA
         LM    14,12,12(13)            RESTORE REGISTERS       IPOXA
         LA    15,4                    SET RETURN CODE TO 4    IPOXA
         BSM   0,R14                   RESTORE CALLER'S MODE   IPOXA
         EJECT
*
*  DATA AREA
*
SMFQNAME DC    CL8'IPOSMF01'                                  SIPO50
SMFRNAME DC    CL7'DATASET'
*
CMDL     DS    0F                      START COMMAND FORMAT FOR SVC 34
         DC    AL2(CMDLEN),AL2(00)     LENGTH OF STRING
         DC    C'START DUMPXY,DSNAME=XXXXXXXXX    ' . COMMAND + BLANK
*                456789012345678901234567890123456  . CHECK COLUMNS
CMDLEN   EQU   *-CMDL             LENGTH OF COMMAND STRING
DSNCOFF  EQU   24                 OFFSET OF DSNAME FIELD IN CMD LIST
*
WTOL     WTO   'IEFU29 HAS ISSUED COMMAND ''START DUMPXY,DSNAME=XXXXXXXC
               XX'' ',ROUTCDE=(1,2,11),MF=L
*               456789012345678901234567890 1234567890123456789012
WTOLEN   EQU   *-WTOL             LENGTH OF WTO STRING
DSNOFF   EQU   51                 OFFSET OF DSNAME FIELD IN WTO LIST
ENQLSTX  ENQ   (SMFQNAME,SMFRNAME,E,,SYSTEM),RET=TEST,MF=L
*
* DATA DSECT - AREA TO BE GETMAIN'ED
*
DATA     DSECT
SAVE     DS    18F                REGISTER SAVE AREA
CMDAREA  DS    0F,XL(CMDLEN)      AREA FOR COMMAND
WTOAREA  DS    0F,XL(WTOLEN)      AREA FOR WTO PARM LIST
ENQLIST  ENQ   (SMFQNAME,SMFRNAME,E,,SYSTEM),RET=TEST,MF=L
LENQLIST EQU   *-ENQLIST          LENGTH OF WTO STRING
RESERVED DS    4D                 RESERVED
DATALEN  EQU   *-DATA             EQUATE FOR LENGTH OF DATA DSECT
*
IEFU29   CSECT                    GET BACK TO CSECT
         DS    0D                 FORCE TO DOUBLE WORD
*
* PATCH AREA
*
MODLEN   EQU   *-IEFU29           EQUATE FOR MODULE LENGTH(- PATCH)
PTCHLEN  EQU   ((MODLEN+7)/8+7)/8*8 PATCH AREA LENGTH EQUATE
PTCHLNTH DC    Y(PTCHLEN)          LENGTH OF PATCH AREA
PTCHBASE DC    S(PATCH)            BASE DISPLACED ADDRESS OF PATCH
PATCH    DC    XL(PTCHLEN)'00'     PATCH AREA
         END   IEFU29
