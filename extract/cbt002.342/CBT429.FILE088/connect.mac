         MACRO
&NAME    CONNECT  &L,&D,&CPPL=NO,&BASE=,&PARM=,&CLEAR=,&LI=LA,&R0=,    W
               &MODESET=,&SAVE=YES,&ID=,&ENTRY=,&ENTREG=15,            W
               &SP=0,&MODSET=YES,                              01/06/78W
               &COPYRT=,&EQUREGS=NO,                           11/21/86W
               &LOC=BELOW,                                     12/08/86W
               &AMODE=,&RMODE=,&MODSAVE=,                      12/08/86W
               &REGSAVE=(14,12)                                04/12/77
.*
.*       THIS MACRO GENERATES CSECT START-UP LINKAGE.
.*
.*                                                             01/06/78
.*  CHANGES:                                                   01/06/78
.*                                                             01/06/78
.*       01/06/78 - ADD &SP (SUBPOOL SUPPORT)                  01/06/78
.*       11/21/86 - ADD &COPYRT (COPYRIGHT DATE) SUPPORT       11/21/86
.*       12/08/86 - ADD &LOC (LOCATION) SUPPORT FOR XA         12/08/86
.*                  ALSO ADDED AMODE/RMODE MODE SWITCH SUPPORT 01/06/78
.*       12/11/87 - ADDED SUPPORT FOR "COPYRT=NO"              12/11/87
.*       03/09/88 - ADDED DUAL XA/NONXA SUPPORT                03/09/88
.*                                                             01/06/78
         GBLC  &SYSSPLV                                        03/09/88
         LCLA  &I,&J,&K,&N,&R                                  04/12/77
         LCLC  &CNAME,&XRNAME,&LITLBL,&MODSAV                  04/12/77
         LCLC  &AMODLBL,&AMOD1,&RMOD1                          01/06/78
         LCLC  &CPYYR,&PSTCNS                                  11/21/86
&LITLBL  SETC  'LIT&SYSNDX'
&PSTCNS  SETC  'PST&SYSNDX'
         SPLEVEL TEST
         AIF   ('&SYSSPLV' EQ '1').NOMOD
&AMOD1   SETC  '&AMODE'
&RMOD1   SETC  '&RMODE'
         AIF   ('&AMODE' EQ '').RMODQQ
         AIF   ('&AMODE' EQ '24').RMOD24
         AIF   ('&AMODE' EQ '31').RMODANY
         AIF   ('&AMODE' EQ 'ANY').RMOD24
         MNOTE 12,'** INVALID AMODE, &AMODE, SPECIFIED **'
         MEXIT
.RMODQQ  ANOP
         AIF   ('&RMODE' EQ '').NOMOD
         AIF   ('&RMODE' EQ 'ANY').AMOD31
         AIF   ('&RMODE' EQ '24').AMOD24
         MNOTE 12,'** INVALID RMODE, &RMODE, SPECIFIED **'
         MEXIT
.AMOD31  ANOP
&AMOD1   SETC  '31'
         AGO   .RMODDUN
.AMOD24  ANOP
&AMOD1   SETC  '24'
         AGO   .RMODDUN
.RMODANY ANOP
         AIF   ('&RMODE' EQ 'ANY').RMODDUN
         AIF   ('&RMODE' NE '').RMOD24
&RMOD1   SETC  'ANY'
         AGO   .RMODDUN
.RMOD24  ANOP
         AIF   ('&RMODE' EQ '24').RMODDUN
         AIF   ('&RMODE' NE '').RMODERR
&RMOD1   SETC  '24'
         AGO   .RMODDUN
.RMODERR MNOTE 12,'** INVALID RMODE OR AMODE/RMODE COMBINATION **'
         MNOTE *,'** REQUESTED AMODE=&AMODE, RMODE=&RMODE **'
         MEXIT
.NOMOD   ANOP
&AMOD1   SETC  ''
&RMOD1   SETC  ''
.RMODDUN ANOP
         AIF   ('&COPYRT' EQ '').NOCPYRT                       11/21/86
         AIF   ('&COPYRT' EQ 'NO').SKIPCPR                     12/11/87
         AIF   (('&COPYRT'(1,1) EQ ''''(1,1) OR '&COPYRT'(1,1) EQ '(') +
               AND K'&COPYRT GE 3).TRIMCPY
&CPYYR   SETC  '&COPYRT'                                       11/21/86
         AGO   .COPYSET                                        11/21/86
.TRIMCPY ANOP
&CPYYR   SETC  '&COPYRT'(2,K'&COPYRT-2)
.SKIPCPR ANOP                                                  12/11/87
         AGO   .SETNAME                                        12/11/87
.NOCPYRT ANOP                                                  11/21/86
&CPYYR   SETC  '&SYSDATE'(7,2)                                 11/21/86
&CPYYR   SETC  '19&CPYYR'                                      11/21/86
.COPYSET ANOP                                                  11/21/86
.SETNAME ANOP                                                  12/11/87
&CNAME   SETC  '&SYSECT'
         AIF   ('&AMOD1' EQ '' AND '&RMOD1' EQ '').NOMOD2
&CNAME   AMODE &AMOD1
&CNAME   RMODE &RMOD1
.NOMOD2  ANOP
         AIF   (T'&ENTRY EQ 'O').XANOP1
&CNAME   SETC  '&ENTRY(1)'
.XANOP1  ANOP
&N       SETA  N'&BASE
         AIF   (T'&ID EQ 'O').XIDLEN
&K       SETA  K'&ID-2
&K       SETA  ((&K+1)/2)*2
.XIDLEN  ANOP
         PUSH  USING
         USING *,&ENTREG               TEMP ADDRESSABILITY
&NAME    B     &PSTCNS                  BRANCH AROUND ID       05/24/77
         DROP  &ENTREG
         POP   USING
         DC    AL1(23)                  ID LENGTH
         DC    CL9'&CNAME'              CSECT NAME
         DC    CL8'&SYSDATE'            ASSEMBLY DATE
         DC    CL6' &SYSTIME'           ASSEMBLY TIME
         AIF   ('&COPYRT' EQ 'NO').SKIPCP2                     12/11/87
         DC    C'COPYRIGHT (C)&CPYYR. BY BRIPER IDUSTRIES SYSTEMS CONSU+
               LTING, ALL RIGHTS RESERVED       '             11/21/86
.SKIPCP2 ANOP                                                  12/11/87
         AIF   (T'&ID EQ 'O').XSAVE
         DC    CL&K&ID
.XSAVE   ANOP
&PSTCNS  DS    0H
         AIF   ('&SAVE' EQ 'NO').XBASECK
         AIF   (T'&REGSAVE(1) NE 'N').XSAVERR
         AIF   (&REGSAVE(1) GE 14 OR &REGSAVE(1) LE 2).XSAVE2
         STM   14,15,12(13)             SAVE ENTRY REGISTERS
&I       SETA  &REGSAVE(1)*4+20
         AIF   (N'&REGSAVE NE 2).XSAVE3
         STM   &REGSAVE(1),&REGSAVE(2),&A.(13)  SAVE REGISTERS
         AGO   .XBASECK
.XSAVE3  AIF   (N'&REGSAVE NE 1).XSAVERR
         ST    &REGSAVE(1),&A.(,13)     SAVE REGISTER
         AGO   .XBASECK
.XSAVE2  AIF   (N'&REGSAVE NE 2).XSAVE4
         STM   14,&REGSAVE(2),12(13)    SAVE REGISTERS
         AGO   .XBASECK
.XSAVE4  AIF   (N'&REGSAVE NE 1).XSAVERR
         STM   14,&REGSAVE(1),12(13)    SAVE REGISTERS
         AGO   .XBASECK
.XSAVERR MNOTE 8,'** INVALID REGISTER LIST, &REGSAVE, (14,12) ASSUMED '
.XSAVE0  ANOP
         STM   14,12,12(13)             SAVE REGISTERS
.XBASECK AIF   (&N EQ 0).XCPPL
         LR    &BASE(1),&ENTREG         INITIALIZE BASE REGISTER
         USING &CNAME,&BASE(1)
         AIF   (&N LE 1).XCPPL
&I       SETA  2
.XBASE   ANOP
&J       SETA  &I-1
         LA    &BASE(&I),2048(&BASE(&J))
         LA    &BASE(&I),2048(&BASE(&I))
&K       SETA  &J*4096
         USING &CNAME+&K,&BASE(&I)
&I       SETA  &I+1
         AIF   (&I LE &N).XBASE
.XCPPL   AIF   ('&CPPL' EQ 'NO').XPARM
         LR    &CPPL,1                  CPPL ADDRESS
         USING CPPL,&CPPL
         AGO   .XMODE
.XPARM   AIF   (T'&PARM EQ 'O').XREG0
         LR    &PARM(1),1               SAVE PARAMETER REGISTER
         AIF   (N'&PARM NE 2).XREG0
         USING &PARM(2),&PARM(1)        DSECT ADDRESSABILITY
.XREG0   AIF   (T'&R0 EQ 'O').XMODE
         LR    &R0,R0
.XMODE   AIF   (T'&MODESET EQ 'O').XWORK
         MODESET MF=(E,&MODESET)
.XWORK   AIF   ('&SAVE' EQ 'NO').AMOD030 QUIT IF SAVE=NO
         AIF   ('&SAVE' EQ 'YES').XGETM IF SAVE=YES, THEN GETMAIN
         LA    15,&SAVE                 LOAD UP ADDRESS OF SAVE AREA
         ST    13,4(15)                 START CHAINING SAVE AREAS
         ST    15,8(13)                 FINISH CHAINING
         LR    13,15                    NEW SAVE AREA ADDRESS
         AGO   .AMOD020                 WE IS ALL THRU......
.XGETM   AIF   (T'&L EQ 'O').AMOD020
         &LI   R0,&L
         AIF   ('&SYSSPLV' EQ '1').NOTXA1                      03/09/88
         MNOTE *,'       GETMAIN RU,LV=(0),SP=&SP,LOC=&LOC  '
         GETMAIN RU,LV=(0),SP=&SP,LOC=&LOC                     12/08/86
         AGO   .XAJOIN1                                        03/09/88
.NOTXA1  ANOP  ,                                               03/09/88
         ICM   R0,8,=AL1(&SP)                                  03/09/88
         MNOTE *,'       GETMAIN R,LV=(0)                   '  03/09/88
         GETMAIN R,LV=(0)                                      03/09/88
.XAJOIN1 ANOP  ,                                               03/09/88
         ST    13,4(1)                  CONNECT SAVEAREAS
         ST    1,8(13)
         LR    13,1
         AIF   (T'&D EQ 'O').XCLEAR
         USING &D,13
.XCLEAR  AIF   (T'&CLEAR EQ 'O').AMOD020
         XC    0(4,13),0(13)
         LA    R0,8(13)
         AIF   ('&LI' EQ 'LA').XCLA
         &LI   1,&L
         S     1,=F'8'
         AGO   .XCLB
.XCLA    LA    1,&L-8
.XCLB    SLR   14,14
         SLR   15,15
         MVCL  R0,14                    ZERO WORKAREA
.AMOD020 ANOP
         AIF   ('&SYSSPLV' EQ '1').CKREGS
         AIF   ('&MODSET' EQ 'NO' OR '&MODSET' EQ 'N').CKREGS
         AIF   ('&AMOD1' EQ '').CKREGS
         AIF   ('&MODSAVE' NE '').SETMSAV
.DFTMSAV AIF   ('&SAVE' EQ 'NO').NOMSAVE
         L     1,4(13)                  GO GET PREVIOUS SAVE AREA
&MODSAV  SETC  '12(1)'
         AGO   .MSAVSET
.SETMSAV AIF   ('&MODSAVE' EQ 'NO').NOMSAVE
         AIF   ('&MODSAVE' EQ 'YES').DFTMSAV
&MODSAV  SETC  '&MODSAVE'
.MSAVSET ANOP
         SETAMODE &AMOD1,SAVE=&MODSAV   SET NEW AMODE AND SAVE OLD ONE.
         AGO   .CKREGS
.NOMSAVE AIF   ('&AMOD1' EQ 'ANY').CKREGS
         SETAMODE &AMOD1
.CKREGS  ANOP
         B     &LITLBL
         LTORG ,                        INSURE ADDRESSABILITY TO CNSTS.
&LITLBL  DS    0H
         AIF   ('&EQUREGS' NE 'YES' AND '&EQUREGS' NE 'Y').MEND
&I       SETA  0
.CKRLOOP AIF   (&I GT 15).MEND
R&I      EQU   &I
&I       SETA  &I+1
         AGO   .CKRLOOP
.MEND    MEND
