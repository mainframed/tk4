*  RON MURA, TECHNICAL SERVICES, APRIL 26, 1984
*  THIS PROGRAM EDITS THE ISPF TEMPORARY FILE.
*  IF NO PARM IS PROVIDED, THE DDNAME IS OBTAINED FROM ISPF SYSTEM
*  VARIABLE ZTEMPN.  IF A PARM IS PROVIDED, IT MUST BE ONE CHARACTER.
*  AN ATTEMPT IS MADE TO USE DDNAME ISPCTL?, WHERE ? IS THE PARM
*  VALUE.  IF THIS FAILS, A LAST-DITCH EFFORT IS TO EDIT DSNAME
*  TSOPREFIX.SPFTEMP?.CNTL.
*  ATTRIBUTES: REFRESHABLE, REENTERANT.
*
*  08/28/84 MURA - USE PANEL JHAEFR01 WHEN EDITING.
JH#EDTMP ENTER RENT=YES,ID2=R-MURA,WKBYTES=WKBYTES
         USING DSECT,R13
         XR    R10,R10                INIT. RETURN CODE
         L     R3,0(R1)               GET PARM ADDRESS
         LH    R2,0(R3)               GET PARM LENGTH
*                                         FOLLOWING LINE ADDED 08/28/84
         ISPCALL ('VDEFINE','(JHTITLE)',JHTITLE,'CHAR',=A(L'JHTITLE)), X
               WKAREA=WKAREA
         CH    R2,=H'1'               ANY PARM?
         BL    GETTMPDD               NO - GET TEMP. DD
         BH    BADPARM
         MVC   PARMDDN,=CL8'ISPCTL? '
         MVC   PARMDDN+6(1),2(R3)     GET NUMBER FROM PARM
         ISPCALL ('VDEFINE','(PARMDDN)',PARMDDN,'CHAR',8)
         ISPCALL 'LMINIT DATAID(JHEDTMPD) DDNAME(&&PARMDDN)'
         LTR   R15,R15                WAS ISPCTL# ALLOCATED?
         BZ    EDIT                   YES - OK
         MVI   PARMDSN,X'40'          NO - TRY FOR DSNAME
         MVC   PARMDSN+1(L'PARMDSN-1),PARMDSN  BLANK OUT DSN
         MVC   PARMDSN(13),=C'SPFTEMP?.CNTL'
         MVC   PARMDSN+7(1),2(R3)     GET NUMBER FROM PARM
         ISPCALL ('VDEFINE','(PARMDSN)',PARMDSN,'CHAR',17)
         ISPCALL 'LMINIT DATAID(JHEDTMPD) DATASET(&&PARMDSN)'
         LTR   R15,R15                WAS SPFTEMP? ALLOCATED?
         BZ    EDIT                   YES - OK
         MVC   MSG,=C'JHAEM005'       NO - ERROR MSG
         B     SETMSG
GETTMPDD ISPCALL 'VGET (ZTEMPN) SHARED'
         LTR   R15,R15
         BNZ   NODD
         ISPCALL 'LMINIT DATAID(JHEDTMPD) DDNAME(&&ZTEMPN)'
EDIT     ISPCALL 'EDIT DATAID(&&JHEDTMPD) PANEL(JHAEFR01)'     08/28/84
         ISPCALL 'LMFREE DATAID(&&JHEDTMPD)'
LEAVE    LEAVE RC=(R10)
NODD     MVC   MSG,=C'JHAEM003'
SETMSG   ISPCALL ('SETMSG',MSG)
         LA    R10,20                 LOAD RETURN CODE
         B     LEAVE
BADPARM  MVC   MSG,=C'JHAEM004'
         B     SETMSG
JHTITLE  DC    C'EDIT - TEMPORARY CONTROL DATA SET'            08/28/84
         REQUS
DSECT    DSECT
SAVEAREA DS    18F
WKAREA   DS    16F
PARMDDN  DS    CL8
PARMDSN  DS    CL17
MSG      DS    CL8
WKBYTES  EQU   *-WKAREA               LENGTH OF DSECT EXCEPT SAVEAREA
         END
