         MACRO
&LBL     SCANLINE &SEEK,      CHARS SOUGHT (IN QUOTES)                 X
               &RECADDR=,     START OF RECORD TO BE SEARCHED           X
               &STRTCOL=1,    STARTING COLUMN                          X
               &ENDCOL=71,    ENDING COLUMN                            X
               &FIND=,        BRANCH ADDRESS IF CHARS FOUND            X
               &NOFIND=,      BRANCH ADDRESS IF CHARS NOT FOUND        X
               &WKREG=15      WORK REGISTER
.*                                                      RON MURA, TPD
.*   THIS MACRO GENERATES REENTERANT CODE.
         LCLA  &WORK,&WORK2
         LCLB  &ERRSW,&RECSW
         LCLC  &NDX,&WSEEK                                     9/20/82
&NDX     SETC  '&SYSNDX'       KEEP IN SHORT-NAMED FIELD FOR ALIGNMENT
         AIF   (T'&SEEK EQ 'O').ERR1
.CHK2    AIF   (T'&RECADDR EQ 'O').WARN1
.CHK4    AIF   (T'&FIND EQ 'O' AND T'&NOFIND EQ 'O').ERR2
.CHK5    AIF   (T'&STRTCOL NE 'N' OR T'&ENDCOL NE 'N').ERR3
         AIF   (&STRTCOL GT &ENDCOL).ERR4
         AIF   (&ERRSW EQ 1).MEND
         AIF   (&RECSW EQ 1).R3OK    BRANCH IF DEFAULT (REG 1)
         AIF   ('&RECADDR'(1,1) EQ '(').REG    CHECK FOR REG FORMAT
&LBL     LA    1,&RECADDR            LOAD RECORD ADDRESS
         AGO   .R3OK
.REG     AIF   ('&RECADDR(1)' EQ '1').R3OK
&LBL     LR    1,&RECADDR(1)         LOAD RECORD ADDRESS
.R3OK    ANOP
&WORK    SETA  &STRTCOL-1
         AIF   (&WORK EQ 0).SKPLA1   IF 0, LA IS NOT NEEDED
         LA    1,&WORK.(0,1)         POINT AT START LOCATION
.SKPLA1  ANOP
&WORK    SETA  &ENDCOL-&STRTCOL+1    NUMBER OF COLUMNS TO BE SEARCHED
&WORK2   SETA  K'&SEEK-2             ASSUME CHARACTER DATA - SET LENGTH
.*                                   (SUBTRACT 2 FOR QUOTES)
&WORK    SETA  &WORK-&WORK2+1        MAX NUMBER OF SEARCHES NEEDED
         LA    &WKREG,&WORK          LOAD MAX NUMBER OF SEARCHES
         AIF   (&WORK2 GT 1).CLC                               9/20/82
SCCL&NDX CLI   0(1),C&SEEK                                     9/20/82
         AGO   .CHKFIND                                        9/20/82
.CLC     ANOP  ,                                               9/20/82
SCCL&NDX CLC   0(&WORK2,1),=C&SEEK
.CHKFIND AIF   (T'&FIND EQ 'O').FIND2
         BE    &FIND
         AGO   .LA
.FIND2   ANOP
         BE    SCEX&NDX
.LA      ANOP
         LA    1,1(0,1)              INCREMENT TO NEXT POSITION
         BCT   &WKREG,SCCL&NDX
         XR    1,1                   STRING NOT FOUND
         AIF   (T'&NOFIND EQ 'O').END
         B     &NOFIND
.END     ANOP
.*               NO NEED FOR SCEX LABEL IF FIND PROVIDED:
         AIF   (T'&FIND NE 'O').MEND
SCEX&NDX EQU   *                     END OF SCANLINE MACRO
         AGO   .MEND
.ERR1    MNOTE 12,'*** NO STRING TO BE FOUND SPECIFIED ***'
&ERRSW   SETB  1
         AGO   .CHK2
.WARN1   MNOTE *,'*** NO RECORD ADDR SPECIFIED - REG. 1 ASSUMED ***'
&RECSW   SETB  1
         AGO   .CHK4
.ERR2    MNOTE *,'*** NO FIND OR NOFIND ADDRESS GIVEN ***'
         AGO   .CHK5
.ERR3    MNOTE 12,'*** STRTCOL AND ENDCOL MUST BE NUMERIC ***'
         AGO   .MEND
.ERR4    MNOTE 12,'*** STRTCOL MAY NOT BE GREATER THAN ENDCOL ***'
         AGO   .MEND
.FIN     ANOP
.MEND    MEND
