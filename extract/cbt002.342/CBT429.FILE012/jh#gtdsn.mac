***********************************************************************
*                                                                     *
*  JH#GTDSN - DETERMINE DSNAME AND VOLUME SERIAL FOR PROVIDED DDNAME  *
*             (ISPF SUBFUNCTION).                                     *
*  RON MURA, TECHNICAL SERVICES, 9/14/82.                             *
*  THIS CODE IS REENTERANT.                                           *
*                                                                     *
*  PARM - DDNAME.                                                     *
*                                                                     *
*  THIS PROGRAM WILL SET VARIABLE &JHTDSN AND &JHTVOL AND PUT THEM    *
*  IN THE ISPF SHARED POOL.                                           *
*                                                                     *
*  SAMPLE INVOCATION (FROM CLIST):                                    *
*     ISPEXEC SELECT PGM(JH#GTDSN) PARM(SYSUT1)                       *
*     ISPEXEC VGET (JHTDSN JHTVOL) SHARED                             *
*                                                                     *
*  REGISTERS:                                                         *
*  R0 - SVC                       R8  -                               *
*  R1 - WORK, SVC                 R9  -                               *
*  R2 - WORK                      R10 -                               *
*  R3 -                           R11 -                               *
*  R4 - WORK                      R12 - PROGRAM BASE REGISTER         *
*  R5 -                           R13 - DSECT BASE REGISTER           *
*  R6 -                           R14 -                               *
*  R7 -                           R15 - WORK, SVC                     *
*                                                                     *
***********************************************************************
JH#GTDSN ENTER RENT=YES,WKBYTES=DSECTL
         USING DSECT,R13
         L     R4,0(R1)
         LH    R2,0(R4)                LOAD PARM LENGTH
         LTR   R2,R2                   TEST PARM LENGTH
         BZ    BADPARM
         CH    R2,=H'8'                PARM LENGTH <= 8?
         BH    BADPARM                 NO - BAD PARM
         MVC   WKAREA(DCBL),DCB        MOVE DCB INTO WORK AREA
         MVC   WKAREA+40(8),=CL8' '    INITIALIZE DDNAME
         BCTR  R2,0                    DEDUCT 1 FOR EX
         EX    R2,MVC$1                MOVE PARM INTO DCB DDNAME
         LA    R1,JFCB
         ST    R1,EXLST                STORE JFCB ADDR INTO EXLST
         MVI   EXLST,X'87'             SET INDICATOR BYTE
         MVC   JFCB(44),BLANKS
         LA    R1,EXLST
         ST    R1,WKAREA+36            STORE EXLST ADDR INTO DCB
         MVC   RDJFCB$(RDJFCBL),RDJFCB
         LA    R1,WKAREA
         ST    R1,RDJFCB$              STORE DCB ADDR INTO RDJFCB LIST
         OI    RDJFCB$,X'80'           TURN ON HIGH-ORDER BIT
         LA    R1,RDJFCB$
         RDJFCB MF=(E,(1))
         ISPCALL ('VREPLACE','JHTDSN',44,JFCB),WKAREA=WKAREA
         ISPCALL ('VREPLACE','JHTVOL',6,JFCB+118)
         ISPCALL ('VPUT','(JHTDSN JHTVOL)','SHARED')
         LEAVE RC=0
MVC$1    MVC   WKAREA+40(1),2(R4)
BADPARM  WTO   'JH#GTDSN - INVALID OR MISSING PARM',ROUTCDE=11
         ABEND 3,DUMP
BLANKS   DC    CL44' '
RDJFCB   RDJFCB 0,MF=L
RDJFCBL  EQU   *-RDJFCB
DCB      DCB   DDNAME=DDNAME,MACRF=R,DSORG=PS,EXLST=0
DCBL     EQU   *-DCB
         LTORG ,
DSECT    DSECT
         DS    18F                     SAVE AREA
EXLST    DS    A
JFCB     DS    176X
WKAREA   DS    (DCBL)X
RDJFCB$  DS    (RDJFCBL)X
DSECTL   EQU   *-DSECT
         REQUS
         END
