         MACRO
         PANEL &TYPE,                                                  X
               &NAME=,                                                 X
               &USERFLD=,                                              X
               &MSG1=,                                                 X
               &MSG2=,                                                 X
               &CURAD=,                                                X
               &OTITLE=
         GBLC  &BBB1,&BBBL
         AIF   ('&TYPE' NE 'START').TRYEND
  PUNCH 'ZPANEL  CSECT                                                '
  PUNCH 'ZPANEL  AMODE 31                                             '
  PUNCH 'ZPANEL  RMODE ANY                                            '
  PUNCH ' DC    CL8''&NAME''                                          '
  PUNCH ' DC    CL8''&&SYSTIME''                                      '
  PUNCH ' DC    CL8''&&SYSDATE''                                      '
  PUNCH ' DC    AL2(ZLC4-ZPANEL)    LENGTH OF PANEL                   '
  PUNCH ' DC    AL2(1)              PANEL VERSION                     '
  PUNCH ' DC    AL2(ZLC0-ZPANEL)    START OF OFFSET ARRAY             '
  PUNCH ' DC    AL2(ZLC1-ZPANEL)    START OF PANEL                    '
         AIF   ('&MSG1' EQ '').ST10A
  PUNCH ' DC    AL2(&MSG1-ZPANEL)   MSG 1                             '
         AGO   .ST10B
.ST10A   ANOP
  PUNCH ' DC    AL2(0)              MSG 1                             '
.ST10B   ANOP
         AIF   ('&MSG2' EQ '').ST20A
  PUNCH ' DC    AL2(&MSG2-ZPANEL)   MSG 2                             '
         AGO   .ST20B
.ST20A   ANOP
  PUNCH ' DC    AL2(0)              MSG 2                             '
.ST20B   ANOP
  PUNCH ' DC    AL2(ZLC3-ZPANEL)    INSERT CURSOR ADDRESS             '
         AIF   ('&USERFLD' EQ '').ST30A
  PUNCH ' DC    AL2(&USERFLD-ZPANEL)  1ST INSTALL DEFINED HEADER      '
         AGO   .ST30B
.ST30A   ANOP
  PUNCH ' DC    AL2(0)              FIRST INSTALL DEFINED HEADER      '
.ST30B   ANOP
  PUNCH ' DC    AL2(ZLC4-ZLC1)      LENGTH OF PANEL ITSELF            '
  PUNCH ' DC    AL2(ZLC2-ZPANEL)    WCC                               '
         AIF   ('&OTITLE' EQ '').ST40A
  PUNCH ' DC    AL2(&OTITLE-ZPANEL) OFFSET TO OPTIONAL TITLE          '
  PUNCH ' DC    AL2(L''&OTITLE)     LENGTH OF OPTIONAL TITLE          '
         AGO   .ST40B
.ST40A   ANOP
  PUNCH ' DC    AL2(0)              OFFSET TO OPTIONAL TITLE          '
  PUNCH ' DC    AL2(0)              LENGTH OF OPTIONAL TITLE          '
.ST40B   ANOP
  PUNCH 'ZLC0 DS    0F                                                '
         MEXIT
.TRYEND  AIF   ('&TYPE' NE 'END').ER1
  PUNCH ' AGO   .&BBB1                                                '
  PUNCH '.&BBBL   ANOP                                                '
  PUNCH ' DC    X''11''                                               '
  PUNCH 'ZLC3 DS   0X                                                 '
         GRC   &CURAD                       CURSOR ADDRESS            '
  PUNCH ' DC    X''13''                                               '
  PUNCH 'ZLC4 EQU  *                                                  '
  PUNCH ' END                                                         '
         END
         MEXIT
.ER1     MNOTE 8,'PANEL TYPE IS NEITHER START NOR END'
         MEND
         MACRO
&NAME    FLD   &TYPE,                                                  X
               &AD1=,              ADDRESS OF INFO                     X
               &AT1=,              ATTRIBUTE OF INFO                   X
               &TXT1=,             TEXT OF INFO                        X
               &AD2=,              ADDRESS OF DATA                     X
               &AT2=,              ATTRIBUTE OF DATA                   X
               &TXT2=              TEXT OF DATA
         GBLC  &BBB1,&BBBL
         LCLC  &LB1,&LB2,&LB3,&LB4,&AA,&TXT
         LCLA  &I
         AIF   ('&TYPE' NE '').TRYEND
&LB1     SETC  'ZLB1&SYSNDX'
&LB2     SETC  'ZLB2&SYSNDX'
&LB3     SETC  'ZLB3&SYSNDX'
&LB4     SETC  'ZLB4&SYSNDX'
&AA      SETC  'ZAA&SYSNDX'
         AIF   ('&BBB1' NE '').OK1
&BBBL    SETC  'ZBB100'
&BBB1    SETC  'ZBB100'
.OK1     ANOP
  PUNCH '&NAME DC    AL2(&LB1-ZPANEL)    HIGHLIGHT ATTR               '
  PUNCH ' DC    AL2(&LB2-ZPANEL)    HIGHLIGHT CHAR                    '
  PUNCH ' DC    AL2(&LB3-ZPANEL)    PROT ATTR                         '
         GRC   &AD2,INCR
  PUNCH ' DC    AL2(L''&LB4)        LENGTH OF INPUT FIELD             '
  PUNCH ' DC    AL2(&LB4-ZPANEL)    INPUT FIELD                       '
  PUNCH ' DC    AL2(0,0)            RESERVED                          '
  PUNCH ' AGO   .&AA                                                  '
  PUNCH '.&BBBL ANOP                                                  '
&BBBL    SETC  'ZBB1&SYSNDX'
  PUNCH ' DC    X''11''                                               '
         GRC   &AD1
  PUNCH ' DC    X''1D''                                               '
  PUNCH '&LB1 DC    X''&AT1''                                         '
&I       SETA  K'&TXT1
&TXT     SETC  '&TXT1'(2,&I-2)
  PUNCH '&LB2 DC    &TXT'
  PUNCH ' DC    X''11''                                               '
         GRC   &AD2
  PUNCH ' DC    X''1D''                                               '
  PUNCH '&LB3 DC    X''&AT2''                                         '
&I       SETA  K'&TXT2
&TXT     SETC  '&TXT2'(2,&I-2)
  PUNCH '&LB4 DC    &TXT'
  PUNCH ' DC    X''1DF0''                                             '
  PUNCH ' AGO   .&BBBL                                                '
  PUNCH '.&AA     ANOP                                                '
         MEXIT
.TRYEND  AIF   ('&TYPE' NE 'END').ER1
  PUNCH ' DC    XL16''FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF''              '
  PUNCH 'ZLC1 DC    X''27F5''           EXC, ERASE WRITE              '
  PUNCH 'ZLC2 DC    X''C3''             WCC                           '
  PUNCH ' DC    X''114040''         SET BUFFER TOP LEFT OF SCREEN     '
  PUNCH ' DC    X''3C404040''       REPEAT BLANK TO TOP OF SCREEN     '
         MEXIT
.ER1     MNOTE 8,'FIELD TYPE IS NOT END OR NULL'
         MEND
         MACRO
&NAME    FLD2  &TYPE,       ** FIELD NAME FOLLOWING INPUT **           X
               &AD1=,              ADDRESS OF INFO                     X
               &AT1=,              ATTRIBUTE OF INFO                   X
               &TXT1=,             TEXT OF INFO                        X
               &AD2=,              ADDRESS OF DATA                     X
               &AT2=,              ATTRIBUTE OF DATA                   X
               &TXT2=              TEXT OF DATA
         GBLC  &BBB1,&BBBL
         LCLC  &LB1,&LB2,&LB3,&LB4,&AA,&TXT
         LCLA  &I
         AIF   ('&TYPE' NE '').TRYEND
&LB1     SETC  'ZLB1&SYSNDX'
&LB2     SETC  'ZLB2&SYSNDX'
&LB3     SETC  'ZLB3&SYSNDX'
&LB4     SETC  'ZLB4&SYSNDX'
&AA      SETC  'ZAA&SYSNDX'
         AIF   ('&BBB1' NE '').OK1
&BBBL    SETC  'ZBB100'
&BBB1    SETC  'ZBB100'
.OK1     ANOP
  PUNCH '&NAME DC    AL2(&LB1-ZPANEL)    HIGHLIGHT ATTR               '
  PUNCH ' DC    AL2(&LB2-ZPANEL)    HIGHLIGHT CHAR                    '
  PUNCH ' DC    AL2(&LB3-ZPANEL)    PROT ATTR                         '
         GRC   &AD2,INCR
  PUNCH ' DC    AL2(L''&LB4)        LENGTH OF INPUT FIELD             '
  PUNCH ' DC    AL2(&LB4-ZPANEL)    INPUT FIELD                       '
  PUNCH ' DC    AL2(0,0)            RESERVED                          '
  PUNCH ' AGO   .&AA                                                  '
  PUNCH '.&BBBL ANOP                                                  '
&BBBL    SETC  'ZBB1&SYSNDX'
  PUNCH ' DC    X''11''                                               '
         GRC   &AD1
  PUNCH ' DC    X''1D''                                               '
  PUNCH ' DC    X''E8''                                               '
  PUNCH '&LB2 DC  X''00'''
  PUNCH ' DC    X''11''                                               '
         GRC   &AD2
  PUNCH ' DC    X''1D''                                               '
  PUNCH '&LB3 DC    X''&AT1''                                         '
&I       SETA  K'&TXT1
&TXT     SETC  '&TXT1'(2,&I-2)
  PUNCH '&LB4 DC    &TXT'
  PUNCH ' DC    X''1D''                                               '
  PUNCH '&LB1 DC    X''&AT2''                                         '
&I       SETA  K'&TXT2
&TXT     SETC  '&TXT2'(2,&I-2)
  PUNCH ' DC    &TXT'
  PUNCH ' DC    X''1D60''                                             '
  PUNCH ' AGO   .&BBBL                                                '
  PUNCH '.&AA     ANOP                                                '
         MEXIT
.TRYEND  AIF   ('&TYPE' NE 'END').ER1
  PUNCH ' DC    XL16''FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF''              '
  PUNCH 'ZLC1 DC    X''27F5''           EXC, ERASE WRITE              '
  PUNCH 'ZLC2 DC    X''C3''             WCC                           '
  PUNCH ' DC    X''114040''         SET BUFFER TOP LEFT OF SCREEN     '
  PUNCH ' DC    X''3C404040''       REPEAT BLANK TO TOP OF SCREEN     '
         MEXIT
.ER1     MNOTE 8,'FIELD TYPE IS NOT END OR NULL'
         MEND
         MACRO
         GRC   &RC,&INCR
         LCLC  &C,&C1,&C2,&ROW,&COL
         LCLA  &POS,&P1,&P2
.*       COMPUTE 3270 ADDRESSES FROM ROW/COLUMN
&ROW     SETC  '&SYSLIST(1,1)'
&COL     SETC  '&SYSLIST(1,2)'
&POS     SETA  80*(&ROW-1)+(&COL-1)
         AIF   ('&INCR' EQ '').OK23
&POS     SETA  &POS+1
.OK23    ANOP
&P1      SETA  &POS/64             GET FIRST CHAR POS
&P2      SETA  &POS-(&P1*64)       GET SECOND CHAR POS
&C SETC '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5B5C5D5EX
               5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6F7F8F97AX
               7B7C7D7E7F'
&C1      SETC  '&C'(&P1*2+1,2)
&C2      SETC  '&C'(&P2*2+1,2)
  PUNCH ' DC    X''&C1&C2''         GENERATE ADDRESS CHARACTERS       '
         MEND
