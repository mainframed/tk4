TSOWAIT  ENTER
*
         LR    R2,R1                   SAVE PARM ADRESS
         USING CPPL,R2                 SET ADRESSEBILITY
*
* SET UP ADRESSEBILITY FOR PARS
*
         LA    R3,MYPPL
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R5,MYECB
         ST    R5,PPLECB
         L     R5,=V(PARM)
         ST    R5,PPLPCL
         LA    R5,ANSWER
         ST    R5,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         LR    R1,R3
*
         CALLTSSR EP=IKJPARS
         LTR   15,15                   CHECK RETURN-CODE
         BZ    GOODPARS
         LA    15,8                    SET RETURN-CODE = 8
         B     OUT                     GOTO END
*
GOODPARS L     R9,ANSWER               POINT TO PARS ANSWER
         USING IKJPARMD,R9             ADRESSIBILITY TO PDL
*
         LA    R5,WAITTIME             POINTER TO WAITTIME PCE
         L     R6,0(R5)                POINT TO WAITTIME
         L     R5,0(R6)                GET WAITTIME VALUE
         SRA   R5,16                   SHIFT 16 BITS TO THE RIGHT
         STH   R5,TIJD+4               STORE IT
*******************************************************************
* CHECK BELOW IS DONE FOR THE FOLLOWING REASON:
*
* IF USER ENTERS A WAITTIME VALUE OF < 10 (F.I. 7) THE RESULT
* WILL BE THAT THE WAITTIME VALUE IN THE PROGRAM IS FILLED LIKE
* F740 (IT SHOULD BE IN THE FORM OF F0F7.
* THEREFORE TIJD+5 IS CHECKED FOR THE PRESENCE OF X'1111'.
*******************************************************************
         TM    TIJD+5,X'80'
         BO    CHECKMAX                WAITTME RIGHT FORMAT GO CHECK
*******************************************************************
* IF WAITTIME WAS IN THE FORM OF F740, IT IS TRANSFORMED TO F0F7
*******************************************************************
         MVC   TIJD+5(1),TIJD+4        MOVE 1 BYTE
         MVI   TIJD+4,X'F0'            AND MAKE 1ST BYTE F0
*
CHECKMAX CLC   TIJD+4,MAXWAIT          WAIT > 60 ?
         BL    GOWAIT                  NO. GO WAIT
*
         MVI   TIJD+4,X'F6'            SET WAITTIME
         MVI   TIJD+5,X'F0'            TO 60 SECONDS
         LA    7,L'MAXMSG              TELL THE CALLER
         LA    8,MAXMSG                WAITTIME SET TO DEFAULT
         TPUT  (8),(7)
*
GOWAIT   STIMER WAIT,DINTVL=TIJD
*
         LA    15,0
OUT      BACK  CC=(15)
         EJECT
         DS    2F
TIJD     DC    XL8'F0F0F0F0F0F0F0C0'
MAXMSG   DS    0CL240
         DC    CL80'TSOWAIT0 : SPECIFIED WAITTIME IS TOO LARGE.      '
         DC    CL80'MAXIMUM WAITTIME IS 60 SECONDS.                  '
         DC    CL80'60 SECONDS WILL BE USED.                         '
MAXWAIT  DC    X'F6F0'
MYECB    DC    F'0'
MYPPL    DC    7F'0'
ANSWER   DC    F'0'
UWA      DC    F'0'
PARSBACK DC    F'0'
*        PRINT OFF
PARM     IKJPARM
WAITTIME IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,DEFAULT='05',   *
               MAXLNTH=2
         IKJENDP
         IKJCPPL
         IKJPPL
         CVT DSECT=YES
         END
