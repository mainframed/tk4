         MACRO
&NAME    BACK  &CC=
         GBLA  &XYZ
&NAME    DS    0H
         AIF   ('&XYZ' EQ '0').VERDER
         SPACE
         CLOSE PDUMPOS
         SPACE
.VERDER  ANOP
         LR    1,13 .                  *LOAD ADR SAVEAREA
         L     13,4(13) .              *LOAD ADR.HIGER SAVEAREA
  AIF   ('&CC' EQ '(RF)' OR '&CC' EQ '(15)' OR '&CC' EQ '(R15)').R15
         LA    0,72(0,0) .             *LOAD LENGTH
         SVC   10 .                    *ISSUE FREEMAIN
         LM    14,12,12(13) .          *RESTORE REGISTERS
         AIF   ('&CC' EQ '').EXIT      *TEST CONDITIONCODE
         LA    15,&CC .                *LOAD CONDITIONCODE
         AGO   .EXIT
.R15     ANOP
         ST    15,16(13) .             *SAVE RET. CODE IN HIGHER SA
         LA    0,72(0,0) .             *LOAD LENGTH
         SVC   10 .                    *ISSUE FREEMAIN
         LM    14,12,12(13) .          *RESTORE REGISTERS
.EXIT    ANOP
         BCR   15,14 .                 *RETURN TO CALLER
         LTORG
         MEND
