         LCLC  &ID
&ID      SETC  'SLEEP V1R0 / J.J. JOOLEN /'.'&SYSDATE'.' '.'&SYSTIME'.'*
                - SUSPEND EXECUTION'
*
*   Calling sequence:
*      From TSO/E Clist or Rexx:   'SLEEP   nnnn'
*                                  nnnn = nr. of seconds to sleep
*
*   (c) Hans Joolen
*       Delft University of Technology
*       The Netherlands
*
*
SLEEP    CSECT ,
         SAVE  (14,12),,'&ID'
         LR    12,15
         USING SLEEP,12
*   Chain savearea's
         LA    11,SVA
         ST    11,8(,13)
         ST    13,4(,11)
         LR    13,11
*   Process environment for calling TSO/E services
         LR    2,1               Command processor parameter list
         USING CPPL,2
         L     1,=A(PARSROOM)    Parse parameter list
         USING PPL,1             Structure from DSECT
         MVC   PPLUPT,CPPLUPT    User profile table
         MVC   PPLECT,CPPLECT    Environment control table
         MVC   PPLECB,=A(ECB)    Command Processor's ECB
         MVC   PPLPCL,=A(PARSLIST)
         MVC   PPLANS,=A(PDLPTR) Answer area for IKJPARS
         MVC   PPLCBUF,CPPLCBUF  Command buffer
         MVC   PPLUWA,=A(0)      User work area (validity check rtn's)
         DROP  2
*   Get nr. of seconds and wait
         LINK  EP=IKJPARS        Reg 1 contains Parse parameter list
         L     2,PDLPTR          Get pointer to PDL (created by IKJPARS
         USING IKJPARMD,2
         L     3,INTVL           PDE within PDL, contains ptr to value
         L     4,0(,3)           Fullword (because of IKJIDENT/INTEG)
         MH    4,=H'100'         Stimer wants centiseconds
         ST    4,0(,3)
         STIMER WAIT,BINTVL=(3)  Sleep for specified nr. of seconds
*   Return to calling clist
         IKJRLSA PDLPTR          Release storage allocated by IKJPARS
         L     13,SVA+4
         RETURN (14,12),RC=0
*   Scan parameter list for IKJPARS
PARSLIST IKJPARM
INTVL    IKJIDENT 'interval',PROMPT='nr. of seconds to sleep',INTEG
         IKJENDP
SVA      DC    18F'0'
PDLPTR   DC    A(0)              Receives A(PDL) from IKJPARS
PARSROOM DC    7A(0)             Room for parse PCL
ECB      DC    A(0)
         LTORG ,
         IKJCPPL ,               Command processor parameter list
         IKJPPL ,                Parse parameter list
         IKJECT ,                Environment control table
         IKJUPT ,                User profile table
         END   SLEEP
