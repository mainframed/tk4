DBACONSL CSECT
         ENTRY DBAWTO
         ENTRY DBAWTOR
         REGS
*
*        WTO/WTOR PROCESSOR
*
         TITLE 'DBAWTO - ENTRY POINT LOGIC'
DBAWTO   EQU   *
         LA    R15,MAINEP-DBAWTO(R15) A(MAINLINE)
         USING MAINEP,R15     ADDRESSABILITY
         MVI   SWEPA+3,X'00'  INDICATE WTO
         BR    R15            CONTINUE
         DROP  R15
*
*
*
DBAWTOR  EQU   *
         LA    R15,MAINEP-DBAWTOR(R15) A(MAINLINE)
         USING MAINEP,R15     ADDRESSABILITY
         MVI   SWEPA+3,X'04'  INDICATE WTOR
         BR    R15
         TITLE 'DBACONSL - MAINLINE'
MAINEP   EQU   *
         SAVE  (14,12)        SAVE REGS
         ST    R13,SAVEAREA+4 BACK CHAIN
         LR    R12,R13        SAVE BACK POINTER
         CNOP  0,4            ALIGN
         BAL   R13,SKIPSAVE   A(NEW SAVE AREA)
SAVEAREA DC    18F'0'
SKIPSAVE EQU   *
         ST    R13,8(R12)     FOREWARD CHAIN
         DROP  R15
         USING SAVEAREA,R13   ADDRESSABILITY
         LM    R6,R7,0(R1)    POST PARMS
ONETIME  NOP   JNAMEOK
         OI    ONETIME+1,X'F0' SKIP INIT 2ND TIME THRU
         EXTRACT TIOT,FIELDS=TIOT
         L     R2,TIOT        A(TIOT)
         MVC   JOBNAMES,0(R2) POST REAL JOB NAME
JNAMEOK  EQU   *
         TIME  DEC
         ST    R0,HHMMSSTH    POST HHMMSSTH
         OI    HHMMSSTH+2,X'0F' PUT GOOD SIGN IN MIDDLE
         MVC   TIME,MASK      POST EDIT MASK
         ED    TIME,HHMMSSTH  EDIT
         SR    R5,R5          CLEAR
         IC    R5,1(R6)       PICK UP LENGTH
         BCTR  R5,0           LESS 1 FOR MVC
         STC   R5,COPY1+1     POST
COPY1    MVC   USERMSG,2(R6)  COPY TEXT
         LA    R4,20(R5)      FULL LENGTH
         STH   R4,MSGL        POST
         LA    R4,MSGL(R4)    NOW ADDR PAST END
         MVC   0(4,R4),ROUTDESC POST ROUTING AND DESCRIPTOR
         MVC   0(1,R4),0(R6)  FIX TRAILER
         L     R15,SWEPA      WTO/WTOR
         B     *+4(R15)
         B     DOWTO          00 - WTO
         B     DOWTOR         04 - WTOR
DOWTO    EQU   *
         WTO   MF=(E,WTOBUF)
         B     RETURN
DOWTOR   EQU   *
         NI    ECB,X'00'      CLEAR
         LA    R1,WTORBUF
         MVC   ANS(80),BLANK  CLEAR
         WTOR  MF=(E,(1))
         WAIT  1,ECB=ECB
         LH    R5,0(R7)        L'ANS
         BCTR  R5,0            LESS 1 FOR MVC
         STC   R5,COPY2+1      POST
COPY2    MVC   2(0,R7),ANS     COPY
         B     RETURN
         TITLE 'DBACONSL - RETURN'
RETURN   EQU   *
         L     R13,4(,R13)
         RETURN (14,12)
         TITLE  'DBACONSL - DATA AREAS'
TIOT     DC     A(0)
HHMMSSTH DC     XL4'00'
SWEPA    DC     A(0)
BLANK    DC     C' '
ANS      DC     CL80' '
ECB      DC     A(0)
WTORBUF  DS     0F
REPLYL   DC     AL1(L'ANS)
         DC     AL3(ANS)
         DC     A(ECB)
WTOBUF   EQU    *
MSGL     DC     AL2(116)
         DC     X'8000'       MCS FLAGS
MSG      DC     112C' '
         ORG    MSG
JOBNAMES DC     C'JOBNAMES'
TIME     DC     C' HH.MM'
         DC     C' '
USERMSG  DC     CL4' '
         ORG    ,
         DC     X'40004000'
         DC     XL4'00'
ROUTDESC DC     XL4'00004000'
MASK     DC     X'4021204B2020'
         END
