         MACRO
&LABEL   KEYS  &KEYS
         GBLA  &SUF,&KYNUM
         GBLC  &STRADDR,&STRLEN,&DELIM,&ERR
         LCLA  &SCAN,&KYLNG,&INX,&IX,&SXTNS,&DEC
         LCLC  &RTFRM,&KYLEN,&KYWRD,&TGT,&VLLEN,&REIT,&NUMS,&HEX,&HEXR
.**********************************************************************
.*  1 OR MORE KEYS MACROS TO BE USED ONLY FOLLOWING PARSE MACRO. USED
.*  TO SPECIFY KEYWORDS, TARGET FIELDS & RETURN FORMATS FOR VALUES TO
.*  BE PARSED FROM A CHARACTER STRING. FORMAT IS:
.*
.*  LABEL  KEYS  (KYW(TGT,F,L,R),KYW(TGT,F,L,R),...KYW(TGT,F,L,R),*)
.*
.*  WHERE LABEL IS OPTIONAL LABEL FOR MACRO
.*        KYW IS KEYWORD, MAX 12 CHARACTERS
.*        TGT IS NAME OF TARGET FIELD IN USING PROGRAM WHERE VALUES
.*           WILL BE RETURNED
.*        F IS FORMAT IN WHICH VALUES WILL BE RETURNED:
.*           A - CHARACTERS LEFT JUSTIFIED PADDED W/ SPACES
.*           R - CHARACTERS RIGHT JUSTIFIED PADDED W/ SPACES
.*           N - DISPLAY NUMERICS, RIGHT JUSTIFIED PADDED W/ ZEROS
.*           Z - DISPLAY NUMERICS, RIGHT JUSTIFIED PADDED W/ SPACES
.*           L - DISPLAY NUMERICS, LEFT JUSTIFIED PADDED W/ SPACES
.*           P - NUMERICS CONVERTED TO 8 BYTE SIGNED PACKED DECIMAL
.*           B - NUMERICS CONVERTED TO BINARY FULLWORD
.*        L - OPTIONAL - LENGTH OF TARGET FIELD, ALSO MAX NUM
.*           CHARACTERS ALLOWED FOR PARSED VALUE. DEFAULT=8, MAX=256
.*           IGNORED WHEN FORMAT IS P OR B
.*        R - OPTIONAL - NUMBER OF TIMES TARGET FIELD OCCURS, ALSO MAX
.*           NUM REITERATIONS OF VALUES ALLOWED. DEFAULT=1, MAX=256
.*        * - END OF KEYWORD LIST. MUST APPEAR ONLY AT END OF LAST
.*           'KEYS' MACRO FOR THE PARSE MACRO.
.*
.*  EXAMPLE: IF THE USING PROGRAM SPECIFIES:
.*      PARSE CARDIN(80,=)
.*      KEYS  (NAME(INNAM,A,10,2),IQ(INIQ,N))
.*      KEYS  (SCORE(INSCR,P,,3),HATSIZE(HATIN,Z),*)
.*  AND THE PROGRAM'S 80 CHARACTER FIELD NAMED CARDIN CONTAINS:
.*    NAME=REAGAN,RON        IQ=23 SCORE=83,42,1234567890
.*  THEN THESE USING PROGRAM'S FIELDS ARE REPLACED AS FOLLOWS:
.*    INNAM: C'REAGAN    RON       '
.*     INIQ: C'00000023'
.*    INSCR: X'000000000000083C000000000000042C000001234567890C'
.*    HATIN:  (UNCHANGED)
.*******************************************************************
         AIF   (&KYNUM GT 0).NOT1ST
PRS1&SUF DS    0F
.NOT1ST  ANOP
&NUMS    SETC  '0123456789ABCDEF'
&KYNUM   SETA  &KYNUM+N'&KEYS
&INX     SETA  0
.NEXTKEY ANOP
&INX     SETA  &INX+1
         AIF   (&INX LT N'&KEYS).PROCKEY
         AIF   ('&KEYS(&INX)'(1,K'&KEYS(&INX)) EQ '*').PRMLIST
.PROCKEY ANOP
&SCAN    SETA  0
         AIF   ('&KEYS(&INX)'(K'&KEYS(&INX),1) EQ ')').KEYL
         MNOTE '*** MISSING RIGHT PARENTHESIS ***'
&ERR     SETC  'Y'
         MEXIT
.KEYL    ANOP
&SCAN    SETA  &SCAN+1
         AIF   ('&KEYS(&INX)'(&SCAN,1) EQ '(').GOTL
         AIF   (&SCAN LT K'&KEYS(&INX)-4).KEYL
         AGO   .INVAL1
.GOTL    AIF   (&SCAN LT 14).KEYOK
         MNOTE '*** KEYWORD LONGER THAN 12 CHARACTERS ***'
&ERR     SETC  'Y'
         MEXIT
.KEYOK   ANOP
&KYLNG   SETA  &SCAN-1
&KYLEN   SETC  '0'.'&NUMS'(&KYLNG,1)
&KYWRD   SETC  '&KEYS(&INX)'(1,&KYLNG)
.TARL    ANOP
&SCAN    SETA  &SCAN+1
         AIF   ('&KEYS(&INX)'(&SCAN,1) EQ ',').TARR
         AIF   (&SCAN LT K'&KEYS(&INX)-2).TARL
.INVAL1  MNOTE '*** INVALID KEYWORD SUB-PARAMETERS ***'
&ERR     SETC  'Y'
         MEXIT
.TARR    ANOP
&TGT     SETC  '&KEYS(&INX)'(&KYLNG+2,&SCAN-&KYLNG-2)
&VLLEN   SETC  '07'
&REIT    SETC  'RR'
&RTFRM   SETC  '00'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'A').GOTRET
&RTFRM   SETC  '02'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'R').GOTRET
&RTFRM   SETC  '04'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'L').GOTRET
&RTFRM   SETC  '06'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'Z').GOTRET
&RTFRM   SETC  '07'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'N').GOTRET
&RTFRM   SETC  '0B'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'P').GOTRET
&RTFRM   SETC  '0F'
         AIF   ('&KEYS(&INX)'(&SCAN+1,1) EQ 'B').GOTRET
.INVAL2  MNOTE '*** INVALID RETURN FORMAT SPECIFIED ***'
&ERR     SETC  'Y'
         MEXIT
.GOTRET  ANOP
&SCAN    SETA  &SCAN+3
         AIF   (&SCAN GE K'&KEYS(&INX)).GOTLEN
         AIF   ('&KEYS(&INX)'(&SCAN-1,1) NE ',').INVAL2
         AIF   ('&KEYS(&INX)'(&SCAN,1) EQ ',').GOTLEN
         AGO   .CNVRT
.GOTLEN  ANOP
&REIT    SETC  '00'
         AIF   (&SCAN GE K'&KEYS(&INX)-1).GOTREIT
&SCAN    SETA  &SCAN+1
.CNVRT   ANOP
&HEX     SETC  '00'
&DEC     SETA  0
.CNVRT1  ANOP
&IX      SETA  0
.CNVRT2  ANOP
&IX      SETA  &IX+1
         AIF   ('&KEYS(&INX)'(&SCAN,1) EQ '&NUMS'(&IX,1)).CNVRT3
         AIF   (&IX LT 10).CNVRT2
.INVAL3  MNOTE '** INVALID TARGET FIELD LENGTH OR OCCURS VALUE **'
&ERR     SETC  'Y'
         MEXIT
.CNVRT3  ANOP
&DEC     SETA  &DEC*10
&DEC     SETA  &DEC+&IX-1
&SCAN    SETA  &SCAN+1
         AIF   (&DEC GT 256).INVAL3
         AIF   (&SCAN EQ K'&KEYS(&INX)).CNVRT4
         AIF   ('&KEYS(&INX)'(&SCAN,1) EQ ',').CNVRT4
         AGO   .CNVRT1
.CNVRT4  AIF   (&DEC EQ 0).CNVRT5
&SXTNS   SETA  &DEC-1
&SXTNS   SETA  &SXTNS/16
&DEC     SETA  &DEC-(&SXTNS*16)
&SXTNS   SETA  &SXTNS+1
&HEX     SETC  '&NUMS'(&SXTNS,1).'&NUMS'(&DEC,1)
.*&HEXR    SETC  '&NUMS'(&DEC,1)
.*&HEX     SETC  '&NUMS'(&SXTNS,1)'&HEXR'
.CNVRT5  AIF   ('&REIT'(1,2) EQ '00').CNVRT6
&VLLEN   SETC  '&HEX'(1,2)
         AGO   .GOTLEN
.CNVRT6  ANOP
&REIT    SETC  '&HEX'(1,2)
.GOTREIT ANOP
         DC    A(&TGT.),X'&RTFRM.&REIT.&VLLEN.&KYLEN.',CL12'&KYWRD.'
         AIF   (&INX LT N'&KEYS).NEXTKEY
         AGO   .END
.PRMLIST AIF   ('&ERR'(1,1) EQ 'Y').END
&KYNUM   SETA  &KYNUM-1
PRS2&SUF DS    2F
         DC    A(&STRADDR.),H'&STRLEN.',CL2'&DELIM.'
         DC    A(PRS1&SUF.),H'&KYNUM.'
PRXT&SUF DS    0H
         STM   14,15,PRS2&SUF
         L     15,=V(PRSTRNG)
         LA    1,PRS2&SUF+8
         BALR  14,15
         LM    14,15,PRS2&SUF
.END     MEND
