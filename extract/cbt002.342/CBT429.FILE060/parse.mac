         MACRO
&LABEL   PARSE &STRING
         GBLA  &SUF,&KYNUM
         GBLC  &STRADDR,&STRLEN,&DELIM,&ERR
         LCLA  &SCAN,&SCAN2
.**********************************************************************
.*  PARSE MACRO, USED IN CONJUNCTION WITH KEYS MACRO, WILL PARSE A
.*  CHARACTER STRING CONTAINING KEYWORDS FOLLOWED BY 1 OR MORE VALUES
.*  PER KEYWORD. FORMAT REQUIRED IS:
.*  (LABEL)  PARSE  SSSSS(LL,DD)
.*     WHERE LABEL IS OPTIONAL LABEL FOR MACRO
.*           SSSSS IS NAME OF STRING TO BE PARSED
.*           LL IS LENGTH OF STRING IN BYTES, MAX 65535
.*           DD IS OPTIONAL PAIR OF DELIMITING CHARACTERS:
.*               THE FIRST IS THE CHARACTER THAT WILL DELIMIT KEYWORDS.
.*               THE SECOND IS THE CHARACTER THAT WILL DELIMIT VALUES.
.*               DEFAULT IS SPACES. IF ONLY ONE IS SPECIFIED IT BECOMES
.*               THE FIRST AND THE SECOND IS SPACE. COMMAS NOT ALLOWED.
.*     EXAMPLES:
.*        PARSE  CARDIN(80,())
.*           WILL PARSE THE 80 BYTE STRING 'CARDIN' FOR KEYWORD/VALUES
.*           OF THE FORM:  KEYWORD1(VALUE)  KEYWORD2(VAL1,VAL2,...VALN)
.*        PARSE  INREC(512,=)
.*           WILL PARSE THE 512 BYTE STRING 'INREC' FOR KEYWORD/VALUES
.*           OF THE FORM:  KEYWORD1=VALUE  KEYWORD2=VAL1,VAL2,...VALN
.*        PARSE  BIGFINK(65535)
.*           WILL PARSE THE 65535 BYTE STRING 'BIGFINK' FOR KEY/VALS
.*           OF THE FORM:  KEYWORD1 VALUE  KEYWORD2 VAL1,VAL2,...VALN
.*  KEYWORDS, TARGET FIELDS FOR VALUES AND RETURN FORMATS OF VALUES
.*  ARE SPECIFIED USING 1 OR MORE 'KEYS' MACROS IMMEDIATELY FOLLOWING
.*  PARSE MACRO. A CALL IS GENERATED TO MODULE PRSTRNG WHICH RETURNS
.*  PARSED VALUES IN THE FORM REQUESTED TO THEIR RESPECTIVE FIELDS AND
.*  A RETURN CODE IN REGISTER 1. ONLY REG1 IS CORRUPTED BY THE CALL.
.**********************************************************************
&SUF     SETA  &SYSNDX
&ERR     SETC  'N'
&KYNUM   SETA  0
         AIF   ('&STRING'(K'&STRING,1) NE ')').INVAL
&SCAN    SETA  1
.TESTL   ANOP
&SCAN    SETA  &SCAN+1
         AIF   ('&STRING'(&SCAN,1) EQ '(').GOTL
         AIF   (&SCAN LT K'&STRING-1).TESTL
         AGO   .INVAL
.GOTL    ANOP
&STRADDR SETC  '&STRING'(1,&SCAN-1)
&SCAN2   SETA  &SCAN+1
.GETLEN  ANOP
&SCAN2   SETA  &SCAN2+1
         AIF   ('&STRING'(&SCAN2,1) EQ ',').GOTLEN
         AIF   (&SCAN2 LT K'&STRING).GETLEN
         AIF   ('&STRING'(&SCAN2,1) EQ ')').GOTLEN
.INVAL   MNOTE '*** INVALID SUB-PARAMETERS ***'
&ERR     SETC  'Y'
         MEXIT
.GOTLEN  ANOP
&STRLEN  SETC  '&STRING'(&SCAN+1,&SCAN2-&SCAN-1)
&DELIM   SETC  '  '
         AIF   (&SCAN2 GE K'&STRING-1).END
&DELIM   SETC  '&STRING'(&SCAN2+1,1)
         AIF   (&SCAN2 EQ K'&STRING-2).END
&DELIM   SETC  '&STRING'(&SCAN2+1,2)
.END     ANOP
&LABEL   B     PRXT&SUF
         MEND
