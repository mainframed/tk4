         TITLE 'DATE CONVERSION ROUTINE'
*        THIS PROGRAM IS A UTILITY DATE CONVERSION ROUTINE. IT CAN
*        BE LINKED TO BY COBOL PROGRAMS. THE CALLING PROGRAM MUST
*        DEFINE A 66 BYTE STORAGE AREA FOR PARAMETER PASSING. THE
*        FUNCTION CODE (ZONED 0 (X'F0') THRU 7) IS PLACED IN THE FIRST
*        BYTE OF STORAGE PASSED. THE FUNCTIONS ARE:
*        0 - VALIDATE A DATE
*        1 - CONVERT GREGORIAN DATE TO JULIAN DATE
*        2 - CONVERT JULIAN DATE TO GREGORIAN DATE
*        3 - PASSED A DATE AND A NUMBER FROM 0-999 RETURN THE
*            CORRESPONDING DATE (PROCEEDING FOWARD).
*        4 - PASSED A DATE AND A NUMBER FROM 0-999 RETURN THE
*            CORRESPONDING DATE (PROCEEDING BACKWARD).
*        5 - PASSED A DATE AND A DAY OF THE WEEK RETURN THE DATE
*            OF SPECIFIED DAY (PROCEEDING FOWARD).
*        6 - PASSED A DATE AND A DAY OF THE WEEK RETURN
*            (YOU GUESSED IT!) THE DATE OF SPECIFIED DAY (BACKWARD)
*        7 - PASSED TWO DATES RETURN THE NUMBER OF DAYS DIFFERENT.
*            THE YEAR VALUES OF THE DATES MAY NOT DIFFER BY MORE
*            THAN ONE.
*         NOTE: ALL DATES MAY BE ENTERED IN EITHER FORMAT BUT THIS
*               PROGRAM ASSUMES GREGORIAN UNLESS A SPACE (X'40') IS
*               IN THE SIXTH POSITION OF THE DATE FIELD.
*
*        REVISED 9/14/89 TO ALLOW NUMBER OF DAYS ADDED OR SUBTRACTED
*             TO BE 999 INSTEAD OF 365.         CDW
*
         EJECT
IODATA   DSECT
*************************************************************
OPTION   DS    X         OPTION                             *
MONTH1G  DS    CL2       *                                  *
DAY1G    DS    CL2       * GREG DATE 1                      *
YEAR1G   DS    CL2       *                                  *
         ORG   MONTH1G                                 I    *
YEAR1J   DS    CL2       *                             N    *
DAY1J    DS    CL3       * JULIAN DATE 1               P    *
JULF1    DS    X         *                             U    *
MONTH2G  DS    CL2        *                            T    *
DAY2G    DS    CL2        * GREG DATE 2                     *
YEAR2G   DS    CL2        *                            D    *
         ORG   MONTH2G                                 A    *
YEAR2J   DS    CL2        *                            T    *
DAY2J    DS    CL3        * JULIAN DATE 2              A    *
JULF2    DS    X          *                                 *
DAYSTOSK DS    CL3       NO. DAYS TO SKIP                   *
DAYTO    DS    CL9       DAY TO SKIP TO                     *
*************************************************************
MNTHOUTG DS    CL2       GREG MONTH                    O    *
DAYOUTG  DS    CL2       GREG DAY                      U    *
YEAROUTG DS    CL2       GREG YEAR                     T    *
YEAROUTJ DS    CL2       JULIAN YEAR                   P    *
DAYOUTJ  DS    CL3       JULIAN DAY                    U    *
QUARTER  DS    C         QUARTER (1,2,3 OR 4)          T    *
MNTHNAME DS    CL9       MONTH (JANUARY ETC.)               *
DAYNAME  DS    CL9       DAY (MONDAY ETC.)             D    *
LEAPFLAG DS    C         "Y"=LEAP, "N"=NOT             A    *
DAYDIFF  DS    CL3                                     T    *
WEEKNOY  DS    CL2                                     A    *
WEEKNOQ  DS    CL2                                          *
ERRCODE  DS    C                                            *
*************************************************************
         SPACE 3
WORKSECT DSECT
         DS    CL8
GTOBRTN  DS    F
XDAYRTN  DS    F
DECIMAL  DS    0D
         DS    XL6
DECWORD  DS    PL2
ZONEAREA DS    0CL3
ZONEBYTE DS    C
ZONEHALF DS    CL2
FLAG     DS    X
BINWORD  DS    0F
         DS    XL2
LASTPRTB DS    XL2
BINMNTH  DS    H                      BINARY MONTH
BINYEAR  DS    H                      BINARY YEAR
BINDAYG  DS    H                      BINARY DAYS THIS MONTH
BINDAYJ  DS    H                      BINARY DAYS THIS YEAR
BINMNTH1 DS    H
BINYEAR1 DS    H
BINDAYG1 DS    H
BINDAYJ1 DS    H
PACKIT   DS    0D
         DS    XL6
LAST2    DS    PL2
NOYEARS  DS    H                      NO. OF ELAPSED YEARS
HIGHJUL  DS    H                      NO. DAYS THIS YEAR
HIGHJUL1 DS    H
NODAYS   DS    H                      NO. DAYS TO SKIP (BINARY)
MONTHS   DS    9X
MONTHS2  DS    X
FEB      DS    X
         DS    10X
         EJECT
DATEMVS  REG   DATE-CONVERT-MVS
         L     R8,0(R1)               SAVE ADDRESS OF PARAMETERS
         USING IODATA,R8
         GETMAIN R,LV=96              GET A WORK AREA
         LR    R9,R1                  ADDRESS OF WORK AREA
         USING WORKSECT,R9
         XC    WORKSECT(96),WORKSECT  CLEAR WORK AREA
STARTIT  MVC   MONTHS(21),MNTHNOS     MOVE THE MONTH NO TABLE
         MVC   HIGHJUL,=H'365'
         LA    R4,MONTH1G             GET ADDR OF INPUT DATE
         BAL   R1,VALNUMER            VALIDATE FIRST INPUT DATE
         IC    R4,OPTION              GET THE OPTION
         N     R4,LOW4                MAKE IT LOOK BINARY
         CH    R4,=H'7'               OPTION TOO HIGH?
         BH    BADOPTON               YES, BAD OPTION
         MH    R4,=H'4'               CALCULATE BRANCH ADDR
         A     R4,BRANCHES            *
         BR    R4                     G0!
BRANCHES DC    A(BRVAL)
BRVAL    B     VALIDATE               OPTION=0,
         B     GTOJ                   1, GREG TO JULIAN
         B     JTOG                   2, JULIAN TO GREG
         B     XDAYSFRM               3, X DAYS FROM
         B     XDAYSAGO               4, X DAYS AGO
         B     NEXTDAY                5, NEXT SPECIFIED DAY OF WEEK
         B     LASTDAY                6, LAST SPECIFIED DAY OF WEEK
         B     DIFFEREN               7, FIND THE DIFFERENCE
*
*                            PROCESS OPTION 0
VALIDATE EQU   *
         BAL   R1,GETBINS             THIS WILL VALIDATE
         BAL   R3,JULBTOZ             FORMAT JULIAN DATE FOR OUTPUT
         BAL   R3,GREGBTOZ              "     GREG   "    "    "
         B     ENDINFO
*
*                            PROCESS OPTION 1
GTOJ     EQU   *
         TM    FLAG,X'02'             GOT JULIAN INPUT?
         BO    WRNFRMAT               YES,ERROR
         BAL   R10,GREGZTOB            ZONED GREG TO BIN GREG
         BAL   R3,BINGTOJ             BIN GREG TO BIN JUL
         BAL   R3,JULBTOZ             BIN JUL TO ZONED JUL
         B     ENDINFO
*
*                            PROCESS OPTION 2
JTOG     EQU   *
         TM    FLAG,X'02'             GOT JULIAN INPUT?
         BZ    WRNFRMAT               NO, ERRR
         BAL   R10,JULZTOB            ZONED JUL TO BIN JUL
         BAL   R3,BINJTOG             BIN JUL TO BIN GREG
         BAL   R3,GREGBTOZ            BIN GREG TO ZONED GREG
         B     ENDINFO
*
*                            PROCESS OPTION 3
XDAYSFRM EQU   *
         BAL   R10,XDAYBGN            CHANGE Z TO B + GET NO. DAYS
         LH    R4,BINDAYJ             GET TODAYS DAY NO.
         AH    R4,NODAYS              ADD DAYS TO SKIP
FNDDAYF  EQU   *
         CH    R4,HIGHJUL             ARE WE INTO NEXT YEAR?
         BNH   NOTOVER                NO, WERE O.K.
         SH    R4,HIGHJUL             YES, SUB OFF DAYS THIS YEAR
         STH   R4,BINDAYJ             SAVE DAY NO. OF NEXT YEAR
         LH    R4,BINYEAR             GET YEAR (BINARY)
         LA    R4,1(,R4)              BUMP IT
         STH   R4,BINYEAR             SAVE IT
         BAL   R3,ELAPYEAR            SQUARE AWAY YEAR DATA
         LH    R4,BINDAYJ             GET TODAYS DAY NO. BACK
         B     FNDDAYF                SEE IF YEAR IS CORRECT YET
NOTOVER  EQU   *
NOTUNDER EQU   *
         STH   R4,BINDAYJ             SAVE DAY NO.
         BAL   R3,BINJTOG             BIN JUL TO BIN GREG
         BAL   R3,GREGBTOZ            BIN GREG TO ZONED GREG
         BAL   R3,JULBTOZ             BIN JUL TO ZONED JUL
         B     ENDINFO
*
*                            PROCESS OPTION 4
XDAYSAGO EQU   *
         BAL   R10,XDAYBGN            CHANGE Z TO B + GET NO. DAYS
         LH    R4,BINDAYJ             GET TODAYS DAY NO
         SH    R4,NODAYS              SUB OFF DAYS TO SKIP
FNDDAYB  EQU   *
         BP    NOTUNDER               BRANCH IF STILL POSITIVE
         LH    R5,BINYEAR             GET YEAR
         SH    R5,=H'1'               SUB ONE
         STH   R5,BINYEAR             SAVE NEW YEAR
         BAL   R3,ELAPYEAR            GET SCOOP FOR LAST YEAR
         AH    R4,HIGHJUL             ADD DAYS LAST YEAR
*                                     IF R4 IS NEGATIVE YEAR STILL BAD
         B     FNDDAYB                CHECK FOR CORRECT YEAR
XDAYBGN  EQU   *
         ST    R10,XDAYRTN            SAVE EXIT
         BAL   R1,GETBINS             CHANGE THE DATE TO BINARY
         LA    R4,DAYSTOSK            ADDR OF INPUT (NO DAYS TO SKIP)
         TM    0(R4),X'F0'            V
         BNO   NODAYINV                A
         TM    1(R4),X'F0'              L
         BNO   NODAYINV                  I
         TM    2(R4),X'F0'                D
         BNO   NODAYINV                    ATE
         MVC   ZONEAREA,DAYSTOSK      MOVE NO DAYS TO SKIP
         BAL   R3,ZONETOBN            CHANGE ZONED DAYSTOSK TO BIN
         MVC   NODAYS,LASTPRTB        SAVE
         L     R10,XDAYRTN
         BR    R10
GETBINS  EQU   *
         TM    FLAG,X'02'             GOT A JULIAN DATE?
         BO    CHNGJUL                YES, CHANGE JUL TO BIN
         BAL   R10,GREGZTOB           ZONED GREG TO BIN GREG
         BAL   R3,BINGTOJ             BIN GREG TO BIN JUL
         BR     R1
CHNGJUL  EQU   *
         BAL   R10,JULZTOB            ZONED JUL TO BIN JUL
         BAL   R3,BINJTOG             BIN JUL TO BIN GREG
         BR    R1
*
*                            PROCESS OPTION 5
NEXTDAY  EQU   *
         BAL   R1,GETBINS             CHANGE DATE TO BINARY
         BAL   R3,ELAPYEAR            FIND SCOOP FOR YEAR
         BAL   R3,GETDAY              FIND TODAYS DATE
         LR    R5,R4                  R5 NOW POINTS TO TODAY
         LH    R4,BINDAYJ             GET BINARY DAY COUNT
         LA    R5,9(,R5)              DONT WANT TODAY SO ....
         LA    R4,1(,R4)                 BUMP PTR AND COUNTER
         LA    R6,7                   LOOP CONTROL
LOOKDAYF EQU   *
         CLC   0(9,R5),DAYTO          GOT A MATCH?
         BE    FNDDAYF                YES, GO TO END OF XDAY ROUTINE
         LA    R5,9(,R5)              NO, BUMP DAY PTR
         LA    R4,1(,R4)              BUMP DAY COUNTER
         BCT   R6,LOOKDAYF            BRANCH IF STILL MORE TO CHECK
         B     BADDAY                 BAD INPUT DAY
*
*                            PROCESS OPTION 6
LASTDAY  EQU   *
         BAL   R1,GETBINS             GET DATE IN BINARY FORM
         BAL   R3,ELAPYEAR            FIND SCOOP FOR THIS YEAR
         BAL   R3,GETDAY              FIND TODAYS DAY
         LR    R5,R4                  R5 POINTS TO TODAY
         LH    R4,BINDAYJ             GET BINARY DAYS
         SH    R5,=H'9'               DONT WANT TODAY SO ...
         SH    R4,=H'1'                  DEC PTR AND COUNTER
         LA    R6,7
LOOKDAYB EQU   *
         CLC   0(9,R5),DAYTO          GOT A MATCH
         BE    ENDCHK                 YES NEED TO SET COND CODE
         SH    R5,=H'9'               DEC
         SH    R4,=H'1'
         BCT   R6,LOOKDAYB            BRANCH IF MORE
         B     BADDAY                 ERRRRRRR
ENDCHK   EQU   *
         LTR   R4,R4                  SET COND CODE
         B     FNDDAYB                BORROW CODE FROM XDAY ROUTINE
*
*                            PROCESS OPTION 7
DIFFEREN EQU   *
         BAL   R1,GETBINS             CHANGE FIRST ONE TO BINARY
         MVC   HIGHJUL1,HIGHJUL       SAVE MAX DAYS THIS YEAR
         MVC   BINMNTH1(8),BINMNTH    SAVE BINARY INFO THIS MONTH
         LA    R4,MONTH2G
         BAL   R1,VALNUMER            VALIDATE SECOND INPUT DATE
         MVC   MONTH1G(6),MONTH2G     MOVE TO FIRST LOCATION
         BAL   R1,GETBINS             CHANGE IT TO BINARY
         CLC   BINYEAR,BINYEAR1       SAME YEAR?
         BE    FINALTST               YES, FINE
         BL    ADDDAY                 FISRT LOW
         LH    R4,HIGHJUL1            SECOND LOW, GET DAYS LOW YEAR
         AH    R4,BINDAYJ             ADD DAYS HIGH YEAR
         STH   R4,BINDAYJ             SAVE DAYS HIGH YEAR
         LH    R4,BINYEAR             GET HIGH YEAR
         SH    R4,=H'1'               DEC BY ONE
         STH   R4,BINYEAR
         B     FINALTST
ADDDAY   EQU   *
         LH    R4,HIGHJUL             GET DAYS LOW YEAR
         AH    R4,BINDAYJ1            ADD DAYS HIGH YEAR
         STH   R4,BINDAYJ1
         LH    R4,BINYEAR1
         SH    R4,=H'1'
         STH   R4,BINYEAR1
FINALTST EQU   *
         CLC   BINYEAR,BINYEAR1       ARE THEY THE SAME NOW?
         BNE   DIFTOBIG               NO, CANT HANDLE IT
         LH    R4,BINDAYJ             GET DAYS YEAR 1
         SH    R4,BINDAYJ1            SUB YEAR 2
         BP    *+6                    BRANCH ON POSITIVE
         LCR   R4,R4                  COMPLIMENT IF NOT
         CVD   R4,PACKIT              BINARY TO PACKED
         UNPK  DAYDIFF,LAST2          PACKED TO ZONED
         OI    DAYDIFF+2,X'F0'
         B     THATSIT-4
VALNUMER EQU   *
         LA    R3,5                   LOOP CONTROL
CHKNUMER EQU   *
         TM    0(R4),X'F0'            NUMERIC FORMAT?
         BNO   BADFRMAT               NO, SO ERROR
         LA    R4,1(,R4)              BUMP PTR
         BCT   R3,CHKNUMER            CHECKED FIRST 5 ALREADY?
         TM    0(R4),X'F0'            YES, IS LAST ONE NUMERIC?
         BO    DATEOK                 YES, SO MUST BE A GREG DATE
         CLI   0(R4),C' '             FLAGED FOR JULIAN?
         BNE   BADFRMAT               NO, MUST BE ERRRRRRRR
         OI    FLAG,X'02'             SET JULIAN FLAG
         BR    R1
DATEOK   EQU   *
         NI    FLAG,X'FD'             TURN OFF JUL FLAG
         BR    R1
GREGZTOB EQU   *
         BAL   R3,GREGTOBN            GREG DATE TO BINARY
         BAL   R3,ELAPYEAR            FIND ELAPSED YRS AND LEAP
         CLC   BINMNTH,=H'12'         MONTH TOO HIGH?
         BH    BADGMNTH               YEP
         LH    R4,BINMNTH             GET BINARY MONTH
         LH    R5,=H'12'              FIND ADDR OF
         SR    R5,R4                   DAYS
         LA    R4,MONTHS                THIS
         AR    R5,R4                     MONTH
         CLC   0(1,R5),BINDAYG+1       TO MANY DAYS THIS MONTH?
         BL    BADGDAY                YES
         BR    R10                    EXIT
JULZTOB  EQU   *
         MVC   ZONEAREA,DAY1J         MOVE DAYS THIS YEAR
         BAL   R3,ZONETOBN            CONVERT TO BINARY
         MVC   BINDAYJ,LASTPRTB       SAVE IT
         CLC   BINDAYJ,=H'0'          IS DAY ZERO?
         BNH   BADJDAY                YES, (OR NEG) SO ERROR
         MVC   ZONEHALF,YEAR1J        MOVE THE YEAR
         MVI   ZONEBYTE,C'0'
         BAL   R3,ZONETOBN            CONVERT TO BINARY
         MVC   BINYEAR,LASTPRTB       SAVE BINARY YEAR
         BAL   R3,ELAPYEAR            FIND ELAPSED AND LEAP
         CLC   BINDAYJ,HIGHJUL        TO MANY DAYS THIS YEAR?
         BH    BADJDAY                YES
         BR    R10                    EVERYTHING IS O.K.
GREGTOBN EQU   *
         ST    R3,GTOBRTN             SAVE EXIT
         MVC   ZONEHALF,MONTH1G       MOVE THE MONTH
         MVI   ZONEBYTE,X'F0'         MUST BE ZONED 0
         BAL   R3,ZONETOBN            CONVERT TO  BINARY
         MVC   BINMNTH,LASTPRTB       SAVE BINARY MONTH
         CLC   BINMNTH,=H'0'          IS MONTH ZERO?
         BNH   BADGMNTH               YES, (OR NEG) SO ERROR
         MVC   ZONEHALF,DAY1G         MOVE DAY
         BAL   R3,ZONETOBN
         MVC   BINDAYG,LASTPRTB       SAVE BINARY DAYS
         CLC   BINDAYG,=H'0'          IS DAY ZERO?
         BNH   BADGDAY                YES, (OR NEG) SO ERROR
         MVC   ZONEHALF,YEAR1G        MOVE THE YEAR
         BAL   R3,ZONETOBN
         MVC   BINYEAR,LASTPRTB       SAVE BINARY YEAR
         L     R3,GTOBRTN             RESTORE EXIT
         BR    R3                     BYE
ZONETOBN EQU   *
         PACK  DECWORD,ZONEAREA       ZONED TO PACKED
         CVB   R4,DECIMAL             PACKED TO BINARY
         ST    R4,BINWORD             STORE
         BR    R3
ELAPYEAR EQU   *
         LH    R6,BINYEAR             GET BINARY YEAR
         LR    R5,R6                  SAVE IN R5
         SRDL  R6,32                  SHIFT RIGHT TO DIVIDE TO
         D     R6,=F'4'               FIND NO. OF LEAPS
         AR    R5,R7                  ADD LEAPS TO ELAPSED YEARS
         CH    R6,=H'0'               GOT A LEAP YEAR?
         BE    LEAPYEAR               YEP
         LA    R5,1(,R5)              NO, SO ADD 1 TO ELAPSED
         MVI   FEB,X'1C'              FEB HAS 28
         MVC   HIGHJUL,=H'365'        YEAR HAS 365
         NI    FLAG,X'FE'             TURN OFF LEAP FLAG
         B     EPEXIT
LEAPYEAR EQU   *
         OI    FLAG,X'01'             SET FLAG
         MVI   FEB,X'1D'              FEB HAS 29
         MVC   HIGHJUL,=H'366'        YEAR HAS 365
EPEXIT   EQU   *
         STH   R5,NOYEARS             SAVE NO ELAPSED YEARS
         BR    R3
BINGTOJ  EQU   *
         XR    R6,R6                  ZAP
         XR    R5,R5                    REGS
         LH    R4,BINMNTH             LOOP CONTROL AND INDEX
         BCT   R4,*+8                 IF MONTH=1 WE FALL THROUGH
         B     ADDDAYS                AND SKIP THE LOOP
ADDLOOP  EQU   *
         IC    R6,MONTHS2-1(R4)       GET DAYS THIS MONTH
         AR    R5,R6                  ADD TO TOTAL
         BCT   R4,ADDLOOP             BRANCH IF MORE MONTHS TO ADD
ADDDAYS  EQU   *
         LH    R6,BINDAYG             ADD DAYS OF CURRENT
         AR    R5,R6                   MONTH TO TOTAL
         STH   R5,BINDAYJ             SAVE IT
         BR    R3
BINJTOG  EQU   *
         LH    R4,BINDAYJ             GET BINARY DAYS THIS YEAR
         XR    R5,R5                  ZAP     -DAYS THIS MONTH
         XR    R6,R6                    REGS  -DAYS SO FAR
         LH    R7,=H'12'              LOPP CONTROL AND INDEX
SUBLOOP  EQU   *
         IC    R5,MONTHS-1(R7)        GET DAYS THIS MONTH
         AR    R6,R5                  ADD TO TOTAL
         CR    R6,R4                  ARE WE OVER?
         BNL   GOTMONTH               YES WRAP IT UP
         BCT   R7,SUBLOOP             DEC INDEX AND GO BACK
         B     BADJDAY                SHOULD NOT GET HERE
GOTMONTH EQU   *
         SR    R6,R5                  WERE OVER SO SUB THIS MONTH
         SR    R4,R6                  FIND DAYS THIS MONTH
         STH   R4,BINDAYG             SAVE BINARY DAY OF MONTH
         LH    R5,=H'13'              FIND WHICH MONTH
         SR    R5,R7                  SUB OFF THE INDEX
         STH   R5,BINMNTH             SAVE THE BINARY MONTH
         BR    R3
JULBTOZ  EQU   *
         LH    R5,BINDAYJ             GET BINARY DAYS THIS YEAR
         CVD   R5,PACKIT              CONVERT TO DECIMAL
         UNPK  DAYOUTJ,LAST2          UNPACK IT TO OUTPUT
         OI    DAYOUTJ+2,X'F0'
         LH    R5,BINYEAR             GET BIN YEAR
         CVD   R5,PACKIT              BINARY TO PACKED
         UNPK  YEAROUTJ,LAST2         PACKED TO ZONED
         OI    YEAROUTJ+1,X'F0'
         BR    R3
GREGBTOZ EQU   *
         LH    R4,BINDAYG             GET BINARY DAYS THIS MONTH
         CVD   R4,PACKIT              CONVERT TO DECIMAL
         UNPK  DAYOUTG,LAST2          UNPACK TO OUTPUT
         OI    DAYOUTG+1,X'F0'
         LH    R5,BINMNTH             GET THE MONTH
         CVD   R5,PACKIT              TO PACKED
         UNPK  MNTHOUTG,LAST2         TO ZONED
         OI    MNTHOUTG+1,X'F0'
         LH    R5,BINYEAR             GET BINARY YEAR
         CVD   R5,PACKIT              BINARY TO PACKED
         UNPK  YEAROUTG,LAST2         PACKED TO ZONED
         OI    YEAROUTG+1,X'F0'
         BR    R3
GETDAY   EQU   *
         LA    R4,DAYTAB              ADDR OF DAY TABLE
         LH    R7,BINYEAR             GET BINARY YEAR
         CH    R7,=H'4'               IS IT GREATER THAN 4?
         BH    NORMAL                 YES GO TO NORMAL RTN
* SINCE 1900 IS NOT A LEAP YEAR 00-4 MUST GO THROUGH SPECIAL ROUTINE
         LA    R7,1(,R7)              BUMP BY 1!!
         MH    R7,=H'9'               9 CHAR PER DAY
         AR    R4,R7                  ADD TO TAB BEGIN
         B     FINDDAY
NORMAL   EQU   *   NORMAL DAY CALCULATION
         LH    R7,NOYEARS             GET NO OF ELAPSED YEARS
         XR    R6,R6                  CLEAR 6 TO DIVIDE
         D     R6,=F'7'               DIVIDE MOD 7
         SRDL  R6,32                  NEED ONLY THE REMAINDER
         M     R6,=F'9'               9 CHARS PER DAY
         AR    R4,R7                  R4 NOW POINTS TO DAY 0
FINDDAY  EQU   *
         LH    R7,BINDAYJ             GET BINARY DAY COUNT
         XR    R6,R6                  CLEAR TO DIVIDE
         D     R6,=F'7'
         SRDL  R6,32                  NEED ONLY THE REMAINDER
         M     R6,=F'9'               9 CHARS PER DAY
         AR    R4,R7                  R4 NOW POINTS TO TODAY
         MVC   DAYNAME,0(R4)          MOVE DAY TO OUTPUT
         BR    R3
GETMONTH EQU   *
         LH    R5,BINMNTH             GET BINARY MONTH
         MH    R5,=H'9'               9 CHARS PER MONTH
         SH    R5,=H'9'               STEP BACK 1
         BM    BADGMNTH               MONTH IS 0, SO ERR
         LA    R4,MONTHTAB            ADDR OF MONTH TABLE
         AR    R4,R5                  R4 NOW POINTS TO THIS MONTH
         MVC   MNTHNAME,0(R4)         MOVE MONTH TO OUTPUT
         BR    R3
ENDINFO  EQU   *
         BAL   R3,GETDAY
         BAL   R3,GETMONTH
         TM    FLAG,X'01'             LEAPYEAR?
         BZ    NOLEAP                 NO
         MVI   LEAPFLAG,C'Y'          MOVE A "Y"
         B     YESLEAP
NOLEAP   MVI   LEAPFLAG,C'N'          MOVE A "N"
YESLEAP  XR    R6,R6
         LH    R7,BINDAYJ
         CLC   BINMNTH,=H'4'          MONTH > 4
         BNL   NOT1QTR                YES; CONTINUE
         MVI   QUARTER,C'1'           NO;  SET 1ST QTR
         B     WEEKOFF
NOT1QTR  CLC   BINMNTH,=H'7'          MONTH > 7
         BNL   NOT2QTR                YES; CONTINUE
         MVI   QUARTER,C'2'            SET 2ND QTR
         SH    R7,=H'90'
         B     WEEKOFF
NOT2QTR  CLC   BINMNTH,=H'10'         MONTH > 10
         BNL   NOT3QTR                YES; CONTINUE
         MVI   QUARTER,C'3'           NO;  SET 3ED QTR
         SH    R7,=H'181'
         B     WEEKOFF
NOT3QTR  MVI   QUARTER,C'4'           MUST BE 4TH QTR
         SH    R7,=H'273'
WEEKOFF  EQU   *
         D     R6,=F'7'
         LTR   R6,R6
         BZ    WKZERO
         LA    R7,1(,R7)
WKZERO   CVD   R7,PACKIT
         UNPK  WEEKNOQ,LAST2
         OI    WEEKNOQ+1,X'F0'
         LH    R7,BINDAYJ
         XR    R6,R6
         D     R6,=F'7'
         LTR   R6,R6
         BZ    WEEK0
         LA    R7,1(,R7)
WEEK0    CVD   R7,PACKIT
         UNPK  WEEKNOY,LAST2
         OI    WEEKNOY+1,X'F0'
         MVI   ERRCODE,C'0'           NO ERRORS!
THATSIT  EQU   *
         FREEMAIN R,LV=96,A=(R9)
         LR    R5,R13                 SAVE ADDRESS OF SAVE AREA FOR
*                                          FREEMAIN
         L     R13,4(R13)             RESTORE R13
         FREEMAIN R,LV=72,A=(R5)
         RETURN (14,12),RC=0          GO BACK TO CALLER
BADOPTON EQU   *                      OPTION NOT 0 - 7
         MVI   ERRCODE,C'1'
         B     THATSIT
BADJDAY  EQU   *                      DAY < 0 OR > NO DAYS IN YEAR
BADGDAY  EQU   *                      DAY > NO DAYS IN MONTH
         MVI   ERRCODE,C'2'
         B     THATSIT
BADGMNTH EQU   *                      MONTH > 12
         MVI   ERRCODE,C'3'
         B     THATSIT
BADGYEAR EQU   *                      NOT USED
BADJYEAR EQU   *                      NOT USED
         MVI   ERRCODE,C'4'
         B     THATSIT
BADFRMAT EQU   *                      DATE FIELD NON NUMERIC
         MVI   ERRCODE,C'9'
         B     THATSIT
WRNFRMAT EQU   *           CANNOT CONVERT TO SAME FORMAT STARTED WITH
         MVI   ERRCODE,C'5'
         B     THATSIT
NODAYINV EQU   *                      NUMBER DAYS TO SKIP NON NUMERIC
         MVI   ERRCODE,C'6'
         B     THATSIT
BADDAY   EQU   *                      INVALID DAY OF WEEK SPECIFIED
         MVI   ERRCODE,C'7'
         B     THATSIT
DIFTOBIG EQU   *                  YEAR FIELD DIFFERS BY MORE THAN ONE
         MVI   ERRCODE,C'8'
         B     THATSIT
         LTORG
LOW4     DC    F'15'
         DC    CL9' SATURDAY'
         DC    CL9'   SUNDAY'
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
DAYTAB   EQU   *
         DC    CL9' SATURDAY'
         DC    CL9'   SUNDAY'
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
         DC    CL9' SATURDAY'
         DC    CL9'   SUNDAY'
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
         DC    CL9' SATURDAY'
         DC    CL9'   SUNDAY'
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
         DC    CL9' SATURDAY'
MNTHNOS  DC    AL1(31,30,31,30,31,31,30,31,30)
         DC    AL1(31)
         DC    AL1(28,31)
         DC    AL1(30,31,30,31,31,30,31,30,31)
MONTHTAB EQU   *
         DC    CL9'  JANUARY'
         DC    CL9' FEBRUARY'
         DC    CL9'    MARCH'
         DC    CL9'    APRIL'
         DC    CL9'      MAY'
         DC    CL9'     JUNE'
         DC    CL9'     JULY'
         DC    CL9'   AUGUST'
         DC    CL9'SEPTEMBER'
         DC    CL9'  OCTOBER'
         DC    CL9' NOVEMBER'
         DC    CL9' DECEMBER'
         END
