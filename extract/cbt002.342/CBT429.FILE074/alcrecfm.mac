         MACRO
&NAME    ALCRECFM &RECFM=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'RECFM' KEYWORD
.*
.*       RECFM='FBA'            1 - 3 BYTES
.*       RECFM=NAME             NAME  DC  CL3'VBS'  1 - 3 BYTES
.*
.*---------------------------------------------------------------------
         GBLA  &SPF1,&SPF2
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
&NAME    DS    0H
         AIF   ('&RECFM'(1,1) EQ '''').LITERAL
*----------------------------------------------------------------------
*        RECORD FORMAT
*----------------------------------------------------------------------
         CLI   &RECFM,C' '        ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALRECFM        KEY FOR RECFM
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         CLI   &RECFM,C'F'        FIXED ?
         BE    MAAA&X             YES
         CLI   &RECFM,C'V'        VARIABLE ?
         BE    MBBB&X             YES
         CLI   &RECFM,C'U'        UNDEFINED ?
         BE    MCCC&X             YES
         BAL   R14,ERR1&SPF2
         B     MZZZ&SPF1
MAAA&X   EQU   *
         OI    S99TUPAR,X'80'     FIXED
         B     MDDD&X
MBBB&X   EQU   *
         OI    S99TUPAR,X'40'     VARIABLE
         B     MDDD&X
MCCC&X   EQU   *
         OI    S99TUPAR,X'C0'     UNDEFINED
MDDD&X   EQU   *
         AIF   (L'&RECFM LT 2).NOMORE
         CLI   &RECFM+1,C'B'      BLOCKED ?
         BE    MEEE&X             YES
         CLI   &RECFM+1,C'A'      ANS ?
         BE    MFFF&X             YES
         CLI   &RECFM+1,C'M'      MACHINE ?
         BE    MGGG&X             YES
         CLI   &RECFM+1,C' '      END OF DATA ?
         BE    MKKK&X             YES
         B     ERR1&SPF2
MEEE&X   EQU   *
         OI    S99TUPAR,X'10'     BLOCKED
         B     MHHH&X
MFFF&X   EQU   *
         OI    S99TUPAR,X'04'     ANS
         B     MHHH&X
MGGG&X   EQU   *
         OI    S99TUPAR,X'02'     MACHINE
MHHH&X   EQU   *
         AIF   (L'&RECFM LT 3).NOMORE
         CLI   &RECFM+2,C'A'      ANS ?
         BE    MIII&X             YES
         CLI   &RECFM+2,C'M'      MACHINE ?
         BE    MJJJ&X             YES
         CLI   &RECFM+2,C' '      END OF DATA ?
         BE    MKKK&X             YES
         B     ERR1&SPF2
MIII&X   EQU   *
         OI    S99TUPAR,X'04'     ANS
         B     MKKK&X
MJJJ&X   EQU   *
         OI    S99TUPAR,X'02'     MACHINE
.NOMORE  ANOP
MKKK&X   EQU   *
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         AGO   .DONE
.LITERAL ANOP
*----------------------------------------------------------------------
*        RECORD FORMAT
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALRECFM        KEY FOR RECFM
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         AIF   ('&RECFM'(2,1) EQ 'F').AAA
         AIF   ('&RECFM'(2,1) EQ 'V').BBB
         AIF   ('&RECFM'(2,1) EQ 'U').CCC
         MNOTE 8,'INVALID RECFM'
         MEXIT
.AAA     ANOP
         OI    S99TUPAR,X'80'     FIXED
         AGO   .DDD
.BBB     ANOP
         OI    S99TUPAR,X'40'     VARIABLE
         AGO   .DDD
.CCC     ANOP
         OI    S99TUPAR,X'C0'     UNDEFINED
.DDD     ANOP
         AIF   ('&RECFM'(3,1) EQ '''').KKK
         AIF   ('&RECFM'(3,1) EQ 'B').EEE
         AIF   ('&RECFM'(3,1) EQ 'A').FFF
         AIF   ('&RECFM'(3,1) EQ 'M').GGG
         MNOTE 8,'INVALID RECFM'
         MEXIT
.EEE     ANOP
         OI    S99TUPAR,X'10'     BLOCKED
         AGO   .HHH
.FFF     ANOP
         OI    S99TUPAR,X'04'     ASA CONTROL CHARACTER
         AGO   .KKK
.GGG     ANOP
         OI    S99TUPAR,X'02'     MACHINE CHARACTER
         AGO   .KKK
.HHH     ANOP
         AIF   ('&RECFM'(4,1) EQ '''').KKK
         AIF   ('&RECFM'(4,1) EQ 'A').III
         AIF   ('&RECFM'(4,1) EQ 'M').JJJ
         MNOTE 8,'INVALID RECFM'
         MEXIT
.III     ANOP
         OI    S99TUPAR,X'04'     ASA CONTROL CHARACTER
         AGO   .KKK
.JJJ     ANOP
         OI    S99TUPAR,X'02'     MACHINE CHARACTER
.KKK     ANOP
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
.DONE    ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
