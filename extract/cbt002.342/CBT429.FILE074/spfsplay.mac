         MACRO
&NAME    SPFSPLAY &PAN,&MSG,&CUR,&ZERO,&NZERO
.*---------------------------------------------------------------------
.*       THIS MACRO WILL INVOKE THE SPF DISPLAY SERVICE FOR PANELS,
.*       AND/OR MESSAGES
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLC  &P,&M,&C,&LIT
         AIF   (T'&CUR EQ 'O').NOCUR
         AIF   ('&CUR'(1,1) NE '''').AAACUR
&LIT     SETC  '&CUR'(2,K'&CUR-2)
         MVC   CURSOR,=CL8'&LIT'
         AGO   .BBBCUR
.AAACUR  ANOP
         AIF   ('&CUR' EQ 'CURSOR').BBBCUR
         MVC   CURSOR,&CUR
.BBBCUR  ANOP
&C       SETC  'CURSOR'
         AIF   (T'&PAN EQ 'O').EEECUR
         AIF   ('&PAN'(1,1) NE '''').CCCCUR
&LIT     SETC  '&PAN'(2,K'&PAN-2)
         MVC   PANEL,=CL8'&LIT'
         AGO   .DDDCUR
.CCCCUR  ANOP
         AIF   ('&PAN' EQ 'PANEL').DDDCUR
         MVC   PANEL,&PAN
.DDDCUR  ANOP
&P       SETC  'PANEL'
.EEECUR  ANOP
         AIF   (T'&MSG EQ 'O').HHHCUR
         AIF   ('&MSG'(1,1) NE '''').FFFCUR
&LIT     SETC  '&MSG'(2,K'&MSG-2)
         MVC   MSG,=CL8'&LIT'
         AGO   .GGGCUR
.FFFCUR  ANOP
         AIF   ('&MSG' EQ 'MSG').GGGCUR
         MVC   MSG,&MSG
.GGGCUR  ANOP
&M       SETC  'MSG'
.HHHCUR  ANOP
         CALL  ISPLINK,(DISPLAY,&P,&M,&C),VL
         AGO   .XXX
.*------------------------------------------------------------------
.NOCUR   ANOP
         AIF   (T'&MSG EQ 'O').NOMSG
         AIF   ('&MSG'(1,1) NE '''').AAAMSG
&LIT     SETC  '&MSG'(2,K'&MSG-2)
         MVC   MSG,=CL8'&LIT'
         AGO   .BBBMSG
.AAAMSG  ANOP
         AIF   ('&MSG' EQ 'MSG').BBBMSG
         MVC   MSG,&MSG
.BBBMSG  ANOP
&M       SETC  'MSG'
         AIF   (T'&PAN EQ 'O').EEEMSG
         AIF   ('&PAN'(1,1) NE '''').CCCMSG
&LIT     SETC  '&PAN'(2,K'&PAN-2)
         MVC   PANEL,=CL8'&LIT'
         AGO   .DDDMSG
.CCCMSG  ANOP
         AIF   ('&PAN' EQ 'PANEL').DDDMSG
         MVC   PANEL,&PAN
.DDDMSG  ANOP
&P       SETC  'PANEL'
.EEEMSG  ANOP
         CALL  ISPLINK,(DISPLAY,&P,&M),VL
         AGO   .XXX
.*------------------------------------------------------------------
.NOMSG   ANOP
         AIF   (T'&PAN EQ 'O').NOPAN
         AIF   ('&PAN'(1,1) NE '''').AAAPAN
&LIT     SETC  '&PAN'(2,K'&PAN-2)
         MVC   PANEL,=CL8'&LIT'
         AGO   .BBBPAN
.AAAPAN  ANOP
         AIF   ('&PAN' EQ 'PANEL').BBBPAN
         MVC   PANEL,&PAN
.BBBPAN  ANOP
&P       SETC  'PANEL'
         CALL  ISPLINK,(DISPLAY,&P),VL
         AGO   .XXX
.NOPAN   ANOP
         MNOTE 8,'*** PANEL, MESSAGE, AND CURSOR NOT SPECIFIED'
         AGO   .ZZZ
.XXX     ANOP
         LTR   R15,R15
         AIF   ('&NZERO' EQ '').YYY
         BNZ   &NZERO
.YYY     ANOP
         AIF   ('&ZERO' EQ '').ZZZ
         BZ    &ZERO
.ZZZ     ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
