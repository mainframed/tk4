         MACRO
&NAME    SPFBRWSE &IDS,&MBR,&VOL,&PAN                 @MK
.*---------------------------------------------------------------------
.*       THIS MACRO INVOKES SPF BROWSE FOR A DATA SET OR DATA SET AND
.*       MEMBER.
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLC  &LIT,&REG,&LA
         LCLA  &X
&X       SETA  &SYSNDX
         MVC   SPFEPAN,BLANKS          SET PANEL NAME TO BLANKS
         MVC   SPFDYNAM,BLANKS         TO BLANKS
         MVI   SPFDYNAM,X'7D'          ADD A QUOTE
         AIF   ('&IDS'(1,1) EQ '''').AAA
         AIF   ('&IDS'(1,1) EQ '(').REG1
&LA      SETC  'L''&IDS'
         MVC   SPFDYNAM+1(&LA),&IDS    MOVE DSN
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&IDS'(2,K'&IDS-2)
         MVC   SPFDYNAM+1(54),=CL54'&LIT' MOVE DSN
         AGO   .BBB
.REG1    ANOP
&REG     SETC  '&IDS'(2,K'&IDS-2)
         MVC   SPFDYNAM+1(54),0(&REG)  MOVE DSN
.BBB     ANOP
         LA    R14,SPFDYNAM            START OF FIELD
         XC    SPFTRT,SPFTRT           CLEAR TRT TABLE
         MVI   SPFTRT+C' ',X'FF'       SET FOR A BLANK
         TRT   0(56,R14),SPFTRT        FIND END
         BNZ   MAAA&X                  FOUND IT
         LA    R1,55(R14)              SET DEFAULT
MAAA&X   EQU   *
.*-------------------------------------------------------------------
         AIF   (T'&MBR EQ 'O').EEE
         MVI   0(R1),C'('              ADD A PAREN
         LA    R14,1(R1)               BUMP
         AIF   ('&MBR'(1,1) EQ '''').CCC
         AIF   ('&MBR'(1,1) EQ '(').REG2
         CLI   &MBR,C' '               ANY SPECIFIED ?
         BNH   MCCC&X                  NO - SKIP IT
&LA      SETC  'L''&MBR'
         MVC   0(&LA,R14),&MBR         MOVE MBR NAME
         AGO   .DDD
.CCC     ANOP
&LIT     SETC  '&MBR'(2,K'&MBR-2)
         MVC   0(8,R14),=CL8'&LIT'     MOVE MBR NAME
         AGO   .DDD
.REG2    ANOP
&REG     SETC  '&MBR'(2,K'&MBR-2)
         MVC   0(8,R14),0(&REG)        MOVE MBR NAME
.DDD     ANOP
         TRT   0(8,R14),SPFTRT         FIND END
         BNZ   MBBB&X                  FOUND IT
         LA    R1,8(R14)               SET DEFAULT
MBBB&X   EQU   *
         MVI   0(R1),C')'              ADD A PAREN
         LA    R1,1(R1)                BUMP
MCCC&X   EQU   *
.EEE     ANOP
         MVI   0(R1),X'7D'             ADD ENDING QUOTE
.*-------------------------------------------------------------------
         MVC   SPFVOL,BLANKS
         AIF   (T'&VOL EQ 'O').FFF
         MVC   SPFVOL,&VOL
.FFF     ANOP
         MVC   SPFEPAN,BLANKS          SET PANEL NAME TO BLANKS @MK
         AIF   (T'&PAN EQ 'O').GGG                              @MK
         MVC   SPFEPAN,&PAN                                     @MK
.GGG     ANOP                                                   @MK
         CALL  ISPLINK,(BROWSE,SPFDYNAM,SPFVOL,SPFPSWD,SPFEPAN),VL @MK
         B     MZZZ&X                  GO AROUND CONSTANT
MZZZ&X   DS    0H
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
