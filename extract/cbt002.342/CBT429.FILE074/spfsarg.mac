         MACRO
&NAME    SPFSARG &TBL,&FLD
.*---------------------------------------------------------------------
.*       THIS MACRO INVOKES THE SPF TBSARG SERVICE
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
         MVC   SMSG,BLANKS        CLEAR
         MVC   LMSG,BLANKS        CLEAR
         STM   R2,R3,SPFREGS      SAVE REGS
         LA    R3,ZCMD            ADDR OF ZCMD
         XC    SPFTRT,SPFTRT      CLEAR
         MVI   SPFTRT+C' ',X'FF'  SET FOR A BLANK
         TRT   0(12,R3),SPFTRT    FIND BLANK AFTER COMMAND
         LA    R3,0(R1)           BUMP TO FIRST BLANK
*
         MVI   SPFTRT,X'FF'       SET FOR
         MVC   SPFTRT+1(255),SPFTRT   ALL
         MVI   SPFTRT+C' ',X'00'  BUT A BLANK
         TRT   0(12,R3),SPFTRT    FIND NON-BLANK AFTER COMMAND
         BZ    MYYY&X             NO DATA, ERROR
         LA    R3,0(R1)           BUMP TO START OF STRING
*
         XC    SPFTRT,SPFTRT      CLEAR
         MVI   SPFTRT+C' ',X'FF'  SET FOR A BLANK
         TRT   0(3,R3),SPFTRT     FIND END OF STRING
         BZ    MYYY&X             INVALID VALUE
         SR    R1,R3              LENGTH OF STRING
         C     R1,=F'2'           MUST BE 2
         BNE   MYYY&X             INVALID
         MVC   CND&X,0(R3)        COPY CONDITION
         LA    R3,3(R3)           BUMP PAST COND
*
         MVI   SPFTRT,X'FF'       SET FOR
         MVC   SPFTRT+1(255),SPFTRT   ALL
         MVI   SPFTRT+C' ',X'00'  BUT A BLANK
         TRT   0(12,R3),SPFTRT    FIND NON-BLANK AFTER COND
         BZ    MYYY&X             NO DATA, ERROR
         LA    R3,0(R1)           BUMP TO START OF STRING
         CALL  ISPLINK,(TBTOP,&TBL),VL
         CALL  ISPLINK,(TBVCLEAR,&TBL),VL
         MVC   &FLD,0(R3)         COPY VALUE
.*---------------------------------------------------------------------
         CALL  ISPLINK,(TBSARG,&TBL,,,FLD&X),VL
         B     MZZZ&X
.*---------------------------------------------------------------------
FLD&X    DC    C'(&FLD,'
CND&X    DC    CL2' '
         DC    C')'
MYYY&X   DS    0H
         MVC   SMSG,=CL24'INVALID SEARCH STRING'
         LA    R15,24
MZZZ&X   DS    0H
         LM    R2,R3,SPFREGS      RESTORE REGS
         MEND
