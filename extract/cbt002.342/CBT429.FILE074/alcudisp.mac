         MACRO
&NAME    ALCUDISP &DISP=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'DISP' KEYWORD FOR UNALLOCATION REQUESTS. IT WILL OVERRIDE THE
.*       DISPOSITION ASSIGNED TO A DATA SET WHEN IT WAS ALLOCATED.
.*       @MK           @MK              @MK        @MK
.*       DISP=CATLG   ANY VALID DISPOSITION (UNCATLG,CATLG,KEEP,DELETE)
.*       DISP=NAME    NAME  DC  CL20'KEEP'  DO NOT CODE VALUE IN PARENS
.*---------------------------------------------------------------------
         GBLA  &SPF1,&SPF2
         GBLC  &SPF3
         LCLA  &X,&LL,&D
         LCLC  &LIT,&LA
&X       SETA  &SYSNDX
&NAME    DS    0H
         AIF   ('&SPF2' NE '0').GBSET
&SPF2    SETA  &SYSNDX
.GBSET   ANOP
         AIF   ('&DISP'(1,1) NE '''').ZZZ
*----------------------------------------------------------------------
*        DISP
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DUNOVDSP        KEY FOR OVERRIDING DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
&D       SETA  2
&LL      SETA  &D
.AAA     ANOP
         AIF   ('&DISP'(&D,1) EQ '''').BBB
&D       SETA  &D+1
         AGO   .AAA
.BBB     ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
         AIF   ('&DISP'(&D,&LL) EQ 'DELETE').CCC
         AIF   ('&DISP'(&D,&LL) EQ 'CATLG').DDD
         AIF   ('&DISP'(&D,&LL) EQ 'KEEP').EEE
         AIF   ('&DISP'(&D,&LL) EQ 'UNCATLG').FFF
         MNOTE 8,'INVALID DISPOSITION FIELD'
         MEXIT
.CCC     ANOP
         MVI   S99TUPAR,X'04'     DISP=DELETE
         AGO   .GGG
.DDD     ANOP
         MVI   S99TUPAR,X'02'     DISP=CATLG
         AGO   .GGG
.EEE     ANOP
         MVI   S99TUPAR,X'08'     DISP=KEEP
         AGO   .GGG
.FFF     ANOP
         MVI   S99TUPAR,X'01'     DISP=UNCATLG
.GGG     ANOP
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         AGO   .DONE
.ZZZ     ANOP
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DUNOVDSP        KEY FOR OVERRIDING DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         CLI   &DISP,C' '         ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         LA    R3,&DISP
         CLC   0(6,R3),=C'DELETE'
         BE    MCCC&X             DISP=DELETE
         CLC   0(5,R3),=C'CATLG'
         BE    MDDD&X             DISP=CATLG
         CLC   0(4,R3),=C'KEEP'
         BE    MEEE&X             DISP=KEEP
         CLC   0(7,R3),=C'UNCATLG'
         BE    MFFF&X             DISP=UNCATLG
         BAL   R14,ERR1&SPF2
         B     MZZZ&SPF1
MCCC&X   EQU   *
         MVI   S99TUPAR,X'04'     DISP=DELETE
         B     MGGG&X
MDDD&X   EQU   *
         MVI   S99TUPAR,X'02'     DISP=CATLG
         B     MGGG&X
MEEE&X   EQU   *
         MVI   S99TUPAR,X'08'     DISP=KEEP
         B     MGGG&X
MFFF&X   EQU   *
         MVI   S99TUPAR,X'01'     DISP=UNCATLG
MGGG&X   EQU   *
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
.DONE    ANOP
         MEND
