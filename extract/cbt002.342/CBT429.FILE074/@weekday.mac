         MACRO
&LABEL   @WEEKDAY &JDATE=,&DAY=,&WORK=
         LCLC  &DAYINRG
         LCLC  &WRKINRG
         PUSH  USING
         USING WK&SYSNDX,14
         AIF   ('&WORK'(1,1) EQ '(').WRKREG
         LA    14,&WORK           ADDRESS OF WORK AREA
         AGO   .GOTWRK
.WRKREG  ANOP
&WRKINRG SETC  '&WORK'(2,K'&WORK-2)
         LR    14,&WRKINRG
.GOTWRK  ANOP
         AIF   ('&JDATE'(1,1) EQ '(').REGNOT
         TM    &JDATE,X'F0'           IS IT CHARACTER FORMAT ?
         BO    AAA&SYSNDX             YES - PACK IT
         ZAP   WKD&SYSNDX.A,&JDATE    NO  - MOVE IT TO OUR WORKAREA
         B     BBB&SYSNDX
AAA&SYSNDX EQU   *
         PACK  WKD&SYSNDX.A,&JDATE      SAVE PACKED JULIAN DATE.
BBB&SYSNDX EQU   *
         AGO   .FROMSYS
.REGNOT  ANOP
&DAYINRG SETC  '&JDATE'(2,K'&JDATE-2)
         PACK  WKD&SYSNDX.A,0(5,&DAYINRG)     SAVE PACKED JULIAN DATE.
.FROMSYS ANOP
         MVC   WKD&SYSNDX.C,WKC&SYSNDX.C     MOVE CONSTANT
         MVO   WKD&SYSNDX.B(2),WKD&SYSNDX.A+1(1)  SAVE YEAR
         CVB   1,WKD&SYSNDX.C     R1 = YEAR FROM JULIAN DATE.
         LR    0,1                R0 = YEAR FROM JULIAN DATE.
         BCTR  0,0                R0 = YEAR -1.
         SRA   0,2     DIVIDE YEAR - 1 BY FOUR  (= # LEAP YEARS).
         AR    1,0     ADD RESULT TO # OF YEARS (= # DAY INCREMENTS).
         MVC   WKD&SYSNDX.B(2),WKD&SYSNDX.A+2     MOVE PACKED JULIAN DA
         CVB   0,WKD&SYSNDX.C     CONVERT JULIAN DAY FOR COMPUTATION.
         BCTR  0,0     MAKE JULIAN DAY RELATIVE TO ZERO.
         AR    1,0     ADD # DAY INCREMENTS AND # DAYS.
         SR    0,0     PREPARE R0--R1 FOR DIVISION.
         D     0,=F'+7'     COMPUTE MOD 7 EQUIVALENCE OF SUM.
         MH    0,=H'11'     COMPUTE DISPLACEMENT INTO DAY TABLE.
         LR    1,0      R1 NECESSARY FOR BASE REG OF MOVE.
         LA    1,WKC&SYSNDX.E(1)     R1 = PTR INTO DAY TABLE.
         AIF   ('&DAY'(1,1) EQ '(').DAYREG
         MVC   &DAY,0(1)        RETURN WEEKDAY TO USER.
         AGO   .DONE
.DAYREG  ANOP
&DAYINRG SETC  '&DAY'(2,K'&DAY-2)
         MVC   0(9,&DAYINRG),0(1)     RETURN WEEKDAY TO USER.
         AGO   .DONE
.DONE    ANOP
         POP   USING
         B     WKD&SYSNDX.Z
WK&SYSNDX DSECT
WKD&SYSNDX.C   DS    XL8
WKD&SYSNDX.B   EQU   *-2
WKD&SYSNDX.A   DS    F
&SYSECT  CSECT
WKC&SYSNDX.C   DC    XL8'000000000000000F'  *  LO-ORDER POS SIGN.
WKC&SYSNDX.E   EQU   *
         DC    C'MONDAY   ',X'01',X'05'
         DC    C'TUESDAY  ',X'02',X'04'
         DC    C'WEDNESDAY',X'03',X'03'
         DC    C'THURSDAY ',X'04',X'02'
         DC    C'FRIDAY   ',X'05',X'01'
         DC    C'SATURDAY ',X'06',X'07'
         DC    C'SUNDAY   ',X'07',X'06'
*
         DS    0H
WKD&SYSNDX.Z   EQU   *
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
