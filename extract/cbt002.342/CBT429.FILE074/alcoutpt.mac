         MACRO
&NAME    ALCOUTPT &OUTPUT=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'OUTPUT' KEYWORD
.*
.*       OUTPUT='DOCPRT'        OUTPUT STATEMENT NAME
.*       OUTPUT=NAME            NAME  DC  CL8'DOCPRT'
.*
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT,&LL
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        OUTPUT NAME
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         MVC   S99TUKEY(2),=X'8002' KEY FOR OUTPUT
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'26'    SET DEFAULT TO 26
         AIF   ('&OUTPUT'(1,1) EQ '''').AAA
         CLI   &OUTPUT,C' '       ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         MVI   S99TUPAR,C' '
         MVC   S99TUPAR+1(25),S99TUPAR
&LL      SETC  'L''&OUTPUT'
         MVC   S99TUPAR(&LL),&OUTPUT NAME
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&OUTPUT'(2,K'&OUTPUT-2)
         MVC   S99TUPAR(26),=CL26'&LIT'
.BBB     ANOP
         XC    SPFTRT,SPFTRT      CLEAR
         MVI   SPFTRT+C' ',X'FF'  SET FOR A BLANK
         TRT   S99TUPAR(26),SPFTRT   FIND END
         BNZ   MAAA&X             FOUND THE END
         LA    R1,26              SET TO 26, ALL BYTES USED
         B     MBBB&X
MAAA&X   EQU   *
         LA    R2,S99TUPAR        START
         SR    R1,R2              LENGTH OF DATA
         STCM  R1,3,S99TULNG      SET THE LENGTH CODE
MBBB&X   EQU   *
         LA    R14,6(R1,R14)      BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
