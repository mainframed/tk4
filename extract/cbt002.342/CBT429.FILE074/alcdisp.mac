         MACRO
&NAME    ALCDISP &DISP=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'DISP' KEYWORD
.*
.*       DISP='(NEW,CATLG,DELETE)'    ANY VALID DISPOSITION
.*       DISP=NAME                    NAME  DC  CL20'SHR'
.*
.*---------------------------------------------------------------------
         GBLA  &SPF1,&SPF2
         GBLC  &SPF3
         LCLA  &X,&LL,&D
         LCLC  &LIT,&LA
&X       SETA  &SYSNDX
&NAME    DS    0H
         AIF   ('&SPF2' NE '0').GBSET
&SPF2    SETA  &SYSNDX
.GBSET   ANOP
         AIF   ('&DISP'(1,1) NE '''').ZZZ
*----------------------------------------------------------------------
*        DISP
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSTATS        KEY FOR DSN STATUS
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
&D       SETA  2
&LL      SETA  &D
         AIF   ('&DISP'(2,1) NE '(').AAA
&D       SETA  &D+1
&LL      SETA  &D
.AAA     ANOP
         AIF   ('&DISP'(&D,1) EQ ',').BBB
         AIF   ('&DISP'(&D,1) EQ ')').BBB
         AIF   ('&DISP'(&D,1) EQ '''').BBB
&D       SETA  &D+1
         AGO   .AAA
.BBB     ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
         AIF   ('&DISP'(&D,&LL) EQ 'NEW').CCC
         AIF   ('&DISP'(&D,&LL) EQ 'MOD').DDD
         AIF   ('&DISP'(&D,&LL) EQ 'SHR').EEE
         AIF   ('&DISP'(&D,&LL) EQ 'OLD').FFF
         MNOTE 8,'INVALID DISPOSITION FIELD'
         MEXIT
.CCC     ANOP
         MVI   S99TUPAR,X'04'     DISP=(NEW,
         AGO   .GGG
.DDD     ANOP
         MVI   S99TUPAR,X'02'     DISP=(MOD,
         AGO   .GGG
.EEE     ANOP
         MVI   S99TUPAR,X'08'     DISP=(SHR,
         AGO   .GGG
.FFF     ANOP
         MVI   S99TUPAR,X'01'     DISP=(OLD,
.GGG     ANOP
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
&D       SETA  &D+&LL
&LL      SETA  &D
         AIF   ('&DISP'(&D,1) EQ ')').DONE
         AIF   ('&DISP'(&D,1) EQ '''').DONE
&D       SETA  &D+1
&LL      SETA  &D
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALNDISP        KEY FOR DSN NORMAL DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
.HHH     ANOP
         AIF   ('&DISP'(&D,1) EQ ',').III
         AIF   ('&DISP'(&D,1) EQ ')').III
         AIF   ('&DISP'(&D,1) EQ '''').III
&D       SETA  &D+1
         AGO   .HHH
.III     ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
         AIF   ('&DISP'(&D,&LL) EQ 'CATLG').JJJ
         AIF   ('&DISP'(&D,&LL) EQ 'UNCATLG').KKK
         AIF   ('&DISP'(&D,&LL) EQ 'DELETE').LLL
         AIF   ('&DISP'(&D,&LL) EQ 'KEEP').MMM
         MNOTE 8,'INVALID DISPOSITION FIELD'
.JJJ     ANOP
         MVI   S99TUPAR,X'02'     CATLG,
         AGO   .NNN
.KKK     ANOP
         MVI   S99TUPAR,X'01'     UNCATLG,
         AGO   .NNN
.LLL     ANOP
         MVI   S99TUPAR,X'04'     DELETE,
         AGO   .NNN
.MMM     ANOP
         MVI   S99TUPAR,X'08'     KEEP,
.NNN     ANOP
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
&D       SETA  &D+&LL
&LL      SETA  &D
         AIF   ('&DISP'(&D,1) EQ ')').DONE
         AIF   ('&DISP'(&D,1) EQ '''').DONE
&D       SETA  &D+1
&LL      SETA  &D
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALCDISP        KEY FOR DSN CONDITIONAL DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
.OOO     ANOP
         AIF   ('&DISP'(&D,1) EQ ',').PPP
         AIF   ('&DISP'(&D,1) EQ ')').PPP
         AIF   ('&DISP'(&D,1) EQ '''').PPP
&D       SETA  &D+1
         AGO   .OOO
.PPP     ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
         AIF   ('&DISP'(&D,&LL) EQ 'CATLG').QQQ
         AIF   ('&DISP'(&D,&LL) EQ 'UNCATLG').RRR
         AIF   ('&DISP'(&D,&LL) EQ 'DELETE').SSS
         AIF   ('&DISP'(&D,&LL) EQ 'KEEP').TTT
         MNOTE 8,'INVALID DISPOSITION FIELD'
.QQQ     ANOP
         MVI   S99TUPAR,X'02'     CATLG,
         AGO   .YYY
.RRR     ANOP
         MVI   S99TUPAR,X'01'     UNCATLG,
         AGO   .YYY
.SSS     ANOP
         MVI   S99TUPAR,X'04'     DELETE,
         AGO   .YYY
.TTT     ANOP
         MVI   S99TUPAR,X'08'     KEEP,
.YYY     ANOP
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         AGO   .DONE
.ZZZ     ANOP
*----------------------------------------------------------------------
*        DISP
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSTATS        KEY FOR DSN STATUS
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         CLI   &DISP,C' '         ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         XC    SPFTRT,SPFTRT      CLEAR TRT TABLE
         MVI   SPFTRT+C' ',X'FF'  BLANK
         MVI   SPFTRT+C',',X'FF'  COMMA
         MVI   SPFTRT+C')',X'FF'  PAREN
         TRT   &DISP,SPFTRT       FIND END
         BNZ   MAAA&X             FOUND IT
&LA      SETC  'L''&DISP'
         LA    R1,&DISP+&LA       SET TO END OF FIELD
MAAA&X   EQU   *
         LA    R3,&DISP
         CLI   0(R3),C'('         IN PARENS ?
         BNE   MBBB&X             NO - OK
         LA    R3,1(R3)           YES - SKIP PAREN
MBBB&X   EQU   *
         SR    R1,R3              LENGTH OF DATA
         CLC   0(3,R3),=C'NEW'
         BE    MCCC&X             DISP=NEW
         CLC   0(3,R3),=C'MOD'
         BE    MDDD&X             DISP=MOD
         CLC   0(3,R3),=C'SHR'
         BE    MEEE&X             DISP=SHR
         CLC   0(3,R3),=C'OLD'
         BE    MFFF&X             DISP=OLD
         BAL   R14,ERR1&SPF2
         B     MZZZ&SPF1
MCCC&X   EQU   *
         MVI   S99TUPAR,X'04'     DISP=(NEW,
         B     MGGG&X
MDDD&X   EQU   *
         MVI   S99TUPAR,X'02'     DISP=(MOD,
         B     MGGG&X
MEEE&X   EQU   *
         MVI   S99TUPAR,X'08'     DISP=(SHR,
         B     MGGG&X
MFFF&X   EQU   *
         MVI   S99TUPAR,X'01'     DISP=(OLD,
MGGG&X   EQU   *
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         LA    R3,1(R1,R3)        BUMP DISP FIELD
         LA    R1,&DISP+&LA       SET TO END OF FIELD
         CR    R3,R1              END OF FIELD ?
         BNL   MZZZ&X             YES - DONE
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALNDISP        KEY FOR DSN NORMAL DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
MHHH&X   EQU   *
         TRT   0(8,R3),SPFTRT     FIND END OF NEXT PARM
         BNZ   MIII&X             FOUND IT
&LA      SETC  'L''&DISP'
         LA    R1,&DISP+&LA       SET TO END OF FIELD
MIII&X   EQU   *
         SR    R1,R3              LENGTH OF DATA
         BM    MZZZ&X             NO DATA - DONE
         CLC   0(5,R3),=C'CATLG'
         BE    MJJJ&X             CATALOG
         CLC   0(7,R3),=C'UNCATLG'
         BE    MKKK&X             UNCATLG
         CLC   0(6,R3),=C'DELETE'
         BE    MLLL&X             DELETE
         CLC   0(4,R3),=C'KEEP'
         BE    MMMM&X             KEEP
         B     MZZZ&X             DONE
MJJJ&X   EQU   *
         MVI   S99TUPAR,X'02'     CATLG,
         B     MNNN&X
MKKK&X   EQU   *
         MVI   S99TUPAR,X'01'     UNCATLG,
         B     MNNN&X
MLLL&X   EQU   *
         MVI   S99TUPAR,X'04'     DELETE,
         B     MNNN&X
MMMM&X   EQU   *
         MVI   S99TUPAR,X'08'     KEEP,
MNNN&X   EQU   *
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         LA    R3,1(R1,R3)        BUMP DISP FIELD
         LA    R1,&DISP+&LA       SET TO END OF FIELD
         CR    R3,R1              END OF FIELD ?
         BNL   MZZZ&X             YES - DONE
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALCDISP        KEY FOR DSN CONDITIONAL DISP
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         B     MHHH&X
MZZZ&X   EQU   *
.DONE    ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
