         MACRO
&NAME    ALCBUFNO &BUFNO=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'BUFNO' KEYWORD
.*    @MK                  @MK                         @MK
.*       BUFNO='NNN'          NUMERIC VALUE - MAX 255
.*       BUFNO=NAME           NAME  DC  FL1'NNN'  (1-BYTE)
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        BUFNO
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALBUFNO        KEY FOR BUFNO
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         AIF   ('&BUFNO'(1,1) EQ '''').AAA
         CLI   &BUFNO,X'00'       ZERO ?
         BE    MZZZ&X             YES - SKIP IT
         MVC   S99TUPAR(1),&BUFNO BUFNO
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&BUFNO'(2,K'&BUFNO-2)
         MVC   S99TUPAR(1),=AL1(&LIT)   BUFNO
.BBB     ANOP
         LA    R14,8(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
