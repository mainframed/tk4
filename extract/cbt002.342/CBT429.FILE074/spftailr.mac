         MACRO
&NAME    SPFTAILR &PROC,&SUBMIT,&JOBCARD=
.*---------------------------------------------------------------------
.*       THIS MACRO WILL INVOKE SPF FILE TAILORING SERVICES AND
.*       OPTIONALLY SUBMIT THE JOB. IT WILL ALSO UPDATE THE JOBNAME
.*       SUFFIX IF REQUESTED.
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
         AIF   (T'&PROC NE 'O').AAA
         MNOTE 8,'SPF SKELETON NOT SPECIFIED'
         MEXIT
.AAA     ANOP
         AIF   ('&PROC'(1,1) EQ '''').BBB
         MVC   PROC,&PROC           MOVE SKELETON NAME
         AGO   .CCC
.BBB     ANOP
&LIT     SETC  '&PROC'(2,K'&PROC-2)
         MVC   PROC,=CL8'&LIT'
.CCC     ANOP
         XC    SPFTRT,SPFTRT        CLEAR
         MVI   SPFTRT+C' ',X'FF'
         TRT   ZPREFIX,SPFTRT       FIND END OF USER-PREFIX
         BNZ   MAAA&X
         LA    R1,ZPREFIX+8
MAAA&X   EQU   *
         LA    R14,ZPREFIX         START OF USER-PREFIX
         SR    R1,R14              LENGTH OF USER-PREFIX
         LA    R14,ZTEMPF          START OF ZTEMPF
         SH    R1,=H'1'            DECR FOR EX
         EX    R1,CLC1&X           COMPARE USER-PREFIX TO ZTEMPF
         BE    MEEE&X              ZTEMPF IS A REAL NAME
         TRT   ZUSER,SPFTRT        FIND END OF USER-ID
         BNZ   MBBB&X
         LA    R1,ZUSER+8
MBBB&X   EQU   *
         LA    R15,ZUSER           START OF USER-ID
         SR    R1,R15              LENGTH OF USER-ID
         SH    R1,=H'1'            LENGTH OF USER-ID
LPA&X    EQU   *
         LA    R14,1(R14)          BUMP
         EX    R1,CLC2&X           FIND USER-ID IMBEDDED IN ZTEMPF
         BE    MCCC&X              FOUND IT
         B     LPA&X
MCCC&X   EQU   *
         MVC   SUBD&X,BLANKS       CLEAR
         MVC   SUBD&X.(18),0(R14)   MOVE USER-ID + ANOTHER QUALIFIER
         LA    R14,SUBD&X           START OF NAME
         LA    R14,2(R1,R14)        BUMP PAST USER-ID AND DOT
         MVC   0(7,R14),=C'SPFTEMP'  ADD THIS QUALIFIER
         TRT   SUBD&X,SPFTRT        FIND END OF DSN
         MVC   0(6,R1),=C'.TEMP1'   ADD ANOTHER QUALIFIER
         SPFALLOC A,DD='ISPFILE',DSN=SUBD&X,DISP='SHR',                +
               LRECL='80',BLKSIZE='6160',RECFM='FB'
         CLC   SPFDYNER,=4C'0'
         BE    MDDD&X
         SPFALLOC A,DD='ISPFILE',DSN=SUBD&X,DISP='(NEW,CATLG)',        +
               LRECL='80',BLKSIZE='6160',RECFM='FB',                   +
               SPACE='(6160,(10,10),RLSE)'
MDDD&X   EQU   *
         CALL  ISPLINK,(FTOPEN),VL
         B     MFFF&X
MEEE&X   EQU   *
         MVC   SUBD&X,ZTEMPF
         CALL  ISPLINK,(FTOPEN,TEMP),VL
MFFF&X   EQU   *
         CALL  ISPLINK,(FTINCL,PROC),VL
         ST    R15,SPFRC
         CALL  ISPLINK,(FTCLOSE),VL
         CLC   SUBD&X,ZTEMPF             SAME DSN ?
         BE    MGGG&X                    YES - SKIP UN-ALLOCATE
         SPFALLOC U,DD='ISPFILE'
MGGG&X   EQU   *
         L     R15,SPFRC
         LTR   R15,R15
         AIF   (T'&SUBMIT EQ 'O').DONE
         BNZ   MZZZ&X
         SPFSUB SUBD&X
         AIF   (T'&JOBCARD EQ 'O').DONE
         SPFJOBNM &JOBCARD
.DONE    ANOP
         B     MZZZ&X
CLC1&X   CLC   0(0,R14),ZPREFIX    COMPARE
CLC2&X   CLC   0(0,R14),ZUSER      COMPARE
SUBD&X   DC    CL44' '
MZZZ&X   EQU   *
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
