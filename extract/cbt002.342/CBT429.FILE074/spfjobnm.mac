         MACRO
&NAME    SPFJOBNM &CRD
.*---------------------------------------------------------------------
.*       THIS MACRO WILL BUMP THE JOBNAME SUFFIX BY 1, IF THE JOBNAME
.*       MATCHES THE USER-ID
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
&X       SETA  &SYSNDX
*----------------------------------------------------------------------
*        UPDATE THE JOB CARD
*----------------------------------------------------------------------
         LA    R1,ZUSER           ADDR OF USER-ID
         LA    R14,7              MAX LENGTH
LPA&X    EQU   *
         CLI   0(R1),C' '         END ?
         BE    MAAA&X             YES - OK
         LA    R1,1(R1)           BUMP
         BCT   R14,LPA&X
MAAA&X   EQU   *
         LA    R15,6              MAX-1
         SR    R15,R14            LENGTH OF ID-1
         EX    R15,CLCI&X         JOBNAME = TO USER-ID
         BNE   MFFF&X             NO - OK
         LA    R1,&CRD+2          JOBCARD
         LA    R1,1(R1,R15)       BUMP TO LAST CHARACTER
         CLI   0(R1),C' '         IS IT BLANK ?
         BE    MFFF&X             YES - SKIP IT
         CLI   0(R1),C'9'         IS IT 9 ?
         BNE   MBBB&X             NO - OK
         MVI   0(R1),C'0'         YES - SET TO ZERO
         B     MFFF&X
MBBB&X   EQU   *
         CLI   0(R1),C'I'         IS IT I ?
         BNE   MCCC&X             NO - OK
         MVI   0(R1),C'J'         YES - SET TO J
         B     MFFF&X
MCCC&X   EQU   *
         CLI   0(R1),C'R'         IS IT R ?
         BNE   MDDD&X             NO - OK
         MVI   0(R1),C'S'         YES - SET TO S
         B     MFFF&X
MDDD&X   EQU   *
         CLI   0(R1),C'Z'         IS IT Z ?
         BNE   MEEE&X             NO - OK
         MVI   0(R1),C'A'         YES - SET TO A
         B     MFFF&X
MEEE&X   EQU   *
         XR    R14,R14
         IC    R14,0(R1)          PICK UP CHARACTER
         LA    R14,1(R14)         ADD 1
         STC   R14,0(R1)          REPLACE IT
         B     MFFF&X
CLCI&X   CLC   &CRD+2(0),ZUSER
MFFF&X   EQU   *
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
