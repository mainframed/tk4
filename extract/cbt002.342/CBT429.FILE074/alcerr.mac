         MACRO
&NAME    ALCERR &FUNC,&DD=,&DSN=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       RESULTS OF SVC 99 AND FILL IN THE SMSG AND LMSG FIELDS.
.*
.*---------------------------------------------------------------------
         GBLA  &SPF2
         GBLC  &SPF3
         LCLA  &X
         LCLC  &LITDD,&LITDSN,&LA,&LB                       @MK
&X       SETA  &SYSNDX
         AIF   ('&SPF2' NE '0').GBSET
&SPF2    SETA  &SYSNDX
.GBSET   ANOP
*----------------------------------------------------------------------
*        DYNAMIC ALLOCATION ERROR MESSAGES
*----------------------------------------------------------------------
         MVC   LMSG,BLANKS
         MVC   SMSG,BLANKS
.*
         AIF   (T'&DD EQ 'O' AND T'&DSN EQ 'O').GGG
&LA      SETC  '5'
         MVI   LMSG,C'&FUNC'
         AIF   (T'&DD EQ 'O').DDD
         MVC   LMSG+2(3),=C'DD='
         AIF   ('&DD'(1,1) NE '(').AAA
         MVC   LMSG+&LA.(8),0&DD
         AGO   .CCC
.AAA     ANOP
         AIF   ('&DD'(1,1) NE '''').BBB
&LITDD   SETC  '&DD'(2,K'&DD-2)
         MVC   LMSG+&LA.(8),=CL8'&LITDD'
         AGO   .CCC
.BBB     ANOP
         MVC   LMSG+&LA.(8),&DD
.CCC     ANOP
&LA      SETC  '18'
.DDD     ANOP
         AIF   (T'&DSN EQ 'O').GGG
         MVC   LMSG+14(4),=C'DSN='
         AIF   ('&DSN'(1,1) NE '(').EEE
         MVC   LMSG+&LA.(44),0&DSN
         AGO   .GGG
.EEE     ANOP
         AIF   ('&DSN'(1,1) NE '''').FFF
&LITDSN  SETC  '&DSN'(2,K'&DSN-2)
         MVC    LMSG+&LA.(44),=CL44'&LITDSN'
         AGO   .GGG
.FFF     ANOP
&LB      SETC  'L''&DSN'
         MVC   LMSG+&LA.(&LB),&DSN
.GGG     ANOP
         BAL   R14,SPF2&SPF2
         AIF   ('&SPF3' EQ 'X').DONE
&SPF3    SETC  'X'
         B     M999&X
SPF2&SPF2 DS  0H
         LTR   R15,R15            ALLOCATION SUCCESSFUL ?
         BZ    MZZZ&X             YES - OK
         CVD   R15,SPFDBWRD
         UNPK  SPFDYN15,SPFDBWRD+6(2)
         OI    SPFDYN15+3,X'F0'
*
         UNPK  SPFDBWRD(5),S99ERROR(3) 1 MORE THAN FIELD SIZE
         TR    SPFDBWRD(4),TR&X-240 TRANSLATE
         MVC   SPFDYNER,SPFDBWRD   MOVE TO ERROR SLOT
*
         UNPK  SPFDBWRD(5),S99INFO(3)
         TR    SPFDBWRD(4),TR&X-240 TRANSLATE
         MVC   SPFDYNIN,SPFDBWRD
         CLC   SPFDYNER,=C'0210'    DATA SET IN USE ?
         BNE   MBBB&X             NO
         MVC   SMSG(15),=C'DATA SET IN USE'
         B     MZZZ&X
MBBB&X   EQU   *
         CLC   SPFDYNER,=C'031C'    INVALID MEMBER NAME ?
         BNE   MCCC&X             NO
         MVC   SMSG(19),=C'INVALID MEMBER NAME'
         B     MZZZ&X
MCCC&X   EQU   *
         CLC   SPFDYNER,=C'0320'    INVALID DSNAME ?
         BNE   MDDD&X             NO
         MVC   SMSG(21),=C'INVALID DATA SET NAME'
         B     MZZZ&X
MDDD&X   EQU   *
         CLC   SPFDYNER,=C'0330'    MBR SPECIFIED, DSN NOT A PDS
         BNE   MEEE&X             NO
         MVC   SMSG(17),=C'THIS IS NOT A PDS'
         B     MZZZ&X
MEEE&X   EQU   *
         CLC   SPFDYNER,=C'0440'    DATA SET NOT FOUND
         BNE   MFFF&X             NO
         MVC   SMSG(18),=C'DATA SET NOT FOUND'
         B     MZZZ&X
MFFF&X   EQU   *
         CLC   SPFDYNER,=C'1708'    DATA SET NOT CATALOGED
         BNE   MGGG&X             NO
         MVC   SMSG(22),=C'DATA SET NOT CATALOGED'
         B     MZZZ&X
MGGG&X   EQU   *
         CLC   SPFDYNER,=C'0410'    DDNAME UNAVAILABLE
         BNE   MHHH&X             NO
         MVC   SMSG(18),=C'DDNAME UNAVAILABLE'
         B     MZZZ&X
MHHH&X   EQU   *
MYYY&X   EQU   *
         MVC   SMSG(16),=C'ALLOCATION ERROR'
         MVC   SMSG+17(4),SPFDYNER
MZZZ&X   EQU   *
         BR    R14
TR&X     DC    C'0123456789ABCDEF'
ERR1&SPF2 DS    0H
         LA    R15,32
         MVC   SPFDYN15,=C'0032'
         MVC   SPFDYNER,=C'9999'
         MVC   SPFDYNIN,=C'9999'
         BR    R14
M999&X   DS    0H
.DONE    ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
