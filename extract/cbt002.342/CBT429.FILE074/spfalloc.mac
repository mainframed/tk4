         MACRO
&NAME    SPFALLOC &FUNC,&DD=,&DSN=,&MBR=,&DISP=,&SPACE=,&VOL=,&UNIT=,  +
               &RECFM=,&BLKSIZE=,&LRECL=,&SYSOUT=,&COPIES=,&FCB=,      +
               &DEST=,&FREE=,&TERM=,&DUMMY=,&RETDD=,&MODEL=,&BUFNO=,   +
               &CCDD=,&RLSE=,&OUTPUT=,&RETDSN=
         GBLA  &SPF1
         LCLA  &X
         LCLC  &REGD
&X       SETA  &SYSNDX
&SPF1    SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*   R2 - TRT
*   R3 - FIELD POINTER
*   R4 - DYNAM REQUEST BLOCK
*   R5 - RETURNED DD NAME
*   R14 - DYNAMIC TEXT UNITS
*   R15 - DYNAMIC TEXT POINTERS
*----------------------------------------------------------------------
         PUSH  USING
         STM   R2,R5,SPFREGS      SAVE REGISTERS
         AIF   (T'&DSN EQ 'O').REG1
         AIF   ('&DSN'(1,1) NE '(').REG1
&REGD    SETC  '&DSN'(2,K'&DSN-2)
         ST    &REGD,SPFREGS+16
.REG1    ANOP
         AIF   (T'&MBR EQ 'O').REG2
         AIF   ('&MBR'(1,1) NE '(').REG2
&REGM    SETC  '&MBR'(2,K'&MBR-2)
         ST    &REGM,SPFREGS+20
.REG2    ANOP
         LA    R0,SPFDYNAM        ADDR OF WORKAREA
         LA    R1,SPFDYNLL        LENGTH OF WORKAREA
         LA    R14,*              DUMMY ADDR
         LA    R15,0              LENGTH AND PAD CHARACTER
         MVCL  R0,R14             CLEAR WORKAREA
         LA    R15,SPFDYNAM       ADDR OF DYN WORKAREA
         USING S99RBP,R15         REQ BLK POINTER DSECT
         LA    R4,S99RBP+4        ADDRESSABILITY OF RB DSECT
         USING S99RB,R4           RB DSECT
         ST    R4,S99RBPTR        MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND  TURN ON HOB IN RBPTR
         MVI   S99RBLN,RBLEN      PUT LEN IN ITS LENGTH FIELD
         LA    R15,S99RB+RBLEN    POINT BEYOND RB (1ST TEXT PTR)
         USING S99TUPL,R15        ADDRESSABILITY OF TEXT UNIT POINTERS
         ST    R15,S99TXTPP       INIT TEXT PTR IN RB
         LA    R14,S99TUPL+128    POINT BEYOND LAST TXT PTR (1ST TXT
         USING S99TUNIT,R14       ADDRESSABILITY OF TEXT UNITS
*----------------------------------------------------------------------
*        SET FUNCTION
*----------------------------------------------------------------------
         AIF   ('&FUNC' EQ 'A').AAA
         AIF   ('&FUNC' EQ 'U').BBB
         AIF   ('&FUNC' EQ 'C').CCC
         AIF   ('&FUNC' EQ 'D').DECON                           @MK
         AIF   ('&FUNC' EQ 'I').INFO                            @MK
         MNOTE 8,'INVALID FUNCTION CODE'
         MEXIT
.AAA     ANOP
         MVI   S99VERB,S99VRBAL   SET VERB CODE TO ALLOCATE
         AGO   .ALLOC
.BBB     ANOP
         MVI   S99VERB,S99VRBUN   SET VERB CODE TO UN-ALLOCATE
         ALCUNAL
         AGO   .ALLOC
.CCC     ANOP
         MVI   S99VERB,S99VRBCC   SET VERB CODE TO CONCATENATE
         AGO   .CONCAT
.DECON   ANOP                                                     @MK
         MVI   S99VERB,S99VRBDC   SET VERB CODE TO DE-CONCATENATE @MK
         AGO   .ALLOC                                             @MK
.INFO    ANOP                                                     @MK
         MVI   S99VERB,S99VRBIN   SET VERB CODE TO RETRIEVE INFORM@MK
         AGO   .ALLOC                                             @MK
.ALLOC   ANOP
         AIF   (T'&DD EQ 'O').NODD
         ALCDD DD=&DD
.NODD    ANOP
         AIF   (T'&DSN EQ 'O').NODSN
         ALCDSN DSN=&DSN
.NODSN   ANOP
         AIF   (T'&MBR EQ 'O').NOMBR
         ALCMBR MBR=&MBR
.NOMBR   ANOP
         AIF   (T'&DISP EQ 'O').NODISP
         AIF   ('&FUNC' EQ 'U').UDISP       @MK
         ALCDISP DISP=&DISP
         AGO   .NODISP                      @MK
.UDISP   ANOP                               @MK
         ALCUDISP DISP=&DISP                @MK
.NODISP  ANOP
         AIF   (T'&SPACE EQ 'O').NOSPACE
         ALCSPACE SPACE=&SPACE
.NOSPACE ANOP
         AIF   (T'&VOL EQ 'O').NOVOL
         ALCVOL VOL=&VOL
.NOVOL   ANOP
         AIF   (T'&UNIT EQ 'O').NOUNIT
         ALCUNIT UNIT=&UNIT
.NOUNIT  ANOP
         AIF   (T'&BLKSIZE EQ 'O').NOBLKSZ
         ALCBLKSZ BLKSIZE=&BLKSIZE
.NOBLKSZ ANOP
         AIF   (T'&BUFNO EQ 'O').NOBUFNO      @MK
         ALCBUFNO BUFNO=&BUFNO                @MK
.NOBUFNO ANOP                                 @MK
         AIF   (T'&LRECL EQ 'O').NOLRECL
         ALCLRECL LRECL=&LRECL
.NOLRECL ANOP
         AIF   (T'&RECFM EQ 'O').NORECFM
         ALCRECFM RECFM=&RECFM
.NORECFM ANOP
         AIF   (T'&SYSOUT EQ 'O').NOSOUT
         ALCSSOUT SYSOUT=&SYSOUT
.NOSOUT  ANOP
         AIF   (T'&DEST EQ 'O').NODEST
         ALCDEST DEST=&DEST
.NODEST  ANOP
         AIF   (T'&OUTPUT EQ 'O').NOOUTPT
         ALCOUTPT OUTPUT=&OUTPUT
.NOOUTPT ANOP
         AIF   (T'&COPIES EQ 'O').NOCOPY
         ALCCOPYS COPIES=&COPIES
.NOCOPY  ANOP
         AIF   (T'&FCB EQ 'O').NOFCB
         ALCFCB FCB=&FCB
.NOFCB   ANOP
         AIF   (T'&FREE EQ 'O').NOFREE
         ALCFREE FREE=&FREE
.NOFREE  ANOP
         AIF   (T'&TERM EQ 'O').NOTERM
         ALCTERM TERM=&TERM
.NOTERM  ANOP
         AIF   (T'&DUMMY EQ 'O').NODUMMY
         ALCDUMMY DUMMY=&DUMMY
.NODUMMY ANOP
         AIF   (T'&RETDD EQ 'O').NORETDD
         ALCRETDD RETDD=&RETDD
.NORETDD ANOP
         AIF   (T'&MODEL EQ 'O').NOMODEL
         ALCMODEL MODEL=&MODEL
.NOMODEL ANOP                                         @MK
         AIF   (T'&RETDSN EQ 'O').NORETDS             @MK
         ALCRETDS RETDSN=&RETDSN                      @MK
.NORETDS ANOP                                         @MK
         AIF   (T'&RLSE EQ 'O').NORLSE
         ALCRLSE RLSE=&RLSE
.NORLSE  ANOP
         AGO   .DDD
.CONCAT  ANOP
         AIF   (T'&CCDD EQ 'O').DDD
         ALCCC CCDD=&CCDD
         AGO   .DDD
.DDD     ANOP
         S     R15,=F'4'
         OI    S99TUPTR,X'80'     END OF LIST
         LA    R1,SPFDYNAM
         MVC   SPFDYN15,=4C'0'    INITIALIZE
         MVC   SPFDYNER,=4C'0'    INITIALIZE
         MVC   SPFDYNIN,=4C'0'    INITIALIZE
         SVC   99
         ALCERR &FUNC,DD=&DD,DSN=&DSN
.EEE     ANOP
         AIF   (T'&RETDD EQ 'O').GGG
         AIF   ('&RETDD'(1,1) EQ '(').FFF
         MVC   &RETDD.(8),0(R5)   MOVE RETURNED DDNAME
         AGO   .ZZZ
.FFF     ANOP
&REGD    SETC  '&RETDD'(2,K'&RETDD-2)
         MVC   0(8,&REGD),0(R5)
         AGO   .ZZZ
.GGG     ANOP                                              @MK
         AIF   (T'&RETDSN EQ 'O').ZZZ                      @MK
         AIF   ('&RETDSN'(1,1) EQ '(').HHH                 @MK
         MVC   &RETDSN.(44),0(R5)   MOVE RETURNED DSNAME   @MK
         AGO   .ZZZ                                        @MK
.HHH     ANOP                                              @MK
&REGD    SETC  '&RETDSN'(2,K'&RETDSN-2)                    @MK
         MVC   0(44,&REGD),0(R5)                           @MK
.ZZZ     ANOP
MZZZ&SPF1 EQU   *
         LM    R2,R5,SPFREGS
         POP   USING
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
