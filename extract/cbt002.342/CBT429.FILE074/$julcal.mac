         MACRO
&NAME    $JULCAL &JDATE,&CDATE
.*---------------------------------------------------------------------
.*       THIS MACRO CONVERTS A JULIAN DATE TO A CALENDAR DATE
.*
.*       JDATE=  YYDDD+         PACKED JULIAN DATE
.*       CDATE=  YY/MM/DD       8 BYTE FIELD
.*
.*       AT EXIT:
.*             R15 - 00 VALID DATE
.*             R15 - 04 INVALID DATE
.*
.*             R0  - 00 NON-LEAP YEAR
.*             R0  - 01 LEAP YEAR
.*
.*---------------------------------------------------------------------
         LCLA  &X
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        CONVERT JULIAN DATE TO CALENDAR
*----------------------------------------------------------------------
         USING MTHD&X,R1
         XR    R15,R15            INITIALIZE RETURN CODE
         XR    R0,R0              INITIALIZE LEAPYEAR INDICATOR
         OI    &JDATE+2,X'0F'     MAKE SURE IT'S AN 'F'
         MVC   WKYY&X,&JDATE      MOVE THE YEAR FOR LEAPYEAR TEST
         UNPK  WRK3&X,&JDATE.(2)  UNPACK THE YEAR AND ANOTHER BYTE
         MVC   &CDATE.(2),WRK3&X  MOVE THE YEAR - CHARACTER FORMAT
         LA    R1,NOLP&X          ADDR OF NOT LEAPYEAR TABLE
         TM    WKYY&X,X'01'       ODD YEAR ?
         BO    MAAA&X             YES - NOT A LEAP YEAR
         TM    WKYY&X,X'12'       TEST BITS 3 AND 6
         BM    MAAA&X             IF MIXED, NOT A LEAP YEAR
         LA    R1,LPYR&X          ADDR OF LEAPYEAR TABLE
         LA    R0,1               SET LEAP YEAR INDICATOR
MAAA&X   EQU   *
         ZAP   WRK2&X,&JDATE+1(2) MOVE DDD TO WORK AREA
MBBB&X   EQU   *
         CP    MTD&X,WRK2&X       FIND THIS MONTH IN TABLE
         BNL   MCCC&X             FOUND IT
         LA    R1,MTL&X.(R1)      BUMP TO NEXT SLOT IN TABLE
         CLI   MTD&X,X'FF'        END OF TABLE ?
         BNE   MBBB&X             NO - KEEP LOOKING
         LA    R15,4              YES - SET RETURN CODE
         B     MZZZ&X             KEEP LOOKING
MCCC&X   EQU   *
         MVC   &CDATE+3(2),MTM&X MOVE MONTH - CHARACTER
         S     R1,=A(MTL&X)       BACKUP TO LAST MONTH
         SP    WRK2&X,MTD&X       SUBTRACT LAST DAY OF LAST MONTH
         UNPK  WRK3&X,WRK2&X      UNPACK - DAY OF THIS MONTH
         OI    WRK3&X+2,X'F0'     CHANGE SIGN
         MVC   &CDATE+6(2),WRK3&X+1 MOVE DAY
         MVI   &CDATE+2,C'/'
         MVI   &CDATE+5,C'/'
         B     MZZZ&X
*----------------------------------------------------------------------
         DC    PL2'000',CL2'00'
NOLP&X   DC    PL2'031',CL2'01'
         DC    PL2'059',CL2'02'
         DC    PL2'090',CL2'03'
         DC    PL2'120',CL2'04'
         DC    PL2'151',CL2'05'
         DC    PL2'181',CL2'06'
         DC    PL2'212',CL2'07'
         DC    PL2'243',CL2'08'
         DC    PL2'273',CL2'09'
         DC    PL2'304',CL2'10'
         DC    PL2'334',CL2'11'
         DC    PL2'365',CL2'12'
         DC    X'FFFF'
*----------------------------------------------------------------------
         DC    PL2'000',CL2'00'
LPYR&X   DC    PL2'031',CL2'01'
         DC    PL2'060',CL2'02'
         DC    PL2'091',CL2'03'
         DC    PL2'121',CL2'04'
         DC    PL2'152',CL2'05'
         DC    PL2'182',CL2'06'
         DC    PL2'213',CL2'07'
         DC    PL2'244',CL2'08'
         DC    PL2'274',CL2'09'
         DC    PL2'305',CL2'10'
         DC    PL2'335',CL2'11'
         DC    PL2'366',CL2'12'
         DC    X'FFFF'
*----------------------------------------------------------------------
MTHD&X   DSECT
MTD&X    DS    PL2
MTM&X    DS    CL2
MTE&X    EQU   *
MTL&X    EQU   MTE&X-MTD&X
&SYSECT  CSECT
*----------------------------------------------------------------------
WRK2&X   DC    CL2' '
WRK3&X   DC    CL3' '
WKYY&X   DC    CL1' '
*----------------------------------------------------------------------
MZZZ&X   DS    0H
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
