         MACRO
&NAME    SPFLOC &TDSN
&NAME    DS    0H
         GBLA  &Y
         LCLC  &REGD
         LCLA  &X
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
.*       LOCATE THE CATALOG ENTRY FOR THE DATA SET
.*---------------------------------------------------------------------
&REGD    SETC  'R2'
         ST    R2,SPFREGS
         AIF   ('&TDSN'(1,1) EQ '(').AAA
         LA    &REGD,&TDSN             ADDR OF TARGET FIELD
         AGO    .BBB
.AAA     ANOP
&REGD    SETC  '&TDSN'(2,K'&TDSN-2)
         MVC   DSN&X,0(&REGD)          MOVE DSN
.BBB     ANOP
         CLI   0(&REGD),C' '           DO WE HAVE A DSN ?
         BNH   MCCC&X                  NO - SKIP OBTAIN
         MVC   LMSG,BLANKS             CLEAR
         MVC   LMSG(4),=C'DSN='
         MVC   LMSG+4(44),0(&REGD)     MOVE DSN
         LOCATE LOC&X                  LOCATE CATALOG ENTRY
         LTR   R15,R15                 CATALOGED ?
         BZ    MAAA&X                  YES - OK
         LA    R15,12                  NO - SET RC
         MVC   SMSG,BLANKS             CLEAR
         MVC   SMSG(22),=C'DATA SET NOT CATALOGED'
         B     MCCC&X
MAAA&X   EQU   *
         MVC   SPFDEVT,SPFDSCB+2       PICK UP DEVICE TYPE
         MVC   SPFVOL,SPFDSCB+6        PICK UP VOL FROM LOCATE
         CLC   SPFVOL,=CL6'MIGRAT'     HAS IT BEEN MIGRATED ?
         BNE   MCCC&X                  NO - OK
         LA    R15,8                   YES - SET RC
         MVC   SMSG,BLANKS             CLEAR
         MVC   SMSG(20),=C'DATA SET IS MIGRATED'
         B     MCCC&X
         AIF   ('&TDSN'(1,1) EQ '(').FFF
LOC&X    CAMLST  NAME,&TDSN,,SPFDSCB
         AGO   .GGG
.FFF     ANOP
LOC&X    CAMLST  NAME,DSN&X,,SPFDSCB
DSN&X    DC    CL44' '
.GGG     ANOP
MCCC&X   DS    0H
         L     R2,SPFREGS
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
