         MACRO
&NAME    SPFDS2 &PRJ,&LIB,&TYP,&MBR,&TDSN,&TMBR
&NAME    DS    0H
         GBLA  &Y
         LCLC  &REGD,&REGM,&LA
         LCLA  &X
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
.*       PROJECT LIBRARY TYPE MEMBER FIELDS
.*---------------------------------------------------------------------
         MVI   FLD&Y,X'00'             INDICATE PRJ LIB TYP USED
&REGD    SETC  'R15'
         AIF   ('&TDSN'(1,1) EQ '(').AAA
         LA    R15,&TDSN               TARGET FIELD
         AGO   .BBB
.AAA     ANOP
&REGD    SETC  '&TDSN'(2,K'&TDSN-2)
         LR    R15,&REGD               COPY ADDR
.BBB     ANOP
         LA    R14,&PRJ                FROM FIELD
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET TRT KEY
         MVI   SPFTRT,X'FF'            SET TRT KEY
&LA      SETC  'L''&PRJ'
         TRT   0(&LA,R14),SPFTRT       FIND END OF PROJECT
         BNZ   MAAA&X                  FOUND END
         LA    R1,&LA.(R14)            SET R1 TO END+1
MAAA&X   DS    0H
         SR    R1,R14                  LENGTH OF PROJECT
         BCTR  R1,R0                   DECR FOR 'EX'
         EX    R1,MVCD&X               MOVE TO TARGET FIELD
         LA    R15,1(R1,R15)           BUMP TARGET
         MVI   0(R15),C'.'             ADD DOT
         LA    R15,1(R15)              BUMP PAST DOT
         LA    R14,&LIB                FROM FIELD
&LA      SETC  'L''&LIB'
         TRT   0(&LA,R14),SPFTRT       FIND END OF LIB
         BNZ   MBBB&X                  FOUND END
         LA    R1,&LA.(R14)            SET R1 TO END+1
MBBB&X   DS    0H
         SR    R1,R14                  LENGTH OF LIB
         BCTR  R1,R0                   DECR FOR 'EX'
         EX    R1,MVCD&X               MOVE TO TARGET FIELD
         LA    R15,1(R1,R15)           BUMP TARGET
         MVI   0(R15),C'.'             ADD DOT
         LA    R15,1(R15)              BUMP PAST DOT
         LA    R14,&TYP                FROM FIELD
&LA      SETC  'L''&TYP'
         TRT   0(&LA,R14),SPFTRT       FIND END OF TYPE
         BNZ   MCCC&X                  FOUND END
         LA    R1,&LA.(R14)            SET R1 TO END+1
MCCC&X   DS    0H
         SR    R1,R14                  LENGTH OF TYPE
         BCTR  R1,R0                   DECR FOR 'EX'
         EX    R1,MVCD&X               MOVE TO TARGET FIELD
         AIF   (T'&MBR EQ 'O').ZZZ
         LA    R14,&MBR                FROM FIELD
&REGM    SETC  'R15'
         AIF   ('&TMBR'(1,1) EQ '(').CCC
         LA    R15,&TMBR               TARGET FIELD
         AGO   .DDD
.CCC     ANOP
&REGM    SETC  '&TMBR'(2,K'&TMBR-2)
         LR    R15,&REGM               TARGET FIELD
.DDD     ANOP
&LA      SETC  'L''&MBR'
         TRT   0(&LA,R14),SPFTRT       FIND END OF MBR
         BNZ   MDDD&X                  FOUND END
         LA    R1,&LA.(R14)            SET R1 TO END+1
MDDD&X   DS    0H
         SR    R1,R14                  LENGTH OF MBR
         BNP   MZZZ&X                  NO MEMBER NAME
         BCTR  R1,R0                   DECR FOR 'EX'
         EX    R1,MVCM&X               MOVE TO TARGET FIELD
.ZZZ     ANOP
         B     MZZZ&X
MVCD&X   MVC   0(0,R15),0(R14)
MVCM&X   MVC   0(0,R15),0(R14)
MZZZ&X   DS    0H
         LA    R15,0
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
