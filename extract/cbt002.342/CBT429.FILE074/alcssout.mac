         MACRO
&NAME    ALCSSOUT &SYSOUT=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'SYSOUT' KEYWORD
.*
.*       SYSOUT='(F,,973)'      ANY VALID SYSOUT PARAMETERS
.*       SYSOUT=NAME            NAME  DC  CL8'(A,INTRDR)'
.*
.*---------------------------------------------------------------------
         LCLA  &X,&LL,&D
         LCLC  &LIT,&LA
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        SYSOUT CLASS
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSYSOU        KEY FOR SYSOUT CLASS
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         AIF   ('&SYSOUT'(1,1) EQ '''').AAA010
         CLI   &SYSOUT,C' '       ANY SPECIFIED ?
         BNH   Z030&X             NO - SKIP IT
         LA    R3,&SYSOUT         START OF FIELD
         CLI   0(R3),C'('         IN PARENS ?
         BNE   A010&X             NO - OK
         LA    R3,1(R3)           YES - SKIP PAREN
A010&X   EQU   *
         CLI   0(R3),C'*'         SYSOUT=* ?
         BNE   B010&X             NO - OK
         MVC   S99TUNUM,=H'0'     MOVE 0 TO TXT UNIT NUMBR FLD
         LA    R14,4(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         B     C010&X
B010&X   EQU   *
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(1),0(R3)  SYSOUT CLASS
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
C010&X   EQU   *
         AGO   .ZZZ010
.AAA010  ANOP
&D       SETA  2
         AIF   ('&SYSOUT'(&D,1) NE '(').BBB010
&D       SETA  &D+1
.BBB010  ANOP
&LIT     SETC  '&SYSOUT'(&D,1)
&D       SETA  &D+1
         AIF   ('&LIT' NE '*').CCC010
         MVC   S99TUNUM,=H'0'     MOVE 0 TO TXT UNIT NUMBR FLD
         LA    R14,4(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
         AGO   .ZZZ010
.CCC010  ANOP
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'1'     MOVE 1 TO TXT UNIT LNGTH FLD
         MVI   S99TUPAR,C'&LIT'
         LA    R14,7(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
.ZZZ010  ANOP
         AIF   ('&SYSOUT'(1,1) EQ '''').AAA020
         AGO   .BBB020
.AAA020  ANOP
         AIF   ('&SYSOUT'(&D,1) EQ '''').ZZZ030
         AIF   (K'&SYSOUT LE &D).ZZZ030
.BBB020  ANOP
*----------------------------------------------------------------------
*        SYSOUT PROGRAM NAME
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSPGNM        KEY FOR SYSOUT PROGRAM NAME
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'8'     MOVE 8 TO TXT UNIT LNGTH FLD
         AIF   ('&SYSOUT'(1,1) EQ '''').CCC020
         LA    R3,1(R3)           BUMP PAST CLASS
         CLI   0(R3),C')'         END OF DATA ?
         BE    Z030&X
         CLI   0(R3),C' '         END OF DATA ?
         BE    Z030&X
         LA    R3,1(R3)           BUMP PAST COMMA
         CLI   0(R3),C','         DOUBLE COMMA ?
         BE    Z020&X
&LA      SETC  'L''&SYSOUT'
         C     R3,=A(&SYSOUT+&LA)  END OF FIELD ?
         BNL   Z030&X
         XC    SPFTRT,SPFTRT
         MVI   SPFTRT+C' ',X'FF'
         MVI   SPFTRT+C',',X'FF'
         MVI   SPFTRT+C')',X'FF'
         TRT   0(8,R3),SPFTRT
         BNZ   A020&X
         LA    R1,8(R3)           MAX NAME LENGTH
A020&X   EQU   *
         SR    R1,R3
         MVC   S99TUPAR(8),=8C' '
         EX    R1,MVC1&X
         B     B020&X
MVC1&X   MVC   S99TUPAR(0),&SYSOUT+2
         AGO   .YYY020
.CCC020  ANOP
&LL      SETA  &D
         AIF   ('&SYSOUT'(&D,1) EQ ',').XXX020
         MNOTE 8,'INVALID SYSOUT LITERAL'
         MEXIT
.XXX020  ANOP
&LL      SETA  &D+1
.DDD020  ANOP
&D       SETA  &D+1
         AIF   ('&SYSOUT'(&D,1) EQ ',').ZZZ020
         AIF   ('&SYSOUT'(&D,1) EQ '''').EEE020
         AIF   ('&SYSOUT'(&D,1) EQ ')').EEE020
         AGO   .DDD020
.EEE020  ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
&LIT     SETC  '&SYSOUT'(&D,&LL)
         MVC   S99TUPAR(8),=CL8'&LIT'
&D       SETA  &D+&LL
&LL      SETA  &D
.YYY020  ANOP
B020&X   EQU   *
         LA    R14,14(R14)        BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
.ZZZ020  ANOP
         AIF   ('&SYSOUT'(1,1) EQ '''').AAA030
         LA    R3,1(R1,R3)        BUMP TO NEXT SYSOUT PARM
Z020&X   EQU   *
         AGO   .BBB030
.AAA030  ANOP
         AIF   (K'&SYSOUT LE &LL).ZZZ030
         AIF   ('&SYSOUT'(&D,1) EQ ')').ZZZ030
.BBB030  ANOP
&D       SETA  &D+1
&LL      SETA  &D
*----------------------------------------------------------------------
*        SYSOUT FORM NUMBER
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSFMNO        KEY FOR FORM
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'4'     MOVE 4 TO TXT UNIT LNGTH FLD
         AIF   ('&SYSOUT'(1,1) EQ '''').CCC030
         LA    R3,1(R3)           BUMP PAST CLASS
         CLI   0(R3),C')'         END OF DATA ?
         BE    Z030&X
&LA      SETC  'L''&SYSOUT'
         C     R3,=A(&SYSOUT+&LA)  END OF FIELD ?
         BNL   Z030&X
         XC    SPFTRT,SPFTRT
         MVI   SPFTRT+C' ',X'FF'
         MVI   SPFTRT+C',',X'FF'
         MVI   SPFTRT+C')',X'FF'
         TRT   0(4,R3),SPFTRT
         BNZ   A030&X
         LA    R1,4(R3)
A030&X   EQU   *
         SR    R1,R3              LENGTH OF PARM
         BCTR  R1,R0              DECR FOR EXECUTE
         MVC   S99TUPAR(4),=4C' '
         EX    R1,MVC2&X
         B     B030&X
MVC2&X   MVC   S99TUPAR(0),0(R3)
         AGO   .YYY030
.CCC030  ANOP
         AIF   ('&SYSOUT'(&D,1) EQ '''').DDD030
         AIF   ('&SYSOUT'(&D,1) EQ ')').DDD030
&D       SETA  &D+1
         AGO   .CCC030
.DDD030  ANOP
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
&LIT     SETC  '&SYSOUT'(&D,&LL)
         MVC   S99TUPAR(4),=CL4'&LIT'
.YYY030  ANOP
B030&X   EQU   *
         LA    R14,10(R14)        BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
Z030&X   EQU   *
.ZZZ030  ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
