         MACRO
&NAME    ALCCC &CCDD=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'CCDD' KEYWORD (CONCATENATE DD NAMES)
.*
.*       CCDD='DD1,DD2,DD3'     DDNAMES SEPARATED BY COMMAS OR BLANKS
.*       CCDD=NAME              NAME  DC  CL20'(DD1 DD2 DD3)'
.*
.*---------------------------------------------------------------------
         LCLA  &X,&D,&LL,&NUM                     @MK
         LCLC  &LA,&LIT                           @MK
&X       SETA  &SYSNDX
&NAME    DS    0H
         AIF   ('&CCDD'(1,1) EQ '''').DDLIT
*----------------------------------------------------------------------
*        CONCATENATE DATA SETS
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT POINTER UNIT
         LA    R1,DCCDDNAM        KEY FOR DDNAME
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         LA    R3,&CCDD+1         BUMP TO FIRST DDNAME IN LIST
         XR    R4,R4              CLEAR COUNTER
         LA    R5,S99TUNUM        ADDR OF TEXT UNIT NUMBER FIELD
MAAA&X   EQU   *
         XC    SPFTRT,SPFTRT      INIT TRT TABLE
         MVI   SPFTRT+C' ',X'FF'  BLANK
         MVI   SPFTRT+C',',X'FF'  COMMA
         MVI   SPFTRT+C')',X'FF'  PAREN
&LA      SETC  'L''&CCDD'
         TRT   0(&LA,R3),SPFTRT   FIND BLANK OR COMMA
         BNZ   MBBB&X             FOUND
         LA    R1,&CCDD+&LA       SET TO END
MBBB&X   EQU   *
         SR    R1,R3              LENGTH OF THIS DDNAME
         BCTR  R1,0              DECR FOR 'EX'
         EX    R1,MVC1&X          MOVE DDNAME TO S99TUPAR
         LA    R4,1(R4)           ADD 1 TO COUNTER
         LA    R1,1(R1)           LENGTH OF DDNAME
         LA    R3,0(R1,R3)        BUMP TO LAST FOUND+1
         STCM  R1,3,S99TULNG      MOVE LL TO TEXT UNIT LENGTH
         LA    R14,2(R1,R14)      BUMP TEXT FIELDS
&LA      SETC  'L''&CCDD'
         LA    R0,&CCDD+&LA                             @MK
         CR    R3,R0                                    @MK
*        C     R3,=A(&CCDD+&LA)   PAST END OF FIELD ?   @MK
         BNL   MCCC&X             YES - DONE
         CLI   0(R3),C')'         END OF DDNAMES ?
         BE    MCCC&X             YES - DONE
         LA    R3,1(R3)           NO - BUMP PAST COMMA OR BLANK
         MVI   SPFTRT,X'FF'       SET FOR ALL
         MVC   SPFTRT+1(255),SPFTRT
         MVI   SPFTRT+C' ',X'00'  EXCEPT A BLANK
         TRT   0(&LA,R3),SPFTRT   FIND NON-BLANK
         BZ    MCCC&X             I GIVE UP
         LR    R3,R1              FOUND 1
         CLI   0(R3),C')'         END OF DDNAMES ?
         BE    MCCC&X             I GIVE UP
         B     MAAA&X             FIND NEXT DDNAME
MCCC&X   EQU   *
         STCM  R4,3,0(R5)         STORE NUMBER OF DDNAMES
         LA    R15,4(R15)         BUMP BECAUSE OF DECR BELOW
         B     MDDD&X
MVC1&X   MVC   S99TUPAR(0),0(R3)  MOVE DDNAME
MDDD&X   EQU   *
         AGO   .DONE
.DDLIT   ANOP
*----------------------------------------------------------------------
*        CONCATENATE DATA SETS
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT POINTER UNIT
         LA    R1,DCCDDNAM        KEY FOR DDNAME
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         LA    R5,S99TUNUM        ADDR OF TEXT UNIT NUMBER FIELD
&D       SETA  2
&LL      SETA  &D
&NUM     SETA  0
.AAA     ANOP
         AIF   (&D EQ K'&CCDD).BBB
         AIF   ('&CCDD'(&D,1) EQ ',').BBB
         AIF   ('&CCDD'(&D,1) EQ ' ').BBB
&D       SETA  &D+1
         AGO   .AAA
.BBB     ANOP
&NUM     SETA  &NUM+1
&LL      SETA  &D-&LL
&D       SETA  &D-&LL
&LIT     SETC  '&CCDD'(&D,&LL)
         MVC   S99TUPAR(8),=CL8'&LIT'
         MVC   S99TULNG,=H'8'
         LA    R14,10(R14)        BUMP TEXT FIELDS
&D       SETA  &D+&LL
         AIF   (&D EQ K'&CCDD).CCC
&D       SETA  &D+1
&LL      SETA  &D
         AGO   .AAA
.CCC     ANOP
         MVC   0(2,R5),=AL2(&NUM)  NUMBER OF DD NAMES
         LA    R15,4(R15)         BUMP BECAUSE OF DECR BELOW
         AGO   .DONE
.DONE    ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
