         MACRO
&NAME    SPFTEMP &IDS,&ODS,&QUAL
.*---------------------------------------------------------------------
.*       THIS MACRO WILL GENERATE A DATA SET NAME FROM A USER SUPPLIED
.*       BASE NAME BY CHANGING THE LAST QUALIFIER OF THE BASE NAME.
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
         LCLC  &INDS,&LIT,&LA
&X       SETA  &SYSNDX
&INDS    SETC  '&IDS'
         AIF   (T'&IDS NE 'O').AAA
&INDS    SETC  'SPFDYNAM'
         XC    SPFTRT,SPFTRT        CLEAR
         MVI   SPFTRT+C' ',X'FF'
         TRT   ZPREFIX,SPFTRT       FIND END OF USER ID
         BNZ   MAAA&X
         LA    R1,ZPREFIX+8
MAAA&X   EQU   *
         LA    R14,ZPREFIX         START OF USER PREFIX
         SR    R1,R14              LENGTH OF USER PREFIX
         LA    R14,ZTEMPF          START OF ZTEMPF
         SH    R1,=H'1'            DECR FOR EX
         EX    R1,CLC1&X           COMPARE USER PREFIX TO ZTEMPF
         BE    MDDD&X              ZTEMPF IS A REAL NAME
         TRT   ZUSER,SPFTRT        FIND END OF USER ID
         BNZ   MBBB&X
         LA    R1,ZUSER+8
MBBB&X   EQU   *
         LA    R15,ZUSER           START OF USER-ID
         SR    R1,R15              LENGTH OF USER PREFIX
         SH    R1,=H'1'            DECR FOR EX
LPA&X    EQU   *
         LA    R14,1(R14)          BUMP
         EX    R1,CLC2&X           FIND USER-ID IMBEDDED IN ZTEMPF
         BE    MCCC&X              FOUND IT
         B     LPA&X
MCCC&X   EQU   *
         MVC   SPFDYNAM(45),BLANKS  CLEAR
         MVC   SPFDYNAM(18),0(R14)   MOVE USER-ID + NEXT QUALIFIER
         LA    R14,SPFDYNAM         START OF NEW DSN
         LA    R14,2(R14,R1)        BUMP PAST ID AND DOT
         MVC   0(7,R14),=C'SPFTEMP'   ADD THIS QUALIFIER
         TRT   SPFDYNAM(45),SPFTRT  FIND END OF DSN
         MVC   0(5,R1),=C'.CNTL'   ADD ANOTHER QUALIFIER
         B     MEEE&X
CLC1&X   CLC   0(0,R14),ZPREFIX    COMPARE
CLC2&X   CLC   0(0,R14),ZUSER      COMPARE
MVC1&X   MVC   SPFDYNAM(0),0(R14)  MOVE
MDDD&X   EQU   *
         MVC   SPFDYNAM(44),ZTEMPF
MEEE&X   EQU   *
.AAA     ANOP
         MVI   &ODS,C' '          CLEAR THE
&LA      SETC  'L''&ODS-1'
         MVC   &ODS+1(&LA),&ODS TARGET FIELD
         MVC   &ODS,&INDS         MOVE DSN TO TARGET
         XC    SPFTRT,SPFTRT      CLEAR TRT TABLE
         MVI   SPFTRT+C'.',X'FF'  DOT
         MVI   SPFTRT+C' ',X'FF'  BLANK
         LA    R15,&ODS
LPR&X    EQU   *
&LA      SETC  'L''&ODS'
         TRT   0(&LA,R15),SPFTRT FIND A DOT OR BLANK
         CLI   0(R1),C' '         WAS IT A BLANK ?
         BE    MFFF&X
         LR    R14,R1             DOT
         LA    R15,1(R1)
         B     LPR&X              KEEP GOING
MFFF&X   EQU   *
         LA    R14,1(R14)         BUMP PAST DOT
         AIF   ('&QUAL'(1,1) EQ '''').BBB
         MVC   0(8,R14),=8C' '    CLEAR
&LA      SETC  'L''&QUAL'
         MVC   0(&LA,R14),&QUAL MOVE QUALIFIER
         AGO   .DONE
.BBB     ANOP
&LIT     SETC  '&QUAL'(2,K'&QUAL-2)
         MVC   0(8,R14),=CL8'&LIT'  MOVE LAST QUALIFIER
.DONE    ANOP
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
