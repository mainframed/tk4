         MACRO
&NAME    SPFTBLOC &TBL,&KEY1,&KEY2,&KEY3,&START=X,&LREPL=NO
         LCLA  &X
         LCLC  &LL
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
.*       THIS MACRO LOCATES A KEY FIELD IN A TABLE
.*
.*---------------------------------------------------------------------
&NAME    DS    0H
*----------------------------------------------------------------------
         AIF   ('&START' NE 'TOP').NOTOP
         CALL  ISPLINK,(TBTOP,&TBL),VL
.NOTOP   ANOP
         MVC   SMSG,BLANKS             CLEAR
         MVC   CURSOR,BLANKS           CLEAR
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET FOR BLANK
         TRT   ZCMD,SPFTRT             FIND FIRST BLANK AFTER COMMAND
         BNZ   MAAA&X                  FOUND IT
         MVC   SMSG,=CL24'INVALID COMMAND'
         NI    FLAG&X,X'FF'-BODR&X     TURN OFF FLAG
         B     MZZZ&X                  EXIT
MAAA&X   EQU   *
         LR    R3,R1                   SAVE ADDR OF BLANK
         MVI   SPFTRT,X'FF'            TURN ON ALL TRT BYTES
         MVC   SPFTRT+1(255),SPFTRT
         MVI   SPFTRT+C' ',X'00'       TURN OFF BLANK
         TRT   0(45,R3),SPFTRT         FIND START OF DATA
         BNZ   MBBB&X                  FOUND IT
         CALL  ISPLINK,(TBSKIP,&TBL),VL
         B     MCCC&X                  PROCESS REPEAT FIND
MBBB&X   EQU   *
         NI    FLAG&X,X'FF'-BODR&X     TURN OFF FLAG&X
         MVC   MSV1&X.(44),0(R1)       ISOLATE DATA FOLLOWING COMMAND
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET FOR BLANK
         TRT   MSV1&X,SPFTRT           FIND FIRST BLANK AFTER DATA
         MVI   0(R1),C'*'              DO A GENERIC SEARCH ON DATA
         AIF   (T'&KEY2 EQ 'O').NOKY23
         MVC   MSV2&X.(L'&KEY2),&KEY2  SAVE SEARCH ARG &KEY2
         AIF   (T'&KEY3 EQ 'O').NOKY23
         MVC   MSV3&X.(L'&KEY3),&KEY3  SAVE SEARCH ARG &KEY2
.NOKY23  ANOP
MCCC&X   EQU   *
         AIF   ('&START' EQ 'TOP').TOP
         SPFTBLRS &TBL                 SET ROW
.TOP     ANOP
         TM    FLAG&X,BODR&X           BOT OF DATA ON PREV ?
         BZ    MDDD&X                  NO - OK
         NI    FLAG&X,X'FF'-BODR&X     TURN OFF FLAG&X
         CALL  ISPLINK,(TBTOP,&TBL),VL
MDDD&X   EQU   *
         MVC   KEYS&X,BLANKS           CLEAR
         MVC   KEYS&X.(30),=CL30'(&KEY1 &KEY2 &KEY3)' SEARCH KEY
         MVC   &KEY1,MSV1&X            MOVE SEARCH ARG TO &KEY1
         AIF   ('&LREPL' EQ 'NO').NOREP1
&LL      SETC  'L''&KEY1'
         SPFVREPL (&KEY1,&LL)
.NOREP1  ANOP
         AIF   (T'&KEY2 EQ 'O').NOKEY23
         MVC   &KEY2,MSV2&X            MOVE SEARCH ARG TO &KEY2
         AIF   ('&LREPL' EQ 'NO').NOREP2
&LL      SETC  'L''&KEY2'
         SPFVREPL (&KEY2,&LL)
.NOREP2  ANOP
         AIF   (T'&KEY3 EQ 'O').NOKEY23
         MVC   &KEY3,MSV3&X            MOVE SEARCH ARG TO &KEY3
         AIF   ('&LREPL' EQ 'NO').NOREP3
&LL      SETC  'L''&KEY3'
         SPFVREPL (&KEY3,&LL)
.NOREP3  ANOP
.NOKEY23 ANOP
         CALL  ISPLINK,(TBSCAN,&TBL,KEYS&X),VL     SEARCH THE TABLE
         MVC   SMSG(6),=CL6'CHARS'     SET SMSG
         MVC   SMSG+6(18),MSV1&X       "
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C'*',X'FF'       SET TO "*"
         TRT   SMSG+6(18),SPFTRT       FIND THE "*" WE ADDED
         BZ    MEEE&X                  BEYOND LENGTH OF SMSG
         MVI   0(R1),C' '              REMOVE THE "*"
         LA    R2,SMSG+23              END OF SMSG
         SR    R2,R1                   LENGTH REMAINING
         BNP   MEEE&X                  NONE
         EX    R2,MVCM&X               MOVE REST OF MESSAGE
MEEE&X   EQU   *
         LTR   R15,R15                 WAS THE SEARCH SUCCESSFUL ?
         BZ    MFFF&X                  YES - OK
         SPFTBLRS &TBL                 NO - RESET ROW
         MVC   SMSG,=CL24'*BOTTOM OF DATA REACHED*'
         MVC   ROW,=F'0'               SET TO ZERO
         OI    FLAG&X,BODR&X           INDICATE BOT OF DATA
         B     MZZZ&X
MFFF&X   EQU   *
         MVC   CURSOR,=CL8'&KEY1'
         SPFCRP &TBL
         MVC   ROW,CRP
         B     MZZZ&X
MVCM&X   MVC   1(0,R1),=CL20'FOUND'
KEYS&X   DC    CL40' '
MSV1&X   DC    CL40' '
MSV2&X   DC    CL40' '
MSV3&X   DC    CL40' '
FLAG&X   DC    X'00'
BODR&X   EQU   X'80'
MZZZ&X   DS    0H
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
