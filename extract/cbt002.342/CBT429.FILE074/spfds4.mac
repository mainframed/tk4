         MACRO
&NAME    SPFDS4 &TDSN,&TMBR
&NAME    DS    0H
         GBLA  &Y
         LCLC  &REGD,&REGM
         LCLA  &X
&X       SETA  &SYSNDX
.*---------------------------------------------------------------------
.*       BLDL FOR THE MEMBER
.*---------------------------------------------------------------------
&REGM    SETC  'R2'
         AIF   ('&TMBR'(1,1) EQ '(').AAA
         LA    &REGM,&TMBR             ADDR OF MEMBER NAME
         AGO    .BBB
.AAA     ANOP
&REGM    SETC  '&TMBR'(2,K'&TMBR-2)
.BBB     ANOP
         CLI   0(&REGM),C' '           MEMBER ?
         BNH   MBBB&X                  NO - SKIP BLDL
         SPFALLOC A,FREE='CLOSE',RETDD=SPFDBWRD,DSN=&TDSN,DISP='SHR'
         MVC   SPFBDCB+40(8),SPFDBWRD  MOVE DDNAME TO DCB
         CLC   SPFDYN15,=4C'0'         ALLOCATED OK ?
         BE    MAAA&X                  YES - OK
         MVC   SMSG,BLANKS             CLEAR
         LA    R15,20                  SET RETURN CODE
         MVC   SMSG(20),=C'BLDL DATA SET IN USE'
         CLC   SPFDYNER,=C'0210'       DATA SET IN USE ?
         BE    MBBB&X                  YES - OK
         LA    R15,28                  ALL OTHER ERRORS
         MVC   SMSG(22),=C'BLDL ALLOCATION FAILED'
         B     MBBB&X                  EXIT
MAAA&X   EQU   *
         MVC   SPFBMBR,0(&REGM)        MOVE MBR NAME TO BLDL LIST
         OPEN  SPFBDCB                 OPEN THE PDS
         BLDL  SPFBDCB,SPFBLIST
         ST    R15,SPFRC               SAVE RETURN CODE
         CLOSE SPFBDCB
         L     R15,SPFRC               PICK UP BLDL RETURN CODE
         LTR   R15,R15                 OK ?
         BZ    MBBB&X                  YES - EXIT
         LA    R15,4                   NO - SET RETURN CODE
         MVC   SMSG,BLANKS             CLEAR
         MVC   SMSG(16),=C'MEMBER NOT FOUND'
         MVC   LMSG,BLANKS             CLEAR
         MVC   LMSG(7),=C'MEMBER='
         MVC   LMSG+7(8),0(&REGM)      MOVE MBR
MBBB&X   EQU   *
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
.*
.*       FREEWARE: THIS MACRO IS DISTRIBUTED ON AN AS-IS BASIS
.*                 WITHOUT WARRANTY, EITHER EXPRESSED OR IMPLIED, BY:
.*
.*                 THE NATIONAL SYSTEMS PROGRAMMERS ASSOCIATION (NASPA)
.*                 P.O. BOX 21525
.*                 GREENFIELD, WI 53221
.*
.*
.*       MACRO WRITTEN BY:
.*
.*                 PRODUCTIVITY SYSTEMS, INC.
.*                 MILWAUKEE, WI
.*
.*---------------------------------------------------------------------
.*
.*    THIS MACRO HAS MET A BASIC SET OF PROGRAMMING AND
.*    DOCUMENTATION STANDARDS, BUT HAS NOT BEEN PROGRAM TESTED IN ANY
.*    FORMAL FASHION BY NASPA. THE USER IS EXPECTED TO MAKE THE FINAL
.*    EVALUATION OF THE USE OF THIS MACRO IN THEIR ENVIRONMENT.
.*
.*    NASPA MAKES NO WARRANTY, EXPRESSED OR IMPLIED, OF THE FITNESS
.*    FOR A PARTICULAR PURPOSE AS RELATES TO THE DOCUMENTATION,
.*    FUNCTION, RESULTS, OR PERFORMANCE OF THIS MACRO.
.*
.*    IMPLEMENTATION AND USE OF THIS MACRO CONSTITUTES A RELEASE
.*    FROM LIABILITY OF NASPA FOR ANY PROBLEMS ARISING FROM THE USE OF
.*    THIS MACRO AT THE USER'S INSTALLATION.
.*
.*    THIS MACRO IS MADE AVAILABLE BY NASPA WITHOUT CHARGE.
.*    RECIPIENTS ARE FREE TO MAKE THIS MACRO AVAILABLE TO OTHERS IN
.*    LIKE MANNER, PROVIDED THIS DISCLAIMER IS KEPT INTACT.  IT MAY
.*    NOT BE SOLD, TRADED, OR EXCHANGED FOR CASH, SERVICES, OR AS AN
.*    INCENTIVE FOR SERVICES AND/OR OTHER PURCHASES, NOR MAY IT BE
.*    INTEGRATED INTO ANY PROGRAM FOR SUCH USE.
.*
.*---------------------------------------------------------------------
.*---------------------------------------------------------------------
         MEND
