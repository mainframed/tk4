//**********************************************************************
//**  RACF POST-PROCESSING EXIT FOR RACHECK (ICHRCX02)                **
//**********************************************************************
//UMOD000  EXEC PGM=GIMSMP,REGION=4096K
//SMPCSI   DD DSN=SMP.GLOBAL.CSI,DISP=SHR
//SMPOUT   DD SYSOUT=*
//SMPRPT   DD SYSOUT=*
//SYSPRINT DD SYSOUT=*
//SMPCNTL  DD *
 SET BDY(TARGET).
 RESTORE S(UMOD000).
 SET BDY(GLOBAL).
 REJECT S(UMOD000).
 RECEIVE SYSMODS.
 SET BDY(TARGET).
 APPLY S(UMOD000) ASSEM REDO COMPRESS(ALL).
/*
//SMP.SMPPTFIN DD DATA,DLM=$$
++USERMOD(UMOD000).
++VER (Z038)
  FMID(HRF1702)
  /*
     THIS IS A POST PROCESSING EXIT FOR RACHECK(S) THAT ALLOWS
     USERS W/"AUDIT" TO 'READ' ANYTHING ON THE SYSTEM(CLASS=DATASET),
     REGARDLESS OF THEIR AUTHORITY WITHIN THE PROFILE FOR THE
     RESOURCE.

     CHECK FOR A USER CONNECTED TO GROUP, "SUPERMAN", THAT ALLOWS
     THE USER TO BYPASS ALL RACHECKS FOR CLASS=DATASET.

     IF THE ACCESS IS A 'TAPE' ACCESS, THEN ISSUE "WARNING" MESSAGES
     WHILE STILL "FAILING" DASD ACCESS REQUESTS.

                                                                  */.
++JCLIN.
//ICHRCX02 EXEC PGM=IEWL,PARM='XREF,LIST,LET,NCAL,RENT'
//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(2,2))
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=SYS1.LPALIB,DISP=SHR
//SYSLIN   DD *
  NAME ICHRCX02(R)
/*
++SRC (ICHRCX02)
    SYSLIB  (UMODSRC)
    DISTLIB (AUMODSRC)
    DISTMOD (ALPALIB).
C02      TITLE 'ICHRCX02 -- RACHECK POST-PROCESSING EXIT ROUTINE'
**********************************************************************
*                                                                    *
*  MODULE NAME -                                                     *
*                                                                    *
*     ICHRCX02 -- RACHECK POST-PROCESSING EXIT ROUTINE               *
*                                                                    *
*  FUNCTION -                                                        *
*                                                                    *
*     THIS EXIT ALLOWS:                                              *
*                                                                    *
*     -  USERS WITH THE 'AUDITOR' ATTRIBUTE ON A GLOBAL LEVEL        *
*        'READ' ACCESS TO ALL DATASETS, REGARDLESS OF "ACCESS"       *
*        AUTHORITY                                                   *
*                                                                    *
*     -  USERS WITH AN ACEE ANCHORED GROUP OF 'SUPERMAN'             *
*        TO BYPASS RACHECK AUTHORIZATION APPROVAL                    *
*                                                                    *
*        PROVIDE WARNING FOR TAPEDS ACCESSES                         *
*                                                                    *
*                                                                    *
*     ALL ACCESSES THAT ALLOWED THAT NORMALLY WOULD NOT BE           *
*     ARE LOGGED.                                                    *
*                                                                    *
*  ATTRIBUTES -                                                      *
*                                                                    *
*     REENTRANT, LPA RESIDENT.                                       *
*                                                                    *
*  ENTRY CONDITIONS -                                                *
*                                                                    *
*     R1  - ADDRESS OF AREA DERIVED FROM PARAMETERS ON RACHECK MACRO *
*                                                                    *
*  EXIT CONDITIONS -                                                 *
*                                                                    *
*     R15 - CONTAINS A RETURN CODE:                                  *
*        R15 = 0 - PROCESSING COMPLETE                               *
*        R15 = 4 - RETRY RACHECK PROCESSING                          *
*                                                                    *
*  REGISTER USAGE -                                                  *
*                                                                    *
*     R13 - ADDRESS OF STANDARD SAVE AREA                            *
*     R12 - BASE ADDRESS (ICHRCX02)                                  *
*     R11 - ADDRESS OF PARAMETER AREA                                *
*     R10 - ADDRESS OF COMMUNICATIONS VECTOR TABLE (CVT)             *
*     R9  - ADDRESS OF RACF CVT (RCVT)                               *
*     R8  - ADDRESS OF ACCESSOR ENVIRONMENT ELEMENT (ACEE)           *
*     R7  - ADDRESS OF ADDRESS SPACE CONTROL BLOCK (ASCB)            *
*     R6  - ADDRESS OF ADDRESS SPACE EXTENSION BLOCK (ASXB)          *
*     R5  - WORK                                                     *
*     R4  - WORK                                                     *
*     R3  - WORK                                                     *
*     R2  - WORK                                                     *
*     R1  - WORK                                                     *
*                                                                    *
*  OPERATION -                                                       *
*                                                                    *
*     1.  IF RACF IS INACTIVE, ALLOW THE ACCESS REGARDLESS OF THE    *
*          LEVEL AUTHORIZED, ISSUE A MESSAGE TO THE OPERATOR, AND    *
*          EXIT.                                                     *
*     2.  IF THE USER IS HAS AUDITOR AUTHORITY, ALLOW READ/ONLY      *
*          ACCESS REGARDLESS OF ALLOWED ACCESS TO THE DATASET.       *
*     3.  IF THE USER IS CONNECTED TO GROUP, 'SUPERMAN', THEN BYPASS *
*          RACHECK AUTHORIZATION REGARDLESS OF AUTHORITY SPECIFIED.  *
*     4.  IF THE USER HAS BEEN ALLOWED A LEVEL OF ACCESS WHICH HE    *
*          DID NOT HAVE BEFORE THIS EXIT WAS CALLED, CAUSE RACF TO   *
*          LOG THE ACCESS.                                           *
*     5.  CHECK TO ALLOW ONLY BYPASS OF RACHECK FOR 'DATASET'        *
*          CLASS.                                                    *
*     6.  PROVIDE 'WARNING' FOR TAPEDS ACCESS                        *
*                                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
         EJECT
         USING SAVEAREA,R13
         USING ICHRCX02,R12
         USING ACHKLIST,R11
         USING CVT,R10
         USING RCVT,R9
         USING ACEE,R8
         USING ASCB,R7
         USING ASXB,R6
         USING RRPF,R3
         USING PSA,R0
ICHRCX02 CSECT
ICHRCX02 AMODE 31                  SET ADDRESSING MODE TO 31-BIT
ICHRCX02 RMODE ANY                 SET RESIDENCY MODE TO ANY
         SAVE  (14,12),,'ICHRCX02&SYSDATE.&SYSTIME'    SAVE REGISTERS
         LR    R12,R15             ESTABLISH BASE REGISTER
         LR    R11,R1              SAVE PARAMETER AREA ADDRESS
         GETMAIN R,LV=WORKSIZE     OBTAIN A SAVE AREA
         ST    R1,SAVENEW          FORWARD CHAIN
         ST    R13,SAVEOLD-SAVEAREA(,R1)     BACK CHAIN
         LR    R13,R1              SAVE SAVE AREA ADDRESS
         L     R10,FLCCVT          GET ADDRESS OF CVT
         L     R1,CVTTCBP          GET THE CURRENT TCB POINTER
         L     R7,12(,R1)          GET THE CURRENT ASCB POINTER
         ICM   R4,B'1111',ASCBJBNI GET JOBNAME PTR FOR JOBS
         BNZ   GOTJOBNM            IF NOT ZERO, WE GOT JOBNAME
         ICM   R4,B'1111',ASCBJBNS GET JOBNAME PTR FOR STC/TSU
GOTJOBNM L     R3,ACHK@EOP         GET ENTITY OR PROFILE ADDRESS
         L     R1,ACHK@FG3         GET THE ADDRESS OF FLAG 3
         TM    0(R1),ACHKTPRF      PROFILE OR ENTITY GIVEN?
         BZ    NOTPRO              NOT PROFILE, ENTITY
         MVC   DSN,DSPDSNM         MOVE DSN IN FROM PROFILE
         B     CHKRACF             CHECK TO SEE IF RACF IS ACTIVE
NOTPRO   MVC   DSN,0(R3)           COPY OVER THE DSN
         EJECT
CHKRACF  L     R9,CVTRAC           GET THE RCVT ADDRESS
         TM    RCVTSTAT,RCVTRNA    IS RACF ACTIVE?
         BO    NORACF              NO, WARN OPERATOR
         TM    RCVTFLGS,RCVTROFF   HAS RVARY INACT BEEN ISSUED?
         BNZ   NORACF              YES, WARN OPERATOR
         MVC   WTOLIST(WTOLISTL),WTOPROTO    MOVE IN SKELETON WTO
         MVC   MSG1(MSG1LGTH),MSG1PRTO
         MVC   MSG1DSN,DSN         MOVE DSN INTO MESSAGE
         MVC   MSG1JOBN,0(R4)      MOVE JOBNAME INTO MESSAGE
         MVC   WTOLIST+4(MSG1LGTH),MSG1
         L     R1,ACHK@RTC         GET PTR TO REASON/RETURN CODE
         ICM   R2,B'1111',0(R1)    PICK UP REASON/RETURN CODE
         BZ    EXIT                ACCESS ALLOWED, EXIT
         L     R8,ACHK@ACE         GET THE ACEE ADDRESS POINTER
         ICM   R8,B'1111',0(R8)    DID RACF GIVE US AN ACEE?
         BNZ   GOTACEE             YES, CHECK AUTHORITY
         L     R6,ASCBASXB         GET THE ASXB ADDRESS
         L     R8,ASXBSENV         GET THE ACEE ADDRESS
GOTACEE  TM    ACEEFLG1,ACEEAUDT   DOES THE USER HAVE AUDITOR ATTR?
         BNO   GROUPCHK            NO, GO SEE IF THIS IS 'SUPERMAN'
         L     R1,ACHK@FG2         GET THE FLAG 2 ADDRESS
         TM    0(R1),ACHKTRD       DID S/HE WANT READ ACCESS?
         BNO   EXIT                NO, DISALLOW THE ACCESS
         B     CLASSCHK            GO AROUND 'SUPERMAN'/TAPEDS CHECK
*
GROUPCHK CLC   ACEEGRPN(8),=CL8'SUPERMAN' USER CONNECTED TO 'SUPERMAN'?
         BNE   TAPEDS              NO, GO CHECK IF TAPEDS PROTECT IS ON
         B     CLASSCHK            YES, THIS IS 'SUPERMAN', CHECK CLASS
*
TAPEDS   TM    RCVTFLG1,RCVTTDSN   IS TAPEDS PROTECTION ON?
         BNO   EXIT                NO, EXIT
*                                  YES, TAPEDS PROTECTION IS ON,
*                                     FIND OUT IF "DSTYPE=T"
*
         L     R1,ACHK@FG3         LOAD FLAG 3 ADDRESS
TAPECHK  TM    0(R1),ACHKTDST      IS THIS A TAPEDS ACCESS?
         BNO   EXIT                NO, EXIT
*                                  YES, MAKE SURE 'CLASS=DATASET'
*                                     & ISSUE WARNING MSGS
*
CLASSCHK L     R1,ACHK@CLS         GET THE CLASS POINTER
         CLC   0(8,R1),DATASET     IS 'CLASS=DATASET'?
         BNE   EXIT                NO, EXIT TO DISALLOW
*
         WTO   MF=(E,WTOLIST)      WRITE THE WARNING MESSAGE
         L     R1,ACHK@RTC         GET THE REASON CODE ADDRESS
         XC    0(4,R1),0(R1)       CLEAR THE REASON CODE
         L     R1,ACHK@ABE         GET THE ABEND CODE ADDRESS
         XC    0(4,R1),0(R1)       CLEAR THE ABEND CODE
         L     R1,ACHK@LOG         GET THE LOGGING CONTROL ADDRESS
         MVC   0(4,R1),=F'4'       UNCOND. LOG THIS ACCESS
         L     R1,ACHK@ACC         GET AUTHORIZED ACCESS ADDRESS
         CLI   0(R1),X'00'         IS THERE AN AUTHORIZATION?
         BNE   CHKAUTH             YES, CREATE MSG SUMMARIZING ACTION
         MVC   MSG1(MSG1LGTH),NOPROF    MOVE IN NO PROFILE MESSAGE
         MVC   WTOLIST+4(MSG1LGTH),MSG1
         B     WTOIT               WRITE NO PROFILE MESSAGE
CHKAUTH  MVC   NOAUTH,NOAUTHP      MOVE IN SKELETON WTO
         MVC   AUTH,=CL8'NONE'     ASSUME AUTHORIZATION TO BE NONE
         TM    0(R1),ACHKAALT      IS AUTHORIZATION ALTER?
         BNO   NOALT               NO, CHECK NEXT LEVEL
         MVC   AUTH,=CL8'ALTER'    YES, INSERT INTO MESSAGE
NOALT    TM    0(R1),ACHKACTL      IS AUTHORIZATION CONTROL?
         BNO   NOCON               NO, CHECK NEXT LEVEL
         MVC   AUTH,=CL8'CONTROL'  YES, INSERT INTO MESSAGE
NOCON    TM    0(R1),ACHKAUPD      IS AUTHORIZATION UPDATE?
         BNO   NOUPD               NO, CHECK NEXT LEVEL
         MVC   AUTH,=CL8'UPDATE'   YES, INSERT INTO MESSAGE
NOUPD    TM    0(R1),ACHKARD       IS AUTHORIZATION READ ONLY?
         BNO   NOTRED              NO, MUST HAVE BEEN NONE
         MVC   AUTH,=CL8'READ'     YES, INSERT INTO MESSAGE
NOTRED   L     R1,ACHK@FG2         GET FLAG BYTE 2 ADDRESS
         TM    0(R1),ACHKTALT      WAS THE REQUEST FOR ALTER?
         BNO   NOALTR              NO, CHECK NEXT LEVEL
         MVC   REQUEST,=CL8'ALTER' YES, INSERT INTO MESSAGE
NOALTR   TM    0(R1),ACHKTCTL      WAS THE REQUEST FOR CONTROL?
         BNO   NOCONR              NO, CHECK NEXT LEVEL
         MVC   REQUEST,=CL8'CONTROL'    YES, INSERT INTO MESSAGE
NOCONR   TM    0(R1),ACHKTUPD      WAS THE REQUEST FOR UPDATE?
         BNO   NOUPDR              NO, CHECK NEXT LEVEL
         MVC   REQUEST,=CL8'UPDATE'     YES, INSERT INTO MESSAGE
NOUPDR   TM    0(R1),ACHKTRD       WAS THE REQUEST FOR READ ONLY?
         BNO   WTOM                NO, THEN WHAT THE HECK
         MVC   REQUEST,=CL8'READ'  YES, INSERT INTO MESSAGE
WTOM     MVC   WTOLIST+4(MSG1LGTH),NOAUTH    MOVE IN SKELETON MESSAGE
WTOIT    WTO   MF=(E,WTOLIST)      WRITE THE MESSAGE
         B     EXIT                EXIT
NORACF   WTO   '*** WARNING *** RACF NOT ACTIVE *** ACCESS ALLOWED'
         L     R1,ACHK@ABE         GET ABEND CODE ADDRESS
         XC    0(4,R1),0(R1)       CLEAR THE ABEND CODE
         L     R1,ACHK@RTC         GET RETURN CODE ADDRESS
         XC    0(4,R1),0(R1)       CLEAR THE RETURN CODE
         EJECT
EXIT     LR    R1,R13              SAVE SAVE AREA ADDRESS
         L     R13,SAVEOLD         GET CALLER'S SAVE AREA
         XC    SAVER15(4),SAVER15  CLEAR THE RETURN CODE
         FREEMAIN R,A=(R1),LV=WORKSIZE  FREE OUR SAVE AREA
         XC    SAVENEW,SAVENEW     CLEAR FORWARD CHAIN
         RETURN (14,12)            EXIT
         EJECT
WTOPROTO WTO   '                                                       X
                                                           ',MF=L,     X
               ROUTCDE=(9,11)
WTOLISTL EQU   *-WTOPROTO          LENGTH OF PROTOTYPE WTO LIST
DATASET  DC    AL1(7),CL7'DATASET' DATASET CLASS LENGTH & NAME
MSG1PRTO EQU   *
         DC    C'ICHRCX02 -- JOB='
         DC    CL8' '
         DC    C', DSN='
BLANKS   DC    CL69' '
MSG1LGTH EQU   *-MSG1PRTO
NOPROF   DC    CL99'ICHRCX02 ** WARNING ** NO RACF PROFILE -- ACCESS ALX
               LOWED'
NOAUTHP  DS    0CL99
         DC    CL38'ICHRCX02 ** WARNING ** ACCESS INTENT ('
REQUESTP DC    CL08' '
         DC    CL21')    ACCESS ALLOWED ('
AUTHP    DC    CL08' '
         DC    CL03')  '
         DC    CL21' '
         LTORG
         EJECT
         REGS
         EJECT
         SAVEAREA
WORKAREA EQU   *
WTOLIST  WTO   '1234567890123456789012345678901234567890123456789012345X
               67890123456789012345678901234567890123456789',MF=L,     X
               ROUTCDE=(9,11)
MSG1     EQU   *
         DC    C'ICHRCX02 -- JOB='
MSG1JOBN DC    CL8' '
         DC    C', DSN='
MSG1DSN  DC    CL44' '
         ORG   MSG1
NOAUTH   DS    0CL99
         DC    CL38'ICHRCX02 ** WARNING ** ACCESS INTENT ('
REQUEST  DC    CL08' '
         DC    CL21')    ACCESS ALLOWED ('
AUTH     DC    CL08' '
         DC    CL03')  '
         DS    CL21
         ORG
DSN      DS    CL44
WORKSIZE EQU   *-SAVEAREA
         EJECT
         ACHKLIST
         EJECT
         ICHACHKL
         EJECT
         CVT   DSECT=YES,LIST=YES
         EJECT
         IHAPSA
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         ICHPRCVT
         EJECT
         ICHRRPF
         EJECT
         IHAACEE
         EJECT
         END   ICHRCX02
$$
