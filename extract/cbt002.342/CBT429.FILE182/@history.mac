         TITLE 'P D S  --  PDS HISTORY                       09/28/88'
***********************************************************************
***      HISTORY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
HISTORY  CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         OI    FLAGSGG,FOUTSUB     SUBCOMMAND GAINED CONTROL
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    HIST020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTAT2
         MVI   MTHIGHL,72
         TM    FLAGSHH,FHEAD       HEADER WRITTEN YET?
         BO    HIST000             YES, BRANCH
         M$MSG L230$1
         OI    FLAGSHH,FHEAD       NOW IT HAS BEEN WRITTEN
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         SPACE 1
HIST000  MVC   INSERT#1(72),BLANK128
         MVC   INSERT#1+1(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   HIST010             NO, BRANCH
         TM    DIRSPFCR,X'FE'      0C OF 0CYYDDDF ZEROS?      Y2K AUG97
         BNZ   HIST010             NO, BRANCH                 Y2K AUG97
         TM    DIRSPFCD,X'FE'      0C OF OTHER 0CYYDDDF ZERO? Y2K AUG97
         BNZ   HIST010             NO, BRANCH                 Y2K AUG97
         SPACE 1
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         OI    27(R2),X'F0'            UNSUPPRESS ZERO        Y2K AUG97
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         OI    37(R2),X'F0'            UNSUPPRESS ZERO        Y2K AUG97
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFSI     CURRENT SIZE           DRK JAN98
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFIN     INITIAL SIZE           DRK JAN98
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFMD     MODIFIED LINES         DRK JAN98
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         LA    R1,L230$1
         B     MSGNEW                  NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
HIST010  LA    R1,L230$1
         CLC   ZERO,0(R2)              ZERO?
         BE    MSGNEW                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MSGNEW                  YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         B     MSGNEW
         SPACE 3
HIST020  OC    #ZAPOPT(4),#ZAPOPT        ANY ZAP, USER, TRAN OR LKED?
         BNZ   HIST021                   YES, BRANCH
         OC    #ZAPOPT(4),=X'01010101'   NO, DEFAULT TO DO THEM ALL
         TM    FLAGSGG,FTRANCON          TRANSLATOR DATA OFF?
         BZ    HIST021                   NO, BRANCH
         MVI   #TRANOPT,X'02'            YES, NO TRANSLATOR DATA
         SPACE 1
HIST021  L     R15,=A(READESD)           FORMAT THE ESD DATA
         BASR  R14,R15                   ANY ESD DATA?
         B     *+8                       NO, BRANCH
         B     HIST024                   YES, BRANCH
         SPACE 1
         TM    FLAGSHH,FDELAYM     DELAYED MESSAGE AVAILABLE?
         BNO   HIST022             NO, BRANCH
         TM    PMEMMIN,X'80'       MEMBER LIST?
         BO    *+12                YES, BRANCH
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP?
         BNO   HIST022             NO, BRANCH
         CLI   #MODLEN,0           ANY FILTERING?
         BH    NEWCMD              NO, BRANCH
         M$ERRST MSGBLANK
         M$ERRST DELAYMSG
         XI    FLAGSHH,FDELAYM     MESSAGE WAS WRITTEN
         SPACE 1
HIST022  LA    R1,MSGTEXT1         MESSAGE IDENTIFIER
         B     MSGNEW
         SPACE 2
HIST024  L     R15,=A(READIDR)     FORMAT THE IDR DATA
         BASR  R14,R15             IS IDR DATA AVAILABLE?
         B     NOHIST              NO, MESSAGE AND QUIT
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         B     HISTTRN$            YES, CONTINUE
         SPACE 1
NOHIST   SR    R6,R6
         BAS   R4,HISTHDR
         LA    R1,L460             NO HISTORY AVAILABLE
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         B     MSGNEW
         SPACE 3
HISTHDR  TM    FLAGSHH,FDELAYM     ANY DELAYED HEADER?
         BNO   HISTHDR2            NO, BRANCH
         XI    FLAGSHH,FDELAYM     MESSAGE WAS WRITTEN
         M$ERRST MSGBLANK
         M$ERRST DELAYMSG
HISTHDR2 TM    FLAGSCC,F1IDR       FIRST IDR RECORD?
         BZR   R4                  NO, RETURN
         NI    FLAGSCC,FF-F1IDR
         LTR   R1,R6               ANY SECOND HEADER?
         BZR   R4                  NO, RETURN
         M$MSG (R1)                INDIVIDUAL HISTORY TITLE
         BR    R4
         EJECT
*
*        FORMAT TRANSLATOR IDR DATA ENTRIES
*
HISTTRN$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         CLI   #TRANOPT,X'02'      NO TRANSLATOR DATA?
         BE    HISTCOB$            YES, BRANCH
         SPACE 2
HISTTRN  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOB$            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTTRN             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTTRN1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTTRN             YES, BRANCH
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTTRNP            YES, ALLOW THIS
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTTRN1            NO, SKIP THIS
HISTTRNP CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTTRN1            NO, BRANCH
         SPACE 1
         LA    R6,L060
         BAS   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(8),ESDNAME
         MVC   MSGTEXT1+12(9),DATEMASK
         ED    MSGTEXT1+12(9),IDRDATE
         MVC   MSGTEXT1+24(10),IDRTDATA   TRANSLATOR 1
         UNPK  DOUBLE(5),IDRTDATA+10(3)   VVMM PACKED
         MVI   MSGTEXT1+36,C'V'
         MVC   MSGTEXT1+36+1(2),DOUBLE    VV
         MVI   MSGTEXT1+40,C'M'
         MVC   MSGTEXT1+40+1(2),DOUBLE+2  MM
         CLI   IDRLDATA,15                TWO TRANSLATORS?
         BL    HISTTRN3                   NO, BRANCH
         SPACE 1                          YES
         OI    IDRTDATA+27+2,X'0F'        FIX THE SIGN FIELD  DRK MAY99
         CLC   IDRTDATA+27+1(2),=X'000F'  XPEDITER TIMESTAMP? DRK JUN99
         BNE   HISTTRN2                   NO                  DRK MAY99
*        MVC   MSGTEXT1+48(9),TIMEMASK    YES, DISPLAY        DRK MAY99
*        ED    MSGTEXT1+48(9),IDRTDATA+25                     DRK MAY99
         B     HISTTRN3                                       DRK MAY99
TIMEMASK DC    X'4021207A20207A2020'      HH:MM:SS            DRK MAY99
         SPACE 1
HISTTRN2 DS    0H                                             DRK MAY99
         OI    IDRTDATA+27+2,X'0F'        FIX THE SIGN FIELD
         LA    R15,FULLWORD               OUTPUT LOCATION
         LA    R1,IDRTDATA+27             SECOND YYDDD
         BAS   R14,CONVDATE               CONVERT TO MMDDYY FORMAT
         MVC   MSGTEXT1+48(9),DATEMASK
         ED    MSGTEXT1+48(9),FULLWORD
         MVC   MSGTEXT1+60(10),IDRTDATA+15  TRANSLATOR 2
         UNPK  DOUBLE(5),IDRTDATA+25(3)     VVMM PACKED
         MVI   MSGTEXT1+72,C'V'
         MVC   MSGTEXT1+72+1(2),DOUBLE      VV
         MVI   MSGTEXT1+76,C'M'
         MVC   MSGTEXT1+76+1(2),DOUBLE+2    MM
         SPACE 1
HISTTRN3 M$ERRST MSGTEXT1
         B     HISTTRN1            NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 3
*
*        FORMAT SPECIFIC ATTRIBUTES FOR COBOL CSECTS
*
HISTCOB$ LA    R3,#IDRPTR            SCAN IDR CHAIN
         USING IDRENTRY,R3
         LA    R2,#ESDPTR            START OF ESD CHAIN
         SPACE 2
HISTCOBA ICM   R3,B'1111',IDRLINK    END OF IDR CHAIN?
         BZ    HISTCOBB              YES, BRANCH
         CLI   IDRTYPE,IDRTRAN       TRANSLATOR ENTRY?
         BNE   HISTCOBA              NO, BRANCH
         CLC   IDRTDATA(6),HISTCTR1  COBOL TRANSLATOR ID?
         BE    HISTCOBC              YES, BRANCH
         CLC   IDRTDATA(6),HISTCTR2  COBOL TRANSLATOR ID?
         BE    HISTCOBC              YES, BRANCH              DRK MAY99
         CLC   IDRTDATA(6),HISTCTR3  COBOL TRANSLATOR ID?     DRK MAY99
         BNE   HISTCOBA              NO, BRANCH               DRK MAY99
         B     HISTCOBC              YES, COBOL -- BRANCH
         SPACE 2
         USING ESDENTRY,R2
HISTCOBB ICM   R2,B'1111',ESDLINK    END OF ESD CHAIN?
         BZ    HISTZAP$              YES, BRANCH
         CLI   ESDTYPE,CODESD        CSECT ENTRY?
         BNE   HISTCOBB              NO, SKIP THIS
         CLC   ESDNAME(3),HISTLILB   COBOL ESD ENTRY NAME?
         BNE   HISTCOBB              NO, BRANCH
         SPACE 2
HISTCOBC LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         TM    DIRATTR,ATTREXEC    EXECUTABLE?
         BNO   HISTZAP$            NO, IGNORE COBOL ATTRIBUTES
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,HISTCABE         RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD  EPLOC=DIRNAME,DCB=INDCB
         LR    R4,R0               ENTRY POINT ADDRESS OF MODULE
         SR    R1,R1               CLEAR THE TOP BYTE
         ICM   R1,B'0111',DIREPA   ENTRY POINT OFFSET OF MODULE
         SR    R4,R1               START OF MODULE
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         XC    MSGTEXT2(4),MSGTEXT2
         B     HISTCOB
         SPACE 1
HISTCABE LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         B     HISTZAP$            LOAD DID NOT WORK
         SPACE 2
HISTCOB  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOBZ            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTCOB             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTCOB1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTCOBZ            YES, DONE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTCOB1            NO, SKIP THIS
         SPACE 1
         SR    R5,R5
         ICM   R5,B'0111',ESDADDR         OFFSET TO THIS CSECT
         SR    R15,R15
         ICM   R15,B'0111',ESDLEN         LENGTH OF CSECT
         CH    R15,HISTL450               POSSIBLY COBOL?
         BL    HISTCOB1                   NO, BRANCH
         AR    R5,R4                      ADDRESS OF CSECT START
         LR    R0,R5
         SRL   R0,24                      SHIFT OFF LOWER BYTES
         LTR   R0,R0                      31-BIT ADDRESS?
         BZ    HISTCOB9                   NO, BRANCH
         LA    R14,HISTCOB3               31-BIT ADDRESSING
         O     R14,HISTMASK               HIGH-ORDER X'80'
         DC    X'0B0E'                    SWITCH ADDRESSING MODE
*        BSM   0,R14                      SWITCH ADDRESSING MODE
HISTCOB3 MVC   MSGTEXT2+4(136),0(R5)      MOVE TO 24 BIT STOR DRK MAY99
         LA    R14,HISTCOB4               24-BIT ADDRESSING
         DC    X'0B0E'                    SWITCH ADDRESSING MODE
*        BSM   0,R14                      SWITCH ADDRESSING MODE
HISTCOB4 LA    R5,MSGTEXT2+4              POINT INTO STORAGE
HISTCOB9 LR    R1,R5
         LA    R1,0(,R1)
         AR    R15,R5                     ADDRESS OF CSECT END
         CLC   0(2,R5),HISTLX07           PADDED NOP ENTRY POINT?
         BNE   HISTCEP                    NO, BRANCH
         CLC   14(8,R5),BLANK128          PADDED ENTRY POINT?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,22(,R5)                 GET THE REAL POINTER
         B     HISTCGOT
HISTCEP  CLC   12(8,R5),BLANK128          ENTRY POINT CODING?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,20(,R5)                 GET THE REAL POINTER
HISTCGOT LA    R5,0(,R5)
         CR    R5,R1                      IN THIS CSECT?
         BL    HISTCOB1                   NO, BRANCH
         CR    R5,R15                     IN THIS CSECT?
         BH    HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCGT2 MVC   INSERT#1(70),HIST2HDR      INITIALIZE THE HEADER LINE
         LA    R1,HISTCV2                 ASSUME C2 COMPILER
         CLC   13(4,R5),HISTCV2L          CORRECT?
         BE    HISTCMSG                   YES, BRANCH
         SPACE 1
         LA    R1,HISTCMV                 ASSUME COBOL MVS    DRK MAY99
         CLC   4(4,R5),HISTCCEE           CORRECT?            DRK MAY99
         BNE   *+4+6+6+6+6+6+4            NO                  DRK MAY99
         MVC   INSERT#1+29(5),=C'TIME '   MODIFY HEADER LINE  DRK MAY99
         MVC   COBTIME(4),112(R5)         SAVE COBOL MVS TIME DRK MAY99
         MVC   WORKTMP(20),104(R5)        MOVE TIMESTAMP      DRK MAY99
         NC    WORKTMP(20),=20X'F0'       TURN OFF DIGITS     DRK MAY99
         CLC   WORKTMP(20),=20X'F0'       @TIMEVRS AREA?      DRK MAY99
         BE    HISTCMSG                   YES, COBOL MVS      DRK MAY99
         SPACE 1
         MVC   INSERT#1(70),HISTCHDR      INITIALIZE THE HEADER LINE
         LA    R1,HISTCVS                 ASSUME VS COMPILER
         CLC   20(4,R5),HISTCVSR          CORRECT?
         BE    HISTCMSG                   YES, BRANCH
         LA    R1,HISTCV4                 ASSUME V4 COMPILER
         CLC   20(4,R5),HISTCANS          CORRECT?
         BNE   HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCMSG MVI   MTHIGHL,70                 70 CHARACTER MESSAGE
         TM    FLAGSCC,F1IDR              A COBOL HEADER WRITTEN?
         BNO   HISTCCOM                   NO, BRANCH
         XI    FLAGSCC,F1IDR              HEADER HAS BEEN WRITTEN
         TM    FLAGSHH,FDELAYM            ANY DELAYED HEADER?
         BNO   HISTCMS2                   NO, BRANCH
         XI    FLAGSHH,FDELAYM            MESSAGE WAS WRITTEN
         M$ERRST MSGBLANK
         M$ERRST DELAYMSG
HISTCMS2 CLC   INSERT#1(20),HIST2HDR      COBOL V2?           DRK MAY99
         BE    HISTC210                   YES, BRANCH
         M$MSG L250$1
         MVC   INSERT#1(70),HISTCUND      INITIALIZE THE HEADER LINE
         M$MSG L250$1
         B     HISTCGT2                   FIND THE COMPILER TYPE AGAIN
         SPACE 1
HISTCCOM CLC   INSERT#1(20),HIST2HDR      COBOL V2?           DRK MAY99
         BE    HISTC220                   YES, BRANCH
         MVC   INSERT#1+00(8),ESDNAME     SET THE CSECT NAME
         MVC   INSERT#1+09(3),0(R1)       SET THE COMPILER NAME
         L     R6,60(,R5)                 TGT POINTER
         CLC   X'1BC'(8,R6),HISTCOUT      SYSOUT?
         BE    HISTCBIT                   YES, BRANCH
         M$MSG L500
         B     HISTCOB1
         SPACE 1
HISTCBIT CLI   INSERT#1+9+1,C'4'          V4 COMPILER?
         BE    HISTCBI6                   YES, BRANCH
         MVI   INSERT#1+9+2,C'2'          ASSUME LANGUAGE LEVEL 2
         TM    X'1C5'(R6),X'80'           CORRECT?
         BO    *+8                        YES, BRANCH
         MVI   INSERT#1+9+2,C'1'          MUST BE LANGUAGE LEVEL 1
         SPACE 1
HISTCBI6 TM    X'48'(R6),X'08'            SYMDUMP?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+52(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'04'            FLOW?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+19(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'02'            STATE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+24(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'49'(R6),X'10'            TEST?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+30(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'80'            RESIDENT?
         BZ    HISTCRS2                   YES, BRANCH (INVERSE TEST)
         MVC   INSERT#1+41(03),BLANK128   NO, CLEAR THE DOC
         OI    #TRANOPT,X'80'             MARK NORES FOR LATER
         B     HISTCEN0
HISTCRS2 OI    #TRANOPT,X'40'             MARK RES FOR LATER
         SPACE 1
HISTCEN0 TM    X'4A'(R6),X'40'            ENDJOB?
         BO    HISTCEN2                   YES, BRANCH
         MVC   INSERT#1+45(06),BLANK128   NO, CLEAR THE DOC
         B     HISTCOBJ
HISTCEN2 OI    #TRANOPT,X'20'             MARK ENDJOB FOR LATER
         SPACE 1
HISTCOBJ MVC   INSERT#1+57(03),BLANK128   CLEAR "OBJ"
         TM    X'4A'(R6),X'20'            OBJ370?
         BNO   *+10                       NO, BRANCH
         MVC   INSERT#1+57(03),HISTC370   YES, ADD "370"
         SPACE 1
         MVC   INSERT#1+61(10),BLANK128   CLEAR "OPTIMIZE "
         TM    X'48'(R6),X'01'            COBOL OPTIMIZED?
         BNO   HISTCCPX                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCOPT   YES, ADD "COBOL"
         B     HISTCTWO
         SPACE 1
HISTCCPX CLC   X'48'(5,R5),HISTCCAP       CAPEX?
         BNE   HISTCTWO                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCCAP   YES, ADD "CAPEX"
         L     R5,X'38'(,R5)              PGT POINTER
         SH    R5,=H'8'
         ICM   R5,B'1111',0(R5)           DTECT POSSIBLE?
         BZ    HISTCTWO                   NO, BRANCH
         AR    R5,R6                      POINT TO OPTION BYTE
         TM    0(R5),X'60'                XCOUNT OR PFLOW?
         BZ    HISTCTWO                   YES, BRANCH
         MVC   INSERT#1+61(09),HISTCC$D   NO, ADD "CAP/DTECT"
         SPACE 1
HISTCTWO CLC   INSERT#1(2),HISTCVS        VS COMPILER?
         BE    HISTCFIN                   NO, BRANCH
         TM    X'4A'(R6),X'08'            COUNT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+13(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'10'            TRACE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+35(05),BLANK128   NO, CLEAR THE DOC
HISTCFIN M$MSG L250$1
         B     HISTCOB1
         SPACE 3
HISTC210 M$MSG L260$1
         MVC   INSERT#1(70),HIST2UND      INITIALIZE THE HEADER LINE
         M$MSG L260$1
         B     HISTCGT2                   FIND THE COMPILER TYPE AGAIN
         SPACE 1
HISTC220 DS    0H                         COBOL V2
         CLC   0(3,R1),=C'CEE'            COBOL FOR MVS?      DRK MAY99
         BE    HISTMV20                   YES                 DRK MAY99
         MVC   INSERT#1+00(8),ESDNAME     SET THE CSECT NAME
         MVC   INSERT#1+09(3),0(R1)       SET THE COMPILER NAME
         TM    47(R5),X'80'               TEST?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+13(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    46(R5),X'02'               SSRANGE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+18(06),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    45(R5),X'02'               OPT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+25(03),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    44(R5),X'01'               FDUMP?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+29(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    47(R5),X'04'               ZWB?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+35(03),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    46(R5),X'80'               PFDSN?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+39(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    47(R5),X'40'               TRUNC?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+45(06),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         LA    R1,1                       ASSUME RES
         TM    46(R5),X'20'               RES?
         BO    *+10+4                     YES, BRANCH
         MVC   INSERT#1+51(03),BLANK128   NO, CLEAR THE DOC
         LA    R1,2                       NORES
         O     R1,MSGTEXT2                FLAG FOR LATER
         ST    R1,MSGTEXT2                SAVE FOR LATER
         SPACE 1
         TM    46(R5),X'40'               RENT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+55(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    44(R5),X'04'               DYNAM?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+60(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         MVC   INSERT#1+66(4),HISTC31     DATA 31 ADDRESSING
         TM    44(R5),X'20'               DATA 31?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+66(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         M$MSG L260$1
         B     HISTCOB1
         SPACE 1
HISTMV20 DS    0H                         COBOL FOR MVS       DRK MAY99
         MVC   INSERT#1+00(8),ESDNAME     SET THE CSECT NAME
         MVC   INSERT#1+09(3),0(R1)       SET THE COMPILER NAME
         TM    47+88(R5),X'80'            TEST?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+13(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    46+88(R5),X'02'            SSRANGE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+18(06),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    45+88(R5),X'02'            OPT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+25(03),BLANK128   NO, CLEAR THE DOC
         SPACE 1
*        TM    44+88(R5),X'01'            FDUMP?              DRK MAY99
*        BO    *+10                       YES, BRANCH         DRK MAY99
*        MVC   INSERT#1+29(05),BLANK128   NO, CLEAR THE DOC   DRK MAY99
         MVC   INSERT#1+29(02),COBTIME    HH (COMPILE TIME)   DRK MAY99
         MVI   INSERT#1+31,C':'           :                   DRK MAY99
         MVC   INSERT#1+32(02),COBTIME+2  MM (COMPILE TIME)   DRK MAY99
         SPACE 1
         TM    47+88(R5),X'04'            ZWB?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+35(03),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    46+88(R5),X'80'            PFDSN?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+39(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    47+88(R5),X'40'            TRUNC?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+45(06),BLANK128   NO, CLEAR THE DOC
         SPACE 1
*        LA    R1,1                       ASSUME RES          DRK MAY99
*        TM    46+88(R5),X'20'            RES?                DRK MAY99
*        BO    *+10+4                     YES, BRANCH         DRK MAY99
         MVC   INSERT#1+51(03),BLANK128   NO, CLEAR THE DOC
*        LA    R1,2                       NORES               DRK MAY99
*        O     R1,MSGTEXT2                FLAG FOR LATER      DRK MAY99
*        ST    R1,MSGTEXT2                SAVE FOR LATER      DRK MAY99
         SPACE 1
         TM    46+88(R5),X'40'            RENT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+55(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    44+88(R5),X'04'            DYNAM?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+60(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         MVC   INSERT#1+66(4),HISTC31     DATA 31 ADDRESSING
         TM    44+88(R5),X'20'            DATA 31?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+66(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         M$MSG L260$1
         B     HISTCOB1
         SPACE 1
HISTCOBZ DELETE EPLOC=DIRNAME
         TM    MSGTEXT2+3,2+1             MIXED RES AND NORES?
         BNO   HISTZAP$                   NO, BRANCH
         M$MSG L503                       YES, ERROR MESSAGE
         B     HISTZAP$
         SPACE 2
HISTC31  DC    C'31  '
HISTCTR1 DC    C'5740CB'   COBOL VS, COBOL V4                 DRK MAY99
HISTCTR2 DC    C'566895'   COBOL II, COBOL FOR MVS            DRK MAY99
HISTCTR3 DC    C'5648A2'   COBOL FOR OS/390                   DRK MAY99
HISTLILB DC    C'ILB'                     COBOL SUBROUTINE NAME
HISTCHDR DC    C'CSECT    VER COUNT FLOW STATE TEST TRACE '
         DC    C'RES ENDJOB SYMD OBJ OPTIMIZE  '
HISTCUND DC    C'-----    --- ----- ---- ----- ---- ----- '
         DC    C'--- ------ ---- --- --------  '
HIST2HDR DC    C'CSECT    VER TEST SSRANG OPT FDUMP ZWB '
         DC    C'PFDSN TRUNC RES RENT DYNAM DATA  '
HIST2UND DC    C'-----    --- ---- ------ --- ----- --- '
         DC    C'----- ----- --- ---- ----- ----  '
HISTCVS  DC    C'VS '
HISTCV4  DC    C'V4 '
HISTCV2  DC    C'C2 '
HISTCMV  DC    C'CEE'                                         DRK MAY99
         DS    0F
HISTMASK DC    XL4'80000000'
HISTCANS DC    C'ANS4'
HISTCVSR DC    C'VSR1'
HISTCV2L DC    C' C2 '
HISTCCEE DC    X'00',C'CEE'                                   DRK MAY99
HISTCOUT DC    CL8'SYSOUT'
HISTC370 DC    C'370'
HISTCOPT DC    C'COBOL'
HISTCCAP DC    C'CAPEX'
HISTCC$D DC    C'CAP/DTECT'
SPFSTAT2 DC    CL72'SPF STATS:  VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'
         EJECT
*
*        FORMAT AMASPZAP IDR DATA ENTRIES
*
HISTZAP$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         MVI   MTHIGHL,8
         TM    #TRANOPT,X'80'+X'40'+X'20'  ENDJOB WITH RES AND NORES?
         BNO   HISTZAP                     NO, BRANCH
         M$MSG L502
         SPACE 1
HISTZAP  ICM   R3,B'1111',IDRLINK  END OF ESD CHAIN?
         BZ    HISTUSR$            YES, BRANCH
         CLI   IDRTYPE,IDRZAP      AMASPZAP ENTRY?
         BNE   HISTZAP             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
         SPACE 1
HISTZAP1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTZAP3            YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTZAPP            YES, BRANCH
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTZAP1            NO, SKIP THIS
HISTZAPP CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTZAP1            NO, BRANCH
         SPACE 1
HISTZAP2 LA    R6,L061
         BAS   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         M$ERRST MSGTEXT1
         B     HISTZAP             NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 1
HISTZAP3 CLI   #MODLEN,0           ANY MODULE NAME SELECTION?
         BH    HISTZAP             YES, CANNOT DETERMINE MISSING ESD'S
         LA    R2,HISTZAP4-4       MESSAGE IF CSECT NAME UNKNOWN
         B     HISTZAP2            NEXT IDR DATA RECORD THIS ESD ENTRY
HISTZAP4 DC    C'?UNKNOWN'         UNKNOWN CSECT NAME
         EJECT
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE 1
HISTUSR$ LA    R3,#IDRPTR
         OI    FLAGSCC,F1IDR
         SPACE 1
HISTUSR  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTLKD$            YES, BRANCH
         CLI   IDRTYPE,IDRUSER     USER IDR DATA RECORD?
         BNE   HISTUSR             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          ADDRESS OF ESD CHAIN
         SPACE 1
HISTUSR1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTUSR             YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTUSRP            YES, BRANCH
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTUSR1            NO, BRANCH
HISTUSRP CLC   ESDID,IDRESDID      WANTED ESD RECORD?
         BNE   HISTUSR1            NO, BRANCH
         SPACE 1
         LA    R6,L062
         BAS   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         ICM   R1,B'0001',IDRLDATA ZERO BYTES?
         BZ    HISTUSRS            YES, SKIP THE MOVE (LENGTH ZERO)
         SPACE 1
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(*-*),IDRDATA  <<EXEC>>
         EX    R1,*-6
         SPACE 1
HISTUSRS M$ERRST MSGTEXT1
         B     HISTUSR1
         DROP  R2,R3
         EJECT
*
*        FORMAT THE LINKAGE-EDITOR IDR DATA RECORD
*
HISTLKD$ CLI   #LKEDOPT,X'01'           "LKED" REQUESTED?
         BNE   NEWCMD                   NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN      ANY LKED NAME CHECK?
         BZ    HISTLKDG                 NO, BRANCH
         BCTR  R1,0                     MACHINE LENGTH
         CLC   #LKEDTXT(*-*),LKEDNAME   <<EXECUTED>>
         EX    R1,*-6                   THIS LINKAGE EDITOR?
         BNE   NEWCMD                   NO, BRANCH
HISTLKDG SR    R6,R6
         BAS   R4,HISTHDR
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTHIGHL+4,18+14          LENGTH OF THIS INSERT
         MVC   INSERT#2(10),LKEDNAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),LKEDVVMM(2)
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),LKEDVVMM+1(2)
         MVI   INSERT#2+10+2+2+2+2,C' '                       DRK JUN99
         SPACE 1
         CLC   LKEDTIME(4),=X'0000000F' ANY LINK EDIT TIME?   DRK JUL99
         BE    *+4+6+6+6                NO                    DRK MAY99
         MVC   INSERT#2+10+2+2+2+2(4),=CL4'  AT'              DRK MAY99
         MVC   INSERT#2+10+2+2+2+2+4(10),LKDMASK1             DRK MAY99
         ED    INSERT#2+10+2+2+2+2+4(10),LKEDTIME             DRK MAY99
         SPACE 1
         M$MSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTHIGHL+4,8                    LENGTH OF STANDARD INSERT
         B     NEWCMD
LKDMASK1 DC    X'402120207A20207A2020'     HH:MM:SS           DRK MAY99
         TITLE 'P D S  --  PDS HISTORY IDR SCAN SUBROUTINE   09/28/88'
*
*        IDR SCAN SUBROUTINE
*
         SPACE 3
READIDR  STM   R14,R12,ESDIDRSV    SAVE REGISTERS
         LR    R8,R15              BASE FOR THIS SUBROUTINE
         USING READIDR,R8
         NI    FLAGSCC,255-FIDR    IDR DATA DOES NOT EXIST YET
         LA    R6,IDRNORML         BRANCH ADDRESS IF NOT CONTINUED
         LA    R2,#IDRPTR          BASE FOR IDR LIST
         USING IDRENTRY,R2
         MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
IDREXCP  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     IDREXCP1              00 - GOOD READ
         B     LASTIDR               04 - END OF MEMBER
         B     LASTIDR               08 - END OF DATA SET
         B     NEWCMD                12 - I/O ERROR
         SPACE 1
IDREXCP1 LR    R15,R0              START OF RECORD
         TM    0(R15),X'50'        TEST SYM OR SCATTER RECORD?
         BM    IDREXCP             YES, SKIP IT
         SPACE 1
         CLI   0(R15),X'20'        CESD RECORD?
         BE    IDREXCP             YES, SKIP
         SPACE 1
         CLI   0(R15),X'80'        IDR RECORD?
         BNE   LASTIDR             NO, BRANCH
         SPACE 1
         LA    R3,3(,R15)          START OF IDR DATA
         SR    R5,R5
         IC    R5,1(,R15)          BYTE COUNT THIS RECORD
         LA    R5,0(R5,R15)        END OF BUFFER ADDRESS
         BR    R6                  BRANCH TO IDRNORML OR AS CONTINUED
         SPACE 2
IDRNORML TM    2(R15),IDRLKED      LINKAGE EDITOR IDR RECORD?
         BO    LKEDIDR             YES, BRANCH
         TM    2(R15),IDRTRAN      TRANSLATOR IDR RECORD?
         BO    TRANIDR             YES, BRANCH
         TM    2(R15),IDRZAP       AMASPZAP IDR RECORD?
         BO    ZAPIDR              YES, BRANCH
         TM    2(R15),IDRUSER      USER-SUPPLIED IDR RECORD?
         BNO   NEXTIDR             NO, NEXT RECORD
         EJECT
*  USER-SUPPLIED IDR RECORDS:
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
*   0 - 1  ×  2 - 4          ×  5             ×  6 - 6+COUNT          *
*   ESDID  ×  IDENTIFY DATE  ×  COUNT (0-40)  ×  IDRDATA              *
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
         SPACE 1
USERIDR  CLI   #USEROPT,0          "USERDATA"?
         BE    NEXTIDR             NO, BRANCH
         BAS   R14,GETIDR          GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1               ADDRESS OF NEW RECORD
         SPACE 2
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH MOVED
         CH    R4,=H'5'            CONTINUED USER HEADER?
         BNL   USER20              NO, BRANCH
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R4,*-6              MOVE IN DATA
         BAS   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         LA    R14,IDRUPREF+1(R4)  TARGET OF MOVE
         LA    R15,5-1(,R3)        LENGTH BYTE ADDRESS-1 IF NOT SPLIT
         SR    R15,R4              AMOUNT WE ALREADY HAVE
         SR    R1,R1
         IC    R1,0(,R15)          LENGTH BYTE
         LA    R15,6-2(,R1)        MACHINE LENGTH OF RECORD -1
         SR    R15,R4              MACHINE LENGTH TO MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         B     USER30
         SPACE 2
USER20   SR    R15,R15
         IC    R15,5(,R3)          LENGTH OF USER DATA AREA
         LA    R15,5(,R15)         MACHINE LENGTH FOR MOVE
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         SPACE 1
USER30   LA    R4,1(,R15)          ACTUAL RECORD LENGTH
         MVI   IDRTYPE,IDRUSER     USER IDR DATA IDENTIFIER
         MVC   IDRESDID,IDRUPREF   ADD THE CSECT ID
         LA    R1,IDRUPREF+2       YYDDD IDR RECORD CREATED
         LA    R15,IDRDATE
         BAS   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SR    R15,R15
         IC    R15,IDRUPREF+5      LENGTH OF USER DATA AREA
         BCTR  R15,0
         STC   R15,IDRLDATA
         SPACE 2
USER40   BXLE  R3,R4,USERIDR
         EJECT
         SR    R3,R5
         BCTR  R3,0                ACTUAL REMAINING LENGTH
         LTR   R4,R3               CONTINUED DATA PORTION?
         BNP   NEXTIDR             NO, BRANCH
         BAS   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRDATA+1(R14)  END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R4,1(,R4)           ACTUAL LENGTH OF LAST PORTION
         B     USER40
         SPACE 4
*  AMASPZAP IDR RECORDS:
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
*  0 - 1  ×  2 - 4     ×  5 - 12                                      *
*  ESDID  ×  ZAP DATE  ×  IDRDATA OR USER IDENTIFICATION              *
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
         SPACE 1
ZAPIDR   CLI   #ZAPOPT,X'01'       "ZAP"?
         BNE   NEXTIDR             NO, BRANCH
         SR    R4,R4
         NI    0(R3),X'3F'
         IC    R4,0(R3)            GET COUNT OF AMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R4,1(R4)            JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  BAS   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         MVC   IDRESDID,0(R3)      MOVE ESDID TO IDR RECORD
         LA    R1,2(R3)            ADDRESS OF DATE OF RECORD
         LA    R15,IDRDATE
         BAS   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SPACE 1
         MVC   IDRDATA,5(R3)       MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8          SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP      INDICATE AMASPZAP ENTRY
         LA    R3,13(R3)           JUMP DATA ADDRESS
         SPACE 1
ZAPIDR1  BCT   R4,ZAPIDR2          IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR             READ NEXT IDR RECORD
         EJECT
*  TRANSLATOR IDR RECORDS:
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*  0 - 1 ×  2 - 3 ×  N - N+1          ×  0    (0 IS 1 TRANSLATOR)  ×
*  ESDID ×  ...   ×  X'80'+LAST ESDID ×  FLAG (1 IS 2 TRANSLATORS) ×
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
*    1 - 10      ×  11 - 12  ×  13 -15     ×  16 - 30                 *
*    TRANSLATOR  ×  VVMM     ×  TRANS DATE ×  OPTIONAL, SAME AS 1-15  *
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
         SPACE 1
TRANIDR  CLI   #TRANOPT,0          NOTRAN?
         BE    NEXTIDR             YES, BRANCH
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH OK?
         BM    NEXTIDR             NO, DONE WITH THIS RECORD
         BAS   R14,GETIDR
         ST    R1,IDRLINK          CHAIN ON A
         LR    R2,R1                         NEW IDR RECORD
         SPACE 1
         MVI   IDRTYPE,IDRTRAN     TRANSLATOR TYPE RECORD
         LTR   R4,R4               MACHINE LENGTH OK?
         BP    TRAN20              YES, NOT SPLIT
         MVC   IDRESDID(1),0(R3)   NO, ONLY ONE ESDID BYTE
         BAS   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         MVC   IDRESDID+1(1),0(R3) SECOND BYTE OF ESDID
         BCTR  R3,0                BACK UP THE INPUT POINTER BY ONE
         B     TRAN30
         SPACE 2
TRAN20   MVC   IDRESDID(2),0(R3)   ESDID FIELD
         SPACE 1
TRAN30   MVC   IDRUPREF(2),IDRESDID  ORIGINAL ESDID
         NI    IDRESDID,FF-X'80'   TURN OFF THE HIGH-ORDER BIT
         TM    IDRUPREF,X'80'      LAST ESDID?
         BO    TRAN50              YES, BRANCH
         LA    R4,2                ESDID WIDTH
         BXLE  R3,R4,TRANIDR       CONTINUE FOR ALL ESDID'S
         B     NEXTIDR             GET THE NEXT RECORD
         SPACE 1
TRAN50   LA    R3,2(,R3)
         LR    R4,R5
         SR    R4,R3               FLAG BYTE INPUT?
         BNM   TRAN60              YES, BRANCH
         BAS   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
TRAN60   LA    R1,14               ASSUME ONE TRANSLATOR
         CLI   0(R3),0             CORRECT?
         BE    *+8                 YES, BRANCH
         LA    R1,29               NO, TWO TRANSLATORS
         MVC   IDRTDATA(30),1(R3)  MOVE IN THE MAXIMUM LENGTH
         STC   R1,IDRLDATA         MACHINE LENGTH
         LA    R4,2(,R1)           ACTUAL SEGMENT LENGTH
TRAN70   BXLE  R3,R4,TRAN80
         EJECT
         LR    R4,R3
         SR    R4,R5               BYTES LEFT +1
         S     R4,=F'1'            ANY ACTUAL REMAINING BYTES?
         BNP   TRAN80              NO, BRANCH
         BAS   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRTDATA+1(R14) END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R3,1(R3,R4)         POSITION FOR THE NEXT SEGMENT
         SPACE 2
TRAN80   LA    R1,IDRTDATA+12      ADDRESS OF CREATION YYDDD
         LA    R15,IDRDATE
         BAS   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         LA    R1,#IDRPTR          SCAN IDR CHAIN
         SPACE 1
TRAN90   ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)
         BZ    TRANIDR                        DONE, BRANCH
         CLI   IDRTYPE-IDRENTRY(R1),IDRTRAN   TRANSLATOR TYPE?
         BNE   TRAN90                         NO, BRANCH
         CLI   IDRDATE-IDRENTRY(R1),0         ANY DATE YET?
         BNE   TRAN90                         YES, BRANCH
         MVC   IDRDATE-IDRENTRY(3+6+1+30,R1),IDRDATE
         B     TRAN90
         DROP  R2
         SPACE 3
*  LINKAGE EDITOR IDR RECORDS:                             APAR OW29593
*                                                             ×
*                                                             V
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  -- +--  --  -- -*
*  0 - 9                ×  10 - 11  ×  12 - 14           ×  15-18     *
*  LINKAGE EDITOR NAME  ×  VVMM     ×  LINKAGE EDIT DATE ×  LKED TIME *
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  -- +--  --  -- -*
         SPACE 1
LKEDIDR  OI    FLAGSCC,FIDR
         MVC   LKEDTIME(4),=X'0000000F' ASSUME NO TIME        DRK JUL99
         CLI   1(R15),X'15'        LKED TIME PRESENT?         DRK MAY99
         BL    *+4+6               NO                         DRK MAY99
         MVC   LKEDTIME(4),15(R3)  YES                        DRK MAY99
         LA    R1,12(R3)           ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAS   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         MVC   LKEDNAME(10),0(R3)  LINKAGE EDITOR NAME
         MVC   LKEDVVMM(2),10(R3)  VV.MM IN HEX
         SPACE 2
NEXTIDR  LA    R6,IDRNORML         NOT A CONTINUED RECORD
         SPACE 1
IDRCONT  B     IDREXCP             CONTINUED RECORD
         EJECT
LASTIDR  MVC   RLDCOUNT(1),3(R15)    SAVE THE FIRST RLD/CONTROL AMOUNT
         OC    #ZAPLEN(3),#ZAPLEN    ANY IDR FILTERING?
         BZ    LASTIDRZ              NO, BRANCH
         SPACE 2
         USING IDRENTRY,R3
         SR    R4,R4
         LA    R3,#IDRPTR            SCAN IDR CHAIN
IDRFIL10 LR    R2,R3                 PREVIOUS ENTRY
         ICM   R3,B'0111',IDRLINK+1  CURRENT ENTRY?
         BZ    LASTIDRZ              NO, BRANCH
         ICM   R4,B'0111',IDRLINK+1  NEXT ENTRY
         SPACE 2
         SR    R15,R15               COMPARE STRING LENGTH
         CLI   IDRTYPE,IDRTRAN       IDR TYPE:
         BH    IDRFIL40                USER, BRANCH
         BL    IDRFIL30                ZAP, BRANCH
         LA    R1,#TRANTXT             TRANSLATOR
         SPACE 1
         ICM   R15,B'0001',#TRANLEN  ANY TRANSLATOR FILTERING?
         BZ    IDRFIL10              NO, BRANCH
         CLI   IDRLDATA,15           ONE TRANSLATOR ONLY?
         BL    IDRFIL60              YES, BRANCH
         BCTR  R15,0                 COMPARE MACHINE LENGTH
         CLC   IDRTDATA+15(*-*),#TRANTXT  <<EXECUTED>>
         EX    R15,*-6                    THIS PL/S TRANSLATOR?
         BE    IDRFIL10                   YES, BRANCH
         IC    R15,#TRANLEN          COMPARE STRING LENGTH (AGAIN)
         B     IDRFIL60              CHECK THE FIRST TRANSLATOR TOO
         SPACE 2
IDRFIL30 IC    R15,#ZAPLEN           ZAP IDR RECORD
         LA    R1,#ZAPTXT
         B     IDRFIL60
         SPACE 2
IDRFIL40 IC    R15,#USERLEN          USER IDR RECORD
         LA    R1,#USERTXT
         SPACE 2
IDRFIL60 S     R15,=F'1'             VALID MACHINE LENGTH?
         BM    IDRFIL10              NO, BRANCH
         SPACE 1
         CLC   IDRDATA(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6               PARTIAL NAME MATCH?
         BE    IDRFIL10              YES, BRANCH
         SPACE 1
         LR    R3,R2                 NO, UNCHAIN THE
         ST    R4,IDRLINK                           CURRENT ENTRY
         B     IDRFIL10
         EJECT
LASTIDRZ LA    R3,#IDRPTR          PERFORM AN IDR RECORD SORT
         SPACE 2
SORTIDR  ICM   R3,B'1111',IDRLINK
         BZ    SORTIDRZ
         LR    R1,R3
         SPACE 1
SORTIDR2 ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)    GET THE NEXT ENTRY
         BZ    SORTIDR
         SPACE 1
         SR    R14,R14                    CURRENT             Y2K NOV99
         SR    R15,R15                    NEXT                Y2K NOV99
         CLI   IDRDATE+2,X'&WIN19XX'      < 19XX WINDOW?      Y2K NOV99
         BNL   *+4+4                      NO,  THEN 19XX      Y2K NOV99
         ICM   R14,B'0001',=X'01'         YES, THEN 20XX      Y2K NOV99
         CLI   IDRDATE-IDRENTRY+2(R1),X'&WIN19XX' < 19XX?     Y2K NOV99
         BNL   *+4+4                      NO,  THEN 19XX      Y2K NOV99
         ICM   R15,B'0001',=X'01'         YES, THEN 20XX      Y2K NOV99
*        SORT BY DATE (DESCENDING ORDER):  CYYMMDD            Y2K NOV99
         CLR   R14,R15                            C:C         Y2K NOV99
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - SWITCH
         ICM   R0,B'1110',IDRDATE                 MMDDYY      Y2K NOV99
         CLM   R0,B'0010',IDRDATE-IDRENTRY+2(R1)  YY:YY       Y2K NOV99
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - SWITCH
         CLM   R0,B'1000',IDRDATE-IDRENTRY+0(R1)  MM:MM       Y2K NOV99
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - SWITCH
         CLM   R0,B'0100',IDRDATE-IDRENTRY+1(R1)  DD:DD       Y2K NOV99
         BNL   SORTIDR2                           HIGH/EQUAL - BRANCH
         SPACE 1
SORTIDR3 XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         B     SORTIDR2
         SPACE 1
SORTIDRZ LM    R14,R12,ESDIDRSV    RESTORE REGISTERS
         TM    FLAGSCC,FIDR        ANY IDR RECORDS?
         BZR   R14                 NO, RETURN AT +0
         B     4(,R14)             YES, RETURN AT +4
         DROP  R3
         SPACE 2
GETIDR   CLC   ##SUBCOM(8),$ATTL     ATTRIB SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         L     R0,=A(IF)             START OF IF SUBCOMMAND
         CLM   R0,B'0111',##ADRCMD+1 REALLY IF SUBCOMMAND?
         BE    GETIDRM               YES, BRANCH
         CLC   ##SUBCOM(8),$MML      MEMLIST SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         CLC   ##SUBCOM(8),$ML       ML SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
GETIDRM  MVI   SUBPOOLT,21
         LA    R0,LENIDR
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENIDR,R1),0(R1)
         OI    FLAGSCC,FIDR          IDR DATA EXISTS
         BR    R14
HISTL450 DC    H'450'
HISTLX07 DC    X'0700'
