/* Rexx */
/*
   Offload members in MEMLIST table to a sequential dataset.

   Invoke this exec from the command line of the PDS 8.5
   "MEMLIST Source Member List" table display.

   Add the following line to panel P85OPUX:

     OFFLOAD,'CMD(%OFFLOAD &PDSDSN &PDSTNAME)'
*/
Trace
Arg pdsdsn pdstname

offload_pgm = "OFFLOAD"   /* OFFLOAD pgm name, CBT File 093 */

Address "ISPEXEC"
"CONTROL ERRORS RETURN"
"TBTOP" pdstname
If rc > 0 Then Do
  zmsg000l = "TBTOP failed for table" pdstname "RC="rc
  "SETMSG MSG(ISPZ000)"
  Exit rc
  End

If Left(pdstname,3) /= 'PDM' Then Do
  zmsg000l =  "Table is not MEMLIST Source Member List."
  "SETMSG MSG(ISPZ000)"
  Exit
  End

Address "TSO"
"NEWSTACK"

rows = 0
Address "ISPEXEC"
Do Forever
  "TBSKIP" pdstname
  If rc > 0 Then Leave
  "TBGET" pdstname
  If rc > 0 Then Leave
  rows = rows + 1
  Queue " S M="memlname
  End

Address "TSO"
x = OUTTRAP("delete.","*")
"DELETE OFFLOAD."pdstname
x= OUTTRAP("off")
"ALLOC F(SYSIN) UNIT(VIO) NEW REUSE SPACE(1,1) TRACKS",
  "LRECL(80) RECFM(F B) BLKSIZE(0)"

Push " O I=IN,O=OUT,T=IEBUPDTE"
Queue ""

"EXECIO * DISKW SYSIN (FINIS"
"DELSTACK"
"ALLOC F(IN) DA('"pdsdsn"') SHR REUSE"
"ALLOC F(OUT) DA(OFFLOAD."pdstname") NEW REUSE UNIT(SYSALLDA)",
  "SPACE(15,15) TRACKS RECFM(F B) LRECL(80) BLKSIZE(6160)"
"ALLOC F(SYSPRINT) DUMMY REUSE"

Address "LINKMVS" offload_pgm

Address "TSO"
"ALLOC F(SYSPRINT) DA(*) SHR REUSE"
"ALLOC F(SYSIN)    DA(*) SHR REUSE"
"FREE  F(IN OUT)"

Address "ISPEXEC"
"ISPEXEC CONTROL ERRORS RETURN"
"ISPEXEC BROWSE DATASET(OFFLOAD."pdstname")"
"ISPEXEC CONTROL ERRORS CANCEL"

If rows = 0 Then
  zmsg000l =  "All members processed by OFFLOAD."
ELSE
  zmsg000l =  rows "members processed by OFFLOAD."
Address "ISPEXEC"
"SETMSG MSG(ISPZ000)"
Exit

/* J. Kalinich, X4521 */
/* Offload members in MEMLIST table to a sequential dataset      */
