         TITLE 'P D S  --  PDS CHANGE                        09/28/88'
***********************************************************************
***      CHANGE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
CHANGE   CSECT
         USING *,R8
         L     R1,=A(DSNAMES)           ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         SPACE 2
CHANGE2  L     R8,=A(CHANGE)            BASE REGISTER
         OI    FLAGSJJ,FCHANGEB         CHANGE DATA SET BUFFERING
         NI    FLAGSFF,FF-FCHANGE       DATA SET WAS CHANGED
         LA    R0,RESTART0              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         OI    SPFLAG3,SPFCHN           SIGNAL ISPMODE         SS SEP84
         BAS   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAS   R2,CLOSEIT               CLOSE THE DATA SET
         BAS   R2,DEALLDCB              FREE THE DATA SET
         TM    FLAGSHH,FOPTIONS         ANY OPERANDS?
         BO    RESTART0                 YES, BRANCH
         SPACE 2
         OI    FLAGSBB,FNOTFILE         NO FILE NAME NOW
         CLI   SAVDSN+1+2,0             ANY OTHER DSNAME?
         BE    RESTART0                 NO, DONE
         SPACE 2
         XC    SAVDSN+1(SAVLEN),DSNLEN  EXCHANGE
         XC    DSNLEN(SAVLEN),SAVDSN+1          OLD AND NEW
         XC    SAVDSN+1(SAVLEN),DSNLEN                     DSNAMES
         MVC   DSPALLOC,SAVDSN          GET PREVIOUS DISPOSITION
         MVC   SAVDSN(1),DSPREQST       SAVE REQUESTED DISPOSITION
         MVC   DSPREQST,DSPALLOC        GET PREVIOUS DISPOSITION
         B     RESTART0                 BRANCH TO REALLOCATE
