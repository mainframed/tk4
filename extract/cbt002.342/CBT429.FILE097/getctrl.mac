       IDENTIFICATION DIVISION.
       PROGRAM-ID.
                   GETCTRL
                   CALLABLE SUB-ROUTINE.
      *PURPOSE - TO OPEN A CONTROL FILE, LOCATE THE APPROPRIATE CONTROL
                 RECORD, MOVE IT TO WORKING STORAGE, CLOSE THE FILE.
      *METHOD  - IF MORE THAN ONE CONTROL RECORD IS PRESENT IN THE FILE,
                 WITH THE CALLING PROGRAM'S ID IN THE NAME FIELD
                 THE LAST ONE WILL BE IN THE CALLING PROGRAM'S WORKING
                 STORAGE AREA WHEN CONTROL IS RETURNED.
                 THE CALLING PROGRAM MUST SET UP, IN WORKING STORAGE,
                 AN 80 BYTE FIELD IN THE SAME FORMAT AS CTRL001.
                 YOU MAY INCLUDE CTRL001.  IN PROCEDURE DIVISION,
                 MOVE YOUR PROGRAM ID TO THE ID FIELD IN THAT AREA.
                 PRIOR TO CALLING 'GETCTRL', AN EDIT OF THE CONTROL
                 RECORD MAY BE INVOKED BY MOVING 'EDIT' TO CTRL-CARD.
                 IF 'EDIT' IS NOT MOVED TO CTRL-CARD SPACES SHOULD BE
                 MOVED TO THAT AREA TO CLEAR IT.
                 IF 'MM/DD/YY' IS THE DATE IN THE CONTROL RECORD AND
                 'EDIT' IS LOCATED IN THE FIRST FOUR CHARACTERS OF
                 CTRL-CARD, THE SYSTEM DATE WILL BE SUBSTITUTED BY
                 'GETCTRL'.  IF A DATE IS PRESENT IT WILL BE TESTED FOR
                 TWO NUMERIC CHARACTERS FOLLOWED BY A SLASH FOLLOWED
                 BY TWO NUMERIC CHARACTERS FOLLOWED BY A SLASH
                 FOLLOWED BY TWO MORE NUMERIC CHARACTERS.  IF THE TESTS
                 NOT MET, AN ABEND WILL OCCUR WITH A USER CODE OF 1004.
                 A DD CARD MUST BE INCLUDED IN JCL AS FOLLOWS:
                   FOR CARD INPUT,
                 //CTRLFILE  DD    *
                 (THE CONTROL CARD/S GO HERE)
                 /*
                   FOR DISK INPUT,
                 //CTRLFILE  DD    DSN=(               )
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           IBM-370.
       OBJECT-COMPUTER.
           IBM-370.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CONTROL-FILE
               ASSIGN TO UT-S-CTRLCTRL.
       DATA DIVISION.
       FILE SECTION.
       FD  CONTROL-FILE
           RECORDING MODE IS F
           LABEL RECORDS ARE STANDARD
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           DATA RECORD IS CTRL-RECORD.
       01  CTRL-RECORD.
           02  CR-CTRL             PIC XXXX.
           02  FILLER              PIC X.
           02  CR-PROG-ID          PIC X(8).
           02  FILLER              PIC X(67).
       WORKING-STORAGE SECTION.
       01  USER-RC                 PIC S9999  COMP SYNC  VALUE +0.
       01  EOF-SWITCH              PIC X   VALUE LOW-VALUES.
           88  CTRL-EOF    VALUE IS HIGH-VALUES.
       01  FOUND-SWITCH            PIC X   VALUE LOW-VALUES.
           88  CTRL-NOT-FOUND  VALUE IS LOW-VALUES.
       01  EDIT-SWITCH             PIC X VALUE LOW-VALUES.
           88  EDIT-ON  VALUE IS HIGH-VALUES.
       01  TEST-DATE.
           02  TEST-MO             PIC 99.
           02  TEST-SL-1           PIC X.
           02  TEST-DA             PIC 99.
           02  TEST-SL-2           PIC X.
           02  TEST-YR             PIC 99.
       LINKAGE SECTION.
      ************************************************************
      *              CTRL001,BRFB *
      * **********************************************************
           SKIP1
       01  CTRL-CARD.
           02  CTRL-TYPE           PIC XXXX.
           02  FILLER              PIC X.
           02  CTRL-PID            PIC X(8).
           02  FILLER              PIC X.
           02  CTRL-BANK           PIC XXX.
           02  FILLER              PIC X.
           02  CTRL-DATE           PIC X(8).
           02  FILLER              PIC X.
           02  CTRL-SWITCHES.
               03  SW-1            PIC X.
               03  SW-2            PIC X.
               03  SW-3            PIC X.
               03  SW-4            PIC X.
               03  SW-5            PIC X.
               03  SW-6            PIC X.
               03  SW-7            PIC X.
               03  SW-8            PIC X.
               03  SW-9            PIC X.
               03  SW-10           PIC X.
      *  FOLLOWING FIELDS MATCH DDA CTRL CARD FORMAT *
           02  CTRL-USER-AREA.
               03  FILLER          PIC X.
               03  CTRL-CUR-DT.
                   04  CTRL-CUR-MM     PIC XX.
                   04  CTRL-CUR-DD     PIC XX.
                   04  CTRL-CUR-YY     PIC XX.
               03  CTRL-PREV-DT.
                   04  CTRL-PREV-MM    PIC XX.
                   04  CTRL-PREV-DD    PIC XX.
                   04  CTRL-PREV-YY    PIC XX.
               03  CTRL-DAYS-SINCE-LAST-PROC   PIC 9.
               03  CTRL-NEXT-DT.
                   04  CTRL-NEXT-MM    PIC XX.
                   04  CTRL-NEXT-DD    PIC XX.
                   04  CTRL-NEXT-YY    PIC XX.
               03  CTRL-DAYS-TO-NEXT-PROC      PIC 9.
               03  CTRL-FILLER-1   PIC X(13).
               03  FILLER REDEFINES CTRL-FILLER-1.
                   04  FILLER      PIC X.
                   04  CTRL-2ND-NEXT-DATE.
                       05  CTRL-2ND-NEXT-MM  PIC XX.
                       05  CTRL-2ND-NEXT-DD  PIC XX.
                       05  CTRL-2ND-NEXT-YY  PIC XX.
                   04  FILLER      PIC X(6).
           02  CTRL-COL-72         PIC X.
           02  CTRL-COL-73-80      PIC X(8).
      ******************************************************************
      *
       PROCEDURE DIVISION USING CTRL-CARD.
      *
           MOVE SPACES TO TEST-DATE.
           IF CTRL-TYPE = 'EDIT',
                   MOVE HIGH-VALUES TO EDIT-SWITCH.
           OPEN INPUT CONTROL-FILE.
           MOVE LOW-VALUES TO EOF-SWITCH, FOUND-SWITCH.
           PERFORM READ-CONTROL THRU RC-EXIT UNTIL CTRL-EOF.
           CLOSE CONTROL-FILE.
           IF CTRL-NOT-FOUND,
                   MOVE -1004 TO USER-RC,
                   CALL 'USERDUMP' USING USER-RC.
           MOVE CTRL-DATE TO TEST-DATE.
           IF EDIT-ON,
               AND TEST-DATE = 'MM/DD/YY',
                   MOVE CURRENT-DATE TO TEST-DATE, CTRL-DATE.
           IF EDIT-ON AND (TEST-MO NOT NUMERIC,
                           OR TEST-SL-1 NOT = '/',
                           OR TEST-DA NOT NUMERIC,
                           OR TEST-SL-2 NOT = '/',
                           OR TEST-YR NOT NUMERIC),
                   MOVE -1004 TO USER-RC,
                   CALL 'USERDUMP' USING USER-RC.
           GOBACK.
      *
       READ-CONTROL.
           READ CONTROL-FILE AT END
                   MOVE HIGH-VALUES TO EOF-SWITCH,
                   GO TO RC-EXIT.
           IF CR-CTRL = 'CTRL',
               AND CR-PROG-ID = CTRL-PID,
                   MOVE CTRL-RECORD TO CTRL-CARD,
                   MOVE HIGH-VALUES TO FOUND-SWITCH.
       RC-EXIT.        EXIT.
