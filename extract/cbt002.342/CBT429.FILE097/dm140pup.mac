         TITLE 'DM140PUP - VVDS EXTRACT'
DM140PUP CSECT
*
         REGS
*
*   SCAN THE VVDS OF SELECTED VOLUMES
*   PROCESS ONLY 'Z' (VVR PRIMARY) AND 'Q' (VVR SECONDARY) RECORDS
*   (Q RECORDS NOT USED IN THIS VERSION)
*   PRODUCE A CHARACTER FIXED FORMAT EXTRACT FILE
*
*   REF:  LY26-3956 - MVS/EXTENDED ARCHITECTURE CATALOG
*                     DIAGNOSIS REFERENCE
*
         USING DM140PUP,R15
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'DM140PUP - VVDS EXTRACT &SYSDATE'
         EJECT
MAINEP   DS    0H
         SAVE  (14,12)
         LR    R12,R15        ENTRY ADR TO BASE REG
         DROP  R15
         USING DM140PUP,R12   PGM BASE
         LR    R2,R13
         LA    R13,SAVEAREA   SAVEAREA AND WORKING STORAGE
         USING SAVEAREA,R13   ADDRESSABILITY
         ST    R2,SAVEAREA+4  SAVE CALLERS R13
         ST    R13,8(,R2)     BACK LINK SAVEAREA
         USING IHADCB,R6      ADDRESSABILITY
         LA    R6,EXTRACT     A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROEXT        NO
         DROP  R6
         GENCB BLK=ACB,                                                X
               AM=VSAM,                                                X
               DDNAME=XXXXXXXX,                                        X
               MACRF=(ADR,SEQ)
         ST    R1,ADDRACB     SAVE A(ACB)
         LR    R6,R1          POST TO NEW REG
         L     R0,BLRECL      POST REC LEN OF BUFFER
         GETMAIN R,LV=(0)     GETMAIN A RECORD AREA
         ST    R1,ADDRVREC    SAVE
         LR    R7,R1          POST TO A MORE CONVENIENT REG
         L     R8,BLRECL      POST LRECL
         GENCB BLK=RPL,                                                X
               ACB=(6),                                                X
               AM=VSAM,                                                X
               AREA=(7),                                               X
               AREALEN=(8),                                            X
               OPTCD=(ADR,SEQ)
         ST    R1,ADDRRPL     SAVE A(RPL)
         EXTRACT TIOT,FIELDS=TIOT GET ADDRESS OF TIOT
         L     R2,TIOT        LOAD ADDRESS OF TIOT IN REG2
         LA    R2,24(R2)      SKIP TO DD ENTRIES
         ST    R2,NEXTDD      SAVE FIRST DD TO INITIALIZE LOOP
         TITLE 'TIOTLOOP - SCAN TIOT'
*                                                                     *
***********************************************************************
*                                                                     *
*        SCAN TIOT FOR DDNAMES 'VVDSXXXX' WHERE X CAN BE ANYTHING     *
*        WHEN FINDING ONE TAKE OFF SCAN FOR VVR RECORDS               *
*        WHEN HITTING END OF TIOT GO TO END OF JOB                    *
*                                                                     *
***********************************************************************
*                                                                     *
TIOTLOOP EQU   *
         L     R2,NEXTDD      PICK UP CURRENT DD SEG
         SR    R3,R3          CLEAR REG3
         IC    R3,0(R2)       DETERMINE IF END OF TIOT
         LTR   R3,R3          END OF TIOT?
         BZ    EXIT           GO TO EOJ NORMAL
         LA    R1,0(R2,R3)    ADDR OF NEXT TIOT
         ST    R1,NEXTDD      SAVE
         CLC   =C'VVDS',4(R2) IS THIS A VALID DD ENTRY
         BNE   TIOTLOOP       NO -- SKIP IT
         MVC   DDNAME,4(R2)   SAVE DDNAME
         L     R6,ADDRACB     POST A(ACB)
         MODCB ACB=(R6),                                               X
               DDNAME=(*,DDNAME)
         OPEN  ((6))          OPEN FILE
         CH    R15,=H'4'      ABOVE 'WARNING'
         BH    ERROPV         YES -- ERROR
         SHOWCB ACB=(6),                                               X
               AREA=RLRECL,                                            X
               LENGTH=L'RLRECL,                                        X
               FIELDS=LRECL
         L     R6,ADDRRPL     POST A(RPL)
         CLC   RLRECL,BLRECL  CHECK BUF SIZE
         BNH   VVDSSCAN       NO -- OK
         L     R1,ADDRVREC    A(REC AREA)
         L     R0,BLRECL      L'REC AREA
         FREEMAIN R,LV=(0),A=(1)
         L     R0,RLRECL      NEW L'REC AREA
         ST    R0,BLRECL      POST
         GETMAIN R,LV=(0)     GETMAIN A RECORD AREA
         ST    R1,ADDRVREC    SAVE
         LR    R7,R1          POST TO A MORE CONVENIENT REG
         L     R8,BLRECL      POST TO A MORE CONVENIENT REG
         MODCB RPL=(R6),                                               X
               AREA=(R7),                                              X
               AREALEN=(R8)
VVDSSCAN EQU   *
         MVI   SWV,C'O'       POST FILE OPEN
         BAL   R14,SCAN       SCAN THE VVDS
         L     R6,ADDRACB     POST A(ACB)
         CLOSE ((R6))         CLOSE THIS FILE
         B     TIOTLOOP       LOOP BACK
         TITLE 'SCAN     - SCAN THE VVDS FOR VVR RECS '
SCAN     EQU   *              PROCESS NEXT REC
         ST    R14,SCANR14    SAVE RETURN ADDR
         BAL   R14,GETV       GET VVCR (IGNORE)
         CLI   SWV,C'E'       EOF?
         BE    ERRNOCR        YES -- ERROR
         BAL   R14,GETV       GET SELF DEFINING VVR
         CLI   SWV,C'E'       EOF?
         BE    ERRNOSDR       YES -- ERROR
         L     R7,ADDRVREC    REFRESH RECORD ADDR
         BAL   R14,SVVR       PROCESS SELF DEFINING VVR REC
SCANLOOP EQU   *
         BAL   R14,GETV       GET NEXT VVR REC
         CLI   SWV,C'E'       EOF
         BE    SCANEXIT       YES -- DONE
         L     R7,ADDRVREC    REFRESH RECORD ADDR
         USING DSE9,R7        ADDRESSABILITY
         CLI   VVRTYPE,C'Z'   PRIMARY
         BE    SCANZ          YES -- PROCESS
         CLI   VVRTYPE,C'Q'   SECONDARY
         BE    SCANQ          YES -- PROCESS
         B     SCANLOOP       DROP REC -- LOOP BACK
SCANZ    EQU   *
         BAL   R14,PVRZ       PROCESS Z REC
         B     SCANLOOP       LOOP BACK
SCANQ    EQU   *
         BAL   R14,PVRQ       PROCESS Q REC
         B     SCANLOOP       LOOP BACK
SCANEXIT EQU   *
         L     R14,SCANR14    RESTORE RETURN ADDR
         BR    R14            DONE -- EXIT
         TITLE 'SVVR     - PROCESS SELF DEFINING VVR REC'
SVVR     EQU   *
         ST    R14,SVVRR14    SAVE RETURN ADDR
         USING DSE9,R7        ADDRESSABILITY
         CLI   VVRTYPE,C'Z'   PRIMARY
         BNE   ERRSVVRZ       NO -- ERROR
         TM    VVRFLAG,B'01000000' SELF DEFINING VVR
         BZ    ERRSVVRF       NO -- ERROR
         MVC   ERECVOL,VVRCMPNM+11 PICK UP VOLSER FROM VVR DSN
         BAL   R14,PVRZ       PROCESS Z REC
         L     R14,SVVRR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PVRQ     - PROCESS VVR RECORD TYPE Q'
PVRQ     EQU   *
         ST    R14,PVRQR14    SAVE RETURN ADDR
         L     R14,PVRQR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PVRZ     - PROCESS VVR RECORD TYPE Z'
PVRZ     EQU   *
         ST    R14,PVRZR14    SAVE RETURN ADDR
         USING DSE9,R7        ADDRESSABILITY
         BAL   R14,PCE9       PROCESS CELL E9
         AH    R7,VVRHDLEN    BUMP TO NEXT CELL
         LA    R7,L'VVRLEN(R7) ALLOW FOR VVRLEN
         USING DS21,R7        ADDRESSABILITY
         BAL   R14,PC21       PROCESS CELL 21
         AH    R7,VVRDSLEN    BUMP TO NEXT CELL
         USING DS60,R7        ADDRESSABILITY
         BAL   R14,PC60       PROCESS CELL 60
         AH    R7,VVRAMDCN    BUMP TO NEXT CELL
         USING DS23,R7        ADDRESSABILITY
         BAL   R14,PC23       PROCESS CELL 23
         DROP  R7
         BAL   R14,PUTE       WRITE EXTRACT REC
         L     R14,PVRZR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PC21     - PROCESS VVR CELL TYPE 21'
PC21     EQU   *
         ST    R14,PC21R14    SAVE RETURN ADDR
         USING DS21,R7        ADDRESSABILITY
         DROP  R7
         L     R14,PC21R14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PC23     - PROCESS VVR CELL TYPE 23'
PC23     EQU   *
         ST    R14,PC23R14    SAVE RETURN ADDR
         USING DS23,R7        ADDRESSABILITY
         MVC   WRKBYTTK,VVRBYTTK POST BYTES/TRACK
         CLC   WRKBYTTK,=F'0' ZERO CHECK
         BNZ   PC23BTOK       NON-ZERO -- OK
         ICM   R1,B'1111',VVRBLKSZ    POST BLOCK SIZE
         SR    R14,R14        CLEAR
         SR    R15,R15        CLEAR
         ICM   R15,B'0011',VVRBLKTK POST BLKS/TRK
         MR    R14,R1         X BLKSZ = BYTES/TRK
         ST    R15,WRKBYTTK   POST
PC23BTOK EQU   *
         SR    R15,R15        CLEAR
         ICM   R15,B'0001',VVRNOEXT NUMBER OF EXTENTS
         CVD   R15,WORKDW     NOW PACKED
         MVC   WRKMXTNT,=X'40202120'
         ED    WRKMXTNT,WORKDW+6
         MVC   ERECXTNT,WRKMXTNT+1
         MVC   WRKDSHA,VVRHARBA HIGH ALLOC RBA
         MVC   WRKDSHU,VVRHURBA HIGH USED  RBA
         DROP  R7
         L     R14,PC23R14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PC60     - PROCESS VVR CELL TYPE 60'
PC60     EQU   *
         ST    R14,PC60R14    SAVE RETURN ADDR
         USING DS60,R7        ADDRESSABILITY
         ICM   R15,B'1111',VVRAMCIV CONTROL INTERVAL SIZE
         CVD   R15,WORKDW     NOW PACKED
         MVC   WRKMCISZ,=X'402020202120'
         ED    WRKMCISZ,WORKDW+5
         MVC   ERECCISZ,WRKMCISZ+1
         ICM   R15,B'1111',VVRAMLCL MAXIMUM REC SIZE
         CVD   R15,WORKDW     NOW PACKED
         MVC   WRKMRECL,=X'402020202120'
         ED    WRKMRECL,WORKDW+5
         MVC   ERECRECL,WRKMRECL+1
         DROP  R7
         L     R14,PC60R14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PCE9     - PROCESS VVR CELL TYPE E9'
PCE9     EQU   *
         ST    R14,PCE9R14    SAVE RETURN ADDR
         USING DSE9,R7        ADDRESSABILITY
         MVC   ERECDSN,SPACES CLEAR
         LA    R0,ERECDSN     POST 'TO' ADDR
         SR    R1,R1          CLEAR
         IC    R1,VVRCMPNL    POST NAME LENGTH
         LA    R14,VVRCMPNM   POST 'FROM' ADDR
         LR    R15,R1         POST NAME LENGTH
         MVCL  R0,R14         MOVE NAME
         TR    ERECDSN,TRNAME CLEAN UP PAD BYTE
         DROP  R7
         L     R14,PCE9R14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'GETV     - GET NEXT VVDS REC'
GETV     EQU   *
         ST    R14,GETVR14    SAVE RETURN ADDR
         CLI   SWV,C'O'       FILE OPEN
         BNE   ERRGVE         NO -- ERROR
         GET   RPL=((6))      READ NEXT REC
         LTR   R15,R15        OK?
         BZ    GETVX          YES -- DONE
         ST    R15,FDBKR15    SAVE RETURN CODE
         SHOWCB RPL=(6),                                               X
               AREA=FDBKRPL,                                           X
               LENGTH=L'FDBKRPL,                                       X
               FIELDS=FDBK
         CLC   FDBKR15,=F'8'  LOGIC ERROR
         BNE   ERRRPL         NO -- ERROR
         CLC   FDBKRPL,=F'4'  EOF
         BNE   ERRRPL         NO -- ERROR
         MVI   SWV,C'E'       SET EOF SW
GETVX    EQU   *
         L     R14,GETVR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PUTE     - WRITE OUT E REC'
PUTE     EQU   *
         ST    R14,PUTER14    SAVE RETURN ADDR
         L     R1,WRKBYTTK    POST BYTES/TRACK
         SR    R14,R14        CLEAR
         L     R15,WRKDSHA    HIGH ALLOC RBA
         DR    R14,R1         TRACKS ALLOCATED
         LTR   R14,R14        REMAINDER
         BZ    PUTEAOK        NO -- ALLOCATED OK
         LA    R15,1(R15)     BUMP QUOTIENT
PUTEAOK  EQU   *
         CVD   R15,WORKDW     NOW PACKED
         MVC   WRKMALOC,=X'40202020202020202120'
         ED    WRKMALOC,WORKDW+3
         MVC   ERECALOC,WRKMALOC+1
         SR    R14,R14        CLEAR
         L     R15,WRKDSHU    HIGH USED RBA
         DR    R14,R1         TRACKS USED
         LTR   R14,R14        REMAINDER
         BZ    PUTEUOK        NO -- USED OK
         LA    R15,1(R15)     BUMP QUOTIENT
PUTEUOK  EQU   *
         CVD   R15,WORKDW     NOW PACKED
         MVC   WRKMUSED,=X'40202020202020202120'
         ED    WRKMUSED,WORKDW+3
         MVC   ERECUSED,WRKMUSED+1
         PUT   EXTRACT,EREC   WRITE REC
         L     R14,PUTER14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'ERROR MESSAGES'
ERRGVE   EQU   *
         WTO   'DM140PUP - INTERNAL LOGIC ERROR AT LABEL ERRGVE',      *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRNOCR  EQU   *
         WTO   'DM140PUP - VVCR RECORD MISSING ',                      *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRNOSDR EQU   *
         WTO   'DM140PUP - SELF DEFINING VVR MISSING',                 *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROEXT  EQU   *
         WTO   'DM140PUP - OPEN FAILED FOR EXTRACT FILE',              *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPV   EQU   *
         WTO   'DM140PUP - OPEN FAILED FOR VVDS FILE',                 *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRRPL   EQU   *
         WTO   'DM140PUP - I/O ERROR READING VVDS',                    *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRSVVRF EQU   *
         WTO   'DM140PUP - FIRST VVR NOT SELF DEFINING',               *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRSVVRZ EQU   *
         WTO   'DM140PUP - FIRST VVR NOT TYPE Z',                      *
               ROUTCDE=11,DESC=7
         B     ABEND
         TITLE 'EXIT/ABEND'
ABEND    EQU   *
         ABEND 1000,DUMP
EXIT     EQU   *
         CLOSE (EXTRACT)
         L     R6,ADDRACB
         CLOSE ((6))
         L     R13,4(R13)
         RETURN (14,12),RC=0
         LTORG
         TITLE 'DATA AREAS - MISCELLANEOUS'
SAVEAREA DS    9D                  REG SAVE AREA
WORKDW   DS    D                   WORK AREA
GETVR14  DS    F                   R14 SAVE AREA FOR GETV
PC21R14  DS    F                   R14 SAVE AREA FOR PC21
PC23R14  DS    F                   R14 SAVE AREA FOR PC23
PC60R14  DS    F                   R14 SAVE AREA FOR PC60
PCE9R14  DS    F                   R14 SAVE AREA FOR PCE9
PUTER14  DS    F                   R14 SAVE AREA FOR PUTE
PVRQR14  DS    F                   R14 SAVE AREA FOR PVRQ
PVRZR14  DS    F                   R14 SAVE AREA FOR PVRZ
SCANR14  DS    F                   R14 SAVE AREA FOR SCAN
SVVRR14  DS    F                   R14 SAVE AREA FOR SVVR
*
ADDRACB  DS    F                   A(ACB) - ARCDIR
ADDRRPL  DS    F                   A(RPL) - ARCDIR
ADDRVREC DS    F                   A(REC) - ARCDIR
FDBKRPL  DS    F                   FDBK FROM ARCDIR IF ERROR
FDBKR15  DS    F                   R15 FROM GET ON ARCDIR IF ERROR
RLRECL   DS    F                   LRECL OF CURRENT FILE
BLRECL   DC    F'4096'             LRECL OF BUFFER
TIOT     DS    F                   ADDR OF TIOT
NEXTDD   DS    F                   ADDR OF NEXT DD SEG IN TIOT
WRKBYTTK DS    F                   BYTES / TRK
WRKDSHA  DS    F                   HIGH ALLOC RBA
WRKDSHU  DS    F                   HIGH USED  RBA
*
SWV      DC    C'C'                FILE V STATUS SW VVDSXXXX
*              C'C'                CLOSED
*              C'O'                OPEN
*              C'E'                FILE AT EOF
*
DDNAME   DS    CL8                 DD NAME OF NEXT VVDS TO PROCESS
SPACES   DC    CL133' '
WRKMALOC DS    CL10                WORK AREA FOR ALLOC   EDIT MASK
WRKMUSED DS    CL10                WORK AREA FOR USED    EDIT MASK
WRKMRECL DS    CL6                 WORK AREA FOR LRECL   EDIT MASK
WRKMCISZ DS    CL6                 WORK AREA FOR CI SIZE EDIT MASK
WRKMXTNT DS    CL4                 WORK AREA FOR EXTENT  EDIT MASK
         TITLE 'DM140PRP - RECORD AREAS'
*
EREC     DS    0CL81               E RECORD
ERECDSN  DS    CL44                DATASET NAME
ERECALOC DS    CL9                 TRACKS ALLOCATED
ERECUSED DS    CL9                 TRACKS USED
ERECCISZ DS    CL5                 CI SIZE
ERECXTNT DS    CL3                 NUMBER OF EXTENTS
ERECRECL DS    CL5                 HIGH LRECL
ERECVOL  DS    CL6                 VOL SER
         TITLE 'WORK AREAS - TRANSLATE TABLES'
TRNAME   EQU   *
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'40404040404040404040404040404040'    0
         DC    X'40404040404040404040404040404040'    1
         DC    X'40404040404040404040404040404040'    2
         DC    X'40404040404040404040404040404040'    3
         DC    X'40404040404040404040404B40404040'    4  PERIOD
         DC    X'40404040404040404040405B40404040'    5  $
         DC    X'40404040404040404040404040404040'    6
         DC    X'40404040404040404040407B7C404040'    7  @#
         DC    X'40404040404040404040404040404040'    8
         DC    X'40404040404040404040404040404040'    9
         DC    X'40404040404040404040404040404040'    A
         DC    X'40404040404040404040404040404040'    B
         DC    X'40C1C2C3C4C5C6C7C8C9404040404040'    C  LETTERS
         DC    X'40D1D2D3D4D5D6D7D8D9404040404040'    D  LETTERS
         DC    X'4040E2E3E4E5E6E7E8E9404040404040'    E  LETTERS
         DC    X'F0F1F2F3F4F5F6F7F8F9404040404040'    F  NUMBERS
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         TITLE 'WORK AREAS - DCBS'
         PUSH  PRINT
         PRINT NOGEN
EXTRACT  DCB   DDNAME=EXTRACT,                                         X
               DSORG=PS,                                               X
               LRECL=81,                                               X
               MACRF=PM,                                               X
               RECFM=FB
*
*
         DCBD  DSORG=(PS)
         POP   PRINT
         TITLE 'WORK AREAS - DSECTS'
DSE9     DSECT                PRIMARY HEADER CELL
*                        ALSO SECONDARY HEADER CELL (DSD8)
VVRLEN   DS    XL2            LENGTH OF ENTIRE VVR
VVRHDLEN DS    XL2            LENGHT OF PRIMARY/SECONDARY HEADER CELL
VVRTYPE  DS    X              C'Z' PRIMARY OR C'Q' SECONDARY
VVRFLAG  DS    X              HEADER FLAG
*              1... ....      RESERVED
*              .1.. ....      SELF DESCRIBING VVR
*              ..1. ....      CATALOG SELF DESCRIBING VVR
*              ...1 ....      RESERVED
*              .... 1...      COMPONENT TYPE 'D' DATA
*              .... 0...      COMPONENT TYPE 'I' INDEX
*              .... .111      RESERVED
VVRKRQ   DS    XL4            KEY RANGE QUALIFIER
VVRCMPNL DS    X              COMPONENT NAME LENGTH
VVRCMPNM DS    X              1ST BYTE OF COMPONENT NAME
*                             FOLLOWS COMPONENT NAME
VVRKEYL  DS    X              KEY LENGTH            *** OFFSET INVALID
VVRKEY   DS    X              KEY (CL NAME + PAD)   *** OFFSET INVALID
*                             FOLLOWS KEY
VVRCATNL DS    X              CATALOG NAME LENGTH   *** OFFSET INVALID
VVRCATNM DS    X              CATALOG NAME          *** OFFSET INVALID
*                             FOLLOWS CATALOG NAME
VVRBSENL DS    X              ENTRY NAME LENGTH     *** OFFSET INVALID
VVRBSENM DS    X              ENTRY NAME            *** OFFSET INVALID
         EJECT
DS21     DSECT                DATA SET INFORMATION CELL
VVRDSLEN DS    XL2            DATA SET CELL LENGTH
VVRDSTYP DS    X              X'21'
VVRATTR1 DS    X              DATA SET ATTRIBUTES
*              1... ....      SPEED
*              .1.. ....      UNIQUE
*              ..1. ....      REUSABLE
*              ...1 ....      ERASE
*              .... 1...      RESERVED
*              .... .1..      INHIBIT UPDATE
*              .... ..1.      TEMPORARY EXPORT
*              .... ...1      TRACK OVERFLOW
VVRATTR2 DS    X              SHARE ATTRIBUTES
*              11.. ....      REGIONAL SHARING ATTRIBUTES
*              ..11 ....      SYSTEM SHARING ATTRIBUTES
*              .... 11..      RESERVED
*              .... ..1.      INTERNAL SYSTEM DATA SET
*              .... ...1      COMPONENT IS NOT USABLE
VVROPIND DS    X              OPEN INIDCATOR
VVRBUFSZ DS    XL4            BUFFER SIZE
VVRPRISP DS    XL3            PRIMARY SPACE ALLOCATION
VVRSECSP DS    XL3            SECONDARY SPACE ALLOCATION
VVRSPCFG DS    X              SPACE FLAGS
*              11.. ....      SPACE OPTIONS
*              ..11 1111      RESERVED
VVRDSHU  DS    XL4            DATA SET HIGH USED      RBA
VVRDSHA  DS    XL4            DATA SET HIGH ALLOCATED RBA
VVRLRECL DS    XL4            AVERAGE LOGICAL RECORD LENGTH
         DS    XL2            RESERVED
VVREXCPX DS    CL8            EXCEPTION EXIT NAME
VVRDSHK  DS    XL4            DATA SET HIGH KEY RBA
VVRCLSFG DS    X              CLUSTER ATTRIBUTE FLAG
*              1111 ....      RESERVED
*              .... 1...      TIMESTAMPS EXIST IN THIS CELL
*              .... .1..      VERIFY IS REQUIRED
*              .... ..1.      CLUSTER DESCRIBES SWAPSPACE
*              .... ...1      CLUSTER DESCRIBES PAGESPACE
VVRAIXFG DS    X              AIX ATTRIBUTE FLAG
*              1... ....      UPGRADE AIX
*               111 1111      RESERVED
VVRTMSTP DS    XL8            TIMESTAMP (CAT ONLY)
VVRALTSP DS    XL8            ALIAS TABLE TIMESTAMP
         DS    XL24           RESERVED
*        WARNING:  TIMESTAMPS AND RESERVED MAY BE TRUNCATED2
*                  CHECK TIMESTAMP BIT IN VVRCLSFG
         EJECT
DS60     DSECT                AMSDB CELL
VVRAMDCN DS    XL2            LENGTH OF ENTIRE AMSDB CELL
VVRAMID  DS    X              X'60'
VVRAMATR DS    X              ATTRIBUTES
*              1... ....      KEY SEQUENCED
*              0... ....      ENTRY SEQUENCED
*              .1.. ....      WRITE CHECK
*              ..1. ....      SEQUENCE SET WITH DATA (IMBED)
*              ...1 ....      REPLICATE
*              .... 1...      USE VOLUMES IN LIST ORDER
*              .... .1..      KEY RANGE DATA SET
*              .... ..1.      RELATIVE RECORD DATA SET
*              .... ....      SPANNED RECORDS ALLOWED
VVRAMLEN DS    XL2            LENGTH OF AMDSB (VVRAMDCN - 2)
VVRAMNST DS    XL2            NUMBER OF ENTRIES IN INDEX SECTION
         ORG   VVRAMNST
VVRAMAKP DS    XL2            ALTERNATE KEY RKP
VVRAMRKP DS    XL2            RELATIVE KEY POSITION
VVRAMKLN DS    XL2            KEY LENGTH
VVRAMPCA DS    X              % FREE CI IN CA
VVRAMPCI DS    X              % FREE BYTES IN CI
VVRAMCCA DS    XL2            NUMBER OF CI'S PER CA
VVRAMFCA DS    XL2            FREE CI'S PER CA
VVRAMFCI DS    XL4            FREE BYTES PER CI
VVRAMCIV DS    XL4            CONTROL INTERVAL SIZE
VVRAMLCL DS    XL4            MAXIMUM RECORD SIZE
VVRAMHLR DS    XL4            RBA OF HIGH LEVEL INDEX RECORD
         ORG   VVRAMHLR
VVRAMSLT DS    XL4            RECORD SLOTS PER CI
VVRAMSSR DS    XL4            RBA OF FIRST SEQUENCE SET RECORD
         ORG   VVRAMSSR
VVRAMMRR DS    XL4            MAX RELATIVE RECORD NUMBER
VVRAMARD DS    XL4            POINTER TO FIRST ARDB
VVRAMAT3 DS    X              ATTRIBUTES
*              1... ....      NON-UNIQUE OR UNIQUE KEYS
*              .1.. ....      CYL FAULT MSS OR STAGE
*              ..1. ....      BIND OR DO NOT BIND ON MSS
*              ...1 ....      WAIT OR DO NOT WAIT ON RELINQUISH
*              .... 1...      LOAD MODE OR DATA SET LOADED
*              .... .111      RESERVED
VVRAMSNO DS    X              NUMBER OF CONCURRENT REQUESTS
VVRAMDUI DS    XL4            IMS DRCC USAGE INDICATOR
VVRAMBFI DS    XL2            NUMBER OF INDEX BUFFERS
         ORG   VVRAMBFI
VVRAMBFD DS    XL2            NUMBER OF DATA  BUFFERS
VVRAMSTS DS    XL8            SYSTEM TIME STAMP
VVRAMNIL DS    XL2            NUMBER OF INDEX LEVELS
VVRAMNXT DS    XL2            NUMBER OF EXTENTS
VVRAMNLR DS    XL4            NUMBER OF LOGICAL   RECORDS
VVRAMDLR DS    XL4            NUMBER OF DELETED   RECORDS
VVRAMINR DS    XL4            NUMBER OF INSERTED  RECORDS
VVRAMUPR DS    XL4            NUMBER OF UPDATED   RECORDS
VVRAMRTR DS    XL4            NUMBER OF RETRIEVED RECORDS
VVRAMASP DS    XL4            BYTES OF FREE SPACE IN DATA SET
VVRAMCIS DS    XL4            NUMBER OF CI SPLITS
VVRAMCAS DS    XL4            NUMBER OV CA SPLITS
VVRAMEXC DS    XL4            NUMBER OF EXCP'S
         EJECT
DS23     DSECT                VOLUME INFORMATION CELL
VVRVOLLN DS    XL2            LENGTH OF VOLUME CELL
VVRVOLTP DS    X              X'23'
VVRVOLFG DS    X              VOLUME FLAGS
*              1... ....      PRIME VOLUME
*              .1.. ....      EXTENT INFO OUT OF SYNC (BY DEFRAG)
*              ..1. ....      OVERFLOW VOLUME
*              ...1 1111      RESERVED
VVRNOEXT DS    X              NUMBER OF EXTENTS
VVRHKRBA DS    XL4            HIGH KEY RBA
VVRHURBA DS    XL4            HIGH USED RBA
VVRHARBA DS    XL4            HIGH ALLOCATED RBA
VVRBLKSZ DS    XL4            BLOCK SIZE
VVRBLKTK DS    XL2            BLOCKS PER TRACK
VVRTRKAU DS    XL2            TRACKS PER ALLOCATION UNIT
VVRTPEXT DS    X              EXTENT TYPE FLAG
*              1... ....      SEQUENCE SET WITH DATA
*              .1.. ....      EXTENTS NOT PREFORMATTED
*              ..1. ....      CONVERTED VSAM DATASET VOLUME
*              ...1 1111      RESERVED
VVRTKCYL DS    XL2            TRACKS PER CYLINDER
VVRBYTTK DS    XL4            BYTES PER TRACK
VVRBYTAU DS    XL4            BYTES PER ALLOCATION UNIT
VVRLOKYL DS    XL2            LOW KEY LENGTH
VVRLOKYV DS    X              LOW KEY
*                             FOLLOWS LOW KEY
VVRHIKYL DS    XL2            HIGH KEY LENGTH       *** OFFSET INVALID
VVRHYKYV DS    X              HIGH KEY              *** OFFSET INVALID
*                             FOLLOWS HIGH KEY
VVRXTNTL DS    XL2            LENGTH OF EXTENT SECT *** OFFSET INVALID
*                             VVRXTNTL/20 = NUMBER OF EXTENT ENTRIES
*                             EXTENT ENTRIES IMMEDIATELY FOLLOW
*                             THEY ARE MAPPED BY THE VVRX DSECT
         EJECT
VVRX     DSECT                EXTENT ENTRY
VVRXSEQN DS    XL2            SEQUENCE NUMBER
VVRXSC   DS    XL2            STARTING CC
VVRXSH   DS    XL2            STARTING HH
VVRXEC   DS    XL2            ENDING   CC
VVRXEH   DS    XL2            ENDING HH
VVRXNTRK DS    XL2            NUMBER OF TRACKS
VVRXSRBA DS    XL4            STARTING RBA
VVRXERBA DS    XL4            ENDING   RBA
         END
