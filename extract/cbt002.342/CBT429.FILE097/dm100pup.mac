         TITLE 'DM100PUP - EXTRACT DATA FROM HSM MCDS'
DM100PUP CSECT
         REGS
*
*        SCAN HSM MCDS FOR A RECS AND D RECS
*        IF A:
*              CHECK MCAINTTP
*                 MUST BE 'M' - BASE DATASET
*              WRITE:  KEY
*                      MCAINTNM (D REC KEY)
*        IF D:
*              CHECK MCDFLG1
*                 MUST BE X'80' MIGRATED COPY EXISTS ON LEVEL1, OR
*                 MUST BE X'84' MIGRATED COPY EXISTS ON LEVEL2
*              CHECK MCDMDEVT
*                 MUST BE X'80' MIGRATED COPY EXISTS ON TAPE, OR
*                 MUST BE X'20' MIGRATED COPY EXISTS ON DISK
*              WRITE:  KEY
*                      MCDVSN   MIGR VOL SER
*                      MCDFRVSN FROM VOL SER
*                      MCDMCANM (A REC KEY)
*
         USING DM100PUP,R15   ADDRESSABILITY
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'DM100PUP - EXTR DATA FROM HSM MCDS &SYSDATE &SYSTIME'
         TITLE 'DM100PUP - INITIALIZATION'
MAINEP   DS    0H
         SAVE (14,12)         SAVE REGS
         LR    R12,R15        MOVE BASE
         DROP  R15
         USING DM100PUP,R12   ADDRESSABILITY
         LR    R2,R13         A(OLD SAVE AREA)
         LA    R13,SAVEAREA   A(NEW SAVE AREA)
         USING SAVEAREA,R13   ADDRESSABILITY
         ST    R13,8(R2)      FORWARD CHAIN
         ST    R2,4(R13)      BACK CHAIN
         USING IHADCB,R6      ADDRESSABILITY
         LA    R6,EXTRACTD    A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,X'10' OK?
         BZ    ERROPD         NO -- ERROR
         LA    R6,EXTRACTA    A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,X'10' OK?
         BZ    ERROPA         NO -- ERROR
         DROP  R6
         GENCB BLK=ACB,                                                X
               AM=VSAM,                                                X
               DDNAME=MCDS,                                            X
               MACRF=(KEY,SEQ)
         ST    R1,ADDRACB     SAVE A(ACB)
         LR    R6,R1          POST TO NEW REG
         OPEN  ((6))          OPEN FILE
         CH    R15,=H'4'      ABOVE 'WARNING'
         BH    ERROPM         YES -- ERROR
         SHOWCB ACB=(6),                                               X
               AREA=LRECL,                                             X
               LENGTH=L'LRECL,                                         X
               FIELDS=LRECL
         L     R0,LRECL       POST REC LEN
         LR    R8,R0          SAVE IN A MORE CONVENIENT REG
         GETMAIN R,LV=(0)     GETMAIN A RECORD AREA
         ST    R1,ADDRREC     SAVE
         LR    R7,R1          POST TO A MORE CONVENIENT REG
         GENCB BLK=RPL,                                                X
               ACB=(6),                                                X
               AM=VSAM,                                                X
               AREA=(7),                                               X
               AREALEN=(8),                                            X
               OPTCD=(KEY,SEQ)
         ST    R1,ADDRRPL     SAVE A(RPL)
         LR    R6,R1          POST A(RPL) TO A CONVENIENT REG
         USING MCDSREC,R7     ADDRESSABILITY
         TITLE 'DM100PUP - MAIN LOOP'
LOOP     EQU   *
         GET   RPL=((6))      READ NEXT REC
         LTR   R15,R15        OK?
         BNZ   LOOPGCK        NO -- CHECK WHY
         CLI   MCHTYPE,MCHTYPED 'D' REC
         BE    HITD           YES
         CLI   MCHTYPE,MCHTYPEA 'A' REC
         BE    HITA           YES
         B     LOOP           IGNORE
HITA     EQU   *
         CLI   MCAINTTP,MCAINTTM MIGRATE REC
         BNE   LOOP           NO -- DROP
         BAL   R14,PUTA       WRITE AN 'A' REC
         B     LOOP           LOOP BACK
HITD     EQU   *
         TM    MCDFLG1,MCDFLASN MIGRAT
         BZ    LOOP           NO -- DROP
         BAL   R14,PUTD       WRITE A 'D' REC
         B     LOOP           LOOP BACK
LOOPGCK  EQU   *
         ST    R15,FDBKR15    SAVE RETURN CODE
         SHOWCB RPL=(6),                                               X
               AREA=FDBKRPL,                                           X
               LENGTH=L'FDBKRPL,                                       X
               FIELDS=FDBK
         CLC   FDBKR15,=F'8'  LOGIC ERROR
         BNE   ERRRPL         NO -- ERROR
         CLC   FDBKRPL,=F'4'  EOF
         BNE   ERRRPL         NO -- ERROR
         B     EXIT           DONE -- EXIT
         TITLE 'DM100PUP - PUTA - WRITE AN A REC'
PUTA     EQU   *
         ST    R14,PUTAR14    SAVE RETURN ADDR
         MVC   ARECDSND,MCAINTNM POST D REC DSN
         MVC   ARECDSNA,MCK   POST A REC DSN
         PUT   EXTRACTA,AREC
         L     R14,PUTAR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'DM100PUP - PUTD - WRITE A D REC'
PUTD     EQU   *
         ST    R14,PUTDR14    SAVE RETURN ADDR
         MVC   DRECDSND,MCK   POST D REC DSN
         MVC   DRECDSNA,MCDMCANM  POST A REC DSN
         MVC   DRECPVOL,MCDFRVSN  POST USER VSN
         MVC   DRECMVOL,MCDVSN  POST MIGRAT VSN
         MVI   DRECLVL,C'1'   ASSUME LEVEL1
         TM    MCDFLG1,MCDFL2 LEVEL2
         BZ    PUTDDEVT       CHECK FOR DEVICE TYPE
         MVI   DRECLVL,C'2'   SET LEVEL2
PUTDDEVT EQU   *
         CLI   MCDMDEVT+2,MCDTPDEV   IS IT TAPE?
         BNE   PUTDDEVD       NO -- CHECK FOR DISK
         MVI   DRECDEV,C'T'   SET TAPE
         B     PUTDDEVX       CONTINUE
PUTDDEVD EQU   *
         CLI   MCDMDEVT+2,MCDFMVDA   IS IT DISK?
         BNE   ERRDEV         NO -- ERROR
         MVI   DRECDEV,C'D'   SET DISK
PUTDDEVX EQU   *
         L     R1,MCDCSZ      SPACE IN 2K BLOCKS
         CVD   R1,WORKDW      NOW PACKED
         MVC   WORKDWB(8),=X'4020202020202120' POST EDIT MASK
         ED    WORKDWB(8),WORKDW+4  EDIT
         MVC   DRECCSZ,WORKDWB+1    POST
         UNPK  DRECRDAT,MCDDLR    POST DATE LAST REFED
         OI    DRECRDAT+L'DRECRDAT-1,X'F0'    FIX SIGN
         UNPK  DRECCDAT,MCDDLC    POST CREATE DATE
         OI    DRECCDAT+L'DRECCDAT-1,X'F0'    FIX SIGN
         UNPK  DRECMDAT,MCDDMIG   POST DATE MIGRATED
         OI    DRECMDAT+L'DRECMDAT-1,X'F0'    FIX SIGN
         PUT   EXTRACTD,DREC
         L     R14,PUTDR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'DM100PUP - ERROR ROUTINES'
ERRDEV   EQU   *
         WTO   'DM100PUP - INVALID DEVICE IN D RECORD',                X
               ROUTCDE=2,DESC=7
         B     ABEND
ERROPA   EQU   *
         WTO   'DM100PUP - OPEN FAILED FOR EXTRACTA',                  X
               ROUTCDE=2,DESC=7
         B     ABEND
ERROPD   EQU   *
         WTO   'DM100PUP - OPEN FAILED FOR EXTRACTD',                  X
               ROUTCDE=2,DESC=7
         B     ABEND
ERROPM   EQU   *
         WTO   'DM100PUP - OPEN FAILED FOR MCDS',                      X
               ROUTCDE=2,DESC=7
         B     ABEND
ERRRPL   EQU   *
         WTO   'DM100PUP - I/O ERROR ON MCDS',                         X
               ROUTCDE=2,DESC=7
         B     ABEND
ABEND    EQU   *
         ABEND 1000,DUMP
         TITLE 'DM100PUP - EXIT - TERMINATION'
EXIT     EQU   *
         CLOSE EXTRACTA
         CLOSE EXTRACTD
         L     R6,ADDRACB
         CLOSE ((6))
         L     R13,4(R13)     A(OLD SAVE AREA)
         RETURN (14,12),RC=0
         LTORG
         TITLE 'DM100PUP - DATA AREA'
SAVEAREA DS    9D             REG SAVE AREA
WORKDW   DS    D              8 BYTE WORK AREA
WORKDWB  DS    D              8 BYTE WORK AREA
*
PUTAR14  DS    F              R14 SAVE AREA FOR PUTA - WRITE A REC
PUTDR14  DS    F              R14 SAVE AREA FOR PUTD - WRITE D REC
*
ADDRACB  DS    F
ADDRREC  DS    F
ADDRRPL  DS    F
FDBKRPL  DS    F
FDBKR15  DS    F
LRECL    DS    F
*
AREC     DS    0CL88          A RECORDS
ARECDSND DS    CL44           D REC DSN
ARECDSNA DS    CL44           A REC DSN
*
DREC     DS    0CL124         D RECORDS
DRECDSND DS    CL44           D REC DSN
DRECDSNA DS    CL44           A REC DSN
DRECPVOL DS    CL6            PRIMARY VOL SER
DRECMVOL DS    CL6            MIGRATE VOL SER
DRECLVL  DS    CL1            MIGRATION LEVEL 1 OR 2
DRECDEV  DS    CL1            MIGRATION DEVICE TYPE DISK OR TAPE
DRECCSZ  DS    CL7            SPACE IN 2K BLOCKS
DRECRDAT DS    CL5            PRIMARY FILE REF DATE
DRECCDAT DS    CL5            PRIMARY FILE CREATE DATE
DRECMDAT DS    CL5            PRIMARY FILE MIGRATE DATE
         TITLE 'DM100PUP - DCBS, DCBD'
         PUSH  PRINT
         PRINT NOGEN
EXTRACTA DCB   DDNAME=EXTRACTA,                                        X
               DSORG=PS,                                               X
               LRECL=88,                                               X
               MACRF=PM,                                               X
               RECFM=FB
*
EXTRACTD DCB   DDNAME=EXTRACTD,                                        X
               DSORG=PS,                                               X
               LRECL=124,                                              X
               MACRF=PM,                                               X
               RECFM=FB
*
         DCBD  DSORG=PS
         POP   PRINT
         TITLE 'DM100PUP - MCDS RECORD DSECT'
MCDSREC  DSECT
         COPY  HSMMCDS
         END
