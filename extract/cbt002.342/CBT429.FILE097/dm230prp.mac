       IDENTIFICATION DIVISION.
       PROGRAM-ID.
           DM230.
       AUTHOR.
           PAT RUSSELL.
       REMARKS.
      *
      *    MONTHLY DM REPORT AND BILLING FILE.
      *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           IBM-370.
       OBJECT-COMPUTER.
           IBM-370.
       SPECIAL-NAMES.
           C01 IS TO-TOP.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT AAMIN
               ASSIGN TO UT-S-AAMIN.
           SELECT GRPIN
               ASSIGN TO UT-S-GRPIN.
           SELECT MASTER
               ASSIGN TO UT-S-MASTER.
           SELECT HISTORY
               ASSIGN TO UT-S-HISTORY.
           SELECT WORK-HIST
               ASSIGN TO UT-S-WORKHIST.
           SELECT RPT-DETAIL
               ASSIGN TO UT-S-RPTDET.
           SELECT RPT-BILL
               ASSIGN TO UT-S-RPTBILL.
           SELECT RPT-SUMMARY
               ASSIGN TO UT-S-RPTSUM.
           SELECT BILL-OUT
               ASSIGN TO UT-S-BILLOUT.
           SELECT SORT-FILE
               ASSIGN TO UT-S-SORTWK01.
       DATA DIVISION.
       FILE SECTION.
       FD  AAMIN
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD
           DATA RECORD IS AAMIN-REC.
       01  AAMIN-REC.
           02  AAMIN-REC-AAM       PIC XXX.
           02  FILLER              PIC XX.
           02  AAMIN-REC-GROUP     PIC 999.
           02  FILLER              PIC X(72).
       FD  GRPIN
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD
           DATA RECORD IS GRPIN-REC.
       01  GRPIN-REC.
           02  GRPIN-TYPE          PIC XXX.
           02  FILLER              PIC XX.
           02  GRPIN-NUM           PIC 99.
           02  FILLER              PIC XX.
           02  GRPIN-DESC          PIC X(30).
           02  FILLER              PIC X(41).
       FD  MASTER
           RECORDING MODE IS F
           RECORD CONTAINS 150 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD
           DATA RECORD IS MASTER-REC.
       01  MASTER-REC.
           COPY CDMFD000.
       FD  HISTORY
           RECORDING MODE IS F
           RECORD CONTAINS 55 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD
           DATA RECORDS ARE DM-HST-HEAD
                            DM-HST-REC.
       01  DM-HST-HEAD.
           02  DM-HST-LIT   PIC X(11).
           02  DM-HST-DATE  PIC X(8).
           02  DM-HST-FILL  PIC X(36).
       01  DM-HST-REC.
           02  DM-HST-GRP         PIC 999.
           02  DM-HST-AAM         PIC XXX.
           02  DM-HST-DSNAME      PIC X(44).
           02  DM-HST-ALLOC       PIC 9(5).
       FD  WORK-HIST
           RECORDING MODE IS F
           RECORD CONTAINS 55 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORDS ARE STANDARD
           DATA RECORD IS WK-HST-REC.
       01  WK-HST-REC.
           02  WK-HST-GRP         PIC 999.
           02  WK-HST-AAM         PIC XXX.
           02  WK-HST-DSNAME      PIC X(44).
           02  WK-HST-ALLOC       PIC 9(5).
       FD  RPT-DETAIL
           RECORDING MODE IS F
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD IS STANDARD
           DATA RECORD IS DET-REC.
       01  DET-REC.
           02  DET-CC              PIC X.
           02  DET-LINE            PIC X(132).
       FD  RPT-BILL
           RECORDING MODE IS F
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD IS STANDARD
           DATA RECORD IS RBL-REC.
       01  RBL-REC.
           02  RBL-CC              PIC X.
           02  RBL-LINE            PIC X(132).
       FD  RPT-SUMMARY
           RECORDING MODE IS F
           RECORD CONTAINS 133 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD IS STANDARD
           DATA RECORD IS SUM-REC.
       01  SUM-REC.
           02  SUM-CC              PIC X.
           02  SUM-LINE            PIC X(132).
       FD  BILL-OUT
           RECORDING MODE IS F
           RECORD CONTAINS 80 CHARACTERS
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD IS STANDARD
           DATA RECORD IS BILL-REC.
       01  BILL-REC.
           02  BILL-INDIC          PIC X.
           02  BILL-NUM            PIC 99.
           02  BILL-TOT1           PIC ZZZZZZZ9.
           02  BILL-TOT2           PIC ZZZZZZZ9.
           02  BILL-TOT3           PIC ZZZZZZZ9.
           02  BILL-TOT4           PIC ZZZZZZZ9.
           02  BILL-TOT5           PIC ZZZZZZZ9.
           02  BILL-TOT6           PIC ZZZZZZZ9.
           02  BILL-TOT7           PIC ZZZZZZZ9.
           02  BILL-TOT8           PIC ZZZZZZZ9.
           02  BILL-FILL           PIC X(13).
       SD  SORT-FILE
           RECORDING MODE IS F
           DATA RECORD IS SORT-REC.
       01  SORT-REC.
           02  SORT-GRP           PIC 9(3).
           02  SORT-AAM           PIC XXX.
           02  SORT-DSNAME        PIC X(44).
           02  SORT-ALLOC         PIC 9(5).
       WORKING-STORAGE SECTION.
       01  GROUP-TABLE.
           02  GROUP-ENTRY         OCCURS 1 TO 1000
                                   DEPENDING ON WS-GROUP-HI
                                   ASCENDING KEY IS GROUP-AAM
                                   INDEXED BY GROUP-INDEX.
               03  GROUP-AAM       PIC XXX.
               03  GROUP-NUMBER    PIC S9(3) COMP-3.
       01  WS-COMP                 COMP SYNC.
           02  LINE-MAX            PIC S9(4) VALUE +56.
           02  DET-SKIP-CT         PIC S9(4) VALUE +1.
           02  DET-LINE-CT         PIC S9(4) VALUE +0.
           02  DET-PAGE-CT         PIC S9(4) VALUE +0.
           02  BIL-SKIP-CT         PIC S9(4) VALUE +1.
           02  BIL-LINE-CT         PIC S9(4) VALUE +0.
           02  BIL-PAGE-CT         PIC S9(4) VALUE +0.
           02  SUM-SKIP-CT         PIC S9(4) VALUE +1.
           02  SUM-LINE-CT         PIC S9(4) VALUE +0.
           02  SUM-PAGE-CT         PIC S9(4) VALUE +0.
           02  WS-DEV-DETAIL-IX    PIC S9(4) VALUE +0.
           02  WS-GROUP-HI         PIC S9(4) VALUE +1.
           02  ABEND-CODE          PIC S9(4) VALUE +1000.
       01  WS-DISP.
           02  SW-STAT-WORK-HIST   PIC X VALUE 'N'.
           02  SW-STAT-SORT        PIC X VALUE 'N'.
           02  WS-CUR-AAM          PIC XXX VALUE '   '.
           02  WS-PRV-AAM          PIC XXX VALUE '   '.
           02  WS-CUR-GRP          PIC 999 VALUE ZERO.
           02  WS-PRV-GRP          PIC 999 VALUE ZERO.
           02  DETAIL-LINE.
               03  WS-DETAIL-CC    PIC X.
               03  WS-DETAIL-TEXT  PIC X(132) VALUE SPACES.
           02  WS-OLD-HIST-DATE    PIC X(8) VALUE SPACES.
           02  WS-INDEX-SW-TBL     PIC X(99) VALUE ALL 'N'.
           02  FILLER              REDEFINES WS-INDEX-SW-TBL.
               03  INDEX-SW        PIC X OCCURS 99.
           02  WS-SUB-SW-TABLE     PIC X(8) VALUE ALL 'N'.
           02  FILLER              REDEFINES WS-SUB-SW-TABLE.
               03  SUB-SW          PIC X OCCURS 8.
           02  WS-DESC             PIC X(30) OCCURS 99.
           02  SUB-DESC-TABLE      PIC X(88) VALUE SPACES.
           02  FILLER              REDEFINES SUB-DESC-TABLE.
               03  FILLER          OCCURS 8.
                   04  SUB-DESC    PIC X(10).
                   04  FILLER      PIC X.
           02  WS-GRP              PIC 999.
           02  FILLER              REDEFINES WS-GRP.
               03  WS-GRP-INDEX    PIC 99.
               03  WS-GRP-SUB      PIC 9.
           02  WS-BILL-GRP         PIC 999.
           02  FILLER              REDEFINES WS-BILL-GRP.
               03  WS-BILL-GRP-INDEX   PIC 99.
               03  WS-BILL-GRP-SUB     PIC 9.
           02  WS-BILL-PRV-INDEX       PIC 99   VALUE ZERO.
           02  WS-BILL-TOT1        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT2        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT3        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT4        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT5        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT6        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT7        PIC 99999999 VALUE ZERO.
           02  WS-BILL-TOT8        PIC 99999999 VALUE ZERO.
           02  WS-WORK-DATE.
               03  WS-WORK-MM      PIC 99.
               03  FILLER          PIC X.
               03  WS-WORK-DD      PIC 99.
               03  FILLER          PIC X.
               03  WS-WORK-YY      PIC 99.
       01  WS-COMP-3               COMP-3.
           02  WS-DET-OLD          PIC S9(11) VALUE +0.
           02  WS-DET-NEW          PIC S9(11) VALUE +0.
           02  WS-DET-NET          PIC S9(11) VALUE +0.
           02  WS-AAM-OLD          PIC S9(11) VALUE +0.
           02  WS-AAM-NEW          PIC S9(11) VALUE +0.
           02  WS-AAM-NET          PIC S9(11) VALUE +0.
           02  WS-GRP-OLD          PIC S9(11) VALUE +0.
           02  WS-GRP-NEW          PIC S9(11) VALUE +0.
           02  WS-GRP-NET          PIC S9(11) VALUE +0.
           02  WS-TOT-OLD          PIC S9(11) VALUE +0.
           02  WS-TOT-NEW          PIC S9(11) VALUE +0.
           02  WS-TOT-NET          PIC S9(11) VALUE +0.
           02  WS-DATCNVRT-DOW     PIC S9(8).
           02  WS-DATCNVRT-JDAY    PIC S9(8).
       01  CTRL-CARD.
           02  CTRL-TYPE           PIC XXXX.
           02  FILLER              PIC X.
           02  CTRL-PID            PIC X(8).
           02  FILLER              PIC X.
           02  CTRL-BANK           PIC XXX.
           02  FILLER              PIC X.
           02  CTRL-DATE           PIC X(8).
           02  FILLER              PIC X.
           02  CTRL-SWITCHES.
               03  SW-1            PIC X.
               03  SW-2            PIC X.
               03  SW-3            PIC X.
               03  SW-4            PIC X.
               03  SW-5            PIC X.
               03  SW-6            PIC X.
               03  SW-7            PIC X.
               03  SW-8            PIC X.
               03  SW-9            PIC X.
               03  SW-10           PIC X.
      *  FOLLOWING FIELDS MATCH DDA CTRL CARD FORMAT *
           02  CTRL-USER-AREA.
               03  FILLER          PIC X.
               03  CTRL-CUR-DT.
                   04  CTRL-CUR-MM     PIC XX.
                   04  CTRL-CUR-DD     PIC XX.
                   04  CTRL-CUR-YY     PIC XX.
               03  CTRL-PREV-DT.
                   04  CTRL-PREV-MM    PIC XX.
                   04  CTRL-PREV-DD    PIC XX.
                   04  CTRL-PREV-YY    PIC XX.
               03  CTRL-DAYS-SINCE-LAST-PROC   PIC 9.
               03  CTRL-NEXT-DT.
                   04  CTRL-NEXT-MM    PIC XX.
                   04  CTRL-NEXT-DD    PIC XX.
                   04  CTRL-NEXT-YY    PIC XX.
               03  CTRL-DAYS-TO-NEXT-PROC      PIC 9.
               03  CTRL-FILLER-1   PIC X(13).
               03  FILLER REDEFINES CTRL-FILLER-1.
                   04  FILLER      PIC X.
                   04  CTRL-2ND-NEXT-DATE.
                       05  CTRL-2ND-NEXT-MM  PIC XX.
                       05  CTRL-2ND-NEXT-DD  PIC XX.
                       05  CTRL-2ND-NEXT-YY  PIC XX.
                   04  FILLER      PIC X(6).
           02  FILLER              REDEFINES CTRL-USER-AREA.
               03  FILLER          PIC X.
               03  CTRL-FACT       PIC 999V9999.
               03  FILLER          PIC X.
               03  CTRL-GRP-NUM    PIC 999.
               03  FILLER          PIC X(22).
           02  CTRL-COL-72         PIC X.
           02  CTRL-COL-73-80      PIC X(8).
       01  HEADING-1.
           02  FILLER              PIC X(12) VALUE '    DM230-01'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE '   '.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE
                                             'MONTHLY DETAIL REPORT'.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  H-1-DATE-OLD        PIC X(9).
           02  FILLER              PIC XXX   VALUE ' - '.
           02  H-1-DATE-CUR        PIC X(9).
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(11) VALUE '       PAGE'.
           02  FILLER              PIC X     VALUE SPACES.
           02  H-1-PAGE            PIC Z,ZZ9.
       01  HEADING-2.
           02  FILLER              PIC X(12) VALUE '    DM230-02'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE '   '.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE
                                             'MONTHLY SUMMARY REPORT'.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  H-2-DATE-OLD        PIC X(9).
           02  FILLER              PIC XXX   VALUE ' - '.
           02  H-2-DATE-CUR        PIC X(9).
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(11) VALUE '       PAGE'.
           02  FILLER              PIC X     VALUE SPACES.
           02  H-2-PAGE            PIC Z,ZZ9.
       01  HEADING-3.
           02  FILLER              PIC X(12) VALUE '       GROUP'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE 'AAM'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE 'DSNAME'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(14) VALUE '     OLD ALLOC'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(14) VALUE '     NEW ALLOC'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(15) VALUE '     NET CHANGE'.
       01  HEADING-4.
           02  FILLER              PIC X(12) VALUE '       GROUP'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE '   '.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE '      '.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(14) VALUE '     OLD ALLOC'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(14) VALUE '     NEW ALLOC'.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(15) VALUE '     NET CHANGE'.
       01  HEADING-5.
           02  FILLER              PIC X(10) VALUE ' DM230-05 '.
           02  FILLER              PIC X(44) VALUE
                                             'MONTHLY BILLING REPORT'.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  H-5-DATE-OLD        PIC X(9).
           02  FILLER              PIC XXX   VALUE ' - '.
           02  H-5-DATE-CUR        PIC X(9).
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(11) VALUE '       PAGE'.
           02  FILLER              PIC X     VALUE SPACES.
           02  H-5-PAGE            PIC Z,ZZ9.
       01  HEADING-6.
           02  FILLER              PIC X(8) VALUE ' GROUP  '.
           02  FILLER              PIC X(31) VALUE 'DESCRIPTION '.
           02  H-6-DET             PIC X(95) VALUE SPACES.
       01  DET-D.
           02  FILLER              PIC X(08) VALUE SPACES.
           02  DET-D-GRP           PIC XXX.
           02  FILLER              PIC X     VALUE SPACES.
           02  DET-D-AAM           PIC XXX.
           02  FILLER              PIC X     VALUE SPACES.
           02  DET-D-DSNAME        PIC X(44).
           02  FILLER              PIC X     VALUE SPACES.
           02  DET-D-OLD           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  DET-D-NEW           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  DET-D-NET           PIC ---,---,---,--9.
       01  DET-B.
           02  FILLER              PIC XXX VALUE SPACES.
           02  DET-B-NUM           PIC 99.
           02  FILLER              PIC XX VALUE SPACES.
           02  DET-B-DESC          PIC X(30).
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT1          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT2          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT3          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT4          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT5          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT6          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT7          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X VALUE SPACES.
           02  DET-B-TOT8          PIC ZZ,ZZZ,ZZ9.
           02  FILLER              PIC X(8) VALUE SPACES.
       01  SUM-A.
           02  FILLER              PIC X(08) VALUE SPACES.
           02  SUM-A-GRP           PIC XXX.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-A-AAM           PIC XXX.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE 'THIS AAM'.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-A-OLD           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-A-NEW           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-A-NET           PIC ---,---,---,--9.
       01  SUM-G.
           02  FILLER              PIC X(08) VALUE SPACES.
           02  SUM-G-GRP           PIC XXX.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE 'THIS GROUP'.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-G-OLD           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-G-NEW           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-G-NET           PIC ---,---,---,--9.
       01  SUM-T.
           02  FILLER              PIC X(08) VALUE SPACES.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC XXX   VALUE SPACES.
           02  FILLER              PIC X     VALUE SPACES.
           02  FILLER              PIC X(44) VALUE 'GRAND TOTAL'.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-T-OLD           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-T-NEW           PIC ZZ,ZZZ,ZZZ,ZZ9.
           02  FILLER              PIC X     VALUE SPACES.
           02  SUM-T-NET           PIC ---,---,---,--9.
           EJECT
       PROCEDURE DIVISION.
           MOVE CURRENT-DATE TO H-1-DATE-CUR
                                H-2-DATE-CUR
                                H-5-DATE-CUR
                                WS-WORK-DATE.
           MOVE 'DM230PRP' TO CTRL-PID.
           MOVE 'EDIT' TO CTRL-TYPE.
           CALL 'GETCTRL' USING CTRL-CARD.
           IF CTRL-GRP-NUM NOT NUMERIC
               THEN DISPLAY 'CTRL CARD DEFAULT GROUP NOT NUMERIC'
                            UPON CONSOLE
                    CALL 'USERDUMP' USING ABEND-CODE.
           PERFORM INIT-DESC-TABLE.
           MOVE SUB-DESC-TABLE TO H-6-DET.
           PERFORM INIT-GROUP-TABLE.
           PERFORM INIT-WORK-HIST.
           SORT SORT-FILE ON ASCENDING KEY SORT-GRP
                                           SORT-AAM
                                           SORT-DSNAME
               INPUT PROCEDURE IS SORT-IN
               OUTPUT PROCEDURE IS SORT-OUT.
           GO TO E-O-J.
       SORT-IN SECTION.
           OPEN INPUT MASTER.
       SORT-IN-LOOP.
           READ MASTER AT END
               CLOSE MASTER
               GO TO SORT-IN-EXIT.
           MOVE DM-MST-AAM TO SORT-AAM.
           MOVE DM-MST-DSNAME TO SORT-DSNAME.
           MOVE DM-MST-ALLOC TO SORT-ALLOC.
           PERFORM FIND-GROUP-NUM.
           RELEASE SORT-REC.
           GO TO SORT-IN-LOOP.
       SORT-IN-EXIT.
           EXIT.
       SORT-OUT SECTION.
           OPEN INPUT  WORK-HIST
                OUTPUT HISTORY
                       BILL-OUT
                       RPT-DETAIL
                       RPT-SUMMARY
                       RPT-BILL.
           MOVE LINE-MAX TO DET-LINE-CT
                            SUM-LINE-CT
                            BIL-LINE-CT.
           MOVE 'DM-HST-HEAD' TO DM-HST-LIT.
           MOVE CURRENT-DATE  TO DM-HST-DATE.
           MOVE SPACES        TO DM-HST-FILL.
           WRITE DM-HST-HEAD.
           MOVE SPACES        TO BILL-REC.
           MOVE '1'           TO BILL-INDIC.
           MOVE 00            TO BILL-NUM.
           MOVE WS-WORK-MM    TO BILL-TOT1.
           MOVE WS-WORK-DD    TO BILL-TOT2.
           MOVE WS-WORK-YY    TO BILL-TOT3.
           MOVE ZERO          TO BILL-TOT4
                                 BILL-TOT5
                                 BILL-TOT6
                                 BILL-TOT7
                                 BILL-TOT8.
           WRITE BILL-REC.
           MOVE WS-OLD-HIST-DATE TO H-1-DATE-OLD
                                    H-2-DATE-OLD
                                    H-5-DATE-OLD.
           MOVE 'R' TO SW-STAT-WORK-HIST
                       SW-STAT-SORT
           READ WORK-HIST AT END
               MOVE 'E' TO SW-STAT-WORK-HIST.
           RETURN SORT-FILE AT END
               MOVE 'E' TO SW-STAT-SORT.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               DISPLAY 'NO DATA TO PROCESS'
                       UPON SYSOUT
               MOVE +1000 TO ABEND-CODE
               CALL 'USERDUMP' USING ABEND-CODE.
           IF SW-STAT-WORK-HIST EQUAL 'E' THEN
               PERFORM RESET-SORT
               GO TO SORT-OUT-LOOP.
           IF SW-STAT-SORT EQUAL 'E' THEN
               PERFORM RESET-HIST
               GO TO SORT-OUT-LOOP.
           IF WK-HST-GRP LESS THAN SORT-GRP THEN
               PERFORM RESET-HIST
               GO TO SORT-OUT-LOOP.
           IF WK-HST-GRP GREATER THAN SORT-GRP THEN
               PERFORM RESET-SORT
               GO TO SORT-OUT-LOOP.
           IF WK-HST-AAM LESS THAN SORT-AAM THEN
               PERFORM RESET-HIST
               GO TO SORT-OUT-LOOP.
           PERFORM RESET-SORT.
       SORT-OUT-LOOP.
           IF SW-STAT-WORK-HIST EQUAL 'N' THEN
               MOVE 'R' TO SW-STAT-WORK-HIST
               READ WORK-HIST AT END
                   MOVE 'E' TO SW-STAT-WORK-HIST.
           IF SW-STAT-SORT EQUAL 'N' THEN
               MOVE 'R' TO SW-STAT-SORT
               RETURN SORT-FILE AT END
                   MOVE 'E' TO SW-STAT-SORT.
           PERFORM LEDGER-CHECK.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               GO TO SORT-OUT-END.
           GO TO SORT-OUT-LOOP.
       SORT-OUT-END.
           CLOSE       WORK-HIST
                       HISTORY
                       BILL-OUT
                       RPT-DETAIL
                       RPT-BILL
                       RPT-SUMMARY.
       SORT-OUT-EXIT.
           EXIT.
       RESET-SORT SECTION.
           MOVE SORT-GRP TO WS-CUR-GRP
                            WS-PRV-GRP
                            WS-BILL-GRP.
           MOVE WS-BILL-GRP-INDEX TO WS-BILL-PRV-INDEX.
           MOVE SORT-AAM TO WS-CUR-AAM
                            WS-PRV-AAM.
       RESET-SORT-EXIT.
           EXIT.
       RESET-HIST SECTION.
           MOVE WK-HST-GRP TO WS-CUR-GRP
                              WS-PRV-GRP
                              WS-BILL-GRP.
           MOVE WS-BILL-GRP-INDEX TO WS-BILL-PRV-INDEX.
           MOVE WK-HST-AAM TO WS-CUR-AAM
                              WS-PRV-AAM.
       RESET-HIST-EXIT.
           EXIT.
       LEDGER-CHECK SECTION.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               PERFORM AAM-BREAK
               GO TO LEDGER-CHECK-EXIT.
           IF SW-STAT-WORK-HIST EQUAL 'E' THEN
               GO TO LEDGER-CHECK-SORT.
           IF SW-STAT-SORT EQUAL 'E' THEN
               GO TO LEDGER-CHECK-HIST.
           IF WK-HST-GRP LESS THAN SORT-GRP THEN
               GO TO LEDGER-CHECK-HIST.
           IF WK-HST-GRP GREATER THAN SORT-GRP THEN
               GO TO LEDGER-CHECK-SORT.
           IF WK-HST-AAM LESS THAN SORT-AAM THEN
               GO TO LEDGER-CHECK-HIST.
           IF WK-HST-AAM GREATER THAN SORT-AAM THEN
               GO TO LEDGER-CHECK-SORT.
           IF WK-HST-DSNAME LESS THAN SORT-DSNAME THEN
               GO TO LEDGER-CHECK-HIST.
           IF WK-HST-DSNAME GREATER THAN SORT-DSNAME THEN
               GO TO LEDGER-CHECK-SORT.
           MOVE WK-HST-GRP      TO DET-D-GRP.
           MOVE WK-HST-AAM      TO DET-D-AAM.
           MOVE WK-HST-DSNAME   TO DET-D-DSNAME.
           MOVE WK-HST-ALLOC    TO WS-DET-OLD.
           MOVE SORT-ALLOC      TO WS-DET-NEW.
           SUBTRACT WS-DET-OLD FROM WS-DET-NEW GIVING WS-DET-NET.
           MOVE WS-DET-OLD      TO DET-D-OLD.
           MOVE WS-DET-NEW      TO DET-D-NEW.
           MOVE WS-DET-NET      TO DET-D-NET.
           PERFORM WRITE-HIST.
           MOVE 'N' TO SW-STAT-WORK-HIST
                       SW-STAT-SORT.
           MOVE WK-HST-GRP      TO WS-CUR-GRP.
           MOVE WK-HST-AAM      TO WS-CUR-AAM.
           GO TO LEDGER-CHECK-END.
       LEDGER-CHECK-HIST.
           MOVE WK-HST-GRP      TO DET-D-GRP.
           MOVE WK-HST-AAM      TO DET-D-AAM.
           MOVE WK-HST-DSNAME   TO DET-D-DSNAME.
           MOVE WK-HST-ALLOC    TO WS-DET-OLD.
           MOVE ZERO            TO WS-DET-NEW.
           SUBTRACT WS-DET-OLD FROM WS-DET-NEW GIVING WS-DET-NET.
           MOVE WS-DET-OLD      TO DET-D-OLD.
           MOVE WS-DET-NEW      TO DET-D-NEW.
           MOVE WS-DET-NET      TO DET-D-NET.
           MOVE 'N' TO SW-STAT-WORK-HIST.
           MOVE WK-HST-GRP      TO WS-CUR-GRP.
           MOVE WK-HST-AAM      TO WS-CUR-AAM.
           GO TO LEDGER-CHECK-END.
       LEDGER-CHECK-SORT.
           MOVE SORT-GRP        TO DET-D-GRP.
           MOVE SORT-AAM        TO DET-D-AAM.
           MOVE SORT-DSNAME     TO DET-D-DSNAME.
           MOVE ZERO            TO WS-DET-OLD.
           MOVE SORT-ALLOC      TO WS-DET-NEW.
           MOVE SORT-ALLOC      TO WS-DET-NET.
           MOVE WS-DET-OLD      TO DET-D-OLD.
           MOVE WS-DET-NEW      TO DET-D-NEW.
           MOVE WS-DET-NET      TO DET-D-NET.
           PERFORM WRITE-HIST.
           MOVE 'N' TO SW-STAT-SORT.
           MOVE SORT-GRP        TO WS-CUR-GRP.
           MOVE SORT-AAM        TO WS-CUR-AAM.
       LEDGER-CHECK-END.
           IF NOT (WS-CUR-GRP EQUAL WS-PRV-GRP AND
                   WS-CUR-AAM EQUAL WS-PRV-AAM) THEN
                   PERFORM AAM-BREAK.
           ADD WS-DET-OLD TO WS-AAM-OLD.
           ADD WS-DET-NEW TO WS-AAM-NEW.
           ADD WS-DET-NET TO WS-AAM-NET.
           MOVE DET-D           TO WS-DETAIL-TEXT.
           PERFORM DET-A-LINE.
       LEDGER-CHECK-EXIT.
           EXIT.
       AAM-BREAK SECTION.
           MOVE WS-PRV-GRP      TO SUM-A-GRP.
           MOVE WS-PRV-AAM      TO SUM-A-AAM.
           MOVE WS-AAM-OLD      TO SUM-A-OLD.
           MOVE WS-AAM-NEW      TO SUM-A-NEW.
           MOVE WS-AAM-NET      TO SUM-A-NET.
           MOVE SUM-A           TO WS-DETAIL-TEXT.
           PERFORM SUM-A-LINE.
           MOVE WS-CUR-AAM TO WS-PRV-AAM.
           ADD WS-AAM-OLD  TO WS-GRP-OLD.
           ADD WS-AAM-NEW  TO WS-GRP-NEW.
           ADD WS-AAM-NET  TO WS-GRP-NET.
           MOVE ZERO       TO WS-AAM-OLD
                              WS-AAM-NEW
                              WS-AAM-NET.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               PERFORM GRP-BREAK.
           IF WS-CUR-GRP NOT EQUAL WS-PRV-GRP THEN
               PERFORM GRP-BREAK.
           MOVE +2 TO DET-SKIP-CT.
       AAM-BREAK-EXIT.
           EXIT.
       GRP-BREAK SECTION.
           MOVE WS-PRV-GRP      TO SUM-G-GRP.
           MOVE WS-GRP-OLD      TO SUM-G-OLD.
           MOVE WS-GRP-NEW      TO SUM-G-NEW.
           MOVE WS-GRP-NET      TO SUM-G-NET.
           MOVE SUM-G           TO WS-DETAIL-TEXT.
           PERFORM SUM-A-LINE.
           PERFORM BILL-BREAK.
           MOVE WS-CUR-GRP TO WS-PRV-GRP.
           ADD WS-GRP-OLD  TO WS-TOT-OLD.
           ADD WS-GRP-NEW  TO WS-TOT-NEW.
           ADD WS-GRP-NET  TO WS-TOT-NET.
           MOVE ZERO       TO WS-GRP-OLD
                              WS-GRP-NEW
                              WS-GRP-NET.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               PERFORM TOT-BREAK.
           MOVE +2 TO SUM-SKIP-CT.
       GRP-BREAK-EXIT.
           EXIT.
       TOT-BREAK SECTION.
           MOVE WS-TOT-OLD      TO SUM-T-OLD.
           MOVE WS-TOT-NEW      TO SUM-T-NEW.
           MOVE WS-TOT-NET      TO SUM-T-NET.
           MOVE SUM-T           TO WS-DETAIL-TEXT.
           MOVE +2              TO SUM-SKIP-CT.
           PERFORM SUM-A-LINE.
       TOT-BREAK-EXIT.
           EXIT.
       BILL-BREAK SECTION.
       BILL-BREAK-SUB.
           IF WS-BILL-GRP-SUB EQUAL 1 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT1.
           IF WS-BILL-GRP-SUB EQUAL 2 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT2.
           IF WS-BILL-GRP-SUB EQUAL 3 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT3.
           IF WS-BILL-GRP-SUB EQUAL 4 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT4.
           IF WS-BILL-GRP-SUB EQUAL 5 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT5.
           IF WS-BILL-GRP-SUB EQUAL 6 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT6.
           IF WS-BILL-GRP-SUB EQUAL 7 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT7.
           IF WS-BILL-GRP-SUB EQUAL 8 THEN
               ADD WS-GRP-NEW TO WS-BILL-TOT8.
           IF (SW-STAT-WORK-HIST EQUAL 'E' AND
               SW-STAT-SORT     EQUAL 'E') THEN
               GO TO BILL-BREAK-INDEX.
           MOVE WS-CUR-GRP      TO WS-BILL-GRP.
           IF WS-BILL-GRP-INDEX EQUAL WS-BILL-PRV-INDEX
               THEN GO TO BILL-BREAK-EXIT.
       BILL-BREAK-INDEX.
           MOVE SPACES            TO BILL-REC.
           MOVE '6'               TO BILL-INDIC.
           MOVE WS-BILL-PRV-INDEX TO BILL-NUM.
           MOVE WS-BILL-TOT1      TO BILL-TOT1.
           MOVE WS-BILL-TOT2      TO BILL-TOT2.
           MOVE WS-BILL-TOT3      TO BILL-TOT3.
           MOVE WS-BILL-TOT4      TO BILL-TOT4.
           MOVE WS-BILL-TOT5      TO BILL-TOT5.
           MOVE WS-BILL-TOT6      TO BILL-TOT6.
           MOVE WS-BILL-TOT7      TO BILL-TOT7.
           MOVE WS-BILL-TOT8      TO BILL-TOT8.
           WRITE BILL-REC.
           MOVE SPACES TO DET-B.
           MOVE WS-BILL-PRV-INDEX TO DET-B-NUM.
           IF SUB-SW (1) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT1 TO DET-B-TOT1.
           IF SUB-SW (2) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT2 TO DET-B-TOT2.
           IF SUB-SW (3) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT3 TO DET-B-TOT3.
           IF SUB-SW (4) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT4 TO DET-B-TOT4.
           IF SUB-SW (5) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT5 TO DET-B-TOT5.
           IF SUB-SW (6) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT6 TO DET-B-TOT6.
           IF SUB-SW (7) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT7 TO DET-B-TOT7.
           IF SUB-SW (8) EQUAL 'Y' THEN
               MOVE WS-BILL-TOT8 TO DET-B-TOT8.
           IF WS-BILL-PRV-INDEX EQUAL 00
               THEN MOVE 'ERROR'                     TO DET-B-DESC
               ELSE MOVE WS-DESC (WS-BILL-PRV-INDEX) TO DET-B-DESC.
           MOVE DET-B TO WS-DETAIL-TEXT.
           PERFORM BILL-A-LINE.
           MOVE ZERO TO WS-BILL-TOT1
                        WS-BILL-TOT2
                        WS-BILL-TOT3
                        WS-BILL-TOT4
                        WS-BILL-TOT5
                        WS-BILL-TOT6
                        WS-BILL-TOT7
                        WS-BILL-TOT8.
           MOVE WS-BILL-GRP-INDEX TO WS-BILL-PRV-INDEX.
       BILL-BREAK-EXIT.
           EXIT.
       WRITE-HIST SECTION.
           MOVE SORT-GRP     TO DM-HST-GRP.
           MOVE SORT-AAM     TO DM-HST-AAM.
           MOVE SORT-DSNAME  TO DM-HST-DSNAME.
           MOVE SORT-ALLOC   TO DM-HST-ALLOC.
           WRITE DM-HST-REC.
       INIT-GROUP-TABLE SECTION.
           OPEN INPUT AAMIN.
           SET GROUP-INDEX TO +1.
           MOVE LOW-VALUES TO GROUP-ENTRY (GROUP-INDEX).
           MOVE CTRL-GRP-NUM TO GROUP-NUMBER (GROUP-INDEX).
       INIT-GROUP-TABLE-LOOP.
           READ AAMIN AT END
               GO TO INIT-GROUP-TABLE-END.
           IF AAMIN-REC-AAM NOT GREATER THAN GROUP-AAM (GROUP-INDEX)
               THEN
               DISPLAY 'AAM TABLE OUT OF SEQUENCE'
                       ' OLD ENTRY = ' GROUP-AAM (GROUP-INDEX)
                       ' NEW ENTRY = ' AAMIN-REC-AAM
                       UPON SYSOUT
               MOVE +1000 TO ABEND-CODE
               CALL 'USERDUMP' USING ABEND-CODE.
           IF GROUP-INDEX GREATER THAN +999 THEN
               DISPLAY 'BUSINESS GROUP TABLE OVERFLOW'
                   UPON SYSOUT
               MOVE +1000 TO ABEND-CODE
               CALL 'USERDUMP' USING ABEND-CODE.
           MOVE AAMIN-REC-GROUP TO WS-GRP.
           IF (INDEX-SW (WS-GRP-INDEX) EQUAL 'N' OR
               SUB-SW (WS-GRP-SUB) EQUAL 'N') THEN
               DISPLAY 'INVALID GROUP NUM = ' WS-GRP
                       ' FOR AAM = ' AAMIN-REC-AAM
                       UPON SYSOUT
               MOVE +1000 TO ABEND-CODE
               CALL 'USERDUMP' USING ABEND-CODE.
           ADD +1 TO WS-GROUP-HI.
           SET GROUP-INDEX UP BY +1.
           MOVE AAMIN-REC-AAM TO GROUP-AAM (GROUP-INDEX).
           MOVE AAMIN-REC-GROUP TO GROUP-NUMBER (GROUP-INDEX).
           GO TO INIT-GROUP-TABLE-LOOP.
       INIT-GROUP-TABLE-END.
           CLOSE AAMIN.
       INIT-GROUP-TABLE-EXIT.
           EXIT.
       FIND-GROUP-NUM SECTION.
           SEARCH ALL GROUP-ENTRY
               WHEN GROUP-AAM (GROUP-INDEX) EQUAL SORT-AAM
                   GO TO FIND-GROUP-NUM-HIT.
           SET GROUP-INDEX TO +1.
       FIND-GROUP-NUM-HIT.
           MOVE GROUP-NUMBER (GROUP-INDEX) TO SORT-GRP.
       FIND-GROUP-NUM-EXIT.
           EXIT.
       INIT-WORK-HIST SECTION.
           OPEN INPUT  HISTORY
                OUTPUT WORK-HIST.
           READ HISTORY AT END
               MOVE '00/00/00' TO WS-OLD-HIST-DATE
               GO TO INIT-WORK-HIST-END.
           MOVE DM-HST-DATE    TO WS-OLD-HIST-DATE.
       INIT-WORK-HIST-LOOP.
           READ HISTORY AT END
               GO TO INIT-WORK-HIST-END.
           WRITE WK-HST-REC FROM DM-HST-REC.
           GO TO INIT-WORK-HIST-LOOP.
       INIT-WORK-HIST-END.
           CLOSE HISTORY WORK-HIST.
       INIT-WORK-HIST-EXIT.
           EXIT.
       DET-A-LINE SECTION.
           ADD DET-SKIP-CT TO DET-LINE-CT.
           IF DET-LINE-CT GREATER THAN LINE-MAX THEN
               ADD +2 DET-SKIP-CT GIVING DET-LINE-CT
               ADD +1 TO DET-PAGE-CT
               MOVE DET-PAGE-CT TO H-1-PAGE
               WRITE DET-REC FROM HEADING-1
                   AFTER ADVANCING TO-TOP
               WRITE DET-REC FROM HEADING-3
                   AFTER ADVANCING 1.
           WRITE DET-REC FROM DETAIL-LINE AFTER ADVANCING DET-SKIP-CT.
           MOVE +1 TO DET-SKIP-CT.
       BILL-A-LINE SECTION.
           ADD BIL-SKIP-CT TO BIL-LINE-CT.
           IF BIL-LINE-CT GREATER THAN LINE-MAX THEN
               ADD +2 BIL-SKIP-CT GIVING BIL-LINE-CT
               ADD +1 TO BIL-PAGE-CT
               MOVE BIL-PAGE-CT TO H-5-PAGE
               WRITE RBL-REC FROM HEADING-5
                   AFTER ADVANCING TO-TOP
               WRITE RBL-REC FROM HEADING-6
                   AFTER ADVANCING 1.
           WRITE RBL-REC FROM DETAIL-LINE AFTER ADVANCING DET-SKIP-CT.
           MOVE +1 TO BIL-SKIP-CT.
       SUM-A-LINE SECTION.
           ADD SUM-SKIP-CT TO SUM-LINE-CT.
           IF SUM-LINE-CT GREATER THAN LINE-MAX THEN
               ADD +2 SUM-SKIP-CT GIVING SUM-LINE-CT
               ADD +1 TO SUM-PAGE-CT
               MOVE SUM-PAGE-CT TO H-2-PAGE
               WRITE SUM-REC FROM HEADING-2
                   AFTER ADVANCING TO-TOP
               WRITE SUM-REC FROM HEADING-4
                   AFTER ADVANCING 1.
           WRITE SUM-REC FROM DETAIL-LINE AFTER ADVANCING SUM-SKIP-CT.
           MOVE +1 TO SUM-SKIP-CT.
       INIT-DESC-TABLE SECTION.
           OPEN INPUT GRPIN.
       INIT-DESC-READ.
           READ GRPIN AT END
               GO TO INIT-DESC-END.
           IF GRPIN-NUM NOT NUMERIC THEN
               DISPLAY 'INVALID GRPIN REC - NUM NOT NUMERIC'
                       UPON SYSOUT
               CALL 'USERDUMP' USING ABEND-CODE.
           IF GRPIN-NUM EQUAL 00 THEN
               DISPLAY 'INVALID GRPIN REC - NUM = ZERO'
                       UPON SYSOUT
               CALL 'USERDUMP' USING ABEND-CODE.
           IF GRPIN-TYPE EQUAL 'GRP' THEN
               GO TO INIT-DESC-GRP.
           IF GRPIN-TYPE EQUAL 'TYP' THEN
               GO TO INIT-DESC-TYP.
           DISPLAY 'INVALID GRPIN REC - INVALID TYPE'
                   UPON SYSOUT
           CALL 'USERDUMP' USING ABEND-CODE.
       INIT-DESC-GRP.
           MOVE 'Y' TO INDEX-SW (GRPIN-NUM).
           MOVE GRPIN-DESC TO WS-DESC (GRPIN-NUM).
           GO TO INIT-DESC-READ.
       INIT-DESC-TYP.
           IF GRPIN-NUM GREATER THAN 08 THEN
               DISPLAY 'INVALID GRPIN REC - TYP GREATER THAN 8'
                       UPON SYSOUT
               CALL 'USERDUMP' USING ABEND-CODE.
           MOVE 'Y' TO SUB-SW (GRPIN-NUM).
           MOVE GRPIN-DESC TO SUB-DESC (GRPIN-NUM).
           GO TO INIT-DESC-READ.
       INIT-DESC-END.
           CLOSE GRPIN.
       INIT-DESC-TABLE-EXIT.
           EXIT.
       E-O-J SECTION.
           STOP RUN.
