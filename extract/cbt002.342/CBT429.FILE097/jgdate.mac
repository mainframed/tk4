JGDATE   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)
         BALR  R9,0
         USING *,R9                BASE REGISTER
         USING ARGLIST,R1          ESTABLISH ADDRESSABILITY TO ARG LIST
         L     R4,DDDAD            ADDR OF JULIAN DAY TO R4
         L     R4,0(R4)            JULIAN DAY TO R4
         L     R5,YYAD             ADDR OF YEAR TO R5
         L     R5,0(R5)            YEAR TO R5
         L     R10,DDAD            ADDR OF MONTH DAY TO R10
         L     R11,MMAD            ADDR OF MONTH NUMBER TO R11
         DROP  R1                  LOSE ADDRESSABILITY TO ARG LIST
*
*
*        THE FOLLOWING LOGIC IS BASED ON ACM ALGORITHM 398
*
*
         SR    R14,R14             ZERO HIGH ORDER REGISTER
         LR    R15,R5              LOAD YY INTO LOW ORDER REG
         LA    R1,4                PUT DIVISOR (4) INTO R1
         DR    R14,R1              DIVIDE YY BY 4
         OR    R14,R14             IS REMAIDER ZERO
         BE    LEAPYEAR
         LA    R2,0                NOT LEAP YEAR -- T=0
         B     ENDLEAP
LEAPYEAR EQU   *
         LA    R2,1                LEAP YEAR -- T=1
ENDLEAP  EQU   *
         LA    R6,100              PUT 100 IN R6
         LA    R7,3055             PUT 3055 IN R7
         CR    R5,R6               IS YY LESS THAN 100
         BL    ENDCENT             YES -- BYPASS CENTURY LOGIC
         SR    R14,R14             ZERO HIGH ORDER REG
         LR    R15,R5              PUT YY IN LOW ORDER REG
         DR    R14,R6              DIVIDE BY 100
         OR    R14,R14             IS REMAINDER 0 (CENTURY)
         BNE   ENDCENT             NO -- SKIP CENTURY PROCESSING
         SR    R14,R14             ZERO HIGH ORDER REG
         DR    R14,R1              DIVIDE CENTURY NO BY 4
         OR    R14,R14             IS REMAINDER 0 (LEAP CENTURY)
         BE    ENDCENT             YES -- LEAP CENTURY -- T OK
         LA    R3,0                NOT A LEAP YEAR T = 0
ENDCENT  EQU   *
         LA    R3,59(R2)           R3 = T + 59
         CR    R4,R3               IS DDD GREATER T + 59
         BH    PASTFEB             AFTER FEBUARY
         LA    R3,0                R3=0 = NEW T
         B     DONEFEB
PASTFEB  EQU   *
         LA    R3,2                R3 = 2
         SR    R3,R2               R3 = 2-T = NEW T
DONEFEB  EQU   *
         LA    R2,91(R3,R4)        R2 = (D+91) = (91 + N + T)
         LR    R15,R2              COPY TO R15
         MR    R14,R6              (D+91) * 100
         DR    R14,R7              R15 = (M+2) = ((D+91) * 100) / 3055)
         LR    R1,R15              MOVE TO R1
         MR    R14,R7              (M+2) * 3055
         DR    R14,R6              (((M+2) * 3055) / 100)
         SR    R2,R15              D = (D+91) - (((M+2) * 3055) / 100)
         ST    R2,0(R10)           STORE AT MONTH DAY ADDR
         LA    R2,2                PUT 2 IN R2
         SR    R1,R2               M = ((M+2) - 2)
         ST    R1,0(R11)           STORE AT ADDR OF MONTH NUMBER
         MVC   16(4,R13),RC        MOVE RETURN CODE TO SAVED R15
         RETURN (14,12)            GO HOME
RC       DC    AL4(0)
ARGLIST  DSECT
YYAD     DS    F
DDDAD    DS    F
MMAD     DS    F
DDAD     DS    F
         END   JGDATE
