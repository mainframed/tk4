         TITLE 'DM120PUP - MATCH A RECS TO VTOC'
DM120PUP CSECT
*
         REGS
*
*   PROCESS EXTRACTED A RECS AGAINST SORTED VTOC LIST
*   REPORT ALL EXTRA VTOC RECS
*   MERGE VOL SER INTO A REC AND WRITE REC OUT
*
*
         USING DM120PUP,R15
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'DM120PUP - MATCH A RECS TO VTOC &SYSDATE &SYSTIME'
         EJECT
MAINEP   DS    0H
         SAVE  (14,12)
         LR    R12,R15        ENTRY ADR TO BASE REG
         DROP  R15
         USING DM120PUP,R12   PGM BASE
         LR    R2,R13
         LA    R13,SAVEAREA   SAVEAREA AND WORKING STORAGE
         USING SAVEAREA,R13
         ST    R2,SAVEAREA+4  SAVE CALLERS R13
         ST    R13,8(,R2)     BACK LINK SAVEAREA
         USING IHADCB,R6      ADDRESSABILITY
         LA    R6,SORTVDSN    A(DCB)
         OPEN  ((R6))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPV         NO
         LA    R6,EXTRACTA    A(DCB)
         OPEN  ((R6))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPA         NO
         LA    R6,MERGEA      A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPM         NO
         LA    R6,SYSPRINT    A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPP         NO
         DROP  R6
         BAL   R14,CHKA       READ FIRST REC
         CLI   SWA,C'E'       AT EOF
         BE    ERRAMT         EMPTY FILE -- ERROR
         BAL   R14,CHKV       READ FIRST REC
         CLI   SWV,C'E'       AT EOF
         BE    ERRVMT         EMPTY FILE -- ERROR
MAINLOOP EQU   *              PROCESS NEXT REC
         BAL   R14,CHKA       READ FILE IF NECESSARY
         BAL   R14,CHKV       READ FILE IF NECESSARY
         CLI   SWA,C'E'       EOF ON A FILE
         BE    MTA            YES -- EMPTY
         CLI   SWV,C'E'       EOF ON V FILE
         BE    MTV            YES -- EMPTY
         CLC   ARECDSNA,VRECDSN CHECK
         BE    HIT            MATCH -- OK
         BL    XTRAA          EXTRA A REC
         B     XTRAV          EXTRA V REC
HIT      EQU   *
         MVC   MRECDSND,ARECDSND POST
         MVC   MRECVOL,VRECVOL   POST
         MVC   MRECALOC,VRECALOC POST
         MVC   MRECUSED,VRECUSED POST
         MVC   MRECDSO,VRECDSO   POST
         MVC   MRECBSIZ,VRECBSIZ POST
         MVC   MRECRECL,VRECRECL POST
         MVC   MRECRFM,VRECRFM   POST
         MVC   MRECXTNT,VRECXTNT POST
         MVC   MRECCDAT,VRECCDAT POST
         MVC   MRECRDAT,VRECRDAT POST
         MVC   MRECDSNA,ARECDSNA POST
         PUT   MERGEA,MREC       WRITE THE MERGED REC
         MVI   SWA,C'N'          POST NO REC IN A FILE
         MVI   SWV,C'N'          POST NO REC IN V FILE
         B     MAINLOOP          LOOP BACK
MTA      EQU   *
         CLI   SWV,C'E'          EOF HERE TOO
         BE    EXIT              YES -- DONE
         B     XTRAV             MUST BE EXTRA V REC
MTV      EQU   *                 MUST BE EXTRA A
XTRAA    EQU   *                 EXTRA A REC
         MVC   MRECDSND,ARECDSND POST
         MVC   MRECVOL,SPACES    NO VOL SER
         MVC   MRECALOC,SPACES   NO ALLOC
         MVC   MRECUSED,SPACES   NO USED
         MVC   MRECDSO,SPACES    NO DSORG
         MVC   MRECBSIZ,SPACES   NO BLOCK SIZE
         MVC   MRECRECL,SPACES   NO LRECL
         MVC   MRECRFM,SPACES    NO REC FMT
         MVC   MRECXTNT,SPACES   NO EXTENT COUNT
         MVC   MRECCDAT,SPACES   NO CREATE DATE
         MVC   MRECRDAT,SPACES   NO REF DATE
         MVC   MRECDSNA,ARECDSNA POST
         PUT   MERGEA,MREC       WRITE THE MERGED REC
         MVI   SWA,C'N'          POST NO REC IN A FILE
         B     MAINLOOP          LOOP BACK
XTRAV    EQU   *                 EXTRA V REC
         MVC   PRECDSNA,VRECDSN  POST
         MVC   PRECVOL,VRECVOL   POST VOL SER
         BAL   R14,PRTL          PRINT A LINE
         MVI   SWV,C'N'          POST NO REC IN A FILE
         B     MAINLOOP          LOOP BACK
         TITLE 'DM120PUP - CHKA - DO WE NEED TO READ AREC'
CHKA     EQU   *
         ST    R14,CHKAR14       SAVE RETURN ADDR
         CLI   SWA,C'N'          NO REC IN BUF
         BNE   CHKAX             NO -- EXIT
         MVI   SWA,C'Y'          SET REC IN BUF
         GET   EXTRACTA,AREC     GET NEXT REC
*
*        NOTE:  EOF ROUTINE MAY RESET SWA TO 'E'
*
CHKAX    EQU   *
         L     R14,CHKAR14       RESTORE RETURN ADDR
         BR    R14               RETURN
CHKAEOF  EQU   *                 EOF ROUTINE
         MVI   SWA,C'E'          SET EOF SW
         BR    R14               RETURN TO GET ROUTINE
         TITLE 'DM120PUP - CHKV - DO WE NEED TO READ VREC'
CHKV     EQU   *
         ST    R14,CHKVR14       SAVE RETURN ADDR
         CLI   SWV,C'N'          NO REC IN BUF
         BNE   CHKVX             NO -- EXIT
         MVI   SWV,C'Y'          SET REC IN BUF
         GET   SORTVDSN,VREC     GET NEXT REC
*
*        NOTE:  EOF ROUTINE MAY RESET SWV TO 'E'
*
CHKVX    EQU   *
         L     R14,CHKVR14       RESTORE RETURN ADDR
         BR    R14               RETURN
CHKVEOF  EQU   *                 EOF ROUTINE
         MVI   SWV,C'E'          SET EOF SW
         BR    R14               RETURN TO GET ROUTINE
         TITLE 'DM120PUP - PRTL - PRINT A LINE'
PRTL     EQU   *
         ST    R14,PRTLR14       RESTORE RETURN ADDR
         AP    PRTLLINE,=P'1'    BUMP LINE COUNT
         CP    PRTLLINE,PRTLMAX  PAST END OF PAGE
         BL    PRTLP             NO -- PRINT IT
         AP    PRTLPAGE,=P'1'    BUMP PAGE NUMBER
         UNPK  PRTPT1P,PRTLPAGE  PAGE NUMBER TO TITLE
         OI    PRTPT1P+L'PRTPT1P-1,X'F0' FIX SIGN
         PUT   SYSPRINT,PRTPT1   PRINT TITLE LINE 1
         PUT   SYSPRINT,PRTPT2   PRINT TITLE LINE 2
         PUT   SYSPRINT,SPACES   PRINT BLANK LINE
         ZAP   PRTLLINE,=P'4'    RESET LINE COUNT
PRTLP    EQU   *
         PUT   SYSPRINT,PREC     PRINT DATA LINE
         L     R14,PRTLR14       RESTORE RETURN ADDR
         BR    R14               RETURN
         TITLE 'ERROR MESSAGES'
ERRAMT   EQU   *
         WTO   'DM120PUP - EXTRACTA FILE EMPTY',                       *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRVMT   EQU   *
         WTO   'DM120PUP - SORTVDSN FILE EMPTY',                       *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPA   EQU   *
         WTO   'DM120PUP - OPEN FAILED FOR EXTRACTA',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPV   EQU   *
         WTO   'DM120PUP - OPEN FAILED FOR SORTVDSN',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPM   EQU   *
         WTO   'DM120PUP - OPEN FAILED FOR MERGEA',                    *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPP   EQU   *
         WTO   'DM120PUP - OPEN FAILED FOR SYSPRINT',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
         TITLE 'EXIT/ABEND'
ABEND    EQU   *
         ABEND 1000,DUMP
EXIT     EQU   *
         CLOSE (EXTRACTA)
         CLOSE (SORTVDSN)
         CLOSE (MERGEA)
         CLOSE (SYSPRINT)
         L     R13,4(R13)
         RETURN (14,12),RC=0
         LTORG
         TITLE 'DATA AREAS - MISCELLANEOUS'
SAVEAREA DS    9D                  REG SAVE AREA
CHKAR14  DS    F                   R14 SAVE AREA FOR CHKA
CHKVR14  DS    F                   R14 SAVE AREA FOR CHKV
PRTLR14  DS    F                   R14 SAVE AREA FOR PRTL
SWA      DC    C'N'                FILE A STATUS SW
*              C'N'                NO REC IN BUF
*              C'Y'                REC IN BUF
*              C'E'                FILE AT EOF
SWV      DC    C'N'                FILE V STATUS SW
*              C'N'                NO REC IN BUF
*              C'Y'                REC IN BUF
*              C'E'                FILE AT EOF
PRTLLINE DC    PL2'56'             CURRENT LINE NUMBER
PRTLMAX  DC    PL2'56'             LINES PER PAGE
PRTLPAGE DC    PL2'0'              CURRENT PAGE NUMBER
*
VREC     DS    0CL89               V RECORD  (VTOC INFO)
VRECDSN  DS    CL44
VRECVOL  DS    CL6
VRECALOC DS    CL5
VRECUSED DS    CL5
VRECDSO  DS    CL3
VRECBSIZ DS    CL5
VRECRECL DS    CL5
VRECRFM  DS    CL3
VRECXTNT DS    CL3
VRECCDAT DS    CL5
VRECRDAT DS    CL5
*
AREC     DS    0CL88               A RECORD  (MCDS A REC INFO)
ARECDSND DS    CL44
ARECDSNA DS    CL44
*
MREC     DS    0CL133              M RECORD  (MERGED OUTOUT)
MRECDSND DS    CL44
MRECVOL  DS    CL6
MRECALOC DS    CL5
MRECUSED DS    CL5
MRECDSO  DS    CL3
MRECBSIZ DS    CL5
MRECRECL DS    CL5
MRECRFM  DS    CL3
MRECXTNT DS    CL3
MRECCDAT DS    CL5
MRECRDAT DS    CL5
MRECDSNA DS    CL44
*
PREC     DC    CL133' '            PRINT RECORD
         ORG   PREC
         DS    C                   CARRIAGE CONTROL
PRECDSNA DS    CL44
         DS    C
PRECVOL  DS    CL6
         ORG   ,                   TO END OF REC
*
PRTPT1   DC    CL133' '            TITLE LINE 1
         ORG   PRTPT1
         DC    C'1 DM120-01'       CARRIAGE CONTROL
         ORG   PRTPT1+30
         DC    C'UNMATCHED REPORT - MIGRATED FILES WITH NO'
         DC    C' MATCHING A RECORD'
         ORG   PRTPT1+120
         DC    C'PAGE '
PRTPT1P  DS    CL3                 PAGE NUMBER
         ORG   ,                   TO END OF REC
*
PRTPT2   DC    CL133' '            TITLE LINE 2
         ORG   PRTPT2
         DC    C'0DATA SET NAME'
         ORG   PRTPT2+46
         DC    C'VOL SER'
         ORG   ,                   TO END OF REC
SPACES   DC    CL133' '
         TITLE 'WORK AREAS - DCBS'
         PRINT NOGEN
EXTRACTA DCB   DDNAME=EXTRACTA,                                        X
               DSORG=PS,                                               X
               EODAD=CHKAEOF,                                          X
               LRECL=88,                                               X
               MACRF=GM,                                               X
               RECFM=FB
*
SORTVDSN DCB   DDNAME=SORTVDSN,                                        X
               DSORG=PS,                                               X
               EODAD=CHKVEOF,                                          X
               LRECL=89,                                               X
               MACRF=GM,                                               X
               RECFM=FB
*
MERGEA   DCB   DDNAME=MERGEA,                                          X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=PM,                                               X
               RECFM=FB
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=PM,                                               X
               RECFM=FBA
*
         DCBD  DSORG=(PS)
         END
