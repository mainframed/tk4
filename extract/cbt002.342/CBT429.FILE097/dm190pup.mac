         TITLE 'DM190PUP - ISSUE HSM COMMANDS FROM CARDS'
DM190PUP CSECT
         REGS
*
*        READ CARDS AND ISSUE HSM COMMANDS
*        COLS 01 - 72 CONTAIN THE COMMAND
*        COLS 73 - 80 ARE IGNORED
*
         USING DM190PUP,R15   ADDRESSABILITY
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'DM190PUP - XEQ COMMANDS FROM CARDS &SYSDATE &SYSTIME'
         TITLE 'DM190PUP - INITIALIZATION'
MAINEP   DS    0H
         SAVE (14,12)         SAVE REGS
         LR    R12,R15        MOVE BASE
         DROP  R15
         USING DM190PUP,R12   ADDRESSABILITY
         LR    R2,R13         A(OLD SAVE AREA)
         LA    R13,SAVEAREA   A(NEW SAVE AREA)
         USING SAVEAREA,R13   ADDRESSABILITY
         ST    R13,8(R2)      FORWARD CHAIN
         ST    R2,4(R13)      BACK CHAIN
         USING IHADCB,R6      ADDRESSABILITY
         LA    R6,SYSIN       A(DCB)
         OPEN  ((R6))
         TM    DCBOFLGS,X'10' OK?
         BZ    ERROPIN        NO -- ERROR
         LA    R6,SYSPRINT    A(DCB)
         OPEN  ((R6),OUTPUT)
         TM    DCBOFLGS,X'10' OK?
         BZ    ERROPPR        NO -- ERROR
         TITLE 'DM190PUP - MAIN LOOP'
LOOP     EQU   *
         GET   SYSIN,INPREC   READ NEXT REC
         MVC   HSMTEXT,INPCMD POST TO HSM AREA
         MVC   PRTINP,INPREC  POST TO PRINT LINE
         LA    R1,XEQPARM     POST PARAM LIST
         L     R15,=V(HSMXEQ) A(SUBROUTINE)
         BALR  R14,R15        EXECUTE IT
         ST    R15,XEQRC      SAVE RETURN CODE
         L     R15,HSMREQ     POST REQUEST CODE
         CVD   R15,WORKDW     NOW PACKED
         OI    WORKDW+7,X'0F' FIX SIGN
         UNPK  PRTREQ,WORKDW  NOW CHAR
         L     R15,HSMRC      POST MWE RETURN CODE
         CVD   R15,WORKDW     NOW PACKED
         OI    WORKDW+7,X'0F' FIX SIGN
         UNPK  PRTMRC,WORKDW  NOW CHAR
         L     R15,XEQRC      POST SVC RETURN CODE
         CVD   R15,WORKDW     NOW PACKED
         OI    WORKDW+7,X'0F' FIX SIGN
         UNPK  PRTSRC,WORKDW  NOW CHAR
         BAL   R14,PRTL       PRINT IT OUT
         B     LOOP
         TITLE 'DM190PUP - PRTL - PRINT A LINE'
PRTL     EQU   *
         ST    R14,PRTLR14    SAVE RETURN ADDR
         PUT   SYSPRINT,PRTREC
         L     R14,PRTLR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'HSMDRVE - ERROR ROUTINES'
ERROPIN  EQU   *
         WTO   'DM190PUP - OPEN FAILED FOR SYSIN',                     X
               ROUTCDE=2,DESC=7
         B     ABEND
ERROPPR  EQU   *
         WTO   'DM190PUP - OPEN FAILED FOR SYSPRINT',                  X
               ROUTCDE=2,DESC=7
         B     ABEND
ABEND    EQU   *
         ABEND 1000,DUMP
         TITLE 'DM190PUP - EXIT - TERMINATION'
EXIT     EQU   *
         CLOSE SYSIN
         CLOSE SYSPRINT
         L     R13,4(R13)     A(OLD SAVE AREA)
         RETURN (14,12),RC=0
         LTORG
         TITLE 'DM190PUP - DATA AREA'
SAVEAREA DS    9D             REG SAVE AREA
WORKDW   DS    D              DOUBLEWORD WORK AREA
*
PRTLR14  DS    F              R14 SAVE AREA FOR PRTL - PRINT A LINE
*
XEQPARM  DS    0F             EXECUTE PARAMETER LIST
         DC    A(HSMFUNC)
         DC    A(HSMLEN)
         DC    A(HSMTEXT)
         DC    A(HSMREQ)
         DC    X'80',AL3(HSMRC)
*
HSMFUNC  DC    F'9'
HSMLEN   DC    F'72'
HSMREQ   DS    F
HSMRC    DS    F
XEQRC    DS    F
HSMTEXT  DS    CL80
*
INPREC   DS    0CL80          INPUT CARD BUF
INPCMD   DS    CL72           HSM COMMAND AREA
         DS    CL8            FILLER
*
PRTREC   DC    CL133' '       PRINT LINE BUF
         ORG   PRTREC
         DS    C              CHAR CONT
PRTINP   DS    CL80           INPUT CARD
         DS    C              FILLER
PRTREQ   DS    CL15           HSM REQUEST NUMBER
         DS    C              FILLER
PRTMRC   DS    CL15           MWE RETURN CODE
         DS    C              FILLER
PRTSRC   DS    CL15           SVC RETURN CODE
         ORG   ,              PAST LINE
         TITLE 'DM190PUP - DCBS, DCBD'
         PUSH  PRINT
         PRINT NOGEN
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               EODAD=EXIT,                                             X
               LRECL=80,                                               X
               MACRF=GM,                                               X
               RECFM=FB
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=PM,                                               X
               RECFM=FBA
*
         DCBD  DSORG=PS
         END
