         TITLE 'DM150PUP - EXTRACT RECS FROM VTOCS'
DM150PUP CSECT
         REGS
         USING DM150PUP,R15
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'DM150PUP - MERGE VVDS AND VTOC DATA &SYSDATE'
*
*   MERGE SORTED VVDS EXTRACT FILE WITH SORTED VTOC FILE
*   FILES MUST BE SORTED BY VOLSER WITHIN DATASET NAME
*
*   ON MATCH:
*             (VVDS ALLOCATED NOT COPIED)
*              VVDS USED      WILL REPLACE VTOC USED
*              VVDS CI SIZE   WILL REPLACE VTOC BLOCK SIZE
*              VVDS MAX LRECL WILL REPLACE VTOC LRECL
*              VVDS EXTENT CT WILL REPLACE VTOC EXTENT CT
*              'VB'                REPLACE VTOC RECFM
*
*              AN UPDATED VTOC RECORD WILL THEN BE WRITTEN
*
*   ON EXTRA VTOC:
*
*              AN UNCHANGED VTOC RECORD WILL THEN BE WRITTEN
*              IF NOT VSAM THEN A LINE WILL BE WRITTEN TO
*                               THE ERROR REPORT
*
*   ON EXTRA VVDS:
*
*              A LINE WILL BE WRITTEN TO THE ERROR REPORT
*
         EJECT
MAINEP   DS    0H
         SAVE  (14,12)
         LR    R12,R15        ENTRY ADR TO BASE REG
         DROP  R15
         USING DM150PUP,R12   PGM BASE
         LR    R2,R13
         LA    R13,SAVEAREA   SAVEAREA AND WORKING STORAGE
         USING SAVEAREA,R13
         ST    R2,SAVEAREA+4  SAVE CALLERS R13
         ST    R13,8(,R2)     BACK LINK SAVEAREA
         USING IHADCB,R6      ADDRESSABILITY
         LA    R6,SORTVDSN    A(DCB)
         OPEN  ((R6))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPSD        NO -- ERROR
         LA    R6,SORTVVVR    A(DCB)
         OPEN  ((R6))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPSV        NO -- ERROR
         LA    R6,MERGVDSN    A(DCB)
         OPEN  ((R6),(OUTPUT))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPMV        NO -- ERROR
         LA    R6,SYSPRINT    A(DCB)
         OPEN  ((R6),(OUTPUT))
         TM    DCBOFLGS,DCBOFOPN OPEN OK
         BNO   ERROPPR        NO -- ERROR
         DROP  R6
         BAL   R14,GETD       PRIME D REC
         CLI   SWD,C'E'       EOF
         BE    ERRMTSD        YES -- ERROR
         BAL   R14,GETV       PRIME V REC
         CLI   SWV,C'E'       EOF
         BE    ERRMTSV        YES -- ERROR
         TITLE 'MAIN PROCESS'
MAINLOOP EQU   *              PROCESS NEXT REC
         BAL   R14,GKEY       CALCULATE KEY
         B     *+4(R15)       CHECK RETURN CODE
         B     MAINKOK        00 -- KEY FOUND - PROCESS IT
         B     EXIT           04 -- EOF - DONE
MAINKOK  EQU   *
         CLI   SWD,C'E'       CHECK FOR D MATCH
         BE    MAINXV         NO D -- EXTRA V
         CLC   DRECDSN,KEYDSN
         BNE   MAINXV
         CLC   DRECVOL,KEYVOL
         BNE   MAINXV
         CLI   SWV,C'E'       CHECK FOR V MATCH
         BE    MAINXD         NO V -- EXTRA D
         CLC   VRECDSN,KEYDSN
         BNE   MAINXD
         CLC   VRECVOL,KEYVOL
         BNE   MAINXD
*
*                             MATCH
*
         MVC   MREC,DREC      COPY ENTIRE RECORD
         MVC   MRECUSED,VRECUSED UPDATE USED
         MVC   MRECBSIZ,VRECCISZ UPDATE BLOCKSIZE
         MVC   MRECRECL,VRECRECL UPDATE LRECL
         MVC   MRECRFM,=C'VB ' SET RECFM
         BAL   R14,FIXR       FIX REFDATE ON VSAM INDEXES
         BAL   R14,PUTM       WRITE MERGED REC
         BAL   R14,GETD       GET NEW D
         BAL   R14,GETV       GET NEW V
         B     MAINLOOP       LOOP BACK
MAINXD   EQU   *              EXTRA D REC
         CLC   DRECDSO,=C'VS ' VSAM
         BNE   MAINXDNR       NO -- SKIP REPORT
         BAL   R14,RPTD       PUT REPORT LINE
MAINXDNR EQU   *
         MVC   MREC,DREC      COPY ENTIRE RECORD
         BAL   R14,PUTM       WRITE MERGED REC
         BAL   R14,GETD       GET NEW D
         B     MAINLOOP       LOOP BACK
MAINXV   EQU   *              EXTRA V REC
         BAL   R14,RPTV       PUT REPORT LINE
         BAL   R14,GETV       GET NEW V
         B     MAINLOOP       LOOP BACK
         TITLE 'GETD     - GET NEXT D REC'
GETD     EQU   *
         ST    R14,GETDR14    SAVE RETURN ADDR
         GET   SORTVDSN,DREC
         CLI   SWD,C'E'       EOF
         BE    GETDX          YES -- DONE
         CLI   SWD,C'F'       FIRST REC
         BE    GETDU          YES -- SKIP SEQ CHK
         CLC   GETDODSN,DRECDSN  SEQ CHK DSN
         BL    GETDU          OK -- DONE
         BH    ERRSEQD        ERROR
         CLC   GETDOVOL,DRECVOL DSN SAME -- SEQ CHK VOL
         BNL   ERRSEQD        ERROR
GETDU    EQU   *
         MVC   GETDODSN,DRECDSN POST DSN
         MVC   GETDOVOL,DRECVOL POST VOL
         MVI   SWD,C'V'       POST VALID
GETDX    EQU   *
         L     R14,GETDR14    RESTORE RETURN ADDR
         BR    R14            RETURN
GETDEOF  EQU   *
         MVI   SWD,C'E'       POST 'EOF'
         BR    R14            RETURN TO GET MACRO
         TITLE 'GETV     - GET NEXT V REC'
GETV     EQU   *
         ST    R14,GETVR14    SAVE RETURN ADDR
         GET   SORTVVVR,VREC
         CLI   SWV,C'E'       EOF
         BE    GETVX          YES -- DONE
         CLI   SWV,C'F'       FIRST REC
         BE    GETVU          YES -- SKIP SEQ CHK
         CLC   GETVODSN,VRECDSN  SEQ CHK DSN
         BL    GETVU          OK -- DONE
         BH    ERRSEQV        ERROR
         CLC   GETVOVOL,VRECVOL DSN SAME -- SEQ CHK VOL
         BNL   ERRSEQV        ERROR
GETVU    EQU   *
         MVC   GETVODSN,VRECDSN POST DSN
         MVC   GETVOVOL,VRECVOL POST VOL
         MVI   SWV,C'V'       POST VALID
GETVX    EQU   *
         L     R14,GETVR14    RESTORE RETURN ADDR
         BR    R14            RETURN
GETVEOF  EQU   *
         MVI   SWV,C'E'       POST 'EOF'
         BR    R14            RETURN TO GET MACRO
         TITLE 'PUTM     - WRITE NEXT M REC'
PUTM     EQU   *
         ST    R14,PUTMR14    SAVE RETURN ADDR
         PUT   MERGVDSN,MREC
         L     R14,PUTMR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'GKEY     - CALCULATE KEY'
GKEY     EQU   *
         ST    R14,GKEYR14    SAVE RETURN ADDR
         MVI   SWK,C'I'       POST INVALID
         CLI   SWD,C'E'       VALID D KEY
         BE    GKEYV          NO - TRY V REC
         MVC   KEYDSN,DRECDSN POST DSN
         MVC   KEYVOL,DRECVOL POST VOLSER
         MVI   SWK,C'V'       POST VALID
GKEYV    EQU    *
         CLI   SWV,C'E'       VALID V KEY
         BE    GKEYE          NO - CHECK FOR EOF
         CLI   SWK,C'V'       VALID YET
         BNE   GKEYVM         NO -- MOVE V FIELDS
         CLC   KEYDSN,VRECDSN CHECK KEYS
         BL    GKEYXG         D KEY GOOD
         BH    GKEYVM         V KEY GOOD
         CLC   KEYVOL,VRECVOL LOWEST?
         BNH   GKEYXG         YES -- DONE
GKEYVM   EQU   *
         MVC   KEYDSN,VRECDSN POST DSN
         MVC   KEYVOL,VRECVOL POST VOLSER
         MVI   SWK,C'V'       POST VALID
         B     GKEYXG         DONE
GKEYE    EQU   *              CHECK FOR ALL EOF
         CLI   SWK,C'I'       STILL INVALID
         BNE   GKEYXG         NO -- KEY GOOD
         LA    R15,4          POST RC=04 - ALL EOF
         B     GKEYX
GKEYXG   EQU   *
         SR    R15,R15        POST RC=00 - OK
GKEYX    EQU   *
         L     R14,GKEYR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'RPTV     - VVR LINE TO RPT'
RPTV     EQU   *
         ST    R14,RPTVR14    SAVE RETURN ADDR
         MVC   DET,SPACES     CLEAR DETAIL LINE
         MVC   DETTYPE,=C'VVR '
         MVC   DETDSN,VRECDSN
         MVC   DETVOL,VRECVOL
         MVC   DETALOC,VRECALOC
         MVC   DETUSED,VRECUSED
         MVC   DETXTNT,VRECXTNT
         MVC   DETRECL,VRECRECL
         MVC   DETBSIZ,VRECCISZ
         BAL   R14,PUTD
         L     R14,RPTVR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'RPTD     - DSCB LINE TO RPT'
RPTD     EQU   *
         ST    R14,RPTDR14    SAVE RETURN ADDR
         MVC   DET,SPACES     CLEAR DETAIL LINE
         MVC   DETTYPE,=C'DSCB'
         MVC   DETDSN,DRECDSN
         MVC   DETVOL,DRECVOL
         MVC   DETALOC,DRECALOC
         MVC   DETUSED,DRECUSED
         MVC   DETCDAT,DRECCDAT
         MVC   DETRDAT,DRECRDAT
         MVC   DETXTNT,DRECXTNT
         MVC   DETDSO,DRECDSO
         MVC   DETRFM,DRECRFM
         MVC   DETRECL,DRECRECL
         MVC   DETBSIZ,DRECBSIZ
         BAL   R14,PUTD
         L     R14,RPTDR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'PUTD     - WRITE OUT DETAIL REC'
PUTD     EQU   *
         ST    R14,PUTDR14    SAVE RETURN ADDR
         AP    PUTDLC,=P'1'   BUMP LINE COUNT
         CP    PUTDLC,PUTDLM  PAST MAX
         BNH   PUTDP          NO -- WRITE THE LINE
         AP    PUTDPC,=P'1'   BUMP PAGE COUNT
         MVC   TTL1PMSK,=X'40202120'
         ED    TTL1PMSK,PUTDPC EDIT PAGE COUNT
         MVI   RPTCC,C'1'     POST CC (EJECT)
         MVC   RPTDET,TTL1    POST DETAIL
         PUT   SYSPRINT,RPT   PRINT LINE
         MVI   RPTCC,C' '     POST CC (SINGLE SPACE)
         MVC   RPTDET,TTL2    POST DETAIL
         PUT   SYSPRINT,RPT   PRINT LINE
         MVC   RPTDET,SPACES  CLEAR
         PUT   SYSPRINT,RPT   PRINT LINE
         ZAP   PUTDLC,=P'3'   RESET LINE COUNT
PUTDP    EQU   *
         MVC   RPTDET,DET     POST DETAIL
         PUT   SYSPRINT,RPT   PRINT LINE
         L     R14,PUTDR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'FIXR     - FIX REFDATE ON VSAM INDEXES'
FIXR     EQU   *
         ST    R14,FIXRR14    SAVE RETURN ADDR
         LA    R0,MRECDSN     A(CURRENT DSN)
         LA    R1,L'MRECDSN   L'(CURRENT DSN)
         LA    R14,FIXRODSN   A(OLD DSN)
         LR    R15,R1         SAME LENGTH
         CLCL  R0,R14         CHECK NAMES
         BE    FIXRSAVE       S/B IMPOSSIBLE
         CH    R1,=H'6'       ROOM FOR 'INDEX '
         BL    FIXRSAVE       NO -- NO MATCH
         LR    R1,R0          MOVE A(REST OF CURR NAME)
         CLC   =C'INDEX ',0(R1) CHECK NEW DSN
         BNE   FIXRSAVE       NO MATCH
         CLC   =C'DATA ',0(R14) CHECK OLD DSN
         BNE   FIXRSAVE       NO MATCH
         MVC   MRECRDAT,FIXRORDT MATCH - OVERLAY REF DATE
FIXRSAVE EQU   *
         MVC   FIXRODSN,MRECDSN SAVE OLD DSN
         MVC   FIXRORDT,MRECRDAT SAVE OLD REF DATE
         L     R14,FIXRR14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'ERROR MESSAGES'
ERRMTSD  EQU   *
         WTO   'DM150PUP - SORTVDSN FILE IS EMPTY',                    *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRMTSV  EQU   *
         WTO   'DM150PUP - SORTVVVR FILE IS EMPTY',                    *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPMV  EQU   *
         WTO   'DM150PUP - OPEN FAILED FOR MERGVDSN',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPPR  EQU   *
         WTO   'DM150PUP - OPEN FAILED FOR SYSPRINT',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPSD  EQU   *
         WTO   'DM150PUP - OPEN FAILED FOR SORTVDSN',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERROPSV  EQU   *
         WTO   'DM150PUP - OPEN FAILED FOR SORTVVVR',                  *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRSEQD  EQU   *
         WTO   'DM150PUP - SORTVDSN FILE OUT OF SEQUENCE',             *
               ROUTCDE=11,DESC=7
         B     ABEND
ERRSEQV  EQU   *
         WTO   'DM150PUP - SORTVVVR FILE OUT OF SEQUENCE',             *
               ROUTCDE=11,DESC=7
         B     ABEND
         TITLE 'EXIT/ABEND'
ABEND    EQU   *
         ABEND 1000,DUMP
EXIT     EQU   *
         CLOSE SORTVDSN
         CLOSE SORTVVVR
         CLOSE MERGVDSN
         CLOSE SYSPRINT
         L     R13,4(R13)
         RETURN (14,12),RC=0
         LTORG
         TITLE 'DATA AREAS - MISCELLANEOUS'
*
*        DOUBLEWORDS
*
SAVEAREA DS    9D                  REG SAVE AREA
*
*        FULLWORDS
*
FIXRR14  DS    F              R14 SAVE AREA FOR FIXR
GETDR14  DS    F              R14 SAVE AREA FOR GETD
GETVR14  DS    F              R14 SAVE AREA FOR GETV
GKEYR14  DS    F              R14 SAVE AREA FOR GKEY
PUTDR14  DS    F              R14 SAVE AREA FOR PUTD
PUTMR14  DS    F              R14 SAVE AREA FOR PUTM
RPTDR14  DS    F              R14 SAVE AREA FOR RPTD
RPTVR14  DS    F              R14 SAVE AREA FOR RPTV
*
*        HEX AND CHAR DATA
*
SWD      DC    C'F'           SWITCH FOR SORTVDSN
*              C'E'           EOF
*              C'F'           FIRST REC
*              C'V'           VALID
SWK      DC    C'I'           SWITCH FOR GKEY ROUTINE
*              C'I'           INVALID
*              C'V'           VALID
SWV      DC    C'F'           SWITCH FOR SORTVVVR
*              C'E'           EOF
*              C'F'           FIRST REC
*              C'V'           VALID
*
PUTDLC   DC    PL2'56'        LINE COUNT
PUTDLM   DC    PL2'56'        LINES PER PAGE
PUTDPC   DC    PL2'0'         PAGE COUNT
*
KEYDSN   DS    CL44
KEYVOL   DS    CL6
GETDODSN DS    CL44
GETDOVOL DS    CL6
GETVODSN DS    CL44
GETVOVOL DS    CL6
FIXRODSN DC    XL44'00'
FIXRORDT DC    CL5' 00001'
*
SPACES   DC    CL132' '
*
         TITLE 'DATA AREAS - RECORD AREAS'
DREC     DS    0CL89
DRECDSN  DS    CL44
DRECVOL  DS    CL6
DRECALOC DS    CL5
DRECUSED DS    CL5
DRECDSO  DS    CL3
DRECBSIZ DS    CL5
DRECRECL DS    CL5
DRECRFM  DS    CL3
DRECXTNT DS    CL3
DRECCDAT DS    CL5
DRECRDAT DS    CL5
*
MREC     DS    0CL89
MRECDSN  DS    CL44
MRECVOL  DS    CL6
MRECALOC DS    CL5
MRECUSED DS    CL5
MRECDSO  DS    CL3
MRECBSIZ DS    CL5
MRECRECL DS    CL5
MRECRFM  DS    CL3
MRECXTNT DS    CL3
MRECCDAT DS    CL5
MRECRDAT DS    CL5
*
VREC     DS    0CL81
VRECDSN  DS    CL44
         DS    CL4
VRECALOC DS    CL5
         DS    CL4
VRECUSED DS    CL5
VRECCISZ DS    CL5
VRECXTNT DS    CL3
VRECRECL DS    CL5
VRECVOL  DS    CL6
*
         TITLE 'DATA AREAS - PRINT AREAS'
RPT      DS    0CL33
RPTCC    DC    C' '
RPTDET   DC    CL132' '
*
TTL1     DC    CL132' '
         ORG   TTL1
         DC    C'DM150-01'
         ORG   TTL1+60
         DC    C'UNMATCHED REPORT'
         ORG   TTL1+122
         DC    C'PAGE'             UCBTYPE
TTL1PMSK DS    CL4
         ORG   TTL1+L'TTL1
*
TTL2     DC    CL132' '
         ORG   TTL2
         DC    C'TYPE '
         DC    CL45'DSN'
         DC    C'VOLSER '
         DC    C'ALLOC '
         DC    C' USED '
         DC    C'CR-DT '
         DC    C'RF-DT '
         DC    C' XT '
         DC    C'DSO '
         DC    C'RFM '
         DC    C'LRECL '
         DC    C'BSIZE '
         ORG   TTL2+L'TTL2
*
DET      DC    CL132' '
         ORG   DET
DETTYPE  DS    CL4
         DS    C
DETDSN   DS    CL44
         DS    C
DETVOL   DS    CL6
         DS    C
DETALOC  DS    CL5
         DS    C
DETUSED  DS    CL5
         DS    C
DETCDAT  DS    CL5
         DS    C
DETRDAT  DS    CL5
         DS    C
DETXTNT  DS    CL3
         DS    C
DETDSO   DS    CL3
         DS    C
DETRFM   DS    CL3
         DS    C
DETRECL  DS    CL5
         DS    C
DETBSIZ  DS    CL5
         ORG   DET+L'DET
*
         TITLE 'DATA AREAS - DCBS'
         PUSH  PRINT
         PRINT NOGEN
SORTVVVR DCB   DDNAME=SORTVVVR,                                        X
               DSORG=PS,                                               X
               EODAD=GETVEOF,                                          X
               LRECL=81,                                               X
               MACRF=GM,                                               X
               RECFM=FB
*
SORTVDSN DCB   DDNAME=SORTVDSN,                                        X
               DSORG=PS,                                               X
               EODAD=GETDEOF,                                          X
               LRECL=89,                                               X
               MACRF=GM,                                               X
               RECFM=FB
*
MERGVDSN DCB   DDNAME=MERGVDSN,                                        X
               DSORG=PS,                                               X
               LRECL=89,                                               X
               MACRF=PM,                                               X
               RECFM=FB
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               LRECL=133,                                              X
               MACRF=PM,                                               X
               RECFM=FBA
*
         DCBD  DEVD=(DA),DSORG=(PS)
         POP   PRINT
         END
