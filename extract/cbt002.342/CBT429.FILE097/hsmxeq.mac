         TITLE 'HSMXEQ  - ISSUE MWE TO HSM'
HSMXEQ   CSECT
         REGS
*
*        INVOKE HSM SVC TO PERFORM FUNCTION
*
*        PARAM LIST FOR THIS ROUTINE:
*
*                    R1:  --> 5 FULL WORD PARAMETER ADDRESS LIST
*
*                    P1:  --> FULL WORD, MUST CONTAIN F'9'
*                    P2:  --> FULL WORD, MUST CONTAIN LENGTH OF TEXT
*                    P3:  --> TEXT AREA
*                    P4:  --> FULL WORD, WILL CONTAIN HSM REQ NUMBER
*                             ON RETURN
*                    P5:  --> FULL WORD, WILL CONTAIN HSM RETURN CODE
*                             ON RETURN
*
*        LOGIC FOR THIS SUBROUTINE IS:
*
*            1)  VALIDATE PARAM LIST
*            2)  (BMWE) BUILD MWE
*            3)  (XHSM) PASS MWE TO HSM IGX00024 ROUTINE
*            4)  POST REQ NUMBER AND MWE RETURN CODE TO PARAM LIST
*            5)  RETURN TO CALLER WITH R15 = R15 FROM HSM
*
         USING HSMXEQ,R15     ADDRESSABILITY
         B     MAINEP
         DC    AL1(L'CSECTLIT)
CSECTLIT DC    C'HSMXEQ - EXECUTE AN HSM COMMAND &SYSDATE &SYSTIME'
         TITLE 'HSMXEQ - ENTRY AND PARM PROCESSING'
MAINEP   DS    0H
         SAVE (14,12)         SAVE REGS
         LR    R12,R15        MOVE BASE
         DROP  R15
         USING HSMXEQ,R12     ADDRESSABILITY
         LR    R2,R13         A(OLD SAVE AREA)
         LA    R13,SAVEAREA   A(NEW SAVE AREA)
         USING SAVEAREA,R13   ADDRESSABILITY
         ST    R13,8(R2)      FORWARD CHAIN
         ST    R2,4(R13)      BACK CHAIN
         MVC   RC,=F'0'       CLEAR RETURN CODE
         ST    R1,ADDRPARM    POST A(PARAM LIST)
         LTR   R1,R1          IS THERE A PARAM LIST
         BZ    ERRPARAM       NO -- ERROR
         L     R15,0(R1)      A(FIRST PARM) - FUNCTION CODE
         L     R14,0(R15)     POST FUNCTION CODE
         CH    R14,=H'9'      IS IT 'PROCESS AN OPR COMD'
         BNE   ERRBADFN       NO -- FUNCTION NUMBER BAD
         ST    R15,ADDRFUNC   POST A(FUNC)
         TM    0(R1),X'80'    IS THIS THE LAST PARAM
         BO    ERRPARAM       YES -- ERROR
         L     R15,4(R1)      A(SECOND PARAM) TEXT LENGTH
         MVC   TEXTLEN,0(R15) POST TEXT LENGTH
         LA    R14,CMDMAX     POST MAX LEGAL COMMAND
         C     R14,TEXTLEN    TEXT TOO LONG
         BL    ERRTEXTL       YES -- ERROR
         TM    4(R1),X'80'    IS THIS THE LAST PARAM
         BNZ   ERRPARAM       YES -- ERROR
         L     R15,8(R1)      A(THIRD PARM) TEXT
         ST    R15,ADDRTEXT   POST
         TM    8(R1),X'80'    IS THIS THE LAST PARAM
         BNZ   ERRPARAM       YES -- ERROR
         L     R15,12(R1)     A(FOURTH PARAM) HSM REQUEST NUMBER
         ST    R15,ADDRHREQ   POST
         TM    12(R1),X'80'   IS THIS THE LAST PARAM
         BNZ   ERRPARAM       YES -- ERROR
         L     R15,16(R1)     A(FIFTH PARAM) HSM RETURN CODE
         ST    R15,ADDRHRC
         TM    16(R1),X'80'   IS THIS THE LAST PARAM
         BZ    ERRPARAM       NO -- ERROR
         L     R6,TEXTLEN     L'TEXT
         LTR   R6,R6          IS IT EMPTY
         BNP   ERRNOTXT       NO -- ERROR
         BAL   R14,BMWE       BUILD MWE
         BAL   R14,XHSM       EXECUTE HSM CALL
         L     R14,ADDRHREQ   A(HSM REQ NUMBER)
         MVC   0(4,R14),MWERQN POST
         L     R14,ADDRHRC    A(HSM RETURN CODE)
         MVC   0(4,R14),MWERC POST
         B     EXIT
         TITLE 'HSMXEQ - BMWE - BUILD MWE'
BMWE     EQU   *
*
*        WARNING:  THIS CODE USES AN INTERFACE THAT IS NOT SUPPORTED
*                  BY IBM AS A STANDARD EXTERNAL INTERFACE.  THUS,
*                  IT IS NOT DOCUMENTED IN THE USUAL SENSE AND IS
*                  SUBJECT TO CHANGE WITHOUT NOTICE.  HOWEVER,
*                  SINCE THIS INTERFACE IS USED BY TSO, JES, AND
*                  OTHER MVS COMPONENTS IT IS UNLIKELY TO CHANGE
*                  DRASTICALLY.
*
*        LOGIC FOR THIS SECTION WAS DEVELOPED BY EXAMINING MICROFICHE
*        FOR HSM COMPONENTS THAT USE THIS INTERFACE AND BY TESTING.
*        SOME DOCUMENTATION IS AVAILABLE IN LY35-0076 DATA FACILITY
*        HSM DIAGNOSIS REFERENCE VOLUME 1.  THERE IS A
*        SECTION ON IGX00024, THE MODULE THAT FIELDS HSM REQUESTE
*        FROM OTHER COMPONENTS.  AN EXAMINATION OF SEVERAL OF THE
*        COMMAND SERVICE ROUTINES SUCH AS ARCQARC SHOWS THAT THEY CALL
*        IGX00024 VIA AN SVC.  EXAMINATION OF THE MICROFICHE FOR
*        ANY OF THESE MODULES INDICATES FIELDS IN THE MWE THAT MUST BE
*        FILLED IN BEFORE CALLING THE SVC.
*
*        ARCQARC USES AN MWE WHOSE TOTAL LENGTH IS:
*
*            HEADER (140 BYTES) + 4 (FOR CONTROL) + 256
*
*        ARCQARC LOGIC FLOW IN INITIALIZATION IS:
*
*            1)  SET L = 0 (MESSAGE LENGTH)
*
*        ARCQARC LOGIC FLOW IN 'INSERT' IS:
*
*            2)  APPEND TEXT TO MESSAGE AREA (STARTS AT MWECMDT)
*            3)  ADD TEXT LENGTH TO L
*
*        ARCQARC LOGIC FLOW IN 'SENDMWE' IS:
*
*            4)  ZERO OUT HEADER
*            5)  SET MWELEN = HEADER LEN + 4 + L
*            6)  SET MWEFUNF = 9
*            7)  SET MWEFNOW BIT = 1 - INDICATES NO WAIT BY HSM
*            8)  SET MWECMDL = L + 4 - COMMAND LENGTH
*            9)  SET MWECMDO = 0     - COMMAND OFFSET
*           10)  ISSUE ROUTER SVC WITH  R0 = 9 AND R15 = 24
*
*        LOGIC FLOW IN THIS ROUTINE IS:
*
*            1)  ZERO OUT ENTIRE MWE
*            2)  SET MWEID TO 'MWE '
*            3)  SET MWECMDL = TEXT LEN FROM PARAM LIST + 4
*            4)  SET MWECMDO = 0
*            5)  COPY TEXT FROM PARAM AREA
*            6)  SET MWELEN = HEADER LEN + 4 + TEXT LEN
*            7)  RETURN (SVC IS ISSUED IN XHSM)
*
*       NOTE:  TSO (MWEFTSOR) AND NO WAIT (MWEFNOW) BITS ARE NOT SET.
*              THIS MEANS WE ARE NOT TSO AND WE WANT HSM TO WAIT
*              US UNTIL THE COMMAND COMPLETES.
*
         ST    R14,BMWER14    SAVE RETURN ADDR
         LA    R0,MWE         A(MWE)
         LA    R1,MWEMAX      L'MWE AREA
         L     R14,=F'0'      ZEROES
         LA    R15,0          L'ZEROES
         MVCL  R0,R14         CLEAR MWE TO ZEROES
         MVC   MWEID,=C'MWE ' POST
         L     R1,TEXTLEN     L'TEXT
         LA    R1,4(R1)       PLUS HEADER
         STH   R1,MWECMDL     POST
         MVC   MWECMDO,=H'0'  CLEAR
         LA    R0,MWECMDT     A(MWE TEXT)
         L     R1,TEXTLEN     L'TEXT
         L     R14,ADDRTEXT   A(SOURCE TEXT)
         LR    R15,R1         L'TEXT
         MVCL  R0,R14         COPY TEXT
         L     R14,BMWER14    RESTORE RETURN ADDR
         BR    R14            RETURN
         TITLE 'HSMXEQ - XHSM - EXECUTE HSM SEND CMD'
XHSM     EQU   *
         ST    R14,XHSMR14    SAVE
*
*        TO CALL HSM:
*              ISSUE SVC109   (ROUTER SVC)
*              R15 = 24       ROUTES TO IGX00024 (HSM INTERFACE)
*
*        THE FOLLOWING INFORMATION WAS FOUND IN COMMENTS IN THE
*        MICROFICHE FOR IGX00024:
*
*        PARAMS:
*
*              R0 = FUNC CODE
*                   00 - RETURN QCT POINTER
*                   01 - START HSM
*                   02 - SHUTDOWN HSM
*                   03 - RECALL MIGRATED DATASET
*                   04 - UNUSED
*                   05 - RECOVER BACKUP VERSION OF A DATASET
*                   06 - MIGRATE A DATASET
*                   07 - MAKE BACKUP COPY OF A DATASET
*                   08 - RETURN HSM CDS RECORD
*                   09 - SEND TSO/OPR COMMAND TO HSM
*                   10 - POST REQUEST COMPLETE
*                   11 - UNUSED (WAS PURGE REQUESTS PREVIOUSLY)
*                   12 - DELETE MIGRATED DATASET
*
*              R1 - POINTER TO MWE (FUNC 2 - 12)
*                      "    "  ECB (FUNC 1)
*
*              R4 - A(TCB)
*
*              R5 - A(SVRB)
*
*              R7 - A(ASCB)
*
*        RETURN:
*
*              R1  - POINTER TO QCT (FUNC = 0,1 ONLY)
*              R15 - RETURN CODE
*                    00 - OK
*                    04 - HSM NOT ACTIVE
*                    08 - NO SPACE FOR MWE
*                    12 - POST REQUESTED FOR NONEXISTENT MWE
*                    16 - JES3 REQ AND HSM NOT ACCEPTING JES3 REQ'S
*                    20 - HSM ALREADY ACTIVE
*                    24 - USER NOT AUTHORIZED FOR REQUEST
*                    28 - INVALID OR ZERO MWE POINTER
*                    32 - INVALID BUFFER SIZE FOR CDS READ
*                    36 - INPUT MWE NOT WITHIN USER STORAGE
*                    40 - CSA RESERVED FOR MWES EXHAUSTED
*                   100 - INVALID FUNCTION CODE
*                   104 - FUNCTION CODE NOT RESERVED IN HSM AS (?)
*              MWERC MAY ALSO HAVE ADDITIONAL INFORMATION
*
*        SOME COMMANDS SUCH AS FIXCDS CAN RETURN SUBSTANTIAL AMOUNTS
*        OF DATA.  AN EXAMINATION OF ARCCPFC FICHE WHICH IMPLEMENTS
*        FIXCDS INDICATES THAT RESPONSES ARE HANDLED BY ARCWTO,
*        ARCWTU (AN ENTRY POINT IN ARCWTO), AND ARCPRINT (AN ENTRY
*        POINT IN ARCPROPN).  ARCPRINT IS USED FOR THE HSM LOG.
*        ARCWTO ISSUES TPUTS FOR TSO USERS AND WTO/WTOR FORCONSOLES.
*        NO SUPPORT IS PROVIDED IN THIS ROUTINE TO INTERCEPT OR
*        SERVICE THIS DATA.
*
*        ONLY FUNC 09 IS CURRENTLY SUPPORTED BY THIS SUBROUTINE
*        IN ADDITION, R5 IS NOT SET TO A(SVRB)
*
*        LOGIC FLOW IN THIS ROUTINE IS:
*
*            1)  SET R0 = FUNC FROM PARAM LIST
*            2)  SET R1 = A(MWE)
*            3)  SET R7 = A(ASCB)
*            4)  SET R4 = A(TCB)
*            5)  SET R15 = 24
*            6)  ISSUE SVC 109
*            7)  SAVE R15, REQUEST NO, AND MWERC
*            8)  RESTORE R4 AND R7
*            9)  RETURN
*
         ST    R4,XHSMR4      SAVE
         ST    R7,XHSMR7      SAVE
         L     R15,ADDRFUNC   A(FUNC CODE)
         L     R0,0(R15)      POST FUNC CODE
         LA    R1,MWE         A(MWE)
         LA    R4,16          A((A(CVT))
         L     R4,0(R4)       A(CVT)
         L     R4,0(R4)       A(TCBPTRS)
         L     R7,12(R4)      A(ASCB)
         L     R4,4(R4)       A(TCB)
         LA    R15,24         SELECT IGX00024 (HSM INTERFACE)
         SVC   109            ISSUE ROUTER SVC
         ST    R15,RC         POST RETURN CODE
         L     R4,XHSMR4      RESTORE
         L     R7,XHSMR7      RESTORE
         L     R14,XHSMR14    RESTORE
         BR    R14            RETURN
         TITLE 'XHSM - ERROR ROUTINES'
ERRBADFN EQU   *
         WTO   'HSMXEQ ERROR - FUNCTION CODE NOT 09',                  X
               ROUTCDE=2,DESC=7
         B     ABEND
ERRNOTXT EQU   *
         WTO   'HSMXEQ ERROR - INVALID TEXT LENGTH',                   X
               ROUTCDE=2,DESC=7
         B     ABEND
ERRPARAM EQU   *
         WTO   'HSMXEQ ERROR - BAD PARAMETER LIST',                    X
               ROUTCDE=2,DESC=7
         B     ABEND
ERRTEXTL EQU   *
         WTO   'HSMXEQ ERROR - TEXT LENGTH TOO LONG',                  X
               ROUTCDE=2,DESC=7
         B     ABEND
ABEND    EQU   *
         ABEND 1000,DUMP
         TITLE 'HSMXEQ - EXIT - RETURN TO CALLER'
EXIT     EQU   *
         L     R15,RC
         L     R13,4(R13)     A(OLD SAVE AREA)
         RETURN (14,12),RC=(15) RETURN
         LTORG
         TITLE 'HSMXEQ - DATA AREA'
SAVEAREA DS    9D             REG SAVE AREA
ADDRPARM DS    F              A(CALLING PARAMS (R1 ON ENTRY))
ADDRFUNC DS    F              A(FUNCTION CODE)       FROM CALLER
ADDRTEXT DS    F              A(TEXT)                FROM CALLER
ADDRHREQ DS    F              A(HSM REQUEST CODE)    FROM CALLER
ADDRHRC  DS    F              A(HSM RETURN CODE)     FROM CALLER
*
BMWER14  DS    F              R14 SAVE AREA FOR BMWE - BUILD MWE
XHSMR4   DS    F              R4  SAVE AREA FOR XHSM - EXECUTE HSM
XHSMR7   DS    F              R7  SAVE AREA FOR XHSM
XHSMR14  DS    F              R14 SAVE AREA FOR XHSM
*
TEXTLEN  DS    F              LENGTH OF TEXT FROM USER
RC       DS    F              RETURN CODE FOR THIS ROUTINE
         EJECT
MWE      DS    0D
         COPY  HSMMWE
         DS    0D
MWEMAX   EQU   *-MWE          LENGTH OF MWE
CMDMAX   EQU   *-MWECMDT      MAXIMUM COMMAND LENGTH
         END
