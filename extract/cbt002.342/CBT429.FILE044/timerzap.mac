*ZAP CARD ++PTF(HG00102)
*ZAP CARD   /* MODIFY 'SET CLOCK COMPARATOR' TO ESTABLISH  AN
*ZAP CARD      OPTIONAL CPU AFFINITY FOR TQES ASSOCIATED WITH
*ZAP CARD      DIE ROUTINES */.
*ZAP CARD ++VER(Z037) PRE(UZ80700 UZ08819).
*ZAP CARD ++ZAP(IEAVRTI0).
*ZAP CARD  NAME IEA0TI00
*ZAP CARD  EXPAND IEA0TI00(256)
IEA0TI00 CSECT
ZAPPED   EQU   IEA0TI00+X'AA4'
NOQUE    EQU   IEA0TI00+X'B78'
PATCH    EQU   IEA0TI00+X'EF0'
*ZAP START VER
         USING IEA0TI00+X'6A',R9
         USING PSA,R0
         USING TQE,R11
         ORG   ZAPPED
         BNZ   NOQUE
QUE      L     R12,PSAPCCAV
RET      EQU   *
         ORG   PATCH
         DC    XL12'00'
*ZAP START REP
         ORG   QUE
         B     PATCH
         ORG   PATCH
* THE 'SET CLOCK COMPARATOR' ENTRY POINT OF THE
* TIMER SUPERVISOR MODULE IS INVOKED BY TQE ENQUE AND DEQUE
* SERVICES WHEN A TQE IS ADDED OR REMOVED FROM THE REAL TIME
* QUEUE. THIS ZAP IS ENTERED WHEN THAT ROUTINE HAS DETERMINED
* THAT THE FIRST TQE IS NOT BEING TIMED OR THAT
* THE CURRENT CPU IS NOT TIMING ANYTHING.
* UNDER THIS APPROACH, A TQE CAN BE SIMULTANEOUSLY TIMED ON
* BOTH CPUS IN AN MP OR AP CONFIGURATION, BUT IT IS NOT
* ALWAYS DOUBLY TIMED. THE GUARANTEE THAT CPUS ARE
* EACH TIMING SOMETHING APPEARS TO BE A RELIABILITY FEATURE
* TO RECOVER TIMING SHOULD ONE CLOCK COMPARATOR SUDDENLY STOP
* GENERATING INTERRUPTS.
* THERE ARE TWO POSSIBLE
* DISPOSITIONS OF THE TQE:
*   1) A BRANCH TO 'RET' WILL RETURN TO THE NORMAL LOGIC AND
*      WILL CAUSE THE CLOCK COMPARATOR ON THIS CPU TO BE SET
*      TO THE TQEVAL VALUE IN THE TQE POINTED TO BY R11.
*   2) A BRANCH TO NOQUE WILL BYPASS PROCESSING FOR ANY TQE
*      CAUSING THE CLOCK COMPARATOR FOR THIS CPU TO REMAIN
*      UNCHANGED.
* THIS IDENTICAL CODE CAN BE INVOKED ON THE OTHER CPU IN AN MP
* OR AP ENVIRONMENT BY ISSUING A RPSGNL MACRO AS IS DONE BELOW.
* HENSE, OPTION 2) DOES NOT NECESSARILY LEAVE THE TQE 'HANGING'
* AS LONG AS THE OTHER CPU IS NOTIFIED TO PICK IT UP.
* THERE IS A LITTLE KLUGE IN THIS CODE FOR WHICH APOLOGIES ARE
* DUE. TO AVOID USING RESERVED BITS IN THE TQE, WE USE THE LOW
* ORDER 8 BITS OF THE TOD TIME STAMP IN 'TQEVAL'. SINCE THE
* STCK INSTRUCTION ON ALL 370 COMPUTERS LEAVE THE LOW ORDER 12
* BITS ZERO, THESE BITS ARE ALL USUALLY OFF. IF THEY ARE ALMOST
* ALL ON (SEE BELOW), THEN THIS IS TREATED AS AN INDICATION OF
* CPU AFFINITY. NOTE THAT NO JOB IS IMPACTED ADVERSLY BY
* INADVERTENTLY BEING CAUGHT BY THIS TRAP.
         L     R12,PSAPCCAV       PERFORM ZAPPED INSTRUCTION
         TM    TQEFLGS3,TQEDIE    IS THIS A DIE
         BZ    RET                NO, THEN NO CHANGE
         TM    TQETOD8,X'FE'      KLUGE: TEST UNUSUAL TIME STAMP
         BNO   RET                IF NOT FUNNY, THEN NO CHANGE
         SR    R14,R14            R14=0 PROVISIONALLY
         TM    TQETOD8,X'01'      CHECK CPU0/CPU1 AFFINITY
         BZ    *+8                IF CPU0 AFFINITY, R14 STAYS 0
         LA    R14,1              ELSE R14=1
         CH    R14,PSACPUPA       IS CURRENT CPU THE CHOSEN ONE?
         BE    RET                YES, RETURN
         TM    TQEFLGS2,TQECOMP   IS IT BEING TIMED ON THAT CPU
         BZ    SIGNAL             NO, GOT TO WAKE IT UP
*                                 GET HERE CAUSE THE COMPARATOR
*                                 ON THIS CPU IS IDLE, BUT THE
*                                 TOP TQE HAS AFFINITY TO THE
*                                 OTHER FELLOW. CANT JUST QUIT
*                                 CAUSE RECOVERY DEPENDS ON
*                                 US TIMING SOMETHING, SO GO TO
*                                 THE NEXT TQE IN THE QUEUE AND
*                                 PROCESS IT.
         L     R11,TQEFLNK        TRY NEXT TQE
         B     PATCH              GO BACK AND TEST
SIGNAL   L     R1,CVTPTR
         L     R1,CVTPCCAT-CVT(R1)
         USING PCCAVT,R1
         SLL   R14,2              SHIFT CPU ID
         L     R1,PCCAT00P(R14)   FIND TARGET PCCA
         USING PCCA,R1
         LTR   R1,R1              DOES THE CPU EXIST?
         BZ    RET                NO, THEN CAN'T USE IT
         TM    PCCACCE,PCCANUCC   IS THE CLOCK GOOD ON THAT CPU?
         BO    RET                NO, THEN TIME ON THIS ONE
         DROP  R1
         RPSGNL RQCHECK,CPU=(1)   SIGNAL OTHER CPU TO QUE TCB
         LTR   R15,R15            SIGNAL SUCCESSFUL?
         BNZ   RET                NO, BETTER QUE IT ON THIS CPU
         B     NOQUE
*ZAP STOP
         PRINT NOGEN
         CVT   DSECT=YES
         IHAPSA
         IHATQE
         IHAPCCAT
         IHAPCCA
         EQUREGS
TQETOD8  EQU   TQEVAL+7
         END
