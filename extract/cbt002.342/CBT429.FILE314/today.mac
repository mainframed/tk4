         PRINT   GEN
*******************************************
* TODAY IS A SUBROUTINE WHICH RETURNS THE *
* DAY OF THE WEEK. RETURNED VALUE IS NINE *
* CHARACTERS. INPUT FROM TIME MACRO IS IN *
* CCYYDDDF PACKED DECIMAL FORMAT IN R1.   *
* EXAMPLE: CALL TODAY,(WEEKDAY),VL        *
* WHERE WEEKDAY IS C9                     *
*                         D.LEES 1997/091 *
*******************************************
* BASE YEAR (IBM) IS JAN 1,1900           *
*******************************************
TODAY    CSECT
         STM     R14,R12,12(R13)
         BALR    R12,0
         USING   *,R12
         LA      R2,SAVEAREA
         ST      R2,8(R13)
         ST      R13,SAVEAREA+4
         LR      R13,R2
         LR      R11,R1                    SAVE FOR RETURN VALUE
         L       R1,0(R11)
         L       R1,0(R1)
*--------------------------------------------------------------------
STEP1    EQU     * ----------------------- GET MVS TIME
*--------------------------------------------------------------------
         TIME    BIN
         STH     R1,DDD                    JULIAN 0000DDDF
         ST      R1,FWORD     DIAGNOSTIC
         SP      DDD,=P'1'                 MAKE 0 RELATIVE
         SRL     R1,12                     JULIAN 000CCYYF
         ST      R1,YYYY
         OI      YYYY+3,X'0F'              SIGN IT +
         TM      YYYY+2,X'10'              MASK=00010000
         BO      ADD20                     TIME RETURNS CENTURY FLAG
         AP      YYYY,BASEYR               0=19TH 1=20TH
         B       STEP2
ADD20    EQU     *
         NI      YYYY+2,X'EF'              REMOVE FLAG BIT
         AP      YYYY,=P'2000'
*--------------------------------------------------------------------
STEP2    EQU     * ----------------------- BASE CALCULATIONS
*--------------------------------------------------------------------
*
*   REM((#-OF-YEARS + LEAP-YEARS + JULIAN-DAY) / 7 ) = 0->6 {MON-FRI}
*
         ZAP     WORKP4,YYYY
         SP      WORKP4,BASEYR
         ZAP     #YEARS,WORKP4             # OF YEARS SINCE BASE
*
         ZAP     WORKP4,#YEARS
         DP      WORKP4,=PL2'4'
         ZAP     #LEAPS,WORKP4(2)          # OF "LEAP" YEARS
*
         ZAP     WORKP4,#YEARS
         DP      WORKP4,=PL2'100'
         ZAP     #CENTS,WORKP4(2)          # OF NN00 YEARS SINCE BASE
         CP      #CENTS,=P'0'
         BE      SIMPLE
*--------------------------------------------------------------------
STEP3    EQU     * ----------------------- FIND ACTUAL # OF DAYS
*--------------------------------------------------------------------
         ZAP     #COUNT,#CENTS
LEAPER   EQU     *
         ZAP     WORKP4,#CTEST
         CP      WORKP4,=P'3600'            RULE: 3600 NOT A LEAP YEAR
         BE      NOLEAP
         DP      WORKP4,=PL2'400'
         CP      WORKP4+2(2),=P'0'
         BE      NEXTLEAP                  RULE: REM(YR00/400)=0 > LEAP
         B       NOLEAP                    RULE: YR00'S ARE NOT LEAPS
NOLEAP   EQU     *
         AP      #NOLEAPS,=P'1'            REDUCE #LEAPS BY #NOLEAPS
NEXTLEAP EQU     *
         AP      #CTEST,=P'100'            TEST EACH '00' YEAR FOR /400
         SP      #COUNT,=P'1'
         CP      #COUNT,=P'0'
         BNZ     LEAPER
SIMPLE   EQU     *
         SP      #LEAPS,#NOLEAPS           TRUE # OF LEAP DAYS
         ZAP     WORKP4,#YEARS
         MP      WORKP4,=P'365'            YEARS -> DAYS
         AP      WORKP4,#LEAPS             ADD # OF LEAPDAYS
         AP      WORKP4,DDD                # OF DAYS THIS YEAR
         DP      WORKP4,=PL1'7'
         XC      WORKP4(3),WORKP4          REMAINDER (0->6)
*--------------------------------------------------------------------
STEP4    EQU     * ----------------------  RETURN THE DAY NAME
*--------------------------------------------------------------------
         MP      WORKP4,=P'9'              TABLE OFFSET
         ZAP     DWORD,WORKP4              REMAINDER = INDEX TO DAY
         CVB     R10,DWORD
         LA      R10,DAYS(R10)
         L       R11,0(R11)                RETURN VALUE
         MVC     0(9,R11),0(R10)           DAYNAME!
*        MVC     WORKSTOR(RESLEN),RESETS   SERIALLY REUSABLE CODE
*+++++++++++++++++++++++++++++++++*
*+++++++++++++++++++++++++++++++++*
TERM     EQU     *
         L       R13,SAVEAREA+4
         LM      R14,R12,12(R13)
         XR      R15,R15
         BR      R14
*+++++++++++++++++++++++++++++++++*
SAVEAREA DC      18F'0'
DWORD    DC      D'0'
FWORD    DC      F'0'
         DS      0F
WORKSTOR EQU     *
YYYY     DC      PL4'0'
BASEYR   DC      PL4'1900'          REPRESENTS 1/1/1900
#CTEST   DC      PL4'1900'          REPRESENTS 1/1/1900
#COUNT   DC      PL4'0'
WORKP4   DC      PL4'0'
#YEARS   DC      PL4'0'
#LEAPS   DC      PL4'0'
#NOLEAPS DC      PL4'0'
#CENTS   DC      PL4'0'
         DS      0H
DDD      DC      PL2'0'
RESETS   EQU     *
RES1     DC      PL4'0'
RES2     DC      PL4'1900'          REPRESENTS 1/1/1900
RES3     DC      PL4'1900'          REPRESENTS 1/1/1900
RES4     DC      PL4'0'
RES5     DC      PL4'0'
RES6     DC      PL4'0'
RES7     DC      PL4'0'
RES8     DC      PL4'0'
RES9     DC      PL4'0'
         DS      0H
RES10    DC      PL2'0'
RESLEN   EQU     *-RESETS
DAYS     EQU     *
         DC      CL9'MONDAY'        1/1/1900 WAS A MONDAY
         DC      CL9'TUESDAY'
         DC      CL9'WEDNESDAY'
         DC      CL9'THURSDAY'
         DC      CL9'FRIDAY'
         DC      CL9'SATURDAY'
         DC      CL9'SUNDAY'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
