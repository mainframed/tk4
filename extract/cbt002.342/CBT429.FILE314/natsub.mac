* VERSION 2
* THIS MACRO SIMPLY CONVERTS A BINARY NUMBER IN REG (&REG)
* TO AN UNPACKED DECIMAL FIELD (&DEST) FOR PRINTOUT.
* DEFINE: &DWORD  DS  D  - FOR USE BY ALL MACRO EXECUTIONS.
         MACRO
&NAME    CONVERT   &REG,&DEST,&DWORD
&NAME    CVD   &REG,&DWORD
         UNPK  &DEST,&DWORD
         OI    &DEST+L'&DEST-1,X'F0'
         MEND
         MACRO
         REGS
         PRINT OFF
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         PRINT ON
         MEND
*******************************************
* SUBMIT IS A GENERAL PURPOSE SUBROUTINE  *
* WHICH WILL DYNAMICALLY ALLOCATE THE JES *
* INTERNAL READER AND SUBMIT JCL ETC. FROM*
* A NATURAL PROGRAM. THE LAST CALL TO THIS*
* SUBROUTINE MUST BE A '/*EOF'.           *
* CALL 'TSOSUB' P1 P2                     *
*          P1 = CL80 TEXT TO SUBMIT       *
*          P2 = CL6 (CL2=RC,CL4=REASON)   *
* WRITTEN BY D.LEES 8/88                  *
* PROGRAM IS REUSEABLE; NATURAL LOADS A   *
* CALLED PROG AT 1ST CALL AND DELETES AT  *
* END OF NATURAL PGM (OR SO IT APPEARS).  *
*                                         *
* ERRORS: R15 CONTAINS RETURN CODE        *
*   RC    REASON   CAUSE                  *
*   __    ______   ____________________   *
*   NN    NNNN     ACTUAL R15 & S99ERROR  *
*   66    0000     WRITE TO INTRDR ERROR  *
*   77    0000     INTRDR OPEN ERROR      *
*******************************************
SUBMIT   CSECT
         PRINT NOGEN
         REGS
INIT     STM   R14,R12,12(R13)
         BALR  R12,0
         USING *,R12
         LR    R11,R1                      PARMLIST
         LA    R1,SAVEAREA
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         LM    R10,R11,0(R11)               R10->P1 R11->P2
         MVC   RETCODE,=C'000000'           RC=0 REASON=0
         MVI   CC,X'00'
         LA    R9,RBBLK
         USING S99RB,R9
         TM    SW@ALLOC,X'80'
         BO    WRITE                       ALREADY ALLOCATED
         OI    RBPTR,X'80'
ALLOC    EQU   *
         OI    ALLOCLST+12,X'80'
         XC    RBBLK,RBBLK                 CLEAR RB
         MVI   S99RBLN,RBLEN               SET LENGTH
         MVI   S99VERB,S99VRBAL            SET FUNCTION
         MVC   S99TXTPP,=A(ALLOCLST)       SET PTR TO LIST
         LA    R1,RBPTR                    LOAD  RB ADDR
         SVC   99                          REQ ALLOC SERVICES
         LTR   R15,R15                     TEST SVC 99 RC
         BNZ   SVC99ERR
OPENRDR  MVC   INTRDR+DCBDDNAM-IHADCB(8),DDNAME
         OPEN  (INTRDR,(OUTPUT))
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   OPENERR
         OI    SW@ALLOC,X'80'              ALLOC & OPEN COMPLETED
WRITE    EQU   *
         PUT   INTRDR,0(R10)
*        LA    R15,PUTERR
*        ICM   R15,8,=X'02'
*        LA    R0,=X'41000000'
*        LA    R1,INTRDR
*        ICM   R1,8,=X'40'
*        BALR  R14,R15
         CLC   0(5,R10),=C'/*EOF'
         BNE   TERM
         CLOSE (INTRDR)                    FREE=CLOSE
         NI    SW@ALLOC,X'7F'              ALLOW ALLOC AGAIN
TERM     EQU   *
         MVC   0(6,R11),RETCODE            SET RETURN VALUES
         L     R13,SAVEAREA+4
         L     R14,12(R13)
         XR    R15,R15
         IC    R15,CC
         RETURN (0,12)
         BR    R14
         EJECT
SVC99ERR EQU   *
         STC   R15,CC
         CONVERT R15,RESPONSE,DWORD
         LH    R15,S99ERROR
         CONVERT R15,REASON,DWORD
         B     TERM
OPENERR  EQU   *
         MVC   RETCODE,=C'770000'
         MVI   CC,X'77'
FREE     EQU   *
         OI    FREELST,X'80'
         XC    RBBLK,RBBLK                 CLEAR RB
         MVI   S99RBLN,RBLEN               SET LENGTH
         MVI   S99VERB,S99VRBUN            SET FUNCTION
         MVC   S99TXTPP,=A(FREELST)        SET PTR TO LIST
         MVC   DDNAMX,DDNAME
         LA    R1,RBPTR                    LOAD  RB ADDR
         SVC   99                          REQ ALLOC SERVICES
         LTR   R15,R15                     TEST SVC 99 RC
         BNZ   SVC99ERR
         B     TERM
PUTERR   EQU   *                           PRESERVE R13,R14 !
         SYNADAF ACSMETH=QSAM              GET SAVE
         MVI   CC,X'66'
         MVC   RETCODE,=C'660000'
         NI    SW@ALLOC,X'7F'              ALLOW ALLOC AGAIN
         CLOSE (INTRDR)                    FREE=CLOSE
         SYNADRLS                          RELEASE SAVE
         RETURN (2,12)                    EROPT=SKP (NO ABEND OF
*                                          NATURAL FOR SOFT ERRORS)
*
*        DATA AREAS AND DSECTS
*
         SPACE
SAVEAREA DC    18F'0'
DWORD    DS    D
         SPACE 2
RETCODE  DS    0CL6
RESPONSE DS    CL2
REASON   DS    CL4
CC       DS    C
SW@ALLOC DC    X'00'
         SPACE 2
*++++++++++++++++++++++++++++++++++++++*
*   DYNAMIC ALLOCATION STRUCTURES      *
*++++++++++++++++++++++++++++++++++++++*
         DS    0F
RBPTR    DC    A(RBBLK)                ADDR OF RB HEADER
         SPACE
         DS    0F
RBLEN    EQU   (S99RBEND-S99RB)
RBBLK    DC    BL(RBLEN)'0'              RB STORAGE
         SPACE 2
ALLOCLST DC    A(SYSOU)                TEXT POINTER LIST
         DC    A(SPGNM)
         DC    A(CLOSE)
         DC    A(RTDDN)
         SPACE 2
FREELST  DC    A(DEALOC)               TEXT POINTER LIST
         SPACE 2
         DS    0F
SYSOU    DC    AL2(DALSYSOU)            REQUEST SYSOUT ALLOCATION
         DC    H'1',H'1'
         DC    CL1'A'                   DEFAULT CLASS OF A
         SPACE
         DS    0F
RTDDN    DC    AL2(DALRTDDN)            REQUEST GENERATED DDN
         DC    H'1'
         DC    H'8'
DDNAME   DC    CL8'********'            RETURNED DDN FLD
         SPACE
         DS    0F
SPGNM    DC    AL2(DALSPGNM)            WRITER
         DC    H'1'
         DC    H'8'
         DC    CL8'INTRDR  '            WRITER NAME
         SPACE
         DS    0F
CLOSE    DC    AL2(DALCLOSE)            FREE=CLOSE
         DC    H'0'
         SPACE
DEALOC   DC    AL2(DUNDDNAM)            UN-ALLOCATE
         DC    H'1'
         DC    H'8'
DDNAMX   DC    CL8'********'            WRITER NAME
         EJECT
INTRDR   DCB   MACRF=(PM),DSORG=PS,BLKSIZE=80,LRECL=80,                X
               RECFM=FB,SYNAD=PUTERR,EROPT=SKP
         IEFZB4D0
         EJECT
         IEFZB4D2
         DCBD  DSORG=PS,DEVD=DA
         END
