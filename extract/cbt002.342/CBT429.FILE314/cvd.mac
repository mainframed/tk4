         TITLE '80-BYTE HEX VERTICAL DUMP ROUTINE'
         SPACE
***********************************************************************
*                                                                     *
* THIS ROUTINE WILL DUMP 80 BYTES OF DATA IN HEX VERTICAL FORMAT      *
* ON DDNAME LISTDD. THE ADDRESS OF THE AREA TO BE DUMPED IS PASSED    *
* VIA A STANDARD CALL STATEMENT.                                      *
*        CALL CVD,(DATA)                                              *
*                                                                     *
* JCL STREAM:                                                         *
*                                                                     *
*    //DUMPOUT  DD SYSOUT=A         (IN EXECUTION RUN STREAM JCL)     *
*                                                                     *
***********************************************************************
         EJECT
CVD      START 0
         USING *,15
         STM   14,12,12(13)
         LR    10,13
         CNOP  0,4
         BAL   13,GO
         DROP  15
         USING *,13
         DC    18F'0'
GO       DS   0H
         ST    13,8(10)
         ST    10,4(13)
EXIT     DS   0H
         L     3,0(1)
         TM    DCB+48,16
         BO    GO2
         OPEN  (DCB,(OUTPUT))
         TM    DCB+48,16
         BO    GO2
         MVC   EXIT(4),EXITBR
EXITBR   DS   0H
         B     FORCERET
GO2      DS   0H
         MVC   PLINE,0(3)
         MVI   POUTCC,C'0'
         PUT   DCB,POUT
         MVI   POUTCC,C' '
         TR    PLINE,HEXUPPER
         PUT   DCB,POUT
         MVC   PLINE,0(3)
         TR    PLINE,HEXLOWER
         PUT   DCB,POUT
         TRUNC DCB
FORCERET DS   0H
         L     13,4(13)
         LM    14,12,12(13)
         MVI   12(13),X'FF'
         XR    15,15
         BR    14
         ENTRY DUMPDCB
DUMPDCB  DS    0H
DCB      DCB   DSORG=PS,DDNAME=DUMPOUT,MACRF=PM,LRECL=81,RECFM=FBA,    X
               BLKSIZE=810
POUT     DS   0F
POUTCC   DC    C'0'
PLINE    DC    CL80' '
HEXUPPER DC    16CL1'0'
         DC    16CL1'1'
         DC    16CL1'2'
         DC    16CL1'3'
         DC    16CL1'4'
         DC    16CL1'5'
         DC    16CL1'6'
         DC    16CL1'7'
         DC    16CL1'8'
         DC    16CL1'9'
         DC    16CL1'A'
         DC    16CL1'B'
         DC    16CL1'C'
         DC    16CL1'D'
         DC    16CL1'E'
         DC    16CL1'F'
HEXLOWER DC    16CL16'0123456789ABCDEF'
         END
