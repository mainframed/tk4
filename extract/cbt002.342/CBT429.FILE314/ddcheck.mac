*******************************************************
* DDCHECK IS A CALLABLE ASSEMBLER SUBROUTINE WHICH    *
* WILL TEST A PASSED DDNAME TO DETERMINE IF IT IS A   *
* REAL DATASET, A DUMMY, OR MISSING.    D.LEES 1998   *
* EXAMPLE: CALL DDCHECK,(DDNAME)                      *
*  - WHERE DDNAME IS CL8 CONTAINING THE NAME TO CHECK *
*  - RETURN CODE 0 FOR A REAL  DATASET                *
*  - RETURN CODE 4 FOR A DUMMY DATASET                *
*  - RETURN CODE 8 FOR A MISSING DDNAME               *
*  FOR PL/1: DECLARE DDCHECK ENTRY(CHARACTER(8)) -    *
*            OPTIONS(ASM RETCODE) ;                   *
*******************************************************
         EJECT
DDCHECK  CSECT
DDCHECK  AMODE      ANY
DDCHECK  RMODE      ANY
         PRINT      ON,GEN
         SAVE       (14,12)
         BALR       R12,0
         USING      *,R12
         ST         R13,SAVE+4
         LA         R15,SAVE
         ST         R15,8(R13)
         LR         R13,R15
GETPARM  EQU        *
         LR         R9,R1
         L          R9,0(R9)
* COMMENTED OUT CODE FOR JCL PARM='DDNAME'
*        LH         R1,0(R9)
*        LTR        R1,R1                   IF 0 THEN
*        BZ         PROCESS                 USE DEFAULT OF MASTERO
*        CH         R1,EIGHT
*        BH         ERROR1                  TOO LONG!!
OVERRIDE EQU        *
*        MVC        CHECKDD,BLANKS
*        LA         R9,2(R9)                OVERRIDE DEFAULT
*        BCTR       R1,R0
*        EX         R1,EXEC1
*        B          PROCESS
*XEC1    MVC        CHECKDD(0),0(R9)        SET DDNAME
EXEC1    MVC        CHECKDD(8),0(R9)        SET DDNAME
PROCESS  EQU    *
         EXTRACT    TIOTADDR,'S',FIELDS=(TIOT)
         L          R11,TIOTADDR
         USING      TIOTD,R11
* LOOP TO FIND DDNAME, INCREMENTING R11, REMEMBER TO STOP.
* IF NOT FOUND . . . .
         LA         R11,TIOENTRY
         DROP       R11
         USING      TIOENTRY,R11
         LA         R3,1635                 ( MAX DD'S CIRCA 1977 )
DDLOOKUP EQU        *
         CLC        TIOEDDNM,CHECKDD
         BE         SETJFCB
         XR         R1,R1
         ICM        R1,1,TIOELNGH
         BZ         NODDNAME                END OF TIOT (LEN = 0)
         LA         R11,0(R1,R11)
         BCT        R3,DDLOOKUP
         B          ERROR1
SETJFCB  EQU        *
         XR         R1,R1
         ICM        R1,7,TIOEJFCB           GASP! 3 BYTE ADDR.
         DROP       R11
         LA         R11,16(,R1)             +16 TO ACTUAL JFCB
         USING      JFCBD,R11
* EXAMINE DSNAME, IS IT NULLFILE? IF IT IS . . . . .
         CLC        JFCBDSNM(8),NULLF
         BNE        REALDD
         B          NULLFILE
NODDNAME EQU        *
         MVC        MSG1DDN,CHECKDD
         MVC        MSG1DSN(9),NODDMSG
         MVC        MSG1RC,RC08
         LA         R10,08
         B          RETURN
NULLFILE EQU        *
         MVC        MSG1DDN,CHECKDD
         MVC        MSG1DSN,JFCBDSNM
         MVC        MSG1RC,RC04
         LA         R10,04
         B          RETURN
REALDD   EQU        *
         MVC        MSG1DDN,CHECKDD
         MVC        MSG1DSN,JFCBDSNM
         MVC        MSG1RC,RC00
         LA         R10,00
*        B          RETURN
RETURN   EQU        *
         WTO TEXT=MSG1,                                                X
               ROUTCDE=11,DESC=6
REUS     EQU        *
         MVC        CHECKDD,MASTERO
         MVC        MSG1DDN,BLANKS
         MVC        MSG1DSN,BLANKS
         MVC        MSG1RC,BLANKS
         L          R13,SAVE+4
         LR         R15,R10
         RETURN     (14,12),RC=(15)
         SPACE      3
ERROR1   EQU        *
         ABEND      911                     UNRECOVERABLE ERROR
         EJECT
SAVE     DC         9D'0'
TIOTADDR DC         F'0'
MSG1     EQU        *
MSG1L    DC         AL2(MSG1LEN)
MSG1ID   DC         CL8'DDCHECK1'
         DC         C' '
MSG1DDN  DC         CL8' '
         DC         C' '
MSG1DSN  DC         CL44' '
         DC         C' '
         DC         C'RC='
MSG1RC   DC         CL2' '
MSG1LEN  EQU        *-MSG1ID
         SPACE      3
CHECKDD  DC         CL8'MASTERO'
MASTERO  DC         CL8'MASTERO'            DEFAULT TO CHECK
NULLF    DC         CL8'NULLFILE'
EIGHT    DC         H'8'
RC00     DC         C'00'
RC04     DC         C'04'
RC08     DC         C'08'
NODDMSG  DC         C'NO DDNAME'
BLANKS   DC         CL44' '
         EJECT
*        PRINT      ON,GEN
         PRINT      OFF,NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R00      EQU   0
R01      EQU   1
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SPACE    3
JFCBD    DSECT
         IEFJFCBN LIST=NO
TIOTD    DSECT
         IEFTIOT1
         END
