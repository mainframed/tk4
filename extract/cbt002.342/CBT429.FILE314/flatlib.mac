FLATLIB  CSECT
         REGS
         SAVE  (14,12)
         LR    R12,R15
         USING FLATLIB,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         OPEN (DIRECT,(INPUT))
         OPEN (PDS,(INPUT))
         LA   R9,PDS           POINT TO DCB
         USING IHADCB,R9       GET ADDRESSABILITY
         OPEN (PRINT,(OUTPUT))
DIRLOOP  EQU   *
         GET   DIRECT,LIBLINE
         SR    R7,R7
         LA    R7,LIBLINE
         AH    R7,LIBLINE
         LA    R4,LIBLINE+2
DIRSCAN  EQU   *
         CLC   0(8,R4),=8X'FF'
         BE    EOFPROC
         CR    R4,R7
         BNL   DIRLOOP
SRCHMEM  EQU   *
         MVC  MEMBER,0(R4)
         FIND PDS,MEMBER,D     POINT TO MEMBER
READLOOP EQU   *
         XC    DECB,DECB
         READ  DECB,SF,PDS,PDSBLOCK       READ BLOCK FROM MEMBER
         CHECK DECB
         SR   R8,R8            CLEAR REG 8
         LH   R8,DCBBLKSI      GET DCB BLOCK SIZE
         L     R3,DECB+16                GET IOB ADDR
         SH    R8,14(R3)                  R8 NOW ACTUAL BLOCKSIZE
         SR    R3,R3                      CLEAR REG 7
         LA    R3,PDSBLOCK
         LA    R6,PDSBLOCK
         AR    R3,R8                      POINT TO END OF BLOCK
BLKLOOP  EQU   *
         CR    R6,R3                      LAST RECORD IN BLOCK
         BE    READLOOP                   GET NEXT BLOCK
         MVC   MEMLINE,0(R6)              MOVE THIS RECORD
         LA    R5,MEMLINE
YES      EQU   *
         MVC   OUTLINE,SPACES
         MVC   OUTLINE(8),MEMBER
         MVC   OUTLINE+8(80),MEMLINE
         PUT   PRINT,OUTLINE                PRINT THIS RECORD
NEXTREC  LA    R6,80(R6)                  POINT TO NEXT RECORD
         B     BLKLOOP
EOFMEM   EQU   *
         SR    R5,R5
         IC    R5,11(R4)
         SLL   R5,1
         LA    R5,12(R5)
         AR    R4,R5
         B     DIRSCAN
EOFPROC  EQU   *
         CLOSE PDS
         CLOSE PRINT
         CLOSE DIRECT
         L     R13,SAVE+4
         RETURN (14,12),RC=0
SAVE     DS  9D
MEMBER   DC  CL8'MEMBERXX'
MEMLINE  DS  CL80
OUTLINE  DS  CL88
PRINT DCB DDNAME=SYSPRINT,LRECL=88,BLKSIZE=0,RECFM=FB,MACRF=(PM),      C
               DSORG=PS
PDS     DCB DDNAME=LIBRARY,EODAD=EOFMEM,LRECL=080,MACRF=(R),DSORG=PO
DIRECT DCB DDNAME=LIBRARY,BLKSIZE=256,MACRF=(GM),DSORG=PS,             C
               EODAD=EOFPROC,RECFM=U
LIBLINE DS CL256
SPACES  DS CL132' '
SEARCH  DS CL80
COUNT   DC PL5'0'
LENGTH  DS F
        LTORG
PDSBLOCK DS  CL32760
        DCBD    DSORG=(PO),DEVD=(DA)
        END
