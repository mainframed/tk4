         TITLE '3270 IO TEST DEMO  VER=1.0.0'
*********************************************************************
*
*  A3270 - ASSEMBLER PROGRAM TO DEMO MAP3270
*          NOTE - THE PROGRAM IS INTENDED TO BE USED FOR DEMO
*          PURPOSES AND IS NOT PRODUCTION QUALITY.
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
*
*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
*  AND FOR NON-COMERCIAL PURPOSES.
*
*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
*
*********************************************************************
A3270    ESTART TYPE=START,DESC='3270 IO TEST DEMO',VER='1.0.0',       *
               REGS=YES,BASE=R12
*
         L     R1,T3270ADR
         LTR   R1,R1
         BNZ   SKIPLD
         LOAD  EPLOC=T3270IO
         ST    R0,T3270ADR
SKIPLD   EQU   *
*
*  INITIALIZE THE IOCB AND SET IT UP FOR AN STRT
*
         MVI   IOCB,X'00'
         MVC   IOCB+1(IOCBLN-1),IOCB
         MVC   IOCBFUNC,=C'STRT'
         TCALL T3270ADR,IOCB,TPARM
*
*  SET UP IOCB FOR AN IO
*
IOLOOP   EQU   *
         MVC   IOCBFUNC,=C'IO  '
         MVI   R12C46C,C'X'
         MVC   R15C40D,=C'ASSEMBLER CALLING'
         TCALL T3270ADR,IOCB,TPARM
         CLI   TAID,AIDPFK03            PF3 PRESSED?
         BE    EXIT
         UNPK  WORK3,TAID(2)
         TR    WORK3(2),TRTAB-C'0'
         MVC   R12C46D,WORK3
         MVI   R12C46C,C'X'
         MVC   R15C40D,=C'ASSEMBLER CALLING'
         L     R2,IOCBADR
         MVC   R21C01D(79),0(R2)
         MVC   R22C01D(79),0(R2)
         MVC   R23C01D(79),0(R2)
         TR    R21C01D(79),CHRTBL
         TR    R22C01D(79),HITBL
         TR    R23C01D(79),LOTBL
         B     IOLOOP
*
*  SET UP IOCB FOR AN END
*
EXIT     EQU   *
         MVC   IOCBFUNC,=C'END '
         TCALL T3270ADR,IOCB,TPARM
LEAVE    ERETURN RC=0
CNVWRK   DS    F
DWORD    DS    D
WORK3    DC    XL3'00'
         DS    CL240
TRTAB    DC    C'0123456789ABCDEF'
*
MEMDISP  DS    0H
CHRTBL   DC    8CL16'................'
*                     A B C D E F G H I
         DC    XL16'4B8182838485868788894B4B4B4B4B4B' X'8X'
*                     J K L M N O P Q R
         DC    XL16'4B9192939495969798994B4B4B4B4B4B' X'9X'
*                       S T U V W X Y Z
         DC    XL16'4B4BA2A3A4A5A6A7A8A94B4B4B4B4B4B' X'AX'
         DC    CL16'................'   X'BX'
         DC    CL16'.ABCDEFGHI......'   X'CX'
         DC    CL16'.JKLMNOPQR......'   X'DX'
         DC    CL16'..STUVWXYZ......'   X'EX'
         DC    CL16'0123456789......'   X'FX'
HITBL    DC    16C'0'
         DC    16C'1'
         DC    16C'2'
         DC    16C'3'
         DC    16C'4'
         DC    16C'5'
         DC    16C'6'
         DC    16C'7'
         DC    16C'8'
         DC    16C'9'
         DC    16C'A'
         DC    16C'B'
         DC    16C'C'
         DC    16C'D'
         DC    16C'E'
         DC    16C'F'
LOTBL    DC    16CL16'0123456789ABCDEF'
T3270IO  DC    CL8'T3270IO'
T3270ADR DC    F'0'
IOCB     DS    0F
IOCBFUNC DS    CL4                     FUNCTION CODE (STRT/IO/END)
IOCBRC   DS    AL2                     RETURN CODE
IOCBLEN  DS    AL2                     IO BUFFER LENGTH
IOCBADR  DS    A                       IO BUFFER ADDRESS
         DS    XL20                    RESERVED
IOCBLN   EQU   *-IOCB
*
         COPY  DBG3270
*
         COPY  AIDCODES
         END   A3270
