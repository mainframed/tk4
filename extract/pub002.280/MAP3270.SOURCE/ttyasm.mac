         TITLE '3270 TTY TEST DRIVER - VER 1.0.0'
TTYDEMO  ESTART TYPE=START,DESC='3270 TTY TEST DRIVER',VER='1.0.0',    *
               REGS=YES,BASE=R11
*
*   THIS IS A DEMO FOR THE TTYRTN.  TTYRTN SUPPORTS 2 PARM FORMATS,
*   BOTH OF WHICH CAN BE INTERCHANGED.
*
*
         LOAD  EPLOC=TTYNAME
         ST    R0,TTYRTN
*
         MVC   FUNC,=C'STRT'
         LA    R1,R1PARM
         L     R15,TTYRTN
         BALR  R14,R15
*
******** LA    R10,25     DEBUG CODE - BREAK OUT AFTER 25 ITTERATIONS
LOOPIO   EQU   *
         MVC   LINEOUT,=CL79'>-------PUT'
         MVC   FUNC,=C'PUT '
         LA    R1,R1PARM1
         L     R15,TTYRTN
         BALR  R14,R15
         MVI   LINEOUT,C'>'
         MVC   LINEOUT+1(78),LINEIN
         MVC   LINEIN,=CL78'>'
         MVC   FUNC,=C'IO  '
         LA    R1,R1PARM
         L     R15,TTYRTN
         BALR  R14,R15
         CLI   TTYAID,C'3'
         BE    EXIT
         MVI   ERRMSG,C' '
         MVC   ERRMSG+1(L'ERRMSG-1),ERRMSG
         CLC   LINEIN(3),=C'CLS'
         BNE   LOOPIO
         MVC   FUNC,=C'CLR '
         LA    R1,R1PARM1
         L     R15,TTYRTN
         BALR  R14,R15
******** BCT   R10,LOOPIO        DEBUG CODE - SEE ABOVE
         B     LOOPIO
*
EXIT     EQU   *
         MVC   FUNC,=C'END '
         LA    R1,R1PARM
         L     R15,TTYRTN
         BALR  R14,R15
         ERETURN RC=0
*
TTYNAME  DC    CL8'TTYRTN'
TTYRTN   DS    A
R1PARM   DC    A(FUNC)
         DC    A(LINEOUT)
         DC    A(LINEIN)
         DC    A(ERRMSG)
         DC    AL1(X'80')
         DC    AL3(TTYAID)
R1PARM1  DC    AL1(X'80')
         DC    AL3(FUNC)
FUNC     DS    CL4
LINEOUT  DC    CL79' '
LINEIN   DC    CL78' '
ERRMSG   DC    CL79'THIS IS THE PROMPT LINE'
TTYAID   DS    CL1
         LTORG
         DROP  R11
         END   TTYDEMO
