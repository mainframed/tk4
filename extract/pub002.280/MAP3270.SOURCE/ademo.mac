*********************************************************************
*
*  ADEMO - ASSEMBLER PROGRAM TO DEMO MAP3270
*          NOTE - THE PROGRAM IS INTENDED TO BE USED FOR DEMO
*          PURPOSES AND IS NOT PRODUCTION QUALITY.
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
*
*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
*  AND FOR NON-COMERCIAL PURPOSES.
*
*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
*
*********************************************************************
         MACRO
&NAME    CLEAR &FLD,&CHR
         MVI   &FLD.D,&CHR
.*       AIF   (L'&FLD.D EQ 1).ONE
         MVC   &FLD.D+1(L'&FLD.D-1),&FLD.D
.ONE     MEND
         MACRO
&NAME    MVCTR &INP
         TR    &INP,CHRTBL
         SR    R14,R14
         SR    R15,R15
         LA    R14,&INP
         LA    R15,&INP+L'&INP
LUP&SYSNDX BCTR  R15,0
         CR    R14,R15
         BE    DUN&SYSNDX
         CLI   0(R15),C' '
         BE    LUP&SYSNDX
DUN&SYSNDX CLI   0(R15),C' '
         BE    NUL&SYSNDX
         SR    R15,R14
         LA    R15,1(,R15)
         EX    R15,MVCTREX
         LA    R1,1(R15,R1)
NUL&SYSNDX EQU *
         MEND
ADEMO    ESTART TYPE=START,DESC='3270 IO NAME ADDR DEMO',VER='1.0.0',  *
               REGS=YES,BASE=R12
*
         L     R1,T3270ADR
         LTR   R1,R1
         BNZ   SKIPLD
         LOAD  EPLOC=T3270IO
         ST    R0,T3270ADR
SKIPLD   EQU   *
*
*  INITIALIZE THE IOCB AND SET IT UP FOR AN STRT
*
         MVI   IOCB,X'00'
         MVC   IOCB+1(IOCBLN-1),IOCB
         MVC   IOCBFUNC,=C'STRT'
         TCALL T3270ADR,IOCB,TPARM
*
CLRINP   EQU   *
         CLEAR FNAME,C'_'
         MVI   MNAMED,C'_'
         CLEAR LNAME,C'_'
         CLEAR R07C17,C'_'
         CLEAR R07C33,C'_'
         CLEAR R07C61,C'_'
         CLEAR R08C17,C'_'
         CLEAR R09C09,C'_'
         CLEAR R09C37,C'_'
         CLEAR R09C52,C'_'
         CLEAR R14C20,C' '
         CLEAR R15C20,C' '
         CLEAR R16C20,C' '
         CLEAR R17C20,C' '
         CLEAR R18C20,C' '
*
*  SET UP IOCB FOR AN IO
*
IOLOOP   EQU   *
         MVC   IOCBFUNC,=C'IO  '
         MVI   FNAMEC,C'X'                  FORCE THE CURSOR
         TCALL T3270ADR,IOCB,TPARM
         CLI   TAID,AIDPFK03            PF3 PRESSED?
         BE    EXIT
         CLI   TAID,AIDPFK12            PF12 PRESSED?
         BE    CLRINP
         L     R2,IOCBADR
         MVC   R22C01D(79),0(R2)
         MVC   R23C01D(79),0(R2)
         MVC   R24C01D(79),0(R2)
         TR    R22C01D(79),CHRTBL
         TR    R23C01D(79),HITBL
         TR    R24C01D(79),LOTBL
*
*     PROCESS INPUT
*
         CLEAR R14C20,C' '
         SR    R1,R1
         LA    R1,R14C20D
         MVCTR FNAMED
         MVCTR MNAMED
         MVCTR LNAMED
*
         CLEAR R15C20,C' '
         LA    R1,R15C20D
         MVCTR R07C17D
         MVCTR R07C33D
         MVCTR R07C61D
*
         CLEAR R16C20,C' '
         LA    R1,R16C20D
         MVCTR R08C17D
*
         CLEAR R17C20,C' '
         LA    R1,R17C20D
         MVCTR R09C09D
         MVCTR R09C37D
         MVCTR R09C52D
*
         CLEAR R18C20,C' '
         B     IOLOOP
*
*  SET UP IOCB FOR AN END
*
EXIT     EQU   *
         MVC   IOCBFUNC,=C'END '
         TCALL T3270ADR,IOCB,TPARM
LEAVE    ERETURN RC=0
MVCTREX  MVC   0(0,R1),0(R14)
CNVWRK   DS    F
DWORD    DS    D
WORK3    DC    XL3'00'
         DS    CL240
TRTAB    DC    C'0123456789ABCDEF'
*
MEMDISP  DS    0H
CHRTBL   DC    4CL16'................'                X'0X'-X'3X'
         DC    1CL16' ...............'                X'4X'
         DC    1CL16'................'                X'5X'
         DC    1CL16'............. ..'                X'6X'
         DC    1CL16'................'                X'7X'
*                     A B C D E F G H I
         DC    XL16'4B8182838485868788894B4B4B4B4B4B' X'8X'
*                     J K L M N O P Q R
         DC    XL16'4B9192939495969798994B4B4B4B4B4B' X'9X'
*                       S T U V W X Y Z
         DC    XL16'4B4BA2A3A4A5A6A7A8A94B4B4B4B4B4B' X'AX'
         DC    CL16'................'   X'BX'
         DC    CL16'.ABCDEFGHI......'   X'CX'
         DC    CL16'.JKLMNOPQR......'   X'DX'
         DC    CL16'..STUVWXYZ......'   X'EX'
         DC    CL16'0123456789......'   X'FX'
HITBL    DC    16C'0'
         DC    16C'1'
         DC    16C'2'
         DC    16C'3'
         DC    16C'4'
         DC    16C'5'
         DC    16C'6'
         DC    16C'7'
         DC    16C'8'
         DC    16C'9'
         DC    16C'A'
         DC    16C'B'
         DC    16C'C'
         DC    16C'D'
         DC    16C'E'
         DC    16C'F'
LOTBL    DC    16CL16'0123456789ABCDEF'
T3270IO  DC    CL8'T3270IO'
T3270ADR DC    F'0'
IOCB     DS    0F
IOCBFUNC DS    CL4                     FUNCTION CODE (STRT/IO/END)
IOCBRC   DS    AL2                     RETURN CODE
IOCBLEN  DS    AL2                     IO BUFFER LENGTH
IOCBADR  DS    A                       IO BUFFER ADDRESS
         DS    XL20                    RESERVED
IOCBLN   EQU   *-IOCB
*
         COPY  AIDCODES
*
         COPY  DEMOPAN
         END   ADEMO
