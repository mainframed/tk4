***********************************************************************
*                                                                     *
*              COLPAGE --- COLUMN EJECT ROUTINE                       *
*                                                                     *
***********************************************************************
         SPACE
COLPAGE $ENTER ,
         CLI   ICORFLG,ICORNONE    'NO KEEP' IN PROGRESS
         BNE   COLGE043            ERROR IF NOT
         CLI   ICOLFLG,ICOLMC      MULTI-COLUMN MODE .Q
         BE    COLG01              BRANCH IF YES
        $CALL  PAGE                TREAT AS PAGE EJECT
         B     COLG03              GO TO RETURN
         SPACE
COLG01   DS    0H
         L     R1,CDCURR           CURRENT COLUMN BEING PROCESSED
         C     R1,CDCOUNT          PROCESSING LAST COLUMN .Q
         BE    COLG02              BRANCH IF YES
         BCTR  R1,0                DECREMENT COLUMN BY ONE
         MH    R1,MCALEN           LENGTH OF ONE COLUMN DESCRIPTOR
         LA    R1,MCAREA(R1)       POINT TO COLUMN ANCHOR
         USING CPFKFN,R1           AREA LOOKS LIKE THIS
        $ON    CPFKFNG,COLLOCK     MARK COLUMN LOCKED
         DROP  R1
COLG02   DS    0H
        $CALL  COLNEW              NEW COLUMN REQUIRED
COLG03   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
COLGE043 DS    0H
         MVI   EFLAG043,ERRSET     INVALID ROUTINE IN KEEP
         B     COLG03              GO TO RETURN
