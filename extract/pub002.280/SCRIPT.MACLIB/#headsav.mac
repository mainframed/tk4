***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'HEADSAVE' --- SAVE                  *
*              HEADING OR FOOTING INFORMATION                         *
*                   ON ENTRY -- (X1>0) -> HEADING TARGET AREA         *
*                               (X1<0) -> FOOTING TARGET AREA         *
*                            -- FNOCASE OFF IF QUOTES SEPARATE        *
*                                       ON IF FIRST CHAR SEPARATE     *
*                            -- DTTSWS  ON IF DEFAULT TITLE BEING SET *
*                                       OFF IF USER TITLE BEING SET   *
*                   ON EXIT  -- CC = BPOS IF OK                       *
*                               CC = BDNO IF ERROR                    *
*                            -- DTTSWS  SWITCH IS ALWAYS RESET        *
*                                                                     *
***********************************************************************
         SPACE
HEADSAVE $ENTER ,
         L     R1,ARGSTART         RETAIN ARGUMENT START ADDR
        $CALL  GETNUM              GET AND TEXT 1ST ARGUMENT
         BC    BOMIT+BDNO,HDS02    BRANCH IF INVALID OR OMITTED
         LTR   COUNT,COUNT         TOO SMALL .Q
         BP    HDS01               BRANCH IF NOT
         MVI   EFLAG014,ERRSET     SET TOO SMALL ERROR
         B     HDS15               AND TAKE ERROR EXIT
HDS01    DS    0H
         L     R0,DEFHSFS+TAB3DEF  MAXIMUM VALUE
         CR    COUNT,R0            TOO BIG .Q
         BNH   HDS03               BRANCH IF NOT
         MVI   EFLAG013,ERRSET     SET TOO BIG ERROR
         B     HDS15               AND TAKE ERROR EXIT
HDS02    DS    0H
         ST    R1,ARGSTART         RESCAN FROM BEGINNING
         LA    COUNT,1             ONE IS DEFAULT VALUE
HDS03    DS    0H
         LA    R1,L'EVNHEAD        LENGTH OF ONE TABLE ENTRY
         BCTR  COUNT,0             DECREMENT FIRST OPND BY ONE
         LTR   X1,X1               HEADINGS .Q
         BP    HDS04               BRANCH IF YES
         L     R0,DEFHSFS+TAB3DEF  MAX WRAP VALUE
         BCTR  R0,0                MAX WRAP VALUE -1
         SR    COUNT,R0            NEGATIVE INVERSE
         LPR   COUNT,COUNT         FOOTING VALUE
         LPR   X1,X1               REAL STARTING ADDR FOR FOOTINGS
HDS04    DS    0H
         MR    R0,COUNT            RESULTING TARGET OFFSET
         STH   R1,HSFSWRK          PASS THIS BACK
         AR    X1,R1               RESULTING TARGET ADDR
         SPACE
         L     TEMP,0(,X1)         BUFFER WORK ADDRESS
         LA    TEMP,0(,TEMP)       CLEAR DEFAULT FLAG, IF ANY
         LTR   TEMP,TEMP           IS AREA DEFINED .Q
         BNZ   HDS05               BRANCH IF YES
         LA    R1,246              NEED THIS MUCH SPACE
        $CALL  GETCELL             GET A WORK BUFFER
         BC    BPOS,HDS05          BRANCH IF SUCCESSFUL
         MVI   EFLAG024,ERRSET     NO SPACE AVAILABLE
         B     HDS15               AND TAKE ERROR EXIT
         SPACE
HDS05    DS    0H
         ST    TEMP,0(,X1)         SET BUFFER WORK ADDRESS
        $IFOFF SWITCHS,DTTSWS,HDS06     BRANCH IF NOT DEFAULT TITLE
         OI    0(X1),X'80'         TOP BIT FOR DEFAULTS
        $OFF   SWITCHS,DTTSWS      RESET DEFAULT COMMUNICATION BIT
HDS06    DS    0H
         LR    X1,TEMP             POINT TO WORK BUFFER
         L     TEMP,ARGSTART       ARGUMENT START ADDRESS
HDS07    DS    0H
         CLI   0(TEMP),C' '        LEADING BLANK .Q
         BNE   HDS08               BRANCH OUT IF NOT
         LA    TEMP,1(,TEMP)       BUMP ARGUMENT POINTER
         C     TEMP,ARGSTOP        END OF THE LINE .Q
         BNH   HDS07               BRANCH IF NOT
HDS08    DS    0H
         MVC   BREAKCHR,BREAKCHR+1 DEFAULT BREAK CHARACTER
        $IFOFF SWITCHS,FNOCASE,HDS09    UPPERCASE IMPLIES .HE FORMAT
         MVC   BREAKCHR,0(TEMP)    FIRST CHAR IS BREAK CHAR
         SPACE
HDS09    DS    0H
         XC    0(246,X1),0(X1)     RESET LENGTHS AND DATA TO ZERO
         SPACE
         L     X3,ARGSTOP          GET BUFFER COUNT
         LA    X3,1(,X3)           END OF BUFFER ADDRESS +1
         SR    X3,TEMP             LENGTH OF HEADER TEXT
         BNP   HDS14               IF NOT, NO HEADERS
         SPACE
         CLC   0(1,TEMP),BREAKCHR  ARE WE GETTING 3 ITEMS .Q
         BE    HDS10               IF YES, SKIP AHEAD
         SPACE
         LR    R1,X3               LENGTH OF FIRST AND ONLY SEGMENT
         BAL   RETURN,HDSSAV       ENTER TEXT SEGMENT
         B     HDS14               AND RETURN TO CALLER
         SPACE
HDS10    DS    0H
         LA    X2,3                COUNT OF STRINGS TO RETRIEVE
         SPACE
HDS11    DS    0H
         LA    TEMP,1(,TEMP)       SKIP STARTING BREAK CHARACTER
         LR    COUNT,TEMP          PRIME THE LOOP
         SPACE
HDS12    DS    0H
         LTR   X3,X3               IS COUNT DOWN TO ZERO .Q
         BNP   HDS14               IF SO, DONE
         CLC   0(1,COUNT),BREAKCHR A BREAK CHARACTER .Q
         BE    HDS13               IF YES, COMPLETE STRING FOUND
         LA    COUNT,1(,COUNT)     NO, LOOK AT NEXT CHARACTER
         BCT   X3,HDS12            (BUT DON'T EXCEED BUFFER)
         BCTR  COUNT,0             RESET END PTR TO STRING END
         LA    X2,1                MAKE SURE LOOP STOPS
         SPACE
HDS13    DS    0H
         LR    R1,COUNT            CHARACTER AFTER STRING END
         SR    R1,TEMP             STRING LENGTH
         BCTR  X3,0                ACCOUNT FOR QUOTE
         BAL   RETURN,HDSSAV       ENTER TEXT SEGMENT
         LR    TEMP,COUNT          WHERE TO RESUME SCAN
         BCT   X2,HDS11            LOOP THROUGH 3 ITEMS
         SPACE
HDS14    DS    0H
        $CC    POS                 SET OK EXIT
         B     HDS16               BRANCH TO RESTORE
HDS15    DS    0H
        $CC    DNO                 SET ERROR EXIT
HDS16    DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGS
         BR    RETURN              RETURN TO CALLER
         SPACE
HDSSAV   DS    0H
         STC   R1,1(,X1)           SET LENGTH OF SEGMENT
         S     R1,F1               LENGTH-1 FOR MVC
         BM    HDSSAV01            BRANCH IF EMPTY
         EX    R1,HDSMVC           COPY STRING SEGMENT IN
HDSSAV01 DS    0H
         LA    R1,1(,R1)           RESTORE ORIGINAL LENGTH
         LA    X1,2(R1,X1)         POINT TO NEXT DATA AREA
         BR    RETURN              AND RETURN TO CALLER
         SPACE
HDSMVC   MVC   2(*-*,X1),0(TEMP)   COPIES HEADING TEXT
