***********************************************************************
*                                                                     *
*              .DH (DEFINE HEADER) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
DH      $ENTER ,
        $CALL  GETYESNO            CHECK FOR SET OPERAND
         BC    BOMIT,DHE003        BRANCH IF NO ARGUMENTS AT ALL
         CLI   YNRESULT,YNSET      WAS SET SPECIFIED .Q
         BNE   DH03                BRANCH IF NO
        $CALL  GETNUM              GET SET OPERAND
         BC    BOMIT,DHE003        ERROR IF OMITTED
         BC    BDNO,DHE035         ERROR IF NOT NUMERIC
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    DH01                BRANCH IF YES
         A     COUNT,DHNUMTC       MAKE RELATIVE CHANGE
DH01     DS    0H
         LTR   COUNT,COUNT         IS OPERAND BIG ENOUGH .Q
         BM    DHE014              ERROR IF TOO SMALL
         LA    X3,DHMAXTC          GET MAX # OF TABLE OF CONTENTS
         CR    X3,COUNT            CHECK VALUE AGAINST MAX
         BNH   DHE013              BRANCH IF TOO HIGH
         ST    COUNT,DHNUMTC       SET TABLE OF CONTENTS NUMBER
         SLL   COUNT,2             MULTIPLY TABLE NUMBER BY 4
         L     TEMP,DHANCH(COUNT)  OBTAIN ANCHOR POINTER
         LTR   TEMP,TEMP           HAS ADDRESS BEEN ASSIGNED .Q
         BNZ   DH02                BRANCH IF YES
        $CALL  DHCLR               INITIALIZE TABLE AND SET ADDRESS
DH02     DS    0H
        $CALL  GETNUM              GET HEAD LEVEL NUMBER
         BC    BOMIT,DHDONE        BRANCH IF THAT'S ALL
         B     DH04                ELSE CONTINUE WITH CHECKS
         SPACE
DH03     DS    0H
         ST    COUNT,ARGSTART      RESET STARTING SCAN PTR
        $CALL  GETNUM              GET HEAD LEVEL NUMBER
DH04     DS    0H
         BC    BOMIT,DHE003        ISSUE ERROR IF OMITTED
         BC    BDNO,DHE035         ISSUE ERROR IF NOT NUMERIC
         BC    BNEG,DHE014         ISSUE ERROR IF NEGATIVE
         LA    X3,DHMAX            GET MAXIMUM HEAD LEVEL NUMBER
         CR    X3,COUNT            COMPARE WITH VALUE SPECIFIED
         BNH   DHE013              BRANCH IF GREATER OR EQUAL
         LR    R0,COUNT            SAVE HEAD LEVEL NUMBER
DHSCAN   DS    0H
        $CALL  GETYESNO            SCAN NEXT ARGUMENT
         BC    BOMIT,DHDONE        RETURN IF NONE
         CLI   YNRESULT,YNDHSTRT   ARGUMENT BELOW BOTTOM RANGE .Q
         BL    DHE005              BRANCH IF YES
         CLI   YNRESULT,YNDHEND    ARGUMENT ABOVE TOP LIMIT .Q
         BH    DHE005              BRANCH IF YES
         L     X1,DHNUMTC          GET TABLE OF CONTENTS LEVEL
         SLL   X1,2                TIMES 4
         L     X3,DHANCH(X1)       GET CORRECT CONTENTS ANCHOR
         SR    R15,R15             ZERO REGISTER
         IC    R15,YNRESULT        GET RESULT CODE
         LA    TEMP,YNDHSTRT       GET BASE CODE
         SR    R15,TEMP            NORMALIZE RESULT CODE
         SLL   R15,2               MULTIPLY BY 4
         STH   R0,DECM             ALIGN HEAD LEVEL NUMBER
         LA    TEMP,DHDLEN         LENGTH OF ONE DH AREA
         MH    TEMP,DECM           TIMES HEAD LEVEL NUMBER
         AR    X3,TEMP             PLUS BASE ADDRESS
         USING DHDSECT,X3          SET UP FOR ADDRESSABILITY
         EX    R0,DHSET(R15)       PERFORM APPROPRIATE ACTION
         B     DHSCAN              RESUME SCANNING
DHDONE   DS    0H
        $EXIT  RC=0                RETURN
         SPACE
DHSET    DS    0H
         B     DHGETNUM            OBTAIN NUMERIC ARGUMENT
         B     DHGETNUM            OBTAIN NUMERIC ARGUMENT
         B     DHGETNUM            OBTAIN NUMERIC ARGUMENT
         B     DHGETNUM            OBTAIN NUMERIC ARGUMENT
        $ON    DHFLAGS,DHTO        SET TABLE OF CONTENTS ENTRY ONLY
        $ON    DHFLAGS,DHTC        SET TABLE OF CONTENTS ENTRY WANTED
        $ON    DHFLAGS,DHTS        SET SPACE BEFORE TABLE OF CONTENTS
        $ON    DHFLAGS,DHUS        UNDERSCORE THE HEAD LEVEL
        $ON    DHFLAGS,DHUP        PUT THE HEAD LEVEL IN UPPERCASE
        $ON    DHFLAGS,DHOJ        OUTJUSTIFY THE HEAD LEVEL
        $ON    DHFLAGS,DHPA        ISSUE PAGE EJECT BEFORE HEAD LEVEL
        $ON    DHFLAGS,DHBR        DO A BREAK AFTER THE HEAD
        $OFF   DHFLAGS,DHTO        NO TABLE OF CONTENTS ENTRY ONLY OFF
        $OFF   DHFLAGS,DHTC        NO TABLE OF CONTENTS ENTRY WANTED
        $OFF   DHFLAGS,DHTS        NO SPACE BEFORE TABLE OF CONTENTS
        $OFF   DHFLAGS,DHUS        DO NOT UNDERSCORE THE HEAD LEVEL
        $OFF   DHFLAGS,DHUP        NO HEAD LEVEL IN UPPERCASE
        $OFF   DHFLAGS,DHOJ        DONT OUTJUSTIFY THE HEAD LEVEL
        $OFF   DHFLAGS,DHPA        NO PAGE EJECT BEFORE HEAD LEVEL
        $OFF   DHFLAGS,DHBR        NO BREAK AFTER THE HEAD
         SPACE
DHGETNUM DS    0H
         LR    X2,R15              SAVE INDEX REGISTER
        $CALL  GETNUM              SCAN FOR NUMERIC ARGUMENT
         BC    BOMIT,DHE003        ERROR IF OMITTED
         BC    BDNO,DHE005         ERROR IF NOT NUMERIC
         EX    R0,DHNUM(X2)        EXTRACT ADDRESS
         SR    X2,X2               CLEAR WORK REGISTER
         IC    X2,0(,X1)           OBTAIN VALUE FROM TABLE
         CLI   NUMSIGN,C' '        WAS NO SIGN GIVEN .Q
         BNE   DHNOSIGN            BRANCH IF NOT
         SR    X2,X2               ADD ZERO TO VALUE SPECIFIED
DHNOSIGN DS    0H                  RELATIVE NUMERIC ARGUMENT
         AR    X2,COUNT            ADD VALUE SPECIFIED
         LTR   X2,X2               VERIFY TOTAL ZERO OR POSITIVE
         BM    DHE014              BRANCH IF NOT
         C     X2,F255             IS TOTAL LESS THAN 255 .Q
         BH    DHE013              ERROR IF NO
         STC   X2,0(,X1)           SAVE BACK IN TABLE
         B     DHSCAN              RESUME SCANNING
         SPACE
DHNUM    DS    0H
         LA    X1,DHSKBF           POINT TO SKBF ENTRY
         LA    X1,DHSPAF           POINT TO SPAF ENTRY
         LA    X1,DHTCIN           POINT TO TCON ENTRY
         LA    X1,DHTCOF           POINT TO TCOF ENTRY
         DROP  X3
         SPACE
DHE013   DS    0H
         MVI   EFLAG013,ERRSET     NUMERIC ARGUMENT TOO LARGE
         B     DHDONE              GO TO RETURN
DHE003   DS    0H
         MVI   EFLAG003,ERRSET     ARGUMENT OMITTED
         B     DHDONE              GO TO RETURN
DHE005   DS    0H
         MVI   EFLAG005,ERRSET     ILLEGAL PARAMETER VALUE
         B     DHDONE              GO TO RETURN
DHE014   DS    0H
         MVI   EFLAG014,ERRSET     NUMERIC ARGUMENT TOO SMALL
         B     DHDONE              GO TO RETURN
DHE035   DS    0H
         MVI   EFLAG035,ERRSET     ARGUMENT MUST BE NUMERIC
         B     DHDONE              GO TO RETURN
