***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'GETARG' --- SCAN OPERAND FIELD            *
*                                                                     *
***********************************************************************
         SPACE
*                                                                     *
*        CC=BOMIT IF ARG IS OMITTED           (COUNT=1)               *
*        CC=BPOS  IF ARG IS PRESENT           (COUNT=ADDRESS)         *
*                                             (TEMP=LENGTH-1)         *
*                                                                     *
         SPACE
GETARG  $ENTER ,
         LA    R0,1                BXLE INCREMENT
         L     R1,ARGSTOP          BXLE COMPARAND
         L     COUNT,ARGSTART      WHERE TO START SCAN
         CLR   COUNT,R1            ALREADY AT END OF STRING .Q
         BH    GET02               IF YES, QUIT
         SPACE
GET01    DS    0H
         CLI   0(COUNT),C' '       SKIP LEADING BLANKS
         BNE   GET03               ..
         BXLE  COUNT,R0,GET01      ..
GET02    DS    0H
        $OFF   SWITCHS,FNOCASE     RESET CASE CONVERSION IF REQUIRED
         LA    COUNT,1             OMITTED ARG DUMMY ADDR
        $CC    OMIT                SET CONDITION CODE
         B     GET07               GO TO RETURN
         SPACE
GET03    DS    0H
         LR    TEMP,COUNT          NOW LOOK FOR ARGUMENT END
GET04    DS    0H
         CLI   0(TEMP),C' '        ..
         BE    GET05               ..
         SPACE
         CLI   0(TEMP),C'('        IS THIS AN OPEN PARENTHESIS
         BNE   *+8                 SKIP INSTRUCTION, IF NOT
         ST    TEMP,OPENPARN       SAVE POINTER TO IT
         SPACE
         CLI   0(TEMP),C')'        IS THIS A CLOSE PARENTHESIS
         BNE   *+8                 SKIP INSTRUCTION IF NOT
         ST    TEMP,CLOSPARN       ELSE SAVE POINTER
         SPACE
         BXLE  TEMP,R0,GET04       ..
GET05    DS    0H
         ST    TEMP,ARGSTART       ADDRESS OF START FOR NEXT CALL
         SR    TEMP,COUNT          TRUE LENGTH OF ARGUMENT
         BCTR  TEMP,0              SS-TYPE COUNT OF ARG
         SPACE
        $IFON  SWITCHS,FNOCASE,GET06    BRANCH IF NO UPPER CASE
         EX    TEMP,GETARG0        CONVERT ARG TO UPPER CASE
GET06    DS    0H
        $OFF   SWITCHS,FNOCASE     RESET CASE CONVERSION
        $CC    POS                 SET CC BPOS FOR ARG PRESENT
GET07    DS    0H
         ST    COUNT,ERRSTART      IN CASE OF ERROR
        $RESTOR KEEP=(COUNT,TEMP)  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
GETARG0  OC    0(*-*,COUNT),BLANKS CONVERTS TO UPPER CASE
