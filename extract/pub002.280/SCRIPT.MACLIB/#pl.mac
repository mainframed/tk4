***********************************************************************
*                                                                     *
*              .PL (PAGE LENGTH) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
PL      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,PL04           ERROR IF INVALID
         BC    BOMIT,PL01          SET DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    PL02                ABSOLUTE ARG IF NOT
         AH    COUNT,PLZ           ALTER ORIGINAL PAGE LENGTH
         B     PL02                GO CHECK AND ENTER
PL01     DS    0H
         L     COUNT,DEFPL+TAB3DEF  DEFAULT PAGE LENGTH VALUE
PL02     DS    0H
         LH    R0,BMMRG            CURRENT BOTTOM MARGIN VALUE
         AH    R0,TOPMRG           PLUS CURRENT TOP MARGIN VALUE
         CR    COUNT,R0            IS THIS BIGGER THAN SUM .Q
         BNH   PL05                GOT TO BE BAD IF NOT
         LH    X1,PLZ              ORIGINAL PAGE LENGTH MINUS
         SH    X1,PLCT             LINES REMAINING = LINES ALREADY PRT
         STH   COUNT,PLZ           SET PAGE LENGTH
         SR    COUNT,X1            NEW PGLN - LNS PRT = LN REM NEW PG
         BNH   PL03                NEW PAGE NEEDED. PRT LN GT NEW PG LN
         STH   COUNT,PLCT          SAVE LN REM ON NEW PG LENGTH
         B     PL06                NEW LENGTH NOT YET FULFILLED
PL03     DS    0H
         MVC   PLCT(2),PLZ         PUT NEW .PL INTO PLCT TO EJECT PAGE
        $CALL  PAGEREAL            NEW PGLG COMPLETE, EJECT
         B     PL06                GO TO RETURN
         SPACE
PL04     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     PL06                GO TO RETURN
PL05     DS    0H
         MVI   EFLAG045,ERRSET     PAGE LENGTH TOO SMALL
         B     PL06                GO TO RETURN
         SPACE
PL06     DS    0H
        $CALL  RMNEXT              SET UP AGAIN FOR NEXT AUTO REMOTE
        $EXIT  RC=0                RETURN TO MAIN CONTROLLER
