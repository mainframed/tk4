***********************************************************************
*                                                                     *
*              .PA (PAGE EJECT) --- CONTROL                           *
*                                                                     *
***********************************************************************
         SPACE
PA      $ENTER ,
         CLI   ICORFLG,ICORNONE    SAVING FORMATTED TEXT INCORE .Q
         BNE   PAE012              ERROR IF YES
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         L     X1,ARGSTOP          END  OF ALL ARGS
         LA    X1,1(,X1)           PLUS ONE
         ST    X1,ARGSTART         FOR GUARANTEED NULL NEXT TIME
         BC    BOMIT+BPOS,PA03     BRANCH IF NULL OR AFFIRMATIVE
         BC    BNEG,PA10           RETURN IF NEGATIVE
         CLI   YNRESULT,YNODD      IS ARG 'ODD' .Q
         BNE   PA01                BRANCH IF NOT
        $CALL  PD                  ODD PAGE ROUTINE
         B     PA10                GO TO RETURN
PA01     DS    0H
         CLI   YNRESULT,YNEVN      IS ARG 'EVEN' .Q
         BNE   PA02                BRANCH IF NOT
        $CALL  PV                  EVEN PAGE ROUTINE
         B     PA10                GO TO RETURN
PA02     DS    0H
         ST    COUNT,ARGSTART      RESCAN FOR NUMERIC ARG
PA03     DS    0H
        $CALL  PRINTNB             PRINT REMAINDER OF CURRENT LINE
         BC    BNEG,PA04           BRANCH IF NOBREAK EJECT
        $CALL  PAGEREAL            CAUSE PAGE EJECT
         SPACE
PA04     DS    0H
        $CALL  GETNUM              GET NEW INTEGER PAGE NUMBER
         BC    BOMIT,PA09          BRANCH IF OMITTED ARGUMENT
         BC    BDNO,PAE005         BRANCH IF INVALID ARGUMENT
         CLI   NUMSIGN,C' '        WAS ARGUMENT UNSIGNED .Q
         BE    PA05                BRANCH IF YES
         A     COUNT,PACNTI        ALTER INTEGER PART ACCORDINGLY
PA05     DS    0H
         LTR   COUNT,COUNT         TEST RESULT
         BNP   PAE014              ERROR IF NOT POSITIVE
         ST    COUNT,PACNTI        UPDATE CURRENT PAGE NUMBER
         SPACE
PA06     DS    0H
        $CALL  GETNUM              GET OPTIONAL DECIMAL PART
         BC    BOMIT,PA08          BRANCH IF NOTHING THERE
         BC    BDNO,PAE005         BRANCH IF INVALID ARGUMENT
         CLI   NUMSIGN,C' '        WAS ARGUMENT UNSIGNED .Q
         BE    PA07                BRANCH IF YES
         L     R0,PACNTD           FORMER DECIMAL VALUE
         LTR   R0,R0               IS OLD VALUE UNDEFINED .Q
         BM    PA07                BRANCH IF YES
         AR    COUNT,R0            ALTER THE DECIMAL PART
PA07     DS    0H
         LTR   COUNT,COUNT         TEST DECIMAL RESULT
         BM    PAE014              ERROR IF NEGATIVE
         ST    COUNT,PACNTD        UPDATE DECIMAL PART OF PAGE NUMBER
         B     PA09                GO TO EXIT RETURN
         SPACE
PA08     DS    0H
         L     R0,PACNTDI          INITIAL VALUE FOR DECIMAL PART
         ST    R0,PACNTD           SET DEFAULT
         SPACE
PA09     DS    0H
         B     PA10                GO TO RETURN
         SPACE
PAE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     PA10                GO TO RETURN
PAE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     PA10                GO TO RETURN
PAE012   DS    0H
         MVI   EFLAG012,ERRSET     ILLEGAL PLACEMENT OF CONTROL WORD
         B     PA10                GO TO RETURN
         SPACE
PA10     DS    0H
        $EXIT  RC=0                DONE WITH .PA CONTROL
