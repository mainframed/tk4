***********************************************************************
*                                                                     *
*              LKGESC  ---  ADD ESCAPE SEQUENCE TO OUTPUT             *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1)    BASE OF CALLER'S SAVE
*                      SAVEF0  OUTPUT POINTER
*                      SAVEH0  COUNT OR COLUMN (IF REQUIRED)
*                      SAVEX0  FUNCTION OFFSET IN TDEVICEA
*
*        ON EXIT   --  (R1)    NEXT AVAILABLE OUTPUT POINTER
         SPACE
LKGESC  $ENTER ,
         LR    X1,R1               ENTRY PARM POINTER
         USING SAVED0,X1           AND TELL THE ASSEMBLER
         L     R1,SAVEF0           OUTPUT POINTER
         L     X2,VDEVICEA         DEVICE POINTER
         LTR   X2,X2               DOES IT EXIST .Q
         BZ    LKGESC04            BRANCH IF NOT
         USING TDEVICEA,X2         TELL THE ASSEMBLER
         SR    TEMP,TEMP           CLEAR WORK REG
         IC    TEMP,SAVEX0         GET FUNCTION BYTE
         LA    TEMP,TDEVICEA(TEMP) POINT TO CODE
         CLI   0(TEMP),X'00'       IS IT DEFINED .Q
         BE    LKGESC04            BRANCH IF NOT
         SPACE
         SR    COUNT,COUNT         CLEAR LENGTH
         IC    COUNT,0(,TEMP)      GET LENGTH
         BCTR  COUNT,0             LENGTH-1
         MVI   0(R1),CESCAPE       ENTER ESCAPE CHARACTER
         EX    COUNT,LKGESCMV      ENTER FUNCTION STRING
         LA    R1,2(COUNT,R1)      NEXT FREE CHARACTER
         IC    COUNT,1(,TEMP)      GET FUNCTION TYPE
         B     *+4(COUNT)          DO IT
         B     LKGESC03                 ALL DONE
         B     LKGESC01                 ADD COUNT TO FUNCTION
         B     LKGESC02                 ADD COUNT+1 TO FUNCTION
         SPACE
LKGESC01 DS    0H
         LH    COUNT,SAVEH0        GET FUNCTION CODE
         A     COUNT,TDEVATEA      PLUS ASCII TO EBCDIC BASE
         IC    R0,0(,COUNT)        GET EBCDIC CHARACTER
         STC   R0,0(,R1)           AND SET IN OUTPUT
         LA    R1,1(,R1)           BUMP OUTPUT POINTER
         B     LKGESC03            AND GO TO RETURN
LKGESC02 DS    0H
         LH    COUNT,SAVEH0        GET FUNCTION CODE
         A     COUNT,TDEVATEA      PLUS ASCII TO EBCDIC BASE
         IC    R0,1(,COUNT)        GET EBCDIC CHARACTER
         STC   R0,0(,R1)           AND SET IN OUTPUT
         LA    R1,1(,R1)           BUMP OUTPUT POINTER
         SPACE
LKGESC03 DS    0H
        $CC    POS                 SUCCESSFUL TERMINATION
         B     LKGESC05            RETURN TO CALLER
         SPACE
LKGESC04 DS    0H
        $CC    NEG                 NO FUNCTION PERFORMED
         SPACE
LKGESC05 DS    0H
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
         SPACE
LKGESCMV MVC   1(*-*,R1),2(TEMP)   MOVE FUNCTION STRING
         DROP  X1,X2
