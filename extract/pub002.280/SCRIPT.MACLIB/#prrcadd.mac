***********************************************************************
*                                                                     *
*              PRRCADD --- ADD REVISION CODE TO OUTPUT LINE           *
*                                                                     *
***********************************************************************
         SPACE
PRRCADD $ENTER ,
         L     X1,RCAREAAD         REVISION CODE WORK AREA
         LTR   X1,X1               DOES IT EXIST .Q
         BZ    PRRC03              BRANCH IF NOT
         SPACE
         USING RCAREA,X1           AREA LOOKS LIKE THIS
        $IFON  SWITCHS,FOVLY,PRRC03     NO REV CODE IF LINE OVERLAID
        $IFON  SWITCHS,HDPSWS,PRRC03    NO REV CODE IF HEADING/FOOTING
         LH    R1,ADJCNT           CURRENT ADJUST VALUE
         S     R1,F1               LESS ONE FOR BLANK
         BNP   PRRC03               BRANCH IF NO ROOM AT ALL
         SPACE
         LA    R0,L'RCACHAR        MAX REVISION CODE LENGTH
         CR    R0,R1                IS IT SHORTER THAN AVAILABLE SPACE
         BH    PRRC01              BRANCH IF YES
         LR    R1,R0               TAKE MAX INSTEAD
PRRC01   DS    0H
         LA    TEMP,BUFF1          TARGET LINE ADDRESS
         AH    TEMP,ADJCNT          PLUS ADJUST VALUE
         S     TEMP,F1             LESS ONE FOR BLANK
         SR    TEMP,R1             LESS LENGTH OF REV CODE
         SPACE
         LA    COUNT,L'RCACHAR      MAX REV CODE LENGTH
         SR    COUNT,R1            OFFSET OF SOURCE
         BCTR  R1,0                 LENGTH -1 OF REV CODE
         SPACE
         CLC   RCASTALL,BLANKS     STALLED 'RC OFF' CHARS .Q
         BE    PRRC02              BRANCH IF NOT
         LA    COUNT,RCASTALL(COUNT)    POINT TO SOURCE
         EX    R1,PRRCMVC          SET REVISION CODE
         MVC   RCASTALL,BLANKS     CLEAR STALLED REV CODE
         B     PRRC03              AND CONTINUE
         SPACE
PRRC02   DS    0H
         CLC   RCACHAR,BLANKS      ANY REVISION CODE PRESENT .Q
         BE    PRRC03              BRANCH IF NONE
         LA    COUNT,RCACHAR(COUNT)     POINT TO SOURCE
         EX    R1,PRRCMVC          SET REVISION CODE
         SPACE
PRRC03   DS    0H
         $EXIT  RC=0               RETURN TO CALLER
         SPACE
PRRCMVC  MVC   0(*-*,TEMP),0(COUNT)     ENTER REV CODE IN TEXT
         DROP  X1
