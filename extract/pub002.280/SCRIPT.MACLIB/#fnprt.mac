***********************************************************************
*                                                                     *
*        INTERNAL ROUTINE 'FNPRT' --- PRINT QUEUED FOOTNOTES.         *
*                                                                     *
***********************************************************************
         SPACE
FNPRT   $ENTER ,
         LH    COUNT,PLCT          LINES LEFT ON THIS PAGE
         SH    COUNT,BMMRG         LESS BOTTOM MARGIN
         OC    FNSEP,FNSEP         ANY FOOTNOTES HERE .Q
         BZ    FNP08               BRANCH IF NOT
         CH    COUNT,FTCNT         FOOTNOTE TITLE DEPTH
         BNH   FNP08               BRANCH IF NOT FOOTNOTE TITLE +1
         SPACE
         L     X1,AFTHEAD          ADDR OF FOOTNOTE TITLES
         LH    COUNT,FTCNT         FOOTNOTE TITLE DEPTH
FNP01    DS    0H
         S     COUNT,F1            LESS ONE
         BM    FNP03               BRANCH OUT IF DONE
         L     X3,LLZ              RETAIN LINE LENGTH
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   FNP02               BRANCH IF NOT
         MVC   LLZ,CLLZ            SET COLUMN LENGTH FOR SEPARATOR
FNP02    DS    0H
        $CALL  HEADPRT             PRINT ONE FOOTNOTE TITLE LINE
         ST    X3,LLZ              RESTORE LINE LENGTH
         LA    X1,L'FTHEAD(,X1)    NEXT FOOTNOTE TITLE BUFFER
         B     FNP01               LOOP TILL COMPLETE
         SPACE
FNP03    DS    0H
         L     X2,FNQANCH          ADDRESS OF NEXT QUEUED LINE
         LTR   X2,X2               ANY SAVED LINE THERE .Q
         BNZ   FNP04               BRANCH IF YES
         OC    FNQCNT,FNQCNT       ANY LINES SUPPOSED TO BE QUEUED .Q
         BZ    FNRET               NO, TIME TO RETURN
         B     ABFNP03             ELSE QUIT NOW
         SPACE
FNP04    DS    0H
         USING FNQELEM,X2          AND TELL THE ASSEMBLER
        $IFOFF FNQFLAG,FNQTC,FNP05 BRANCH IF NOT TC ENTRY
         LA    R1,FNQTEXT          POINT TO DATA
         LNR   R1,R1               'FIFO' ENTRY
         LH    TEMP,FNQRDW         LENGTH INCLUDING RDW
         S     TEMP,F5             LENGTH-1 OF DATA
         BM    ABFNP04             TROUBLE IF NEGATIVE
         L     X3,DHNUMTC          CURRENT TABLE OF CONTENTS NUMBER
         LH    COUNT,FNQNUMTC      TC NUMBER WHEN LINE DEFINED
         ST    COUNT,DHNUMTC       SET FORMER FOR SAVE
        $CALL  HIDTCAD             ENTER IN CONTENTS CHAIN
         ST    X3,DHNUMTC          RESTORE TABLE OF CONTENTS NUMBER
         B     FNP07               FREE THIS ENTRY
         SPACE
FNP05    DS    0H
        $ON    SWITCHS,FOVLY       ASSUME AN OVERLAID LINE
        $IFON  FNQFLAG,FNQOVLY,FNP06    BRANCH IF OVERLAY LINE
         SPACE
        $OFF   SWITCHS,FOVLY       NOT AN OVERLAID LINE
         LH    COUNT,PLCT          ACCOUNT FOR LINE WE'RE ABOUT
         BCTR  COUNT,0             TO PRINT
         CH    COUNT,BMMRG         DOES IT PUT US BELOW MARGIN .Q
         BL    FNRET               IF YES, DONE FOR THIS PAGE
         STH   COUNT,PLCT          NO, SAVE UPDATED COUNT
         SPACE
FNP06    DS    0H
         MVC   FNQANCH,FNQFCHN     POINT TO NEXT ONE
         LH    R1,FNQRDW           LOAD LENGTH RDW/TEXT
         S     R1,F4               LESS LENGTH OF RDW
         BM    ABFNP01             BAD TROUBLE IF NEGATIVE
         SPACE
         LA    R0,FNQRDW           POINT TO OUTPUT RECORD
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY
        $CALL  IOPRINT             CAUSE LINE TO BE OUTPUT
        $OFF   SWITCHS,OOSWS       REENABLE OUTPUT OVERLAY
         SPACE
        $OFF   SWITCHS,FOVLY       RESET OVERLAY SWITCH
        $IFON  FNQFLAG,FNQOVLY,FNP07    BRANCH IF OVERLAY LINE
         L     COUNT,FNQCNT        CURRENT NUMBER OF FOOTNOTE LINES
         S     COUNT,F1            DECREMENT BY ONE
         BM    ABFNP02             GONE WRONG IF NEGATIVE
         ST    COUNT,FNQCNT        UPDATE QUEUED FOOTNOTE LINES
FNP07    DS    0H
         LH    R1,FNQRDW           LENGTH OF DATA AND RDW
         LA    R1,FNQOVHD(,R1)     PLUS PREFIX OVERHEAD
         LA    R0,FNQELEM          POINTER TO AREA
        $CALL  FREECELL            FREE THIS ENTRY
         B     FNP03               CONTINUE PRINTING
         SPACE
FNP08    DS    0H
        $CALL  SPACER              SPACE DOWN TO BOTTOM MARGIN
         SPACE
FNRET    DS    0H
         L     COUNT,FNQCNT        GET REMAINING NUMBER OF FOOTNOTES
         LTR   COUNT,COUNT         IS IT ZERO YET .Q
         BNP   FNRET2              IF YES, NO MORE QUEUED
         AH    COUNT,FTCNT         ACCOUNT FOR NEXT PAGE'S BREAK LINES
FNRET2   DS    0H
         AH    COUNT,BMMRG         STORE CORRECT BOTTOM MARGIN FOR
         LH    R1,PLZ              CURRENT PAGE LENGTH
         SH    R1,TOPMRG           LESS TOP MARGIN
         AH    R1,BMMRG            PLUS BOTTOM MARGIN
         AH    R1,FTCNT            PLUS NEXT PAGE BREAK LINES
         LA    R1,3(,R1)           ROUNDING PLUS ONE LINE
         SRA   R1,1                DIVIDED BY TWO
         CR    COUNT,R1            TOO MUCH STACKED .Q
         BNH   FNRET3              BRANCH IF NOT
         LR    COUNT,R1            TAKE WHAT WE CAN HANDLE NEXT PAGE
FNRET3   DS    0H
         SH    COUNT,BMMRG         LESS BOTTOM MARGIN
         STH   COUNT,FNSEP         NEXT PAGE
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
         DROP  X2
         SPACE
ABFNP01  DS    0H                  NULL FOOTNOTE SAVED FOR PRINT
ABFNP02  DS    0H                  NEGATIVE NUMBER OF FOOTNOTES
ABFNP03  DS    0H                  MISSING FOOTNOTE LINES
ABFNP04  DS    0H
        $LOGERR 1003
