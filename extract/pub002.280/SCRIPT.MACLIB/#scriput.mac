***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'SCRIPUT' --- OUTPUT RECORD TO FORMAT FILE *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   R0 -> RDW OF OUTPUT RECORD
*                   $CALL  SCRIPUT
         SPACE
SCRIPUT $ENTER ,
         LR    X1,R0               RECORD RDW POINTER
         MVC   DECM(2),0(X1)       ALIGN LENGTH TO PUT
         LH    X2,DECM             GET LENGTH TO PUT
         LTR   COUNT,X2            TEST FOR VALID
         BNP   ABSCPUT             ERROR IF TOO SMALL
        $IFOFF TTYPE,TONLINE,SCPUT01    BRANCH IF NOT ONLINE
         SPACE
         LR    R0,X1               RESTORE RDW POINTER
        $CALL  PUTCONS             DISPLAY LINE TO TERMINAL
         B     SCPUT07             GO TO RETURN
         SPACE
SCPUT01  DS    0H
         LA    R1,BLNKRDWL         LENGTH OF BLANK LINE
         CH    R1,0(,X1)           A CANDIDATE .Q
         BNE   SCPUT03             BRANCH IF NOT
         CLI   5(X1),C' '          BLANK DATA .Q
         BNE   SCPUT03             BRANCH IF NOT
         LA    R1,1                SINGLE SPACE
         CLI   4(X1),SINGLSP       IS IT .Q
         BE    SCPUT02             BRANCH IF YES
         LA    R1,2                DOUBLE SPACE
         CLI   4(X1),DOUBLSP       IS IT .Q
         BE    SCPUT02             BRANCH IF YES
         LA    R1,3                TRIPLE SPACE
         CLI   4(X1),TRIPLSP       IS IT .Q
         BNE   SCPUT03             BRANCH IF NOT
SCPUT02  DS    0H
         A     R1,FLOFFSP          CURRENT OUTSTANDING SPACES
         ST    R1,FLOFFSP          NEW OUTSTANDING TOTAL
         B     SCPUT07             AND GO TO RETURN
         SPACE
SCPUT03  DS    0H
         CLI   4(X1),SINGLSP       IS THIS JUST SINGLE SPACE .Q
         BE    SCPUT05             BRANCH IF YES
         CLI   4(X1),C'1'          IS THIS A CHANNEL SKIP .Q
         BL    SCPUT04             BRANCH IF NOT
         XC    FLOFFSP,FLOFFSP     CLEAR OUTSTANDING SPACES
         XC    FLOFFCH,FLOFFCH     AND OUTSTANDING CHANNEL SKIP
         SPACE
SCPUT04  DS    0H
         SR    R0,R0               SET PARM TO FLUSH THEM ALL
        $CALL  FLUSHSP             FLUSH ALL SPACES
         LR    R0,X1               RESTORE RDW POINTER
         B     SCPUT06             AND CONTINUE
SCPUT05  DS    0H
        $CALL  FLUSHSP             FLUSH TRIPLE SPACES
         L     R1,FLOFFSP          OUTSTANDING SPACES LEFT
         IC    R1,SPACTABL+1(R1)   INDEX CARRIAGE CONTROL
         STC   R1,4(,X1)           SET CARRIAGE CONTROL
         XC    FLOFFSP,FLOFFSP     CLEAR OUTSTANDING SPACES
SCPUT06  DS    0H
        $CALL  PUTPRT              OUTPUT THE RECORD
SCPUT07  DS    0H
        $RESTOR ,                  RESTORE ALL REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
ABSCPUT  DS    0H
        $LOGERR 1013               QUIT GRACEFULLY
