***********************************************************************
*                                                                     *
*              'SYGMAIN' --- GETMAIN ROUTINE                          *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0)  CONTAINS DESIRED LENGTH
*        ON EXIT   --  IF SUCCESSFUL  CC = BPOS
*                                     (R1) IS ADDR OF GOTTEN AREA
*                    IF UNSUCCESSFUL  CC = BNEG
*                                     (R1) IS ZERO
         SPACE
SYGMAIN $ENTER ,
         L     R15,=V(SYSINT)      SYSTEM DEPENDENT ROUTINE
         BAL   R14,4(,R15)         GET ADDRESS OF SICA
         LR    R2,R13              RETAIN CURRENT SAVE AREA ADDR
         LR    R13,R1              GIVE GETMAIN A WORK AREA
         SPACE
         LR    R1,R0               SET MAX CORE EQUAL MIN CORE
         L     R15,=V(SYSINT)      SYSTEM DEPENDENT ROUTINE
         BAL   R14,12(,R15)        GET SYSTEM STORAGE
         BNZ   SYGMA01             BRANCH IF FAILURE
        $CC    POS                 SET SUCCESSFUL RETURN
         B     SYGMA02             AND GO TO RETURN
         SPACE
SYGMA01  DS    0H
         SR    R1,R1               CLEAR RETURN POINTER
        $CC    NEG                 SET UNSUCCESSFUL
         SPACE
SYGMA02  DS    0H
         LR    R13,R2              RESTORE SAVE AREA ADDRESS
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
         SPACE
         LTORG ,
