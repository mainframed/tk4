***********************************************************************
*                                                                     *
*              PRINTNB  ---  CHECK FOR NOBREAK CHAR                   *
*                                                                     *
***********************************************************************
         SPACE
*        ON EXIT  --  CC=BPOS  IF NORMAL PRINT ACTION
*                     CC=BNEG  IF BREAK BYPASSED
         SPACE
PRINTNB $ENTER ,
        $IFON  SWITCHS,JUADSWS,PRNB01   BRANCH IF FORCE JUSTIFY
         CLC   PERIND(1),BUFF2     NORMAL CONTROL WORD .Q
         BE    PRNB01              BRANCH IF YES
         CLC   PERNBIND(1),BUFF2   NOBREAK CONTROL WORD .Q
         BE    PRNB04              BRANCH AROUND IF YES
PRNB01   DS    0H
         CLC   OLDCOUNT,F0         IS PRINT BUFFER EMPTY .Q
         BE    PRNB03              BRANCH IF YES
        $OFF   SWITCHS,JUADSWS     INITIALIZE JUSTIFY SWITCH
         CLI   JUVAL,JUVPOS        MORE THAN LEFT/RIGHT JUSTIFY .Q
         BNH   PRNB02              BRANCH IF NOT
        $ON    SWITCHS,JUADSWS     JUSTIFY LINE LATER WITH ADJUST
PRNB02   DS    0H
        $CALL  PRINT               NORMAL PRINT ROUTINE
PRNB03   DS    0H
        $OFF   SWITCHS,HYSUPSW     KILL AUTO HYPHENATION SUPPRESSION
         MVC   HYSUPWRK,HYSUPCNT   RESET NUMBERED HYPHENATION SUPPRESS
        $OFF   SWITCHS,JUADSWS     RESET ADJUSTMENT/JUSTIFY FLAG
        $CC    POS                 NORMAL PRINT ACTION
         B     PRNB05              AND GO TO RETURN
         SPACE
PRNB04   DS    0H
        $CC    NEG                 BREAK WAS BYPASSED
         SPACE
PRNB05   DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGS
         BR    RETURN              AND RETURN TO CALLER
