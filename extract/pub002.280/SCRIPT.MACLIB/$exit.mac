         MACRO
&LABEL  $EXIT  &KEEP=,&RC=
         LCLA  &I
&I       SETA  N'&KEEP
         AIF   ('&LABEL' EQ '').SKIP01
&LABEL   DS    0H
.SKIP01  AIF   (&I LE 0).SKIP04
         AIF   ('&KEEP(&I)' EQ 'R15').SKIP02
         ST    &KEEP(&I),4*&KEEP(&I).(,BASESAVE)  SAVE KEEP REGISTER
         AGO   .SKIP03
.SKIP02  ANOP
         MNOTE 1,'$EXIT  --  R15 USED AS KEEP'
.SKIP03  ANOP
&I       SETA  &I-1
         AGO   .SKIP01
.SKIP04  AIF   ('&RC' NE '').SKIP05
         MNOTE 1,'$EXIT  --  MISSING RETURN CODE'
         AGO   .SKIP07
.SKIP05  AIF   ('&RC'(1,1) EQ '(').SKIP06
         LA    R15,&RC             RETURN CODE
         AGO   .SKIP07
.SKIP06  ANOP
         AIF   ('&RC' EQ '(R15)').SKIP07
         LR    R15,&RC             RETURN CODE
.SKIP07  ANOP
         LM    0,14,0(BASESAVE)    RESTORE CALLER'S REGISTERS
         BR    6                   RETURN TO CALLER
         MEND
