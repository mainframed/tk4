***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'OUSTAT' --- FORMAT USAGE STATISTICS       *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  OUSTAT                                    *
         SPACE
OUSTAT  $ENTER ,
         L     COUNT,BUFF3AD       BUFFER WORK AREA
         L     X1,ADBASE           MAIN SCRIPT ENTRY POINT
         SR    R1,R1               ZERO LENGTH REGISTER
         IC    R1,4(,X1)           LENGTH OF VERSION IDENT
         BCTR  R1,0                LENGTH-1 FOR MOVE
         EX    R1,OUSTMVC1         MOVE SCRIPT IDENTIFIER
         XC    0(4,COUNT),0(COUNT) CLEAR RDW
         LA    R1,6(,R1)           LENGTH OF RESULTING RECORD
         STH   R1,0(,COUNT)        ENTER RECORD LENGTH
         MVI   4(COUNT),C'0'       DOUBLE SPACED
         BAL   RETURN,OUSTPRT      DUMP THAT
         B     OUST01              AND CONTINUE
OUSTMVC1 MVC   5(*-*,COUNT),5(X1)  EXECUTED MOVE VERSION IDENT
         SPACE
OUST01   DS    0H
         L     X1,PLSTSV           PARM LIST ADDRESS
         L     X1,0(,X1)           GET ADDR OF ACTUAL LIST
         LH    R1,0(,X1)           GET LENGTH OF PARM
         LTR   R1,R1               IS IT NULL .Q
         BZ    OUST02              BRANCH IF YES
         MVC   0(STPRM1L,COUNT),STPRM1S PARM MESSAGE
         BCTR  R1,0                LENGTH-1 FOR MVC
         EX    R1,OUSTMVC2         MOVE USER PARM
         LA    R1,1+STPRM1L(,R1)   NEW MESSAGE LENGTH
         STH   R1,0(,COUNT)        UPDATE WITH NEW LENGTH
         BAL   RETURN,OUSTPRT      DUMP THAT
         B     OUST02              AND CONTINUE
OUSTMVC2 MVC   STPRM1L(*-*,COUNT),2(X1) MOVE USER PARM
         SPACE
OUST02   DS    0H
         L     TEMP,PRGHIGH        REGION MANAGER HIGH WATER MARK
         LTR   TEMP,TEMP           ANYTHING USED .Q
         BNP   OUST03              BRANCH IF NOT
         MVC   0(STHGH1L,COUNT),STHGH1S HIGH CORE MESSAGE
         CVD   TEMP,DECM           CONVERT CORE TO DECIMAL
         ED    STHGH1B-STHGH1S(8,COUNT),DECM+4 CORE TO CHARACTER
         BAL   RETURN,OUSTPRT      AND DUMP THE RESULT
         SPACE
OUST03   DS    0H
         MVC   0(STMSG1L,COUNT),STMSG1S      FIRST MESSAGE
         BAL   RETURN,OUSTPRT      DUMP THAT
         SPACE
         MVC   0(STMSG2L,COUNT),STMSG2S      SECOND MESSAGE
         BAL   RETURN,OUSTPRT      DUMP THAT
         SPACE
         LM    X1,X3,INDEXS        SET CONTROL WORD INDEXS
         SR    R0,R0               ZERO TOTAL USE COUNT
OUST04   DS    0H
         LH    TEMP,SPCTLC(,X1)    GET USE COUNT
         LTR   TEMP,TEMP           IS IT ZERO .Q
         BZ    OUST05              BRANCH IF YES
         AR    R0,TEMP             BUMP TOTAL USE COUNT
         MVC   0(STMSG3L,COUNT),STMSG3S      THIRD MESSAGE
         MVC   STMSG3A-STMSG3S+3(2,COUNT),SPCTLI(X1)  ENTER ORIG NAME
         CVD   TEMP,DECM           CONVERT COUNT TO DECIMAL
         ED    STMSG3B-STMSG3S(6,COUNT),DECM+5  CONVERT TO CHARACTER
         BAL   RETURN,OUSTPRT      AND DUMP THE RESULT
OUST05   DS    0H
         BXLE  X1,X2,OUST04        DO AS MANY AS THERE ARE
         CVD   R0,DECM             CONVERT TOTAL COUNT TO DECIMAL
         MVC   0(STTOT1L,COUNT),STTOT1S ENTER TOTAL MESSAGE
         ED    STTOT1B-STTOT1S(8,COUNT),DECM+4    CONVERT TO CHARACTER
         BAL   RETURN,OUSTPRT      AND DUMP THE RESULT
         SPACE
         MVC   0(STCNT1L,COUNT),STCNT1S FIRST MESSAGE
         BAL   RETURN,OUSTPRT      DUMP THAT
         MVC   0(STCNT2L,COUNT),STCNT2S SECOND MESSAGE
         BAL   RETURN,OUSTPRT      DUMP THAT
         SPACE
         L     X3,VSCRICVT         COMMUNICATION VECTOR TABLE
         USING SCRICVT,X3          TELL THE ASSEMBLER
         LA    X1,VFSTATS          START OF ROUTINE ADDRS
         LA    X2,L'VFSTATS        ENTRY LENGTH
         LA    X3,VFSTATE          END OF ROUTINE ADDRS
         DROP  X3
         SR    R0,R0               ZERO TOTAL CALL COUNT
OUST06   DS    0H
         L     R1,0(,X1)           NEXT CANDIDATE
         LTR   R1,R1               ANYTHING THERE .Q
         BNP   OUST07              BRANCH IF NOT
         USING SCVTDSCT,R1         CANDIDATE AREA
         CLI   SCVTCCNT,X'47'      ENTRY SEQUENCE .Q
         BE    OUST07              BRANCH IF YES
         L     TEMP,SCVTCCNT       ROUTINE CALL COUNT
         LTR   TEMP,TEMP           TEST COUNT
         BNP   OUST07              BRANCH IF NOTHING
         AR    R0,TEMP             BUMP TOTAL CALL COUNT
         MVC   0(STCNT3L,COUNT),STCNT3S ENTER BASIC MESSAGE
         MVC   STCNT3A-STCNT3S(8,COUNT),SCVTCSCT  ENTER ROUTINE NAME
         CVD   TEMP,DECM           CALL COUNT TO DECIMAL
         ED    STCNT3B-STCNT3S(6,COUNT),DECM+5    ENTER CALL COUNT
         BAL   RETURN,OUSTPRT      AND DUMP RESULT
OUST07   DS    0H
         BXLE  X1,X2,OUST06        DO THEM ALL
         DROP  R1
         CVD   R0,DECM             CONVERT TOTAL COUNT TO DECIMAL
         MVC   0(STTOT1L,COUNT),STTOT1S ENTER TOTAL MESSAGE
         ED    STTOT1B-STTOT1S(8,COUNT),DECM+4    CONVERT TO CHARACTER
         BAL   RETURN,OUSTPRT      AND DUMP THE RESULT
         SPACE
         MVC   0(STCHR1L,COUNT),STCHR1S MOVE CHAR BLOCK MESSAGE
         L     TEMP,LKSIZE          FREE CHAR COUNT
         CVD   TEMP,DECM           CONVERT TO DECIMAL
         ED    STCHR1A-STCHR1S(6,COUNT),DECM+5    ENTER FREE CHAR COUNT
         L     TEMP,LKASIZE        TOTAL CHAR COUNT
         CVD   TEMP,DECM            CONVERT TO DECIMAL
         ED    STCHR1B-STCHR1S(6,COUNT),DECM+5    ENTER TOTAL CHAR CNT
         BAL   RETURN,OUSTPRT      DUMP THE RESULT
         SPACE
         MVC   0(STSAV1L,COUNT),STSAV1S SAVE AREAS MESSAGE
         LA    TEMP,SAVEVLS        MAX NUMBER OF SAVE AREAS
         CVD   TEMP,DECM           CONVERT TO DECIMAL
         OI    DECM+7,X'0F'        FIX THE SIGN
         UNPK  STSAV1-STSAV1S+8(3,COUNT),DECM+6(2)  ADD TOTAL TO MSG
         SPACE
OUST08   DS    0H
         LR    R1,TEMP             CURRENT NEST LEVEL
         BCTR  R1,0                GET LEVEL-1
         LA    R0,SAVELEN          LENGTH OF ONE AREA
         MR    R0,R0               STACK AREA OFFSET
         A     R1,SAVEINIT         PLUS INITIAL STACK AREA
         OC    0(SAVELEN,R1),0(R1) WAS IT USED .Q
         BNZ   OUST09              BRANCH OUT IF YES
         BCT   TEMP,OUST08         DO AS REQUIRED
OUST09   DS    0H
         CVD   TEMP,DECM           CONVERT TO DECIMAL
         OI    DECM+7,X'0F'        FIX THE SIGN
         UNPK  STSAV1-STSAV1S+1(3,COUNT),DECM+6(2)  ADD USED COUNT
         BAL   RETURN,OUSTPRT      DUMP IT
         SPACE
         MVC   0(STHIT1L,COUNT),STHIT1S MOVE REF VARIABLES MESSAGE
         L     TEMP,RNTBSRCH       TOTAL SEARCH COUNT
         S     TEMP,RNTBFAIL       LESS LOOKASIDE FAILURES
         CVD   TEMP,DECM           GIVES THE HIT COUNT
         ED    STHIT1A-STHIT1S(6,COUNT),DECM+5    ENTER HIT COUNT
         L     TEMP,RNTBSRCH       GET TOTAL SEARCH COUNT
         CVD   TEMP,DECM           GIVES THE TOTAL SEARCH COUNT
         ED    STHIT1B-STHIT1S(6,COUNT),DECM+5    ENTER SEARCH COUNT
         L     TEMP,RNTBCNT        GET UNSUBSCRIPTED VARIABLE COUNT
         CVD   TEMP,DECM           CONVERT TO DECIMAL
         ED    STHIT1C-STHIT1S(6,COUNT),DECM+5    ENTER USER REF COUNT
         L     TEMP,RNTBSCNT       GET USER SUBSCRIPTED VAR COUNT
         CVD   TEMP,DECM           CONVERT TO DECIMAL
         ED    STHIT1D-STHIT1S(6,COUNT),DECM+5    ENTER SUBSCRIPT REF
         BAL   RETURN,OUSTPRT      DUMP THE RESULT
         SPACE
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
OUSTPRT  DS    0H
        $SAVE ,                    SAVE ALL REGISTERS
         LR    R0,COUNT            MESSAGE RDW ADDRESS
        $CALL  PRTERM              DISPLAY RESULT ON SYSTERM
        $RESTOR ,                  RESTORE ALL REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
STPRM1S  DC    0H'0',AL2(STPRM1L,0)
STPRM1   DC    C'0OVERRIDING PARM='
STPRM1L  EQU   *-STPRM1S
         SPACE
STHGH1S  DC    0H'0',AL2(STHGH1L,0)
STHGH1A  DC    C'0MAXIMUM INTERNAL REGION'
STHGH1B  DC    X'4020202020202120'
STHGH1L  EQU   *-STHGH1S
         SPACE
STMSG1S  DC    0H'0',AL2(STMSG1L,0)
STMSG1   DC    C'0CONTROL  USE'
STMSG1L  EQU   *-STMSG1S
         SPACE
STMSG2S  DC    0H'0',AL2(STMSG2L,0)
STMSG2   DC    C'  WORD   COUNT'
STMSG2L  EQU   *-STMSG2S
         SPACE
STMSG3S  DC    0H'0',AL2(STMSG3L,0)
STMSG3A  DC    C'  ''**'' '
STMSG3B  DC    X'402020202120'
STMSG3L  EQU   *-STMSG3S
         SPACE
STSAV1S  DC    0H'0',AL2(STSAV1L,0)
STSAV1   DC    C'0*** OF *** STACK SAVE AREAS USED'
STSAV1L  EQU   *-STSAV1S
         SPACE
STHIT1S  DC    0H'0',AL2(STHIT1L,0),C'0'
STHIT1A  DC    X'402020202120',C' OF'
STHIT1B  DC    X'402020202120',C' REFERENCE LOOKASIDE HITS FOR'
STHIT1C  DC    X'402020202120',C' VARIABLES AND'
STHIT1D  DC    X'402020202120',C' SUBSCRIPTS'
STHIT1L  EQU   *-STHIT1S
         SPACE
STCNT1S  DC    0H'0',AL2(STCNT1L,0)
STCNT1   DC    C'0ROUTINE   CALL'
STCNT1L  EQU   *-STCNT1S
         SPACE
STCNT2S  DC    0H'0',AL2(STCNT2L,0)
STCNT2   DC    C'  NAME    COUNT'
STCNT2L  EQU   *-STCNT2S
         SPACE
STCNT3S  DC    0H'0',AL2(STCNT3L,0),C' '
STCNT3A  DC    C'********'
STCNT3B  DC    X'402020202120'
STCNT3L  EQU   *-STCNT3S
         SPACE
STTOT1S  DC    0H'0',AL2(STTOT1L,0)
STTOT1A  DC    C' TOTAL '
STTOT1B  DC    X'4020202020202120'
STTOT1L  EQU   *-STTOT1S
         SPACE
STCHR1S  DC    0H'0',AL2(STCHR1L,0),C'0'
STCHR1A  DC    X'402020202120',C' OF'
STCHR1B  DC    X'402020202120',C' CHARACTERS FREE'
STCHR1L  EQU   *-STCHR1S
