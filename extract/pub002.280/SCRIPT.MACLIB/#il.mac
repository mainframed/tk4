***********************************************************************
*                                                                     *
*              .IL (INDENT LINE) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
IL      $ENTER ,
        $CALL  PRINTNB             BREAK
        $CALL  GETNUM              GET AMOUNT
         BC    BDNO,ILE005         ERROR IF INVALID
         BC    BOMIT,IL01          TAKE DEFAULT IF OMITTED
         LCR   COUNT,COUNT         INDENT LINE IS INVERSE OF UNDENT
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    IL02                BRANCH IF YES
         AH    COUNT,UNDL          MAKE RELATIVE CHANGE
         B     IL02                GO CHECK AND ENTER
IL01     DS    0H
         SR    COUNT,COUNT         ZERO IS DEFAULT
IL02     DS    0H
         L     R1,LLZ              GET LINE LENGTH
         AH    R1,INDENTR          PLUS RIGHT INDENT
         LCR   R1,R1               MAKE IT NEGATIVE
         CR    COUNT,R1            IS VALUE TOO SMALL .Q
         BNH   ILE014              TOO SMALL IF NOT
         CH    COUNT,INDL          COMPARE WITH INDENT
         BH    ILE013              ERROR IF LARGER
         STH   COUNT,UNDL          SET UNDENTATION
         STH   COUNT,OFUNCNT       SET MARGIN VALUE FOR FORMAT
         SH    COUNT,INDL          LESS CURRENT INDENT
         LCR   COUNT,COUNT         POSITIVE VALUE
         STH   COUNT,RMARGIN       SET RIGHT MARGIN
         SPACE
IL03     DS    0H
        $EXIT  RC=0                AND CONTINUE
         SPACE
ILE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     IL03                AND GO TO RETURN
ILE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     IL03                AND GO TO RETURN
ILE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     IL03                AND GO TO RETURN
