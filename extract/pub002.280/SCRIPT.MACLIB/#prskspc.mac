***********************************************************************
*                                                                     *
*              PRSKSPC - OUTPUT ANY CONDITIONAL SPACE/SKIP LINES      *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  SKSPCCNT  CONTAINS CONDITIONAL COUNT
*                  --  SKSPFLG2  CONTAINS TYPE OF SPACE/SKIP
*
*        ON EXIT   --  CC = BPOS IF ACTION PERFORMED
*                         = BOMIT IF NO ACTION PERFORMED
         SPACE
PRSKSPC $ENTER ,
         LH    COUNT,SKSPCCNT      GET CONDITIONAL SKIP/SPACE COUNT
         LTR   COUNT,COUNT         ANYTHING PRESENT .Q
         BZ    PRSSC04             BRANCH IF NO SPACES
         MVC   SKSPFLG,SKSPFLG2    RESTORE FLAGS
         MVI   SKSPFLG2,X'00'      CLEAR SAVED FLAGS
         XC    SKSPCCNT,SKSPCCNT   CLEAR COUNT
         CLI   ICORFLG,ICORNONE    ARE WE FORMATTING INTO CORE .Q
         BNE   PRSSC01             BRANCH IF YES
        $CALL  SPEX                DO THE SKIP/SPACE
         B     PRSSC03             CONTINUE
         SPACE
PRSSC01  DS    0H
         MVI   PRNTRCC,SINGLSP     SINGLE SPACE CARRIAGE CONTROL
         LA    R0,BLNKLINE         POINT TO BLANK LINE RDW
PRSSC02  DS    0H
        $CALL  FNSAVE              SAVE BLANK LINE
         BCT   COUNT,PRSSC02       LOOP TILL DONE
PRSSC03  DS    0H
         MVI   SKSPFLG,X'00'       CLEAR SPACING FLAGS
        $CC    POS                 INDICATE ACTION PERFORMED
         B     PRSSC05             AND GO TO RETURN
         SPACE
PRSSC04  DS    0H
        $CC    OMIT                INDICATE NO ACTION PERFORMED
         SPACE
PRSSC05  DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
