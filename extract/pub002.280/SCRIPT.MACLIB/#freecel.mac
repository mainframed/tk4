***********************************************************************
*                                                                     *
*              INTERNAL ROUTINE 'FREECELL' -- FREE A QUEUE BUFFER     *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY -  R0     ADDRESS TO BE FREED
*                 -  R1     LENGTH TO BE FREED
*                 -  RETURN RETURN ADDRESS
*                 -  IF R0 = R1 = 0  TERMINATION CALL
*        ON EXIT  -  RETURN IF SUCCESS
*                 -  ABEND  IF FAILURE
         SPACE
FREECELL $ENTER ,
         L     X1,RGLISTAD         PARM AREA ADDRESS
         USING RGLIST,X1           AND TELL THE ASSEMBLER
         LTR   R0,R0               IS ADDR ZERO .Q
         BNZ   FREC01              NO, NOT TERMINATION
         LTR   R1,R1               IS LENGTH ZERO .Q
         BNZ   FREC01              NO, AGAIN NOT TERMINATION
         CLI   PRGFLAG,PRGINIT     ANY MANAGED REGIONS .Q
         BE    FREC02              BRANCH IF NONE
         MVI   PRGFLAG,PRGFINIS    SET TERMINATION FLAG
         LA    R1,RGLIST           POINT TO PARM LIST
        $CALL  RGNMGR              GO TERMINATE REGION MANAGER
         BC    15-BPOS,ABFCELL     TERMINATE IF ERROR
         B     FREC02              RETURN TO CALLER
         SPACE
FREC01   DS    0H
         ST    R0,PCELLPTR         POINT TO ENTRY
         ST    R1,PCELLLEN         AND SET ITS LENGTH
         S     R1,F2               LENGTH-1 OF REMAINDER TO FREE
         BNP   ABFCELL             ERROR IF TOO SMALL
         LR    COUNT,R0            POINT TO AREA TO FREE
         MVI   0(COUNT),X'7F'      DIRTY REGION TO FREE
         EX    R1,FRECMVC          ALL OF IT
         LA    R1,RGLIST           POINT TO PARM LIST
        $CALL  RGNMGR              RELEASE THE CELL
         BC    15-BPOS,ABFCELL     TERMINATE IF ERROR
         SPACE
         L     R1,PRGUSED          FORMER SPACE FROM REGION MANAGER
         S     R1,PCELLLEN         LESS AREA JUST FREED
         BM    ABFCELL             ERROR IF NEGATIVE
         ST    R1,PRGUSED          SET CURRENT SPACE USED
         DROP  X1
         SPACE
FREC02   DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGS
         BR    RETURN              AND RETURN TO CALLER
         SPACE
FRECMVC  MVC   1(*-*,COUNT),0(COUNT)    DIRTY REST OF FREE REGION
         SPACE
ABFCELL  DS    0H
        $LOGERR 1005               FREECELL ERROR RETURN
