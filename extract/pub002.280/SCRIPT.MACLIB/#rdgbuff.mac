***********************************************************************
*                                                                     *
*        THIS ROUTINE GETS A BUFFER FOR THE CURRENT FILE              *
*                                                                     *
***********************************************************************
         SPACE
RDGBUFF $ENTER ,
         LTR   RDD,RDD             WAS THE FILE SUCCESSFULLY ALLOC .Q
         BZ    RDGB02              IF NOT, UNABLE TO ALLOCATE BUFFERS
         SPACE
        $IFON  FILFLAG,FIL$BUFF,RDGB02  BRANCH IF BUFFER ALLOCATED
         SPACE
         LH    RA,DDDCB+DCBBLKSI   LOAD BLOCKSIZE FROM DCB
         ST    RA,FILBUFLN         STORE IN BUFFER LENGTH FIELD
         LTR   RA,RA               TEST LENGTH TO GET
         BZ    RDGB02              LENGTH IS ZERO FOR REMOTES
         SPACE
         LR    R0,RA               SET DESIRED BUFFER LENGTH
        $CALL  SYGMAIN             GET STORAGE FOR THE BUFFERS
         LR    RA,R1               BUFFER ADDR, ZERO IF FAILED
         BC    BNEG,RDGB01         BRANCH IF FAILED
        $ON    FILFLAG,FIL$BUFF    INDICATE BUFFER ALLOCATED
         SPACE
RDGB01   DS    0H
         LA    RA,0(,RA)           CLEAR HIGH-ORDER BYTE
         ST    RA,FILBUFAD         THIS BECOMES THE BUFFER ADDRESS
         ST    RA,FILENDBL         INITIALIZE POINTERS SO A READ IS FOR
         ST    RA,FILNEXRC
         SPACE
RDGB02   DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
