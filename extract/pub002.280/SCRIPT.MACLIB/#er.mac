***********************************************************************
*                                                                     *
*              .ER (ERROR MESSAGE) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
ER      $ENTER ,
         L     X1,ARGSTART         MESSAGE MAY FOLLOW IMMEDIATELY
        $CALL  GETNUM              GET ERROR RETURN CODE
         BC    BOMIT,ER04          DO NOTHING, IF NOTHING THERE
         BC    BDNO,ER02           BRANCH IF NOT NUMERIC
         LTR   X2,COUNT            TEST RESULT
         BM    ERE014              ERROR IF NEGATIVE
         CH    X2,RETCODE          HIGHER THAN LAST .Q
         BNH   ER01                BRANCH IF NOT
         STH   X2,RETCODE          SET NEW RETURN CODE
ER01     DS    0H
         L     X1,ARGSTART         MESSAGE FOLLOWS
         LA    R0,ERRTERM          TERMINAL ERROR CODE
         CR    X2,R0               WORSE THAN TERMINAL .Q
         BNH   ER03                PROCESS AS IS IF NOT
         LR    X2,R0               ELSE TERMINAL IS BAD ENOUGH
         B     ER03                GO PROCESS MESSAGE
         SPACE
ER02     DS    0H
         ST    COUNT,ARGSTART      RESCAN FOR IDENTIFIER
         LA    X2,ERRINFO          THIS IS AN INFORMATION MESSAGE
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET ARGUMENT STRING
         LTR   TEMP,TEMP           LENGTH-1 ZERO .Q
         BNZ   ER03                BRANCH IF NOT
         CLI   0(COUNT),C'*'       '*' MEANS NO RETURN CODE
         BNE   ER03                BRANCH IF NOT
         L     X1,ARGSTART         MESSAGE FOLLOWS
         SPACE
ER03     DS    0H
         L     COUNT,ARGSTOP       ADDR OF END OF RECORD
         SR    COUNT,X1            COMPUTE LENGTH OF LINE TO PRINT
         BNP   ER04                EXIT IF NOTHING TO PRINT
         LA    X1,1(,X1)           BUMP START OVER BLANK DELIM
         ST    X1,ERRTEXT          POINT TO MESSAGE STRING
         STC   COUNT,ERRTEXT       SET LENGTH OF STRING
         STC   X2,EFLAG050         SET ERROR SEVERITY
        $CALL  ERROUT              PRINT THE RESULT
         SPACE
ER04     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
ERE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND VALUE TOO SMALL
         B     ER04                GO TO RETURN
