***********************************************************************
*                                                                     *
*              .FS (FOOTING SPACING) --- CONTROL                      *
*                                                                     *
***********************************************************************
         SPACE
FS      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,FS03           ERROR IF INVALID
         BC    BOMIT,FS01          ENTER DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    FS02                BRANCH IF NOT
         AH    COUNT,FSCNT         ALTER EXISTING FOOTING
         B     FS02                GO CHECK AND ENTER
FS01     DS    0H
         LA    COUNT,1             DEFAULT FOOTING SPACING VALUE
FS02     DS    0H
         LTR   COUNT,COUNT         IS FOOTING SPACING NEGATIVE
         BM    FS05                ERROR IF YES
         LH    TEMP,BMMRG          BOTTOM MARGIN VALUE
         SH    TEMP,FTMRG          LESS FOOTING MARGIN
         CR    COUNT,TEMP          COMPARE VALUES
         BH    FS06                ERROR IF BIGGER
         C     COUNT,DEFHSFS+TAB3DEF    BIGGER THAN TT/BT OVERLAP .Q
         BH    FS04                ERROR IF YES
         STH   COUNT,FSCNT         SET FOOTING SPACING
         B     FS07                GO TO COMMON EXIT
         SPACE
FS03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID ARGUMENT
         B     FS07                AND GO BACK
FS04     DS    0H
         MVI   EFLAG013,ERRSET     ARG TOO BIG
         B     FS07                AND GO BACK
FS05     DS    0H
         MVI   EFLAG014,ERRSET     ARG TOO SMALL
         B     FS07                AND GO BACK
FS06     DS    0H
         MVI   EFLAG042,ERRSET     ARG TOO BIG
         B     FS07                AND GO BACK
         SPACE
FS07     DS    0H
        $EXIT  RC=0                DONE WITH .FS
