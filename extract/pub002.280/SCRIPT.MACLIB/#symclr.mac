***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'SYMCLR' -- DELETE REFERENCE SYMBOLS       *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER --  LA    R1,SRAREA  POINT TO DICTIONARY
*                    $CALL  SYMCLR     DELETE DICTIONARY ENTRIES
         SPACE
SYMCLR  $ENTER ,
         LR    X2,R1               POINT TO DICT ENTRY
         USING SRAREA,X2           AND TELL THE ASSEMBLER
         L     X1,SRANCH           FIRST SYMBOL POINTER
         XC    SRAREA(SRALEN),SRAREA    CLEAR DICT ENTRY
         DROP  X2
         SPACE
SCLR01   DS    0H
         LTR   X1,X1               AT END OF SYMBOL CHAIN .Q
         BZ    SCLR03              RETURN IF YES
         USING RND,X1              SYMBOL LOOKS LIKE THIS
         L     X2,RNSLINK          NEXT ARRAY ELEMENT
         LA    R1,RND              POINT TO SYMBOL AREA
         L     X1,RNSCHN           NEXT SYMBOL AREA
         BAL   RETURN,SCLR10       DELETE THE SYMBOL
         L     R1,RNTBCNT          VARIABLE COUNT
         S     R1,F1               LESS ONE
         ST    R1,RNTBCNT          UPDATE USER VARIABLE COUNT
         DROP  X1
         SPACE
SCLR02   DS    0H
         LTR   X2,X2               AT END OF ARRAY CHAIN .Q
         BZ    SCLR01              BRANCH IF YES
         USING RND,X2              ARRAY ELEMENT DESCRIPTOR
         LA    R1,RND              POINT TO ARRAY ELEMENT
         L     X2,RNSLINK          NEXT ARRAY ELEMENT
         BAL   RETURN,SCLR10       DELETE ARRAY ELEMENT
         L     R1,RNTBSCNT         ARRAY ELEMENT COUNT
         S     R1,F1               LESS ONE
         ST    R1,RNTBSCNT         UPDATE ARRAY ELEMENT COUNT
         B     SCLR02              GO DOWN ENTIRE ARRAY CHAIN
         DROP  X2
         SPACE
SCLR03   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
SCLR10   DS    0H
        $SAVE  ,                   SAVE ALL REGISTERS
         LR    X1,R1               POINT TO SYMBOL
         USING RND,X1              AND TELL THE ASSEMBLER
         TM    RNFLAG,RNCHAR       A CHARACTER VALUE .Q
         BNO   SCLR11              BRANCH IF NOT
         CLI   RNCLEN,L'RNCVAL     A LONG CHARACTER VALUE .Q
         BNH   SCLR11              BRANCH IF NOT
         L     R0,RNCPTR           POINTER TO LONG STRING
         SR    R1,R1               ZERO LENGTH REG
         IC    R1,RNCLEN           LENGTH OF LONG STRING
        $CALL  FREECELL            RELEASE THE AREA
         SPACE
SCLR11   DS    0H
         LA    R0,RND              POINT TO SYMBOL AREA
         LA    R1,RNLN             LENGTH OF SYMBOL AREA
        $CALL  FREECELL            RELEASE SYMBOL AREA
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         DROP  X1
