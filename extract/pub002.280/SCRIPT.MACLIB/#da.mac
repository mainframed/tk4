***********************************************************************
*                                                                     *
*              .DA (DARK OUTPUT) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
DA      $ENTER ,
        $CALL  GETYESNO            GET AND TEST OPERAND
         BC    BOMIT,DA03          BRANCH IF OMITTED
         BC    BPOS,DA01           BRANCH IF AFFIRMATIVE
         BC    BNEG,DA02           BRANCH IF NEGATIVE
         ST    COUNT,ARGSTART      RESCAN FROM HERE FOR NUMERIC
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,DAE005         ERROR IF INVALID
         CLI   NUMSIGN,C' '        WAS OPERAND SIGNED .Q
         BE    DA04                BRANCH IF NOT
         AH    COUNT,DARKVAL       MAKE RELATIVE CHANGE
         B     DA04                GO CHECK AND ENTER
DA01     DS    0H
         LA    COUNT,2             AFFIRMATIVE MEANS TWICE
         B     DA04                GO TEST AND ENTER
DA02     DS    0H
         LA    COUNT,1             NEGATIVE MEANS BUT ONCE
         B     DA04                GO TEST AND ENTER
DA03     DS    0H
         L     COUNT,DEFDARK+TAB3DEF    GET DEFAULT DARK VALUE
DA04     DS    0H
         C     COUNT,DEFDARK+TAB3MIN    LESS THAN MINIMUM .Q
         BL    DAE014              ERROR IF YES
         C     COUNT,DEFDARK+TAB3MAX    GREATER THAN MAXIMUM .Q
         BH    DAE013              YES, ERROR
         STH   COUNT,DARKVAL       SET DARK OUTPUT VALUE
         SPACE
DA05     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
DAE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     DA05                GO TO TERMINATE
DAE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     DA05                GO TO TERMINATE
DAE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     DA05                GO TO TERMINATE
