***********************************************************************
*                                                                     *
*              .PN (PAGE NUMBER) --- CONTROL                          *
*                                                                     *
***********************************************************************
         SPACE
PN      $ENTER ,
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT+BPOS,PN01     BRANCH IF NULL OR AFFIRMATIVE
         BC    BNEG,PN02           BRANCH IF NEGATIVE
         CLI   YNRESULT,YNARA      IS ARGUMENT 'ARABIC' .Q
         BE    PN03                BRANCH IF YES
         CLI   YNRESULT,YNROM      IS ARGUMENT 'ROMAN' .Q
         BE    PN04                BRANCH IF YES
         CLI   YNRESULT,YNOFN      IS ARGUMENT DOUBLE NEGATIVE .Q
         BE    PN05                BRANCH IF YES
         CLI   YNRESULT,YNPREF     IS ARGUMENT 'PREFIX' .Q
         BE    PN06                BRANCH IF YES
         CLI   YNRESULT,YNSUFX     IS ARGUMENT 'SUFFIX' .Q
         BE    PN07                BRANCH IF YES
         CLI   YNRESULT,YNFRAC     IS ARGUMENT 'FRAC' .Q
         BE    PN11                BRANCH IF YES
         CLI   YNRESULT,YNNORM     IS ARGUMENT 'NORM' .Q
         BE    PN12                BRANCH IF YES
         B     PNE005              ALL HAS FAILED, ERROR
         SPACE
PN01     DS    0H
        $OFF   SWITCHS,(PAGOFF,PINCRNO) ON NUMBERING AND INCREMENTING
         B     PN99                AND CONTINUE
         SPACE
PN02     DS    0H
        $OFF   SWITCHS,PINCRNO     TURN ON INCREMENTING
        $ON    SWITCHS,PAGOFF      TURN OFF PAGE NUMBERING
         B     PN99                AND CONTINUE
         SPACE
PN03     DS    0H
        $CALL  AR                  ARABIC ROUTINE
         B     PN99                AND CONTINUE
PN04     DS    0H
        $CALL  RO                  ROMAN ROUTINE
         B     PN99                AND CONTINUE
         SPACE
PN05     DS    0H
        $ON    SWITCHS,(PAGOFF,PINCRNO) OFF NUMBER AND INCREMENTING
         B     PN99                AND CONTINUE
         SPACE
PN06     DS    0H
         LA    X1,PNPREF           PAGE PREFIX STORED HERE
         B     PN08                ENTER COMMON SCAN CODE
PN07     DS    0H
         LA    X1,PNSUFX           PAGE SUFFIX STORED HERE
PN08     DS    0H
         XC    0(L'PNPREF,X1),0(X1)     CLEAR PREFIX/SUFFIX
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              GET NEXT ARGUMENT
         BC    BOMIT,PN10          BRANCH IF OMITTED
         LA    R0,L'PNPREF-2       MAX LENGTH-1 ALLOWED
         CR    R0,TEMP             ARG TOO LONG .Q
         BNL   PN09                OK IF NOT
         MVI   EFLAG013,ERRSET     ARGUMENT TOO LONG
         LR    TEMP,R0             USE MAXIMUM LENGTH
PN09     DS    0H
         EX    TEMP,PNPRFMVC       ENTER PAGE NUMBER PREFIX/SUFFIX
         LA    TEMP,1(,TEMP)       ACTUAL LENGTH
         STC   TEMP,0(,X1)         ENTER ACTUAL LENGTH
PN10     DS    0H
         B     PN99                AND CONTINUE
         SPACE
PN11     DS    0H
         L     R0,PACNTDI          DEFAULT DECIMAL PART
         S     R0,F1               LESS ONE
         ST    R0,PACNTD           SET DECIMAL PART FOR LATER
         B     PN99                AND CONTINUE
         SPACE
PN12     DS    0H
         L     R0,PACNTD           GET DECIMAL PART
         LTR   R0,R0               IS OR WAS IT USED .Q
         BM    PN13                BRANCH IF NOT
        $CALL  PRINTNB             PRINT REMAINDER OF CURRENT LINE
         BC    BNEG,PN13           BRANCH IF NOBREAK PAGE NUMBER
        $CALL  PAGEREAL            CAUSE PAGE EJECT
PN13     DS    0H
         L     R0,PACNTDI          DEFAULT DECIMAL PART
         ST    R0,PACNTD           SET DECIMAL PART TO DEFAULT
         B     PN99                AND CONTINUE
         SPACE
PNE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         SPACE
PN99     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
PNPRFMVC MVC   1(*-*,X1),0(COUNT)  ENTER PAGE PREFIX/SUFFIX
