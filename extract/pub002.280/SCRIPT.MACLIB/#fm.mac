***********************************************************************
*                                                                     *
*              .FM (FOOTING MARGIN) --- CONTROL                       *
*                                                                     *
***********************************************************************
         SPACE
FM      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER
         BC    BDNO,FM03           ERROR IF INVALID
         BC    BOMIT,FM01          ENTER DEFAULT IF OMITTED
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    FM02                BRANCH IF NOT
         AH    COUNT,FTMRG         ALTER EXISTING FOOTING
         B     FM02                GO CHECK AND ENTER
FM01     DS    0H
         L     COUNT,DEFFTMG+TAB3DEF  DEFAULT FOOTING MARGIN VALUE
FM02     DS    0H
         LTR   COUNT,COUNT         IS FOOTING MARGIN NEGATIVE
         BM    FM04                ERROR IF YES
         LH    TEMP,BMMRG          BOTTOM MARGIN VALUE
         SH    TEMP,FSCNT          LESS FOOTING SPACING
         CR    COUNT,TEMP          COMPARE VALUES
         BH    FM05                ERROR IF BIGGER
         STH   COUNT,FTMRG         SET FOOTING MARGIN
         B     FM06                AND GO TO RETURN
         SPACE
FM03     DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     FM06                AND GO TO RETURN
FM04     DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     FM06                AND GO TO RETURN
FM05     DS    0H
         MVI   EFLAG042,ERRSET     OPERAND TOO BIG
         B     FM06                AND GO TO RETURN
         SPACE
FM06     DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
