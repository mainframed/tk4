         MACRO
&LABEL   WRTERM &UMSG,&ULEN,&EDIT=YES,&COLOR=B
         LCLA  &PNT,&CNT,&END
         LCLC  &FLG,&CLR,&LEN,&MSG
         LCLB  &MR,&LR,&MSD
.****
.** LCLA'S USED TO COMPUTE LENGTH OF SELF-DEFINING MESSAGE
.** LCLC'S USED TO ASSEMBLE PLIST VALUES
.** LCLB'S CONTAIN CODE-GENERATION FLAGS
.****
.** MAKE SURE A MESSAGE OF SOME TYPE WAS GIVEN, AND NOTE
.** IF IT'S SELF-DEFINING OR GIVEN AS A REGISTER
.****
         AIF   (T'&UMSG NE 'O').UMSGOK
         MNOTE 8,'LINE ADDRESS NOT SPECIFIED'
         MEXIT
.*
.UMSGOK  AIF   ('&UMSG'(1,1) NE '''').UMSGR
&MSD     SETB  1              NOTE SELF-DEFINING MSG
&MSG     SETC  'DMS&SYSNDX.D' ASSEMBLE MSG-ADDRESS
         AGO   .UMSGZ
.*
.UMSGR   AIF   ('&UMSG'(1,1) NE '(').UMSGA
&MR      SETB  1              NOTE MSG REGISTER-ADDRESSED
&MSG     SETC  '&UMSG(1)'     DUMMY ASSEMBLE-VALUE
         AGO   .UMSGZ
.*
.UMSGA   ANOP
&MSG     SETC  '&UMSG'        MESSAGE SYMBOLIC NAME
.UMSGZ   ANOP
.****
.** GET THE VALUE OF THE MESSAGE LENGTH.  COMPUTE IT, IF IT
.** WASN'T GIVEN OR IF THE MESSAGE IS SELF-DEFINING.
.****
         AIF   (T'&ULEN EQ 'O').ULENO
         AIF   ('&ULEN'(1,1) EQ '(').ULENR
&LEN     SETC  '&ULEN'        SELF-DEFINING LENGTH
         AGO   .ULENZ
.*
.ULENR   ANOP
&LR      SETB  1              NOTE LENGTH IN REGISTER
         AGO   .COMPL
.*
.ULENO   AIF   (&MSD).COMPL
         MNOTE 8,'LENGTH PARAMETER NOT SPECIFIED'
         MEXIT
.*
.COMPL   ANOP                 PREPARE TO COMPUTE MSG-LENGTH
&PNT     SETA  2              FIRST-CHARACTER INDEX
&END     SETA  K'&UMSG-2      LAST CHARACTER INDEX
         AIF   (&END GT 0).COMPGO
         MNOTE 8,'INVALID LINE SPECIFICATION'
         MEXIT
.*
.COMPGO  AIF   (&PNT GT &END).COMPZ
         AIF   ('&UMSG'(&PNT,2) EQ '''''').QUOTE
&PNT     SETA  &PNT+1
         AGO   .COMPGO
.QUOTE   ANOP
&CNT     SETA  &CNT+1
&PNT     SETA  &PNT+2
         AGO   .COMPGO
.COMPZ   ANOP
&CNT     SETA  K'&UMSG-&CNT-2
&LEN     SETC  '&CNT'         ASSEMBLE LENGTH-VALUE
.*
.ULENZ   ANOP
.****
.** EXAMINE THE EDIT PARAMETER, TRANSLATE IT TO
.** FLAG BITS FOR 'DMSCWR' TO USE.
.****
&FLG     SETC  '00'
         AIF   ('&EDIT' EQ 'YES').UEDITZ
&FLG     SETC  '80'
         AIF   ('&EDIT' EQ 'NO').UEDITZ
&FLG     SETC  '90'
         AIF   ('&EDIT' EQ 'LONG').UEDITZ
         MNOTE 4,'INVALID EDIT SPECIFICATION - YES ASSUMED'
&FLG     SETC  '00'
.UEDITZ  ANOP
.****
.** EXAMINE THE COLOR PARAMETER FOR 'B' OR 'R'
.****
&CLR     SETC  'B'
         AIF   ('&COLOR' EQ 'B').UCLRZ
         AIF   ('&FLG' EQ '90').UCLRERR
         AIF   ('&COLOR' NE 'R').UCLRERR
&CLR     SETC  'R'
         AGO   .UCLRZ
.UCLRERR MNOTE 4,'INVALID COLOR SPECIFICATION - B ASSUMED'
.UCLRZ   ANOP
.****
.** ALIGN TO A WORD, GENERATE LABEL
.****
         CNOP  0,4
&LABEL   DS    0H
.****
.** GENERATE ADDRESS-STORE, IF NEEDED.
.****
         AIF   (NOT &MR).CONT5
         ST    &MSG,DMS&SYSNDX.B  STORE MESSAGE-ADDRESS
         MVI   DMS&SYSNDX.B,X'01' RESTORE FLAG
.CONT5   ANOP
.****
.** GENERATE LENGTH-STORE, IF NEEDED
.****
         AIF   (NOT &LR).CONT6
         STH   &ULEN(1),DMS&SYSNDX.C+2  STORE LENGTH IN PLIST
.CONT6   ANOP
.****
.** GENERATE PLIST, BAL ON R1 AROUND IT
.****
         BAL   1,DMS&SYSNDX.E  POINT R1 TO PLIST
DMS&SYSNDX.A DC   CL8'TYPLIN'
DMS&SYSNDX.B DC   X'01',AL3(&MSG)
DMS&SYSNDX.C DC   C'&CLR',X'&FLG',AL2(&LEN)
.****
.** GENERATE MESSAGE TEXT, IF SELF-DEFINING
.****
         AIF   (NOT &MSD).CONTZ
DMS&SYSNDX.D DC   CL&LEN&UMSG
.CONTZ   ANOP
.****
.** GENERATE SVC, ALIGNED ON HALFWORD
.****
DMS&SYSNDX.E DS   0H
         SVC   202            CALL CMS TO TYPE
         DC    AL4(*+4)
         MEXIT
         MEND
