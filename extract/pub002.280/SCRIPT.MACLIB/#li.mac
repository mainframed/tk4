***********************************************************************
*                                                                     *
*              .LI (LITERAL) --- CONTROL                              *
*                                                                     *
***********************************************************************
         SPACE
LI      $ENTER ,
         L     X3,ARGSTART         REMEMBER 'LINE' START ADDR
        $CALL  GETNUM              GET NUMERIC ARGUMENT
         BC    15-BDNO,LI07        BRANCH IF NOT INVALID
         ST    COUNT,ARGSTART      RESET SCANNING START ADDR
        $CALL  GETYESNO            GET AND TEST ARGUMENT AGAIN
         BC    BPOS,LI05           BRANCH IF POSITIVE
         BC    BNEG,LI06           BRANCH IF NEGATIVE
        $IFON  SWITCHS,LISWS,LI13  BRANCH IF LITERAL 'ON'
         LTR   TEMP,TEMP           LENGTH-1 IS ZERO .Q
         BNZ   LI12                NO, ARGUMENT NOT SINGLE CHAR
         LR    X1,COUNT            POINT TO CHAR
        $CALL  GETYESNO            GET AND TEST 2ND ARG
         BC    BOMIT,LI01          BRANCH IF ONLY SINGLE CHAR IN ARG1
         CLI   YNRESULT,YNNOBRK    IS ARG 'NOBREAK' .Q
         BE    LI03                BRANCH IF YES
         B     LI12                ELSE IT IS A 'LINE' ARG
         SPACE
LI01     DS    0H
         CLI   0(X1),C'*'          IS CHAR AN ASTERISK .Q
         BNE   LI02                BRANCH IF NOT
         LA    X1,PERIND+1         RESTORE THE DEFAULT
LI02     DS    0H
         MVC   PERIND(1),0(X1)     SET CONTROL WORD INDICATOR
         B     LI14                GO TO TERMINATE
         SPACE
LI03     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              ANYTHING AFTER 'NOBREAK' .Q
         BC    15-BOMIT,LI12       YES, THEN A 'LINE' ARG
         CLI   0(X1),C'*'          IS CHAR AN ASTERISK .Q
         BNE   LI04                BRANCH IF NOT
         LA    X1,PERNBIND+1       RESTORE THE DEFAULT
LI04     DS    0H
         MVC   PERNBIND(1),0(X1)   SET NOBREAK CONTROL WORD IND
         B     LI14                GO TO TERMINATE
         SPACE
LI05     DS    0H
         LH    X1,H32767           'ON' IS FOREVER
         B     LI09                CONTINUE CHECKING
LI06     DS    0H
         SR    X1,X1               'OFF' IS FINISHED
         BCTR  X1,0                NEGATIVE INDICATES 'OFF'
         B     LI09                CONTINUE CHECKING
         SPACE
LI07     DS    0H
         BC    15-BOMIT,LI08       BRANCH IF FIRST ARG NOT OMITTED
         LA    COUNT,1             ONE LINE BY DEFAULT
LI08     DS    0H
         LTR   X1,COUNT            TEST NUMERIC RESULT
         BM    LI12                NEGATIVE IS 'LINE' ARG
         CH    X1,H32767           TOO BIG FOR USER .Q
         BNL   LI12                YES, THEN 'LINE' ARG
LI09     DS    0H
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              ANYTHING AFTERWARD .Q
         BC    15-BOMIT,LI12       YES, THEN 'LINE' ARG
        $IFON  SWITCHS,LISWS,LI11  BRANCH IF LITERAL 'ON'
         CH    X1,H32767           WAS THIS 'ON' .Q
         BNE   LI10                BRANCH IF NOT
        $ON    SWITCHS,LISWS       TURN LITERAL 'ON'
LI10     DS    0H
         STH   X1,LICNT            SET ARGUMENT VALUE
         B     LI14                GO TO TERMINATE
         SPACE
LI11     DS    0H
         LTR   X1,X1               WAS OPERAND 'OFF' .Q
         BNM   LI13                NO, REPROCESS 'LINE' LITERALLY
        $OFF   SWITCHS,LISWS       KILL LITERAL 'ON' CONDITION
         SR    X1,X1               NO LINES LEFT
         STH   X1,LICNT            KILL LITERAL COUNT
         B     LI13                REPROCESS TOTAL LINE
         SPACE
LI12     DS    0H
        $IFON  SWITCHS,LISWS,LI13  BRANCH IF LITERAL 'ON'
         ST    X3,ARGSTART         IT ALL STARTED HERE
        $CALL  USARG               RESULTING TEXT LINE
         LA    X1,1                ONLY ONE LINE
         STH   X1,LICNT            SET LITERAL LINE COUNT
         LA    R15,WRETNR          PROCESS THE RESULT
         B     LI15                GO TO EXIT
         SPACE
LI13     DS    0H
         LA    R15,WRETLI          REPROCESS LINE LITERALLY
         B     LI15                GO TO EXIT
         SPACE
LI14     DS    0H
         LA    R15,0               ZERO RETURN CODE
LI15     DS    0H
        $EXIT  RC=(R15)            RETURN TO CALLER
