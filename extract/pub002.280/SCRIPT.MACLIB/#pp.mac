***********************************************************************
*                                                                     *
*              .PP (PARAGRAPH START) --- CONTROL                      *
*                                                                     *
***********************************************************************
         SPACE
PP      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  USARG               ISOLATE 'LINE' ARGUMENT
         BC    BOMIT,PP01          BRANCH IF NONE
         LH    TEMP,BUFF2LGZ       LENGTH OF 'LINE' OPERAND
         BCTR  TEMP,0              LENGTH-1
         LA    R1,BUFF2            POINT TO 'LINE'
        $CALL  HIDBADD             ADD LINE LIFO
         SPACE
PP01     DS    0H
         L     R1,BUFF3AD          POINT TO WORK AREA
         LA    R1,4(,R1)           SKIP RDW AREA
         MVC   0(B2LG,R1),BLANKS   CLEAR SOME SPACE
         MVC   0(L'PPIL,R1),PPIL   ENTER A INDENT LINE
         MVC   0(L'PERIND,R1),PERIND    ENTER CONTROL WORD INDICATOR
         LA    TEMP,L'PPIL-1       LENGTH-1 OF LINE
        $CALL  HIDBADD             ADD LINE LIFO
         SPACE
         MVC   0(B2LG,R1),BLANKS   CLEAR SOME SPACE
         MVC   0(L'PPCC,R1),PPCC   ENTER CONDITIONAL COLUMN
         MVC   0(L'PERIND,R1),PERIND    ENTER CONTROL WORD INDICATOR
         LA    TEMP,L'PPCC-1       LENGTH-1 OF LINE
        $CALL  HIDBADD             ADD LINE LIFO
         SPACE
         MVC   0(B2LG,R1),BLANKS   CLEAR SOME SPACE
         MVC   0(L'PPSK,R1),PPSK   ENTER A SKIP
         MVC   0(L'PERIND,R1),PERIND    ENTER CONTROL WORD INDICATOR
         LA    TEMP,L'PPSK-1       LENGTH-1 OF LINE
        $CALL  HIDBADD             ADD LINE LIFO
         SPACE
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
PPIL     DC    C'.IL +3'           INDENT LINE
PPCC     DC    C'.CC 2'            CONDITIONAL COLUMN
PPSK     DC    C'.SK'              SKIP
