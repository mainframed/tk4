         MACRO
&NAME    TCMD  &CMD=,&ROW=,&COL=,&CHAR=,&STOP=NO,                      *
               &PROT=,&NUM=,&BRIGHT=,&PEN=NO,&MDT=,&LN=,&DATA=,        *
               &HILITE=,&COLOR=
.*********************************************************************
.*
.*   TCMD - GENERATE THE 3270 COMMAND CODES STRINGS.  MANY OF THE
.*          KEYWORDS ARE REQUIED ONLY FOR CERTAIN CMDS.
.*          CMD    DESCRIPTION   REQUIRED                 OPTIONAL
.*          SF     SET FIELD     PROT,NUM,BRIGHT,PEN,MDT  LN,DATA,STOP
.*                                                        HILITE,COLOR
.*          SBA    SET BUFFER AD ROW,COL
.*          IC     INSERT CURSOR
.*          PT     PROGRAM TAB
.*          RA     REPEAT TO AD  ROW,COL,CHAR
.*          EUA    ERASE TO AD   ROW,COL
.*
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*
.*  (C) COPYRIGHT 2010  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
.*
.*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
.*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
.*  AND FOR NON-COMERCIAL PURPOSES.
.*
.*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
.*
.*         REVISED     DESCRIPTION
.*         ----------  -----------------------------------------------
.*  1.0.0              INITIAL RELEASE
.*  1.1.0  01/06/2011  ADDED SUPPORT FOR COLOR EXTENDED ATTRIBUTES.
.*  1.1.0  11/08/2013  correct extended attribute defaults     JW130811
.*
.*********************************************************************
         GBLC  &ZOSVER
         AIF   (T'CMD NE 'O').CMDVAL
         MNOTE 12,'**** CMD MUST BE GIVEN ON TCMD MACRO ****'
         MEXIT
.CMDVAL  ANOP
         AIF   ('&CMD' NE 'SF').NOTSF
.*
.*  PROCESS CMD=SF
.*
         AIF   ('&ZOSVER' EQ 'B').GENSF    SKIP THE SET EXTENDED
         AIF   (T'&HILITE EQ 'O' AND T'&COLOR EQ 'O').GENSF
         DC    X'29C3C0'
         AGO   .GENSF2
.GENSF   DC    X'1D'   3270 SET FIELD CMD
.GENSF2  ANOP
         GBLA  &ADR
         GBLA  &WCC
&WCC     SETA  0
         TFVERI B'00100000',&PROT,'PROT'
         TFVERI B'00010000',&NUM,'NUM'
         TFVERI B'00001000',&BRIGHT,'BRIGHT'
         TFVERI B'00000100',&PEN,'PEN'
         TFVERI B'00000001',&MDT,'MDT'
         TRT3270 &WCC
         DC    AL1(&ADR)  3270 SF
         AIF   (T'&HILITE EQ 'O' AND T'&COLOR EQ 'O').NOXSF
         AIF   ('&ZOSVER' NE 'B').NO281    SKIP THE SET EXTENDED
         DC    X'28'                  SET EXTENDED
.NO281   ANOP
         AIF   (T'&HILITE EQ 'O').DEFHI
         AIF   ('&HILITE' NE 'DEF').NOTDEF
.DEFHI   DC    X'4100'                default hilite           JW130811
         AGO   .GENCLR
.NOTDEF  AIF   ('&HILITE' NE 'BLN').NOTBLN
         DC    X'41F1'                BLINK
         AGO   .GENCLR
.NOTBLN  AIF   ('&HILITE' NE 'REV').NOTREV
         DC    X'41F2'                REVERSE
         AGO   .GENCLR
.NOTREV  AIF   ('&HILITE' NE 'ULN').NOTULN
         DC    X'41F4'                UNDERLINE
         AGO   .GENCLR
.NOTULN  MNOTE 12,'**** INVALID HILITE VALUE ****'
         AGO   .NOXSF
.GENCLR  ANOP
         AIF   ('&ZOSVER' NE 'B').NO282    SKIP THE SET EXTENDED
         DC    X'28'                  SET EXTENDED
.NO282   ANOP
         AIF   (T'&COLOR EQ 'O').DEFCOL
         AIF   ('&COLOR' NE 'DEF').NOTDEFC
.DEFCOL  DC    X'4200'                default color            JW130811
         AGO   .NOXSF
.NOTDEFC AIF   ('&COLOR' NE 'BLU').NOTBLU
         DC    X'42F1'                BLUE
         AGO   .NOXSF
.NOTBLU  AIF   ('&COLOR' NE 'RED').NOTRED
         DC    X'42F2'                RED
         AGO   .NOXSF
.NOTRED  AIF   ('&COLOR' NE 'PNK').NOTPNK
         DC    X'42F3'                PINK
         AGO   .NOXSF
.NOTPNK  AIF   ('&COLOR' NE 'GRN').NOTGRN
         DC    X'42F4'                GREEN
         AGO   .NOXSF
.NOTGRN  AIF   ('&COLOR' NE 'TUR').NOTTUR
         DC    X'42F5'                TURQUOISE
         AGO   .NOXSF
.NOTTUR  AIF   ('&COLOR' NE 'YEL').NOTYEL
         DC    X'42F6'                YELLOW
         AGO   .NOXSF
.NOTYEL  AIF   ('&COLOR' NE 'WHI').NOTWHI
         DC    X'42F7'                WHITE
         AGO   .NOXSF
.NOTWHI  AIF   ('&COLOR' NE 'BLK').NOTBLK
         DC    X'42F8'                BLACK
         AGO   .NOXSF
.NOTBLK  AIF   ('&COLOR' NE 'DBL').NOTDBL
         DC    X'42F9'                DARK BLUE
         AGO   .NOXSF
.NOTDBL  AIF   ('&COLOR' NE 'ORA').NOTORA
         DC    X'42FA'                ORANGE
         AGO   .NOXSF
.NOTORA  AIF   ('&COLOR' NE 'PUR').NOTPUR
         DC    X'42FB'                PURPLE
         AGO   .NOXSF
.NOTPUR  AIF   ('&COLOR' NE 'PGR').NOTPGR
         DC    X'42FC'                PALE GREEN
         AGO   .NOXSF
.NOTPGR  AIF   ('&COLOR' NE 'PTQ').NOTPTQ
         DC    X'42FD'                PALE TURQUOISE
         AGO   .NOXSF
.NOTPTQ  AIF   ('&COLOR' NE 'GRY').NOTGRY
         DC    X'42FE'                GREY
         AGO   .NOXSF
.NOTGRY  AIF   ('&COLOR' NE 'NEU').NOTNEU
         DC    X'42FF'                WHITE
         AGO   .NOXSF
.NOTNEU  MNOTE 12,'**** INVALID COLOR VALUE ****'
.*
.NOXSF   AIF   (T'&LN EQ 'O').NOLN
&NAME    DC    CL&LN&DATA
         AGO   .CHKSB
.NOLN    ANOP
         AIF   (T'&DATA EQ 'O').CHKSB
&NAME    DC    C&DATA
.CHKSB   AIF   (T'&STOP EQ 'O').EXIT
         AIF   ('&STOP' EQ 'NO').EXIT
         AIF   ('&STOP' EQ 'YES').GENSB
         MNOTE 12,'**** STOP  IS INVALID ****'
         MEXIT
.GENSB   ANOP
         AIF   (T'&HILITE EQ 'O' AND T'&COLOR EQ 'O').STBYT
         DC    X'2903C02041004200'   extended stop byte        JW130811
         AGO   .EXIT
.STBYT   DC    X'1D20'               STANDARD STOP BYTE
.EXIT    MEXIT
.*
.*  PROCESS CMD=SBA
.*
.NOTSF   AIF   ('&CMD' NE 'SBA').NOTSBA
         DC    X'11'   3270 SET BUFFER ADDRESS
&NAME    TSBA  ROW=&ROW,COL=&COL
         MEXIT
.NOTSBA  ANOP
         AIF   ('&CMD' NE 'IC').NOTIC
.*
.*  PROCESS CMD=IC
.*
         DC   X'13'   3270 INSERT CURSOR COMMAND
         MEXIT
.NOTIC   ANOP
         AIF   ('&CMD' NE 'PT').NOTPT
.*
.*  PROCESS CMD=PT
.*
         DC   X'05'   3270 PROGRAM TAB COMMAND
         MEXIT
.NOTPT   ANOP
         AIF   ('&CMD' NE 'RA').NOTRA
.*
.*  PROCESS CMD=RA
.*
         DC    X'3C'   3270 REPEAT TO ADDRESS COMMAND
         TSBA  ROW=&ROW,COL=&COL
         DC    CL1&CHAR            CHARACTER TO REPEAT
         MEXIT
.NOTRA   ANOP
         AIF   ('&CMD' NE 'EUA').NOTEUA
.*
.*  PROCESS CMD=EUA
.*
         DC    X'12'   3270 ERASE TO ADDRESS COMMAND
         TSBA  ROW=&ROW,COL=&COL
         MEXIT
.NOTEUA  ANOP
         MNOTE 12,'**** CMD IS NOT VALID ****'
         MEND
