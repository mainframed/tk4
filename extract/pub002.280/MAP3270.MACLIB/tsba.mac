         MACRO
&NAME    TSBA  &ROW=,&COL=
.*********************************************************************
.*
.*   TSBA - GENERATE THE 3270 ADDR WORD FOR ROW, COL
.*
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*
.*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
.*
.*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
.*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
.*  AND FOR NON-COMERCIAL PURPOSES.
.*
.*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
.*
.*********************************************************************
         AIF   (T'&ROW EQ 'O').SBAERR1
         AIF   (T'&COL EQ 'O').SBAERR1
         LCLA  &R,&C
&R       SETA  &ROW
&C       SETA  &COL
         AIF   (&R LT 1 OR &R GT 24).SBAERR2
         AIF   (&C LT 1 OR &C GT 80).SBAERR3
         LCLA  &BA,&HB,&LB
&BA      SETA  ((&R-1)*80)+(&C-1)
&HB      SETA  &BA/64
&LB      SETA  &BA-(&HB*64)
         GBLA &ADR
         TRT3270 &HB
&HB      SETA  &ADR
         TRT3270 &LB
&LB      SETA  &ADR
&NAME    DC    AL1(&HB,&LB)             FROM TSBA MACRO
         MEXIT
.SBAERR1 MNOTE 12,'**** ROW AND COLUMN MUST BE GIVEN FOR SBA ****'
         MEXIT
.SBAERR2 MNOTE 12,'**** INVALID ROW FOR SBA ****'
         MEXIT
.SBAERR3 MNOTE 12,'**** INVALID COL FOR SBA ****'
         MEXIT
         MEND
