         MACRO
&NAME    TTYRTN &BUFLEN=2048,&PLI=,&UCODE=3270
.*********************************************************************
.*
.*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
.*
.*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
.*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
.*  AND FOR NON-COMERCIAL PURPOSES.
.*
.*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
.*
.*  V2.0.0 - RERELASED AND TEST USING HERCULES
.*********************************************************************
         AIF   (T'&PLI EQ 'O').NOPLI
         AIF   ('&PLI' EQ 'NO').NOPLI
         AIF   ('&PLI' EQ 'F').YESPLI
         MNOTE 12,'*** PLI OPTION MUST BE F OR OMITTED ***'
         MEXIT
.*********************************************************************
.YESPLI  ANOP
**AME    ESTART TYPE=START,DESC='3270 TTY ROUTINE FOR PL/I(F)',
**             VER='2.0.0',REGS=YES,BASE=R12
&NAME    ESTART TYPE=START,DESC='3270 TTY ROUTINE FOR PL/I(F)',        *
               VER='2.0.0',REGS=YES,BASE=R12
         USING *+4094,R11
         LA    R11,4094(R12)
         LR    R10,R1                  SAVE THE PARMADDR
         USING TTYPARMS,R10
*
*    VERIFY THE PARM - PARM LIST MUST BE ONE BLOCK OF 5 ITEMS.
*      IF ONE, REFORMAT TO LOOK LIKE 5 ITEMS.
*
         TM    TTYPFUNC,X'80'          ONLY 1 PARM?
         BO    ONEPARM                 YES, BRANCH
BADPARMS EQU   *
**       WTO   'MAP3270-&NAME PARM ERROR',ROUTCDE=11
         WTO   'MAP3270-&NAME PARM ERROR',ROUTCDE=11
**       ABEND 3270,,,USER                 OTHERWISE ABORT
         ABEND 3270,,,USER                 OTHERWISE ABORT
ONEPARM  EQU   *
*
*  GET ADDRESS OF PLI(F) PARM LIST. EACH ADDRESS POINTS TO A DATA
*      CONTROL BLOCK.  OFFSET +0 OF EACH BLOCK HAS THE ACTUAL
*      DATA
*
         MVC   USERPARM,0(R1)          SAVE INTERFACE AREA
         NI    USERPARM,X'7F'          TURN OF HIGH ORDER BIT
         L     R10,USERPARM
         L     R10,0(0,R10)
         ST    R10,USERPARM
         MVC   VFUNC(VPARMLN),0(R10)     MOVE THE REQUEST BLOCK
         LA    R10,VPARMS
         AGO   .PLIDONE
.NOPLI   ANOP
**AME    ESTART TYPE=START,DESC='3270 TTY ROUTINE',VER='2.0.0',
**             REGS=YES,BASE=R12
&NAME    ESTART TYPE=START,DESC='3270 TTY ROUTINE',VER='2.0.0',        *
               REGS=YES,BASE=R12
.*********************************************************************
         USING *+4094,R11
         LA    R11,4094(R12)
         MVC   USERPARM,=4X'00'        CLEAR OUT USERPARM
*
.*********************************************************************
         LR    R10,R1                  SAVE THE PARMADDR
         USING TTYPARMS,R10
*
*    VERIFY THE PARMS - PARM LIST COULD BE ONE BLOCK OF 5 ITEMS
*      OR 5 SEPARATE ITEMS.  IF ONE, REFORMAT TO LOOK LIKE 5 ITEMS.
*
         TM    TTYPFUNC,X'80'          ONLY 1 PARM?
         BO    ONEPARM                 YES, BRANCH
         TM    TTYPLOUT,X'80'
         BO    BADPARMS
         TM    TTYPLINP,X'80'
         BO    BADPARMS
         TM    TTYPERR,X'80'
         BO    BADPARMS
         TM    TTYPAID,X'80'
         BO    PARMSOK
BADPARMS EQU   *
**       WTO   'MAP3270-&NAME PARM ERROR',ROUTCDE=11
         WTO   'MAP3270-&NAME PARM ERROR',ROUTCDE=11
**       ABEND 3270,,,USER                 OTHERWISE ABORT
         ABEND 3270,,,USER                 OTHERWISE ABORT
ONEPARM  EQU   *
         L     R3,TTYPFUNC
         ST    R3,USERPARM               SAVE ADDRESS OF SINGLE PARM
         MVC   VFUNC(VPARMLN),0(R3)      MOVE THE REQUEST BLOCK
         LA    R10,VPARMS
.*********************************************************************
.PLIDONE ANOP
PARMSOK  EQU   *
*
         L     R1,T3270ADR
         LTR   R1,R1
         BNZ   SKIPLD
         LOAD  EPLOC=T3270IO
         ST    R0,T3270ADR
SKIPLD   EQU   *
         L     R3,TTYPFUNC             GET THE FIRST PARM - FUNCTION
         CLC   0(4,R3),=C'IO  '         IO REQUEST?
         BE    IORTN                   YES, BRANCH
         CLC   0(4,R3),=C'PG  '         IO REQUEST?
         BE    IORTN                   YES, BRANCH
         CLC   0(4,R3),=C'PUT '         PUT REQUEST?
         BE    IORTN                   YES, BRANCH
         CLC   0(4,R3),=C'CLR '         PUT REQUEST?
         BE    IOCLR                   YES, BRANCH
         CLC   0(4,R3),=C'END '         END IT?
         BE    EXIT                    YES, BRANCH
         CLC   0(4,R3),=C'STRT'         START IT?
         BE    STRTIT                  YES, BRANCH
         ABEND 20,,,USER                 OTHERWISE ABORT
*
*  INITIALIZE THE IOCB AND SET IT UP FOR AN STRT
*
STRTIT   EQU   *
         MVI   IOCB,X'00'
         MVC   IOCB+1(IOCBLN-1),IOCB
         MVC   IOCBFUNC,=C'STRT'
         TCALL T3270ADR,IOCB,TPARM
         B     LEAVE
*
IOCLR    EQU   *
         MVC   IOCBFUNC,=C'PUT '        USE THE FUNC CD
         LM    R2,R5,BUFCLR
         B     IOCOMM
*
IORTN    EQU   *
         MVC   IOCBFUNC,0(R3)          USE THE PASSED FUNC CD
         LM    R2,R5,BUFSCRL
IOCOMM   EQU   *
         MVCL  R2,R4
         L     R3,TTYPLOUT             MOVE LINEOUT PARM TO
         MVC   BUFFERL,0(R3)             LAST LINE IN BUFFER
         L     R3,TTYPLINP             MOVE LINEIN PARM TO
         MVC   R23C01D,0(R3)             INPUT LINE
         L     R3,TTYPERR              MOVE ERRMSG PARM TO
         MVC   R24C01D,0(R3)             MSG LINE
*
         LA    R2,20
         LA    R3,R02C01D
         LA    R4,BUFFER
MOVTXT   EQU   *
         MVC   0(79,R3),0(R4)
         LA    R3,82(,R3)
         LA    R4,79(,R4)
         BCT   R2,MOVTXT
         L     R3,TTYPLOUT
         MVC   BUFFERL,0(R3)
*
*  SET UP IOCB FOR AN IO
*
         MVI   R23C01C,C'X'                  FORCE THE CURSOR
         TCALL T3270ADR,IOCB,TPARM
         L     R3,TTYPAID              MOVE THE AIDCODE
         MVC   0(1,R3),TAID
**** THIS LOGIC SHOULD BE PROVIDED BY CALLER
*        CLI   TAID,AIDPFK03            PF3 PRESSED?
*        BE    LEAVE
*        CLI   TAID,AIDPFK12            PF12 PRESSED?
*        BE    CLRINP
*
*     PROCESS INPUT
***********************************************************
         L     R3,TTYPLINP             MOVE THE INPUT LINE
         MVC   0(L'R23C01D,R3),R23C01D     TO THE 3RD PARM
         B     LEAVE
*
*  SET UP IOCB FOR AN END
*
EXIT     EQU   *
         MVC   IOCBFUNC,=C'END '
         TCALL T3270ADR,IOCB,TPARM
         MVI   IOCB,X'00'
         MVC   IOCB+1(IOCBLN-1),IOCB
LEAVE    EQU   *
         CLC   USERPARM,=4X'00'        IS USERPARM NULL?
         BE    LEAVE2
         L     R1,USERPARM
         MVC   0(VPARMLN,R1),VFUNC     MOVE PARM AREA BACK TO CALLER
LEAVE2   ERETURN RC=0
USERPARM DS    F
         LTORG
T3270IO  DC    CL8'T3270IO'
T3270ADR DC    F'0'
IOCB     DS    0F
IOCBFUNC DS    CL4         FUNCTION CODE (STRT/IO/PG/PUT/CLR/END)
IOCBRC   DS    AL2                     RETURN CODE
IOCBLEN  DS    AL2                     IO BUFFER LENGTH
IOCBADR  DS    A                       IO BUFFER ADDRESS
         DS    XL20                    RESERVED
IOCBLN   EQU   *-IOCB
BUFSCRL  DS    0F
         DC    A(BUFFER)          BUFFER
         DC    A(20*79)
         DC    A(BUFFER+79)       2ND LINE
         DC    X'40'
         DC    AL3(19*79)
BUFCLR   DS    0F
         DC    A(BUFFER)          BUFFER
         DC    A(20*79)
         DC    A(*+4)             SPACE
         DC    X'40'
         DC    AL3(1)
BUFFER   DC    19CL79' '
BUFFERL  DC    CL79' '
*
         COPY  AIDCODES
*
         COPY  TTY3270
*
VPARMS   DS    0F
         DC    A(VFUNC)      FUNCTION CODE ADDR
         DC    A(VOUTLN)     OUTPUT LINE ADDR
         DC    A(VINLN)      INPUT LINE ADDR
         DC    A(VERRLN)     PROMPT/ERROR LINE (LINE 24) ADDR
         DC    AL1(X'80')
         DC    AL3(VAID)     3270 AID CODE ADDR
VFUNC    DS    CL4
VOUTLN   DS    CL79
VINLN    DS    CL78
VERRLN   DS    CL79
VAID     DS    C' '
VPARMLN  EQU   *-VFUNC
TTYPARMS DSECT
TTYPFUNC DS    A             FUNCTION CODE ADDR
TTYPLOUT DS    A             OUTPUT LINE ADDR
TTYPLINP DS    A             INPUT LINE ADDR
TTYPERR  DS    A             PROMPT/ERROR LINE (LINE 24) ADDR
TTYPAID  DS    A             3270 AID CODE ADDR
         MEND
