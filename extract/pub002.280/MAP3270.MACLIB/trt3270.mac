         MACRO
         TRT3270 &B
.*********************************************************************
.*
.*    TRT3270 - INTERNAL MACRO USED BY MAP3270
.*           CONVERT BUFFER ADDRESS TO 3270 BUFFER ADDRS
.*
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*
.*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
.*
.*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
.*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
.*  AND FOR NON-COMERCIAL PURPOSES.
.*
.*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
.*
.*         REVISED     DESCRIPTION
.*         ----------  -----------------------------------------------
.*  1.0.0              INITIAL RELEASE
.*  1.1.0  01/06/2011  ADDED SUPPORT FOR COLOR EXTENDED ATTRIBUTES.
.*
.*********************************************************************
         GBLA  &ADR
         AIF   (&B EQ 0).R00
         AIF   (&B GE 1 AND &B LE 9).R0109
         AIF   (&B GE 10 AND &B LE 15).R1015
         AIF   (&B EQ 16).R16
         AIF   (&B GE 17 AND &B LE 25).R1725
         AIF   (&B GE 26 AND &B LE 31).R2631
         AIF   (&B EQ 32 OR &B EQ 33).R3233
         AIF   (&B GE 34 AND &B LE 41).R3441
         AIF   (&B GE 42 AND &B LE 47).R4247
         AIF   (&B EQ 48).R48
         AIF   (&B GE 49 AND &B LE 57).R4957
         AIF   (&B GE 58 AND &B LE 63).R5863
         AGO   .GT63
.R00     ANOP
&ADR     SETA  X'40'             X'00'       => X'40'
         AGO   .DONE
.R0109   ANOP
&ADR     SETA  X'C0'+&B          X'01'-X'09' -> X'C1'-X'C9'
         AGO   .DONE
.R1015   ANOP
&ADR     SETA  X'40'+&B          X'0A'-X'0F' -> X'4A'-X'4F'
         AGO   .DONE
.R16     ANOP
&ADR     SETA  X'50'             X'10'       -> X'50'
         AGO   .DONE
.R1725   ANOP
&ADR     SETA  X'C0'+&B          X'11'-X'19' -> X'D1'-X'D9'
         AGO   .DONE
.R2631   ANOP
&ADR     SETA  X'40'+&B          X'1A'-X'1F' -> X'5A'-X'5F'
         AGO   .DONE
.R3233   ANOP
&ADR     SETA  X'40'+&B          X'20'-X'21' -> X'60'-X'61'
         AGO   .DONE
.R3441   ANOP
&ADR     SETA  X'C0'+&B          X'23'-X'29' -> X'E2'-X'E9'
         AGO   .DONE
.R4247   ANOP
&ADR     SETA  X'40'+&B          X'2A'-X'2F' -> X'6A'-X'6F'
         AGO   .DONE
.R48     ANOP
&ADR     SETA  X'F0'             X'30'       -> X'F0'
         AGO   .DONE
.R4957   ANOP
&ADR     SETA  X'C0'+&B          X'30'-X'39' -> X'F1'-X'F9'
         AGO   .DONE
.R5863   ANOP
&ADR     SETA  X'40'+&B          X'3A'-X'3F' -> X'7A'-X'7F'
         AGO   .DONE
.GT63    MNOTE 12,'**** INVALID BUFFER ADDRESS DETECTED ****'
&ADR     SETA  0
.DONE    ANOP
         MEND
