         MACRO
&NAME    TWCC  &ESCAPE=YES,&ERASE=YES,&RESET=NO,&PRNTFMT=NO,           *
               &PRNT=NO,&ALARM=NO,&KBUNLK=NO,&RSTMDT=NO
.*********************************************************************
.*
.*   TWCC - GENERATE THE 3270 WRITE CONTROL COMMAND.  OPTIONALLY,
.*          THE ESCAPE AND WRITE PREFIX CAN BE GENERATED, MAKING
.*          THE 3270 SUITABLE FOR USE WITH TPUT.
.*          THIS MACRO IS FIRST WHEN DEFINING A SCREEN.
.*
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
.*
.*  (C) COPYRIGHT 2009  CEDAR LAKE EMPORIUM LLC   ALL RIGHTS RESERVED
.*
.*  THIS SOURCE CODE AS WELL AS ANY OBJECT CODE RESULTING FROM THIS
.*  SOURCE CODE MAY BE DISTRIBUTED FREELY PROVIDED NO FEE IS CHARGED
.*  AND FOR NON-COMERCIAL PURPOSES.
.*
.*  FOR COMMERCIAL DISTRIBUTION RIGHTS, CONTACT THE COPYRIGHT OWNER.
.*
.*  Enable soft clear using RA to fill screen with zeros       JW120816
.*                                                             JW120816
.*********************************************************************
         GBLA  &WCC
         AIF   (T'&NAME EQ 'O').NAMEOK
&NAME    EQU   *
.NAMEOK  ANOP
&WCC     SETA  0
         TFVERI B'00100111',&ESCAPE,'ESCAPE'
         AIF   ('&ESCAPE' EQ 'NO').NOESC
         DC    AL1(&WCC)  3270 ESCAPE
&WCC     SETA  B'11110001'
         TFVERI B'00000100',&ERASE,'ERASE'
         DC    AL1(&WCC)  3270 WRITE(X'F1') OR WRITE/ERASE(X'F5')
.NOESC   ANOP                                                  JW120816
&WCC     SETA  0
.*
         TFVERI B'01000000',&RESET,'RESET'
         TFVERI B'00110000',&PRNTFMT,'PRNTFMT'
         TFVERI B'00001000',&PRNT,'PRNT'
         TFVERI B'00000100',&ALARM,'ALARM'
         TFVERI B'00000010',&KBUNLK,'KBUNLK'
         TFVERI B'00000001',&RSTMDT,'RSTMDT'
         DC    AL1(&WCC)  3270 WCC
         AIF   ('&ERASE' EQ 'NO').EXIT                         JW120816
         AIF   ('&ESCAPE' EQ 'YES').EXIT                       JW120816
         DC    X'1140403C404000' perform soft clear using RA   JW120816
.EXIT    ANOP
         MEND
