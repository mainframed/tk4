         MACRO
&L       STITL &TIT
.*             THIS MACRO IS USED IN CONJUNCTION WITH MTITL TO
.*             SPECIFY THE SUB-TITLE FOR A TITLE CARD.
.*             THE MAIN PORTION OF THE TITLE IS SPECIFIED ONCE,
.*             VIA MTITL, WHILE EACH STITL SPECIFIES ITS OWN
.*             SUB-TITLE, WHICH IS PLACED ON THE RIGHT HAND SIDE.
.*             AN OPERAND OF OFF GENERATES A TITLE WITH NO SUB-TITLE.
         GBLC  &MTITLE
         LCLC  &STIT
         AIF   (T'&TIT EQ 'O').OFF
         AIF   ('&TIT' EQ 'OFF').OFF
         AIF   ('&TIT'(1,1) EQ '''' AND '&TIT'(K'&TIT,1) EQ '''').OK
         MNOTE 4,'SUBTITLE NOT DELIMITED BY "''" - IGNORED'
&STIT    SETC  '*** INVALID SUBTITLE ***'
         AGO   .TIT
.OK      ANOP
&STIT    SETC  '&TIT'(2,K'&TIT-2)
.TIT     TITLE '&MTITLE- &STIT'
         MEXIT
.OFF     TITLE '&MTITLE'
         MEND
